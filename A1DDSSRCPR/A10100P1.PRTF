/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD66                                                     *
/*XAIA*  CHGPRTF FILE(A10100P1) OVRFLW(65)                            *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/A10100P1 - Effective Interest Rate set today            *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. BA1007  *CREATE    Date 01Jun21               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BA1007 - Store EIR for SE Book Positions                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      **
      **   *******************
      **   PAGE HEADINGS
      **   *******************
      **
     A          R HEADP1                    SKIPB(2)
     A                                      TEXT('(c) Finastra Internation+
     A                                      al Limited 2021')
     A                                     6'REFERENCE A10100'
     A                                      SPACEB(1)
     A            TITL      R             35
     A                                   101'DATE:'
     A            MRDT           7A      108
     A                                   119'PAGE:'
     A            PAGE         004S 0    125EDTCDE(Z)
     A                                   101TIME
     A                                      SPACEB(1)
      *
     A                                    35'SECURITIES TRADING'
     A                                      SPACEB(1)
     A                                    54'POSITIONS Effective Rates -'
     A                                    82'LIST'
     A                                    35'-------------------'
     A                                      SPACEB(1)
     A                                    54'-------------------------------- '
     A                                      SPACEA(2)
     A                                     1'SECURITY SHORTNAME'
     A                                    21'Branch'
     A                                    29'Book'
     A                                    35'Market'
     A                                    47'Trade/Value'
     A                                    72'Previous Rate'
     A                                    99'New Rate'
     A                                      SPACEA(1)
     A                                     1'------------------- '
     A                                    21'------'
     A                                    29'----'
     A                                    35'------'
     A                                    47'-----------'
     A                                    72'-------------'
     A                                    99'--------'
     A                                      SPACEA(1)
      **
      **   *******************
      **   DETAIL PART OF REPORT
      **   *******************
      **
     A          R DETAIL
     A            SESN      R              1
     A            BHBA      R             22
     A            BHBK      R             30
     A            BHMK      R             38
     A            BHTV      R             51
     A            LEIRP         15S 8     68EDTCDE(J)
     A            PRICP         15S 8     89EDTCDE(J)
     A                                      SPACEA(1)
      *
     A          R P1NULL
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    53
     A                                      '*** No details to report ***'
      *
     A          R P1FOOT
     A                                      SPACEB(002)
     A                                    57
     A                                      '*** End of report ***'
