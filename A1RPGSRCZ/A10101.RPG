     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas SE Interest Consideration Calculation ')         *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  A10101   - Utility for Interest & Consideration Calculation  *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. BA1007  *CREATE    Date 01Jun21               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BA1007 - Store EIR for SE Book Positions                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     FSEDEV   IF  E           K        DISK
     FSECEO   IF  E           K        DISK
     F            SEDEVDF                           KRENAMESEDEVDO
     F            SNDEVDF                           KRENAMESNDEVDO
     E/COPY ZSRSRC,ZNCDZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZPOWER8Z1
     E                    ZF29   14  32  5 0             29TH FEB ARRAY
     ISEDEVDF
     I              NMCY                            NMCYSD
     ISEDEVDO
     I              NMCY                            NMCYSD
     I/COPY ZSRSRC,ZYLDZ1
     ILDAX       UDS                            600
     I                                        1 107 ##SECD
     I                                        1   6 ##MATY
     I                                       11  22 ##CPNR
     I                                       31  31 ##DYBS
     I                                       32  32 ##DVBS
     I                                       33  33 ##STBS
     I                                       34  35 ##SYBS
     I                                       36  83 ##CD
     I                                       36  39 ##CD01
     I                                       40  43 ##CD02
     I                                       44  47 ##CD03
     I                                       48  51 ##CD04
     I                                       52  55 ##CD05
     I                                       56  59 ##CD06
     I                                       60  63 ##CD07
     I                                       64  67 ##CD08
     I                                       68  71 ##CD09
     I                                       72  75 ##CD10
     I                                       76  79 ##CD11
     I                                       80  83 ##CD12
     I                                       84  95 ##CR
     I                                       84  84 ##CR01
     I                                       85  85 ##CR02
     I                                       86  86 ##CR03
     I                                       87  87 ##CR04
     I                                       88  88 ##CR05
     I                                       89  89 ##CR06
     I                                       90  90 ##CR07
     I                                       91  91 ##CR08
     I                                       92  92 ##CR09
     I                                       93  93 ##CR10
     I                                       94  94 ##CR11
     I                                       95  95 ##CR12
     I                                       96 101 ##LCPN
     I                                      102 107 ##NCD
      *
     I                                      116 180 XECD
     I                                      116 1170CMFQ
     I                                      118 118 DSCB
     I                                      119 119 DSLC
     I                                      120 120 PROT
     I                                      121 123 NMCY
     I                                      124 1240CDP
     I                                      125 1250NMDP
     I                                    P 126 1280MATY
     I                                    P 129 1310LCPN
     I                                    P 132 1340NCD
     I                                    P 135 1370ISSD
     I                                    P 138 1400ITLD
     I                                    P 141 1430FCPN
     I                                    P 144 1497CPNR
     I                                    P 150 1559CFCT
     I                                    P 156 1630CPDP
     I                                    P 164 1710SFPP
     I                                    P 172 1730IADJ
     I                                    P 174 1760FNCD
     I                                      177 177 DYBS
     I                                      178 178 DVBS
     I                                      179 179 STBS
     I                                      180 181 SYBS
     I                                      182 182 SPBS
     I                                      183 183 PPDI
     I                                      201 248 CD
     I                                      201 248 CDDS
     I                                      201 2040CD01
     I                                      205 2080CD02
     I                                      209 2120CD03
     I                                      213 2160CD04
     I                                      217 2200CD05
     I                                      221 2240CD06
     I                                      225 2280CD07
     I                                      229 2320CD08
     I                                      233 2360CD09
     I                                      237 2400CD10
     I                                      241 2440CD11
     I                                      245 2480CD12
     I                                      249 260 CRDS
     I                                      249 260 CR
     I                                      249 249 CR01
     I                                      250 250 CR02
     I                                      251 251 CR03
     I                                      252 252 CR04
     I                                      253 253 CR05
     I                                      254 254 CR06
     I                                      255 255 CR07
     I                                      256 256 CR08
     I                                      257 257 CR09
     I                                      258 258 CR10
     I                                      259 259 CR11
     I                                      260 260 CR12
     I                                      261 2650IDAYS
     I                                      266 2700IDYYR
     I                                      271 280 SESN
      *
     I                                      281 403 ##TRDD
     I                                      281 292 ##NOML
     I                                      293 308 ##SYLP
     I                                      309 314 ##TDVD
     I                                      315 315 ##ACIN
     I                                      316 331 ##FYLP
     I                                      332 349 ##CONS
     I                                      350 367 ##DSPR
     I                                      368 385 ##INTR
     I                                      386 403 ##CVAL
     I                                      404 409 ##PUPP
      *
     I                                      410 4200NOML
     I                                      421 4358TPDY
     I                                      436 4400TDVD
     I                                      441 441 ACIN
     I                                      442 4568PRIC
     I                                      457 4710CONS
     I                                      472 4860DSPR
     I                                      487 5010INTR
     I                                      502 5160CVAL
     I                                      517 5214PUPP
      *
     I                                      522 522 YP
     I                                      523 523 LDAC
     C**********************************************************************
     C           *NAMVAR   DEFN           LDAX
     C           *LOCK     IN   LDAX
      *
     C                     MOVE '0'       *IN98
     C                     Z-ADDCDP       ZCDP
     C                     Z-ADDCDP       NCDP
      *
      ** Interest Calculation
      *
     C                     EXSR INTCAL
      *
      ** Consideration calculation
      *
     C                     EXSR CONCAL
      *
      ** Countervalue
      *
     C           CONS      ADD  INTR      CVAL
      *
      **  Terminate program
     C                     OUT  LDAX
     C                     RETRN
     C**********************************************************************
     C           INTCAL    BEGSR
      *
      ** Determine the next coupon date after (or on) the trade value date
      *
     C           TDVD      ADD  1         ECD     50
     C                     EXSR ZNCD
     C                     Z-ADDNCD       FNCD
      *
     C                     Z-ADDTDVD      ECD
     C                     EXSR ZNCD
      *
      ** Set the accrual Indicator
      *
     C           TDVD      IFEQ NCD
     C                     MOVEL'F'       ACIN
     C                     ELSE
     C           ACIN      IFNE 'X'
     C                     MOVEL'C'       ACIN
     C                     END
     C                     END
      *
      ** Determine the last coupon date (prior to the next coupon date)
      *
     C           ACIN      IFEQ 'F'
     C           LDAC      ANDNE'Y'
     C                     Z-ADDTDVD      ZZLCD
     C                     ELSE
     C           NCD       SUB  1         ZECD
     C                     EXSR ZLCD
     C                     END
      *
      ** If no security details present
      *
     C           ITLD      IFEQ 0
     C                     Z-ADDZZLCD     ISSD
     C                     Z-ADDZZLCD     ITLD
     C                     Z-ADDZZLCD     FCPN
     C                     END
      *
      ** Last coupon date
      *
     C                     Z-ADDZZLCD     LCPN
      *
      ** Calculate the interest
      *
     C                     Z-ADDNOML      ZAMT             * nominal
     C           ACIN      IFEQ 'X'
     C                     Z-ADDTDVD      ZDCSDT           * start date
     C                     Z-ADDNCD       ZDCEDT           * end date
     C                     ELSE
     C                     Z-ADDZZLCD     ZDCSDT           * start date
     C                     Z-ADDTDVD      ZDCEDT           * end date
     C                     END
      *
     C                     EXSR ZACCZ
     C                     Z-ADDZDCINT    INTR
     C           PPDI      IFNE 'Y'
     C           PUPP      ANDNE*ZERO
     C                     MULT PUPP      INTR      H
     C                     END
      *
     C                     Z-ADDZDDAYS    IDAYS
     C                     Z-ADDZINTDD    IDYYR
      *
      ** Reverse sign if ex-coupon
      *
     C           ACIN      IFEQ 'X'
     C                     Z-SUBINTR      INTR
     C                     END
     C                     ENDSR
      *****************************************************************
     C           CONCAL    BEGSR
      *
      * SET NOMINAL & CUM/EX-COUPON INDICATOR
      *
     C                     Z-ADDNOML      ZNOML
     C                     MOVELACIN      SCUMEX
      *
      * Make interest figure a percentage
      *
     C           ZNOML     IFNE 0
     C           ZDCINT    DIV  ZNOML     WRKPI
     C                     ELSE
     C                     Z-ADD0         WRKPI
     C                     END
      *
     C           ACIN      IFEQ 'F'
     C                     Z-ADD0         WRKPI
     C                     END
      *
     C           ACIN      IFEQ 'X'
     C                     Z-SUBWRKPI     WRKPI
     C                     END
      *
      * Convert price to yield, or yield to price
      *
     C                     Z-ADDTDVD      ZFDATE           * value date
     C           YP        IFEQ 'Y'
     C                     Z-ADDPRIC      ZPRICE 158       % Price > Yield
     C           PUPP      IFNE *ZERO
     C                     DIV  PUPP      ZPRICE    H
     C                     END
     C                     EXSR ZPRCO
     C                     Z-ADDZPRCOT    TPDY
     C                     ELSE
     C                     Z-ADDTPDY      ZPRCIN 158       Yield > % Price
     C                     EXSR ZPRCI
     C                     Z-ADDZPRCOT    PRIC
     C           PUPP      IFNE *ZERO
     C                     MULT PUPP      PRIC      H
     C                     END
     C                     END
      * Consideration
     C                     Z-ADDPRIC      ZPRC
     C                     EXSR ZCNSD
     C                     Z-ADDZCONS     CONS
      *
      * Discount/premium = Face value - consideration
     C                     Z-ADD100       ZPRC
     C           PUPP      IFNE *ZERO
     C                     MULT PUPP      ZPRC
     C                     END
     C                     EXSR ZCNSD
     C           ZCONS     SUB  CONS      DSPR
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
     C/COPY ZSRSRC,ZCNSDZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZACCZZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZACCRZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZDAYSZ2
     C/SPACE 5
     C/COPY ZSRSRC,ZNCDZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZDYYRZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE1Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE2Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZLCDZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZRATEZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZPRCOZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZPRCIZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZYLDOZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZYLDIZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZYLDZ2
     C/SPACE 5
     C/COPY ZSRSRC,ZCALCD
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
