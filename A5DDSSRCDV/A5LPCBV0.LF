      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas A5 Exp evts for Parents by Company of book brch')*
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  LF/A5LPCBV0 - Exposure Events for Parent By Company of       *
      *                Booking Branch/Booking Branch excl "IN".       *
      *                                                               *
      *  Note:                                                        *
      *  This local LF/EVLPCBV0 is based on a copy of LF/EVLPCB.      *
      *  Any changes to the core LF/EVLPCB must also be applied       *
      *  to this local LF.                                            *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. BA5909   *Create   Date 07Aug23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BA5909 - Various Changes to the Events Reports               *
      *                                                               *
      ****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R EVLPF                     JFILE(EVNTXED +
     A                                      SDCLLMPD SDBRCHPD +
     A                                      SDLIMTPD SDLIMTPD +
     A                                      SDLIMTPD SDLIMTPD +
     A                                      SDCLLMPD SDCOMPPD +
     A                                      SDLIMTPD SDLIMTPD +
     A                                      SDLIMTPD SDLIMTPD +
     A                                      SDCLLMPD SDLIMTPD +
     A                                      SDLIMTPD SDLIMTPD +
     A                                      SDLIMTPD)
     A                                      TEXT('(c) Copyright +
     A                                      Finastra International +
     A                                      Limited 2023')
      *  Join Parent to Branch Limit
     A          J                           JOIN(1 2)
     A                                      JFLD(PTNU DZCUST)
     A                                      JFLD(ELVB DZLEVL)
     A                                      JFLD(BR DZBCCD)
      *  Join Branch Limit to Branch Table
     A          J                           JOIN(1 3)
     A                                      JFLD(BR A8BRCD)
      *
      *  Join Branch Limit Types to Limit Types Table - FX
     A          J                           JOIN(2 4)
     A                                      JFLD(FLTYB A2LMTY)
      *
      *  Join Branch Limit Types to Limit Types Table - Lending
     A          J                           JOIN(2 5)
     A                                      JFLD(LLTYB A2LMTY)
      *
      *  Join Branch Limit Types to Limit Types Table - Money Market
     A          J                           JOIN(2 6)
     A                                      JFLD(MMTYB A2LMTY)
      *
      *  Join Branch Limit Types to Limit Types Table - Precious Metals
     A          J                           JOIN(2 7)
     A                                      JFLD(PLTYB A2LMTY)
      *
      *  Join Parent to Company limit
     A          J                           JOIN(1 8)
     A                                      JFLD(PTNU DZCUST)
     A                                      JFLD(ELVC DZLEVL)
     A                                      JFLD(CO DZBCCD)
      *
      *  Join Company Limit to Company Table
     A          J                           JOIN(1 9)
     A                                      JFLD(CO APCMCD)
      *
      *  Join Company Limit Types to Limit Types Table - FX
     A          J                           JOIN(8 10)
     A                                      JFLD(FLTYC A2LMTY)
      *
      *  Join Company Limit Types to Limit Types Table - Lending
     A          J                           JOIN(8 11)
     A                                      JFLD(LLTYC A2LMTY)
      *
      *  Join Company Limit Types to Limit Types Table - Money Market
     A          J                           JOIN(8 12)
     A                                      JFLD(MMTYC A2LMTY)
      *
      *  Join Company Limit Types to Limit Types Table - Precious Metals
     A          J                           JOIN(8 13)
     A                                      JFLD(PLTYC A2LMTY)
      *
      *  Join Parent to System Limit
     A          J                           JOIN(1 14)
     A                                      JFLD(PTNU DZCUST)
     A                                      JFLD(ELVS DZLEVL)
      *
      *  Join System limit Types to Limit Types Table - FX
     A          J                           JOIN(14 15)
     A                                      JFLD(FLTYS A2LMTY)
      *
      *  Join System limit Types to Limit Types Table - Lending
     A          J                           JOIN(14 16)
     A                                      JFLD(LLTYS A2LMTY)
      *
      *  Join System limit Types to Limit Types Table - Money Market
     A          J                           JOIN(14 17)
     A                                      JFLD(MMTYS A2LMTY)
      *
      *  Join System limit Types to Limit Types Table - Precious Metals
     A          J                           JOIN(14 18)
     A                                      JFLD(PLTYS A2LMTY)
      *  Event Fields
     A            PASK
     A            PTNU
     A            CASK
     A            CUNR                      JREF(1)
     A            EDAT
     A            LCEQ
     A            EXEI
     A            IBRE
     A            ECCY
     A            OTHC
     A            IFXS
     A            SIAC
     A            ICYE
     A            DLNO
     A            BR             3A         RENAME(BRCA) JREF(1)
     A            CO                        RENAME(COBB) JREF(1)
     A            NAME                      RENAME(PRNM) JREF(1)
     A            TOWN                      RENAME(PTWN) JREF(1)
      *
      *  Branch Table Field
     A            A8BRNM
      *
      *  Company Table Field
     A            APCMNM
      *
      *  Branch Limit Fields - FX
     A            FLTYB                     RENAME(DZFXLT) JREF(2)
     A            FLAMB                     RENAME(DZFLAM) JREF(2)
     A            FXFAB                     RENAME(DZFXFA) JREF(2)
     A            FLEDB                     RENAME(DZFLED) JREF(2)
      *
      *  Branch Limit Fields - Lending
     A            LLTYB                     RENAME(DZLLTY) JREF(2)
     A            LLAMB                     RENAME(DZLLAM) JREF(2)
     A            LLEDB                     RENAME(DZLLED) JREF(2)
      *
      *  Branch Limit Fields - Money Market
     A            MMTYB                     RENAME(DZMMTY) JREF(2)
     A            MMAMB                     RENAME(DZMMAM) JREF(2)
     A            MMEDB                     RENAME(DZMMED) JREF(2)
      *
      *  Branch Limit Fields - Precious Metals
     A            PLTYB                     RENAME(DZPLTY) JREF(2)
     A            PFAMB                     RENAME(DZPFAM) JREF(2)
     A            PSAMB                     RENAME(DZPSAM) JREF(2)
     A            PLEDB                     RENAME(DZPLED) JREF(2)
      *
      *  Branch Limit Fields - FRA
     A            RLAMB                     RENAME(DZFRLA) JREF(2)
     A            RLEDB                     RENAME(DZFRED) JREF(2)
      *
      *  Branch Limit Fields - IRS
     A            ILAMB                     RENAME(DZIRLM) JREF(2)
     A            ILEDB                     RENAME(DZIRED) JREF(2)
      *
      *  Branch Limit Fields - FX
     A            FLEP1B                    RENAME(A2E1LP) JREF(4)
     A            FLEP2B                    RENAME(A2E2LP) JREF(4)
     A            FLEP3B                    RENAME(A2E3LP) JREF(4)
     A            FLEP4B                    RENAME(A2E4LP) JREF(4)
     A            FLEP5B                    RENAME(A2E5LP) JREF(4)
     A            FLEP6B                    RENAME(A2E6LP) JREF(4)
     A            FLPC1B                    RENAME(A2PL1P) JREF(4)
     A            FLPC2B                    RENAME(A2PL2P) JREF(4)
     A            FLPC3B                    RENAME(A2PL3P) JREF(4)
     A            FLPC4B                    RENAME(A2PL4P) JREF(4)
     A            FLPC5B                    RENAME(A2PL5P) JREF(4)
     A            FLPC6B                    RENAME(A2PL6P) JREF(4)
      *
      *  Branch Limit Types - Lending
     A            LLEP1B                    RENAME(A2E1LP) JREF(5)
     A            LLEP2B                    RENAME(A2E2LP) JREF(5)
     A            LLEP3B                    RENAME(A2E3LP) JREF(5)
     A            LLEP4B                    RENAME(A2E4LP) JREF(5)
     A            LLEP5B                    RENAME(A2E5LP) JREF(5)
     A            LLEP6B                    RENAME(A2E6LP) JREF(5)
     A            LLPC1B                    RENAME(A2PL1P) JREF(5)
     A            LLPC2B                    RENAME(A2PL2P) JREF(5)
     A            LLPC3B                    RENAME(A2PL3P) JREF(5)
     A            LLPC4B                    RENAME(A2PL4P) JREF(5)
     A            LLPC5B                    RENAME(A2PL5P) JREF(5)
     A            LLPC6B                    RENAME(A2PL6P) JREF(5)
      *
      *  Branch Limit Types  - Money Market
     A            MLEP1B                    RENAME(A2E1LP) JREF(6)
     A            MLEP2B                    RENAME(A2E2LP) JREF(6)
     A            MLEP3B                    RENAME(A2E3LP) JREF(6)
     A            MLEP4B                    RENAME(A2E4LP) JREF(6)
     A            MLEP5B                    RENAME(A2E5LP) JREF(6)
     A            MLEP6B                    RENAME(A2E6LP) JREF(6)
     A            MLPC1B                    RENAME(A2PL1P) JREF(6)
     A            MLPC2B                    RENAME(A2PL2P) JREF(6)
     A            MLPC3B                    RENAME(A2PL3P) JREF(6)
     A            MLPC4B                    RENAME(A2PL4P) JREF(6)
     A            MLPC5B                    RENAME(A2PL5P) JREF(6)
     A            MLPC6B                    RENAME(A2PL6P) JREF(6)
      *
      *  Branch Limit Types  - Precious Metals
     A            PLEP1B                    RENAME(A2E1LP) JREF(7)
     A            PLEP2B                    RENAME(A2E2LP) JREF(7)
     A            PLEP3B                    RENAME(A2E3LP) JREF(7)
     A            PLEP4B                    RENAME(A2E4LP) JREF(7)
     A            PLEP5B                    RENAME(A2E5LP) JREF(7)
     A            PLEP6B                    RENAME(A2E6LP) JREF(7)
     A            PLPC1B                    RENAME(A2PL1P) JREF(7)
     A            PLPC2B                    RENAME(A2PL2P) JREF(7)
     A            PLPC3B                    RENAME(A2PL3P) JREF(7)
     A            PLPC4B                    RENAME(A2PL4P) JREF(7)
     A            PLPC5B                    RENAME(A2PL5P) JREF(7)
     A            PLPC6B                    RENAME(A2PL6P) JREF(7)
      *
      *  Company Limit Fields - FX
     A            FLTYC                     RENAME(DZFXLT) JREF(8)
     A            FLAMC                     RENAME(DZFLAM) JREF(8)
     A            FXFAC                     RENAME(DZFXFA) JREF(8)
     A            FLEDC                     RENAME(DZFLED) JREF(8)
      *
      *  Company Limit Fields - Lending
     A            LLTYC                     RENAME(DZLLTY) JREF(8)
     A            LLAMC                     RENAME(DZLLAM) JREF(8)
     A            LLEDC                     RENAME(DZLLED) JREF(8)
      *
      *  Company Limit Fields - Money Market
     A            MMTYC                     RENAME(DZMMTY) JREF(8)
     A            MMAMC                     RENAME(DZMMAM) JREF(8)
     A            MMEDC                     RENAME(DZMMED) JREF(8)
      *
      *  Company Limit Fields - Precious Metals
     A            PLTYC                     RENAME(DZPLTY) JREF(8)
     A            PFAMC                     RENAME(DZPFAM) JREF(8)
     A            PSAMC                     RENAME(DZPSAM) JREF(8)
     A            PLEDC                     RENAME(DZPLED) JREF(8)
      *
      *  Company Limit Fields - FRA
     A            RLAMC                     RENAME(DZFRLA) JREF(8)
     A            RLEDC                     RENAME(DZFRED) JREF(8)
      *
      *  Company Limit Fields - IRS
     A            ILAMC                     RENAME(DZIRLM) JREF(8)
     A            ILEDC                     RENAME(DZIRED) JREF(8)
      *
      *  Company Limit Fields - FX
     A            FLEP1C                    RENAME(A2E1LP) JREF(10)
     A            FLEP2C                    RENAME(A2E2LP) JREF(10)
     A            FLEP3C                    RENAME(A2E3LP) JREF(10)
     A            FLEP4C                    RENAME(A2E4LP) JREF(10)
     A            FLEP5C                    RENAME(A2E5LP) JREF(10)
     A            FLEP6C                    RENAME(A2E6LP) JREF(10)
     A            FLPC1C                    RENAME(A2PL1P) JREF(10)
     A            FLPC2C                    RENAME(A2PL2P) JREF(10)
     A            FLPC3C                    RENAME(A2PL3P) JREF(10)
     A            FLPC4C                    RENAME(A2PL4P) JREF(10)
     A            FLPC5C                    RENAME(A2PL5P) JREF(10)
     A            FLPC6C                    RENAME(A2PL6P) JREF(10)
      *
      *  Company Limit Types - Lending
     A            LLEP1C                    RENAME(A2E1LP) JREF(11)
     A            LLEP2C                    RENAME(A2E2LP) JREF(11)
     A            LLEP3C                    RENAME(A2E3LP) JREF(11)
     A            LLEP4C                    RENAME(A2E4LP) JREF(11)
     A            LLEP5C                    RENAME(A2E5LP) JREF(11)
     A            LLEP6C                    RENAME(A2E6LP) JREF(11)
     A            LLPC1C                    RENAME(A2PL1P) JREF(11)
     A            LLPC2C                    RENAME(A2PL2P) JREF(11)
     A            LLPC3C                    RENAME(A2PL3P) JREF(11)
     A            LLPC4C                    RENAME(A2PL4P) JREF(11)
     A            LLPC5C                    RENAME(A2PL5P) JREF(11)
     A            LLPC6C                    RENAME(A2PL6P) JREF(11)
      *
      *  Company Limit Types - Money Market
     A            MLEP1C                    RENAME(A2E1LP) JREF(12)
     A            MLEP2C                    RENAME(A2E2LP) JREF(12)
     A            MLEP3C                    RENAME(A2E3LP) JREF(12)
     A            MLEP4C                    RENAME(A2E4LP) JREF(12)
     A            MLEP5C                    RENAME(A2E5LP) JREF(12)
     A            MLEP6C                    RENAME(A2E6LP) JREF(12)
     A            MLPC1C                    RENAME(A2PL1P) JREF(12)
     A            MLPC2C                    RENAME(A2PL2P) JREF(12)
     A            MLPC3C                    RENAME(A2PL3P) JREF(12)
     A            MLPC4C                    RENAME(A2PL4P) JREF(12)
     A            MLPC5C                    RENAME(A2PL5P) JREF(12)
     A            MLPC6C                    RENAME(A2PL6P) JREF(12)
      *
      *  Company Limit Types - Precious Metals
     A            PLEP1C                    RENAME(A2E1LP) JREF(13)
     A            PLEP2C                    RENAME(A2E2LP) JREF(13)
     A            PLEP3C                    RENAME(A2E3LP) JREF(13)
     A            PLEP4C                    RENAME(A2E4LP) JREF(13)
     A            PLEP5C                    RENAME(A2E5LP) JREF(13)
     A            PLEP6C                    RENAME(A2E6LP) JREF(13)
     A            PLPC1C                    RENAME(A2PL1P) JREF(13)
     A            PLPC2C                    RENAME(A2PL2P) JREF(13)
     A            PLPC3C                    RENAME(A2PL3P) JREF(13)
     A            PLPC4C                    RENAME(A2PL4P) JREF(13)
     A            PLPC5C                    RENAME(A2PL5P) JREF(13)
     A            PLPC6C                    RENAME(A2PL6P) JREF(13)
      *
      *  System Limit Fields - FX
     A            FLTYS                     RENAME(DZFXLT) JREF(14)
     A            FLAMS                     RENAME(DZFLAM) JREF(14)
     A            FXFAS                     RENAME(DZFXFA) JREF(14)
     A            FLEDS                     RENAME(DZFLED) JREF(14)
      *
      *  System Limit Fields - Lending
     A            LLTYS                     RENAME(DZLLTY) JREF(14)
     A            LLAMS                     RENAME(DZLLAM) JREF(14)
     A            LLEDS                     RENAME(DZLLED) JREF(14)
      *
      *  System Limit Fields - Money Market
     A            MMTYS                     RENAME(DZMMTY) JREF(14)
     A            MMAMS                     RENAME(DZMMAM) JREF(14)
     A            MMEDS                     RENAME(DZMMED) JREF(14)
      *
      *  System Limit Fields - Precious Metals
     A            PLTYS                     RENAME(DZPLTY) JREF(14)
     A            PFAMS                     RENAME(DZPFAM) JREF(14)
     A            PSAMS                     RENAME(DZPSAM) JREF(14)
     A            PLEDS                     RENAME(DZPLED) JREF(14)
      *
      *  System Limit Fields - FRA
     A            RLAMS                     RENAME(DZFRLA) JREF(14)
     A            RLEDS                     RENAME(DZFRED) JREF(14)
      *
      *  System Limit Fields - IRS
     A            ILAMS                     RENAME(DZIRLM) JREF(14)
     A            ILEDS                     RENAME(DZIRED) JREF(14)
      *
      *  System Limit Fields - FX
     A            FLEP1S                    RENAME(A2E1LP) JREF(15)
     A            FLEP2S                    RENAME(A2E2LP) JREF(15)
     A            FLEP3S                    RENAME(A2E3LP) JREF(15)
     A            FLEP4S                    RENAME(A2E4LP) JREF(15)
     A            FLEP5S                    RENAME(A2E5LP) JREF(15)
     A            FLEP6S                    RENAME(A2E6LP) JREF(15)
     A            FLPC1S                    RENAME(A2PL1P) JREF(15)
     A            FLPC2S                    RENAME(A2PL2P) JREF(15)
     A            FLPC3S                    RENAME(A2PL3P) JREF(15)
     A            FLPC4S                    RENAME(A2PL4P) JREF(15)
     A            FLPC5S                    RENAME(A2PL5P) JREF(15)
     A            FLPC6S                    RENAME(A2PL6P) JREF(15)
      *
      *  System Limit Types - Lending
     A            LLEP1S                    RENAME(A2E1LP) JREF(16)
     A            LLEP2S                    RENAME(A2E2LP) JREF(16)
     A            LLEP3S                    RENAME(A2E3LP) JREF(16)
     A            LLEP4S                    RENAME(A2E4LP) JREF(16)
     A            LLEP5S                    RENAME(A2E5LP) JREF(16)
     A            LLEP6S                    RENAME(A2E6LP) JREF(16)
     A            LLPC1S                    RENAME(A2PL1P) JREF(16)
     A            LLPC2S                    RENAME(A2PL2P) JREF(16)
     A            LLPC3S                    RENAME(A2PL3P) JREF(16)
     A            LLPC4S                    RENAME(A2PL4P) JREF(16)
     A            LLPC5S                    RENAME(A2PL5P) JREF(16)
     A            LLPC6S                    RENAME(A2PL6P) JREF(16)
      *
      *  System Limit Types - Money Market
     A            MLEP1S                    RENAME(A2E1LP) JREF(17)
     A            MLEP2S                    RENAME(A2E2LP) JREF(17)
     A            MLEP3S                    RENAME(A2E3LP) JREF(17)
     A            MLEP4S                    RENAME(A2E4LP) JREF(17)
     A            MLEP5S                    RENAME(A2E5LP) JREF(17)
     A            MLEP6S                    RENAME(A2E6LP) JREF(17)
     A            MLPC1S                    RENAME(A2PL1P) JREF(17)
     A            MLPC2S                    RENAME(A2PL2P) JREF(17)
     A            MLPC3S                    RENAME(A2PL3P) JREF(17)
     A            MLPC4S                    RENAME(A2PL4P) JREF(17)
     A            MLPC5S                    RENAME(A2PL5P) JREF(17)
     A            MLPC6S                    RENAME(A2PL6P) JREF(17)
      *
      *  System Limit Types - Precious Metals
     A            PLEP1S                    RENAME(A2E1LP) JREF(18)
     A            PLEP2S                    RENAME(A2E2LP) JREF(18)
     A            PLEP3S                    RENAME(A2E3LP) JREF(18)
     A            PLEP4S                    RENAME(A2E4LP) JREF(18)
     A            PLEP5S                    RENAME(A2E5LP) JREF(18)
     A            PLEP6S                    RENAME(A2E6LP) JREF(18)
     A            PLPC1S                    RENAME(A2PL1P) JREF(18)
     A            PLPC2S                    RENAME(A2PL2P) JREF(18)
     A            PLPC3S                    RENAME(A2PL3P) JREF(18)
     A            PLPC4S                    RENAME(A2PL4P) JREF(18)
     A            PLPC5S                    RENAME(A2PL5P) JREF(18)
     A            PLPC6S                    RENAME(A2PL6P) JREF(18)
     A            BOKC
     A          K PASK
     A          K PTNU
     A          K CO
     A          K BR
     A          S EXEI                      COMP(NE ' ')
     A            PTNU                      COMP(NE ' ')
     A            PTNU                      COMP(NE '000000')
     A            IBRE                      COMP(NE 'Y')
     A            BOKC                      COMP(NE 'IN')
