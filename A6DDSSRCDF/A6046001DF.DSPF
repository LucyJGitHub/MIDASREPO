      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Structured MT940 Mapping details Screen 1')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/A6046001DF - Structured MT940 Mapping details Screen 1  *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. BA6026 *CREATE     Date 18Jan23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  BA6026 - Structuring Current MT940 and SEPA Compliance.      *
      *           Include GSH046.                                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *=========================================================================
     A          R A6046001F1
     A                                      TEXT('(C) Finastra International Sy-
     A                                      stems Ltd 2023')
     A                                      INDTXT(44 'Invalid: S1SEL' )
     A                                      INDTXT(21 'Protect fields')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Prev.')
     A                                      CF10(10 'Delete')
     A                                      OVERLAY
      *                                                                =========
      ** Header details.                                               =========
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A                                  1112DATE
     A                                      EDTCDE(Y)
     A            JOB           10A  O  2  2TEXT('*JOB')
     A                                  2112TIME
     A                                  2 43'Structured MT940 Mapping -
     A                                      Maintenance - ADD MODE'
     A                                      DSPATR(HI UL)
      *                                                                =========
      ** Fields details.                                               =========
     A                                  5 15'Swift Identification'
     A                                      DSPATR(HI)
     A                                  6 15'Code'
     A                                      DSPATR(HI)
     A                                  5 40'Midas Transaction'
     A                                      DSPATR(HI)
     A                                  6 40'Type'
     A                                      DSPATR(HI)
     A            C1SWID         4A  B  8 15TEXT('SWIFT Id')
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N21N20                               DSPATR(UL)
     A  20
     AO 21                                  DSPATR(PR)
     A            C1TRAT         5A  B  8 40TEXT('Midas TRAT')
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N21N20                               DSPATR(UL)
     A  20
     AO 21                                  DSPATR(PR)
      *=========================================================================
      * * Subfile record details format.
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finantial Inter-
     A                                      national Systems Ltd. 2023')
     A  74                                  SFLNXTCHG
     A            S1SEL          1A  B  8  7TEXT('Action')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  46                                  DSPATR(RI)
     A            S1SWID         4A  O  8 15TEXT('Swift Id Code')
     A            S1TRAT         5A  O  8 40TEXT('Trans Type')
     A*=========================================================================
      * * Subfile control format.
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(25 'Disable OVRDTA')
     A                                      INDTXT(51 'Subfile message ID')
     A                                      INDTXT(60 'Clear subfile')
     A                                      INDTXT(61 'Display subfile')
     A                                      INDTXT(62 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  60                                  SFLDSP
     A  61                                  SFLDSPCTL
     A N60N61                               SFLCLR
     A  62                                  SFLEND(*MORE)
     A  51                                  SFLMSGID(CPF5203 *LIBL/QCPFMSG)
     A                                      CA03(03 'Exit.')
     A                                      ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add mode.')
     A                                      HELP
     A          H                           HLPARA(1 1 24 80)
     A                                      HLPRCD(HELPREC)
     A            UISFRN         4S 0H      SFLRCDNBR(CURSOR)
      *                                                                =========
      ** Header details.                                               =========
     A            USER          10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A                                  1112DATE
     A                                      EDTCDE(Y)
     A            JOB           10A  O  2  2TEXT('*JOB')
     A                                  2112TIME
     A                                  2 43'Structured MT940 Mapping -
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
      *                                                                =========
      ** Fields details.                                               =========
     A                                  4 15'Swift Identification'
     A                                      DSPATR(HI)
     A                                  5 15'Code'
     A                                      DSPATR(HI)
     A                                  4 40'Midas Transaction'
     A                                      DSPATR(HI)
     A                                  5 40'Type'
     A                                      DSPATR(HI)
     A            C2SWID         4A  B  6 15TEXT('Swift Id Code')
     A                                      DSPATR(HI UL)
     A            C2TRAT         5A  B  6 40TEXT('Trans Type')
     A                                      DSPATR(HI UL)
      *=========================================================================
     A          R FOOTER                    TEXT('Command keys.')
     A N23                             23  1'Actions Allowed:'
     A N23                             23 18'E/A/D'
     A                                 23 26'F3=Main Menu'
     A N23                             23 40'F9=Go to ''Add'' Mode'
     A  24                             23 40'F10=Confirm delete'
     A  23                             23 60'F12=Previous'
      *                                                                =========
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
      * Help Text Format displayed when help key is pressed
      *
     A          R HELPREC
     A                                  1 12'Structured MT940 Mapping Details -
     A                                       Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  2'This screen allows the definition -
     A                                       of Mapping details for combination-
     A                                        of SWIFT Id Codes and Midas -
     A                                       Transaction Types'
     A                                 23  2'Press ENTER to return'
