      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Utility to Sync GLNW94PD and A6NW94PD')                *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/A6000861P1 - Utility to Sync GLNW94PD and A6NW94PD      *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. BA6026   *CREATE   Date 18Jan23               *
      *  Prev Amend No. xxxxxxxx           Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BA6026 - Structuring Current MT940 and SEPA Compliance.      *
      *           Include MD058337.                                   *
      *                                                               *
      *****************************************************************
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADER1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2023')
     A                                      SPACEA(001)
     A                                      SKIPB(001)
     A                                     6
     A                                      'REFERENCE A6000861P1'
     A            BJURPT        53A  O    37
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7A  O   106
     A                                   119
     A                                      'PAGE'
     A                                    +2
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A N16                                 6'AUDIT MODE'
     A  16                                 6'UPDATE MODE'
     A                                      SPACEA(001)
     A  16                                50'RECORDS UPDATED +
     A                                      IN A6NW94PD'
     A N16                                50'RECORDS FOR UPDATE +
     A                                      IN A6NW94PD'
     A                                      SPACEB(001)
     A  16                                50'----------------+
     A                                      -----------'
     A N16                                50'---------------------+
     A                                      -----------'
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A                                    11'Customer No.'
     A
     A                                    32'Currency'
     A
     A                                    50'Account Code'
     A
     A                                    74'Branch'
     A
     A                                    91'A/C Seq.'
     A
     A                                   111'Action Done'
      *
      *----------------*
      ** Report Detail *
      *----------------*
      *
     A          R DETAIL1
     A                                      SPACEA(001)
      *
     A            G4CNUM         6A  O    14
      *
     A            G4CCY          3A  O    35
      *
     A            G4ACCD        10S 0O    51
     A*
     A            G4BRCA         3A  O    75
      *
     A            G4ACSQ         2S 0O    95
      *
     A            ACTION         6A      116
      *
      *
      *-----------------*
      ** Report Summary *
      *-----------------*
      *
     A          R TRAILER1
     A N16 12                             35
     A                                      'Number of records to be +
     A                                      INSERTED:'
     A                                      SPACEB(001)
     A  16 12                             35
     A                                      'Number of records INSERTED:'
     A                                    81'='
     A  12        RCOUNT         6S 0O    84
     A                                      EDTCDE(3)
     A N16 12                             35
     A                                      'Number of records to be +
     A                                      DELETED:'
     A                                      SPACEB(001)
     A  16 12                             35
     A                                      'Number of records DELETED:'
     A                                    81'='
     A  12        DCOUNT         6S 0O    84
     A                                      EDTCDE(3)
     A  12                                51
     A                                      '   ***  END OF REPORT  ***'
     A                                      SPACEB(4)
     A N12                                51
     A                                      '***  NO DETAILS TO REPORT  +
     A                                      ***'
