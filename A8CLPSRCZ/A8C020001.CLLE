/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas MS Alliance automated transmission')            */
/*********************************************************************/
/*                                                                   */
/*       Midas/SWIFT Direct Link Module                              */
/*                                                                   */
/*       A8C020001 - Automated Transmission                          */
/*                                                                   */
/*       (c) Finastra International Limited 2023                     */
/*                                                                   */
/*       Last Amend No. BA8020  *CREATE    Date 03Apr23              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BA8020 - Enable Inter.Pel transmission of SWIFT outgoing    */
/*                messages from Midas                                */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&ENLPHO &PFX)

             DCLF       FILE(MSLISTPD)

             DCL        VAR(&ENLPHO) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PFX)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&AFTCMD) TYPE(*CHAR) LEN(256)
             DCL        VAR(&RECDEC) TYPE(*DEC) LEN(5 0) VALUE(0)
             DCL        VAR(&RECTXT) TYPE(*CHAR) LEN(7) +
                          VALUE('MXNNNNN')
             DCL        VAR(&DIR) TYPE(*CHAR) LEN(50) VALUE('/usr/bin/')

             COPYRIGHT TEXT('(c) Finastra International Limited 2023')

/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

/* Set off all switches */
             CHGJOB     SWS(XXXXXX00)

/* Access list of collected SWIFTOut*.dat messages and prepare the tmp file */
 LOOP:       RCVF       RCDFMT(MSLISTPD)
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDLOOP))

             CHGVAR     VAR(&RECDEC) VALUE(&RECDEC + 1)
             CHGVAR     VAR(%SST(&RECTXT 3 5)) VALUE(&RECDEC)

             ADDPFM     FILE(A8LPCCPP) MBR(&RECTXT)

             CHGVAR     VAR(&AFTCMD) VALUE(&DIR *TCAT 'cat ' *CAT +
                          &ENLPHO *TCAT '/' *CAT &MSLISTPD *TCAT ' +
                          > ' *CAT &ENLPHO *TCAT '/' *CAT &RECTXT +
                          *CAT '.tmp')
             QSH        CMD(&AFTCMD)

             CHGVAR     VAR(&AFTCMD) VALUE(&DIR *TCAT 'cp ' *CAT +
                          &ENLPHO *TCAT '/' *CAT &RECTXT *CAT '.tmp +
                          /qsys.lib/' *CAT &PFX *CAT +
                          'dplib.lib/A8LPCCPP.file/' *CAT &RECTXT +
                          *CAT '.mbr')
             QSH        CMD(&AFTCMD)

             CHGVAR     VAR(&AFTCMD) VALUE(&DIR *TCAT 'rm -f ' *CAT +
                          &ENLPHO *TCAT '/' *CAT &RECTXT *CAT '.tmp')
             QSH        CMD(&AFTCMD)

             GOTO       CMDLBL(LOOP)

/* End loop */
 ENDLOOP:    CPYF       FROMFILE(A8LPCCPP) TOFILE(A8LPCCPP) +
                          FROMMBR(MX*) TOMBR(A8LPCCPP) +
                          MBROPT(*REPLACE) RCDFMT(*ALL) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF2868 CPF2817)

             GOTO       CMDLBL(END)

/* Abnormal termination */
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          A8C020001 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
               MONMSG     MSGID(CPF0000 MCH0000)

 END:
             ENDPGM
