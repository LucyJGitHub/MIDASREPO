/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas A9 TAB001 upload to A9ACHOMTD')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       A9C005004 - Midas A9 TAB001 upload to A9ACHOMTD             */
/*                                                                   */
/*       (c) Finastra International Limited 2024                     */
/*                                                                   */
/*       Last Amend No. BA9005  *CREATE    Date 11Mar24              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BA9005 - HO and EDW Extracts                                */
/*                                                                   */
/*********************************************************************/
             PGM

             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) Finastra International +
                          Limited 2024')
             DCL        VAR(&LDA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&POPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PSARD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SCSARD) TYPE(*CHAR) LEN(200)
             DCL        VAR(&BA9005) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(8)

             DCL        VAR(&PRE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)

/**/
/** Global monitor message */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO CMDLBL(ABNOR))

             SNDPGMMSG  MSG('A9C005004 - Midas A9 TAB001 upload to A9ACHOMTD') TOMSGQ(MRUNQ)

/**/
             CHGJOB     SWS(00000000)
/**/
/** Retrieve Zone prefix */
/**/
             RTVDTAARA  DTAARA(SDSTAT *ALL) RTNVAR(&SDSTAT)

             CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
             CHGVAR     VAR(&DPLIB) VALUE(&PRE *TCAT 'DPLIB')
/**/
/** Check if BA9005 is ON */
/**/
             CHGVAR     VAR(&PRTCD) VALUE('       ')
             CHGVAR     VAR(&POPTN) VALUE('*VERIFY')
             CHGVAR     VAR(&PSARD) VALUE('BA9005')
             CHGVAR     VAR(&BA9005) VALUE('N')

             CALL       PGM(AOSARDR0) PARM(&PRTCD &POPTN &PSARD &SCSARD)

             IF         COND(&PRTCD *EQ '       ') THEN(DO)
                CHGVAR     VAR(&BA9005) VALUE('Y')
             ENDDO

/**/
/** Process only if BA9005 is ON */
/**/
             IF         COND(&BA9005 *EQ 'Y') THEN(DO)

/**/
/** Upload TAB001TD details to A9ACHOMTD */
/**/
                CPYF       FROMFILE(TAB001) TOFILE(&DPLIB/TAB001TD) MBROPT(*REPLACE) +
                             FMTOPT(*NOCHK)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM FILE(&DPLIB/TAB001TD))

                CALL       PGM(A9005007)

/**/
/** Database error processing */
/**/
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   RTVDTAARA  DTAARA(LDA) RTNVAR(&LDA)
                   CHGVAR     VAR(&MEM) VALUE(%SST(&LDA 134 50))
                   SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) TOMSGQ(MOPERQ MRUNQ)
                ENDDO

             ENDDO


             GOTO       CMDLBL(ENDPGM)

 ABNOR:
             CHGJOB     SWS(XXXXXX11)

/** Abnormal termination */

             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program A9C005004 ended +
                          abnormally - see job log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)

 ENDPGM:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Finastra International Limited 2024')
             ENDPGM
