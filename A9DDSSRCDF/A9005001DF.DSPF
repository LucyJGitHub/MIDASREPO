      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas A9 Account Codes Mapping to HO Details')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  A9005001DF - Midas A9 Account Codes Mapping to HO Details    *
      *               Enquiry screen                                  *
      *                                                               *
      *  (c) Finastra International Limited 2024                      *
      *                                                               *
      *  Last Amend No. BA9005   *Create   Date 11Mar24               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BA9005 - HO and EDW Extracts                                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Subfile record.
      *
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2024')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(35 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1SEL          1A  B  8  5VALUES('E' 'A' 'D')
     A                                      CHECK(AB)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1MDAC        10A  O  8 10TEXT('Midas Account Code')
     A                                      DSPATR(HI)
     A            #1HOAC         5A  O  8 27TEXT('HO Account Code')
     A                                      DSPATR(HI)
     A            #1HOPC         4A  O  8 39TEXT('HO Product Code')
     A                                      DSPATR(HI)
     A            #1HOMC         3A  O  8 54TEXT('HO Market Code')
     A                                      DSPATR(HI)
     A            #1EXCL         1A  O  8 66TEXT('Exclude?')
     A                                      DSPATR(HI)
      *
      ** Subfile control.
      *
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2MDAC')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##URPT        53A  O  2 13TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            ##USR         10A  O  1  2TEXT('*USER')
     A                                  1 13'A9005001'
     A                                  4 10'Midas'
     A                                  4 25'Head Office'
     A                                  5 10'A/C Code'
     A                                  5 25'A/c Code'
     A                                  5 35'Product Code'
     A                                  5 50'Market Code'
     A                                  5 63'Exclude?'
     A            #2MDAC        10A  B  6 10TEXT('Midas Account Code')
     A N70                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A N25                                  OVRDTA
     A N25                                  OVRATR
     A            #2HOAC         5A  B  6 27TEXT('HO Account Code')
     A N25                                  OVRDTA
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            #2HOPC         4A  B  6 39TEXT('HO Product Code')
     A N25                                  OVRDTA
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            #2HOMC         3A  B  6 54TEXT('HO Product Code')
     A N25                                  OVRDTA
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            #2EXCL         1A  B  6 66TEXT('HO Exclude')
     A N25                                  OVRDTA
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *
      ** Function keys record format.
      *
     A          R #CMDTXT1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##CTX1        78A  O 22  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A            ##CTX2        78A  O 23  2TEXT('Command Text 2')
     A N25                                  OVRDTA
      *
      ** Subfile message.
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      ** Subfile control message.
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A  86                                  OVERLAY
     A  86                                  PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)

