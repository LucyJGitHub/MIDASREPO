      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas AA Analytical accounting data area')
      *****************************************************************
      *                                                               *
      *  Midas - Analytical Accounting Module                         *
      *                                                               *
      *  PF/AADTA - Analytical Accounting Data Area                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. 184967             Date 11Sep00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAC003             Date 19JUN97               *
      *                 120831             Date 11Aug97               *
      *                 120826             Date 11Aug97               *
      *                 CAC002             Date 15SEP96               *
      *                 CAC001 *CREATE     Date 01FEB96               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  184967 - PCA Implementation Utilities                        *
      *  CAC003 - Profit Centre Accounting Development - Phase 3      *
      *  120831 - Set up blank Profit centres in APOSTPD and GLACPHPD *
      *  120826 - Set up more PCA fields in implementation to         *
      *           update PCA and Management Accounts fields in        *
      *           SDACODPD, SDCUSTPD, SDPRFCPD.                       *
      *  CAC002 - Profit Centre Accounting Development - Phase 2      *
      *           Add book and transaction profit centres for SE.     *
      *  CAC001 - Profit Centre Accounting Development                *
      *                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R AADTAF                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            AAREPC    R               REFFLD(PRFC)
     A                                      TEXT('Retail Profit Centre')
     A                                      COLHDG('Retail' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AANSPC    R               REFFLD(PRFC)
     A                                      TEXT('Nostro Profit Centre')
     A                                      COLHDG('Nostro' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AAFXPC    R               REFFLD(PRFC)
     A                                      TEXT('FX Profit Centre')
     A                                      COLHDG(' FX ' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AAMMPC    R               REFFLD(PRFC)
     A                                      TEXT('MM Profit Centre')
     A                                      COLHDG(' MM   ' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AANAPC    R               REFFLD(PRFC)
     A                                      TEXT('NA Purch Profit Centre')
     A                                      COLHDG('NAP' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AALEPC    R               REFFLD(PRFC)
     A                                      TEXT('Cust Lend Profit Centre')
     A                                      COLHDG('  LE  ' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AACXPC    R               REFFLD(PRFC)
     A                                      TEXT('RE Cusex Profit Centre')
     A                                      COLHDG('RE Cus' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AAFPPC    R               REFFLD(PRFC)
     A                                      TEXT('FX Pos Proft Centre')
     A                                      COLHDG('FX Pos' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AADMPC    R               REFFLD(PRFC)
     A                                      TEXT('Dump Profit Centre')
     A                                      COLHDG(' Dump ' +
     A                                             'Profit' +
     A                                             'Centre')
      *                                              Begin /CAC002/
     A            AABKPC    R               REFFLD(PRFC)
     A                                      TEXT('Book Profit Centre')
     A                                      COLHDG(' Book ' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AATRPC    R               REFFLD(PRFC)
     A                                      TEXT('Trans Profit Centre')
     A                                      COLHDG('Trans ' +
     A                                             'Profit' +
     A                                             'Centre')
      *                                              End   /CAC002/
      *
      *                                              Begin /120826/
     A            AAMAPC         1          TEXT('Man A/C Anal Profit Cntr')
     A                                      COLHDG('Man A/C Anal' +
     A                                             'Profit Cntr')
      *
     A            AAMABK         1          TEXT('Man A/C Anal Book')
     A                                      COLHDG('Man A/C' +
     A                                             'Anal Book')
      *
     A            AAMAAC         1          TEXT('Man A/C Anal Ass Cust')
     A                                      COLHDG('Man A/C Anal' +
     A                                             'Ass Cust')
      *
     A            AAMAPR         1          TEXT('Man A/C Anal Prod')
     A                                      COLHDG('Man A/C' +
     A                                             'Anal Prod')
      *
     A            AAMCAC         1          TEXT('MA Analysis by Assoc Cust')
     A                                      COLHDG('MA Analysis' +
     A                                             'by Assoc Cust' )
      *
     A            AADFPI         1          TEXT('Direct Fund Profit Centre?')
     A                                      COLHDG('Direct Fund' +
     A                                             'Profit Centre?' )
      *
     A            AAFXPI         1          TEXT('FX Position Profit Centre?')
     A                                      COLHDG('FX Position' +
     A                                             'Profit Centre?')
      *
     A            AALFPC         4          TEXT('Dflt Local Fund Prft Ctr')
     A                                      COLHDG('Dflt Local' +
     A                                             'Fund Prft Ctr' )
      *
     A            AAFFPC         4          TEXT('Dflt Foreign Fund Prft Ct')
     A                                      COLHDG('Dflt Foreign' +
     A                                             'Fund Prft Ct' )
      *
     A            AAXPPC         4          TEXT('FX Position Profit Cntr.')
     A                                      COLHDG('FX Position' +
     A                                             'Profit Cntr.' )
      *
      *                                              End   /120826/
      *
      *                                              Begin /120831/
     A            AAPRFC         4          TEXT('Accnt Post Profit Cntr.')
     A                                      COLHDG('Accnt Post' +
     A                                             'Profit Cntr.' )
      *
      *                                              End   /120831/
      *                                              Begin /CAC003/
     A            AAFFBP    R               REFFLD(PRFC)
     A                                      TEXT('FF Book Profit Centre')
     A                                      COLHDG(' FF Book ' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AAFFTP    R               REFFLD(PRFC)
     A                                      TEXT('FF Trans Profit Centre')
     A                                      COLHDG('FF Trans ' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            AAPMPC    R               REFFLD(PRFC)
     A                                      TEXT('PM Profit Centre')
     A                                      COLHDG('PM' +
     A                                             'Profit' +
     A                                             'Centre')
      *                                              End   /CAC003/
      *                                              Begin /184967/
     A            AARNPI         1          TEXT('Retl/Nost PRFC added to+
     A                                      A/C Ind.')
     A                                      COLHDG('Retail/Nostro ' +
     A                                             'PRFC added to ' +
     A                                             'Accnts')
      *
     A            AAPCBC         1          TEXT('AA0026 has run succesfully')
     A                                      COLHDG('AA0026' +
     A                                             'has' +
     A                                             'run')
      *                                              End   /184967/
      *
      *                                              Begin /CAC002/
     A*********** ZZ064         64A
      *                                              Begin /120826/
     A*********** ZZ056         56A
      *                                              Begin /CAC003/
     A*********** ZZ033         33A
      *                                              Begin /184967/
     A*********** ZZ021         21A
     A            AADZ19        19A         TEXT('Filler field')
      *                                              End   /184967/
      *                                              End   /CAC003/
      *                                              End   /120826/
      *                                              End   /CAC002/
      *
