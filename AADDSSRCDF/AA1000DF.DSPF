      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AA Funding window display')
      *****************************************************************
      *                                                               *
      *  Midas - Window Processing                                    *
      *                                                               *
      *  AA1000DF - Funding Window Display                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC001 *CREATE     Date 01FEB96               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAC001  -  Profit Centre Accounting Development              *
      *                                                               *
      *****************************************************************
     A*
     A                                      PRINT(MPRINT)
     A N23N24                               CA03(03 'Exit')
     A N20N21N23
     AAN24                                  CA05(05 'Reset')
     A                                      CA12(12 'Previous')
     A                                      CHGINPDFT
     A*
     A          R AA1000F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      WINDOW(&SWROW &SWCOL 8 43 *NOMSGLIN)
     A                                      KEEP
     A                                      OVERLAY
     A            SPGM          10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         40A  O  1  1DSPATR(HI)
     A*
     A                                  3  2'Funding Spread/Rate   :'
     A                                  4  2'Funding Spread Sign   :'
     A                                  5  2'Funding Profit Centre :'
     A*
     A            SFSRP         12A  B  3 27
     A N20N21N23
     AAN24                                  DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  20
     AO 21
     AO 23
     AO 24                                  DSPATR(PR)
     A            SFSGN          1A  B  4 27
     A N20N21N22
     AAN23N24                               DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A  20
     AO 21
     AO 22
     AO 23
     AO 24                                  DSPATR(PR)
     A            SFPRC          4A  B  5 27
     A N20N21N23
     AAN24N25                               DSPATR(UL)
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A  20
     AO 21
     AO 23
     AO 24
     AO 25                                  DSPATR(PR)
     A*
     A N23N24                           7  1'F3=Exit'
     A N20N21N23
     AAN24                              7 11'F5=Refresh'
     A N23N24                           7 24'F12=Previous'
     A*================================================================
     A*    Dummy record to prevent full-screen display being cleared
     A*
     A          R AA1000F1
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A            DUMMYF         1A  O  1 80DSPATR(ND)
     A*
     A*================================================================
     A*
     A          R AA1000S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(8)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
     A*
     A*================================================================
     A*
     A          R AA1000C0                  TEXT('Program messages')
     A                                      WINDOW(AA1000F0)
     A                                      SFLCTL(AA1000S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
