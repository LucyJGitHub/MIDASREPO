      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AA Margin window display')
      *****************************************************************
      *                                                               *
      *  Midas - Window Processing                                    *
      *                                                               *
      *  AA1001DF - Margin Window Display                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC001 *CREATE     Date 01FEB96               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAC001  -  Profit Centre Accounting Development              *
      *                                                               *
      *****************************************************************
     A*
     A                                      PRINT(MPRINT)
     A N21N22                               CA03(03 'Exit')
     A N20N21N22                            CA05(05 'Reset')
     A                                      CA12(12 'Previous')
     A                                      CHGINPDFT
     A*
     A          R AA1001F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      WINDOW(&SWROW &SWCOL 8 38 *NOMSGLIN)
     A                                      KEEP
     A                                      OVERLAY
     A            SPGM          10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         35A  O  1  1DSPATR(HI)
     A*
     A                                  3  2'FX Margin Points     :'
     A                                  4  2'Margin P/S Indicator :'
     A                                  5  2'Margin Sign          :'
     A*
     A            SFXMP          8A  B  3 26
     A N20N21N22                            DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  20
     AO 21
     AO 22                                  DSPATR(PR)
     A            SMBSI          1A  B  4 26
     A N20N21N22                            DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A  20
     AO 21
     AO 22                                  DSPATR(PR)
     A            SMSIG          1A  B  5 26
     A N20N21N22                            DSPATR(UL)
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A  20
     AO 21
     AO 22                                  DSPATR(PR)
     A*
     A N21N22                           7  1'F3=Exit'
     A N20N21N22                        7 10'F5=Refresh'
     A N21N22                           7 22'F12=Previous'
     A*================================================================
     A*    Dummy record to prevent full-screen display being cleared
     A*
     A          R AA1001F1
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A            DUMMYF         1A  O  1 80DSPATR(ND)
     A*
     A*================================================================
     A*
     A          R AA1001S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(8)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
     A*
     A*================================================================
     A*
     A          R AA1001C0                  TEXT('Program messages')
     A                                      WINDOW(AA1001F0)
     A                                      SFLCTL(AA1001S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
