      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AA AADTA maintenence screen')
      *****************************************************************
      *                                                               *
      *  Midas - (Module Name) Module                                 *
      *                                                               *
      *  DSPF/AADTADF - AADTA Maintainence Screen                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 186368             Date 22Mar01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 184967  *CREATE    Date 12Sep00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  186368 - Accnt Post Profit Centre not displayed on screen.   *
      *  184967 - PCA Implementation Utilities                        *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit')
     A                                      INDARA
     A                                      MSGLOC(24)
      *
     A          R AA3000F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'AA3000DF'
     A                                  1 25'AADTA Maintenence'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R AA3000F1
      *
     A                                      PUTOVR
     A  25                              7  2'Retail PRFC.......:'
     A  25        AAREPC         4A  B  7 25
     A  25N56                               DSPATR(HI)
     A  25 56                               DSPATR(RI PC)
     A  25N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  8  2'Nostro PRFC.......:'
     A            AANSPC         4A  B  8 25
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  26                              9  2'FX PRFC...........:'
     A  26        AAFXPC         4A  B  9 25
     A  26N33                               DSPATR(HI)
     A  26 33                               DSPATR(RI PC)
     A  26N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A  31                             10  2'MM PRFC...........:'
     A  31        AAMMPC         4A  B 10 25
     A  31N34                               DSPATR(HI)
     A  31 34                               DSPATR(RI PC)
     A  31N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A  26                             11  2'NA PRFC...........:'
     A  26        AANAPC         4A  B 11 25
     A  26N35                               DSPATR(HI)
     A  26 35                               DSPATR(RI PC)
     A  26N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  27                             12  2'Cust Lend PRFC....:'
     A  27        AALEPC         4A  B 12 25
     A  27N36                               DSPATR(HI)
     A  27 36                               DSPATR(RI PC)
     A  27N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  25                             13  2'RE Cusex PRFC.....:'
     A  25        AACXPC         4A  B 13 25
     A  25N37                               DSPATR(HI)
     A  25 37                               DSPATR(RI PC)
     A  25N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A  26                             14  2'FX Pos PRFC.......:'
     A  26        AAFPPC         4A  B 14 25
     A  26N38                               DSPATR(HI)
     A  26 38                               DSPATR(RI PC)
     A  26N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 15  2'Balanced PRFC.....:'
     A            AADMPC         4A  B 15 25
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  28                             16  2'Book PRFC.........:'
     A  28        AABKPC         4A  B 16 25
     A  28N40                               DSPATR(HI)
     A  28 40                               DSPATR(RI PC)
     A  28N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  28                             17  2'Trans PRFC........:'
     A  28        AATRPC         4A  B 17 25
     A  28N41                               DSPATR(HI)
     A  28 41                               DSPATR(RI PC)
     A  28N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 18  2'Man A/C Anal PRFC.:'
     A            AAMAPC         1A  B 18 25 VALUES('Y' 'N')
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 19  2'Man A/C Anal Book.:'
     A            AAMABK         1A  B 19 25 VALUES('Y' 'N')
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      OVRDTA
      *
      *
     A                                  7 40'Man A/C Anal Ass Cust.....:'
     A            AAMAAC         1A  B  7 70 VALUES('Y' 'N')
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                  8 40'Man A/C Anal Prod.........:'
     A            AAMAPR         1A  B  8 70 VALUES('Y' 'N')
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                  9 40'MA Analysis By Assoc Cust.:'
     A            AAMCAC         1A  B  9 70 VALUES('Y' 'N')
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 10 40'Direct Fund PRFC?.........:'
     A            AADFPI         1A  B 10 70 VALUES('Y' 'N')
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 11 40'FX Position PRFC?.........:'
     A            AAFXPI         1A  B 11 70 VALUES('Y' 'N')
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 12 40'Default Local Fund PRFC...:'
     A            AALFPC         4A  B 12 70
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 13 40'Default Foreign Fund PRFC.:'
     A            AAFFPC         4A  B 13 70
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 14 40'FX Position PRFC..........:'
     A            AAXPPC         4A  B 14 70
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A                                 15 40'Accnt Post PRFC...........:'
      *                                              Begin /186368/
     A*********** AAPRPC         4A  B 15 70
     A            AAPRFC         4A  B 15 70
      *                                              End   /186368/
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  29                             16 40'FF Book PRFC..............:'
     A  29        AAFFBP         4A  B 16 70
     A  29N53                               DSPATR(HI)
     A  29 53                               DSPATR(RI PC)
     A  29N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  29                             17 40'FF Trans PRFC.............:'
     A  29        AAFFTP         4A  B 17 70
     A  29N54                               DSPATR(HI)
     A  29 54                               DSPATR(RI PC)
     A  29N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      OVRDTA
      *
      *
     A  30                             18 40'PM PRFC...................:'
     A  30        AAPMPC         4A  B 18 70
     A  30N55                               DSPATR(HI)
     A  30 55                               DSPATR(RI PC)
     A  30N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      OVRDTA
     A                                 23  3'F3=End  Enter=Confirm'
     A*=========================================================================
     A          R AA3000S                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGMQ                    SFLPGMQ
     A*=========================================================================
     A          R AA3000C                   TEXT('Program messages')
     A                                      SFLCTL(AA3000S)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ZAPGMQ                    SFLPGMQ
