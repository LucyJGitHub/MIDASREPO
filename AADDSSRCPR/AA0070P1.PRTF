      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AA Funds transfer transaction audit list')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/AA0070P1 - Funds Transfer Transaction Audit List        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CAC001 *CREATE     Date 01FEB96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAC001 - Profit Centre Accounting Development:               *
      *           New printer file for AA0070.                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AA0070P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    48'FUNDS TRANSFER TRANSACTION +
     A                                      AUDIT LIST'
      *
     A            RHDTX     R             86REFFLD(ZZ025)
      *
     A  37 38                            115'CONSOLIDATED'
     A                                   132' '
     A                                      SPACEA(1)
     A                                    48'---------------------------+
     A                                      ----------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A  37                                 6'BRANCH'
     A  37        RBRCA     R             15REFFLD(BRCA)
     A  37        RBRNM     R             19REFFLD(BRNM)
     A                                   132' '
     A                                      SPACEA(1)
     A                                     6'CURRENCY'
     A            RCCY      R             15REFFLD(CCY)
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                    82'ENTRY CHARGES'
     A                                   116'FX'
     A                                   123'PROFIT'
     A                                      SPACEA(1)
     A  41                                13'ORDERING CUSTOMER'
     A  42                                12'BENEFICIARY CUSTOMER'
     A  43                                15'CREDIT PARTY'
     A  44                                15'DEBIT PARTY'
     A                                    43'DATE'
     A  41                                54'PAY AMOUNT'
     A  42                                51'SETTLEMENT AMOUNT'
     A  43                                53'CREDIT AMOUNT'
     A  44                                53'DEBIT AMOUNT'
     A                                    75'AMOUNT'
     A                                    96'TYPE'
     A                                   114'MARGIN'
     A                                   123'CENTRE'
     A                                      SPACEA(1)
     A                                     3'------------------------------+
     A                                      -----'
     A                                    41'-------'
     A                                    50'------------------'
     A                                    70'-------------------------------+
     A                                      ----------'
     A                                   113'--------'
     A                                   123'------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RCNM1     R              4REFFLD(ORC1)
     A            RPDAT     R             41REFFLD(MRDT)
     A            RAMNT     R             50REFFLD(ZZ018)
     A            RDUM1     R             70REFFLD(ZZ016)
     A                                    87'Transfer Commission'
     A            RFXMP     R            113REFFLD(ZZ008)
     A            RPRFC     R            124REFFLD(PRFC)
     A                                      SPACEA(1)
     A N43N44     RCNM2     R              4REFFLD(ORC1)
     A            RDUM2     R             70REFFLD(ZZ016)
     A N42                                87'Costs Abroad'
     A  42                                87'Advice Charge'
     A                                   132' '
     A                                      SPACEA(1)
     A N43N44     RCNM3     R              4REFFLD(ORC1)
     A            RDUM3     R             70REFFLD(ZZ016)
     A  41                                87'Swift Priority Charge'
     A  42                                87'Telephone Charge'
     A N41N42                             87'Collection Charge'
     A                                   132' '
     A                                      SPACEA(1)
     A N43N44     RCNM4     R              4REFFLD(ORC1)
     A            RDUM4     R             70REFFLD(ZZ016)
     A N43N44                             87'Cheque Charge'
     A                                      SPACEA(1)
     A  50N41N42                          87'FX/Change Commission'
     A                                      SPACEA(1)
     A N43N44     RCNM5     R              4REFFLD(ORC1)
     A            RDUM5     R             70REFFLD(ZZ016)
     A N43N44                             87'Miscellaneous Charge'
     A                                      SPACEA(1)
     A  51N41N42                          87'Total VAT Charge'
     A                                      SPACEA(1)
     A N44        RDUM6     R             70REFFLD(ZZ016)
     A N44                                87'Telex Charge'
     A                                      SPACEA(1)
     A  50N43N44  RDUM7     R             70REFFLD(ZZ016)
     A  50N43N44                          87'FX/Change Commission'
     A                                      SPACEA(1)
     A  51N43N44  RDUM8     R             70REFFLD(ZZ016)
     A  51N43N44                          87'Total VAT Charge'
     A                                      SPACEA(1)
     A                                   132' '
     A                                      SPACEA(1)
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERRF                    SPACEB(3)
     A                                    32'*** REFERENCE AA0070'
     A                                    53'DATABASE ERROR ***'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37N38                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37
     AO 38                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
