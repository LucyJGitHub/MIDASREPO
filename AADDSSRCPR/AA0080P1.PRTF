      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AA Transactions by profit centre - detail rp')
      *****************************************************************
      *                                                               *
      *  Midas - Analytical Accounting Module                         *
      *                                                               *
      *  PRTF/AA0080P1 - Transactions by Profit Centre - Detail Report*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. 124823 *CREATE     Date 08Aug97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  124823 - Provide a report for transactions by profit centre  *
      *           New printer file for AA0080.                        *
      *                                                               *
      *****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A*--------------------------------*
     A**  Report Header Record Format  *
     A*--------------------------------*
     A*
     A          R AA0080D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE AA0080P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    42'TRANSACTIONS BY PROFIT +
     A                                      CENTRE AUDIT LIST'
     A                                      SPACEA(1)
     A                                    42'-----------------------+
     A                                      -----------------'
     A                                      SPACEA(2)
     A*
     A*------------------------*
     A**  Detail Sub-Headings  *
     A*------------------------*
     A*
     A                                     4'TRANSACTION'
     A                                    48'DEAL'
     A                                    54'SUB'
     A                                      SPACEA(1)
     A*
     A                                     5'REFERENCE'
     A                                    31'MODULE'
     A                                    48'TYPE'
     A                                    54'TYPE'
     A                                    60'PROFIT CENTRE'
     A                                      SPACEA(1)
     A*
     A                                     1'------------------'
     A                                    21'-------------------------'
     A                                    48'----'
     A                                    54'----'
     A                                    60'-------------'
     A                                      SPACEA(2)
     A*
     A*-------------------------*
     A**  Detail Record Format  *
     A*-------------------------*
     A*
     A          R AA0080D1
     A*
     A            RTRNM         18A        1
     A            RMODL         25A       21
     A            RDLTP          2A       49
     A            RDLST          2A       55
     A            RPRF1          4A       61
     A            RPRF2          4A       67
     A                                      SPACEA(1)
     A*
     A*-----------------------------*
     A**  Database Error Reference  *
     A*-----------------------------*
     A*
     A          R AA0080D2                  SPACEB(3)
     A                                    32'*** REFERENCE AA0080'
     A                                    53'DATABASE ERROR ***'
     A*
     A*-------------------*
     A**  Report Trailer  *
     A*-------------------*
     A*
     A          R AA0080D3
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
