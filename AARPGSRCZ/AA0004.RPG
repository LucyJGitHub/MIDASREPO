     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AA Initialise account postings profit centre')   *
      *****************************************************************
      *                                                               *
      *  Midas - Analytical Accounting Module                         *
      *                                                               *
      *  AA0004 - Initialise Account Postings Profit Centre           *
      *                                                               *
      *  Function:  This program initialises the profit centre of     *
      *             each spot and swap trade, base equivalent,        *
      *             spot revaluation and forward revaluation a/c      *
      *             postings with a default value                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 03Sep01               *
      *                 CGL020             Date 12Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL007             Date 26Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC001 *CREATE     Date 01Feb96               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (recompiled)                                        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL020 - Midas Compliance Watch - Additional A/C Postings    *
      *           Information (Recompile)                             *
      *  CGL007 - Account Postings Enquiry  (Recompile)               *
      *  CAC001 - Profit Centre Accounting Development                *
      *                                                               *
      *****************************************************************
      *
     FAPOSTL1 UF  E           K        DISK
      **  Account Postings File
      *
     FSDCURRL0IF  E           K        DISK
      **  Currencies
      *
      /SPACE 3
     E**********          AC        999  4                                                    CGL029
     E                    AC        999 10                                                    CGL029
     E                    CPY@    1   1 80
      **  Copyright array
      *
     IAPOSTPDF    01
     IGLACPHPF    02
      *
     IAADTA     E DS
      **  AA Data Area
      *
     ISDDEAL    E DSSDDEALPD
      * Dealing Details
      *
     ISDTRAD    E DSSDTRADPD
     I              QQACCD                          QQACC1                                    CGL029
      * Trading Details
      *
     ISDPRFC    E DSSDPRFCPD
      **  Profit Centre Details
      *
     IDSFDY     E DSDSFDY
      **  Short Data Structure for Access Programs
      *
     I              '0000000000'          C         WTEN0                                     CGL029
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      /EJECT
      *****************************************************************
      *  MAIN PROCESSING
      *****************************************************************
      *
     C                     MOVEACPY@      CPY2@  80
      *
      **  Read in data area AADTA for the FX Position Profit Centre
      *
     C           *NAMVAR   DEFN           AADTA
     C                     IN   AADTA
      *
      **  Bypass if the FX Position Profit Centre is blank
      *
     C           AAFPPC    CABEQ*BLANK    ENDPGM
      *
      ** Initial processing
      *
     C                     EXSR SRINIT
      *
      **  Read APOSTL1
      *
     C                     MOVEA'00'      *IN,01
     C                     READ APOSTL1                  20*
      *
      **  Read all postings
      *
     C           *IN20     DOWEQ'0'
      *
      **  Update the profit centre on GLACPHPD/APOSTPD
      **  with the FX position profit centre
      *
     C                     Z-ADD1         ##I
     C                     MOVELACOD      ##ACOD
     C           ##ACOD    LOKUPAC,##I                   99*
      *
     C           *IN99     IFEQ '1'
     C                     MOVE AAFPPC    PRFC
     C   01                EXCPTAPOST
     C   02                EXCPTGLACPH
     C                     END
      *
     C                     MOVEA'00'      *IN,01
     C                     READ APOSTL1                  20*
     C                     ENDDO
      *
      **  Seton last record indicator and return to calling pgm
      *
     C           ENDPGM    TAG
     C                     SETON                     LR
     C                     RETRN
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Validate the FX Position Profit Centre
      *
     C                     CALL 'AOPRFCR0'
     C                     PARM '*DBERR'  PRTCD   7
     C                     PARM '*KEY'    POPTN   7
     C                     PARM AAFPPC    WPRFC   4
     C           SDPRFC    PARM SDPRFC    DSFDY
      *
      * Dealing Details
      *
     C**********           CALL 'AODEALR0'                                                    CGL029
     C                     CALL 'AODEALR1'                                                    CGL029
     C                     PARM '*DBERR ' PRTCD
     C                     PARM '*FIRST ' POPTN
     C********** SDDEAL    PARM SDDEAL    DSFDY                                               CGL029
     C           SDDEAL    PARM SDDEAL    DSSDY                                               CGL029
      *
      * Trading Details
      *
     C                     CALL 'AOTRADR0'
     C                     PARM '*DBERR ' PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDTRAD    PARM SDTRAD    DSFDY
      *
     C**********           MOVELBNSWTA    ##ACOD  4        Swap trade                         CGL029
     C                     MOVELBNSWTA    ##ACOD 10                                           CGL029
     C                     EXSR SRADAC
      *
     C                     MOVELBNFRPL    ##ACOD           Fwd reval
     C                     EXSR SRADAC
      *
     C                     MOVELBNACCD    ##ACOD           Fwd reval contra
     C                     EXSR SRADAC
      *
     C                     MOVELBLSRAC    ##ACOD           Spot reval
     C                     EXSR SRADAC
      *
     C                     MOVELBLSPTA    ##ACOD           Spot trade
     C                     EXSR SRADAC
      *
      * Loop through all currencies for base equivalent accounts
      *
     C           *LOVAL    SETLLSDCURRL0
     C                     READ SDCURRL0                 20
      *
     C           *IN20     DOWEQ'0'
      *
     C           A6DLCI    IFEQ 'Y'
     C                     MOVELA6SPAE    ##ACOD
     C                     EXSR SRADAC
     C                     END
      *
     C                     READ SDCURRL0                 20
      *
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
     C           SRADAC    BEGSR
      *
      ** Add account code
      *
     C********** ##ACOD    IFNE '0000'                                                        CGL029
     C********** ##ACOD    ANDNE'    '                                                        CGL029
     C           ##ACOD    IFNE WTEN0                                                         CGL029
     C           ##ACOD    ANDNE*BLANKS                                                       CGL029
      *
     C                     Z-ADD1         ##I     30
     C           ##ACOD    LOKUPAC,##I                   99*
      *
     C           *IN99     IFEQ '0'
     C                     Z-ADD1         ##I
     C           *BLANK    LOKUPAC,##I                   99*
     C                     MOVE ##ACOD    AC,##I
     C                     END
     C                     END
     C                     ENDSR
      *****************************************************************
     OAPOSTPDFE                APOST
     O                         PRFC
     OGLACPHPFE                GLACPH
     O                         PRFC
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
