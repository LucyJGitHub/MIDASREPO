     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AA Update balancing postings')
      *****************************************************************
      *                                                               *
      *  Midas - Analytical Accounting Module                         *
      *                                                               *
      *  AA0005B - Update Balancing Postings                          *
      *                                                               *
      *  Function:  This program initialises to a default value the   *
      *             postings for management a/cs that net off to zero *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CGL020             Date 12Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL007             Date 26Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC001 *CREATE     Date 01Feb96               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL020 - Midas Compliance Watch - Additional A/C Postings    *
      *           Information (Recompile)                             *
      *  CGL007 - Account Postings Enquiry (Recompile)                *
      *  CAC001 - Profit Centre Accounting Development                *
      *                                                               *
      *****************************************************************
      *
     FAPOSTL1 UF  E           K        DISK
      **  Account Postings File
      *
     FAAPSTC  IF  E           K        DISK
      **  Postings To Be Changed
      *
      /SPACE 3
     E                    CPY@    1   1 80
      **  Copyright array
      *
     IAPOSTPDF    01
     IGLACPHPF    02
      *
     IAADTA     E DS
      **  AA Data Area
      *
     ISDPRFC    E DSSDPRFCPD
      **  Profit Centre Details
      *
     IDSFDY     E DSDSFDY
      **  Short Data Structure for Access Programs
      *
      /EJECT
      *****************************************************************
      *  MAIN PROCESSING
      *****************************************************************
      *
     C                     MOVEACPY@      CPY2@  80
      *
      **  Read in data area AADTA for the Retail and Nostro Profit
      **  Centres
      *
     C           *NAMVAR   DEFN           AADTA
     C                     IN   AADTA
      *
      **  Validate Dump Profit Centre
      *
     C                     CALL 'AOPRFCR0'
     C                     PARM '*DBERR'  PRTCD   7
     C                     PARM '*KEY'    POPTN   7
     C                     PARM AADMPC    WPRFC   4
     C           SDPRFC    PARM SDPRFC    DSFDY
      *
      **  Define key list for AAPSTC
      *
     C           KAPSTC    KLIST
     C                     KFLD           BRCA
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           CNUM
     C                     KFLD           ACSQ
     C                     KFLD           SPOS
     C                     KFLD           OTTP
     C                     KFLD           OTST
     C                     KFLD           OTRF
     C/COPY WNCPYSRC,AA0005BC01
      *
      **  Read all postings
      *
     C                     MOVEA'00'      *IN,01
     C                     READ APOSTL1                  20*
      *
      **  Do while not end of file
      *
     C           *IN20     DOWEQ'0'
      *
      **  Update the profit centre on GLACPHPD/APOSTPD
      **  with the dump profit centre
      **  if the posting details are present on PF/AAPSTC
      *
     C           KAPSTC    CHAINAAPSTC               30
     C           *IN30     IFEQ '1'
     C                     MOVEL*BLANK    OTRF
     C                     MOVEL'*ALLUNDE'OTRF
     C           KAPSTC    CHAINAAPSTC               30
     C                     END
      *
     C           *IN30     IFEQ '0'
     C                     MOVE AADMPC    PRFC
     C   01                EXCPTAPOST
     C   02                EXCPTGLACPH
     C                     END
      *
      **  Read all postings
      *
     C                     MOVEA'00'      *IN,01
     C                     READ APOSTL1                  20*
     C                     ENDDO
      *
      **  Seton last record indicator and return to calling pgm
      *
     C                     SETON                     LR
     C                     RETRN
      *****************************************************************
     OAPOSTPDFE                APOST
     O                         PRFC
     OGLACPHPFE                GLACPH
     O                         PRFC
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
