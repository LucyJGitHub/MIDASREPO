     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AA Init PCA flds in PFs APOSTPD and GLACPHPD')
      *****************************************************************
      *                                                               *
      *  Midas - Analytical Accounting Module                         *
      *                                                               *
      *  AA0014 - Initialise PCA fields in PFs/APOSTPD & GLACPHPD     *
      *                                                               *
      *  Function:  This program initialises the Profit Centre in     *
      *             PF/APOSTPD and PF/GLACPHPD to the valuses held    *
      *             in dtaara/AADTA                                   *
      *                                                               *
      * NOTE : This utility should be amended as appropriate to suit  *
      *        the local requirements of each PCA implementation.     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CGL020             Date 12Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 164970             Date 28Jul99               *
      *                 CGL007             Date 26Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 120831 *Create     Date 07Jul97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL020 - Midas Compliance Watch - Additional A/C Postings    *
      *           Information (Recompile)                             *
      *  164970 - PTIM=235959 for ALL COB generated postings          *
      *           from SPOS GE-TT, GE-GLEU or GE-SF                   *
      *  CGL007 - Account Postings Enquiry (Recompile)                *
      *  120831 - New Implementation Object to setup PCA Fields       *
      *                                                               *
      *****************************************************************
     F*
     FAPOSTPD UF  E                    DISK
     F**  Account Posting File
     F*
     FGLACPHPDUF  E                    DISK
     F            APOSTPDF                          KRENAMEGLACPDF
     F**  Account Posting History Detail File
     F*
     E/SPACE 3
     E                    CPY@    1   1 80
     E**  Copyright array
     E*
     I/SPACE 3
     IAADTA     E DSAADTA
     I**  PCA Fields Setup
     I*
     I/EJECT
     C                     MOVEACPY@      CPY2@  80
     C*
     C**  Read in dtaara/AADTA  for new PCA fields.
     C*
     C           *NAMVAR   DEFN           AADTA
     C                     IN   AADTA
     C*
     C**  Read account posting file.
     C*
     C                     READ APOSTPD                  20
     C*
     C           *IN20     DOWEQ'0'
     C*
     C           PRFC      IFEQ *BLANKS
     C                     MOVELAAPRFC    PRFC
     C                     ENDIF
     C           SPOS      IFEQ '  GE-TT'                                 164970
     C           SPOS      OREQ 'GE-GLEU'                                 164970
     C           SPOS      OREQ '  GE-SF'                                 164970
     C           PTIM      IFEQ 0                                         164970
     C                     Z-ADD235959    PTIM                            164970
     C                     ENDIF                                          164970
     C                     ENDIF                                          164970
     C*
     C                     UPDATAPOSTPDF
     C                     READ APOSTPD                  20
     C*
     C                     ENDDO
     C*
     C*
     C**  Read account posting history file
     C*
     C                     READ GLACPHPD                 20
     C*
     C           *IN20     DOWEQ'0'
     C*
     C           PRFC      IFEQ *BLANKS
     C                     MOVELAAPRFC    PRFC
     C                     ENDIF
     C           SPOS      IFEQ '  GE-TT'                                 164970
     C           SPOS      OREQ 'GE-GLEU'                                 164970
     C           SPOS      OREQ '  GE-SF'                                 164970
     C           PTIM      IFEQ 0                                         164970
     C                     Z-ADD235959    PTIM                            164970
     C                     ENDIF                                          164970
     C                     ENDIF                                          164970
     C*
     C                     UPDATGLACPDF
     C                     READ GLACPHPD                 20
     C*
     C                     ENDDO
     C*
     C**  Seton last record indicator and return to calling pgm
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
