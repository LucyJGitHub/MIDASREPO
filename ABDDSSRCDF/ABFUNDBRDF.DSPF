      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AB Internal funding deals browse')
      *****************************************************************
      *                                                               *
      *  Midas - Internal Funding Module                              *
      *                                                               *
      *  ABFUNDBRDF - Internal Funding Deals Browse                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAP042  *CREATE    Date 08Jan01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAP042 - Conversion of Internal Funding Deal inputs into     *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
 
      ** Subfile 1 - According to Deal Number.
 
     A          R ABFUNDBRS1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            #1OPTN         1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            #1DLNO         6A  O  7  7DSPATR(HI)
     A            #1DTP1         2A  O  7 14TEXT('DEAL TYPE')
     A            #1CCY          3A  O  7 19TEXT('CURRENCY')
     A            #1AMNP        16A  O  7 23TEXT('AMOUNT')
     A            #1VDAT         7A  O  7 42TEXT('VALUE DATE')
     A            #1MDAT         7A  O  7 50TEXT('MATURITY DATE')
     A            #1STP1         2A  O  7 60TEXT('SUB-TYPE')
     A            #1BOK1         2A  O  7 65TEXT('BOOK CODE')
     A            #1BRC1         3A  O  7 69TEXT('BRANCH CODE')
     A            #1STAT         1A  O  7 76TEXT('STATUS')
 
      **  Select Screen Control for Subfile 1
 
     A          R ABFUNDBRC1                TEXT('Select Screen Control +
     A                                      by Midas Transaction ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(ABFUNDBRS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ABFUNDBRC1'
     A                                  1 26'INTERNAL FUNDING DEALS +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 40'BROWSE'
     A                                      DSPATR(HI)
     A                                  2 66'TIME:'
     A                                  2 72TIME
 
     A                                  4  2'Sel'
     A                                  4  7'Deal'
     A                                  4 14'Type'
     A                                  4 19'Ccy'
     A                                  4 34'Amount'
     A                                  4 43'Value'
     A                                  4 50'Maturity'
     A                                  4 59'Sub'
     A                                  4 64'Book'
     A                                  4 69'Brch'
     A                                  4 74'Status'
     A                                  5  7'Number'
     A                                  5 43'Date'
     A                                  5 52'Date'
     A                                  5 59'Type'
     A                                  5 64'Code'
 
      ** Subfile 2 - According to Front Office ID.
 
     A          R ABFUNDBRS2                TEXT('Select Screen Subfile +
     A                                      by Front Office ID')
     A                                      SFL
     A            #2OPTN         1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            #2FODN        20A     7  6TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            #2DLNO         6A  O  7 27TEXT('DEAL NUMBER')
     A            #2DTP1         2A  O  7 34TEXT('DEAL TYPE')
     A            #2CCY          3A  O  7 38TEXT('CURRENCY')
     A            #2AMNP        16A  O  7 42TEXT('AMOUNT')
     A            #2VDAT         7A  O  7 60TEXT('VALUE DATE')
     A            #2MDAT         7A  O  7 68TEXT('MATURITY DATE')
     A            #2STAT         1A  O  7 78TEXT('STATUS')
 
      **  Select Screen Control for Subfile 2
 
     A          R ABFUNDBRC2                TEXT('Select Screen Control +
     A                                      by Front Office ID')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(ABFUNDBRS2)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #2SFRN         4S 0H      SFLRCDNBR
     A            #2USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ABFUNDBRC2'
     A                                  1 26'INTERNAL FUNDING DEALS +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            #2RUNA    R        O  1 73REFFLD(RUNA)
     A            #2WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 40'BROWSE'
     A                                      DSPATR(HI)
     A                                  2 66'TIME:'
     A                                  2 72TIME
 
     A                                  4  2'Sel'
     A                                  4  6'Front Office Id'
     A                                  4 27'Deal'
     A                                  4 34'Typ'
     A                                  4 38'Ccy'
     A                                  4 53'Amount'
     A                                  4 61'Value'
     A                                  4 68'Maturity'
     A                                  4 77'Sts'
     A                                  5 27'Number'
     A                                  4 74'Status'
     A                                  5  7'Number'
     A                                  5 61'Date'
     A                                  5 68'Date'
 
      ** Select screen footer
 
     A          R ABFUNDBRF0                TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                      DSPATR(HI)
     A                                 24  2'F3=Exit  F11=Toggle Front Office  +
     A                                       F12=Cancel'
     A                                      DSPATR(HI)
