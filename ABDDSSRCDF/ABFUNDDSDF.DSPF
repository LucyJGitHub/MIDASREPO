/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AB Internal funding deals display')
      *****************************************************************
      *                                                               *
      *  Midas - AB Internal Funding Module                           *
      *                                                               *
      *  ABFUNDDSDF - Internal Funding Deals Display                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL019             Date 03Feb04               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CAP042  *CREATE    Date 08Jan01               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL020 - Apply Base Rate at Value Date                       *
      *  CDL019 - Allow Base Rate Changes on Fixed Deposits/Loans     *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAP042 - Conversion of Internal Funding Deal inputs into     *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
 
      *-------------------------------------------------------------------*
      **  MAIN DISPLAY SCREEN                                             *
      *-------------------------------------------------------------------*
 
     A          R ABFUNDDSF1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A
     A                                      CF03
     A  19                                  CF07
     A  19                                  CF08
     A  21                                  CF10
     A                                      CF12
     A  22                                  CF15
     A                                      OVERLAY PUTOVR
 
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'ABFUNDDSF1'
     A N23                              1 27'Internal Funding Deal Maintenance'
     A  23                              1 27'Internal Funding Deal Enquiry    '
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WID         10   O  2  2TEXT('WORKSTATION ID')
     A                                  2 66'Time:'
     A            #1TIME         6  0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
 
     A                                  4  2'Action:'
     A            #1ACTN         1   B  4 11TEXT('ACTION CODE')
     A N30N11                               DSPATR(UL HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  11                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  4 14'(I,A,D,E)'
     A  11                                  DSPATR(ND)
 
     A            #1STS          7   O  4 64TEXT('DEAL STATUS')
     A  12                                  DSPATR(RI HI)
     A N12                                  DSPATR(ND)
     A                                      OVRDTA
 
      *                                              Begin /CDL038/
     A**********************************5 28'Sub-'
     A**********************************6 20'Branch'
     A                                  5 26'Sub-'
     A                                  6 18'Branch'
      *                                              End   /CDL038/
     A N16                                  DSPATR(ND)
      *                                              Begin /CDL038/
     A**********************************6 28'Type'
     A**********************************6 35'Book'
     A**********************************6 42'Profit Centre'
     A                                  6 26'Type'
     A                                  6 31'Book'
     A                                  6 36'Class'
     A  24                                  DSPATR(ND)
     A                                  6 43'Profit Centre'
      *                                              End   /CDL038/
     A N18                                  DSPATR(ND)
 
     A                                  7  2'LOAN:'
     A            #1DLNO    R    6A  B  7 12REFFLD(DEAL)
     A N31N11                               DSPATR(UL HI)
     A  31                                  DSPATR(RI PC)
     A  11                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A************#1BRC1    R        B  7 21REFFLD(BRCA)
     A            #1BRC1    R        B  7 20REFFLD(BRCA)
      *                                              End   /CDL038/
     A N32N13                               DSPATR(UL HI)
     A  32                                  DSPATR(RI PC)
     A  13
     AON16                                  DSPATR(PR)
     A N16                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A************#1STP1    R        B  7 29REFFLD(DLST)
     A            #1STP1    R        B  7 27REFFLD(DLST)
      *                                              End   /CDL038/
     A N33N13                               DSPATR(UL HI)
     A  33                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A************#1BOK1    R        B  7 36REFFLD(BOKC)
     A            #1BOK1    R        B  7 32REFFLD(BOKC)
      *                                              End   /CDL038/
     A N34N13                               DSPATR(UL HI)
     A  34                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A            #1CLS1         4   B  7 37
     A N62N13                               DSPATR(UL HI)
     A  62                                  DSPATR(RI PC)
     A  13
     AO 24                                  DSPATR(PR)
     A  24                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
     A            #1PRF1    R        B  7 43REFFLD(PRFC)
     A N35N13                               DSPATR(UL HI)
     A  35                                  DSPATR(RI PC)
     A  13
     AON18                                  DSPATR(PR)
     A N18                                  DSPATR(ND)
     A                                      OVRDTA
     A            #1PCD1        30   O  7 48TEXT('Profit Centre 1 Description')
     A N18                                  DSPATR(ND)
     A                                      OVRDTA
 
     A                                  8  2'DEPOSIT:'
     A            #1DADN    R    6A  B  8 12REFFLD(DEAL)
     A N36N13                               DSPATR(UL HI)
     A  36                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A************#1BRC2    R        B  8 21REFFLD(BRCA)
     A            #1BRC2    R        B  8 20REFFLD(BRCA)
      *                                              End   /CDL038/
     A N37N13                               DSPATR(UL HI)
     A  37                                  DSPATR(RI PC)
     A  13
     AON16                                  DSPATR(PR)
     A N16                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A************#1STP2    R        B  8 29REFFLD(DLST)
     A            #1STP2    R        B  8 27REFFLD(DLST)
      *                                              End   /CDL038/
     A N38N13                               DSPATR(UL HI)
     A  38                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A************#1BOK2    R        B  8 36REFFLD(BOKC)
     A            #1BOK2    R        B  8 32REFFLD(BOKC)
      *                                              End   /CDL038/
     A N39N13                               DSPATR(UL HI)
     A  39                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A            #1CLS2         4   B  8 37
     A N63N13                               DSPATR(UL HI)
     A  63                                  DSPATR(RI PC)
     A  13
     AO 24                                  DSPATR(PR)
     A  24                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
     A            #1PRF2    R        B  8 43REFFLD(PRFC)
     A N40N13                               DSPATR(UL HI)
     A  40                                  DSPATR(RI PC)
     A  13
     AON18                                  DSPATR(PR)
     A N18                                  DSPATR(ND)
     A                                      OVRDTA
     A            #1PCD2        30   O  8 48TEXT('Profit Centre 2 Description')
     A N18                                  DSPATR(ND)
     A                                      OVRDTA
 
     A                                 10  6'Deal Date:'
     A            #1DDAT         6   B 10 17TEXT('Deal Date')
     A N41N13                               DSPATR(UL HI)
     A  41                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 27'Value Date:'
     A            #1VDAT         6   B 10 39TEXT('Value Date')
     A N42N13                               DSPATR(UL HI)
     A  42                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 48'Maturity Date:'
     A            #1MDAT         6   B 10 63TEXT('Maturity Date')
     A N43N13                               DSPATR(UL HI)
     A  43                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 11 48'Notice Days:'
     A            #1NTCE    R    3A  B 11 63REFFLD(NTCE)
     A N44N13                               DSPATR(UL HI)
     A  44                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 13  6'Currency:'
     A            #1CCY     R        B 13 16REFFLD(CCY)
     A N45N13                               DSPATR(UL HI)
     A  45                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 22'Amount:'
     A            #1AMNP    R   16A  B 13 30REFFLD(PCPL)
     A N46N13                               DSPATR(UL HI)
     A  46                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 48'Base Ccy Rate:'
     A            #1BCXR    R   16A  B 13 63REFFLD(BCYR)
     A N47N13                               DSPATR(UL HI)
     A  47                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A**Start of change CAS004
     A                                 14  6'Loan Yield Curve:'
     A N65                                  DSPATR(ND)
     A            #1OYC1         5   B 14 24TEXT('Loan Yield Curve')
     A N58N13                               DSPATR(UL HI)
     A  58                                  DSPATR(RI PC)
     A  13
     AON65                                  DSPATR(PR)
     A N65                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 14 32'Deposit Yield Curve:'
     A N65                                  DSPATR(ND)
     A            #1OYC2         5   B 14 53TEXT('Deposit Yield Curve')
     A N59N13                               DSPATR(UL HI)
     A  59                                  DSPATR(RI PC)
     A  13
     AON65                                  DSPATR(PR)
     A N65                                  DSPATR(ND)
     A                                      OVRDTA
     A**End of change CAS004
 
     A                                 15  2'Interest'
     A                                      DSPATR(HI UL)
 
     A                                 16  7'Base Rate:'
     A            #1BRTT    R     A  B 16 18REFFLD(BRTT)
     A N48N13                               DSPATR(UL HI)
     A  48                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16 22'Spread/Rate:'
     A            #1RTSP    R   12A  B 16 35REFFLD(RTSP)
     A N49N13                               DSPATR(UL HI)
     A  49                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16 48'Calc. Meth:'
     A            #1ICBS    R     A  B 16 63REFFLD(ICBS)
     A N50N13                               DSPATR(UL HI)
     A  50                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 17  7'Frequency:'
     A            #1IPFR    R     A  B 17 18REFFLD(IPFR)
     A N51N13                               DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 22'Next Int Date:'
     A            #1NIDT         6A  B 17 37TEXT('Next Interest Date')
     A N52N13                               DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 48'Tot. Interest:'
     A            #1TOTI    R   16A  B 17 63REFFLD(TOTI)
     A N53N13                               DSPATR(UL HI)
     A  53                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 18  7'Int Freq Day in Month:'
     A            #1INTF    R     A  B 18 30REFFLD(INTF)
     A N54N13                               DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 48'Cap. Interest:'
     A            #1CPTI    R        B 18 63REFFLD(CPTI)
     A N55N13                               DSPATR(UL HI)
     A  55                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
      * Start of change CDL019
     A                                 19  7'Fix Base Rate:'
     A N66                                  DSPATR(ND)
     A            #1FBRT         1   B 19 22TEXT('Fix Base Rate')
     A N60N13                               DSPATR(UL HI)
     A  60                                  DSPATR(RI PC)
     A  13
     AON66                                  DSPATR(PR)
     A N66                                  DSPATR(ND)
     A                                      OVRDTA
      * End of change CDL019
      * Start of change CDL020
     A                                 19 48'Actual Int Rate:'
     A N67                                  DSPATR(ND)
     A            #1AIR         12A  B 19 65TEXT('Actual Interest Rate')
     A N61N13                               DSPATR(UL HI)
     A  61                                  DSPATR(RI PC)
     A  13
     AON67                                  DSPATR(PR)
     A N67                                  DSPATR(ND)
     A                                      OVRDTA
      * End of change CDL020
     A                                 20  7'Linked Deal Ref:'
     A            #1LNKR    R    6A  B 20 24REFFLD(LNKDN)
     A N56N13                               DSPATR(UL HI)
     A  56                                  DSPATR(RI PC)
     A  13                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20 48'Orig. Branch:'
     A N17                                  DSPATR(ND)
     A            #1ORBR         3A  B 20 63TEXT('Originating Branch')
     A N57N13                               DSPATR(UL HI)
     A  57                                  DSPATR(RI PC)
     A  13
     AON17                                  DSPATR(PR)
     A N17                                  DSPATR(ND)
     A                                      OVRDTA
 
      *-------------------------------------------------------------------*
      ** MAIN SCREEN FOOTER                                               *
      *-------------------------------------------------------------------*
 
     A          R ABFUNDDSF2                TEXT('SCREEN FOOTER')
     A                                      CF03
     A  19                                  CF07
     A  19                                  CF08
     A  21                                  CF10
     A                                      CF12
     A  22                                  CF15
     A                                      OVERLAY PUTOVR
     A                                      CLRL(22)
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI)
     A                                 22 10'F12=Cancel'
     A                                      DSPATR(HI)
     A                                 22 22'F7=Prev Rec.'
     A  19                                  DSPATR(HI)
     A N19                                  DSPATR(ND)
     A                                 22 36'F8=Next Rec.'
     A  19                                  DSPATR(HI)
     A N19                                  DSPATR(ND)
     A                                 22 50'F15=Select'
     A  22                                  DSPATR(HI)
     A N22                                  DSPATR(ND)
     A                                 22 62'F10=Confirm Delete'
     A  21                                  DSPATR(HI)
     A N21                                  DSPATR(ND)
 
     A*-------------------------------------------------------------------*
     A** MESSAGE SUBFILE FORMAT                                           *
     A*-------------------------------------------------------------------*
 
     A          R ABFUNDDSS1                TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      *-------------------------------------------------------------------*
      ** MESSAGE SUBFILE CONTROL                                          *
      *-------------------------------------------------------------------*
 
     A          R ABFUNDDSC1                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(ABFUNDDSS1)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
