      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AB Invalid internal funding deals browse')
      *****************************************************************
      *                                                               *
      *  Midas - Internal Funding Module                              *
      *                                                               *
      *  ABFUNDRPDF - Internal Funding Deals Browse                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAP042  *CREATE    Date 08Jan01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAP042 - Conversion of Internal Funding Deal inputs into     *
      *           modular structure to use as APIs.                   *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R ABFUNDRPS1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            #1OPT          1A  B  7  3TEXT('Select Option')
     A                                      DSPATR(UL)
      *
     A            #1ACTN    R        O  7  7REFFLD(ACTO)
     A                                      TEXT('Action Code')
      *
     A            #1FRNT    R     A  O  7 10REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A                                      TEXT('Front Ofc ID')
      *
     A            #1DLNO         6A  O  7 31TEXT('Deal No.')
      *
     A            #1STP1         2A  O  7 38TEXT('Deal Sub-type')
      *
     A            #1CCY          3A  O  7 42TEXT('Currency')
      *
     A            #1AMNP        16A  O  7 46TEXT('Principal Amt')
      *
     A            #1VDAT         6A  O  7 64TEXT('Value Date')
      *
     A            #1MDAT         6A  O  7 72TEXT('Maturity Date')
 
     A            #1TMSP    R   26A  H      REFFLD(TIMESTMP)
      *
      ** Select Screen Control
      *
     A          R ABFUNDRPC1                TEXT('Select Screen Control')
     A                                      CF03
     A                                      SFLCTL(ABFUNDRPS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ABFUNDRPC1'
     A                                  1 26'INVALID INTERNAL FUNDING +
     A                                      DEALS'
     A                                      DSPATR(HI)
     A                                  1 66'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 37'MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  2 66'TIME '
     A                                  2 72TIME
      *
     A                                  4  2'Sel'
     A                                  4  6'Act'
     A                                  4 10'Front Office Id'
     A                                  4 31'Deal'
     A                                  4 38'Typ'
     A                                  4 42'Ccy'
     A                                  4 57'Amount'
     A                                  4 65'Value'
     A                                  4 72'Maturity'
     A                                  5  6'Cde'
     A                                  5 31'Number'
     A                                  5 65'Date'
     A                                  5 74'Date'
      *
      ** Select screen footer
      *
     A          R ABFUNDRPF0                TEXT('Select Screen Footer')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  61                             22  3'The last transaction was +
     A                                      not applied to the database.'
     A  61                                  DSPATR(HI)
     A  61                                  OVRATR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
      *
      ** End Message
      *
     A          R ABFUNDRPF1                TEXT('End Message')
     A                                      CF03
     A                                      PUTOVR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ABFUNDRPF2'
     A                                  1 26'INVALID INTERNAL FUNDING +
     A                                      DEALS'
     A                                      DSPATR(HI)
     A                                  1 66'DATE '
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 37'MAINTENANCE'
     C                                      DSPATR(HI)
     A                                  2 66'TIME '
     A                                  2 72TIME
     A N10                             11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A N10                                  DSPATR(HI)
     A  10                             11 26'USER IS NOT AUTHORISED TO +
     A                                      BROWSE       '
     A  10                                  DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      *
