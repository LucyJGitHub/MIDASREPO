     A****************************************************************
/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas AB Internal contracts browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Internal Contracts (Dealing) Module                  *
     A*                                                               *
     A*  ABINTCBDF - AB INTERNAL CONTRACTS BROWSE                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAP004             Date 08Sep98               *
     A*                                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CDL038 - Extended Deal Sub Type (Recompile)                  *
     A*  CAP004 - API's Phase 3.                                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R ABINTCS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  5  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
      *
     A            DDL1DN         6      5  4 TEXT('1st.Leg Deal Number')
     A                                       DSPATR(HI)
     A            DDL2DN         6      5 11 TEXT('2nd.Leg Deal Number')
     A                                       DSPATR(HI)
     A            DDBRCD         3      5 18 TEXT('Branch Code')
     A            DDDSTP         2      5 22 TEXT('Deal Sub-Type')
     A            DDL1DT         6      5 26 TEXT('1st.Leg Value Date')
     A            DDL2DT         6      5 35 TEXT('2nd.Leg Value Date')
     A            DDFPTS         8      5 43 TEXT('Forward Points')
     A            DDL1CB         3      5 53 TEXT('1st.Leg Buy Currency')
     A            DDL1CS         3      5 57 TEXT('1st.Leg Sell Currency')
     A            DDL1SA        16      5 61 TEXT('1st.Leg Sell Amount')
     A            DDSTAT         1      5 78 TEXT('Status')
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R ABINTCS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(ABINTCS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(18)
     A                                      SFLPAG(17)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'ABINTCB'
     A                                  1 26'            Midas            '
     A                                       DSPATR(HI)
     A                                  1 56'WID:'
     A            DDWID         10       + 1 TEXT('Workstation ID')
     A            BJMRDT         7      1 73 TEXT('MIDAS Run date')
     A                                  2 23'AB Dealer Internal Contrac-
     A                                      t Selection'
     A            DDTIME         6  0   2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*/COPY WNCPYSRC,ABINTCBD01
     A                                  4  4'1st/2nd.D/No.'
     A                                  4 18'Br.'
     A                                  4 22'ST'
     A                                  4 25'1st/2nd.Val.Dates'
     A                                  4 43'Fwd.Pts. '
     A                                  4 53'Buy'
     A                                  4 57'Sel'
     A                                  4 61'1st.Leg Sell Amt'
     A                                  4 78'D'
     A*/COPY WNCPYSRC,ABINTCBD02
     A          R ABINTCS3                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  5  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
      *
     A            DDFOID        20      5  4 TEXT('FRONT OFFICE ID')
     A                                       DSPATR(HI)
     A            DDAFOI        20      5 25 TEXT('ASSOC.FRONT OFFICE ID')
     A                                       DSPATR(HI)
     A            DDL1DN         6      5 46 TEXT('1st.Leg Deal Number')
     A            DDL2DN         6      5 53 TEXT('2nd.Leg Deal Number')
     A            DDBRCD         3      5 60 TEXT('Branch Code')
     A            DDL1DT         6      5 64 TEXT('1st.Leg Value Date')
     A            DDL2DT         6      5 71 TEXT('2nd.Leg Value Date')
     A            DDSTAT         1      5 78 TEXT('Status')
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R ABINTCS2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(ABINTCS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(18)
     A                                      SFLPAG(17)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'ABINTCB'
     A                                  1 26'            Midas            '
     A                                       DSPATR(HI)
     A                                  1 56'WID:'
     A            DDWID         10       + 1 TEXT('Workstation ID')
     A            BJMRDT         7      1 73 TEXT('MIDAS Run date')
     A                                  2 23'AB Dealer Internal Contrac-
     A                                      t Selection'
     A            DDTIME         6  0   2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*/COPY WNCPYSRC,ABINTCBD03
     A                                  4  4'Front Office ID'
     A                                  4 25'Assoc.Frnt Office ID'
     A                                  4 46'1st/2nd.D/No.'
     A                                  4 60'Br.'
     A                                  4 64'1st/2nd.Val.Dates'
     A                                  4 78'D'
     A*/COPY WNCPYSRC,ABINTCBD04
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R ABINTCF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
