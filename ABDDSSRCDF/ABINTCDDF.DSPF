/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas AB Internal contracts display')
      *****************************************************************
      *                                                               *
      *  Midas - Internal Contracts (Dealing) Module                  *
      *                                                               *
      *  ABINTCDDF - Internal Contracts Details Display               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 196429             Date 21Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP004             Date 08Sep98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                             .*
      *  196429 - Deleted deal did not show as 'DELETED' upon enquiry.*
      *  CAP004 - API's Phase 3.                                      *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R ABINTCS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R ABINTCS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF15
     A                                      SFLCTL(ABINTCS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R ABINTCD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      * Line 1 :
     A                                  1  2 'ABINTC'
     A                                  1 26'            Midas            '
     A                                       DSPATR(HI)
     A                                  1 56'WID:'
     A            DDWID         10       + 1 OVRDTA
     A            BJMRDT         7A     1 73 OVRDTA
      * Line 2 (Title) :
     A                                  2 28'Internal Contract Maintenance'
     A                                       DSPATR(HI)
     A            DDTIME         6  0   2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
      * User Action code :
     A                                  4  2'Action code.....:'
     A            DDACTN         1   B   + 1 TEXT('Action Code')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                       OVRDTA
      * Common details :
     A                                  6  2'Deal date.......:'
     A            DDDLDT         6   B   + 1 TEXT('Deal date')
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                   + 3'Type:'
     A            DDDLTP         2   B   + 1 TEXT('Deal type')
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                   + 3'Sub-type:'
     A            DDDSTP         2   B   + 1 TEXT('Deal Sub-type')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Branch:'
     A  80                                  DSPATR(ND)
     A            DDBRCD         3   B   + 1 TEXT('Branch')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70
     AO 80                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  80                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  7  2'FX Book Code:'
     A            DDFXBC         2   B   + 1
     A                                      TEXT('FX Book Code')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                   + 5'MM Book Code:'
     A            DDMMBC         2   B   + 1
     A                                      TEXT('MM Book Code')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                   + 3'Linked Ref.No:'
     A            DDLNKR         6   B   + 1
     A                                      TEXT('Linked Ref No.')
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      * 1st.Leg details :
     A                                  8  2'1st.Leg'
     A                                       DSPATR(UL HI)
     A                                   + 1'Deal No.:'
     A            DDL1DN         6   B   + 1 TEXT('1st.Leg Deal Number')
     A  18                                  DSPATR(RI)
     A N18                                  DSPATR(HI)
     A  18N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Value date:'
     A            DDL1DT         6   B   + 1 TEXT('1st.Leg Value Date')
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Rate:'
     A            DDL1RT        13   B   + 1 TEXT('1st.Leg Exchange Rate')
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                  9  4'Buy Currency..:'
     A            DDL1CB         3   B   + 1 TEXT('1st.Leg Buy Currency')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 2'Amount:'
     A            DDL1BA        16   B   + 1 TEXT('1st.Leg Buy Amount')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                 10  4'Sell Currency.:'
     A            DDL1CS         3   B   + 1 TEXT('1st.Leg Sell Currency')
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 2'Amount:'
     A            DDL1SA        16   B   + 1 TEXT('1st.Leg Sell Amount')
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                 11  4'Equivalent....:'
     A            DDL1BE        16   B   + 1 TEXT('1st.Leg Base Equivalent')
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
      * 2nd.Leg details :
     A                                 12  2'2nd.Leg'
     A                                       DSPATR(UL HI)
     A                                   + 1'Deal No.:'
     A            DDL2DN         6   B   + 1 TEXT('2nd.Leg Deal Number')
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(HI)
     A  26N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Value date:'
     A            DDL2DT         6   B   + 1 TEXT('2nd.Leg Value Date')
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Rate:'
     A            DDL2RT        13   O   + 1 TEXT('2nd.Leg Exchange Rate')
     A                                       DSPATR(HI)
     A                                       OVRDTA
     A                                 13  4'Buy Currency..:'
     A            DDL2CB         3   O   + 1 TEXT('2nd.Leg Buy Currency')
     A                                       DSPATR(HI) OVRDTA
     A                                   + 2'Amount:'
     A            DDL2BA        16   O   + 1 TEXT('2nd.Leg Buy Amount')
     A                                       DSPATR(HI) OVRDTA
     A                                 14  4'Sell Currency.:'
     A            DDL2CS         3   O   + 1 TEXT('2nd.Leg Sell Currency')
     A                                       DSPATR(HI) OVRDTA
     A                                   + 2'Amount:'
     A            DDL2SA        16   O   + 1 TEXT('2nd.Leg Sell Amount')
     A                                       DSPATR(HI) OVRDTA
     A                                 15  4'Equivalent....:'
     A            DDL2BE        16   B   + 1 TEXT('2nd.Leg Base Equivalent')
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Forward points (signed):'
     A            DDFPTS         9   B   + 1 TEXT('Forward Points')
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A N82N83                          16  2'FX Profit centre:'
     A N82                             16  2'Profit centre ..:'
     A                                       OVRATR
     A            DDPRFC    R        B   + 1REFFLD(PRFC)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  70
     AO 82                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  82                                  DSPATR(ND)
     A                                       OVRDTA
     A                                   + 3'Orig.Branch:'
     A  81                                  DSPATR(ND)
     A            DDORBR    R        B    +1REFFLD(ORBR)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  70
     AO 81                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  81                                  DSPATR(ND)
     A                                       OVRDTA
      * Loan details :
     A                                 17  2'Loan Interest'
     A                                       DSPATR(UL HI)
     A                                 17 24'Round-down ind:'
     A            DDLRDF         1   B   + 1 TEXT('Loan round-down ind')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                 18  4'Calc.Method...:'
     A            DDLOCM         1   B   + 1 TEXT('Loan Int.Calc.Method')
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Rate (signed):'
     A            DDLOIR        13   B   + 1 TEXT('Loan Interest Rate')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Amount:'
     A            DDLOIA        17   O   + 1 TEXT('Loan Interest Amount')
     A                                       DSPATR(HI) OVRDTA
      * Deposit details :
     A                                 19  2'Deposit Interest'
     A                                       DSPATR(UL HI)
     A                                 19 24'Round-down ind:'
     A            DDDRDF         1   B   + 1 TEXT('Deposit round-down ind')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                 20  4'Calc.Method...:'
     A            DDDPCM         1   B   + 1 TEXT('Dep. Int.Calc.Method')
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Rate (signed):'
     A            DDDPIR        13   B   + 1 TEXT('Dep. Interest Rate')
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                       OVRDTA
     A                                   + 3'Amount:'
     A            DDDPIA        17   O   + 1 TEXT('Dep. Interest Amount')
     A                                       DSPATR(HI) OVRDTA
     A                                 21  2'MM Profit Centre:'
     A  83                                  DSPATR(ND)
     A            DDMMPC    R        B   + 1REFFLD(PRFC)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A  70
     AO 83                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  83                                  DSPATR(ND)
     A                                       OVRDTA
      *                                              Begin /CDL038/
     A                                 21 25'Class:'
     A  90                                  DSPATR(ND)
     A            DDCLAS         4A  B 21 32
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  70
     AO 90                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  90                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
     A*
     A* -------------------------------------------------------------------*
     A* * MAIN SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R ABINTCF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A            DDFOTX        46A    23  2OVRDTA
     A                                      DSPATR(HI)
     A**Start of change 196429
     A**********  DDSTS         24     23 45OVRDTA
     A            DDSTS         24     23 50OVRDTA
     A**End of change 196429
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 40'F15=Select'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
