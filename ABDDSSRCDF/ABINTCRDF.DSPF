/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas AB Invalid internal contracts browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Internal Contracts (Dealing) Module                  *
     A*                                                               *
     A*  ABINTCRDF - INVALID INTERNAL CONTRACTS BROWSE                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 166907             Date 27Dec99               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP014             Date 29Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP004             Date 01Sep98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  166907 - Show 'Deal No. 999999 successfully inserted'        *
     A*           message on screen.                                  *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP004 - API's phase 3.                                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R ABIINTCS1                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN    R           8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFOTRANIDR     A     8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDFOASOCIDR     A     8 30REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDDLDT         6      8 51TEXT('Deal Date')
     A            DDL1DT         6      8 58TEXT('1st.Leg Value Date')
     A            DDL2DT         6      8 65TEXT('2nd.Leg Value Date')
     A            DDL1CB         3      8 72TEXT('1st.Leg Buy Currency')
     A            DDL1CS         3      8 76TEXT('1st.Leg Sell Currency')
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDL1DN    R     A  H      REFFLD(DEAL)
     A            DDL2DN    R     A  H      REFFLD(DEAL)
     A            DDL1BA    R   +1A  H      REFFLD(AMNA)
     A            DDL1SA    R   +1A  H      REFFLD(AMNA)
     A            DDL1RT        13A  H      TEXT('1st Leg Exchange Rate')
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R ABIINTCS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(ABIINTCS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'ABINTCRB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'AB DEALER - INVALID DEAL MAINTENAN+
     A                                      CE'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 30'Assoc.Frnt Office ID'
     A                                  6 51' Deal '
     A                                  6 58'1stLeg'
     A                                  6 65'2ndLeg'
     A                                  6 72'Buy'
     A                                  6 76'Sell'
     A*
      * Start CAP014
     A**********************************7  1'A,D'
      * End CAP014
     A                                  7 51' Date '
     A                                  7 58' Date '
     A                                  7 65' Date '
     A                                  7 72'Ccy'
     A                                  7 76'Ccy'
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R ABIINTCF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of 166907
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R ABIINTCEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start of 166907
     A                                      PUTOVR
      * End of 166907
     A                                  1  2'ABINTCRB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'AB DEALER - INVALID DEAL MAINTENAN+
     A                                      CE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      * Start of 166907
     A            DDFOTX        46A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
