     H        1   D
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AC Calculate component end time')
     F*****************************************************************
     F*                                                               *
     F*  Midas - System Control Module                            *
     F*                                                               *
     F*  AC0001 - Calculate component end time.                       *
     F*                                                               *
     F*  Function: This program will use the details from the         *
     F*    (COB)   components file to calculate the end time          *
     F*            of the component.                                  *
     F*                                                               *
     F*  Called By: CBC0050 - Shell program calling COB applications. *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  PREV AMEND NO. S01491 *CREATE     DATE 01OCT94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  S01491 - Autocall.                                           *
     F*                                                               *
     F*****************************************************************
     F*****************************************************************
     F*
     FACFINTL0UF  E           K        DISK         KINFSR *PSSR A
     F*
      /EJECT
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   89  - Record not found                                      *
     F*                                                               *
     F* U7+U8 - Database Error                                        *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
     E*** Array for Object Copyright Statement.
     E*
      /EJECT
     I*
     IPSDS       SDS
     I***  Program Status Data Structure.
     I                                      244 253 JOB
     I                                      254 263 USER
     I                                      276 2810DATE
     I                                      282 2870TIME
     I*
     ICTIME       DS
     I***  Current time data structure.
     I                                        1   20CHOR
     I                                        3   40CMIN
     I                                        5   60CSEC
     I*
     IFTIME       DS
     I***  Finish time data structure.
     I                                        1   20FHOR
     I                                        3   40FMIN
     I                                        5   60FSEC
     I*
     IETIME       DS
     I***  End time data structure.
     I                                        1   20EHOR
     I                                        3   40EMIN
     I                                        5   60ESEC
      /EJECT
     C/TITLE Main Processing
     C*
     C***  Copyright Statement.
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C           *ENTRY    PLIST
     C                     PARM           WCOTT  10
     C                     PARM           WCSEQ   5
     C                     PARM           WTIME   60
     C                     PARM           WMODE   1
     C*
     C           ACFKEY    KLIST
     C                     KFLD           WCOTT
     C                     KFLD           WCSEQ
     C*
     C           WMODE     IFEQ 'A'
     C*
     C***  Calculate the expected end time of the component.
     C*
     C                     MOVE TIME      CTIME
     C                     MOVE WTIME     FTIME
     C*
     C           CSEC      ADD  FSEC      WSEC    30
     C           WSEC      DIV  60        WMIN    30
     C                     MVR            ESEC
     C*
     C                     ADD  CMIN      WMIN
     C                     ADD  FMIN      WMIN
     C           WMIN      DIV  60        WHOR    30
     C                     MVR            EMIN
     C*
     C                     ADD  CHOR      WHOR
     C                     ADD  FHOR      WHOR
     C           WHOR      DIV  24        WDAY    30
     C                     MVR            EHOR
     C*
     C***  Convert the current date to Midas day number.
     C*
     C                     CALL 'ZA0270'
     C                     PARM DATE      WDATE   60
     C                     PARM 'D'       WFRMT   1
     C                     PARM           WERRR   10
     C                     PARM           WDYNO   50
     C*
     C                     ADD  WDAY      WDYNO
     C*
     C***  Write details to PF/ACFINTL0.
     C*
     C                     MOVE ETIME     AOCETM
     C                     Z-ADDWDYNO     AOCPED
     C                     MOVELWCOTT     AOCOTT
     C                     MOVELWCSEQ     AOCSEQ
     C                     WRITE@FINTL0
     C*
     C                     ENDIF
     C*
     C           WMODE     IFEQ 'D'
     C*
     C***  Delete the appropriate record from ACFINTL0.
     C*
     C           ACFKEY    CHAIN@FINTL0              89
     C*
     C           *IN89     IFEQ '1'
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDIF
     C*
     C                     DELET@FINTL0
     C*
     C                     ENDIF
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
      /EJECT
     C/TITLE SR/*PSSR
     C*****************************************************************
     C*                                                               *
     C*  *PSSR  - Subroutine to handle Database Errors.               *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                            *** *PSSR ***
     C*
     C***  Dump and return to the calling program
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
** CPY@ - OBJECT COPYRIGHT
(c) Finastra International Limited 2001
