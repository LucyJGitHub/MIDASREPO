     H        1   D
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AC Checking components processed in COB')
     F*****************************************************************
     F*                                                               *
     F*  Midas - System Control Module                                *
     F*                                                               *
     F*  AC0004 - Checking of components being processed in COB.      *
     F*                                                               *
     F*  Function: This program will check the times taken for        *
     F*    (COB)   components to finish, by looking at LF/ACFINTL1.   *
     F*                                                               *
     F*  Called By: ACC0001 - Checking of components in COB           *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 099770             Date 20FEB96               *
     F*                 S01491 *CREATE     Date 01OCT94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  099770 - Need to set on LR every time control is returned    *
     F*           to reset the pointer to beginning of file.          *
     F*  S01491 - Autocall.                                           *
     F*                                                               *
     F*****************************************************************
     F*****************************************************************
     F*
     FACFINTL1UF  E           K        DISK         KINFSR *PSSR
     F*
      /EJECT
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   88  - End of file                                           *
     F*                                                               *
     F*   U6  - Error has occured : A component is taking too long    *
     F*                                                               *
     F* U7+U8 - Database Error                                        *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
     E*** Array for Object Copyright Statement.
     E*
      /EJECT
     I*
     IPSDS       SDS
     I***  Program Status Data Structure.
     I                                      244 253 JOB
     I                                      254 263 USER
     I                                      276 2810DATE
     I                                      282 2870TIME
     I*
      /EJECT
     C/TITLE Main Processing
     C*
     C***  Copyright Statement.
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C           *ENTRY    PLIST
     C                     PARM           WCOMP  10
     C                     PARM           WSEQ    5
     C*
     C***Program*is*repeatedly*called*and*terminates*with*LR*off,*so      099770
     C***file*pointer*could*be*anywhere.**Therefore*position*the*file     099770
     C***pointer*at*the*top*of*the*file*on*each*call.                     099770
     C           *LOVAL    SETLLACFINTL1
     C*
     C***  Check the end time for components.
     C*
     C                     READ @FINTL1                  88
     C*
     C                     CALL 'ZA0270'
     C                     PARM DATE      WDATE   60
     C                     PARM 'D'       WFRMT   1
     C                     PARM           WERRR   10
     C                     PARM           WDYNO   50
     C*
     C                     MOVE TIME      WTIME   60
     C*
     C***  Do while not EOF and
     C***  the current date is less than the end date
     C***  or not EOF and
     C***  the current date is equal to the end date and
     C***  the current time is less than the end time.
     C*
     C           *IN88     DOWNE'1'
     C           WDYNO     ANDLTAOCPED
     C           *IN88     ORNE '1'
     C           WDYNO     ANDEQAOCPED
     C           WTIME     ANDLTAOCETM
     C                     READ @FINTL1                  88
     C                     ENDDO
     C*
     C           *IN88     IFNE '1'
     C*
     C***  Component is taking too long - error has occured.
     C*
     C                     MOVELAOCOTT    WCOMP
     C                     MOVELAOCSEQ    WSEQ
     C*
     C                     DELET@FINTL1
     C*
     C                     MOVE '1'       *INU6
     C                     ENDIF
     C*
     C*
     C***End*without*setting*on*LR*to*speed*up*processing.                099770
     C*                                                                   099770
     C                     MOVE '1'       *INLR                           099770
     C*                                                                   099770
     C                     RETRN
      /EJECT
     C/TITLE SR/*PSSR
     C*****************************************************************
     C*                                                               *
     C*  *PSSR  - Subroutine to handle Database Errors.               *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                            *** *PSSR ***
     C*
     C***  Dump and return to the calling program
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
** CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
