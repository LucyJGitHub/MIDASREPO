     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AC Utility to calculate component times')
     F*****************************************************************
     F*                                                               *
     F*  Midas - System Control Module                            *
     F*                                                               *
     F*  AC0005 - Calculate component times.                          *
     F*                                                               *
     F*  Function: This program will calculate at implementation, be- *
     F*            fore Autocall is switched ON, the time it would    *
     F*            take each component to complete, during a normal   *
     F*            COB.                                               *
     F*                                                               *
     F*  Called By: None                                              *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 099980             Date 23FEB96               *
     F*                 S01491 *CREATE     Date 01OCT94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  099980 - Increase component times                            *
     F*  S01491 - Autocall.                                           *
     F*                                                               *
     F*****************************************************************
     F*****************************************************************
     F*
     FCBCOMSL0IF  E           K        DISK         KINFSR *PSSR
     F            @DHREGG                           KRENAME@CBREGG
     F*
     FCBCMPNL0UF  E           K        DISK         KINFSR *PSSR
     F*
      /EJECT
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   88  - Record not found on LF/CBCMPNL0                       *
     F*   89  - End of file on LB/CBCOMSL0                            *
     F*                                                               *
     F* U7+U8 - Database Error                                        *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
     E*** Array for Object Copyright Statement.
     E*
      /EJECT
     I*
     I@CBREGG
     I***  Rename record field names of LF/CBCOMSL0
     I              DHCOTT                          CBCOTT
     I              DHCSEQ                          CBCSEQ
     I              DHCSTS                          CBCSTS
     I              DHCSHP                          CBCSHP
     I              DHCEPY                          CBCEPY
     I              DHCTSL                          CBCTSL
     I              DHCFOB                          CBCFOB
     I              DHCTXT                          CBCTXT
     I              DHCPRM                          CBCPRM
     I              DHCRQD                          CBCRQD
     I              DHCMOD                          CBCMOD
     I              DHCEMI                          CBCEMI
     I              DHCRES                          CBCRES
     I              DHCODP                          CBCODP
     I              DHCMEF                          CBCMEF
     I              DHCFRQ                          CBCFRQ
     I              DHCSDE                          CBCSDE
     I              DHCSTI                          CBCSTI
     I              DHCEDE                          CBCEDE
     I              DHCETI                          CBCETI
     I              DHCHTB                          CBCHTB
     I              DHCHTA                          CBCHTA
     I              DHCFAL                          CBCFAL
     I              DHEODT                          CBEODT
     I              DHEOMT                          CBEOMT
     I              DHBOMT                          CBBOMT
     I              DHEOYT                          CBEOYT
     I              DHEODC                          CBEODC
     I              DHEOMC                          CBEOMC
     I              DHBOMC                          CBBOMC
     I              DHEOYC                          CBEOYC
     I*
     ILDA       E DSLDA
     I***  Local data area for database error details.
     I*
     IXTIME       DS
     I***  Time data structure.
     I                                        1   20XHOR
     I                                        3   40XMIN
     I                                        5   60XSEC
     I                                        1   60TIMN
     I*
      /EJECT
     C/TITLE Main Processing
     C*
     C***  Copyright Statement.
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C           WWKEY     KLIST
     C                     KFLD           WWCOTT 10
     C                     KFLD           WWCSEQ  5
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVEL*BLANKS   DBKEY
     C                     MOVEL'AC0005'  DBPGM
     C                     Z-ADD*ZEROS    DBASE
     C                     OUT  LDA
     C*
     C                     READ @CBREGG                  89
     C*
     C***  Do while not end of LF/CBCOMSL0
     C*
     C           *IN89     DOWEQ'0'
     C*
     C***  Check if record exists on LF/CBCMPNL0
     C*
     C                     MOVELCBCOTT    WWCOTT
     C                     MOVELCBCSEQ    WWCSEQ
     C*
     C           WWKEY     CHAIN@DHREGG              88
     C*
     C           *IN88     IFEQ '1'
     C*
     C***  Record not found - Database Error.
     C*
     C           *LOCK     IN   LDA
     C                     MOVELWWCOTT    WKEY   15
     C                     MOVE WWCSEQ    WKEY
     C                     MOVEL'CBCMPNL0'DBFILE           *************
     C                     MOVELWKEY      DBKEY            * DBASE 001 *
     C                     Z-ADD1         DBASE            *************
     C                     OUT  LDA
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
     C*
     C***  Convert start time (HHMMSS) to units of seconds
     C*
     C                     MOVE CBCSTI    XTIME
     C           XHOR      MULT 60        SMIN    40
     C           XMIN      ADD  SMIN      SMIN
     C           SMIN      MULT 60        SSEC    50
     C           XSEC      ADD  SSEC      SSEC
     C*
     C***  Convert end time (HHMMSS) to units of seconds
     C*
     C                     MOVE CBCETI    XTIME
     C           XHOR      MULT 60        EMIN    40
     C           XMIN      ADD  EMIN      EMIN
     C           EMIN      MULT 60        ESEC    50
     C           XSEC      ADD  ESEC      ESEC
     C*
     C***  Calculate the time taken for the component to finish
     C***  (end of day time) in units of seconds
     C*
     C***  End time is greater than or equal start time
     C*
     C           ESEC      IFGE SSEC
     C           ESEC      SUB  SSEC      WWEODT  50
     C*
     C***  End time is less than start time
     C*
     C                     ELSE
     C           86400     SUB  SSEC      WWRK1   50
     C           WWRK1     ADD  ESEC      WWEODT
     C                     END
     C*
     C***  If component takes less than 2 minutes, set minimum time
     C***          to 2 minutes
     C*
     C           WWEODT    MULT 2         WWEODT                          099980
     C           WWEODT    IFLT 120
     C           WWEODT    ADD  120       WWEODT
     C                     END
     C*
     C***  Compute for end of month time, beginning of month time,
     C***  and end of year time
     C*
     C           WWEODT    MULT 1.5       WWEOMT  60
     C           WWEODT    MULT 1.5       WWBOMT  60
     C           WWEODT    MULT 2         WWEOYT  60
     C*                                                                   099980
     C***  Add 10 minutes onto component                                  099980
     C*                                                                   099980
     C           WWEODT    ADD  600       WWEODT                          099980
     C           WWEOMT    ADD  600       WWEOMT                          099980
     C           WWEOYT    ADD  600       WWEOYT                          099980
     C           WWBOMT    ADD  600       WWBOMT                          099980
     C*
     C***  Convert end of day time back to HHMMSS format
     C*
     C           WWEODT    DIV  3600      XHOR
     C                     MVR            WSEC    40
     C           WSEC      DIV  60        XMIN
     C                     MVR            XSEC
     C                     Z-ADDTIMN      DHEODT
     C*
     C***  Convert end of month time back to HHMMSS format
     C*
     C           WWEOMT    DIV  3600      XHOR
     C                     MVR            WSEC
     C           WSEC      DIV  60        XMIN
     C                     MVR            XSEC
     C                     Z-ADDTIMN      DHEOMT
     C*
     C***  Convert beginning of month time back to HHMMSS format
     C*
     C           WWBOMT    DIV  3600      XHOR
     C                     MVR            WSEC
     C           WSEC      DIV  60        XMIN
     C                     MVR            XSEC
     C                     Z-ADDTIMN      DHBOMT
     C*
     C***  Convert end of year time back to HHMMSS format
     C*
     C           WWEOYT    DIV  3600      XHOR
     C                     MVR            WSEC
     C           WSEC      DIV  60        XMIN
     C                     MVR            XSEC
     C                     Z-ADDTIMN      DHEOYT
     C*
     C***  Update record on LF/CBCMPNL0 with the new details
     C*
     C                     UPDAT@DHREGG
     C*
     C***  Read next record on LF/CBCOMSL0
     C*
     C                     READ @CBREGG                  89
     C*
     C                     END
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
      /EJECT
     C/TITLE SR/*PSSR
     C*****************************************************************
     C*                                                               *
     C*  *PSSR  - Subroutine to handle Database Errors.               *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                            *** *PSSR ***
     C*
     C***  Dump and return to the calling program
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C*****************************************************************
      /EJECT
** CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
