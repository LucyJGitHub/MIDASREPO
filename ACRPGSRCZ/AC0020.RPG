     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AC Retreive Default answer for message')
     F*****************************************************************
     F*                                                               *
     F*  Midas - System Control Module                            *
     F*                                                               *
     F*  AC0020 - Retrieve default reply for system messages          *
     F*                                                               *
     F*  Function: This program will verify automatic answer file     *
     F*    (COB)   for system messages.                               *
     F*                                                               *
     F*  Called By: ACC0010 - QSYSOPR message queue monitoring program*
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. S01491 *CREATE     DATE 01OCT94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  S01491 - Autocall.                                           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*
     FDFTRPYL0IF  E           K        DISK
     F*
      /EJECT
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   99  - End of file on LF/DFTRPYL0                            *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
     E*** Array for Object Copyright Statement.
     E*
      /EJECT
     C/TITLE Main Processing
     C*
     C** Copyright Statement.
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C           *ENTRY    PLIST
     C                     PARM           MSGID   7
     C                     PARM           SNDJOB 10
     C                     PARM           SNDUSR 10
     C                     PARM           SNDNBR  6
     C                     PARM           SNDPGM 10
     C                     PARM           SNDDAT  6
     C                     PARM           SNDTIM  6
     C                     PARM           DFTRPY  1
     C                     PARM           ALERT   1
     C*
     C           KEY       KLIST
     C                     KFLD           MSGID
     C                     KFLD           PGM
     C*
     C                     MOVE *LOVAL    PGM    10
     C*
     C           SNDPGM    IFEQ *BLANKS
     C                     MOVEL'*ALL'    SNDPGM
     C                     END
     C*
     C** Read the Default reply file with pgm parameter
     C*
     C           KEY       SETLLDFTRPYL0
     C                     READ DFTRPYL0                 99
     C*
     C** If End of file detected
     C** or no record found for this error code
     C*
     C           *IN99     IFEQ '1'
     C           MSGID     ORNE DRMSGD
     C                     MOVE 'Y'       ALERT
     C                     ELSE
     C*
     C** Check if a group reply exist. ] If *all stop reading
     C**                               ] If *others check if specific answer
     C*
     C           DRPGM     IFEQ '*ALL'
     C           DRPGM     OREQ '*OTHERS'
     C                     MOVE DRDFRY    DFTRPY
     C                     MOVE DRALRT    ALERT
     C                     END
     C*
     C           DRPGM     IFNE '*ALL'
     C*
     C** Continue to read and check if pgm name exist with specific answer
     C*
     C                     READ DFTRPYL0                 99
     C*
     C           *IN99     DOWEQ'0'
     C           MSGID     ANDEQDRMSGD
     C*
     C           SNDPGM    IFEQ DRPGM
     C                     MOVE DRDFRY    DFTRPY
     C                     MOVE DRALRT    ALERT
     C                     MOVE *BLANKS   DRPGM
     C                     END
     C*
     C                     READ DFTRPYL0                 99
     C                     END
     C                     END
     C                     END
     C*
     C                     MOVE '1'       *INLR
     C********************************************************************
      /EJECT
** CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
