      *****************************************************************
/*S*D****DSPFDFRWN*****************************************************                    AR1071025
/*STD *  DSPFBASE                                                     *                    AR1071025
/*EXI *  TEXT('Midas AK Forecast transactions a/c keys generation')   *
      *****************************************************************
      *                                                               *
      *  Midas - Account Key Generator                                *
      *                                                               *
      *  AK000070DF - Midas AK Forecast Transactions A/C keys         *
      *               Generation                                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. AR1071025          Date 16Jan13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  AR1071025 - Erroneous field will only highlight after the    *
      *              second enter (Recompile)                         *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03(03 'Exit')
     A          R ACKEYSF
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      PUTOVR
     A                                      OVERLAY
     A            #1USER        10A  O  1  2
     A                                  1 13'AK000070'
     A            #1RUNA         7A  O  1 72
     A            #1WSID        10A  O  2  2
     A                                  2 18'Forecast Transactions Account +
     A                                      Key Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Type..............:'
     A                                  4 24'FH'
     A                                      DSPATR(HI)
     A                                  4 31'Subtype...........:'
     A            #1STYP         2A  B  4 51
     A                                      OVRDTA
     A                                      CHECK(FE)
     A N26                                  DSPATR(UL HI)
     A  26                                  DSPATR(RI PC)
     A                                 23  1'F3=Exit'
      *
     A          R ACKEYFR
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A            #1USER        10A  O  1  2
     A                                  1 13'AK000070'
     A            #1RUNA         7A  O  1 72
     A            #1WSID        10A  O  2  2
     A                                  2 20'Forecast Transactions Account +
     A                                      Key Generation'
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Type..............:'
     A                                  4 24'FH'
     A                                      DSPATR(HI)
     A                                  4 31'Subtype...........:'
     A            #1STYP         2A  O  4 51DSPATR(HI)
     A                                  8 11'Principal Account.........-
     A                                      ................:'
     A**Start of change CGL029
     A**********  #1PRCPAC       4   B  8 55                                                  CGL029
     A            #1PRCPAC      10   B  8 55
     A**End of change CGL029
     A                                      OVRDTA
     A N28                                  DSPATR(UL HI)
     A  28                                  DSPATR(RI PC)
     A                                  9 25'Contra Account............-
     A                                      ..:'
     A**Start of change CGL029
     A**********  #1PCTRAC       4   B  9 55                                                  CGL029
     A            #1PCTRAC      10   B  9 55
     A**End of change CGL029
     A                                      OVRDTA
     A N29                                  DSPATR(UL HI)
     A  29                                  DSPATR(RI PC)
     A                                 23  1'F3=Exit   F12=Previous'
      *
     A          R #MSGRCD                   TEXT('PROGRAM MESSAGES')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('PROGRAM MESSAGES')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  30
     AON30                                  SFLEND
     A            SPGMQ                     SFLPGMQ
