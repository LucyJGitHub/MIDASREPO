      *****************************************************************
/*S*D****DSPFDFRWN*****************************************************                    AR1071025
/*STD *  DSPFBASE                                                     *                    AR1071025
/*EXI *  TEXT('Midas AK Internal contract account key generation')    *
     A********************************************************************
     A*                                                                  *
     A*  Midas - Account Key Generator
     A*                                                                  *
     A*    AK0050DF - Internal Contracts Account Keys Generation         *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      *  Last Amend No. AR1071025          Date 16Jan13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 XXXXXX             Date ddmmmyy                *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  AR1071025 - Erroneous field will only highlight after the    *
      *              second enter (Recompile)                         *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS004 - Hedge Accounting Phase A                            *
     A*                                                                  *
     A*                                                                  *
     A********************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A********************************************************************
     A*
     A                                      PRINT(MPRINT)
     A                                      CF03(03 'Exit')
     A          R ACKEYSF
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AK0050'
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 18'Internal Contracts Account Key -
     A                                      Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Type..............:'
     A            TYPE           2A  B  4 24
     A                                      OVRDTA
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI PC)
     A                                  4 31'Subtype...........:'
     A            STYP           2A  B  4 51
     A                                      OVRDTA
     A**Start of change CDL038
     A**************************************CHECK(FE)                                         CDL038
     A**End of change CDL038
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A**Start of change CDL038
     A  27                              4 60'Class.........:'                                 CDL038
     A            CLAS           4A  B  4 76                                                  CDL038
     A                                      OVRDTA                                            CDL038
     A N98 27                               DSPATR(UL HI)                                     CDL038
     A  98 27                               DSPATR(RI PC)                                     CDL038
     A N27                                  DSPATR(PR ND)                                     CDL038
     A**End of change CDL038
     A                                 23  1'F3=Exit'
     A*
     A          R ACKEYIC
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AK0050'
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 15'Internal Contracts Account Key -
     A                                       Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Type..............:'
     A            TYPE           2A  O  4 24DSPATR(HI)
     A                                  4 31'Subtype...........:'
     A            STYP           2A  O  4 51DSPATR(HI)
     A**Start of change CDL038
     A  27                              4 60'Class.........:'                                 CDL038
     A            CLAS           4A  O  4 76DSPATR(HI)                                        CDL038
     A**End of change CDL038
     A                                  6  8'Arbitrage Internal Contracts'
     A                                  8 15'Internal Forward Purchases-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F1             4   B  8 66                                                  CGL029
     A            F1            10   B  8 66
     A**End of change CGL029
     A                                      OVRDTA
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A                                  9 15'Internal Forward Sales....-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F2             4   B  9 66                                                  CGL029
     A            F2            10   B  9 66
     A**End of change CGL029
     A                                      OVRDTA
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI PC)
     A                                 10 15'Internal Forward Position.-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F3             4   B 10 66                                                  CGL029
     A            F3            10   B 10 66
     A**End of change CGL029
     A                                      OVRDTA
     A N54                                  DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A                                 11 15'Internal Nostro...........-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F4             4   B 11 66                                                  CGL029
     A            F4            10   B 11 66
     A**End of change CGL029
     A                                      OVRDTA
     A N55                                  DSPATR(UL HI)
     A  55                                  DSPATR(RI PC)
     A                                 12 15'Internal Loan.............-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F5             4   B 12 66                                                  CGL029
     A            F5            10   B 12 66
     A**End of change CGL029
     A                                      OVRDTA
     A N56                                  DSPATR(UL HI)
     A  56                                  DSPATR(RI PC)
     A                                 13 15'Internal Deposit.........-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F6             4   B 13 66                                                  CGL029
     A            F6            10   B 13 66
     A**End of change CGL029
     A                                      OVRDTA
     A N57                                  DSPATR(UL HI)
     A  57                                  DSPATR(RI PC)
     A                                 14 15'Interest Received Accrual-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F7             4   B 14 66                                                  CGL029
     A            F7            10   B 14 66
     A**End of change CGL029
     A                                      OVRDTA
     A N58                                  DSPATR(UL HI)
     A  58                                  DSPATR(RI PC)
     A                                 15 15'Interest Received Income..-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F8             4   B 15 66                                                  CGL029
     A            F8            10   B 15 66
     A**End of change CGL029
     A                                      OVRDTA
     A N59                                  DSPATR(UL HI)
     A  59                                  DSPATR(RI PC)
     A                                 16 15'Interest Paid Accrual....-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F9             4   B 16 66                                                  CGL029
     A            F9            10   B 16 66
     A**End of change CGL029
     A                                      OVRDTA
     A N60                                  DSPATR(UL HI)
     A  60                                  DSPATR(RI PC)
     A                                 17 15'Interest Paid Expense.....-
     A                                      .......................:'
     A**Start of change CGL029
     A**********  F10            4   B 17 66OVRDTA                                            CGL029
     A            F10           10   B 17 66OVRDTA
     A**End of change CGL029
     A N61                                  DSPATR(UL HI)
     A  61                                  DSPATR(RI PC)
     A                                 23  1'F3=Exit   F12=Previous'
      **Start of change CAS004
      **Hedge Accounting Account Keys
     A          R AKEYHAC
      *
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CF12(12 'Previous')
     A            USRID         10A  O  1  2
     A                                  1 13'AK0050'
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 20'Internal Contracts Account Key -
     A                                      Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  4'Type..............:'
     A            TYPE           2A  O  4 24DSPATR(HI)
     A                                  4 31'Sub Type..........:'
     A            STYP           2A  O  4 51DSPATR(HI)
     A**Start of change CDL038
     A  27                              4 60'Class.........:'                                 CDL038
     A            CLAS           4A  O  4 76DSPATR(HI)                                        CDL038
     A**End of change CDL038
      *
     A                                  6 15'Effective Fair Value Hedge Profit -
     A                                      Contra Account:'
     A**Start of change CGL029
     A**********  SEFVHP         4A  B  6 65OVRDTA                                            CGL029
     A            SEFVHP        10A  B  6 65OVRDTA
     A**End of change CGL029
     A N30                                  DSPATR(UL HI)
     A  30                                  DSPATR(RI PC)
      *
     A                                  7 23'Posting Reference 1 or 3..........-
     A                                      ......:'
     A            SFVPPR         1A  B  7 65OVRDTA
     A N31                                  DSPATR(UL HI)
     A  31                                  DSPATR(RI PC)
      *
     A                                  8 23'Other Income Account..............-
     A                                      ......:'
     A**Start of change CGL029
     A**********  SOTINC         4A  B  8 65OVRDTA                                            CGL029
     A            SOTINC        10A  B  8 65OVRDTA
     A**End of change CGL029
     A N32                                  DSPATR(UL HI)
     A  32                                  DSPATR(RI PC)
      *
     A                                 10 15'Effective Fair Value Hedge Loss -
     A                                      Contra Account..:'
     A**Start of change CGL029
     A**********  SEFVHL         4A  B 10 65OVRDTA                                            CGL029
     A            SEFVHL        10A  B 10 65OVRDTA
     A**End of change CGL029
     A N33                                  DSPATR(UL HI)
     A  33                                  DSPATR(RI PC)
      *
     A                                 11 23'Posting Reference 1 or 3..........-
     A                                      ......:'
     A            SFVLPR         1A  B 11 65OVRDTA
     A N34                                  DSPATR(UL HI)
     A  34                                  DSPATR(RI PC)
      *
     A                                 12 23'Other Expense Account.............-
     A                                      ......:'
     A**Start of change CGL029
     A**********  SOTEXP         4A  B 12 65OVRDTA                                            CGL029
     A            SOTEXP        10A  B 12 65OVRDTA
     A**End of change CGL029
     A N35                                  DSPATR(UL HI)
     A  35                                  DSPATR(RI PC)
      *
     A                                 14 15'Effective Cash Flow Hedge Profit -
     A                                      Contra Account.:'
     A**Start of change CGL029
     A**********  SECFHP         4A  B 14 65OVRDTA                                            CGL029
     A            SECFHP        10A  B 14 65OVRDTA
     A**End of change CGL029
     A N36                                  DSPATR(UL HI)
     A  36                                  DSPATR(RI PC)
      *
     A                                 15 23'Posting Reference 1 or 3..........-
     A                                      ......:'
     A            SCFPPR         1A  B 15 65OVRDTA
     A N37                                  DSPATR(UL HI)
     A  37                                  DSPATR(RI PC)
      *
     A                                 16 23'Other Comprehensive Income Account-
     A                                      ......:'
     A**Start of change CGL029
     A**********  SOCOMP         4A  B 16 65OVRDTA                                            CGL029
     A            SOCOMP        10A  B 16 65OVRDTA
     A**End of change CGL029
     A N38                                  DSPATR(UL HI)
     A  38                                  DSPATR(RI PC)
      *
     A                                 18 15'Effective Cash Flow Hedge Loss -
     A                                      Contra Account...:'
     A**Start of change CGL029
     A**********  SECFHL         4A  B 18 65OVRDTA                                            CGL029
     A            SECFHL        10A  B 18 65OVRDTA
     A**End of change CGL029
     A N39                                  DSPATR(UL HI)
     A  39                                  DSPATR(RI PC)
      *
     A                                 19 23'Posting Reference 1 or 3..........-
     A                                      ......:'
     A            SCFLPR         1A  B 19 65OVRDTA
     A N40                                  DSPATR(UL HI)
     A  40                                  DSPATR(RI PC)
      *
     A                                 20 23'Other Comprehensive Income Account-
     A                                      ......:'
     A**Start of change CGL029
     A**********  SOCOML         4A  B 20 65OVRDTA                                            CGL029
     A            SOCOML        10A  B 20 65OVRDTA
     A**End of change CGL029
     A N42                                  DSPATR(UL HI)
     A  42                                  DSPATR(RI PC)
      *
     A                                 23  1'F3=Exit   F12=Previous'
      **End of change CAS004
     A*
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A**Start of change CDL038
     A**27**********************************
     A*ON27*********************************SFLEND
     A  99
     AON99                                  SFLEND
     A**Start of change CDL038
     A            SPGMQ                     SFLPGMQ
