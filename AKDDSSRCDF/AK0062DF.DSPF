      *****************************************************************
/*S*D****DSPFDFRWN*****************************************************                    AR1071025
/*STD *  DSPFBASE                                                     *                    AR1071025
/*EXI *  TEXT('Midas AK SE account key gen - amend screen')           *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Account Key Generator
     A*                                                               *
     A*  AK0062DF - A/C Key Generation - Review/Amend screen          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR1071025          Date 16Jan13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 081881             Date 18Jan95               *
     A*                 S01315             Date 18FEB90               *
     A*                 E22116             Date 30MAY90               *
     A*                 S01253             DATE 17APR90               *
     A*                 AK0007             Date 11/10/89              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR1071025 - Erroneous field will only highlight after the    *
      *              second enter (Recompile)                         *
      *  081881 - This pgm is now used to meintain template keys      *
      *           as well as generated keys - amend pgm to display    *
      *           correct text on screen                              *
     A*  S01315  -  Upgrade to SD standards                           *
     A*             Program name changed from SE4020 to AK0062.       *
     A*  E22116  -  HELP SYSTEM.                                  *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //           DFRWRT(*NO) SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A*                                                    /*E22116*/
     A*                                                    Begin /*S01315*/
     A*                                                    End   /*S01315*/
     A*                                                    /*E22116*/
     A          R AKSUB                     SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CHECK(AB)
     A                                      KEEP
     A  04                                  SFLNXTCHG
     A            OPTN1          1A  B  5  2
     A            KEY1          20A  O  5  4
     A            OPTN2          1A  B  5 28
     A            KEY2          20A  O  5 30
     A            OPTN3          1A  B  5 54
     A            KEY3          20A  O  5 56
     A*
     A          R AKSUBCTL                  SFLCTL(AKSUB)
     A                                      SFLSIZ(9999)
     A*                                                          /*S01315*/
     A**********                            SFLPAG(0019)
     A                                      SFLPAG(0015)
     A*                                                          /*S01315*/
     A                                      TEXT('Control record for review of -
     A                                      generated a/c keys')
     A*                                                          /*S01315*/
     A                                      CA03(10 'Exit')
     A                                      CA09(09 'New Key')
     A                                      CA12(12 'Previous')
     A*                                                          /*S01315*/
     A*                                                    /*E22116*/
     A*                                                    /*E22116*/
     A                                      ROLLUP(35)
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A*                                                          /*S01315*/
     A********N01N02                               SFLINZ
     A N01N02                               SFLCLR
     A********                                                   /*S01315*/
     A  03                                  SFLEND
     A*                                                          /*S01315*/
     A********                              SFLRNA
     A*                                                          /*S01315*/
     A*** AK0007 ****
     A                                      OVERLAY
     A*** AK0007 ****
     A*                                                          /*S01315*/
     A***************                   2  2'SE A/C Key Generation - Review/Ame-
     A***************                       nd Generated Keys'
     A***************                       DSPATR(HI)
     A***************                       DSPATR(UL)
     A*                                                          /*S01315*/
     A*** AK0007 ****
     A*                                                          /*S01315*/
     A          R HEADER                    OVERLAY
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A*                                                    Begin /*S01315*/
     A************                      1 13'SE4020'
     A                                  1 13'AK0062'
     A*                                                    End   /*S01315*/
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A*                                                    Begin /*081881*/
     A************                      2 20'Display Keys Generated In This-
     A************                           Session'
     A N30                              2 23'Maintain Generated Account Keys'
     A                                      DSPATR(HI)
     A  30                              2 23'Maintain Template Account Keys'
     A*                                                    End   /*081881*/
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A*
     A          R FTR                       OVERLAY
     A                                      PUTOVR
     A                                 23  1'A=Amend   D=Delete   E=Enquire   -
     A                                      F3=Main Menu   F9=New Key   -
     A                                      F12=Previous'
     A          R NOFMT                     OVERLAY
     A                                      CA03(10 'Exit')
     A                                      CA09(09 'New Key')
     A                                      CA12(12 'Previous')
     A**********                            PUTRETAIN
     A                                      PUTOVR
     A                                 12 22'****    NO KEYS TO DISPLAY    ****'
     A                                      DSPATR(HI)
     A                                 23  1'F3=Main Menu   F9=New Key   -
     A                                      F12=Previous'
     A*** AK0007 ****
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  27
     AON27                                  SFLEND
     A            SPGMQ                     SFLPGMQ
     A*                                                       /*S01315*/
