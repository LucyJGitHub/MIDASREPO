      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AK FRA/IRS account keys generation menu')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Account Key Generator
     A*                                                               *
     A*  AKC30DF - FRA/IRS Account Keys Generation Menu               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No.                    Date                       *
     A*                                    Date                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     //                        LVLCHK(*NO)  //                 *
     A*    /////////////////////////////////////////                  *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03(03 'Exit')
     A          R MENUFR1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AKC30'
     A            RUNA           7A  O  1 72
     A            JOB           10A  O  2  2
     A                                  2 22'FRA/IRS Account Key -
     A                                      Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  7  7'1'
     A                                      DSPATR(HI)
     A                                  7 10'-  Generate Account Keys +
     A                                      and Produce Report'
     A                                 10  7'2'
     A                                      DSPATR(HI)
     A                                 10 10'-  Transfer Generated Keys to -
     A                                      Live Files'
     A                                 17  7'Enter required option :'
     A                                      DSPATR(HI)
     A            OPTION         1A  B 17 35OVRDTA
     A  81                                  DSPATR(RI PC)
     A                                 23  1'F3=Module Selection'
     A          R MENUFR2
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AKC30'
     A            RUNA           7A  O  1 72
     A            JOB           10A  O  2  2
     A                                  2 25'FRA/IRS Account Key Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'You have selected option 1 to gene-
     A                                      rate more keys, but the workfile st-
     A                                      ill'
     A                                  7  2'contains keys from the previous us-
     A                                      e of this option.'
     A                                  9 19'A'
     A                                      DSPATR(HI)
     A                                  9 22' -  to clear all generated keys -
     A                                      from work file'
     A                                 10 26'and input new parameters'
     A                                 12 19'B'
     A                                      DSPATR(HI)
     A                                 12 22' -  to return to menu and retain -
     A                                      generated keys'
     A                                 13 26'currently in work file'
     A                                 17  2'Enter A or B :'
     A            REPLY          1   B 17 19
