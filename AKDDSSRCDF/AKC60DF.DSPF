      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AK SE account key generation main menu')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Account Key Generator                                *
     A*                                                               *
     A*  AKC60DF - SE A/c Key Generation - Menu                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 092374             Date 08JAN97               *
     A*  PREV AMEND NO. 080060             Date 06DEC94               *
     A*                 E40669             Date 19MAY92               *
     A*                 E22116             DATE 30MAY90               *
     A*                 S01253             DATE 17APR90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  092374  -  Avoid duplicate keys                              *
     A*  080060  -  Display correct options on screen                 *
     A*          -  Strip redundant code to make code legible         *
     A*  E40669  -  UTILITY CHANGED TO PRESERVE CONTENTS OF XSEACKD   *
     A*             UNTIL CLEAR OPTION REQUESTED.                     *
     A*  S01315  -  Program name changed from SEC41 to AKC60.         *
     A*  E22116  -  HELP SYSTEM.                                  *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     //                        LVLCHK(*NO)  //                 *
     A*    /////////////////////////////////////////                  *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A*                                                    /*E22116*/
     A          R AKC60R1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AKC60'
     A            RUNA           7A  O  1 72
     A            JOB           10A  O  2  2
     A                                  2 20'Securities Trading Account Key-
     A                                       Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
      **
     A                                  6  7'1'
     A                                      DSPATR(HI)
     A                                  6 10'-  Generate account keys'
     A                                  8  7'2'
     A                                      DSPATR(HI)
     A                                  8 10'-  Review/Amend generated keys'
     A                                 10  7'3'
     A                                      DSPATR(HI)
     A                                 10 10'-  Produce input form report'
     A                                 12  7'4'
     A                                      DSPATR(HI)
     A                                 12 10'-  Transfer generated keys to -
     A                                      live files'
     A                                 14  7'5'
     A                                      DSPATR(HI)
     A                                 14 10'-  Define generated keys as -
     A                                      a template'
     A                                 16  7'6'
     A                                      DSPATR(HI)
     A                                 16 10'-  Maintain template key'
     A                                 18  7'7'
     A                                      DSPATR(HI)
     A                                 18 10'-  Clear Workfile'
     A*                                                    /*092374*/
     A                                  6 58'8'
     A                                      DSPATR(HI)
     A                                  6 61'-  Clear Template'
     A*                                                    /*092374*/
      **
     A                                 21  7'Enter required option :'
     A                                      DSPATR(HI)
     A            OPTION         1A  B 21 35OVRDTA
     A  81                                  DSPATR(RI PC)
     A                                 23  1'F3=Main Menu   F12=Previous'
     A  80        MSG1          50     24  1OVRDTA
     A                                      DSPATR(HI)
     A  81        MSG2          50     24  1OVRDTA
     A                                      DSPATR(HI)
      **
     A          R AKC60R2
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AKC60'
     A            RUNA           7A  O  1 72
     A            JOB           10A  O  2  2
     A                                  2 20'Securities Trading Account Key-
     A                                       Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'You have selected option 1 to gene-
     A                                      rate more keys, but the workfile st-
     A                                      ill'
     A                                  7  2'contains keys from the previous us-
     A                                      e of this option.'
     A                                  9 19'A'
     A                                      DSPATR(HI)
     A                                  9 22' -  to clear all generated keys -
     A                                      from work file'
     A                                 10 26'and input new parameters'
     A                                 12 19'B'
     A                                      DSPATR(HI)
     A                                 12 22' -  to return to menu and retain -
     A                                      generated keys'
     A                                 13 26'currently in work file'
     A                                 17  2'Enter A or B :'
     A            REPLY          1   B 17 19
     A  82        MSG3          50     24  1OVRDTA
     A                                      DSPATR(HI)
