      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AK FRA/IRS account keys generation menu')
      *****************************************************************
      *                                                               *
      *  Midas - Account Key Generator                                *
      *                                                               *
      *  AKC70DF - Forecast Transtions Account Keys Generation Menu   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG9344            Date 11Dec05               *
      *  Prev Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  BUG9344- Incorrect header title for forecast transaction.    *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CF03(03 'Exit')
     A          R MENUFR1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AKC70'
     A            RUNA           7A  O  1 72
     A            JOB           10A  O  2  2
     A***********                       2 16'Forecast Transactions FRA/IRS -                 BUG9344
     A                                  2 16'Forecast Transactions -                         BUG9344
     A                                      Account Key Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  7  7'1'
     A                                      DSPATR(HI)
     A                                  7 10'-  Generate Account Keys +
     A                                      and Produce Report'
     A                                 10  7'2'
     A                                      DSPATR(HI)
     A                                 10 10'-  Transfer Generated Keys to -
     A                                      Live Files'
     A                                 17  7'Enter required option :'
     A                                      DSPATR(HI)
     A            OPTION         1A  B 17 35OVRDTA
     A  81                                  DSPATR(RI PC)
     A                                 23  1'F3=Module Selection'
     A          R MENUFR2
     A                                      PUTOVR
     A            USRID         10A  O  1  2
     A                                  1 13'AKC70'
     A            RUNA           7A  O  1 72
     A            JOB           10A  O  2  2
     A                                  2 16'Forecast Transactions Account Key -
     A                                      Generation'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'You have selected option 1 to gene-
     A                                      rate more keys, but the workfile st-
     A                                      ill'
     A                                  7  2'contains keys from the previous us-
     A                                      e of this option.'
     A                                  9 19'A'
     A                                      DSPATR(HI)
     A                                  9 22' -  to clear all generated keys -
     A                                      from work file'
     A                                 10 26'and input new parameters'
     A                                 12 19'B'
     A                                      DSPATR(HI)
     A                                 12 22' -  to return to menu and retain -
     A                                      generated keys'
     A                                 13 26'currently in work file'
     A                                 17  2'Enter A or B :'
     A            REPLY          1   B 17 19
