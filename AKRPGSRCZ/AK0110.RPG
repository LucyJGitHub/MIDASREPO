     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AK Transfer Account Keys to template file')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Account Key Generator
     F*                                                               *
     F*  AK0110 - Account Keys Generation Transfer.                   *
     F*           Transfer of generated keys from work file XSEACKD   *
     F*           to live file TSEACKD, program created to avoid pro- *
     F*            plem of duplicate keys (replace copy file)         *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 092556             Date 07Jan97               *
      *                 092374             Date 07Jan97               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*   092374 - Those amendment try to manage the problem of dupli.*
     F*            cate keys.                                         *
      *  MD046248 - Finastra Rebranding                               *
     F*   092556 - Avoid duplicate keys (Use RPG pgm instead CPYF).   *
     F*            NEW PROGRAM CREATED||                              *
     F*****************************************************************
     F*XSEACKD IF  E           K        DISK                              92374
     FXSEACKD UF  E           K        DISK                               92374
     F            TSEACKDF                          KRENAMEXSEACK
     FTSEACKD IF  E           K        DISK                      A
     F/COPY WNCPYSRC,AK0110F001
      *
      *   INDICATOR USAGE
      *   ---------------
      *   10      XSEACKD  end-of-file.
      *   98      Record exists already on PF TSEACKD
      *   U8      Database error.
      *
     E*****************************************************************
     E                    CPY@    1   1 80
     E** Copyright array
     E*
     I*
     IXSEACK
     I              RECI                            ZRECI
     I              ACKE                            ZACKE
     I              ACD1                            ZACD1
     I              ACD2                            ZACD2
     I              ACD3                            ZACD3
     I              ACD4                            ZACD4
     I              ACD5                            ZACD5
     I              ACD6                            ZACD6
     I              NCDA1                           ZNCDA1
     I              NCDA2                           ZNCDA2
     I              NCDA3                           ZNCDA3
     I              NCDA4                           ZNCDA4
     I              NCDA5                           ZNCDA5
     I              NCDA6                           ZNCDA6
     I              PRF1                            ZPRF1
     I              PRF2                            ZPRF2
     I              PRF3                            ZPRF3
     I              PRF4                            ZPRF4
     I              PRF5                            ZPRF5
     I              PRF6                            ZPRF6
     I              LCD                             ZLCD
     I              CHTP                            ZCHTP
     I              TNLU                            ZTNLU
     I              ETYPA                           ZETYPA
     I              ATYPA                           ZATYPA
     I*
     I*
     I*  LDA for database error
     I*
     ILDA         DS                            256
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     ISDBANK    E DSSDBANKPD
     I** Bank Details
     I*
     IDSFDY     E DSDSFDY
     I** First DS for Access Programs, Short Data Structure
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I** Data Structure for Compilation  - Copyright Insertion
     I*
     C*****************************************************************
     C*
     C* Prepare LDA.
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBLOT
     C                     MOVEL'AK0110  'DBPGM
     C*
     C** Use access program AOBANKR0 to retrieve SDBANKPD Details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C*
     C** Ensure no records written to tseackd file
     C** does not exist.
     C*
     C*
     C                     READ XSEACKD                  10
     C** Select only LIVE records **
     C           ZRECI     DOWNE'D'
     C           *IN10     ANDNE*ON
     C                     READ XSEACKD                  10
     C                     ENDDO
     C*
     C           *IN10     DOWEQ'0'
     c*
     C                     EXSR LIVE
     c*
     C                     READ XSEACKD                  10
     C** Select only LIVE records **
     C           ZRECI     DOWNE'D'
     C           *IN10     ANDNE*ON
     C                     READ XSEACKD                  10
     C                     ENDDO
     c*
     C                     ENDDO
     C                     SETON                     LR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C* LIVE - Update/Write new Account Keys to TEMPLATE FILE
     C*
     C* Calls     : None
     C*
     C* Called by : Main processing
     C*
     C********************************************************************
     C           LIVE      BEGSR
     C*
     C                     SETOF                     98
     C*
     C*  Chain to live file (ACKEY detail) to avoid duplication, when
     C*  adding records.
     C*
     C           ZACKE     CHAINTSEACKD              98
     C*
     C** If record not found, write record to TSEACKD
     C*
     C           *IN98     IFEQ '1'
     C** Store values in work file.
     C*
     C                     MOVE ZRECI     RECI
     C                     MOVE ZACKE     ACKE
     C                     MOVE ZACD1     ACD1
     C                     MOVE ZACD2     ACD2
     C                     MOVE ZACD3     ACD3
     C                     MOVE ZACD4     ACD4
     C                     MOVE ZACD5     ACD5
     C                     MOVE ZACD6     ACD6
     C                     MOVE ZNCDA1    NCDA1
     C                     MOVE ZNCDA2    NCDA2
     C                     MOVE ZNCDA3    NCDA3
     C                     MOVE ZNCDA4    NCDA4
     C                     MOVE ZNCDA5    NCDA5
     C                     MOVE ZNCDA6    NCDA6
     C                     MOVE ZPRF1     PRF1
     C                     MOVE ZPRF2     PRF2
     C                     MOVE ZPRF3     PRF3
     C                     MOVE ZPRF4     PRF4
     C                     MOVE ZPRF5     PRF5
     C                     MOVE ZPRF6     PRF6
     C                     MOVE ZLCD      LCD
     C                     MOVE ZCHTP     CHTP
     C                     MOVE ZTNLU     TNLU
     C                     MOVE ZETYPA    ETYPA
     C                     MOVE ZATYPA    ATYPA
     C*RECORD FORMAT IS RENAMED TO BE USED FOR XSEACK TO TSEACKD
     C*AND FROM XSEACKD TO SEACKD
     C                     WRITETSEACKDF
     C*
     C*                                                                   92374
     c*If A/C Keys doesn't exist so delete them in XSEACKD                92374
     c*and avoid clear of file in the calling pgm                         92374
     C                     DELETXSEACK                                    92374
     c*                                                                   92374
     C                     ELSE                                           92374
     C*Switch U5 will be used to indicate that some records are in double 92374
     C*and let the user managed the double keys him self.                 92374
     C                     MOVE *ON       *INU5                           92374
     C*                                                                   92374
     C                     ENDIF
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C* DBERR - Database error processing
     C*
     C* Calls     : None
     C*
     C* Called by : Main processing
     C*
     C********************************************************************
     C           DBERR     BEGSR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     OUT  LDA
     C                     RETRN
     C*
     C                     ENDSR
     C********************************************************************
**  CPY@
(c) Finastra International Limited 2001
