     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AK Forecast Transactions A/C keys generation')
      *****************************************************************
      *                                                               *
      *  Midas - Account Key Generator                                *
      *                                                               *
      *  AKC70 - Forecast Transtions Account Keys Generation Report   *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     FZACKEYL1IF  E           K        DISK
      ** Midas AK SD Account Keys Work File
      *
     FAK0170P1O   E             99     PRINTER      KINFDS SPOOL
      ** Midas AK Forecast Trans. A/c Key Generion Report
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  10 - End of file ZACKEYL1                                    *
      *  70 - Output '*Continued*'                                    *
      *  99 - Overflow indicator                                      *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Description Tables and Array
     E                    CPY@    1   1 80
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Data Structure for Compilation  - Copyright Insertion
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      **  Spool information data structure for main print
     ISPOOL       DS
     I                                       83  90 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 188 1890OFLLN
     I                                    B 367 3680PRTLN
      *
      ** Bank Details
     ISDBANK    E DSSDBANKPD
      *
      ** First DS for Access Programs, Short Data Structure
     IDSFDY     E DSDSFDY
      *
      ** Account Key Data Structure
     IAKEY        DS
     I                                        1  10 SHAKEY                                    CDL038
     I                                        1   2 TYP
     I                                        4   5 SUBTYP
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Main Processing                                              *
      *                                                               *
      *****************************************************************
      *
      ** Use access program AOBANKR0 to retrieve header details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      **  Ensure report spool file recorded by RCF
      *
     C                     Z-ADDSFNUM     ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
      *
      **  Error during ZSFILE processing, return to calling program
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
      ** Write report headings
      *
     C                     WRITEFMTW01
      *
      ** Initialise work fields for Previous Type/Subtype.
      *
     C                     MOVE *BLANKS   PRVSTY  2
      *
     C                     MOVE *BLANKS   TYPE@   5
      *
      ** Set record counter to zero
      *
     C                     Z-ADD0         RECCNT
      *
      ** Read first record for ZACKEYL1
      *
     C                     READ ZACKEYL1                 10
     C           *IN10     IFEQ '1'
     C                     WRITEFMTW02
     C                     ELSE
      *
      ** Add one to record counter
      *
     C                     ADD  1         RECCNT
     C                     END
      *
      ** Do while record in ZACKEYL1
      *
     C           *IN10     DOWEQ'0'
      *
      ** Type/Subtype same as previous Type/Subtype
      *
     C           SUBTYP    IFEQ PRVSTY
     C                     EXSR SRDETL
     C                     WRITEFMTW05
     C                     ELSE
      *
     C                     MOVE *BLANKS   TYPE@
     C                     MOVELTYP       TYPE@
     C                     MOVE SUBTYP    TYPE@
      *
      ** Type/Subtype different to previous Type/Subtype
      *
     C                     EXSR SRHEAD
     C                     EXSR SRDETL
     C                     WRITEFMTW05
     C                     ENDIF
      *
      ** Read record for ZACKEYL1
      *
     C                     READ ZACKEYL1                 10
      *
      ** Overflow
      *
     C           *IN99     IFEQ '1'
     C                     WRITEFMTW01
      *
     C           *IN10     IFEQ '0'
      *
     C           SUBTYP    IFEQ PRVSTY
     C                     MOVE *ON       *IN70
     C                     WRITEFMTW03
     C                     MOVE *OFF      *IN70
     C                     WRITEFMTW04
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVE *OFF      *IN99
      *
      ** Output end of report
      *
     C           *IN10     IFEQ *ON
     C                     WRITEFMTEND
     C                     ELSE
      *
      ** Add one to record counter
      *
     C                     ADD  1         RECCNT
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * SRHEAD -    Subroutine for Forecast Transactions Headings        *
      *                                                                  *
      ********************************************************************
     C           SRHEAD    BEGSR
      *
     C                     WRITEFMTW03
     C                     WRITEFMTW04
      *
      ** Move Subtype into previous Subtype work fields
      *
     C                     MOVE SUBTYP    PRVSTY
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * SRDETL -    Subroutine to move numeric file fields to alpha      *
      *             report fields, and ensure that if file field         *
      *             is zero then blanks are printed.                     *
      *                                                                  *
      ********************************************************************
     C           SRDETL    BEGSR
      *
      ** Set up Account Codes
      *
     C           ACD1      IFEQ *ZERO
     C                     MOVE *BLANKS   ACDA1
     C                     ELSE
     C                     MOVE ACD1      ACDA1
     C                     ENDIF
      *
     C           ACD2      IFEQ *ZERO
     C                     MOVE *BLANKS   ACDA2
     C                     ELSE
     C                     MOVE ACD2      ACDA2
     C                     ENDIF
      *
     C           ACD3      IFEQ *ZERO
     C                     MOVE *BLANKS   ACDA3
     C                     ELSE
     C                     MOVE ACD3      ACDA3
     C                     ENDIF
      *
     C           ACD4      IFEQ *ZERO
     C                     MOVE *BLANKS   ACDA4
     C                     ELSE
     C                     MOVE ACD4      ACDA4
     C                     ENDIF
      *
     C           ACD5      IFEQ *ZERO
     C                     MOVE *BLANKS   ACDA5
     C                     ELSE
     C                     MOVE ACD5      ACDA5
     C                     ENDIF
      *
     C           ACD6      IFEQ *ZERO
     C                     MOVE *BLANKS   ACDA6
     C                     ELSE
     C                     MOVE ACD6      ACDA6
     C                     ENDIF
      *
      ** Set up Narrative Codes
      *
     C                     MOVE NCD1      NCDA1
     C                     MOVE NCD2      NCDA2
     C                     MOVE NCD3      NCDA3
     C                     MOVE NCD4      NCDA4
     C                     MOVE NCD5      NCDA5
     C                     MOVE NCD6      NCDA6
      *
      ** Set up Posting References
      *
     C           PRF1      IFEQ *ZERO
     C                     MOVE *BLANKS   PRFA1
     C                     ELSE
     C                     MOVE PRF1      PRFA1
     C                     END
      *
     C           PRF2      IFEQ *ZERO
     C                     MOVE *BLANKS   PRFA2
     C                     ELSE
     C                     MOVE PRF2      PRFA2
     C                     END
      *
     C           PRF3      IFEQ *ZERO
     C                     MOVE *BLANKS   PRFA3
     C                     ELSE
     C                     MOVE PRF3      PRFA3
     C                     END
      *
     C           PRF4      IFEQ *ZERO
     C                     MOVE *BLANKS   PRFA4
     C                     ELSE
     C                     MOVE PRF4      PRFA4
     C                     END
      *
     C           PRF5      IFEQ *ZERO
     C                     MOVE *BLANKS   PRFA5
     C                     ELSE
     C                     MOVE PRF5      PRFA5
     C                     END
      *
     C           PRF6      IFEQ *ZERO
     C                     MOVE *BLANKS   PRFA6
     C                     ELSE
     C                     MOVE PRF6      PRFA6
     C                     END
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2003
