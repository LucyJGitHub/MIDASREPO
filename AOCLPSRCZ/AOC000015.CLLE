/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas AO Account Transfer Shadow Posting')            */
/*********************************************************************/
/*                                                                   */
/*       Midas - Upgrade Module                                      */
/*                                                                   */
/*       AOC000015 - Midas AO Account Transfer Shadow Posting        */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2010           */
/*                                                                   */
/*       Last Amend No. BUG27966           Date 04Aug10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*       Prev Amend No. BUG27424 *CREATE   Date 09Apr10              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BUG27966 - Enhance ACNTRNSSP                                */
/*       BUG27424 - ABC Reservation Update Flag is not being updated */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RETRNMSG &PACTION &INPDS &RESERVID &SBS)
 
             DCL        VAR(&RETRNMSG) TYPE(*CHAR) LEN(256)
             DCL        VAR(&PACTION)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&INPDS)    TYPE(*CHAR) LEN(267)
             DCL        VAR(&RESERVID) TYPE(*CHAR) LEN(15)
             DCL        VAR(&SBS) TYPE(*CHAR) LEN(2)
 
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(2764)
             DCL        VAR(&INLLIBL) TYPE(*CHAR) LEN(2750)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JOBDNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(20)
             DCL        VAR(&LEN) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PARM) TYPE(*CHAR) LEN(3193)
             DCL        VAR(&OFFSET) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&LIBCNT) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&OFFLEN) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&ERRCDE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&CTR)    TYPE(*DEC)  LEN(1 0)                               /*BUG27966*/
             DCL        VAR(&JOBNO) TYPE(*CHAR) LEN(6)                                  /*BUG27966*/
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1)                                   /*BUG27966*/
             DCL        VAR(&TPINPDS) TYPE(*CHAR) LEN(267)                              /*BUG27966*/
 
             COPYRIGHT  TEXT('(c) Misys International Banking +
                          Systems Ltd. 2010')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                        CMDLBL(ABNOR))
 
             CHGVAR     VAR(&LIB) VALUE(&SBS *CAT 'XLIB')
 
             CHKOBJ     OBJ(&LIB/MIDASIC) OBJTYPE(*JOBD)
             MONMSG     MSGID(CPF9801 CPF9802 CPF9810 CPF9820 +
                          CPF9830 CPF9899) EXEC(GOTO CMDLBL(ABNOR))
 
 
/* Set up parameters for call to IBM API. */
             CHGVAR     VAR(&JOBDNAME) VALUE('MIDASIC')
             CHGVAR     VAR(&JOBD) VALUE(&JOBDNAME *CAT &LIB)
             CHGVAR     VAR(%BIN(&LEN 1 4)) VALUE(3193)
             CHGVAR     VAR(%BIN(&ERRCDE 1 4)) VALUE(0)
 
/* Use API to access the job description library list. */
             CALL       PGM(QWDRJOBD) PARM(&PARM &LEN JOBD0100 &JOBD +
                          &ERRCDE)
 
 INLLIBL:    CHGVAR     &OFFSET %BIN(&PARM 361 4)
             CHGVAR     VAR(&OFFSET) VALUE(&OFFSET + 1)
             CHGVAR     VAR(&LIBCNT) VALUE(%BIN(&PARM 365 4))
             CHGVAR     VAR(&OFFLEN) VALUE(11 * &LIBCNT)
             CHGVAR     VAR(&INLLIBL) VALUE(%SST(&PARM &OFFSET +
                          &OFFLEN))
             MONMSG     MSGID(MCH3601)
 
             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *CAT +
                             &INLLIBL *CAT ')')
 
             CALL       PGM(QCMDEXC) PARM(&CMD 2764)
 
/* Check if the reservation ID dataarea exists, if not create it.  This will hold the */
/* reservation ID parameter                                                           */
 
             CHKOBJ     OBJ(QTEMP/GPRESERVID) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/GPRESERVID) TYPE(*CHAR) LEN(15))
             CHGDTAARA  DTAARA(QTEMP/GPRESERVID) VALUE(&RESERVID)
 
/* Call shadow posting program  */
 
             CHGVAR     VAR(&PACTION) VALUE('*CLEAR  ')                                 /*BUG27966*/
             CHGVAR     VAR(&TPINPDS) VALUE(&INPDS)                                     /*BUG27966*/
             CHGVAR     VAR(&INPDS)   VALUE(' ')                                        /*BUG27966*/
 CYCLE:                                                                                 /*BUG27966*/
             CHGVAR     VAR(&CTR) VALUE(&CTR + 1)                                       /*BUG27966*/
             IF         COND(&CTR *EQ 2) THEN(DO)                                       /*BUG27966*/
                  CHGVAR     VAR(&PACTION) VALUE('*ADD    ')                            /*BUG27966*/
                  CHGVAR     VAR(&INPDS)   VALUE(&TPINPDS)                              /*BUG27966*/
             ENDDO                                                                      /*BUG27966*/
                                                                                        /*BUG27966*/
             CALL       PGM(AO000012) PARM(&RETRNMSG &PACTION &INPDS)
                                                                                        /*BUG27966*/
             IF         COND(&RETRNMSG *EQ ' ') THEN(DO)                                /*BUG27966*/
                  IF         COND(&PACTION *NE '*UPDATE') THEN(DO)                      /*BUG27966*/
                       CHGVAR     VAR(&PACTION) VALUE('*UPDATE ')                       /*BUG27966*/
                       GOTO       CYCLE                                                 /*BUG27966*/
                  ENDDO                                                                 /*BUG27966*/
             ENDDO                                                                      /*BUG27966*/
             ELSE       CMD(GOTO CMDLBL(ABNOR))                                         /*BUG27966*/
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
             GOTO       CMDLBL(END)
 
 ABNOR:
 
 /** Send error message */
 
             SNDPGMMSG  MSG('Program AOC000015 ended abnormally - see joblog') +
                          TOMSGQ(MOPERQ)
 
END:
             ENDPGM
