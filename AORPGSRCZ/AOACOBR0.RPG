     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Aut. COB data update index  *RDR')
     F*****************************************************************
     F*                                                               *
     F*  Midas - ACCESS OBJECTS
     F*                                                               *
     F*  AOACOBR0 - AUTOMATIC COB DATA UPDATE INDEX                   *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 059791             Date 20JAN94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  059791 - New for Save Master Library routine (SCC1712).      *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F/EJECT
     F*FSTART
     F*
     FCBACOBL1IF  E           K        DISK                           UC
     F*
     F** IN - @ACOBF Automatic COB data update index
     F*
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   01  - Initialisation.                                       *
     F*   90  - File error.                                           *
     F*   91  - File open error.                                      *
     F*   92  - File close error.                                     *
     F*   93  - File empty.                                           *
     F*   94  - End of file.                                          *
     F*   95  - No record found.                                      *
     F*   96  - Option error.                                         *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     E*ESTART
     E                    @MG     1   1 50
     E*
     E                    CPY@    1   1 80               Copyright
     E*
     E** Description    : Copyright notice for inclusion
     E**                   in all programs
     E*
     E/EJECT
     I*ISTART
     I*
     IP@MGDA      DS
     I                                        1  50 @MG
     I*
     IPGMDS      SDS
     I*
     I** Program Status data structure
     I*
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
     I*
     ID@FMT     E DSCBACOBPD
     I*
     I** @ACOBF - Automatic COB data update index record format
     I**          data structure.
     I*
     IA@CPY       DS
     I*
     I** Copyright array
     I*
     I                                        1  80 CPY@
     I*
     IP@FMT     E DSDSFDY
     I*
     I** External Data structure to hold the outgoing record format
     I** of the file.
     I*
     C/EJECT
     C*CSTART
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@AUTO 10        I:Key field
     C                     PARM           P@FMT            O:Format
     C*
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C                     GOTO T@SKIP
     C                     END
     C*
     C                     MOVE '0000000' *IN90             ¦ Reset error ind.
     C*
     C** First call - open file.
     C*
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     MOVE '1'       *IN01              Set ind.
     C                     OPEN CBACOBL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
     C*
     C** Last call - close file and terminate program.
     C*
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSECBACOBL1               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     MOVE '1'       *INLR              Set ind.
     C                     END                             FI 2
     C*
     C** Caller requests first record.
     C*
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLL@ACOBF1              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
     C*
     C** Caller requests next record.
     C*
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ @ACOBF1                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
     C*
     C** Caller requests last record.
     C*
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGT@ACOBF1                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
     C*
     C** Caller requests previous record.
     C*
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READP@ACOBF1                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
     C*
     C** Caller requests record indicated by key.
     C*
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEY ACCESS
     C           P@AUTO    CHAIN@ACOBF1              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
     C*
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     MOVE '1'       *IN96              Parm error
     C                     END                             FI 8
     C*
     C** No record found or End of file - return error code.
     C*          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
     C*          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C           *IN90     IFEQ '1'
     C                     MOVE '*ERROR ' P@RTCD           File error
     C                     END
     C           *IN91     IFEQ '1'
     C                     MOVE '*OPEN  ' P@RTCD           Open error
     C                     END
     C           *IN92     IFEQ '1'
     C                     MOVE '*CLOSE ' P@RTCD           Close error
     C                     END
     C           *IN93     IFEQ '1'
     C                     MOVE '*EMPTY ' P@RTCD           Empty file
     C                     END
     C           *IN94     IFEQ '1'
     C                     MOVE '*EOF   ' P@RTCD           End of file
     C                     END
     C           *IN95     IFEQ '1'
     C                     MOVE '*NRF   ' P@RTCD           No record found
     C                     END
     C           *IN96     IFEQ '1'
     C                     MOVE '*OPTION' P@RTCD           Option error
     C                     END
     C*
     C** Caller requests message sent to program queue.
     C*
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C           *IN90     IFEQ '1'
     C                     MOVE 'ACO1001' P@MGID             File error
     C                     END
     C           *IN91     IFEQ '1'
     C                     MOVE 'ACO1002' P@MGID             Open error
     C                     END
     C           *IN92     IFEQ '1'
     C                     MOVE 'ACO1003' P@MGID             Close error
     C                     END
     C           *IN93     IFEQ '1'
     C                     MOVE 'ACO1004' P@MGID             Empty file
     C                     END
     C           *IN94     IFEQ '1'
     C                     MOVE 'ACO1005' P@MGID             End of file
     C                     END
     C           *IN95     IFEQ '1'
     C                     MOVE 'ACO1006' P@MGID             No rec found
     C                     END
     C           *IN96     IFEQ '1'
     C                     MOVE 'ACO1007' P@MGID             Option error
     C                     END
     C*
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
     C*
     C** Caller requests DB error handling.
     C*
     C           W@RTCD    IFEQ '*DBERR'
     C           P@RTCD    ANDNE*BLANK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END
     C*
     C           P@OPTN    IFNE '*VERIFY'
     C           P@OPTN    ANDNE'*FREE'
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
     C*
     C** Return to caller.
     C*
     C                     RETRN
     C*
**
CBACOBPD  AOACOBR0   Automatic COB data update index
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
