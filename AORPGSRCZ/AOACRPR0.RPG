     H        1                                                           S01230
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO A/c codes retention period')
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOACRPR0 - READ ACCESS TO ACCOUNT CODES FILE                 *
     F*                ( OMIT RECORDS WITH RETENTION PERIOD OF ZERO )    *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 01Mar99               *
      *                 CTI002             Date 23Dec98               *
     F*                    S01314             DATE 20FEB91               *S01230
     F*                    S01230             DATE 03/01/90              *S01230
     F*                    S01230             DATE 23/11/89              *S01230
     F*                    S01230             DATE 06/11/89              *S01230
     F*                                                                  *S01230
     F*                                                                  *
     F********************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *           to SDACODPD.  Recompilation of programs.            *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
     F*     S01314 - CHANGES MADE DURING R10 PTF INCORPORATION           *S01230
     F*     S01230 - STANDARDISE ACCESS PROGRAM & COPYRIGHT ADDED        *S01230
     F********************************************************************S01230
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       01      OFF 1ST TIME ROUND
     F*       90      FILE ERROR
     F*       91      FILE OPEN ERROR                                    *
     F*       92      ERROR ON FILE CLOSE                                *
     F*       93      FILE EMPTY
     F*       94      EOF
     F*       95      NO RECORD FOUND
     F*       96      PARAMETER ERROR
     F*       97      IF NO SELECTION
     F*       98      ERROR ON PROGRAM CALL (SDFPSRB)
     F*       99      ERROR ON PROGRAM CALL (ZASNMG)
     F*       LR      LAST RECORD
     F*
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDACODL2IF  E           K        DISK                           UC
      * IN  - @ACODHB
      ************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     E                    @MG     1   1 50                                S01230
     E*                                                                   S01230
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************        S01230
      /EJECT                                                              S01230
     I*ISTART                                                             S01230
      ************************************************************        S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
      * Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*                                                                   S01230
     IP@MGDA      DS                                                      S01230
     I                                        1  50 @MG                   S01230
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I*P@A5RF    E DSSDACODPD                                             S01230
     ID@FMT     E DSSDACODPD                                              S01230
      * @ACODHB
      *
     IP@FMT     E DSDSFDY                                                 S01230
     I******P@FMT       DS                            148                 S01230
      * Internal book code data structure.                                S01230
      *                                                                   S01230
     I*P@AFNB      DS                                                     S01230
     IP@ACCD      DS                                                      S01230
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I                                        3   3 W@SL03
     I                                        4   4 W@SL04
     I                                        5   5 W@SL05                                    CGL029
     I                                        6   6 W@SL06                                    CGL029
     I                                        7   7 W@SL07                                    CGL029
     I                                        8   8 W@SL08                                    CGL029
     I                                        9   9 W@SL09                                    CGL029
     I                                       10  10 W@SL10                                    CGL029
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C*********************PARM           P@AFNB           I:Key field    S01230
     C                     PARM           P@ACCD           I:Key field    S01230
     C*********************PARM           P@A5RF           O:Format       S01230
     C                     PARM           P@FMT            O:Format       S01230
      *****************************************************
      *MAIN
     C           P@OPTN    IFEQ '*SAME  '                                 S01230
     C           *IN01     COMP '0'                      91               S01230
     C                     GOTO T@SKIP                                    S01230
     C                     END                                            S01230
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDACODL2               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PROG
     C                     CLOSESDACODL2               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     RETRN                             Return
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLL@ACODHB              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ @ACODHB                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGT@ACODHB                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READP@ACODHB                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEYED ACS
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           W@SL01    IFEQ '?'
     C           W@SL02    OREQ '?'
     C           W@SL03    OREQ '?'                                                           CGL029
     C           W@SL04    OREQ '?'                                                           CGL029
     C           W@SL05    OREQ '?'                                                           CGL029
     C           W@SL06    OREQ '?'                                                           CGL029
     C           W@SL07    OREQ '?'                                                           CGL029
     C           W@SL08    OREQ '?'                                                           CGL029
     C           W@SL09    OREQ '?'                                                           CGL029
     C           W@SL10    OREQ '?'                                                           CGL029
     C*********************CALL 'SDP2SRR'              98    <SELECT>     S01230
     C                     CALL 'SD0032S'              98    <SELECT>     S01230
     C                     PARM *BLANK    P@RTC2  7          B:Return code
     C*********************PARM *BLANK    P@AFNB             B:Key        S01230
     C                     PARM *BLANK    P@ACCD             B:Key        S01230
     C           P@RTC2    COMP *BLANK               97      No selection
     C           *IN97     CABEQ'1'       T@SKIP             No selection
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C                     END                             FI 8
     C***********P@AFNB    CHAIN@ACODHB              9590    Get record   S01230
     C           P@ACCD    CHAIN@ACODHB              9590    Get record   S01230
     C           *IN95     CABEQ'1'       T@SKIP             No record fnd
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND           S01230
     C           P@OPTN    ANDNE'*FREE  '                   AND           S01230
     C                     SETON                       96    Parameter err
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C***90                MOVE 'ACO0001' P@MGID             File error   S01314
     C***91                MOVE 'ACO0002' P@MGID             Open error   S01314
     C***92                MOVE 'ACO0003' P@MGID             Close error  S01314
     C***93                MOVE 'ACO0004' P@MGID             Empty file   S01314
     C***94                MOVE 'ACO0005' P@MGID             End of file  S01314
     C***95                MOVE 'ACO0006' P@MGID             No record fndS01314
     C***96                MOVE 'ACO0007' P@MGID             Option error S01314
     C***97                MOVE 'ACO0009' P@MGID             No selection S01314
     C***98                MOVE 'ACO0010' P@MGID             Call error   S01314
     C   90                MOVE 'ACO1001' P@MGID             File error   S01314
     C   91                MOVE 'ACO1002' P@MGID             Open error   S01314
     C   92                MOVE 'ACO1003' P@MGID             Close error  S01314
     C   93                MOVE 'ACO1004' P@MGID             Empty file   S01314
     C   94                MOVE 'ACO1005' P@MGID             End of file  S01314
     C   95                MOVE 'ACO1006' P@MGID             No record fndS01314
     C   96                MOVE 'ACO1007' P@MGID             Option error S01314
     C   97                MOVE 'ACO1009' P@MGID             No selection S01314
     C   98                MOVE 'ACO1010' P@MGID             Call error   S01314
     C                     CALL 'ZASNMG'               99    <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Program nme
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Message fle
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM *BLANK    P@MGDA 50          I:Message dta
     C                     PARM '*DIAG'   P@MGTP  7          I:Message tpe
     C                     END                             FI 9
      *
      * Caller Requests DB Error Handling                                 S01230
      *                                                                   S01230
     C           W@RTCD    IFEQ '*DBERR'                   IF             S01230
     C           P@RTCD    ANDNE*BLANK                       AND          S01230
     C                     DUMP                                           S01230
     C                     CALL 'DBERRCTL'                                S01230
     C                     END                             FI             S01230
      *                                                                   S01230
     C           P@OPTN    IFNE '*VERIFY'                                 S01230
     C           P@OPTN    ANDNE'*FREE'                                   S01230
     C           P@RTCD    ANDNE'*OPTION'                                 S01230
     C                     MOVELD@FMT     P@FMT                           S01230
     C                     END                                            S01230
      *                                                                   S01230
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDACODPD  AOACRPR0 Account Codes Retention Period                         S01230
** CPY@     : Copyright notice for inclusion in all programs              S01230
(c) Finastra International Limited 2001
