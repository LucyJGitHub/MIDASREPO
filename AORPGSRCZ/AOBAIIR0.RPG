     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO CI ICD access object')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Access Objects                                       *
     F*                                                               *
     F*  AOBAIIR0 - Read Access to Cashier Interface Details          *
     F*             I.C.D Access Object                               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT003  *CREATE    Date 22JUL96               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CRT003 - Cashier Interface                                   *
     F*                                                               *
     F*****************************************************************
     F* ID F  C  H  L  FUNCTION OF INDICATORS
     F*
     F*       01       OFF ON THE FIRST RUN
     F*       90       FILE ERROR
     F*       91       OPEN ERROR
     F*       92       CLOSE ERROR
     F*       93       EMPTY FILE
     F*       96       OPTION ERROR
     F*
      /EJECT
      ************************************************************
     FSDBAIIL1IF  E           K        DISK                           UC
      * IN  - @BAIIL1 BAI DETAILS
      **
      /EJECT
     E*
     E                    @MG     1   1 50
     E*
      * Desciption     : Copyright notice for inclusion in all programs
     E                    CPY@    1   1 80               Copyright
     E*                                                                ***
      /EJECT
      *
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     ID@FMT     E DSSDBAIIPD
      * @BAIIL1 - CI RECORD FORMAT DATA STRUCTURE
      *
     I*P@FMT     E DSDSSDY
     IP@FMT       DS                            264
     I* External Data structure to hold the outgoing record format of
     I* the file
      *
     I*
      * Description     : Copyright notice for inclusion in all programs
     I*
     IA@CPY       DS
      * Copyright array
     I                                        1  80 CPY@
      ************************************************************
      /EJECT
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM D@FMT     P@FMT            O:Format
      *
      *MAIN
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     9396   ¦ error
     C           P@OPTN    IFEQ '*KEY   '
     C           P@OPTN    OREQ '*FIRST '
     C           P@OPTN    OREQ '*LAST  '
     C           P@OPTN    OREQ '*NEXT  '
     C           P@OPTN    OREQ '*PREV  '
     C           P@OPTN    OREQ '*SAME  '
     C           P@OPTN    OREQ '*VERIFY'
     C           *IN01     IFEQ '0'                          IF2 1'st time
     C                     SETON                     01
     C                     OPEN SDBAIIL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C           *LOVAL    SETLL@BAIIL1              9390    Set file
     C  N93                READ @BAIIL1                  93
     C                     CLOSESDBAIIL1               92    Close file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     END                               FI2
     C                     ELSE
      *
     C           P@OPTN    IFEQ '*FREE  '
     C                     SETON                     LR
     C                     RETRN
     C                     ELSE
      *
     C                     SETON                     96      Parm error
     C                     END
     C                     END
      *
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   96                MOVE '*OPTION' P@RTCD           No selection
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   96                MOVE 'ACO1007' P@MGID
     C                     CALL 'ZASNMG'                     <SND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      *      CALLER REQUESTS DB HANDLING
      *
     C           W@RTCD    IFEQ '*DBERR'                   IF DB ERROR
     C           P@RTCD    ANDNE*BLANK                     AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDBAIIPD  AOBAIIR0  Cashier Interface Details
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
