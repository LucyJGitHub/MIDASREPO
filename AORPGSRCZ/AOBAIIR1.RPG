     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO CI ICD access object')
      *****************************************************************
      *                                                               *
      *  Midas - Access Objects                                       *
      *                                                               *
      *  AOBAIIR1 - Read Access to Cashier Interface Details          *
      *             I.C.D Access Object                               *
      *                                                               *
      *  Function:  This program is similar to AOBAIIR0 except that   *
      *             the return parameter uses DSSDY because of the    *
      *             recently added fields in SDBAIIPD.  P@FMT with    *
      *             length of 264 could not accommodate the added     *
      *             fields.                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CRT005  *CREATE    Date 09Aug01               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT005 - Cashier Enhancement for Cash In/Cash Out            *
      *                                                               *
      *****************************************************************
     FSDBAIIL1IF  E           K        DISK                           UC
      ** Cashier Interface ICD
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   01  - Off on the first run                                  *
      *   90  - File error                                            *
      *   91  - Open error                                            *
      *   92  - Close error                                           *
      *   93    Empty file                                            *
      *   96  - Option error                                          *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E                    @MG     1   1 50
      *
      /EJECT
      *
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      ** Program Status data structure
      *
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     ID@FMT     E DSSDBAIIPD
      ** Cashier Interface record format data structure
      *
     IP@FMT     E DSDSSDY
      ** External Data structure to hold the outgoing record format of
      ** the file
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7
     C                     PARM           P@OPTN  7
     C                     PARM D@FMT     P@FMT
      *
      ** Reset error indicators
      *
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN96
      *
     C           P@OPTN    IFEQ '*KEY   '
     C           P@OPTN    OREQ '*FIRST '
     C           P@OPTN    OREQ '*LAST  '
     C           P@OPTN    OREQ '*NEXT  '
     C           P@OPTN    OREQ '*PREV  '
     C           P@OPTN    OREQ '*SAME  '
     C           P@OPTN    OREQ '*VERIFY'
      *
      ** If first time
      *
     C           *IN01     IFEQ *OFF
     C                     MOVE *ON       *IN01
     C                     OPEN SDBAIIL1               91
      *
      ** Open error
      *
     C           *IN91     CABEQ*ON       T@SKIP         LR
     C           *LOVAL    SETLL@BAIIL1              9390
      *
     C           *IN93     IFEQ *OFF
     C                     READ @BAIIL1                  93
     C                     ENDIF
      *
     C                     CLOSESDBAIIL1               92
      *
      ** Empty file, File error and Close error
      *
     C           *IN93     CABEQ*ON       T@SKIP
     C           *IN90     CABEQ*ON       T@SKIP         LR
     C           *IN92     CABEQ*ON       T@SKIP         LR
     C                     ENDIF
     C                     ELSE
      *
     C           P@OPTN    IFEQ '*FREE  '
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ELSE
     C                     MOVE *ON       *IN96
     C                     ENDIF
     C                     ENDIF
      *
      ** No record found or End of file - return error code:
      *
     C           T@SKIP    TAG
      *
      ** No error
      *
     C                     MOVE *BLANK    P@RTCD
      *
      ** File error
      *
     C           *IN90     IFEQ *ON
     C                     MOVE '*ERROR ' P@RTCD
     C                     ENDIF
      *
      ** Open error
      *
     C           *IN91     IFEQ *ON
     C                     MOVE '*OPEN  ' P@RTCD
     C                     ENDIF
      *
      ** Close error
      *
     C           *IN92     IFEQ *ON
     C                     MOVE '*CLOSE ' P@RTCD
     C                     ENDIF
      *
      ** Empty file
      *
     C           *IN93     IFEQ *ON
     C                     MOVE '*EMPTY ' P@RTCD
     C                     ENDIF
      *
      ** No selection
      *
     C           *IN96     IFEQ *ON
     C                     MOVE '*OPTION' P@RTCD
     C                     ENDIF
      *
      ** Caller requests message sent to program queue:
      *
     C           W@RTCD    IFEQ '*MSG   '
     C           P@RTCD    ANDNE*BLANK
      *
     C           *IN90     IFEQ *ON
     C                     MOVE 'ACO1001' P@MGID
     C                     ENDIF
     C           *IN91     IFEQ *ON
     C                     MOVE 'ACO1002' P@MGID
     C                     ENDIF
     C           *IN92     IFEQ *ON
     C                     MOVE 'ACO1003' P@MGID
     C                     ENDIF
     C           *IN93     IFEQ *ON
     C                     MOVE 'ACO1004' P@MGID
     C                     ENDIF
     C           *IN96     IFEQ *ON
     C                     MOVE 'ACO1007' P@MGID
     C                     ENDIF
      *
     C                     CALL 'ZASNMG'
     C                     PARM *BLANK    W@RTCD  7
     C                     PARM PGNAME    P@PGNM 10
     C                     PARM '*PRV'    P@DEST  5
     C                     PARM           P@MGID  7
     C                     PARM *BLANK    P@MSGF 10
     C                     PARM *BLANK    P@MGKY  4
     C                     PARM           P@MGDA 50
     C                     PARM '*DIAG'   P@MGTP  7
     C                     ENDIF
      *
      ** Caller requests DB handling
      *
     C           W@RTCD    IFEQ '*DBERR'
     C           P@RTCD    ANDNE*BLANK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
      ** Return to caller:
      *
     C                     RETRN
      *
      *****************************************************
** CPY@
(c) Finastra International Limited 2001
** @MG
SDBAIIPD  AOBAIIR0  Cashier Interface Details
