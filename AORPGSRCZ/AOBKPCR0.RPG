     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Book-Profit Centre Matrix')
      *****************************************************************
      *                                                               *
      *  Midas - Access Object                                        *
      *                                                               *
      *  AOBKPCR0 - Read Access to Pseudo-Book                        *
      *                                                               *
      *  Function:  This program access the 2 Book-Profit Centre      *
      *             Matrix Logical Files                              *
      *                                                               *
      *             SDBKPCL1 - keyed on PSBKCD/PSPRFC fetches PSPSBK  *
      *             SDBKPCL3 - keyed on PSPSBK fetches PSBKCD/PSPRFC  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CAC005 *CREATE     Date 09Nov98               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAC005 - PCA-SE Enhancement                                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      * ID F  C  H  L     FUNCTION OF INDICATORS                      *
      *                                                               *
      *  01 - Open File SDBKPCL1                                      *
      *  02 - Open File SDBKPCL3                                      *
      *  91 - Open error                                              *
      *  92 - Close error                                             *
      *  95 - No Record Found                                         *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     FSDBKPCL1IF  E           K        DISK                           UC
     F            SDBKPCD0                          KRENAMESDBKPCD1
      ** Book Profit Centre Matrix File keyed on PSBKCD and PSPRFC
      *
     FSDBKPCL3IF  E           K        DISK                           UC
     F            SDBKPCD0                          KRENAMESDBKPCD3
      ** Book Profit Centre Matrix File keyed on PSPSBK
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80               Copyright     array
      *
      ** Array containing Copyright statement
      *
      /EJECT
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           PRTCD   7
     C                     PARM           POPTN   7
     C                     PARM           PMBKCD  2
     C                     PARM           PMPRFC  4
     C                     PARM           PMPSBK  2
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           KEY1      KLIST
     C                     KFLD           PMBKCD
     C                     KFLD           PMPRFC
      *
      ** First call - open files : IN01 = '0' if first pass on SDBKPCL1
      **                           IN02 = '0' if first pass on SDBKPCL3
      *
     C           POPTN     IFEQ '*PSEUDO'
     C           *IN01     ANDEQ'0'
     C                     MOVE '1'       *IN01
     C                     OPEN SDBKPCL1               91
      *
      ** Check if there is an Open file error
      *
     C           *IN91     IFEQ '1'
     C                     MOVE '*OPEN'   PRTCD
     C                     MOVE '1'       *INLR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           POPTN     IFEQ '*USER'
     C           *IN02     ANDEQ'0'
     C                     MOVE '1'       *IN02
     C                     OPEN SDBKPCL3
      *
     C           *IN91     IFEQ '1'
     C                     MOVE '*OPEN'   PRTCD
     C                     MOVE '1'       *INLR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Files are already opened, get Pseudo Book if option is Pseudo
      *
     C           POPTN     IFEQ '*PSEUDO'
     C           *IN01     ANDEQ'1'
     C           KEY1      CHAINSDBKPCL1             95
      *
      ** If record is found, move variables to the corresponding
      ** parameters.  If not, move blanks.
      *
     C           *IN95     IFEQ '0'
     C                     MOVE *BLANKS   PRTCD
     C                     MOVE PSPSBK    PMPSBK
     C                     ELSE
     C                     MOVE '*NRF'    PRTCD
     C                     MOVE *BLANKS   PMPSBK
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
      *
     C                     ENDIF
      *
      ** Files are already opened, get User Book and Profit Center
      ** if option is 'User'.
      *
     C           POPTN     IFEQ '*USER'
     C           *IN02     ANDEQ'1'
     C           PMPSBK    CHAINSDBKPCL3             95
      *
      ** If record is found, move variables to the corresponding
      ** parameters.  If not, move blanks.
      *
     C           *IN95     IFEQ '0'
     C                     MOVE *BLANKS   PRTCD
     C                     MOVE PSBKCD    PMBKCD
     C                     MOVE PSPRFC    PMPRFC
     C                     ELSE
     C                     MOVE '*NRF'    PRTCD
     C                     MOVE *BLANKS   PMBKCD
     C                     MOVE *BLANKS   PMPRFC
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
      *
     C                     ENDIF
      *
      ** Last call - close files and terminate program :
      *
     C           POPTN     IFEQ '*FREE  '
     C           *IN01     IFEQ '1'
     C                     CLOSESDBKPCL1               92
     C                     ENDIF
      *
      ** Check if there is a Close file error
      *
     C           *IN92     IFEQ '1'
     C                     MOVE '*CLOSE'  PRTCD
     C                     ENDIF
      *
     C           *IN02     IFEQ '1'
     C                     CLOSESDBKPCL3               92
     C                     ENDIF
      *
      ** Check if there is a Close file error
      *
     C           *IN92     IFEQ '1'
     C                     MOVE '*CLOSE'  PRTCD
     C                     ENDIF
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDIF
      *
      *
      * Return to caller :
      *
     C                     RETRN
      * End of main
**  CPY@
(c) Finastra International Limited 2001
