     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Historic base rates')
      *****************************************************************
      *                                                               *
      *  Midas - Access Objects Module                                *
      *                                                               *
      *  AOBSHSR0 - Historic Base Rates                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CMC001 *CREATE     Date 18MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMC001 - Management Accounts Enhancement for PCA:            *
      *           Created for Management Accounts.                    *
      *                                                               *
      *****************************************************************
     F*                                                                  *
     F*        D F C H L   FUNCTION INDICATOR                            *
     F*                                                                  *
     F*        01     INITIALISATION                                     *
     F*        90     FILE ERROR                                         *
     F*        91     FILE OPEN ERROR                                    *
     F*        92     FILE CLOSE ERROR                                   *
     F*        93     FILE EMPTY                                         *
     F*        94     END OF FILE                                        *
     F*        95     NO RECORD FOUND                                    *
     F*        96     OPTION ERROR                                       *
     F*        97     NO SELECTION                                       *
     F*        98     ERROR ON CALL OF SELECTION PROGRAM(SD0020S)        *
     F*                                                                  *
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDBSHSL1IF  E           K        DISK                           UC
      * IN  - SDBSHSD0 Historic Base Rates (CYCD/BSRC/HIDT)
      ************************************************************
     E*ESTART
      ****************************************************************
     E                    @MG     1   1 50
     E*
     E                    CPY@    1   1 80
      **  Array for Object Copyright Statement
      *
      ****************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     I*
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     ID@FMT     E DSSDBSHSPD
      * SDBSHSD0 - Historic Base Rates record format data structure
      *
     IP@FMT     E DSDSFDY
     I* External Data structure to hold the outgoing record format
     I* of the file.
      *
      ************************************************************
      /EJECT
     C*CSTART
      *
      **  Copyright Statement
      *
     C                     MOVEACPY@      CPY2@  80
      *
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@CCY   3        I:Key field
     C                     PARM           P@BSRC  20       I:Key field
     C                     PARM           P@DATE  50       I:Key field
     C                     PARM           P@FMT            O:Format
      *****************************************************
      *KLIST
     C           P@K1      KLIST
     C                     KFLD           P@CCY
     C                     KFLD           P@BSRC
      *
     C           P@K101    KLIST
     C                     KFLD           P@CCY
     C                     KFLD           P@BSRC
     C                     KFLD           P@DATE
      * Define Work Fields
     C           *LIKE     DEFN G0CBSR    W@CBSR
      *****************************************************
      *MAIN
     C           P@OPTN    IFEQ '*SAME   '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDBSHSL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSESDBSHSL1               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 3 FIRST REC
     C           P@K101    SETGTSDBSHSD0               90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           P@K1      REDPESDBSHSD0               9095  Read record
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
      *
     C           *IN95     IFEQ '1'                          No rec found
     C                     MOVE '0'       *IN95
     C           P@K101    SETLLSDBSHSD0               90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           P@K1      READESDBSHSD0               9095  Read record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 3
      *
     C                     END                             FI 3
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           P@K1      SETGTSDBSHSD0               90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C           P@K1      REDPESDBSHSD0               9095  Read record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     Z-ADDG0CBSR    W@CBSR
     C           G0CBSR    DOUNEW@CBSR                     DO Get New Rate
     C           *IN95     OREQ '1'
     C           P@K1      READESDBSHSD0               9095  Get record
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     ENDDO
      *
     C           *IN95     IFEQ '1'                          No rec found
     C                     MOVE '0'       *IN95
     C                     Z-ADD99999     G0HIDT
     C                     Z-ADDW@CBSR    G0CBSR
     C                     ENDIF
      *
     C                     ENDIF                           FI 4
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*SAME  '                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No rec found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
     C           W@RTCD    IFEQ '*DBERR'
     C           P@RTCD    ANDNE*BLANK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END
      *
     C           P@OPTN    IFNE '*FREE'
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDBSHSPD  AOBSHSR0  Historic Base Rates
** CPY@ - OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
