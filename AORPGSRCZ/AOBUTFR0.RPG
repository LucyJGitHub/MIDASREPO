     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO ED bulk transfer access object')
      *****************************************************************
      *                                                               *
      *  Midas  Access Objects                                        *
      *                                                               *
      *  AOBUTFR0 - Read Access to files available for                *
      *             bulk transfer                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 237033             Date 26May06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CED005 *CREATE     Date 11AUG97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  237033 - Apply 145111.                                       *
      *  145111 - DO NOT PRODUCE DUMP WHEN EXPORT DATA MODULE IS OFF. *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CED002 - Export Dta Enhancement for GIB                      *
      *                                                               *
      *****************************************************************
      /EJECT
      ************************************************************
      *
     FEDBUTFL1IF  E           K        DISK                           UC
      ** IN - Files available for bulk transfer
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  01 - No specific transfer request found on LF/EDBUTFL1       *
      *  02 - No general request found on LF/EDBUTFL1                 *
      *  03 - MIDAS EXPORT DATA MODULE IS ON.                         *   145111
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     E                    CPY@    1   1 80
      ** Copyright array
      *
      /EJECT
      ************************************************************
      *
     IED@FMT    E DSEDBUTFPD
      ** Drip feed files record format data structure
      *
     IP@FMT     E DSDSFDY
     I** External Data structure to hold the outgoing record format
     I** of the file.
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     IA@CPY       DS
     I** Copyright data structure
     I                                        1  80 CPY@
      *
     ISDMMOD    E DSSDMMODPD                                              145111
      **  MODULE DETAILS                                                  145111
      *                                                                   145111
      /EJECT
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
     C           *ENTRY    PLIST
     C                     PARM           P@FILE 10
     C                     PARM           P@MBR  10
     C                     PARM           P@RTCD  7
     C                     PARM           P@FMT
      *
      ** Copyright Statement
     C                     MOVEACPY@      ACT@   80
     C*                                                                   145111
     C** ACCESS MODULE DETAILS                                            145111
     C*                                                                   145111
     C                     CALL 'AOMMODR0'                                145111
     C                     PARM '*DBERR'  WRTCD   7                       145111
     C                     PARM '*FIRST'  WOPTN   7                       145111
     C           SDMMOD    PARM SDMMOD    P@FMT                           145111
      *                                                                   145111
      **  CHECK IF EXPORT DATA MODULE IS PRESENT IN THE SYSTEM.           145111
      *                                                                   145111
     C           BGNZST    IFEQ 'Y'                                       145111
     C                     MOVE '1'       *IN03                           145111
     C                     END                                            145111
      *
      ** Key list to access LF/EDBUTFL1
     C           KEY1      KLIST
     C                     KFLD           KFILE  20
      *
      ** Open file, setup key list values
     C                     OPEN EDBUTFL1
     C           P@FILE    CAT  P@MBR     KFILE
      *
      ** Check for specific transfer request (i.e. file and member)
     C           KEY1      CHAINEDBUTFL1             01
      *
      ** Specific request not found, check for general request of
      ** all members for requested file.
     C           *IN01     IFEQ '1'
     C                     MOVELP@FILE    KFILE     P
     C           KEY1      CHAINEDBUTFL1             02
     C                     ENDIF
      *
      ** No general request found, return *FAILED
     C           *IN02     IFEQ '1'
     C                     MOVEL'*FAILED' P@RTCD
     C                     MOVEL*BLANKS   P@FMT
     C                     CLOSEEDBUTFL1
     C                     SETON                     LR
     C           *IN03     IFEQ '1'                                       145111
     C                     DUMP
     C                     END                                            145111
     C                     RETRN
     C                     ENDIF
      *
      ** Record found, set return format to contents of record found
     C                     MOVELED@FMT    P@FMT
      *
      ** Close file, terminate program.
     C                     CLOSEEDBUTFL1
     C                     SETON                     LR
     C                     RETRN
      *
      **************************************************************************
** CPY@     : Copyright notice
(c) Misys International Banking Systems Ltd. 2001
