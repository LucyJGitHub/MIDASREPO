     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Safe Custody Group details')
     F*****************************************************************
     F*                                                               *
     F*  Midas  ACCESS OBJECTS                                *
     F*                                                               *
     F*  AOCFGRR0 - Read access to Safe Custody Group details         *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. S01464 *CREATE     DATE 04APR94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  S01464 - Safe Custody Fees.                                  *
     F*                                                               *
     F*****************************************************************
     FSECFGRL1IF  E           K        DISK                           UC
     F* IN  - SECFGRP0  Safe Custody Fees   (SBCFGC)
      /EJECT
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*  01  - Off on the first run                                   *
     F*  90  - File error                                             *
     F*  91  - Open error                                             *
     F*  92  - Close error                                            *
     F*  93  - Empty file                                             *
     F*  94  - End of file                                            *
     F*  95  - No record found                                        *
     F*  96  - Option error                                           *
     F*  97  - No selection                                           *
     F*  98  - Call error (SDMDSRR)                                   *
     F*  52  - Call error (ZASNMG)                                    *
     F*                                                               *
     F*****************************************************************
      /EJECT
      /TITLE E-SPECIFICATIONS
     E*
     E                    @MG     1   1 50
     E*
     E                    CPY@    1   1 80
     E*** Array for Object Copyright Statement
     E*
      /EJECT
     I*
     IA@CPY       DS
     I*** Copyright array
     I*
     I                                        1  80 CPY@
     I*
     IP@MGDA      DS
     I                                        1  50 @MG
     IPGMDS      SDS
     I*** Program Status data structure
     I*
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
     I*
     ID@FMT     E DSSECFGRPD
     I*** SECFGRP0 - Safe Custody Group details record dormat DS
     I*
     IP@FMT     E DSDSFDY
     I*
      /EJECT
     C*
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@CFGC  2        I:Key field
     C                     PARM           P@FMT            O:Format
     C*
     C*** Main processing
     C*
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
     C*
     C*** First call - open file :
     C*
     C           *IN01     IFEQ '0'                        IF 1 1'st time
     C                     SETON                     01      Set ind.
     C                     OPEN SECFGRL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
     C*
     C*** Last call - close file and terminate program :
     C*
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 Free
     C                     CLOSESECFGRL1               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 First rec
     C           *LOVAL    SETLLSECFGRP0             9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
     C*
     C*** Caller requests next record :
     C*
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 Next rec
     C                     READ SECFGRP0               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
     C*
     C*** Caller requests last record :
     C*
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 Last rec
     C           *HIVAL    SETGTSECFGRP0               90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
     C*
     C*** Caller requests previous record :
     C*
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 Prev rec
     C                     READPSECFGRP0               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
     C*
     C*** Caller requests record indicated by key :
     C*
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 Keyed
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           P@CFGC    CHAINSECFGRP0             9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
     C*
     C           P@OPTN    IFNE '*KEY   '                  IF 8 invalid
     C           P@OPTN    ANDNE'*NEXT  '                   parameter
     C           P@OPTN    ANDNE'*PREV  '
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
     C*
     C*** No record found or End of file - return error code :
     C*          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
     C*          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C*
     C*** Caller requests message sent to program queue :
     C*
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C                     CALL 'ZASNMG'                     <SND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
     C*
     C*** Caller Requests DB Error Handling
     C*
     C           W@RTCD    IFEQ '*DBERR'                   IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
     C*
     C           P@OPTN    IFNE '*VERIFY'
     C           P@OPTN    ANDNE'*FREE'
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
     C*
     C*** Return to caller :
     C*
     C                     RETRN
     C*
**
SECFGRPD  AOCFGRR0  Safe Custody Group
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
