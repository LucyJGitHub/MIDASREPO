     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Customer lending ICD access object')          *
      *****************************************************************
      *                                                               *
      *     Midas  ACCESS OBJECTS                                     *
      *                                                               *
      *     AOCLNDR0 - READ ACCESS TO CUSTOMER LENDING DETAILS        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAP084             Date 05Dec03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 123863             Date 17Oct97               *
      *                 CLE004             Date 02Apr97               *
      *                 S01465             DATE 17MAR94               *
      *                 S01230             DATE 23NOV89               *
      *                 S01230             DATE 06NOV89               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CAP084 - New ICD setting for Authorisation of Fee Adjustments*
      *           (Recompile)                                         *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CLE023 - Lending Facility History Improvements. (Recompiled) *
      *     123863 - Recompiled over changed SDCLNDPD.                *
      *     CLE004 - Lending Enhancements:                            *
      *              - Recompiled over changed SDBRCHPD.              *
      *     S01465 - BLI LENDING DEVELOPMENT:                         *
      *              - RECOMPILED OVER CHANGED SDCLNDPD (L1).         *
      *     S01230 - STANDARDISE ACCESS PROGRAMS                      *
      *                                                               *
      *****************************************************************
      *
      * ID F  C  H  L  FUNCTION OF INDICATORS
      *
      *       01       OFF ON THE FIRST RUN
      *       90       FILE ERROR
      *       91       OPEN ERROR
      *       92       CLOSE ERROR
      *       93       EMPTY FILE
      *       96       OPTION ERROR
      *
      /EJECT
      *FSTART
      ************************************************************
     FSDCLNDL1IF  E           K        DISK                           UC
      * IN  - @BPREER CUSTOMER LENDING DETAILS (B4CD)
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
      ************************************************************
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************        S01230
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I**********D@FMT     E DSSDCLNDD0                                    S01230
     ID@FMT     E DSSDCLNDPD                                              S01230
      * @BPREER - CUSTOMER LENDING RECORD FORMAT DATA STRUCTURE
      *
     I***P@FMT**   E DSDSFDY                                                           S01230 CLE134
     IP@FMT     E DSDSSDY                                                                     CLE134
     I* External Data structure to hold the outgoing record format        S01230
     I* of the file.                                                      S01230
      *                                                                   S01230
     I**********P@FMT       DS                                            S01230
     I**********                              1  46 P@DMMY                S01230
      *
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM D@FMT     P@FMT            O:Format
      *****************************************************
      *MAIN
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     9396   ¦ error
      *
     C           P@OPTN    IFEQ '*KEY   '
     C           P@OPTN    OREQ '*FIRST '
     C           P@OPTN    OREQ '*LAST  '
     C           P@OPTN    OREQ '*NEXT  '
     C           P@OPTN    OREQ '*PREV  '
     C           P@OPTN    OREQ '*FREE  '
     C           P@OPTN    OREQ '*VERIFY'
     C           P@OPTN    OREQ '*SAME  '
     C           *IN01     IFEQ '0'                          IF2
     C                     SETON                     01
     C                     OPEN SDCLNDL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C           *LOVAL    SETLL@BPREER              9390    Set file
     C  N93                READ @BPREER                  93
     C                     CLOSESDCLNDL1               92    Close file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                               FI2
     C                     ELSE
     C                     SETON                     96      Parm error
     C                     END
      *
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   96                MOVE '*OPTION' P@RTCD           No selection
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   96                MOVE 'ACO1007' P@MGID
     C                     CALL 'ZASNMG'                     <SND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA             I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      * Caller requests DB error handling
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF 10
     C           P@RTCD    ANDNE*BLANK                      AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI 10
      *
     C           P@OPTN    IFNE '*VERIFY'                  IF 11
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C           P@RTCD    ANDNE'*OPTION'                   AND
     C                     MOVELD@FMT     P@FMT
     C                     END                             FI 11
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDCLNDL1  AOCLNDR0  Customer Lending data
** CPY@     : Copyright notice for inclusion in all programs
(c) Finastra International Limited 2001
