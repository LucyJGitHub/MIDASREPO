     H        1                                                           S01230
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO SD Collateral Code Detail')
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOCOLLR0 - READ ACCESS TO SD COLLATERAL CODE DETAIL FILE     *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01314             Date 20Feb91               *
     F*                 S01230             Date 23/11/89              *S01230
     F*                    S01230             DATE 06/11/89              *S01230
     F*                    S01230             DATE 23/10/89              *S01230
     F*                                                                  *
     F********************************************************************S01230
     F*                                                                  *S01230
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*     S01314  -  CHANGES MADE DURING R10 PTF INCORPORATION         *S01230
     F*     S01230  -  STANDARDISE ACCESS PROGRAMS                       *S01230
     F*                                                                  *S01230
     F********************************************************************
     F*
     F* ID F  C  H  L     FUNCTION OF INDICATORS
     F*
     F*       01       OPEN FILE - TABT602
     F*       90       FILE ERROR
     F*       91       OPEN ERROR
     F*       92       CLOSE ERROR
     F*       93       EMPTY FILE
     F*       94       END OF FILE
     F*       95       NO RECORD FOUND
     F*       96       INVALID OPTION
     F*       99       ERROR ON CALL ZASNMG                              *S01230
     F*
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FTABT602 IF  E           K        DISK                           UC
      * IN  - TABT602F Collateral Code(CLTP)
      ************************************************************
      /EJECT                                                              S01230
     E*ESTART                                                             S01230
      ************************************************************        S01230
     E                    @MG     1   1 50                                S01230
      ************************************************************        S01230
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
     E********************************************************************S01230
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS                                                      S01230
     I                                        1  50 @MG                   S01230
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I*************************P@BTRF    E DSTABT602                      S01230
     ID@FMT     E DSTABT602                                               S01230
      * TABT602F Collateral Code Detail record format data structure
      *
     IP@FMT     E DSDSFDY                                                 S01230
     I*                                                                   S01230
     I* EXTERNAL DATA STRUCTURE TO HOLD THE OUTGOING RECORD FORMAT OF     S01230
     I* THE FILE                                                          S01230
     I******P@FMT       DS                             96                 S01230
      * INTERNAL - COLLATERAL CODE RECORD FORMAT DATA STRUCTURE           S01230
      *                                                                   S01230
     IP@CLTP      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I                                        3   3 W@SL03
     I                                        4   4 W@SL04
     I                                        5   5 W@SL05
     I                                        6   6 W@SL06
     I                                        7   7 W@SL07
     I                                        8   8 W@SL08
      *
      ************************************************************
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
      ************************************************************        S01230
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@CLTP           I:Key field
     C*********************PARM           P@BTRF           O:Format       S01230
     C                     PARM           P@FMT            O:Format       S01230
      *****************************************************
      *MAIN
     C           P@OPTN    IFEQ '*SAME  '                                 S01230
     C           *IN01     COMP '0'                      91               S01230
     C                     GOTO T@SKIP                                    S01230
     C                     END                                            S01230
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C*********************SETOF                     9697   ¦ indicators  S01230
     C                     SETOF                     96     ¦ indicators  S01230
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN TABT602                91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PROG
     C                     CLOSETABT602                92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     RETRN                             Return
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLLTABT602F             9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ TABT602F               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGTTABT602F               90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READPTABT602F               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEY ACC
     C           P@OPTN    OREQ '*VERIFY'                   OR            S01230
     C           P@CLTP    CHAINTABT602F             9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND           S01230
     C           P@OPTN    ANDNE'*FREE  '                   AND           S01230
     C                     SETON                       96    Parameter err
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No rec found
     C   96                MOVE '*OPTION' P@RTCD           Invalid Option
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C***90                MOVE 'ACO0001' P@MGID             File error   S01314
     C***91                MOVE 'ACO0002' P@MGID             Open error   S01314
     C***92                MOVE 'ACO0003' P@MGID             Close error  S01314
     C***93                MOVE 'ACO0004' P@MGID             Empty file   S01314
     C***94                MOVE 'ACO0005' P@MGID             End of file  S01314
     C***95                MOVE 'ACO0006' P@MGID             No rec found S01314
     C***96                MOVE 'ACO0007' P@MGID             Option Error S01314
     C   90                MOVE 'ACO1001' P@MGID             File error   S01314
     C   91                MOVE 'ACO1002' P@MGID             Open error   S01314
     C   92                MOVE 'ACO1003' P@MGID             Close error  S01314
     C   93                MOVE 'ACO1004' P@MGID             Empty file   S01314
     C   94                MOVE 'ACO1005' P@MGID             End of file  S01314
     C   95                MOVE 'ACO1006' P@MGID             No rec found S01314
     C   96                MOVE 'ACO1007' P@MGID             Option Error S01314
     C                     CALL 'ZASNMG'               99    <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C*********************PARM *BLANK    P@MGDA 50          I:Msg data   S01230
     C                     PARM           P@MGDA 50          I:Msg data   S01230
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *                                                                   S01230
      * Caller Requests DB Error Handling                                 S01230
      *                                                                   S01230
     C           W@RTCD    IFEQ '*DBERR'                   IF             S01230
     C           P@RTCD    ANDNE*BLANK                       AND          S01230
     C                     DUMP                                           S01230
     C                     CALL 'DBERRCTL'                                S01230
     C                     END                             FI             S01230
      *                                                                   S01230
     C           P@OPTN    IFNE '*VERIFY'                                 S01230
     C           P@OPTN    ANDNE'*FREE'                                   S01230
     C           P@RTCD    ANDNE'*OPTION'                                 S01230
     C                     MOVELD@FMT     P@FMT                           S01230
     C                     END                                            S01230
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**                                                                 E01168 S01230
TABT602   AOCOLLR0  SD Collateral Code Detail
** CPY@     : Copyright notice for inclusion in all programs           grams
(c) Finastra International Limited 2001
