     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Collateral location codes access object')
      *****************************************************************
      *                                                               *
      *  Midas - Access Object Module                                 *
      *                                                               *
      *  AOCOLOR0 - Collateral Location Codes Access Object Program   *
      *                                                               *
      *  Function:  This program was created for other programs to    *
      *             have access to the Collateral Location Codes File.*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
     FSDCOLOL2IF  E           K        DISK                           UC
      *****************************************************************
      *                                                               *
      *                   Function of Indicators                      *
      *                                                               *
      *  01       Open File                                           *
      *  90       File Error                                          *
      *  91       Open Error                                          *
      *  92       Close Error                                         *
      *  93       Empty File                                          *
      *  94       End of File                                         *
      *  95       No Record Found                                     *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      *****************************************************************
      /EJECT
      *****************************************************************
     ICPYR@#      DS
      ** Data structure for copyright insertion on compilation
      *
     I                                        1  80 CPY@
     IPGMDS      SDS
      ** Program status data structure
      *
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
     ID@RFMT    E DSSDCOLOPD
      ** File information data structure
      *
     IP@RFMT    E DSDSSDY
      ** External data structure to hold the outgoing record format
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Receive entry parameters
      *
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7
     C                     PARM           P@OPTN  7
     C                     PARM           P@COLO  3
     C                     PARM           P@RFMT
      *
      ** Initialise error indicators
      *
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN94
     C                     MOVE *OFF      *IN95
      *
      ** If option is *SAME
      *
     C           P@OPTN    IFEQ '*SAME  '
      *
      ** If file has been opened
      *
     C           *IN01     IFEQ *ON
     C                     MOVE '*OPEN  ' P@RTCD
     C                     MOVELD@RFMT    P@RFMT
     C                     GOTO SKIP
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Open file
      *
     C           *IN01     IFEQ *OFF
     C                     OPEN SDCOLOL2               91
     C                     MOVE *ON       *IN01
     C                     ENDIF
      *
      ** If error in opening file
      *
     C           *IN91     IFEQ *ON
     C                     MOVE '*OPEN  ' P@RTCD
     C                     GOTO SKIP
     C                     ENDIF
      *
      ** If option is *FREE
      *
     C           P@OPTN    IFEQ '*FREE  '
     C                     CLOSESDCOLOL2               92
      *
      ** If error in closing file
      *
     C           *IN92     IFEQ *ON
     C                     MOVE '*CLOSE ' P@RTCD
     C                     ENDIF
      *
     C                     GOTO SKIP
     C                     ENDIF
      *
      ** If option is *FIRST
      *
     C           P@OPTN    IFEQ '*FIRST '
     C           *LOVAL    SETLLSDCOLOL2             9390
      *
      ** Error in accessing file
      *
     C           *IN90     IFEQ *ON
     C                     MOVE '*ERROR ' P@RTCD
     C                     GOTO SKIP
     C                     ENDIF
      *
      ** File contains no record
      *
     C           *IN93     IFEQ *ON
     C                     MOVE '*EMPTY ' P@RTCD
     C                     GOTO SKIP
     C                     ENDIF
      *
     C                     MOVE '*NEXT  ' P@OPTN
     C                     ENDIF
      *
      ** If option is *NEXT
      *
     C           P@OPTN    IFEQ '*NEXT  '
     C                     READ SDCOLOL2                 94
      *
      ** If EOF
      *
     C           *IN94     IFEQ *ON
     C                     MOVE '*EOF   ' P@RTCD
     C                     ENDIF
      *
     C                     MOVELD@RFMT    P@RFMT
     C                     GOTO SKIP
     C                     ENDIF
      *
      ** If option is *LAST
      *
     C           P@OPTN    IFEQ '*LAST  '
     C           *HIVAL    SETGTSDCOLOL2
     C                     MOVE '*PREV  ' P@OPTN
     C                     ENDIF
      *
      ** If option is *PREV
      *
     C           P@OPTN    IFEQ '*PREV  '
     C                     READPSDCOLOL2               9094
      *
      ** If EOF
      *
     C           *IN94     IFEQ *ON
     C                     MOVE '*EOF   ' P@RTCD
     C                     ENDIF
      *
      ** If error occurs
      *
     C           *IN90     IFEQ *ON
     C                     MOVE '*ERROR ' P@RTCD
     C                     ENDIF
      *
     C                     MOVELD@RFMT    P@RFMT
     C                     GOTO SKIP
     C                     ENDIF
      *
      ** If option is *KEY or *VERIFY
      *
     C           P@OPTN    IFEQ '*KEY   '
     C           P@OPTN    OREQ '*VERIFY'
      *
     C           P@COLO    IFEQ '?'
     C                     CALL 'SD0731'
     C                     PARM *BLANKS   RCODE   7
     C                     PARM '?'       MODE    1
     C                     PARM *BLANKS   KEY     3
      *
     C           RCODE     IFNE *BLANKS
     C                     MOVE '*CALL  ' P@RTCD
     C                     GOTO SKIP
     C                     ELSE
     C           KEY       IFEQ *BLANKS
     C                     MOVE '*NOSEL ' P@RTCD
     C                     GOTO SKIP
     C                     ELSE
     C                     MOVE KEY       P@COLO
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           P@COLO    CHAINSDCOLOL2             95
      *
      ** If no record found
      *
     C           *IN95     IFEQ *ON
     C                     MOVE '*NRF   ' P@RTCD
     C                     ENDIF
      *
     C                     MOVELD@RFMT    P@RFMT
     C                     GOTO SKIP
     C                     ENDIF
      *
      ** If parameter error
      *
     C           P@OPTN    IFNE '*KEY   '
     C           P@OPTN    ANDNE'*NEXT  '
     C           P@OPTN    ANDNE'*PREV  '
     C           P@OPTN    ANDNE'*VERIFY'
     C           P@OPTN    ANDNE'*FREE  '
     C                     MOVE '*OPTION' P@RTCD
     C                     ENDIF
      *
     C           SKIP      TAG
      *
      ** Caller requests message sent to program queue
      *
     C           W@RTCD    IFEQ '*MSG   '
     C           P@RTCD    ANDNE*BLANK
      *
     C           *IN90     IFEQ *ON
     C                     MOVE 'ACO1001' P@MGID
     C                     ENDIF
      *
     C           *IN91     IFEQ *ON
     C                     MOVE 'ACO1002' P@MGID
     C                     ENDIF
      *
     C           *IN92     IFEQ *ON
     C                     MOVE 'ACO1003' P@MGID
     C                     ENDIF
      *
     C           *IN93     IFEQ *ON
     C                     MOVE 'ACO1004' P@MGID
     C                     ENDIF
      *
     C           *IN94     IFEQ *ON
     C                     MOVE 'ACO1005' P@MGID
     C                     ENDIF
      *
     C           *IN95     IFEQ *ON
     C                     MOVE 'ACO1006' P@MGID
     C                     ENDIF
      *
     C                     CALL 'ZASNMG'
     C                     PARM *BLANK    W@RTCD  7
     C                     PARM PGNAME    P@PGNM 10
     C                     PARM '*PRV'    P@DEST  5
     C                     PARM           P@MGID  7
     C                     PARM *BLANK    P@MSGF 10
     C                     PARM *BLANK    P@MGKY  4
     C                     PARM           P@MGDA 50
     C                     PARM '*DIAG'   P@MGTP  7
      *
     C                     ENDIF
      *
      ** Caller Requests DB Error Handling
      *
     C           W@RTCD    IFEQ '*DBERR '
     C           P@RTCD    ANDNE*BLANK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
     C                     RETRN
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
