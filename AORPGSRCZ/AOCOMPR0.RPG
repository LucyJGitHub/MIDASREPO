     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Company Codes')
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOCOMPR0 - COMPANY  CODES                                    *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01461             Date 11Feb94               *
      *                 S01230             Date 23/11/89              *
     F*                    S01230             DATE 06/11/89              *S01230
     F*                                                                  *
     F********************************************************************
      *  MD046248 - Finastra Rebranding                               *
     F*     S01461 - RECOMPILED OVER CHANGED SDCOMPPD.                   *
     F*     S01230 - STANDARDISE ACCESS PROGRAMS                         *S01230
     F********************************************************************S01230
     F*                                                                  *
     F*        D F C H L   FUNCTION INDICATOR                            *
     F*                                                                  *
     F*        01     INITIALISATION                                     *
     F*        90     FILE ERROR                                         *
     F*        91     FILE OPEN ERROR                                    *
     F*        92     FILE CLOSE ERROR                                   *
     F*        93     FILE EMPTY                                         *
     F*        94     END OF FILE                                        *
     F*        95     NO RECORD FOUND                                    *
     F*        96     OPTION ERROR                                       *
     F*        97     NO SELECTION                                       *
     F*        98     ERROR ON CALL OF SELECTION PROGRAM(SDCASRR)        *
     F*                                                                  *
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDCOMPL0IF  E           K        DISK                           UC
      * IN  - @APREB3 Companies        (CMCD)
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
      ************************************************************
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************        S01230
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I***********D@FMT     E DSSDCOMPD0                                   S01230
     ID@FMT     E DSSDCOMPPD                                              S01230
      * @APREB3 - Account code record format data structure
      *
     IP@FMT     E DSDSFDY                                                 S01230
     I* External Data structure to hold the outgoing record format        S01230
     I* the file.                                                         S01230
      *                                                                   S01230
     I*********P@FMT       DS                                             S01230
     I*********                               1  36 P@DMMY                S01230
      *
     IP@CMCD      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I                                        3   3 W@SL03
      *
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C*                    PARM           P@CMCD  2        I:Company code E01168
     C**                   PARM           P@CMCD           I:Company code E01168
     C                     PARM           P@CMCD  3        I:Company code E01168
     C                     PARM           P@FMT            O:Format
      *****************************************************
      *MAIN
      *
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDCOMPL0               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSESDCOMPL0               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLL@APREB3              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ @APREB3                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGT@APREB3                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READP@APREB3                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEY ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           W@SL01    IFEQ '?'                        IF 8 SELECT
     C           W@SL02    OREQ '?'                         OR
     C           W@SL03    OREQ '?'                         OR
     C*********************CALL 'SDCASRR'              98    <SELECT>     S01230
     C                     CALL 'SD0230S'              98    <SELECT>     S01230
     C                     PARM *BLANK    P@RTC2  7          B:Return code
     C                     PARM *BLANK    P@CMCD             B:Key
     C           P@RTC2    COMP *BLANK               97      No selection
     C           *IN97     CABEQ'1'       T@SKIP             No selection
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C                     END                             FI 8
     C           P@CMCD    CHAIN@APREB3              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA             I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      * Caller requests DB error handling
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF 10
     C           P@RTCD    ANDNE*BLANK                      AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI 10
      *
     C           P@OPTN    IFNE '*VERIFY'                  IF 11
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C           P@RTCD    ANDNE'*OPTION'                   AND
     C                     MOVELD@FMT     P@FMT
     C                     END
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDCOMPPD  AOCOMPR0  Company codes
** CPY@     : Copyright notice for inclusion in all programs
(c) Finastra International Limited 2001
