     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Country Codes')                               *
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOCTRYR0 - READ ACCESS TO COUNTRY CODES FILE                 *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                         *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL132             Date 01May13               *
      *                 CER059             Date 19Jul10               *
      *                 CER045             Date 20Jul09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ---------------------------------*
      *                 BUG12458           Date 07Nov06                  *
      *                 CSD031             Date 10Apr06                  *
      *                 CGL031             Date 05Jul04                  *
      *                 BUG3644            Date 12Jul04                  *
      * Midas Release 4 --------------- Base ----------------------------*
      * Midas DBA 3.02 --------------------------------------------------*
      *                 CPB001             Date 10Jun99                  *
      *                 CFT004             Date 19Jul99                  *
      * Midas DBA 3.00 ---------------- Base ----------------------------*
     F*                    S01461             DATE 11FEB94               *
     F*                    S01230             DATE 23/11/89              *S01230
     F*                    S01230             DATE 06/11/89              *S01230
     F*                    E01168             DATE 22/02/89              *S01230
     F*                                                                  *
     F********************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CGL132 - FATCA Phase 1 - Customers' Identification and       *
      *           Classification - Master (Recompile)                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank (Recompile)     *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  BUG12458 - COB component DLC0607 00001 failed producing a    *
      *             database error in DL0400AU report                 *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  CGL031 - Taxation of Savings Income (Recompiled)             *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
     F*     CPB001 - 'Private Banking' Module                            *
     F*              - Recompiled over changed SDCTRYPD.                 *
     F*     CFT004 - Straight Through Processing Phase 2  - Recompiled*
     F*               International Bank Account Numbers (IBAN)       *
     F*     S01461 - RECOMPILED OVER CHANGED SDCTRYPD.                   *
     F*     S01230 - STANDARDISE ACCESS PROGRAM & COPYRIGHT ADDED        *S01230
     F********************************************************************S01230
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       01      OFF 1ST TIME ROUND
     F*       90      FILE ERROR
     F*       91      FILE OPEN ERROR                                    *
     F*       92      ERROR ON FILE CLOSE                                *
     F*       93      FILE EMPTY
     F*       94      EOF
     F*       95      NO RECORD FOUND
     F*       96      PARAMETER ERROR
     F*       97      IF NO SELECTION
     F*       98      ERROR ON PROGRAM CALL (SDFPSRB)
     F*       LR      LAST RECORD
     F*
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDCTRYL1IF  E           K        DISK                           UC
      * IN  - @A4REBR REGION codes    (CNCD)
     F*GPCTRYL1IF  E           K        DISK                           UC            CSD031 BUG12458
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
     E*                                                                   S01230
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
      * Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*                                                                   S01230
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I***********D@FMT     E DSSDCTRYD0                                   S01230
     ID@FMT     E DSSDCTRYPD                                              S01230
      * @A4REBR - Region code record format data structure
      *
     I*P@FMT       DS                             98                      E01168
     I*                                      98  98 P@DMMY                E01168
     I*SCSARD*** E DSSCSARDPD                                                        CSD031 BUG12458
      ** Switchable Features file                                                             CSD031
      *                                                                                       CSD031
     IP@FMT     E DSDSFDY                                                 S01230
     I* External Data structure to hold the outgoing record format        S01230
     I* of the file.                                                      S01230
      *                                                                   S01230
     I*********P@FMT       DS                             40              S01230
     I*********                              40  40 P@DMMY                S01230
      *
     IP@CNCD      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
      *
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@CNCD  2        I:Key field
     C                     PARM           P@FMT            O:Format
      *****************************************************
      *MAIN
      *
      * Access Option *SAME input
      *
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *                                                                                       CSD031
      ** Check if Enhanced Centralised Static Data is installed                               CSD031
      *                                                                                       CSD031
     C**********           CALL 'AOSARDR0'                                           CSD031 BUG12458
     C**********           PARM *BLANKS   PRTCD   7                                  CSD031 BUG12458
     C**********           PARM '*VERIFY' POPTN   7                                  CSD031 BUG12458
     C**********           PARM 'CSD031'  PSAR    6                                  CSD031 BUG12458
     C********** SCSARD    PARM SCSARD    P@FMT                                      CSD031 BUG12458
      *                                                                              CSD031 BUG12458
     C********** PRTCD     IFEQ *BLANKS                                              CSD031 BUG12458
     C**********           MOVE 'Y'       CSD031  1                                  CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C**********           MOVE 'N'       CSD031                                     CSD031 BUG12458
     C**********           ENDIF                                                     CSD031 BUG12458
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C********** CSD031    IFEQ 'Y'                                                  CSD031 BUG12458
     C********** *INU1     ANDEQ*ON                                                  CSD031 BUG12458
     C**********           OPEN GPCTRYL1               91                            CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C                     OPEN SDCTRYL1               91    Open file
     C**********           ENDIF                                                     CSD031 BUG12458
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C********** CSD031    IFEQ 'Y'                                                  CSD031 BUG12458
     C********** *INU1     ANDEQ*ON                                                  CSD031 BUG12458
     C**********           CLOSEGPCTRYL1               92                            CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C                     CLOSESDCTRYL1               92    Close file
     C**********           ENDIF                                                     CSD031 BUG12458
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C********** CSD031    IFEQ 'Y'                                                  CSD031 BUG12458
     C********** *INU1     ANDEQ*ON                                                  CSD031 BUG12458
     C********** *LOVAL    SETLLGPCTRYD0             9390                            CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C           *LOVAL    SETLL@A4REBR              9390    Set file
     C**********           ENDIF                                                     CSD031 BUG12458
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C********** CSD031    IFEQ 'Y'                                                  CSD031 BUG12458
     C********** *INU1     ANDEQ*ON                                                  CSD031 BUG12458
     C**********           READ GPCTRYD0               9094                          CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C                     READ @A4REBR                9094  Read record
     C**********           ENDIF                                                     CSD031 BUG12458
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C********** CSD031    IFEQ 'Y'                                                  CSD031 BUG12458
     C********** *INU1     ANDEQ*ON                                                  CSD031 BUG12458
     C********** *HIVAL    SETGTGPCTRYD0               90                            CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C           *HIVAL    SETGT@A4REBR                90    Set file
     C**********           ENDIF                                                     CSD031 BUG12458
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C********** CSD031    IFEQ 'Y'                                                  CSD031 BUG12458
     C********** *INU1     ANDEQ*ON                                                  CSD031 BUG12458
     C**********           READPGPCTRYD0               9094                          CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C                     READP@A4REBR                9094  Read record
     C**********           ENDIF                                                     CSD031 BUG12458
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEYED ACC
     C           P@OPTN    OREQ '*VERIFY'                  OR
     C           W@SL01    IFEQ '?'                        IF 8 SELECT
     C           W@SL02    OREQ '?'                         OR
     C*********************CALL 'SDFPSRR'              98    <SELECT>     S01230
     C                     CALL 'SD0181S'              98    <SELECT>     S01230
     C                     PARM *BLANK    P@RTC2  7          B:Return code
     C                     PARM *BLANK    P@CNCD             B:Key
     C           P@RTC2    COMP *BLANK               97      No selection
     C           *IN97     CABEQ'1'       T@SKIP             No selection
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C                     END                             FI 8
     C********** CSD031    IFEQ 'Y'                                                  CSD031 BUG12458
     C********** *INU1     ANDEQ*ON                                                  CSD031 BUG12458
     C********** P@CNCD    CHAINGPCTRYD0             9590                            CSD031 BUG12458
     C**********           ELSE                                                      CSD031 BUG12458
     C           P@CNCD    CHAIN@A4REBR              9590    Get record
     C**********           ENDIF                                                     CSD031 BUG12458
     C           *IN95     CABEQ'1'       T@SKIP             No record fnd
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parameter err
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No record fnd
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Program nm
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Message fle
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          I:Message dta
     C                     PARM '*DIAG'   P@MGTP  7          I:Message tpe
     C                     END                             FI 9
      *
      * Caller Requests DB Error Handling
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
     C           P@OPTN    IFNE '*VERIFY'                  IF 13
     C           P@OPTN    ANDNE'*FREE  '                    AND
     C           P@RTCD    ANDNE'*OPTION'                    AND
     C                     MOVELD@FMT     P@FMT            FI 13
     C                     END
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDCTRYPD  AOCTRYR0  Country Codes
** CPY@     : Copyright notice for inclusion in all programs              S01230
(c) Finastra International Limited 2001
