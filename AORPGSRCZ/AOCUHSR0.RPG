     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Historic exchange rates')
     F*****************************************************************
     F*                                                               *
     F*  Midas  ACCESS OBJECTS
     F*                                                               *
     F*  AOCUHSR0 - HISTORIC EXCHANGE RATES                           *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. 074066             DATE 28JUN96               *
     F*  PREV AMEND NO. S10736             DATE 20NOV92               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  074066 - DB error if there is no historic spot rate prior to *
     F*           transaction date, e.g. if transaction is backvalued.*
     F*           Fix is to get following spot rate by doing READP,   *
     F*           as file is keyed by descending historic spot date.  *
     F*  S10736 - CREATED FOR MANAGEMENT ACCOUNTING.                  *
     F*                                                               *
     F*****************************************************************
     F*                                                                  *
     F*        D F C H L   FUNCTION INDICATOR                            *
     F*                                                                  *
     F*        01     INITIALISATION                                     *
     F*        90     FILE ERROR                                         *
     F*        91     FILE OPEN ERROR                                    *
     F*        92     FILE CLOSE ERROR                                   *
     F*        93     FILE EMPTY                                         *
     F*        94     END OF FILE                                        *
     F*        95     NO RECORD FOUND                                    *
     F*        96     OPTION ERROR                                       *
     F*        97     NO SELECTION                                       *
     F*        98     ERROR ON CALL OF SELECTION PROGRAM(SD0020S)        *
     F*                                                                  *
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDCUHSL2IF  E           K        DISK                           UC
      * IN  - SDCUHSD0 Historic Exchange Rates (HIDT/CYCD)
      ************************************************************
     E*ESTART
      ****************************************************************
     E                    @MG     1   1 50
     E*
     E* Description : Copyright notice for inclusion in all programs
     E********************************************************************
     E                    CPY@    1   1 80               Copyright     arr
      ****************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     I* Description : Copyright notice for inclusion in all programs
     I*
     IA@CPY       DS
      * Copyright array
     I                                        1  80 CPY@
     I*
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     ID@FMT     E DSSDCUHSPD
      * SDCUHSD0 - Historic Exchange Rates record format data structure
      *
     IP@FMT     E DSDSSDY
     I* External Data structure to hold the outgoing record format
     I* of the file.
      *
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@DATE  50       I:Key field
     C                     PARM           P@CCY   3        I:Key field
     C                     PARM           P@FMT            O:Format
      *****************************************************
      *KLIST
     C           P@K1      KLIST
     C                     KFLD           P@CCY
      *
     C           P@K101    KLIST
     C                     KFLD           P@CCY
     C                     KFLD           P@DATE
      *****************************************************
      *MAIN
     C           P@OPTN    IFEQ '*SAME   '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDCUHSL2               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSESDCUHSL2               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLLSDCUHSD0             9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ SDCUHSD0               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGTSDCUHSD0               90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READPSDCUHSD0               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEY ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR         ESS
     C           P@K101    SETLLSDCUHSD0             9590    Get record
     C************IN95     CABEQ'1'       T@SKIP             No rec found 074066
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C***********P@K1      READESDCUHSD0               9590  Get record   074066
     C************IN95     CABEQ'1'       T@SKIP             No rec found 074066
     C           *IN95     IFEQ '0'                          Rec found    074066
     C           P@K1      READESDCUHSD0               9095  Get record   074066
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                                            074066
     C           *IN95     IFEQ '1'                          No rec found 074066
     C           P@K101    SETGTSDCUHSD0             9590    Set Pointer  074066
     C           *IN90     CABEQ'1'       T@SKIP             File error   074066
     C           P@K1      REDPESDCUHSD0               9095  Get record   074066
     C           *IN95     CABEQ'1'       T@SKIP             No rec found 074066
     C           *IN90     CABEQ'1'       T@SKIP             File error   074066
     C                     END                                            074066
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*SAME  '                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No rec found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
     C           W@RTCD    IFEQ '*DBERR'
     C           P@RTCD    ANDNE*BLANK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END
      *
      *
      *
     C           P@OPTN    IFNE '*VERIFY'
     C           P@OPTN    ANDNE'*FREE'
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDCUHSPD  AOCUHSR0  Historic Exchange Rates
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
