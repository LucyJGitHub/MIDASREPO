     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Extended settlement access object (2000)')    *
      *****************************************************************
      *                                                               *
      *  Midas - Access Objects Module                                *
      *                                                               *
      *  AOEXSTR1 - Read Access to Extended Settlement (2000 Char)    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSD095             Date 08Apr14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CDL031             Date 05Dec04               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 149721 *CREATE     Date 09Jul99               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD095 - Allow Deal Sub-Type and Branch for MM and FX SSIs   *
      *           (Recompile)                                         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CDL031 - Effective Date on Extended SSI (Recompile)          *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  149721 - New program with extended dat structure.            *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L    FUNCTION OF INDICATORS                       *
      *                                                               *
      *       01      OFF 1ST TIME ROUND                              *
      *       90      FILE ERROR                                      *
      *       91      FILE OPEN ERROR                                 *
      *       92      ERROR ON FILE CLOSE                             *
      *       93      FILE EMPTY                                      *
      *       94      EOF                                             *
      *       95      NO RECORD FOUND                                 *
      *       96      PARAMETER ERROR                                 *
      *       97      IF NO SELECTION                                 *
      *       98      ERROR ON PROGRAM CALL (???????)                 *
      *       LR      LAST RECORD                                     *
      *                                                               *
      *****************************************************************
      /EJECT
      *FSTART
      ************************************************************
     FSDEXSTL1IF  E           K        DISK                           UC
      * IN  - @BYREFY
      ************************************************************
      /EJECT
      *ESTART
      ************************************************************
     E                    @MG     1   1 50
      ************************************************************
      * Description     : Copyright notice for inclusion in all program
      *
      ********************************************************************
     E                    CPY@    1   1 80               Copyright
      ********************************************************************
      /EJECT
      *ISTART
      ************************************************************
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     ID@FMT     E DSSDEXSTPD
      * @BYREFY - EXT.SETcode record format data structure
      *
     IP@FMT     E DSDS2000DY
      *
      * EXTERNAL DATA STRUCTURE TO HOLD THE OUTGOING RECORD FORMAT OF
     I* THE FILE
      *
     IP@CYCD      DS
      * Currency code
      *
     I                                        1   3 K@CYCD
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I                                        3   3 W@SL03
     IP@CUST      DS
      * Customer number
      *
     I                                        1   6 K@CUST
     I                                        1   1 W@SL04
     I                                        2   2 W@SL05
     I                                        3   3 W@SL06
     I                                        4   4 W@SL07
     I                                        5   5 W@SL08
     I                                        6   6 W@SL09
     IP@TRTY      DS
      * Transaction type
      *
     I                                        1   2 K@TRTY
     I                                        1   1 W@SL10
     I                                        2   2 W@SL11
      *
      ************************************************************
      *
      * Description     : Copyright notice for inclusion in all programs
      *
     IA@CPY       DS
      * Copyright array
     I                                        1  80 CPY@
      ************************************************************
      /EJECT
      *CSTART
      ************************************************************
      *
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@CYCD           I:CCY CODE
     C                     PARM           P@CUST           I:CUST NO.
     C                     PARM           P@TRTY           I:TRANS. TYPE
     C                     PARM           P@FMT            O:Format
      ************************************************************
     C           K@KEY     KLIST
     C                     KFLD           K@CYCD
     C                     KFLD           K@CUST
     C                     KFLD           K@TRTY
      *
      *MAIN
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
     C                     SETOF                     505152 ¦
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDEXSTL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PROGR
     C                     CLOSESDEXSTL1               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST RECORD
     C           *LOVAL    SETLL@BYREFY              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT RECORD
     C                     READ @BYREFY                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST RECORD
     C           *HIVAL    SETGT@BYREFY                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.RECORD
     C                     READP@BYREFY                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEYED ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           W@SL01    IFEQ '?'                        IF 8 SELECT
     C           W@SL02    OREQ '?'                         OR
     C           W@SL03    OREQ '?'                         OR
     C                     SETON                     50
     C                     MOVE *BLANK    P@CYCD
     C                     END                              FI 8
     C           W@SL04    IFEQ '?'                         IF 9
     C           W@SL05    OREQ '?'                           OR
     C           W@SL06    OREQ '?'                          OR
     C           W@SL07    OREQ '?'                          OR
     C           W@SL08    OREQ '?'                          OR
     C           W@SL09    OREQ '?'                          OR
     C                     SETON                     51
     C                     MOVE *BLANK    P@CUST
     C                     END                              FI 9
     C           W@SL10    IFEQ '?'                         IF 10
     C           W@SL11    OREQ '?'                           OR
     C                     SETON                     52
     C                     MOVE *BLANK    P@TRTY            FI 10
     C                     END
     C           *IN50     IFEQ '1'                         IF 11
     C           *IN51     OREQ '1'                            OR
     C           *IN52     OREQ '1'                            OR
     C                     CALL 'SD0400S'              98    <SELECT>
     C                     PARM *BLANK    P@RTC2  7          B:Return code
     C                     PARM           P@CYCD             B:Key
     C                     PARM           P@CUST             B:Key
     C                     PARM           P@TRTY             B:Key
     C           P@RTC2    COMP *BLANK               97      No selection
     C           *IN97     CABEQ'1'       T@SKIP             No selection
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C                     END                             FI 11
     C           K@KEY     CHAIN@BYREFY              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No record found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 12 NOTKEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C                     SETON                       96    Parameter error
     C                     END                             FI 12
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MESSAGE
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No record found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MESSAGE>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Program name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Message file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          O:Message key
     C                     PARM '*DIAG'   P@MGTP  7          I:Message type
     C                     END                             FI 9
      *
      * Caller requests DB error handling
      *
     C           W@RTCD    IFEQ '*DBERR'                   IF 12
     C           P@RTCD    ANDNE*BLANK                      AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI 12
      *
     C           P@OPTN    IFNE '*VERIFY'                  IF 13
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C           P@RTCD    ANDNE'*OPTION'                   AND
     C                     MOVELD@FMT     P@FMT
     C                     END                             FI 13
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDEXSTPD  AOEXSTR1  Extended Settlements
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
