     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO FF instrument types details')                 *
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOFFITR0 - READ ACCESS TO FF INSTRUMENT TYPES                *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      *  Last Amend No. CAP208             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 196826             Date 27Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 CFF004             Date 02Oct96               *S01230
     F*                 S01230             Date 23/11/89                 *S01230
     F*                    S01230              DATE 06/11/89             *S01230
     F*                    S01230              DATE 24/10/89             *S01230
     F*                                                                  *
     F********************************************************************
      *  CAP208 - F&O Market Instrument API (Recompile)               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  196826 - Validate if record found is still active or flagged    *
      *           as deleted.                                            *
     F*     CFF004 - Increase in size of Price Fields,(Recompiled Only)  *
     F*     S01230 - STANDARDISE ACCESS PROGRAM & COPYRIGHT ADDED        *S01230
     F********************************************************************S01230
     F* ID F  C  H  L  FUNCTION OF INDICATORS
     F*
     F*       01       OFF ON THE FIRST RUN
     F*       90       FILE ERROR
     F*       91       OPEN ERROR
     F*       92       CLOSE ERROR
     F*       93       EMPTY FILE
     F*       94       END OF FILE
     F*       95       NO RECORD FOUND
     F*       96       OPTION ERROR
     F*       99       CALL ERROR (ZASNMG)
     F*
      /EJECT
     F*FSTART
      ************************************************************
     FINTYP   IF  E           K        DISK                           UC
      * IN  - INTYPDF FF INSTRUMENT TYPES (ISTT)
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
     E*                                                                   S01230
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
      * Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*                                                                   S01230
     IP@MGDA      DS
     I                                        1  50 @MG
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     ID@FMT     E DSINTYPD                                                S01230
     I*D@FMT     E DSINTYPDD0                                             S01230
      * INTYPDF - FF INSTRUMENT TYPE RECORD FORMAT DATA STRUCTURE
      *
     IP@FMT     E DSDSSDY                                                 S01230
     I*                                                                   S01230
     I* EXTERNAL DATA STRUCTURE TO HOLD THE OUTGOING RECORD FORMAT OF     S01230
     I* THE FILE                                                          S01230
     I******P@FMT       DS                            166                 S01230
      * INTERNAL - FF INSTRUMENT TYPE RECORD FORMAT DATA STRUCTURE
      *
     I******                                  1   1 P@DMMY                S01230
     IP@ISTT      DS
      *Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I                                        3   3 W@SL03
     I                                        4   4 W@SL04
     I                                        5   5 W@SL05
      *
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@ISTT           I:Key field
     C*********************PARM           P@FORM           O:Format       S01230
     C                     PARM           P@FMT            O:Format       S01230
      *****************************************************
      *MAIN
     C           P@OPTN    IFEQ '*SAME '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C*********************SETOF                     969798 ¦ indicators
     C                     SETOF                     96     ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 1'st time
     C                     SETON                     01      Set ind.
     C                     OPEN INTYP                  91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 Free
     C                     CLOSEINTYP                  92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C*********************RETRN***************************  Return
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 First rec
     C           *LOVAL    SETLLINTYPDF              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 Next rec
     C                     READ INTYPDF                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 Last rec
     C           *HIVAL    SETGTINTYPDF                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 Prev rec
     C                     READPINTYPDF                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 Keyed
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           P@ISTT    CHAININTYPDF              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           RECI      IFNE 'D'                                                           196826
     C                     MOVE '1'       *IN95                                               196826
     C                     ENDIF                                                              196826
     C           *IN95     CABEQ'1'       T@SKIP             Rec deleted                      196826
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 invalid
     C           P@OPTN    ANDNE'*NEXT  '                   parameter
     C           P@OPTN    ANDNE'*PREV  '
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C                     CALL 'ZASNMG'                     <SND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      * Caller requests DB error handling :
      *
     C           W@RTCD    IFEQ 'DBERR'                    IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
     C           P@OPTN    IFNE '*VERIFY'                  IF 13
     C           P@OPTN    ANDNE'*FREE  '                    AND
     C           P@RTCD    ANDNE'*OPTION'                    AND
     C                     MOVELD@FMT     P@FMT
     C                     END
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
INTYPD    AOFFITR0  INSTRUMENT TYPES
** CPY@     : Copyright notice for inclusion in all programs              S01230
(c) Misys International Banking Systems Ltd. 2001
