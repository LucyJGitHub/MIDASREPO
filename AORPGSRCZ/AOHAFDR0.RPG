     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO HAF installation control data')
      *****************************************************************
      *                                                               *
      *  Midas - ACCESS OBJECTS                                       *
      *                                                               *
      *     AOHAFDR0 - READ ACCESS TO HISTORY AND AUDIT FACILITY      *
      *                I.C.D ACCESS OBJECT                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 049980             Date 02Feb93               *
      *                 S01379             Date 17SEP92               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *     049980 - Recompiled due to changes in SDHAFDPD file.      *
      *     S01379 - NEW FOR HISTORY AND AUDTIT FACILITY DEVELOPMENT. *
      *                                                               *
      *****************************************************************
      * ID F  C  H  L  FUNCTION OF INDICATORS
      *
      *       01       OFF ON THE FIRST RUN
      *       90       FILE ERROR
      *       91       OPEN ERROR
      *       92       CLOSE ERROR
      *       93       EMPTY FILE
      *       96       OPTION ERROR
      *
      *****************************************************************
      /EJECT
     FSDHAFDL1IF  E           K        DISK                           UC
      * IN  - SDHAFDD0 HISTORY AND AUDIT FACILITY (HAFD)
      *****************************************************************
     E                    @MG     1   1 50
      *****************************************************************
     E                    CPY@    1   1 80               Copyright
      * Description     : Copyright notice for inclusion
      *                    in all programs
      *****************************************************************
      /EJECT
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     ID@FMT     E DSSDHAFDPD
      * SDHAFDD0 - HISTORY AND AUDIT FACILITY RECORD FORMAT DATA STRUCTURE
      *
     IP@FMT     E DSDSFDY
      * External Data structure to hold the outgoing record format of
      * the file
     C                     MOVEACPY@      BIS@   80
      /EJECT
      *****************************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM D@FMT     P@FMT            O:Format
      *****************************************************************
      *MAIN
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     9396   ¦ error
     C           P@OPTN    IFEQ '*KEY   '
     C           P@OPTN    OREQ '*FIRST '
     C           P@OPTN    OREQ '*LAST  '
     C           P@OPTN    OREQ '*NEXT  '
     C           P@OPTN    OREQ '*PREV  '
     C           P@OPTN    OREQ '*FREE  '
     C           P@OPTN    OREQ '*SAME  '
     C           P@OPTN    OREQ '*VERIFY'
      *
     C           *IN01     IFEQ '0'                          IF2 1'st time
     C                     SETON                     01
     C                     OPEN SDHAFDL1               91    Open file
      *
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
      *
     C           *LOVAL    SETLL@ETRELT              9390    Set file
     C  N93                READ @ETRELT                  93
     C                     CLOSESDHAFDL1               92    Close file
      *
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
      *
     C                     SETON                     LR      Set ind.
     C                     END                               FI2
      *
      *
     C                     ELSE
     C                     SETON                     96      Parm error
     C                     END
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   96                MOVE '*OPTION' P@RTCD           No selection
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   96                MOVE 'ACO1007' P@MGID
     C                     CALL 'ZASNMG'                     <SND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      *      CALLER REQUESTS DB HANDLING
      *
     C           W@RTCD    IFEQ '*DBERR'                   IF DB ERROR
     C           P@RTCD    ANDNE*BLANK                     AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************************
**
SDHAFDPD  AOHAFDR0  HAF DETAILS
** CPY@     : Copyright notice for inclusion in all programs
(c) Finastra International Limited 2001
