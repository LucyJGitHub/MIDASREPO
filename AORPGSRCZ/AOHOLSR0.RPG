     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Holidays                    *RDR')
     F*******************************************************************
     F*                                                                 *
     F*  Midas ACCESS OBJECTS
     F*                                                                 *
     F*     AOHOLSR0 - READ ACCESS TO HOLIDAYS                          *
     F*                                                                 *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                 *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 123353             Date 23SepT97              *
      *                 S01230             Date 23/11/89              *
     F*                    S01230             DATE 06/11/89             *
     F*                    E01168             DATE 21/02/89             *
     F*                                                                 *
     F*******************************************************************
     F*                                                                 *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
     F*     123353 - Program uses the short data structure when         *
     F*              it needs to use the long one.                      *
     F*     S01230 - STANDARDISE ACCESS PROGRAM & COPYRIGHT ADDED        *S01230
     F********************************************************************S01230
     F*
     F* ID F  C  H  L  FUNCTION OF INDICATORS
     F*
     F*       01       OFF ON THE FIRST RUN
     F*       90       FILE ERROR
     F*       91       OPEN ERROR
     F*       92       CLOSE ERROR
     F*       93       EMPTY FILE
     F*       94       END OF FILE
     F*       95       NO RECORD FOUND
     F*       96       OPTION ERROR
     F*       97       NO SELECTION
     F*       98       CALL ERROR (SDCMSRR)
     F*       99       CALL ERROR (ZASNMG)
     F*
      ********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDHOLL0 IF  E           K        DISK                           UC
      * IN  - @HOLPHG HOLIDAYS (CYCD LCCD YRNB)
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
     E*                                                                   S01230
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
      * Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*                                                                   S01230
     IP@MGDA      DS
     I                                        1  50 @MG
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I*D@FMT     E DSSDHOLD0                                              S01230
     ID@FMT     E DSSDHOLPD                                               S01230
      * @HOLPHG - Holidays record format data structure
      *
     I******P@FMT     E DSDSFDY                                     S01230123353
     IP@FMT     E DSDSSDY                                                 123353
     I*                                                                   S01230
     I* EXTERNAL DATA STRUCTURE TO HOLD THE OUTGOING RECORD FORMAT OF     S01230
     I* THE FILE                                                          S01230
     I******P@FMT       DS                             36                 S01230
      * Internal branch location code data structure.
      *
     I******                                  1   1 P@DMMY                S01230
     IP@CYCD      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I                                        3   3 W@SL03
     IP@LCCD      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL04
     I                                        2   2 W@SL05
     I                                        3   3 W@SL06
     IP@YRNB      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL07
     I                                        2   2 W@SL08
     I                                        3   3 W@SL09
     I                                        4   4 W@SL10
      *
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@CYCD  3        I:Key field
     C                     PARM           P@LCCD  3        I:Key field
     C                     PARM           P@YRNB  4        I:Key field
     C                     PARM           P@FMT            O:Format
      *****************************************************
     C           KEYS      KLIST
     C                     KFLD           P@CYCD
     C                     KFLD           P@LCCD
     C                     KFLD           P@YRNB
      *MAIN
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDHOLL0                91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PROG
     C                     CLOSESDHOLL0                92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLL@HOLPHG              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ @HOLPHG                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGT@HOLPHG                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READP@HOLPHG                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEYED
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           W@SL01    IFEQ '?'                        IF 8 SELECT
     C           W@SL02    OREQ '?'                         OR
     C           W@SL03    OREQ '?'                         OR
     C           W@SL04    OREQ '?'                         OR
     C           W@SL05    OREQ '?'                         OR
     C           W@SL06    OREQ '?'                         OR
     C           W@SL07    OREQ '?'                         OR
     C           W@SL08    OREQ '?'                         OR
     C           W@SL09    OREQ '?'                         OR
     C           W@SL10    OREQ '?'                         OR
     C                     CALL 'SD0160S'              98    <SELECT>
     C                     PARM *BLANK    P@RTC2  7          B:Return code
     C                     PARM *BLANK    P@CYCD             B:Key
     C                     PARM *BLANK    P@LCCD             B:Key
     C                     PARM *BLANK    P@YRNB             B:Key
     C           P@RTC2    COMP *BLANK               97      No selection
     C           *IN97     CABEQ'1'       T@SKIP             No selection
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C                     END                             FI 8
     C           KEYS      CHAIN@HOLPHG              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C*          P@OPTN    ANDNE'*SAME  '                   AND           E01168
     C                     SETON                       96    Parm error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No rec found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      * Caller Requests DB Error Handling
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
     C           P@OPTN    IFNE '*VERIFY'
     C           P@OPTN    ANDNE'*FREE  '
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDHOLPD  AOHOLSR0  Branch Location Codes                                  E01168
** CPY@     : Copyright notice for inclusion in all programs              S01230
(c) Misys International Banking Systems Ltd. 2001
