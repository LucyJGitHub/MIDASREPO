     H        1
      *****************************************************************
/*S*D ***RPGBASE*******************************************************
     F*****************************************************************
     F*                                                               *
     F*  Midas  Access Objects
     F*                                                               *
     F*  AOIBMDR0 - IBM Renames Conversion Access Object.             *
     F*                                                               *
     F*  Function:  This program accesses LF/SCIBMDR0 to determine    *
     F*             the name of the renamed IBM object at the IBM     *
     F*             Operating System level specified.                 *
     F*                                                               *
     F*  Called By: FCC0201                                           *
     F*             FCC0202                                           *
     F*             FCC0203                                           *
     F*             FCC0204                                           *
     F*             FCC0210                                           *
     F*             FCC0220                                           *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CPK030   *REDUNDANTDate 11Apr14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSC012             Date 26Oct01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 049424             DATE 11JAN93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CPK030   - Clean up of deliverable data library.             *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CSC012 - This access object must always end with LR on.      *
     F*  049424 - New program created for this service call.          *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*        D F C H L   FUNCTION INDICATOR                         *
     F*                                                               *
     F*        01     INITIALISATION                                  *
     F*        90     FILE ERROR                                      *
     F*        91     FILE OPEN ERROR                                 *
     F*        92     FILE CLOSE ERROR                                *
     F*        93     FILE EMPTY                                      *
     F*        94     END OF FILE                                     *
     F*        95     NO RECORD FOUND                                 *
     F*        96     OPTION ERROR                                    *
     F*        97     NO SELECTION                                    *
     F*        98     ERROR ON CALL OF SELECTION PROGRAM              *
     F*                                                               *
     F*****************************************************************
      /EJECT
     FSCIBMDL1IF  E           K        DISK                           UC
      *****************************************************************
      /EJECT
     E                    @MG     1   1 50
     E*
     E* Description     : Copyright notice for inclusion in all programs
     E                    CPY@    1   1 80               Copyright     array
      *****************************************************************
      /EJECT
     I* Description     : Copyright notice for inclusion in all programs
     I*
     IA@CPY       DS
      * Copyright array
     I                                        1  80 CPY@
     I*
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     ID@FMT     E DSSCIBMDPD
      *
     IP@FMT     E DSDSSDY
     I* External Data structure to hold the outgoing record format
     I* of the file.
      *
     IP@K101      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I                                        3   3 W@SL03
     I                                        4   4 W@SL04
     I                                        5   5 W@SL05
     I                                        6   6 W@SL06
     IP@K102      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL07
     I                                        2   2 W@SL08
     I                                        3   3 W@SL09
     I                                        4   4 W@SL10
     I                                        5   5 W@SL11
     I                                        6   6 W@SL12
     I                                        7   7 W@SL13
     I                                        8   8 W@SL14
     I                                        9   9 W@SL15
     I                                       10  10 W@SL16
      *****************************************************************
      /EJECT
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@K101           I:Key field
     C                     PARM           P@K102           I:Key field
     C                     PARM           P@FMT            O:Format
      *****************************************************************
     C           KEYS      KLIST
     C                     KFLD           P@K101
     C                     KFLD           P@K102
      *MAIN
     C           P@OPTN    IFEQ '*SAME   '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SCIBMDL1               91    Open file    LN0662
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSESCIBMDL1               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLL@AIREAX              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ @AIREAX                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGT@AIREAX                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READP@AIREAX                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEY ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR         ESS
     C           W@SL01    IFEQ '?'                        IF 8 SELECT
     C           W@SL02    OREQ '?'                         OR
     C           W@SL03    OREQ '?'                         OR
     C           W@SL04    OREQ '?'                         OR
     C           W@SL05    OREQ '?'                         OR
     C           W@SL06    OREQ '?'                         OR
     C           W@SL07    OREQ '?'                         OR
     C           W@SL08    OREQ '?'                         OR
     C           W@SL09    OREQ '?'                         OR
     C           W@SL10    OREQ '?'                         OR
     C           W@SL11    OREQ '?'                         OR
     C           W@SL12    OREQ '?'                         OR
     C           W@SL13    OREQ '?'                         OR
     C           W@SL14    OREQ '?'                         OR
     C           W@SL15    OREQ '?'                         OR
     C           W@SL16    OREQ '?'                         OR
      *
      * Enter the call to the selection program here.
      * e.g.
      *                    CALL 'xx0000S'              98    <SELECT>
      *                    PARM *BLANK    RTNCDE  7          B:Return code
      *                    PARM *BLANK    P@K101             B:Key
      *                    PARM *BLANK    P@K102             B:Key
      *
      *          RTNCDE    COMP *BLANK               97      No selection
     C           *IN97     CABEQ'1'       T@SKIP             No selection
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C                     END                             FI 8
     C           KEYS      CHAIN@AIREAX              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*SAME  '                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No rec found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *                                                                                       CSC012
     C                     SETON                     LR                                       CSC012
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
     C           W@RTCD    IFEQ '*DBERR'
     C           P@RTCD    ANDNE*BLANK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END
      *
      *
      *
     C           P@OPTN    IFNE '*VERIFY'
     C           P@OPTN    ANDNE'*FREE'
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
      *
      * Return to caller :
      *
     C                     SETON                     LR                                       CSC012
     C                     RETRN
      *
      *****************************************************************
**
SCIBMDPD  AOIBMDR0  IBM Rename Conversion
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
