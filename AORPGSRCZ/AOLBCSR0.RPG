     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Portfolio Lending Cust Det.     *RDR')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                      *
     F*                                                               *
     F*  AOLBCSR0 - PORTFOLIO LENDING CUSTOMER DETAILS                *
     F*                                                               *
     F*  Function: Access record from SDLBCSL0.                       *
     F*                                                               *
     F*  Calls: SD0412                                                *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD007             Date 28Aug00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01486             Date 11Aug94               *
     F*                 PM0027             DATE DATE 16A              *
     F*                 PM0026             DATE 12AUG94               *
     F*                 PM0022             DATE 03AUG94               *
     F*                 PM0010             DATE 12JUL94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  CSD007 - Customer Closing:                                   *
     F*           Access the main customer details through AOCUSTR0.  *
     F*  S01486 - Portfolio Management Upgrade to R10.                *
     F*  PM0027 - Should ignore deleted records                       *
     F*  PM0026 - Add '?' processing                                  *
     F*  PM0022 - Change record format name due to Synon changes      *
     F*  PM0010 - Allow customer shortname as a parameter             *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*        D F C H L   FUNCTION INDICATOR                         *
     F*                                                               *
     F*        01     INITIALISATION                                  *
     F*        90     FILE ERROR                                      *
     F*        91     FILE OPEN ERROR                                 *
     F*        92     FILE CLOSE ERROR                                *
     F*        93     FILE EMPTY                                      *
     F*        94     END OF FILE                                     *
     F*        95     NO RECORD FOUND                                 *
     F*        96     OPTION ERROR                                    *
     F*        97     NO SELECTION                                    *
     F*                                                               *
     F*****************************************************************
     F*
     F/EJECT
     FSDLBCSL0IF  E           K        DISK                           UC
     F**IN**-*SDLBCSD0*Customer*Details***********************************PM0022
     F* IN  - @LBCSL0 Customer Details                                    PM0022
     E/EJECT
     E*
     E                    @MG     1   1 50
     E* Description     : Copyright notice for inclusion in all programs
     E*
     E                    CPY@    1   1 80               Copyright     arr
     E*
     E                    @IN        10  1  A            Input array      PM0010
     I/EJECT
     I*
     I* Description     : Copyright notice for inclusion in all programs
     I*
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
     I*
     IP@MGDA      DS
     I                                        1  50 @MG
     I*
     IPGMDS      SDS
     I* Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
     I*                                                                   PM0010
     IP@CUSN      DS                                                      PM0010
     I*                                                                   PM0010
     I** Key input data structure                                         PM0010
     I*                                                                   PM0010
     I                                        1  10 @IN                   PM0010
     I                                        1  10 W@CSHT                PM0010
     I                                        1   1 W@INIT                PM0010
     I                                        1   6 W@CNUM                PM0010
     I                                        6   6 W@LAST                PM0010
     I                                        7  10 W@BLNK                PM0010
     I* Key slicing data structure (for Record Selection)                 PM0026
     I                                        1   1 W@SL01                PM0026
     I                                        2   2 W@SL02                PM0026
     I                                        3   3 W@SL03                PM0026
     I                                        4   4 W@SL04                PM0026
     I                                        5   5 W@SL05                PM0026
     I                                        6   6 W@SL06                PM0026
     I                                        7   7 W@SL07                PM0026
     I                                        8   8 W@SL08                PM0026
     I                                        9   9 W@SL09                PM0026
     I                                       10  10 W@SL10                PM0026
     I*
     ID@FMT     E DSSDLBCSPD
     I* SDLBCSD0 - Customer Details
     I*
     IP@FMT     E DSDSFDY
     I* External Data structure to hold the outgoing record format
     I* of the file.
     I*
     ISDCUST    E DSSDCUSTPD                                              PM0010
     I* SDCUSTP0 - Customer Details                                       PM0010
     I*                                                                   PM0010
     IDSSDY     E DSDSSDY                                                 PM0010
     I* External Data structure for accessin customer details             PM0010
     I*                                                                   PM0010
     I*****************************************************************
     C/EJECT
     C*
     C*  Object Copyright code
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C***********          PARM           P@CUSN  6        I:PL Cust DetlsPM0010
     C                     PARM           P@CUSN 10        I:PL Cust DetlsPM0010
     C                     PARM           P@FMT            O:Format
     C*
     C** Execute Subroutine 'MAIN'.
     C*
     C                     EXSR MAIN
     C*
     C** Return to caller :
     C*
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  INDEX OF SUBROUTINES                                         *
     C*                                                               *
     C*  MAIN - Main Process                                          *
     C*                                                               *
     C*  *PSSR  - File Error Handling                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C*****************************************************************
     C*                                                               *
     C*  MAIN - Main Process                                          *
     C*                                                               *
     C*****************************************************************
     C*
     C           MAIN      BEGSR
     C*
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
     C*
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
     C*
     C* First call - open file :
     C*
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDLBCSL0               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
     C*
     C* Last call - close file and terminate program :
     C*
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSESDLBCSL0               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
     C*
     C* Caller requests first record :
     C*
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C************LOVAL    SETLLSDLBCSD0             9390    Set file     PM0022
     C           *LOVAL    SETLL@LBCSL0              9390    Set file     PM0022
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
     C*
     C* Caller requests next record :
     C*
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C***********          READ SDLBCSD0               9094  Read record  PM0022
     C           CUTYLC    DOUNE'*'                                       PM0027
     C           *IN94     OREQ '1'                                       PM0027
     C                     READ @LBCSL0                9094  Read record  PM0022
     C                     END                                            PM0027
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
     C*
     C* Caller requests last record :
     C*
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C************HIVAL    SETGTSDLBCSD0               90    Set file     PM0022
     C           *HIVAL    SETGT@LBCSL0                90    Set file     PM0022
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
     C*
     C* Caller requests previous record :
     C*
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C***********          READPSDLBCSD0               9094  Read record  PM0022
     C           CUTYLC    DOUNE'*'                                       PM0027
     C           *IN94     OREQ '1'                                       PM0027
     C                     READP@LBCSL0                9094  Read record  PM0022
     C                     END                                            PM0027
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
     C*
     C* Caller requests record indicated by key :
     C*
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEY ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           W@SL01    IFEQ '?'                        IF 8 SELECT    PM0026
     C           W@SL02    OREQ '?'                         OR            PM0026
     C           W@SL03    OREQ '?'                         OR            PM0026
     C           W@SL04    OREQ '?'                         OR            PM0026
     C           W@SL05    OREQ '?'                         OR            PM0026
     C           W@SL06    OREQ '?'                         OR            PM0026
     C           W@SL07    OREQ '?'                         OR            PM0026
     C           W@SL08    OREQ '?'                         OR            PM0026
     C           W@SL09    OREQ '?'                         OR            PM0026
     C           W@SL10    OREQ '?'                         OR            PM0026
     C                     CALL 'SD0412'               98    <SELECT>     PM0026
     C                     PARM *BLANK    P@RTC2  7          B:Return codePM0026
     C                     PARM *BLANK    P@CUSN             B:Key        PM0026
     C           P@RTC2    COMP *BLANK               97      No selection PM0026
     C           *IN97     CABEQ'1'       T@SKIP             No selection PM0026
     C           *IN98     CABEQ'1'       T@SKIP             Program errorPM0026
     C                     END                             FI 8           PM0026
     C*                                                                   PM0010
     C** Align input customer                                             PM0010
     C*                                                                   PM0010
     C                     MOVE *BLANK    W@ALGN 10                       PM0010
     C                     Z-ADD1         X       30                      PM0010
     C*                                                                   PM0010
     C           *BLANK    LOKUP@IN,X                97                   PM0010
     C           *IN97     CABEQ'0'       T@SKIP         97               PM0010
     C                     MOVEA@IN,X     W@ALGN                          PM0010
     C                     MOVE W@ALGN    P@CUSN                          PM0010
     C*                                                                   PM0010
     C** Check whether customer number or shortname                       PM0010
     C*                                                                   PM0010
     C           W@INIT    IFLT '0'                                       PM0010
     C                     SETON                     50                   PM0010
     C                     MOVE '*CSHT  ' P@KYST  7                       PM0010
     C*                                                                   PM0010
     C** Find customer number for shortname input                         PM0010
     C*                                                                   PM0010
     C                     CALL 'AOCUSTR0'                                PM0010
     C                     PARM P@RTCD    RTCD    7                       PM0010
     C                     PARM '*KEY   ' OPTN    7                       PM0010
     C                     PARM P@CUSN    KEYC   10                       PM0010
     C                     PARM *BLANKS   KYST    7                       PM0010
     C           SDCUST    PARM SDCUST    DSSDY                           PM0010
     C*                                                                   PM0010
     C           RTCD      IFNE *BLANKS                                   PM0010
     C                     SETON                     95                   PM0010
     C                     END                                            PM0010
     C           *IN95     CABEQ'1'       T@SKIP                          PM0010
     C                     MOVELBBCUST    P@CNUM                          PM0010
     C*                                                                   PM0010
     C                     ELSE                                           PM0010
     C           W@BLNK    CABNE*BLANK    T@SKIP     9797                 PM0010
     C                     TESTN          W@CNUM     97                   PM0010
     C           *IN97     CABEQ'0'       T@SKIP         97               PM0010
     C           W@LAST    CABLT'0'       T@SKIP       97                 PM0010
     C                     MOVE '*CNUM  ' P@KYST         97               PM0010
     C                     MOVELP@CUSN    P@CNUM  6                       PM0010
      *                                                                   CSD007
     C                     CALL 'AOCUSTR0'                                CSD007
     C                     PARM P@RTCD    RTCD    7                       CSD007
     C                     PARM '*KEY   ' OPTN    7                       CSD007
     C                     PARM P@CUSN    KEYC   10                       CSD007
     C                     PARM *BLANKS   KYST    7                       CSD007
     C           SDCUST    PARM SDCUST    DSSDY                           CSD007
      *                                                                   CSD007
     C           RTCD      IFNE *BLANKS                                   CSD007
     C                     MOVEL'1'       *IN95                           CSD007
     C                     ENDIF                                          CSD007
      *                                                                   CSD007
     C           *IN95     CABEQ'1'       T@SKIP                          CSD007
     C                     MOVELBBCUST    P@CNUM                          CSD007
      *                                                                   CSD007
     C                     END                                            PM0010
     C*                                                                   PM0010
     C***********P@CUSN    CHAINSDLBCSD0             9590    Get record   PM0010
     C***********P@CNUM    CHAINSDLBCSD0             9590    Get recPM0010PM0022
     C           P@CNUM    CHAIN@LBCSL0              9590    Get record   PM0022
     C           CUTYLC    IFEQ '*'                                       PM0027
     C                     SETON                     95                   PM0027
     C                     END                                            PM0027
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
     C*
     C           P@OPTN    IFNE '*KEY   '                   IF NOT SEQ.
     C           P@OPTN    ANDNE'*NEXT  '                   AND
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
     C*
     C* No record found or End of file - return error code :
     C*          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
     C*          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No rec found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C*
     C* Caller requests~message sent to program queue :
     C*
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA             I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
     C*
     C* Caller requests DB error handling
     C*
     C           W@RTCD    IFEQ '*DBERR '                  IF 10
     C           P@RTCD    ANDNE*BLANK                      AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI 10
     C*
     C           P@OPTN    IFNE '*VERIFY'                  IF 11
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C           P@OPTN    ANDNE'*OPTION'                   AND
     C                     MOVELD@FMT     P@FMT
     C                     END                             FI 11
     C*
     C                     ENDSR
     C*
**
SDLBCSL0  AOLBCSR0  Customer Details
** CPY@ - OBJECT COPYRIGHT                                                S01486
(c) Misys International Banking Systems Ltd. 2001
