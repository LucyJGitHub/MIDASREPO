     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Midas/Swift Direct Link     *RDR')
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOMSDLR0 - READ ACCESS TO MIDAS/SWIFT DIRECT LINK FILE       *
     F*                I.C.D. RECORD - ONLY ONE RECORD IN FILE
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01230             Date 23/11/89              *
     F*                 S01230             Date 06/11/89              *S01230
     F*                                                                  *
     F********************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*     S01230 - STANDARDISE ACCESS PROGRAMS                         *S01230
     F********************************************************************S01230
     F*
     F* ID F  C  H  L     FUNCTION OF INDICATORS
     F*
     F*       01       OPEN FILE - SDMSDLL1
     F*       90       FILE ERROR
     F*       91       OPEN ERROR
     F*       92       CLOSE ERROR
     F*       93       EMPTY FILE
     F*       96       OPTION ERROR
     F*
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDMSDLL1IF  E           K        DISK                           UC
      * IN  - @BSREC7 Midas/Swift D.L.(MSDL)
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
      ************************************************************
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
     E********************************************************************S01230
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I***********D@FMT     E DSSDMSDLD0                                   S01230
     ID@FMT     E DSSDMSDLPD                                              S01230
      * @BSREC7 - Midas/Swift Direct Link Record Format Data Structure
      *
     IP@FMT     E DSDSFDY                                                 S01230
     I* EXTERNAL DATA STRUCTURE TO HOLD THE OUTGOING RECORD FORMAT        S01230
     I* OF THE FILE.                                                      S01230
      *                                                                   S01230
     I***********P@FMT       DS                             22            S01230
     I***********                             1  22 BSCOPY                S01230
      * Copy of Record Format Data Structure to be Passed Back
      * to the Calling Program After 1st Call.
      *
      *
      ************************************************************
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
      ************************************************************        S01230
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM D@FMT     P@FMT            O:Format
      *****************************************************
      *MAIN
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     9396   ¦ error flags
      *
      * First call - open file - read record - close file
      *
      * Subsequent calls will return the format name.
      *
     C           P@OPTN    IFEQ '*FIRST  '                 IF 1  VALID
     C           P@OPTN    OREQ '*NEXT   '                       OPTION
     C           P@OPTN    OREQ '*PREV   '                       HAS
     C           P@OPTN    OREQ '*LAST   '                       BEEN
     C           P@OPTN    OREQ '*KEY    '                       ENTERED
     C           P@OPTN    OREQ '*FREE   '
     C           P@OPTN    OREQ '*VERIFY '
     C           P@OPTN    OREQ '*SAME   '
     C           *IN01     IFEQ '0'                        IF 2 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDMSDLL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     READ @BSREE7                9093  Read record
     C                     CLOSESDMSDLL1               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C           *IN93     CABEQ'1'       T@SKIP         LR  Empty error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
     C                     ELSE
     C                     SETON                     96
     C                     END                             FI 1
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   96                MOVE '*OPTION' P@RTCD           Option Inval
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   96                MOVE 'ACO1007' P@MGID             Option Inval
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Prog name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Msg key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      * Caller Requests DB Error Handling
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
AOMSDLPD  AOMSDLR0  Midas/Swift Direct Link
** CPY@     : Copyright notice for inclusion in all programs           graS01230
(c) Finastra International Limited 2001
