     H        1
     F********************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO On-line update of FRA position files')        *
     F********************************************************************
     F*                                                                  *
     F*  Midas - Access Objects Module                                   *
     F*                                                                  *
     F*  AOOUFRU0 - On-line Update of Forward Rate Agreement positions   *
     F*               files                                              *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CAP205             Date 03Mar10               *
      *                 CAP204             Date 09Feb10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CIR013             Date 25Apr05               *
      *                 CDL028             Date 07Feb05               *
      *                 TDA101             Date 02Apr04               *
      *                 CGL029             Date 01Sep03               *
      *                 217684             Date 12May03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU003             Date 05Nov97               *
     F*                 CSW005  *CREATE    Date 01Aug96               *
     F*                 000000             Date ddmmmyy                  *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *  CAP204 - Teller Related APIs - Account Transfer              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *           Recompile over ZINTDY.                              *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  TDA101 - After and Before image data structure (Z#AST        *
      *           and Z#BST) used in program is short FRA/IRS         *
      *           transaction.  (Recompile)                           *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CIR005 - FRA/IRS Business Day Conventions in DEALSDG            *
     F*  CEU003 - EMU Dealing Settlement Ccy Conversion                  *
     F*  CSW005 - MT34n and MT36n Message Generation for FRA/IRS.        *
     F*                                                                  *
     F********************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*****************************************************************
     E/SPACE 5
     E/COPY ZSRSRC,ZDATE2Z1
     E/SPACE 5
     E*
     E**  (Posting) Naratives
     E                    ##NAR   1   1 30
     E*
     E*  Subroutine stack
     E                    @STK       20 10
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     I/SPACE 5
     I*                                                                   CIR002
     IZ#BSTS    E DSZ#BST
     IZ#ASTS    E DSZ#AST
     IIRDEAL    E DSDEALSDG
     I              LCD                             ##LCD                 ?
     I*
     I##PAYD      DS
     I                                        1   1 ##PAY
     I                                        2   3 ##PAYM
     I**********                              4  15 ##PAYA                                    CGL029
     I**********                             16  18 ##PAYB                                    CGL029
     I                                        4  21 ##PAYA                                    CGL029
     I                                       22  24 ##PAYB                                    CGL029
     I##RECD      DS
     I                                        1   1 ##REC
     I                                        2   3 ##RECM
     I**********                              4  15 ##RECA                                    CGL029
     I**********                             16  18 ##RECB                                    CGL029
     I                                        4  21 ##RECA                                    CGL029
     I                                       22  24 ##RECB                                    CGL029
     I##SETD      DS
      * pay/receive (P/R)
     I                                        1   1 ##PYRC
      * settlement method
     I                                        2   3 ##SETM
      * settlement account
     I**********                              4  15 ##SETA                                    CGL029
     I                                        4  21 ##SETA                                    CGL029
     I                                        4   5 ##NOSN
     I                                        4  13 ##ACNO
     I                                        4   9 ##CNUM
     I**********                             10  13 ##ACOD                                    CGL029
     I**********                             14  15 ##ACSQ                                    CGL029
     I                                       10  19 ##ACOD                                    CGL029
     I                                       20  21 ##ACSQ                                    CGL029
      * settlement branch
     I**********                             16  18 ##BRCA                                    CGL029
     I                                       22  24 ##BRCA                                    CGL029
      *
      *  Calling parameter data structure for AOSPOSU0
      *
     IC#DTA       DS                            256
      *  Account type N=nostro nbr F=Full nostro G=General Ledger
      *               P=Partial Ledger R= Retail Number  X= no settlement
     I                                        1   1 C#ATYP
      *               Full General Ledger - CNUM/CCY/ACOD/ACSQ
     I                                        2  19 C#ACCT
     I**********                              2   70C#CNU1                                    CSD027
     I                                        2   7 C#CNU1                                    CSD027
     I                                        8  10 C#CCY1
     I**********                             11  140C#ACO1                                    CGL029
     I                                       15  160C#ACS1
     I                                       17  19 C#BRC1
      *               Nostro number
     I                                        2   30C#NOSN
      *               Full Nostro - CCY/NOSN
     I                                        2   6 C#NOS
      *               Retail Number
     I                                        2  110C#ACNO
      *               Partial Account - CNUM/ACOD/ACSQ
     I**********                              2   70C#CNUM                                    CSD027
     I                                        2   7 C#CNUM                                    CSD027
     I**********                              8  110C#ACOD                                    CGL029
     I                                       12  130C#ACSQ
      *               Branch of account if Partial                      '
     I                                       14  16 C#BRCA
      *               OLPOS Incoming/Outgoing Indicator 'I' or 'O' or ' '
      *                     ' ' no OLPOS update
     I                                       24  24 C#IO
      *               Currency of transaction
     I                                       25  27 C#CCY
      *               Transaction Type
     I                                       28  29 C#TRYP
      *               Transaction Narrative
     I                                       30  59 C#NARR
      *               Cheque Number
     I                                       60  670C#CHQN
      *               FT transaction reference
     I                                       68  82 C#PREF
      *               DL/LE/GL/RE transaction reference
     I                                       83  88 C#TRN
      *               Movement amount
     I                                    P  89  960C#AMT
      *               Processing indicators Not blank = no update
      *                                     ' ' = Update
      *               Prono update indicator
     I                                       97  97 C#PRON
      *               Olpos update indicator
     I                                       98  98 C#OLPS
      *               Memos update indicator
     I                                       99  99 C#MEMS
      *               Rsacmtpd write indicator
     I                                      100 100 C#RSAC
      *               Module
     I                                      101 102 C#MOD
      *               Olpos record number to update
      *                     '01' for FX
      *                     '02' for Interest movements
      *                     '03' for MM loans and deposits
     I                                      103 104 C#OLRC
      *               FT Memos ledger balance override
      *                        'Y' update ledger balance irrespective of
      *                        posting date
     I                                      105 105 C#FTOV
      *               Value date and posting date
     I                                    P 106 1080C#VDAT
     I                                    P 109 1110C#PSTD
      *               Override posting date correction
      *                        for forward valued items
      *                        'Y' prevents date being changed
     I                                      112 112 C#PSTO
      *               Indicator to show if removal allowed Y, N or " "
     I                                      113 113 C#RPST
      *               Removal set number
     I                                      114 1160C#RPNB
      *               Removal type "C" = Credit entries
      *                            "D" = Debit entries
     I                                      117 117 C#RTYP
      *               Booking branch
     I                                      118 120 C#BBRN
      *                                                                                       CGL029
      ** Account code for full general ledger                                                 CGL029
      *                                                                                       CGL029
     I                                      121 1300C#ACO1                                    CGL029
      *                                                                                       CGL029
      ** Account code for partial account                                                     CGL029
      *                                                                                       CGL029
     I                                      131 1400C#ACOD                                    CGL029
      *                                                                                       CAP204
      ** Other Transaction Reference                                                          CAP204
      *                                                                                       CAP204
     I                                      141 160 C#OTR1                                    CAP204
      *                                                                                       CAP205
      ** Movement type                                                                        CAP205
      *                                                                                       CAP205
     I                                      161 161 C#MTYP                                    CAP205
      *                                                                                       CLE134
      ** Exception Reference Id/Number                                                        CLE134
      *                                                                                       CLE134
     I                                      162 164 C#XRFI                                    CLE134
     I                                      165 1740C#XRFN                                    CLE134
     ISCSARD    E DSSCSARDPD                                              CEU003
      ***  External data structures for Switchable Features Details       CEU003
     IDSSDY     E DSDSSDY                                                 CEU003
      **  Second data structure for Access Programs, long DS              CEU003
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I/COPY ZSRSRC,ZINTDYZ1
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRMAIN - Main Processing                       *
      *****************************************************************
      *
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           ##RTCD  7
     C                     PARM           Z#BSTS
     C                     PARM           Z#ASTS
     C                     PARM           Z#BAMT 130
     C                     PARM           Z#AAMT 130
      *
      ** Parameter list for DLZFRS:
      *
     C           D#PLST    PLIST
     C                     PARM           D#VDAT  50
     C                     PARM           D#MDAT  50
     C                     PARM           D#PAMT 130
     C                     PARM           D#IANP 130
     C                     PARM           D#ICBS  10
     C                     PARM           D#SETF  1
     C                     PARM           D#RFLT 117
     C                     PARM           D#RFIX 117
     C                     PARM           D#PYIN  1
     C                     PARM           D#TAXI  1
     C                     PARM           D#NETA 130
     C                     PARM           D#TAXA 130
     C                     PARM           D#RETC  7
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      *  Main processing
     C                     EXSR SRMAIN
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C                     RETRN
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
      ** Clear down all shadow postings in store (within AOSPOSU0)
      *
     C                     EXSR SRCLR
      *
     C                     MOVELZ#BSTS    #BRCID  1
     C                     MOVELZ#ASTS    #ARCID  1
      *
      ** Generate 'BEFORE STATUS' account movements
      ** (if not insertion, when #BRCID will be blank)
      *
     C           #BRCID    IFNE *BLANK
      *
     C                     MOVEL'YES'     ##REVI  3
      *
      ** Set up FRA deal data structure.
     C                     MOVELZ#BSTS    IRDEAL
     C                     MOVEL'B'       ##BEAF  1
     C                     EXSR SRFRAW
      *
     C                     END
      *
      ** Generate 'AFTER STATUS' account movements
      ** (if not deletion, when CHTP will be 'R')
      *
     C                     MOVEL*BLANK    ##REVI
      *
      ** Set up FRA deal data structure.
     C                     MOVELZ#ASTS    IRDEAL
     C           CHTP      IFNE 'R'
     C                     MOVEL'A'       ##BEAF
     C                     EXSR SRFRAW
     C                     END
      *
      ** Update database with shadow postings in store (within AOSPOSU0)
      *
     C                     EXSR SRUPD
      *
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRFRAW                                         *
      * Function    :  Forward Rate Agreement Deal Processing         *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  AOSPOSU0 - On-line Positions Update            *
      *****************************************************************
     C           SRFRAW    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRFRAW'  @STK,Q
      *
      *................................................................
      *
      **  Initialize work fields
      *
     C                     Z-ADDVDAT      ##VDAT  50       * event val dte
     C                     MOVELUCUCY     ##CCY   3        * event currency
     C                     Z-ADD*ZERO     ##AMT  150       * event amount
     C                     MOVEL*BLANK    ##NARR 30        * narrative
      *
      **  Store pay and receive settlement methods, accounts & branches
      *
     C                     MOVELPSETM     ##PAYM           * pay method
     C                     MOVELUPACC     ##PAYA           * pay account
     C                     MOVELPOBR      ##PAYB           * pay branch
     C                     MOVELRSETM     ##RECM           * rec method
     C                     MOVELURACC     ##RECA           * rec account
     C                     MOVELROBR      ##RECB           * rec branch
      *
      **  Store basic deal details
      *
     C                     MOVELDTYP      ##TRYP  2        * deal type
     C                     MOVELDLNO      ##TRN   6        * transact no.
     C                     MOVELBRCA      ##BBRN  3        * booking branc
      *
      *
      ** Generate interest account movement
      ** ==================================
      *
     C           ##BEAF    IFEQ 'B'
     C                     Z-ADDZ#BAMT    ##AMT
     C                     ELSE
     C                     Z-ADDZ#AAMT    ##AMT
     C                     ENDIF
      *
      ** If unknown cashflow determine it
      *
     C           ##AMT     IFEQ *ZERO
      *
      ** Use the routine DLZFRS to determine the cash-flow amount.
      *
     C                     Z-ADDVDAT      D#VDAT
     C                     Z-ADDMDAT      D#MDAT
     C                     MOVELSETF      D#SETF
     C                     MOVELTAXI      D#TAXI
      *
     C           UEINR     IFGE TEINR
     C           UAIAN     SUB  TAIAN     D#IANP
     C                     MOVEL'P'       D#PYIN
     C                     ELSE
     C           TAIAN     SUB  UAIAN     D#IANP
     C                     MOVEL'R'       D#PYIN
     C                     ENDIF
      *
     C********** UBRTT     IFNE 0                                                             CSD103
     C           UBRTT     IFNE *BLANKS                                                       CSD103
     C                     Z-ADDUICBS     D#ICBS
     C                     Z-ADDUPAMT     D#PAMT
     C                     Z-ADDTEINR     D#RFIX
     C                     Z-ADDUEINR     D#RFLT
     C                     ELSE
     C                     Z-ADDTICBS     D#ICBS
     C                     Z-ADDTPAMT     D#PAMT
     C                     Z-ADDUEINR     D#RFIX
     C                     Z-ADDTEINR     D#RFLT
     C                     ENDIF
      *
     C                     CALL 'DLZFRS'  D#PLST       90
      *
     C           *IN90     IFEQ '1'
     C           D#RETC    ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE  8
     C                     MOVEL'DLZFRS  'W0KEY  29
     C                     Z-ADD905       W0ERNB  30
     C                     EXSR SRERR
     C                     END
      *
     C                     Z-ADDD#NETA    ##AMT
      *
      ** If still zero, then no further cash payments can be inferred
      ** (rate is zero, so even payment on other side must be ignored)
      *
     C           ##AMT     CABEQ*ZERO     EXFRAW
      *
     C                     ENDIF
      *
      ** If cash-flow is a payment, reverse the sign to make it -ve.
      *
     C           UEINR     IFGT TEINR
     C                     Z-SUB##AMT     ##AMT
     C                     ENDIF
      *
      ** Final payment
      *
     C           ##AMT     IFNE *ZERO
      *
      ** Pass movement to write program (with appropriate instructions)
      *
     C           ##AMT     IFLT *ZERO
     C                     MOVEL##PAYD    ##SETD           * Pay details
     C                     Z-SUB##AMT     ##AMT
     C                     ELSE
     C                     MOVEL##RECD    ##SETD           * Rec details
     C                     END
     C                     Z-ADD003       ##RPNB  30       * 3=Interest
     C                     MOVEL##NAR,1   ##NARR
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'S'       ##MTYP  1                                           CAP205
     C                     ENDIF                                                              CAP205
     C                     EXSR SRMOVE
     C                     END
      *
      *................................................................
      *
     C           EXFRAW    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRMOVE                                         *
      * Function    :  Pass Movement To Write Program                 *
      *                                                               *
      * Called by   :  Various                                        *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRMOVE    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRMOVE'  @STK,Q
      *
      *................................................................
      *
      ** Bypass if no amount to post
      *
     C           ##AMT     CABEQ*ZERO     EXMOVE
      *
      ** Determine account movement settlement details
      *
     C                     CLEARC#DTA
      *
     C           ##SETM    IFEQ '01'
     C           ##SETM    OREQ '02'
     C           ##SETM    OREQ '07'
     C           ##SETM    OREQ '08'
     C           ##SETM    OREQ '12'
     C                     MOVEL'N'       C#ATYP
     C                     MOVEL##NOSN    C#NOSN
     C                     ELSE
     C           ##SETM    IFEQ '14'
     C                     MOVEL'R'       C#ATYP
     C                     MOVEL##ACNO    C#ACNO
     C                     ELSE
     C           ##SETM    IFEQ '04'
     C           ##SETM    OREQ '05'
     C                     MOVEL'P'       C#ATYP
     C                     MOVEL##CNUM    C#CNUM
     C                     MOVEL##ACOD    C#ACOD
     C                     MOVEL##ACSQ    C#ACSQ
     C                     MOVEL##BRCA    C#BRCA
     C                     END
     C                     END
     C                     END
     C*                                                                   CEU003
     C           CEU003    IFEQ 'Y'                                       CEU003
     C*                                                                   CEU003
     C           RSCY      IFEQ *BLANKS                                   CEU003
     C           PSCY      ANDNE*BLANKS                                   CEU003
     C                     MOVELPSCY      RSCY                            CEU003
     C                     ELSE                                           CEU003
     C           PSCY      IFEQ *BLANKS                                   CEU003
     C           RSCY      ANDNE*BLANKS                                   CEU003
     C                     MOVELRSCY      PSCY                            CEU003
     C                     ENDIF                                          CEU003
     C*                                                                   CEU003
     C                     ENDIF                                          CEU003
     C*                                                                   CEU003
     C                     ENDIF                                          CEU003
      *                                                                   CEU003
      ** If CEU003 is ON, convert the corresponding movement amount       CEU003
      **    to settlement currency equivalent.                            CEU003
      *                                                                   CEU003
     C           CEU003    IFEQ 'Y'                                       CEU003
     C           RSCY      ANDNE*BLANKS                                   CEU003
     C           PSCY      ANDNE*BLANKS                                   CEU003
      *                                                                   CEU003
     C                     CALL 'EU0200'               90                 CEU003
     C                     PARM *BLANKS   P#RTCD  7                       CEU003
     C                     PARM ##AMT     P#FRAM 183                      CEU003
     C                     PARM ##CCY     P#FRCY  3                       CEU003
     C                     PARM RSCY      P#TOCY  3                       CEU003
     C                     PARM           P#OUTA 150                      CEU003
     C                     PARM           P#INTA 183                      CEU003
     C                     PARM           P#EXRT 159                      CEU003
     C                     PARM           P#MDIN  1                       CEU003
      *                                                                   CEU003
     C           *IN90     IFEQ '1'                                       CEU003
     C           P#RTCD    ORNE *BLANKS                                   CEU003
     C                     MOVEL'*CALL   'W0FILE                          CEU003
     C                     MOVEL'EU0200  'W0KEY                           CEU003
     C                     Z-ADD904       W0ERNB                          CEU003
     C                     EXSR SRERR                                     CEU003
      *                                                                   CEU003
     C                     ELSE                                           CEU003
     C                     Z-ADDP#OUTA    ##AMT                           CEU003
     C                     ENDIF                                          CEU003
      *                                                                   CEU003
     C                     ENDIF                                          CEU003
      *
      * OLPOS Incoming/Outgoing Indicator 'I' or 'O'
     C           ##PYRC    IFEQ 'P'
     C                     MOVEL'O'       C#IO             * outgoing
     C                     ELSE
     C                     MOVEL'I'       C#IO             * incoming
     C                     END
      *
      * Currency of transaction
     C           CEU003    IFEQ 'Y'                                       CEU003
     C           RSCY      ANDNE*BLANKS                                   CEU003
     C           PSCY      ANDNE*BLANKS                                   CEU003
     C                     MOVELRSCY      C#CCY                           CEU003
     C                     ELSE                                           CEU003
     C                     MOVEL##CCY     C#CCY
     C                     ENDIF                                          CEU003
      *
      * Transaction type
     C                     MOVEL##TRYP    C#TRYP
      * Transaction Narrative
     C                     MOVEL##NARR    C#NARR
     C           ##REVI    IFEQ 'YES'
     C                     MOVE '*'       C#NARR
     C                     END
      *
      * DL/LE/GL/RE transaction reference
     C                     MOVEL##TRN     C#TRN
      *
      * Movement amount
     C           ##PYRC    IFEQ 'P'
     C           ##REVI    ANDNE'YES'
     C           ##PYRC    OREQ 'R'
     C           ##REVI    ANDEQ'YES'
     C                     Z-SUB##AMT     C#AMT            * pay ie credit
     C                     ELSE
     C                     Z-ADD##AMT     C#AMT            * rec ie debit
     C                     END
      *
      * Module
     C                     MOVEL'DL'      C#MOD
      *
      * Olpos record number to update
      * '01' for FX
      * '02' for Interest movements
      * '03' for MM loans and deposits
     C                     MOVE '01'      C#OLRC
      *
      * Value date and posting date
     C                     Z-ADD##VDAT    C#VDAT
     C                     Z-ADD##VDAT    C#PSTD
      *
      * Indicator to show if removal allowed Y, N or " "
     C                     MOVEL'Y'       C#RPST
      *
      * Removal set number
     C                     Z-ADD##RPNB    C#RPNB
      *
      * Removal type "C" = Credit entries
      *              "D" = Debit entries
     C                     MOVEL'C'       C#RTYP
      *
      * Booking branch
     C                     MOVEL##BBRN    C#BBRN
      *
      *  If settlement details are present
      *  AOSPOSU0 will be called to update PRONO, MEMOS, RSACMTPD, OLPOS
      *
      *  OTHERWISE
      *
      *  If no settlement details are present
      *  AOSPOSU0 will be called to only update OLPOS
      *
     C           C#ATYP    IFEQ *BLANK
      *
      * Set account type indicator to signify no settlement
     C                     MOVEL'X'       C#ATYP
      *
      * Set update indicators for no update
     C                     MOVE 'N'       C#PRON
     C                     MOVE 'N'       C#MEMS
     C                     MOVE 'N'       C#RSAC
     C                     END
      *                                                                                       CAP205
      ** Set C#MTYP to ##MTYP                                                                 CAP205
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL##MTYP    C#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *
      *  Call AOSPOSU0
      *
     C                     CALL 'AOSPOSU0'             90
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*ADD    'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE  8
     C                     MOVEL'AOSPOSU0'W0KEY  29
     C                     Z-ADD902       W0ERNB  30
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
     C           EXMOVE    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRUPD    : Update database with shadow postings in store     *
      *                                                               *
      *  CALLED BY: Various                                           *
      *                                                               *
      *  CALLS    : SRERR - Report errors                             *
      *                                                               *
      *****************************************************************
     C           SRUPD     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRUPD'   @STK,Q
      *
      *................................................................
      *
      *  Call AOSPOSU0 to update database
      *
     C                     CALL 'AOSPOSU0'             90
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*UPDATE 'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE
     C                     MOVEL'AOSPOSU0'W0KEY
     C                     Z-ADD903       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
      *  Unwind subroutine stack name
      *
     C           EXUPD     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRCLR    : Clear all postings in store                       *
      *                                                               *
      *  CALLED BY: UPDATE - Update database                          *
      *                                                               *
      *  CALLS    : SRERR - Report errors                             *
      *                                                               *
      *****************************************************************
     C           SRCLR     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRCLR'   @STK,Q
      *
      *................................................................
      *
      *  Clear arrays in AOSPOSU0
      *
     C                     CLEARC#DTA
     C                     CALL 'AOSPOSU0'             90
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*CLEAR  'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE
     C                     MOVEL'AOSPOSU0'W0KEY
     C                     Z-ADD901       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
      *  Unwind subroutine stack name
      *
     C           EXCLR     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRERR    : Report error and close down program               *
      *                                                               *
      *  CALLED BY: Various                                           *
      *  CALLS    : *NONE                                             *
      *                                                               *
      *****************************************************************
     C           SRERR     BEGSR
      *
      *  Dump program
      *
     C                     DUMP
      *
      *  Close down program
      *
     C                     SETON                     LR
     C                     MOVEL'*ERROR*' ##RTCD
     C                     RETRN
      *
     C           EXERR     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *................................................................
      *
      **  Initialise work fields
     C                     MOVEL'P'       ##PAY
     C                     MOVEL'R'       ##REC
     C                     MOVEL*BLANKS   R#RTN   7
      *
      **  Set Date Format Indicator
      *
     C                     MOVE '0'       *IN98
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
      *
     C*                                                                   CEU003
     C** Access Switchable Feature Details                                CEU003
     C*                                                                   CEU003
     C                     CALL 'AOSARDR0'                                CEU003
     C                     PARM *BLANKS   PRTCD   7                       CEU003
     C                     PARM '*VERIFY 'POPTN   7                       CEU003
     C                     PARM 'CEU003'  PSARD   6                       CEU003
     C           SCSARD    PARM SCSARD    DSSDY                           CEU003
     C*                                                                   CEU003
     C           PRTCD     IFEQ *BLANKS                                   CEU003
     C                     MOVE 'Y'       CEU003  1                       CEU003
     C                     ELSE                                           CEU003
     C                     MOVE 'N'       CEU003                          CEU003
     C                     ENDIF                                          CEU003
      *                                                                                       CAP205
      ** Access Switchable Feature Details                                                    CAP205
      *                                                                                       CAP205
     C                     CALL 'AOSARDR0'                                                    CAP205
     C                     PARM *BLANKS   PRTCD                                               CAP205
     C                     PARM '*VERIFY 'POPTN                                               CAP205
     C                     PARM 'CAP205'  PSARD                                               CAP205
     C           SCSARD    PARM SCSARD    DSSDY                                               CAP205
      *                                                                                       CAP205
     C           PRTCD     IFEQ *BLANKS                                                       CAP205
     C                     MOVE 'Y'       CAP205  1                                           CAP205
     C                     ELSE                                                               CAP205
     C                     MOVE 'N'       CAP205                                              CAP205
     C           PRTCD     IFNE '*NRF'                                                        CAP205
     C                     MOVEL'SCSARDPD'W0FILE                                              CAP205
     C                     MOVEL'CAP205'  W0KEY                                               CAP205
     C                     Z-ADD906       W0ERNB                                              CAP205
     C                     EXSR SRERR                                                         CAP205
     C                     ENDIF                                                              CAP205
     C                     ENDIF                                                              CAP205
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /SPACE 5
     C/COPY ZSRSRC,ZDATE2Z2
      /SPACE 5
     C/COPY ZSRSRC,ZDATE2Z3
** ##NAR  **      NARRATIVES
FRA Interest Settlement
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
