     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Update of position sett posn files (SE)')     *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Access Objects Module                                *
     F*                                                               *
     F*  AOOUPSU0 - On-line Update of (SE) Posn Setlmt Positions files*
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CAP205             Date 03Mar10               *
      *                 CAP204             Date 09Feb10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 247870             Date 06Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CLE025             Date 20Oct03               *
      *                 CRE020             Date 20Jan04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 102929              Date  30Aug01             *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 105034              Date  02Aug96             *
     F*                 088311              Date  26May95             *
     F*                 082738 *CREATE      DATE  03FEB95             *
     F*                                                               *
     F*---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *  CAP204 - Teller Related APIs - Account Transfer              *
      *  247870 - No narrative on Projected Account Movements enquiry *
      *           for Position Settlements entries.                   *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CLE025 - Credit Lines (Recompile)                            *
      * CRE020 - Midas Plus Online Printing of Advices (GE7)          *
      *  CGL029 - Increase Account Code to 10 digits                  *
      * 102929 - Use Value Date if Switchable Feature S01512 is       *
      *          active instead of Due Date for the Value date        *
      *          of the Projected Movement.                           *
     F* 105034 - Correctly verify nostro number when amending sett.   *
     F*          account for settlement methods 01,02,07,08 and 12,   *
     F*          else changes not accepted.                           *
     F* 088311 - Build the settlement account for settlement method 04*
     F* 082738 - New program for redesign of update to PRONO, MEMOS,  *
     F*          OLPOS & RSACMTPD.                                    *
     F*                                                               *
     F*****************************************************************
     FSECTY   IF  E           K        DISK
     FINVTP   IF  E           K        DISK
     FPOSETDY3UF  E           K        DISK         KCOMIT                                    CRE020
     F/COPY WNCPYSRC,AOH00057
     E/SPACE 5
     E*
     E**  (Posting) Naratives
     E                    ##NAR   1   5 30
     E*
     E*  Subroutine stack
     E                    @STK       20 10
     E/COPY WNCPYSRC,AOH00058
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     I/SPACE 5
     ISECTYDF                                                                                 CRE020
     I              TNLU                            STNL1                                     CRE020
     IINVTPDF                                                                                 CRE020
     I              TNLU                            ITNL1                                     CRE020
      *                                                                                       CRE020
     IZ#BSTS    E DSZ#BST
     IZ#ASTS    E DSZ#AST
     I*
     IPOSETF    E DSPOSETD
     I/COPY WNCPYSRC,AOH00059
     I##PAYD      DS
     I                                        1   1 ##PAY
     I                                        2   3 ##PAYM
     I**********                              4  15 ##PAYA                                    CGL029
     I**********                             16  18 ##PAYB                                    CGL029
     I                                        4  21 ##PAYA                                    CGL029
     I                                       22  24 ##PAYB                                    CGL029
     I##RECD      DS
     I                                        1   1 ##REC
     I                                        2   3 ##RECM
     I**********                              4  15 ##RECA                                    CGL029
     I**********                             16  18 ##RECB                                    CGL029
     I                                        4  21 ##RECA                                    CGL029
     I                                       22  24 ##RECB                                    CGL029
     I##SETD      DS
      * pay/receive (P/R)
     I                                        1   1 ##PYRC
      * settlement method
     I                                        2   3 ##SETM
      * settlement account
     I**********                              4  15 ##SETA                                    CGL029
     I                                        4  21 ##SETA                                    CGL029
     I                                        4   5 ##NOSN
     I                                        4  13 ##ACNO
     I                                        4   9 ##CNUM
     I**********                             10  13 ##ACOD                                    CGL029
     I**********                             14  15 ##ACSQ                                    CGL029
     I                                       10  19 ##ACOD                                    CGL029
     I                                       20  21 ##ACSQ                                    CGL029
      * settlement branch
     I**********                             16  18 ##BRCA                                    CGL029
     I                                       22  24 ##BRCA                                    CGL029
      *
      *  Calling parameter data structure for AOSPOSU0
      *
     IC#DTA       DS                            256
      *  Account type N=nostro nbr F=Full nostro G=General Ledger
      *               P=Partial Ledger R= Retail Number  X= no settlement
     I                                        1   1 C#ATYP
      *               Full General Ledger - CNUM/CCY/ACOD/ACSQ
     I                                        2  19 C#ACCT
     I**********                              2   70C#CNU1                                    CSD027
     I                                        2   7 C#CNU1                                    CSD027
     I                                        8  10 C#CCY1
     I**********                             11  140C#ACO1                                    CGL029
     I                                       11  140QQACO1                                    CGL029
     I                                       15  160C#ACS1
     I                                       17  19 C#BRC1
      *               Nostro number
     I                                        2   30C#NOSN
      *               Full Nostro - CCY/NOSN
     I                                        2   6 C#NOS
      *               Retail Number
     I                                        2  110C#ACNO
      *               Partial Account - CNUM/ACOD/ACSQ
     I**********                              2   70C#CNUM                                    CSD027
     I                                        2   7 C#CNUM                                    CSD027
     I**********                              8  110C#ACOD                                    CGL029
     I                                        8  110QQACOD                                    CGL029
     I                                       12  130C#ACSQ
      *               Branch of account if Partial                      '
     I                                       14  16 C#BRCA
      *               OLPOS Incoming/Outgoing Indicator 'I' or 'O' or ' '
      *                     ' ' no OLPOS update
     I                                       24  24 C#IO
      *               Currency of transaction
     I                                       25  27 C#CCY
      *               Transaction Type
     I                                       28  29 C#TRYP
      *               Transaction Narrative
     I                                       30  59 C#NARR
      *               Cheque Number
     I                                       60  670C#CHQN
      *               FT transaction reference
     I                                       68  82 C#PREF
      *               DL/LE/GL/RE transaction reference
     I                                       83  88 C#TRN
      *               Movement amount
     I                                    P  89  960C#AMT
      *               Processing indicators Not blank = no update
      *                                     ' ' = Update
      *               Prono update indicator
     I                                       97  97 C#PRON
      *               Olpos update indicator
     I                                       98  98 C#OLPS
      *               Memos update indicator
     I                                       99  99 C#MEMS
      *               Rsacmtpd write indicator
     I                                      100 100 C#RSAC
      *               Module
     I                                      101 102 C#MOD
      *               Olpos record number to update
      *                     '01' for FX
      *                     '02' for Interest movements
      *                     '03' for loans and deposits
     I                                      103 104 C#OLRC
      *               FT Memos ledger balance override
      *                        'Y' update ledger balance irrespective of
      *                        posting date
     I                                      105 105 C#FTOV
      *               Value date and posting date
     I                                    P 106 1080C#VDAT
     I                                    P 109 1110C#PSTD
      *               Override posting date correction
      *                        for forward valued items
      *                        'Y' prevents date being changed
     I                                      112 112 C#PSTO
      *               Indicator to show if removal allowed Y, N or " "
     I                                      113 113 C#RPST
      *               Removal set number
     I                                      114 1160C#RPNB
      *               Removal type "C" = Credit entries
      *                            "D" = Debit entries
     I                                      117 117 C#RTYP
      *               Booking branch
     I                                      118 120 C#BBRN
      *                                                                                       CGL029
      ** Account Code for Full Acount                                                         CGL029
      *                                                                                       CGL029
     I                                      121 1300C#ACO1                                    CGL029
      *                                                                                       CGL029
      ** Account code for Partial Account                                                     CGL029
      *                                                                                       CGL029
     I                                      131 1400C#ACOD                                    CGL029
      *                                                                                       CAP204
      ** Other Transaction Reference                                                          CAP204
      *                                                                                       CAP204
     I                                      141 160 C#OTR1                                    CAP204
      *                                                                                       CAP205
      ** Movement type                                                                        CAP205
      *                                                                                       CAP205
     I                                      161 161 C#MTYP                                    CAP205
      *                                                                                       CLE134
      ** Exception Reference Id/Number                                                        CLE134
      *                                                                                       CLE134
     I                                      162 164 C#XRFI                                    CLE134
     I                                      165 1740C#XRFN                                    CLE134
      *
     ISDRETL    E DSSDRETLPD                                              088311
     I**  Retail Details                                                  088311
     IDSFDY     E DSDSFDY
     IDSSDY     E DSDSSDY
     I*
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I/COPY WNCPYSRC,AOOUPSU0I1
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRMAIN - Main Processing                       *
      *****************************************************************
      *
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           ##RTCD  7
     C                     PARM           Z#BSTS
     C                     PARM           Z#ASTS
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      *  Main processing
     C                     EXSR SRMAIN
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C                     RETRN
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
      ** Clear down all shadow postings in store (within AOSPOSU0)
      *
     C                     EXSR SRCLR
      *
     C                     MOVELZ#BSTS    #BRCID  2
     C                     MOVELZ#ASTS    #ARCID  2
      *                                                                                       CRE020
     C           CRE020    IFEQ 'Y'                                                           CRE020
     C                     MOVE 'N'       W2POST                                              CRE020
     C                     ENDIF                                                              CRE020
      *
      *-------------------------------------------------------------------
      *
      ** GENERATE 'BEFORE STATUS' ACCOUNT MOVEMENTS
      ** (if not insertion, when __RCID will be blank)
      *
     C           #BRCID    IFNE *BLANK
      *
     C                     MOVEL'YES'     ##REVI  3
     C                     MOVELZ#BSTS    POSETF
      *                                                                                       CRE020
      ** Store TNLU before update.  This TNLU is use to chain POSETDX3.                       CRE020
      *                                                                                       CRE020
     C           CRE020    IFEQ 'Y'                                                           CRE020
     C                     MOVE TNLU      KTNLU                                               CRE020
     C                     ENDIF                                                              CRE020
      *
      ** Access security details
      *
     C                     MOVELPSSH      SESN
     C                     EXSR SRSECT
      *
      ** Position settlement processing
      *
     C                     EXSR SRPOST
      *
      ** If latest TNLU is not equal to previous TNLU, update latest TNLU on                  CRE020
      ** extension file to keep up to date                                                    CRE020
      *                                                                                       CRE020
     C           CRE020    IFEQ 'Y'                                                           CRE020
     C           TNLU      ANDNEKTNLU                                                         CRE020
     C           KPOST3    CHAINPOSETDD3             78                                       CRE020
     C           *IN78     IFEQ '0'                                                           CRE020
     C                     MOVE TNLU      P3TNLU                                              CRE020
     C                     MOVE TNLU      KTNLU                                               CRE020
     C                     UPDATPOSETDD3                                                      CRE020
     C                     ENDIF                                                              CRE020
     C                     ENDIF                                                              CRE020
     C                     MOVE 'Y'       W2POST  1                                           CRE020
      *                                                                                       CRE020
     C                     END                             * Z#BSTS
      *-------------------------------------------------------------------
      *
      ** GENERATE 'AFTER STATUS' ACCOUNT MOVEMENTS
      ** (if not deletion, when __CHTP will be 'D')
      *
     C                     MOVEL*BLANK    ##REVI
     C                     MOVELZ#ASTS    POSETF
     C           CRE020    IFEQ 'Y'                                                           CRE020
     C           W2POST    IFEQ 'N'                                                           CRE020
     C                     MOVE TNLU      KTNLU                                               CRE020
     C                     ENDIF                                                              CRE020
     C                     ENDIF                                                              CRE020
     C           CHTP      IFNE 'D'
      *
      ** Access security details
      *
     C                     MOVELPSSH      SESN
     C                     EXSR SRSECT
      *
      ** Position settlement processing
      *
     C                     EXSR SRPOST
      *
     C                     END
      *-------------------------------------------------------------------
      *
      ** Update database with shadow postings in store (within AOSPOSU0)
      *
     C                     EXSR SRUPD
      *
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRPOST                                         *
      * Function    :  Position Settlement Processing                 *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  AOSPOSU0 - On-line Positions Update            *
      *****************************************************************
     C           SRPOST    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRPOST'  @STK,Q
      *
      *................................................................
      *
      **  Store settlement method, account & branch
      *
     C           PPRE      IFEQ 'P'
     C                     MOVELPSMT      ##PAYM           * pay method
     C                     MOVELPSEA      ##PAYA           * pay account
     C                     MOVELPSEB      ##PAYB           * pay branch
     C                     MOVEL##PAYD    ##SETD           * Pay details
     C                     ELSE
     C                     MOVELPSMT      ##RECM           * rec method
     C                     MOVELPSEA      ##RECA           * rec account
     C                     MOVELPSEB      ##RECB           * rec branch
     C                     MOVEL##RECD    ##SETD           * Rec details
     C                     END
      *
      **  Store basic position settlement details
      *
     C                     MOVELPEVT      ##TRYP  2        * transact type
     C           CRE020    IFEQ 'Y'                                                           CRE020
     C                     MOVELPCPY      ##TRN                                               CRE020
     C                     ELSE                                                               CRE020
     C**********           MOVEL*BLANK    ##TRN   6        * transact no.                     247870
     C                     MOVEL'000000'  ##TRN   6                                           247870
     C                     ENDIF                                                              CRE020
     C                     MOVELPBCA      ##BBRN  3        * booking branch
      *
      ** Generate account movement
      ** =========================
      *
     C** If S01512 is active, use Payment/Value Date of the Pos. Sett.                        102929
     C*                                                                                       102929
     C           S01512    IFEQ 'Y'                                                           102929
     C                     Z-ADDPSVL      ##VDAT           * value date                       102929
     C                     ELSE                                                               102929
     C                     Z-ADDPDUD      ##VDAT  50       * value date
     C                     ENDIF                                                              102929
     C*                                                                                       102929
     C           PSAT      IFEQ *ZERO
     C                     Z-ADDPAMD      ##AMT  150       * amount due in
     C                     MOVELPNCY      ##CCY   3        * nominal ccy
     C                     ELSE
     C                     Z-ADDPSAT      ##AMT            * settlemt amt in
     C                     MOVELPSCU      ##CCY            * settlement ccy
     C                     END
      *
     C                     Z-ADD001       ##RPNB  30
      *
     C           PEVT      IFEQ 'CP'
     C**********           MOVEL##NAR,1   ##NARR 30        * coupon                           247870
     C                     MOVEL##NAR,1   NAR20  20        * coupon                           247870
     C                     ELSE
     C           PEVT      IFEQ 'DV'
     C**********           MOVEL##NAR,2   ##NARR           * dividend                         247870
     C                     MOVEL##NAR,2   NAR20            * dividend                         247870
     C                     ELSE
     C           PEVT      IFEQ 'PP'
     C**********           MOVEL##NAR,3   ##NARR           * part pay                         247870
     C                     MOVEL##NAR,3   NAR20            * part pay                         247870
     C                     ELSE
     C           PEVT      IFEQ 'MP'
     C**********           MOVEL##NAR,4   ##NARR           * mort pay                         247870
     C                     MOVEL##NAR,4   NAR20            * mort pay                         247870
     C                     ELSE
     C**********           MOVEL##NAR,5   ##NARR           * maturity                         247870
     C                     MOVEL##NAR,5   NAR20            * maturity                         247870
     C                     END
     C                     END
     C                     END
     C                     END
      *                                                                                       247870
      ** Include security shortname to be more descriptive                                    247870
      *                                                                                       247870
     C                     MOVE SESN      NAR20                                               247870
     C                     MOVELNAR20     ##NARR 30                                           247870
      *
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       ##MTYP  1                                           CAP205
     C                     ENDIF                                                              CAP205
     C                     EXSR SRMOVE
      *................................................................
      *
     C           EXTRAD    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRSECT                                         *
      * Function    :  Get Security and Investment Type Details       *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRSECT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRSECT'  @STK,Q
      *
      *................................................................
      *
      ** Access security details
      *
     C           SESN      CHAINSECTYDF              99    *
     C           *IN99     IFEQ '1'
     C                     MOVEL'SECTYD  'W0FILE
     C                     MOVELSESN      W0KEY
     C                     Z-ADD001       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      ** Access investment type details
      *
     C           INVTPK    KLIST
     C                     KFLD           NMCY
     C                     KFLD           SITP
     C           INVTPK    CHAININVTPDF              99    *
     C           *IN99     IFEQ '1'
     C                     MOVEL'INVTYPD 'W0FILE
     C                     MOVELNMCY      W0KEY
     C                     MOVE SITP      W0KEY
     C                     Z-ADD002       W0ERNB
     C                     EXSR SRERR
     C                     END
      *................................................................
      *
     C           EXSECT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRMOVE                                         *
      * Function    :  Pass Movement To Write Program                 *
      *                                                               *
      * Called by   :  Various                                        *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRMOVE    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRMOVE'  @STK,Q
      *
      *................................................................
      *
      ** Bypass if no amount to post
      *
     C           ##AMT     CABEQ*ZERO     EXMOVE
      *
      ** Determine account movement settlement details
      *
     C                     CLEARC#DTA
      *
     C           ##SETM    IFEQ '01'
     C           ##SETM    OREQ '02'
     C           ##SETM    OREQ '07'
     C           ##SETM    OREQ '08'
     C           ##SETM    OREQ '12'
     C                     MOVEL'N'       C#ATYP
     C***********          MOVEL##NOSN    C#NOSN
     C                     MOVEL##SETA    C#NOS6  6                       105034
     C                     MOVE C#NOS6    C#NOS1  1                       105034
     C           C#NOS1    IFNE *BLANK                                    105034
     C                     MOVE ##SETA    C#NOSN                          105034
     C                     ELSE                                           105034
     C                     MOVEL##SETA    C#NOS5  5                       105034
     C                     MOVELC#NOS5    C#NOS3  3                       105034
     C                     MOVE C#NOS3    C#NO1B  1                       105034
     C           C#NO1B    IFNE *BLANK                                    105034
     C                     MOVE C#NOS5    C#NOSN                          105034
     C                     ELSE                                           105034
     C                     MOVELC#NOS5    C#NOSN                          105034
     C                     END                                            105034
     C                     END                                            105034
     C                     ELSE
     C           ##SETM    IFEQ '14'
     C                     MOVEL'R'       C#ATYP
     C                     MOVEL##ACNO    C#ACNO
     C                     ELSE
     C***********##SETM    IFEQ '04'                                      088311
     C***********##SETM    OREQ '05'                                      088311
     C           ##SETM    IFEQ '05'                                      088311
     C           ##SETM    OREQ '15'
     C                     MOVEL'P'       C#ATYP
     C                     MOVEL##CNUM    C#CNUM
     C                     MOVEL##ACOD    C#ACOD
     C                     MOVEL##ACSQ    C#ACSQ
     C                     MOVEL##BRCA    C#BRCA
     C                     ELSE                                           088311
     C           ##SETM    IFEQ '04'                                      088311
     C                     CALL 'AORETLR0'                                088311
     C                     PARM '*DBERR ' @RTCD   7                       088311
     C                     PARM '*FIRST ' @OPTN   7                       088311
     C           SDRETL    PARM SDRETL    DSFDY                           088311
     C                     MOVEL'P'       C#ATYP                          088311
     C                     MOVELPCPY      C#CNUM                          088311
     C                     MOVELBMACCD    C#ACOD                          088311
     C                     Z-ADD1         C#ACSQ                          088311
     C                     MOVEL##BRCA    C#BRCA                          088311
     C                     END                                            088311
     C                     END
     C                     END
     C                     END
      *
      * OLPOS Incoming/Outgoing Indicator 'I' or 'O'
     C           ##PYRC    IFEQ 'P'
     C                     MOVEL'O'       C#IO             * outgoing
     C                     ELSE
     C                     MOVEL'I'       C#IO             * incoming
     C                     END
      *
      * Currency of transaction
     C                     MOVEL##CCY     C#CCY
      *
      * Transaction type
     C           PEVT      IFEQ 'MT'                                                          247870
     C                     MOVE 'MA'      ##TRYP                                              247870
     C                     ENDIF                                                              247870
      *                                                                                       247870
     C                     MOVEL##TRYP    C#TRYP
      * Transaction Narrative
     C/COPY WNCPYSRC,AOOUPSU0C3
     C                     MOVEL##NARR    C#NARR
     C/COPY WNCPYSRC,AOH00060
     C           ##REVI    IFEQ 'YES'
     C                     MOVE '*'       C#NARR
     C                     END
      *
      * SE/DL/LE/GL/RE transaction reference
     C                     MOVEL##TRN     C#TRN
      *
      * Movement amount
     C           ##PYRC    IFEQ 'P'
     C           ##REVI    ANDNE'YES'
     C           ##PYRC    OREQ 'R'
     C           ##REVI    ANDEQ'YES'
     C                     Z-SUB##AMT     C#AMT            * pay ie credit
     C                     ELSE
     C                     Z-ADD##AMT     C#AMT            * rec ie debit
     C                     END
      *
      * Module
     C                     MOVEL'SE'      C#MOD
      *
      * Olpos record number to update
      * '01' for FX
      * '02' for Interest movements
      * '03' for loans and deposits
     C                     MOVE '03'      C#OLRC
      *
      * Value date and posting date
     C                     Z-ADD##VDAT    C#VDAT
     C                     Z-ADD##VDAT    C#PSTD
      *
      * Indicator to show if removal allowed Y, N or " "
     C                     MOVEL'Y'       C#RPST
      *
      * Removal set number
     C                     Z-ADD##RPNB    C#RPNB
      *
      * Removal type "C" = Credit entries
      *              "D" = Debit entries
     C                     MOVEL'C'       C#RTYP
      *
      * Booking branch
     C                     MOVEL##BBRN    C#BBRN
      *                                                                                       CRE020
      ** FT Transaction reference = Security Shortname                                        CRE020
      *                                                                                       CRE020
     C           CRE020    IFEQ 'Y'                                                           CRE020
     C                     MOVELSESN      C#PREF                                              CRE020
     C                     EXSR SRTNLU                                                        CRE020
     C                     MOVE WTNLU     C#PREF                                              CRE020
     C                     ENDIF                                                              CRE020
      *
      *  If settlement details are present
      *  AOSPOSU0 will be called to update PRONO, MEMOS, RSACMTPD, OLPOS
      *
      *  OTHERWISE
      *
      *  If no settlement details are present
      *  AOSPOSU0 will be called to only update OLPOS
      *
     C           C#ATYP    IFEQ *BLANK
      *
      * Set account type indicator to signify no settlement
     C                     MOVEL'X'       C#ATYP
      *
      * Set update indicators for no update
     C                     MOVE 'N'       C#PRON
     C                     MOVE 'N'       C#MEMS
     C                     MOVE 'N'       C#RSAC
     C                     END
      *                                                                                       CAP205
      ** Set C#MTYP to ##MTYP                                                                 CAP205
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL##MTYP    C#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *
      *  Call AOSPOSU0
      *
     C                     CALL 'AOSPOSU0'             9090
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*ADD    'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE  8
     C                     MOVEL'AOSPOSU0'W0KEY  29
     C                     Z-ADD902       W0ERNB  30
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
     C           EXMOVE    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRUPD    : Update database with shadow postings in store     *
      *                                                               *
      *  CALLED BY: Various                                           *
      *                                                               *
      *  CALLS    : SRERR - Report errors                             *
      *                                                               *
      *****************************************************************
     C           SRUPD     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRUPD'   @STK,Q
     C/COPY WNCPYSRC,AOOUPSU0C1
      *
      *................................................................
      *
      *  Call AOSPOSU0 to update database
      *
     C                     CALL 'AOSPOSU0'             9090
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*UPDATE 'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE
     C                     MOVEL'AOSPOSU0'W0KEY
     C                     Z-ADD903       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
      *  Unwind subroutine stack name
      *
     C           EXUPD     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRCLR    : Clear all postings in store                       *
      *                                                               *
      *  CALLED BY: UPDATE - Update database                          *
      *                                                               *
      *  CALLS    : SRERR - Report errors                             *
      *                                                               *
      *****************************************************************
     C           SRCLR     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRCLR'   @STK,Q
      *
      *................................................................
      *
      *  Clear arrays in AOSPOSU0
      *
     C                     CLEARC#DTA
     C                     CALL 'AOSPOSU0'             9090
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*CLEAR  'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE
     C                     MOVEL'AOSPOSU0'W0KEY
     C                     Z-ADD901       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
      *  Unwind subroutine stack name
      *
     C           EXCLR     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRERR    : Report error and close down program               *
      *                                                               *
      *  CALLED BY: Various                                           *
      *  CALLS    : *NONE                                             *
      *                                                               *
      *****************************************************************
     C           SRERR     BEGSR
      *
      *  Dump program
      *
     C                     DUMP
     C                     ROLBK
      *
      *  Close down program
      *
     C                     SETON                     LR
     C                     MOVEL'*ERROR*' ##RTCD
     C                     RETRN
      *
     C           EXERR     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************                       CRE020
      *                                                               *                       CRE020
      *  SRTNLU   : Determine original TNLU                           *                       CRE020
      *                                                               *                       CRE020
      *  CALLED BY: Various                                           *                       CRE020
      *  CALLS    : *NONE                                             *                       CRE020
      *                                                               *                       CRE020
      *****************************************************************                       CRE020
     C           SRTNLU    BEGSR                                                              CRE020
      *                                                                                       CRE020
     C           KPOST3    KLIST                                                              CRE020
     C                     KFLD           PBCA                                                CRE020
     C                     KFLD           PSSH                                                CRE020
     C                     KFLD           PCPY                                                CRE020
     C                     KFLD           PDUD                                                CRE020
     C                     KFLD           PEVT                                                CRE020
     C                     KFLD           KTNLU   50                                          CRE020
      *                                                                                       CRE020
     C           KPOST3    CHAINPOSETDD3             78                                       CRE020
     C           *IN78     IFEQ '0'                                                           CRE020
     C                     MOVE P3OTNL    WTNLU   5                                           CRE020
     C                     ELSE                                                               CRE020
     C                     MOVE *BLANKS   WTNLU                                               CRE020
     C                     ENDIF                                                              CRE020
      *                                                                                       CRE020
     C                     ENDSR                                                              CRE020
      *****************************************************************                       CRE020
      /SPACE 5                                                                                CRE020
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
     C/COPY WNCPYSRC,AOOUPSU0C2
      *
      *................................................................
      *
      **  Initialise work fields
     C                     MOVEL'P'       ##PAY
     C                     MOVEL'R'       ##REC
     C                     MOVEL*BLANKS   R#RTN   7
     C                     MOVE '0'       *IN98
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
      *
     C**  Access SAR details file to see if S01512 SAR is installed.                          102929
     C*                                                                                       102929
     C                     CALL 'AOSARDR0'                                                    102929
     C                     PARM *BLANKS   @RTCD                                               102929
     C                     PARM '*VERIFY' @OPTN                                               102929
     C                     PARM 'S01512'  @SARD   6                                           102929
     C*                                                                                       102929
     C           @RTCD     IFEQ *BLANK                                                        102929
     C                     MOVE 'Y'       S01512  1                                           102929
     C                     ELSE                                                               102929
     C                     MOVE 'N'       S01512                                              102929
     C                     END                                                                102929
      *                                                                                       102929
      **  Access SAR details file to see if CRE020 SAR is installed.                          CRE020
      *                                                                                       CRE020
     C                     CALL 'AOSARDR0'                                                    CRE020
     C                     PARM *BLANKS   @RTCD                                               CRE020
     C                     PARM '*VERIFY' @OPTN                                               CRE020
     C                     PARM 'CRE020'  @SARD                                               CRE020
      *                                                                                       CRE020
     C           @RTCD     IFEQ *BLANK                                                        CRE020
     C                     MOVE 'Y'       CRE020  1                                           CRE020
     C                     ELSE                                                               CRE020
     C                     MOVE 'N'       CRE020                                              CRE020
     C                     END                                                                CRE020
      *                                                                                       CRE020
      *                                                                                       CAP205
      **  Access SAR details file to see if CAP205 SAR is installed.                          CAP205
      *                                                                                       CAP205
     C                     CALL 'AOSARDR0'                                                    CAP205
     C                     PARM *BLANKS   @RTCD                                               CAP205
     C                     PARM '*VERIFY' @OPTN                                               CAP205
     C                     PARM 'CAP205'  @SARD                                               CAP205
      *                                                                                       CAP205
     C           @RTCD     IFEQ *BLANK                                                        CAP205
     C                     MOVE 'Y'       CAP205  1                                           CAP205
     C                     ELSE                                                               CAP205
     C                     MOVE 'N'       CAP205                                              CAP205
     C           @RTCD     IFNE '*NRF'                                                        CAP205
     C                     MOVEL'SCSARDPD'W0FILE                                              CAP205
     C                     MOVEL'AOSPOSU0'W0KEY                                               CAP205
     C                     Z-ADD904       W0ERNB                                              CAP205
     C                     EXSR SRERR                                                         CAP205
     C                     ENDIF                                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C/COPY WNCPYSRC,AOH00061
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
** ##NAR  **      NARRATIVES
INTEREST
DIVIDEND
PART PAYMENT                                                                                  247870
MORTGAGE PAYMENT                                                                              247870
MATURITY
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
