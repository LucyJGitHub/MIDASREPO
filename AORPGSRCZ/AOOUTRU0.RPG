     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO On-line update of (SE) trade posn files')     *
      *****************************************************************
      *                                                               *
      *  Midas - Access Objects Module                                *
      *                                                               *
      *  AOOUTRU0 - On-line Update of (SE) Trade Positions files      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. AR1067108          Date 11Dec12               *
      *  Prev Amend No. AR1001183A         Date 20Nov12               *
      *                 CLE134             Date 01Aug12               *
      *                 263420             Date 13Jan10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CAP205             Date 03Mar10               *
      *                 CAP204             Date 09Feb10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 247870             Date 06Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG12811           Date 04Dec06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 BUG3122            Date 17Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CSE035             Date 22Apr02               *
      *                 206316             Date 19Jul02               *
      * Midas Release 4.01 -------------------------------------------*
      *                  180707             Date 25Jul00              *
      * Midas Release 4 --------------- Base -------------------------*
      *                  CSE028             Date 06Jun01              *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                  154862             Date 04Feb99              *
      *                  130862             Date 18Oct98              *
      *                  108093             DATE  12FEB98             *
      *                  101217             DATE  26MAR96             *
      *                  097169             DATE  20DEC95             *
      *                  092961             DATE  04OCT95             *
      *                  082738             DATE  03FEB95             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1067108 - PMC1140 Component failed during Clean COB        *
      *              Applied fix AR1001183A                           *
      *  AR1001183A- Coupon rate was still used in computation of     *
      *              interest instead of using the details in FRN tab *
      *              (Reopen) (Recompile)                             *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  263420 - Problems Encountered when inputing Spot Rates.      *
      *           Similar fix with 251027. Ensure to close open files *
      *           upon return to calling program.                     *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *  CAP204 - Teller Related APIs - Account Transfer              *
      *  247870 - No narrative on Projected Account Movements enquiry *
      *           for Position Settlements entries. Similar fix of    *
      *           BUG12811.                                           *
      *  BUG12811 - Account Movements Enquiry narrative not meaningful*
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CSE035 - NX/NNX Coupon Payment Processing.                   *
      *           (some coding in copy source of ZSRSRC)              *
      *  206316 - Recompiled over changed ZLCDZ1.                     *
      *  180707 - No Position Settlements generated. Recompile over   *
      *           changes in /COPY ZNCDZ3.                            *
      *  CSE028 -  Standing Settlement Instructions Enhancement       *
      *  154862 - Spurious back dated interest projected a/c movements*
      *  130862 - Recompiled over ZLCDZ1 & ZDAYSZ2 changes.           *
      *  108093 - DB error in AOSPOSU0 caused by the corruption of    *
      *           field CHTP and also need to set up settlement A/C   *
      *           for settlement 04.                                  *
      *  101217 - Set up field for settlement method 15               *
      *  097169 - Incorrect 'CP' type movements produced.             *
      *  092961 - Recompile over ZDAYSZ2 for 086491                   *
      *           Wrong number of days calculated for day basis 1     *
      *           when start or end day is last day of February.      *
      * 082738 - New program for redesign of update to PRONO, MEMOS,  *
      *          OLPOS & RSACMTPD.                                    *
      *                                                               *
      *****************************************************************
     FSECTY   IF  E           K        DISK
     F/COPY WNCPYSRC,AOOUTRU0F1
     FINVTP   IF  E           K        DISK
     FTABTG10 IF  E           K        DISK
     FSEDEV   IF  E           K        DISK
     FSECEO   IF  E           K        DISK
     F            SEDEVDF                           KRENAMESEDEVDO
     F            SNDEVDF                           KRENAMESNDEVDO
     FSTDSED  IF  E           K        DISK
     F/COPY WNCPYSRC,AOOUTRU0F2
     E/SPACE 5
     E*
     E**  (Posting) Naratives
     E                    ##NAR   1   3 30
     E/COPY ZSRSRC,ZNCDZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZPOWER8Z1
     E*
     E                    ZF29   14  32  5 0             29TH FEB ARRAY
     E*
     E*  Subroutine stack
     E/COPY WNCPYSRC,AOH00062
     E                    @STK       20 10
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     I/SPACE 5
     ISEDEVDF
     I              NMCY                            NMCYSD
     I              CHTP                            CHTPX                 108093
     ISEDEVDO
     I              NMCY                            NMCYSD
     I              CHTP                            CHTPX                 108093
     ISECTYDF                                                             108093
     I              CHTP                            CHTPX                 108093
     IINVTPDF                                                             108093
     I              CHTP                            CHTPX                 108093
     ISTDSEDF                                                             108093
     I              CHTP                            CHTPX                 108093
     ITABTG10F                                                            108093
     I              CHTP                            CHTPX                 108093
     I/COPY ZSRSRC,ZNCDZ2
     IZ#BSTS    E DSZ#BST
     IZ#ASTS    E DSZ#AST
     I/COPY WNCPYSRC,AOH00063
     I*
     ITRADSF    E DSTRADSD
     I##PAYD      DS
     I                                        1   1 ##PAY
     I                                        2   3 ##PAYM
     I**********                              4  15 ##PAYA                                    CGL029
     I**********                             16  18 ##PAYB                                    CGL029
     I                                        4  21 ##PAYA                                    CGL029
     I                                       22  24 ##PAYB                                    CGL029
     I##RECD      DS
     I                                        1   1 ##REC
     I                                        2   3 ##RECM
     I**********                              4  15 ##RECA                                    CGL029
     I**********                             16  18 ##RECB                                    CGL029
     I                                        4  21 ##RECA                                    CGL029
     I                                       22  24 ##RECB                                    CGL029
     I##CSTI      DS
     I                                        1   1 ##CSPR
     I                                        2   3 ##CSTM
     I**********                              4  15 ##CSTA                                    CGL029
     I                                        4  21 ##CSTA                                    CGL029
     I**********                             10  13 ##CST1                             101217 CGL029
     I**********                             14  15 ##CST2                             101217 CGL029
     I**********                             16  18 ##CSTB                                    CGL029
     I                                       10  19 ##CST1                                    CGL029
     I                                       20  21 ##CST2                                    CGL029
     I                                       22  24 ##CSTB                                    CGL029
     I##DSTI      DS
     I                                        1   1 ##DSPR
     I                                        2   3 ##DSTM
     I**********                              4  15 ##DSTA                                    CGL029
     I**********                             16  18 ##DSTB                                    CGL029
     I                                        4  21 ##DSTA                                    CGL029
     I                                       22  24 ##DSTB                                    CGL029
     I##SETD      DS
      * pay/receive (P/R)
     I                                        1   1 ##PYRC
      * settlement method
     I                                        2   3 ##SETM
      * settlement account
     I**********                              4  15 ##SETA                                    CGL029
     I                                        4  21 ##SETA                                    CGL029
     I                                        4   5 ##NOSN
     I                                        4  13 ##ACNO
     I                                        4   9 ##CNUM
     I**********                             10  13 ##ACOD                                    CGL029
     I**********                             14  15 ##ACSQ                                    CGL029
     I                                       10  19 ##ACOD                                    CGL029
     I                                       20  21 ##ACSQ                                    CGL029
      * settlement branch
     I**********                             16  18 ##BRCA                                    CGL029
     I                                       22  24 ##BRCA                                    CGL029
      *
     I#SET4D      DS
      * Settlement account for 04                                         108093
     I**********                              1  12 #SET04                             108093 CGL029
     I                                        1  18 #SET04                                    CGL029
     I                                        1   6 #CNUM4                108093
     I**********                              7  10 #ACOD4                             108093 CGL029
     I**********                             11  12 #ACSQ4                             108093 CGL029
     I                                        7  16 #ACOD4                                    CGL029
     I                                       17  18 #ACSQ4                                    CGL029
      *                                                                   108093
      *  Calling parameter data structure for AOSPOSU0
      *
     IC#DTA       DS                            256
      *  Account type N=nostro nbr F=Full nostro G=General Ledger
      *               P=Partial Ledger R= Retail Number  X= no settlement
     I                                        1   1 C#ATYP
      *               Full General Ledger - CNUM/CCY/ACOD/ACSQ
     I                                        2  19 C#ACCT
     I**********                              2   70C#CNU1                                    CSD027
     I                                        2   7 C#CNU1                                    CSD027
     I                                        8  10 C#CCY1
     I**********                             11  140C#ACO1                                    CGL029
     I                                       11  140QQACO1                                    CGL029
     I                                       15  160C#ACS1
     I                                       17  19 C#BRC1
      *               Nostro number
     I                                        2   30C#NOSN
      *               Full Nostro - CCY/NOSN
     I                                        2   6 C#NOS
      *               Retail Number
     I                                        2  110C#ACNO
      *               Partial Account - CNUM/ACOD/ACSQ
     I**********                              2   70C#CNUM                                    CSD027
     I                                        2   7 C#CNUM                                    CSD027
     I**********                              8  110C#ACOD                                    CGL029
     I                                        8  110QQACOD                                    CGL029
     I                                       12  130C#ACSQ
      *               Branch of account if Partial                      '
     I                                       14  16 C#BRCA
      *               OLPOS Incoming/Outgoing Indicator 'I' or 'O' or ' '
      *                     ' ' no OLPOS update
     I                                       24  24 C#IO
      *               Currency of transaction
     I                                       25  27 C#CCY
      *               Transaction Type
     I                                       28  29 C#TRYP
      *               Transaction Narrative
     I                                       30  59 C#NARR
      *               Cheque Number
     I                                       60  670C#CHQN
      *               FT transaction reference
     I                                       68  82 C#PREF
      *               DL/LE/GL/RE transaction reference
     I                                       83  88 C#TRN
      *               Movement amount
     I                                    P  89  960C#AMT
      *               Processing indicators Not blank = no update
      *                                     ' ' = Update
      *               Prono update indicator
     I                                       97  97 C#PRON
      *               Olpos update indicator
     I                                       98  98 C#OLPS
      *               Memos update indicator
     I                                       99  99 C#MEMS
      *               Rsacmtpd write indicator
     I                                      100 100 C#RSAC
      *               Module
     I                                      101 102 C#MOD
      *               Olpos record number to update
      *                     '01' for FX
      *                     '02' for Interest movements
      *                     '03' for loans and deposits
     I                                      103 104 C#OLRC
      *               FT Memos ledger balance override
      *                        'Y' update ledger balance irrespective of
      *                        posting date
     I                                      105 105 C#FTOV
      *               Value date and posting date
     I                                    P 106 1080C#VDAT
     I                                    P 109 1110C#PSTD
      *               Override posting date correction
      *                        for forward valued items
      *                        'Y' prevents date being changed
     I                                      112 112 C#PSTO
      *               Indicator to show if removal allowed Y, N or " "
     I                                      113 113 C#RPST
      *               Removal set number
     I                                      114 1160C#RPNB
      *               Removal type "C" = Credit entries
      *                            "D" = Debit entries
     I                                      117 117 C#RTYP
      *               Booking branch
     I                                      118 120 C#BBRN
      *                                                                                       CGL029
      ** Account Code for Full Account                                                        CGL029
      *                                                                                       CGL029
     I                                      121 1300C#ACO1                                    CGL029
      *                                                                                       CGL029
      ** Account Code for Partial Account                                                     CGL029
      *                                                                                       CGL029
     I                                      131 1400C#ACOD                                    CGL029
      *                                                                                       CAP204
      ** Other Transaction Reference                                                          CAP204
      *                                                                                       CAP204
     I                                      141 160 C#OTR1                                    CAP204
      *                                                                                       CAP205
      ** Movement type                                                                        CAP205
      *                                                                                       CAP205
     I                                      161 161 C#MTYP                                    CAP205
      *                                                                                       CLE134
      ** Exception Reference Id/Number                                                        CLE134
      *                                                                                       CLE134
     I                                      162 164 C#XRFI                                    CLE134
     I                                      165 1740C#XRFN                                    CLE134
      *
     ISDBANK    E DSSDBANKPD
     I** Bank details
     I*
     ISDSECS    E DSSDSECSPD
     I** Security Customer details
     ISDRETL    E DSSDRETLPD                                              108093
     I              QQACCD                          WQQACD                                    CGL029
     I** Retail ICD                                                       108093
     I*
     IDSFDY     E DSDSFDY
     IDSSDY     E DSDSSDY
     I*
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I/COPY WNCPYSRC,AOOUTRU0I1
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRMAIN - Main Processing                       *
      *****************************************************************
      *
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           ##RTCD  7
     C                     PARM           Z#BSTS
     C                     PARM           Z#ASTS
      *                                                                                       CSE035
      ** Access SAR details file to see if CSE035 is installed                                CSE035
      *                                                                                       CSE035
     C                     CALL 'AOSARDR0'                                                    CSE035
     C                     PARM *BLANKS   @RTCD                                               CSE035
     C                     PARM '*VERIFY' @OPTN                                               CSE035
     C                     PARM 'CSE035'  WSARD   6                                           CSE035
      *                                                                                       CSE035
     C           @RTCD     IFEQ *BLANK                                                        CSE035
     C                     MOVE 'Y'       CSE035  1                                           CSE035
     C                     ELSE                                                               CSE035
     C                     MOVE 'N'       CSE035                                              CSE035
     C                     END                                                                CSE035
      *                                                                                       CSE035
      *                                                                                       CAP205
      ** Access SAR details file to see if CAP205 is installed                                CAP205
      *                                                                                       CAP205
     C                     CALL 'AOSARDR0'                                                    CAP205
     C                     PARM *BLANKS   @RTCD                                               CAP205
     C                     PARM '*VERIFY' @OPTN                                               CAP205
     C                     PARM 'CAP205'  WSARD                                               CAP205
      *                                                                                       CAP205
     C           @RTCD     IFEQ *BLANK                                                        CAP205
     C                     MOVE 'Y'       CAP205  1                                           CAP205
     C                     ELSE                                                               CAP205
     C                     MOVE 'N'       CAP205                                              CAP205
     C           @RTCD     IFNE '*NRF'                                                        CAP205
     C                     MOVEL'SCSARDPD'W0FILE                                              CAP205
     C                     MOVEL'CAP205'  W0KEY                                               CAP205
     C                     Z-ADD904       W0ERNB                                              CAP205
     C                     EXSR SRERR                                                         CAP205
     C                     ENDIF                                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C/COPY WNCPYSRC,AOOUTRU0C1
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      *  Main processing
     C                     EXSR SRMAIN
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C                     SETON                     LR                                       263420
      *
      **  Terminate program
     C                     RETRN
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
      ** Clear down all shadow postings in store (within AOSPOSU0)
      *
     C                     EXSR SRCLR
      *
     C                     MOVELZ#BSTS    #BRCID  2
     C                     MOVELZ#ASTS    #ARCID  2
      *
      *-------------------------------------------------------------------
      *
      ** GENERATE 'BEFORE STATUS' ACCOUNT MOVEMENTS
      ** (if not insertion, when __RCID will be blank)
      *
     C           #BRCID    IFNE *BLANK
      *
     C                     MOVEL'YES'     ##REVI  3
     C                     MOVELZ#BSTS    TRADSF
      *
      ** If trade is complete
      *
     C           TINC      IFEQ 'C'
      *
      ** Access security details
      *
     C                     MOVELTDSS      SESN
     C                     EXSR SRSECT
      *
      ** Access counterparty details
      *
     C                     MOVELTCNR      ##CUST  6
     C                     EXSR SRCUST
      *
      ** Depot
      *
     C           TDTP      IFEQ 'TP'
     C                     MOVELDELT      ##DEPT  6
     C                     ELSE
     C                     MOVELDELF      ##DEPT
     C                     END
      *
      ** Get default settlement instructions for customer and depot
      *
     C                     EXSR SRPSIN
      *
      ** Trade processing
      *
     C                     EXSR SRTRAD
      *
     C                     END                             * TINC
     C                     END                             * Z#BSTS
      *-------------------------------------------------------------------
      *
      ** GENERATE 'AFTER STATUS' ACCOUNT MOVEMENTS
      ** (if not deletion, when __CHTP will be 'D' and if trade is complete)
      *
     C                     MOVEL*BLANK    ##REVI
     C                     MOVELZ#ASTS    TRADSF
     C           CHTP      IFNE 'D'
     C           TINC      ANDEQ'C'
      *
      ** Access security details
      *
     C                     MOVELTDSS      SESN
     C                     EXSR SRSECT
      *
      ** Access counterparty details
      *
     C                     MOVELTCNR      ##CUST
     C                     EXSR SRCUST
      *
      ** Depot
      *
     C           TDTP      IFEQ 'TP'
     C                     MOVELDELT      ##DEPT
     C                     ELSE
     C                     MOVELDELF      ##DEPT
     C                     END
      *
      ** Get default settlement instructions for customer and depot
      *
     C                     EXSR SRPSIN
      *
      ** Trade processing
      *
     C                     EXSR SRTRAD
      *
     C                     END
      *-------------------------------------------------------------------
      *
      ** Update database with shadow postings in store (within AOSPOSU0)
      *
     C                     EXSR SRUPD
      *
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRTRAD                                         *
      * Function    :  Trade Processing                               *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  AOSPOSU0 - On-line Positions Update            *
      *****************************************************************
     C           SRTRAD    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRTRAD'  @STK,Q
      *
      *................................................................
      *
     C*                                                                   108093
     C*** If settlement method is 04 then build settlement A/C            108093
     C*                                                                   108093
     C           SMTH      IFEQ 04                                        108093
     C                     EXSR AORETL                                    108093
     C                     MOVE TCNR      #CNUM4                          108093
     C                     MOVE BMACCD    #ACOD4                          108093
     C                     MOVE '01'      #ACSQ4                          108093
     C           TDTP      IFEQ 'TS'                                      108093
     C           PAYT      IFEQ *BLANKS                                   108093
     C                     MOVE #SET04    PAYT                            108093
     C                     END                                            108093
     C                     ELSE                                           108093
     C           PYFM      IFEQ *BLANKS                                   108093
     C                     MOVE #SET04    PYFM                            108093
     C                     END                                            108093
     C                     END                                            108093
     C                     END                                            108093
      **  Store pay and receive settlement methods, accounts & branches
      *
     C                     MOVELSMTH      ##PAYM           * pay method
     C                     MOVELPYFM      ##PAYA           * pay account
     C                     MOVELPYFA      ##PAYB           * pay branch
     C                     MOVELSMTH      ##RECM           * rec method
     C                     MOVELPAYT      ##RECA           * rec account
     C                     MOVELPYTA      ##RECB           * rec branch
      *
      **  Store basic trade details
      *
     C                     MOVELTDTP      ##TRYP  2        * transact type
     C                     MOVELTDRF      ##TRN   6        * transact no.
     C                     MOVELTDBA      ##BBRN  3        * booking branch
      *
      **  Initialize work fields
      *
     C                     Z-ADD*ZERO     ##VDAT  50       * event value dte
     C                     Z-ADD*ZERO     ##AMT  150       * amount
     C                     MOVEL*BLANK    ##CCY   3        * currency
     C                     MOVEL*BLANK    ##NARR 30        * narrative
      *
      ** Generate VALUE DATE account movement
      ** ====================================
      *
     C                     Z-ADDTDVD      ##VDAT           * value date
     C           TVSN      ADD  TVSP      ##AMT            * countervalue in
     C                     ADD  TOSV      ##AMT              settlement ccy
     C                     MOVELSETC      ##CCY            * settlement ccy
      *
      ** Pass movement to write program (with appropriate instructions)
      *
     C           TDTP      IFEQ 'TP'
     C                     MOVEL##PAYD    ##SETD           * Pay details
     C                     ELSE
     C                     MOVEL##RECD    ##SETD           * Rec details
     C                     END
      *
     C                     Z-ADD001       ##RPNB  30       * 1=value date
     C**********           MOVEL##NAR,1   ##NARR                                            BUG12811
     C           SESN      CAT  ##NAR,1:1 ##NARR                                            BUG12811
      *
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'S'       ##MTYP  1                                           CAP205
     C                     ENDIF                                                              CAP205
     C                     EXSR SRMOVE
      *
      ** Generate INTEREST PAYMENT DATE account movement
      ** ===============================================
      *
      ** If the security is interest bearing
      *
     C           CD01      IFNE *ZERO
     C           ACIN      ANDNE'F'                                       097169
      *
      ** Determine the next coupon date after (or on) the trade value date
      ** (for ex-coupon trades it's the following coupon date)
      *
     C                     Z-ADDTDVD      ECD
     C                     EXSR ZNCD
     C           ACIN      IFEQ 'X'
     C           NCD       ADD  1         ECD
     C                     EXSR ZNCD
     C                     END
      *
      ** If this coupon date is in the past
      ** determine the next coupon date after (or on) today
      *
     C           NCD       IFLT BJRDNB
     C                     Z-ADDBJRDNB    ECD     50
     C                     EXSR ZNCD
     C                     END
      *
      ** Determine interest due at next coupon date
      ** (this will be put into field ZDCINT)
      *
     C                     EXSR SRDINT
      *
     C                     MOVEL'CP'      ##TRYP           * transact type
     C                     Z-ADDNCD       ##VDAT           * next coupon dat
     C                     Z-ADDZDCINT    ##AMT            * interest in
     C                     MOVELNMCY      ##CCY            * nominal ccy
      *
     C                     Z-ADD002       ##RPNB           * 2 = int. date
     C**********           MOVEL##NAR,2   ##NARR                                            BUG12811
     C           SESN      CAT  ##NAR,2:1 ##NARR                                            BUG12811
      *
      ** Pass movement to write program (with appropriate instructions)
      *
      ** Use either portfolio customer or depot settlement instructions
      *
     C           BFCLAS    IFEQ 'S'
     C           BFCLAS    OREQ 'D'
     C                     MOVEL##CSTI    ##SETD           * Cust. set detls
     C                     ELSE
     C                     MOVEL##DSTI    ##SETD           * Dept. set detls
     C                     END
      *
      ** Interest is received on interest payment date when purchased
      *
     C           TDTP      IFEQ 'TP'
     C                     MOVEL'R'       ##SETD
     C                     ELSE
     C                     MOVEL'P'       ##SETD
     C                     END
      *
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       ##MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
     C                     EXSR SRMOVE
      *
     C                     END                             ** CD01
      *
      ** Generate MATURITY DATE account movement
      ** =======================================
      *
      ** If maturity date present
      *
     C           MATY      IFNE *ZERO
     C           MATY      ANDNE99999
      *
     C                     MOVEL'MA'      ##TRYP           * transact type
     C                     Z-ADDMATY      ##VDAT           * Maturity date
     C                     Z-ADDNOML      ZNOML
     C                     Z-ADD100       ZPRC
     C                     EXSR ZCNSD
     C                     Z-ADDZCONS     ##AMT            * face value in
     C                     MOVELNMCY      ##CCY            * nominal ccy
      *
     C                     Z-ADD003       ##RPNB           * 3 = maty date
     C**********           MOVEL##NAR,3   ##NARR                                            BUG12811
     C           SESN      CAT  ##NAR,3:1 ##NARR                                            BUG12811
      *
      ** Pass movement to write program (with appropriate instructions)
      *
      ** Use either portfolio customer or depot settlement instructions
      *
     C           BFCLAS    IFEQ 'S'
     C           BFCLAS    OREQ 'D'
     C                     MOVEL##CSTI    ##SETD           * Cust. set detls
     C                     ELSE
     C                     MOVEL##DSTI    ##SETD           * Dept. set detls
     C                     END
      *
      ** Nominal is received at maturity date when purchased
      *
     C           TDTP      IFEQ 'TP'
     C                     MOVEL'R'       ##SETD
     C                     ELSE
     C                     MOVEL'P'       ##SETD
     C                     END
      *
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       ##MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
     C                     EXSR SRMOVE
      *
     C                     END                             ** MATY
      *................................................................
      *
     C           EXTRAD    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRSECT                                         *
      * Function    :  Get Security and Investment Type Details       *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRSECT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRSECT'  @STK,Q
      *
      *................................................................
      *
      ** Access security details
      *
     C           SESN      CHAINSECTYDF              99    *
     C           *IN99     IFEQ '1'
     C                     MOVEL'SECTYD  'W0FILE
     C                     MOVELSESN      W0KEY
     C                     Z-ADD002       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      ** Access investment type details
      *
     C           INVTPK    KLIST
     C                     KFLD           NMCY
     C                     KFLD           SITP
     C           INVTPK    CHAININVTPDF              99    *
     C           *IN99     IFEQ '1'
     C                     MOVEL'INVTYPD 'W0FILE
     C                     MOVELNMCY      W0KEY
     C                     MOVE SITP      W0KEY
     C                     Z-ADD003       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      ** Access nominal currency decimal places
      *
     C           NMCY      CHAINTABTG10F             99    *
     C           *IN99     IFEQ '1'
     C                     MOVEL'TABTG10 'W0FILE
     C                     MOVELNMCY      W0KEY
     C                     Z-ADD004       W0ERNB
     C                     EXSR SRERR
     C                     END
     C                     Z-ADDCDP       ZCDP
      *................................................................
      *
     C           EXSECT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRCUST                                         *
      * Function    :  Get Customer Details                           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRCUST    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRCUST'  @STK,Q
      *
      *................................................................
      *
     C                     CALL 'AOSECSR0'
     C                     PARM           @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM ##CUST    @SCKY   6
     C           SDSECS    PARM SDSECS    DSSDY
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDSECSPD'W0FILE
     C                     MOVEL##CUST    W0KEY
     C                     Z-ADD005       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
     C           EXCUST    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRPSIN                                         *
      * Function    :  Get Default Settlement Instructions            *
      *                For Customer and Depot                         *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRPSIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRPSIN'  @STK,Q
      *
      *................................................................
      *
      ** Access Standard Settlement Instructions
      ** (Pay to portfolio customer, receive from depot)
      *
     C           STDSEK    KLIST
     C                     KFLD           SBRA
     C                     KFLD           CSTM
     C                     KFLD           SIBK                                                CSE028
     C                     KFLD           CYSI
     C                     KFLD           ITSI
      *
     C                     MOVE *BLANK    ##CSTI
     C                     MOVE *BLANK    ##DSTI
      *
      ** For the Portfolio Customer
      *  ==========================
      *
     C           BFCLAS    IFEQ 'S'
     C           BFCLAS    OREQ 'D'
     C                     MOVE TDBA      SBRA
     C                     MOVEL##CUST    CSTM
     C                     MOVE TDBK      SIBK                                                CSE028
     C                     MOVE NMCY      CYSI
     C                     MOVE SITP      ITSI
      *
      ***  First try STDSED with appropriate investment type
      *
     C/COPY WNCPYSRC,AOOUTRU0C4
     C           STDSEK    CHAINSTDSED               99
      *
      ***  If no such instructions on STDSED, retry with blank ITSI
      *
     C           *IN99     IFEQ '1'
     C           RECI      ANDNE'*'
     C                     MOVE *BLANK    ITSI
     C           STDSEK    CHAINSTDSED               99
     C                     END
      *
     C/COPY WNCPYSRC,AOOUTRU0C5
     C           *IN99     IFNE '1'
     C           RECI      ANDNE'*'
      *
      ** Use pay fields (as payment to customer)
      *
     C                     MOVE SPSM      ##CSTM
     C                     MOVE SPAT      ##CSTA
     C                     MOVE SPBA      ##CSTB
      *                                                                   101217
     C           ##CSTM    IFEQ '15'                                      101217
     C                     MOVELBFACCD    ##CST1           Account Code   101217
     C                     MOVELBFACSN    ##CST2           Account Seq.   101217
     C                     END                                            101217
      *                                                                   101217
     C                     END
     C                     ELSE
      *
      ** For The Depot
      *  =============
     C                     MOVE TDBA      SBRA
     C                     MOVEL##DEPT    CSTM
     C                     MOVE TDBK      SIBK                                                CSE028
     C                     MOVE NMCY      CYSI
     C                     MOVE SITP      ITSI
      *
      ***  First try STDSED with appropriate investment type
      *
     C/COPY WNCPYSRC,AOOUTRU0C6
     C           STDSEK    CHAINSTDSED               99
      *
      ***  If no such instructions on STDSED, retry with blank ITSI
      *
     C           *IN99     IFEQ '1'
     C           RECI      ANDNE'*'
     C                     MOVE *BLANK    ITSI
     C           STDSEK    CHAINSTDSED               99
     C                     END
      *
     C/COPY WNCPYSRC,AOOUTRU0C7
     C           *IN99     IFNE '1'
     C           RECI      ANDNE'*'
     C*
     C** Use receive fields (as receipt from depot)
     C*
     C                     MOVE SRSM      ##DSTM
     C                     MOVE SRAC      ##DSTA
     C                     MOVE SRAB      ##DSTB
     C                     END
     C                     END
      *................................................................
      *
     C           EXPSIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRMOVE                                         *
      * Function    :  Pass Movement To Write Program                 *
      *                                                               *
      * Called by   :  Various                                        *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRMOVE    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRMOVE'  @STK,Q
      *
      *................................................................
      *
      ** Bypass if no amount to post
      *
     C           ##AMT     CABEQ*ZERO     EXMOVE
      *
      ** Determine account movement settlement details
      *
     C                     CLEARC#DTA
      *
     C           ##SETM    IFEQ '01'
     C           ##SETM    OREQ '02'
     C           ##SETM    OREQ '07'
     C           ##SETM    OREQ '08'
     C           ##SETM    OREQ '12'
     C                     MOVEL'N'       C#ATYP
     C                     MOVEL##NOSN    C#NOSN
     C                     ELSE
     C           ##SETM    IFEQ '14'
     C                     MOVEL'R'       C#ATYP
     C                     MOVEL##ACNO    C#ACNO
     C                     ELSE
     C           ##SETM    IFEQ '04'
     C           ##SETM    OREQ '05'
     C           ##SETM    OREQ '15'
     C                     MOVEL'P'       C#ATYP
     C                     MOVEL##CNUM    C#CNUM
     C                     MOVEL##ACOD    C#ACOD
     C                     MOVEL##ACSQ    C#ACSQ
     C                     MOVEL##BRCA    C#BRCA
     C                     END
     C                     END
     C                     END
      *
      * OLPOS Incoming/Outgoing Indicator 'I' or 'O'
     C           ##PYRC    IFEQ 'P'
     C                     MOVEL'O'       C#IO             * outgoing
     C                     ELSE
     C                     MOVEL'I'       C#IO             * incoming
     C                     END
      *
      * Currency of transaction
     C                     MOVEL##CCY     C#CCY
      *
      * Transaction type
     C                     MOVEL##TRYP    C#TRYP
      * Transaction Narrative
     C/COPY WNCPYSRC,AOOUTRU0K2
     C                     MOVEL##NARR    C#NARR
     C/COPY WNCPYSRC,AOH00064
     C           ##REVI    IFEQ 'YES'
     C                     MOVE '*'       C#NARR
     C                     END
      *
      * SE/DL/LE/GL/RE transaction reference
     C                     MOVEL##TRN     C#TRN
      *
      * Movement amount
     C           ##PYRC    IFEQ 'P'
     C           ##REVI    ANDNE'YES'
     C           ##PYRC    OREQ 'R'
     C           ##REVI    ANDEQ'YES'
     C                     Z-SUB##AMT     C#AMT            * pay ie credit
     C                     ELSE
     C                     Z-ADD##AMT     C#AMT            * rec ie debit
     C                     END
      *
      * Module
     C                     MOVEL'SE'      C#MOD
      *
      * Olpos record number to update
      * '01' for FX
      * '02' for Interest movements
      * '03' for loans and deposits
     C           ##RPNB    IFEQ 2
     C                     MOVE '02'      C#OLRC
     C                     ELSE
     C                     MOVE '03'      C#OLRC
     C/COPY WNCPYSRC,AOOUTRU0C9
     C                     END
     C/COPY WNCPYSRC,AOOUTRU0K1
      *
      * Value date and posting date
     C                     Z-ADD##VDAT    C#VDAT
     C                     Z-ADD##VDAT    C#PSTD
      *
      * Indicator to show if removal allowed Y, N or " "
     C                     MOVEL'Y'       C#RPST
      *
      * Removal set number
     C                     Z-ADD##RPNB    C#RPNB
      *
      * Removal type "C" = Credit entries
      *              "D" = Debit entries
     C                     MOVEL'C'       C#RTYP
      *
      * Booking branch
     C                     MOVEL##BBRN    C#BBRN
      *
      *  If settlement details are present
      *  AOSPOSU0 will be called to update PRONO, MEMOS, RSACMTPD, OLPOS
      *
      *  OTHERWISE
      *
      *  If no settlement details are present
      *  AOSPOSU0 will be called to only update OLPOS
      *
     C           C#ATYP    IFEQ *BLANK
      *
      * Set account type indicator to signify no settlement
     C                     MOVEL'X'       C#ATYP
      *
      * Set update indicators for no update
     C                     MOVE 'N'       C#PRON
     C                     MOVE 'N'       C#MEMS
     C                     MOVE 'N'       C#RSAC
     C                     END
      *                                                                                       CAP205
      ** Set C#MTYP to ##MTYP                                                                 CAP205
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL##MTYP    C#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *
      *  Call AOSPOSU0
      *
     C                     CALL 'AOSPOSU0'             9090
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*ADD    'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE  8
     C                     MOVEL'AOSPOSU0'W0KEY  29
     C                     Z-ADD902       W0ERNB  30
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
     C           EXMOVE    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRDINT   : Determine interest due at next coupon date        *
      *                                                               *
      *  CALLED BY: Various                                           *
      *                                                               *
      *  CALLS    : SRERR - Report errors                             *
      *                                                               *
      *****************************************************************
     C           SRDINT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRDINT'  @STK,Q
      *
      *................................................................
      *
      ** Determine the last coupon date (prior to the next coupon date)
      *
     C           NCD       SUB  1         ZECD
     C                     EXSR ZLCD
      *
     C                     Z-ADDNOML      ZAMT             * nominal
     C                     Z-ADDZZLCD     ZDCSDT           * start date
     C                     Z-ADDNCD       ZDCEDT           * end date
      *
     C                     EXSR ZACCZ
      *
      *................................................................
      *  Unwind subroutine stack name
      *
     C           EXDINT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRUPD    : Update database with shadow postings in store     *
      *                                                               *
      *  CALLED BY: Various                                           *
      *                                                               *
      *  CALLS    : SRERR - Report errors                             *
      *                                                               *
      *****************************************************************
     C           SRUPD     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRUPD'   @STK,Q
     C/COPY WNCPYSRC,AOOUTRU0C2
      *
      *................................................................
      *
      *  Call AOSPOSU0 to update database
      *
     C                     CALL 'AOSPOSU0'             9090
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*UPDATE 'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE
     C                     MOVEL'AOSPOSU0'W0KEY
     C                     Z-ADD903       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
      *  Unwind subroutine stack name
      *
     C           EXUPD     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRCLR    : Clear all postings in store                       *
      *                                                               *
      *  CALLED BY: UPDATE - Update database                          *
      *                                                               *
      *  CALLS    : SRERR - Report errors                             *
      *                                                               *
      *****************************************************************
     C           SRCLR     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRCLR'   @STK,Q
      *
      *................................................................
      *
      *  Clear arrays in AOSPOSU0
      *
     C                     CLEARC#DTA
     C                     CALL 'AOSPOSU0'             9090
     C           R#RTN     PARM *BLANKS   O#RTN   7
     C                     PARM '*CLEAR  'W0ACT   8
     C                     PARM C#DTA     O#DTA 256
      *
     C           *IN90     IFEQ '1'
     C           R#RTN     ORNE *BLANKS
     C                     MOVEL'*CALL   'W0FILE
     C                     MOVEL'AOSPOSU0'W0KEY
     C                     Z-ADD901       W0ERNB
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
      *  Unwind subroutine stack name
      *
     C           EXCLR     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *  SRERR    : Report error and close down program               *
      *                                                               *
      *  CALLED BY: Various                                           *
      *  CALLS    : *NONE                                             *
      *                                                               *
      *****************************************************************
     C           SRERR     BEGSR
      *
      *  Dump program
      *
     C                     DUMP
     C                     ROLBK
      *
      *  Close down program
      *
     C                     SETON                     LR
     C                     MOVEL'*ERROR*' ##RTCD
     C                     RETRN
      *
     C           EXERR     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
     C/COPY WNCPYSRC,AOOUTRU0C3
      *
      *................................................................
      *
      **  Initialise work fields
     C                     MOVEL'P'       ##PAY
     C                     MOVEL'R'       ##REC
     C                     MOVEL*BLANKS   R#RTN   7
     C*********************MOVE '0'       *IN98                           154862
      *
      ** Access bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'W0FILE
     C                     MOVEL'1       'W0KEY
     C                     Z-ADD001       W0ERNB
     C                     EXSR SRERR
     C                     END
     C/COPY WNCPYSRC,AOOUTRU0C8
     C*                                                                   154862
     C** CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.                      154862
     C*                                                                   154862
     C           BJDFIN    COMP 'M'                      98*              154862
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
     C/COPY WNCPYSRC,AOH00065
      *
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************   108093
      /SPACE 5                                                            108093
      *****************************************************************   108093
      * Subroutine  :  AORETL                                         *   108093
      * Function    :  Call access object : For Retail ICD.           *   108093
      *                                                               *   108093
      * Called by   :  Mainline, via Trades Subroutine                *   108093
      * Calls       :  AORETLR0                                       *   108093
      *****************************************************************   108093
     C*                                                                   108093
     C* AORETL - Subroutine to call access object : Retail ICD            108093
     C*                                                                   108093
     C           AORETL    BEGSR                           ** AORETL **   108093
     C*                                                                   108093
     C                     CALL 'AORETLR0'                                108093
     C                     PARM '       ' @RTCD   7                       108093
     C                     PARM '*FIRST ' @OPTN   7                       108093
     C           SDRETL    PARM SDRETL    DSFDY                           108093
     C           @RTCD     IFNE *BLANK                                    108093
     C                     MOVEL'SDRETPD 'W0FILE                          108093
     C                     MOVEL'*FIRST'  W0KEY                           108093
     C                     Z-ADD006       W0ERNB                          108093
     C                     EXSR SRERR                                     108093
     C                     END                                            108093
     C                     ENDSR                                          108093
     C*                                                                   108093
      *****************************************************************   108093
     C/COPY WNCPYSRC,AOH00066
     C/SPACE 5
     C/COPY ZSRSRC,ZCNSDZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZACCZZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZACCRZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZDAYSZ2
     C/SPACE 5
     C/COPY ZSRSRC,ZNCDZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZDYYRZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE1Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE2Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZLCDZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZRATEZ1
** ##NAR  **      NARRATIVES
VALUE
INTEREST
MATURITY
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
** ##CPY  **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
