     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Regions                     *RDR')
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOREGNR0 - READ ACCESS TO REGION CODES FILE                  *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *     Last Amend No. CSD031          Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     Prev Amend No. S01230             DATE 23/11/89              *S01230
     F*                    S01230             DATE 06/11/89              *S01230
     F*                                                                  *
     F********************************************************************
      *     CSD031 - Enhanced Centralised Static Data                 *
     F*     S01230    - STANDARDISE ACCESS PROGRAMS                      *S01230
     F********************************************************************S01230
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       01      OPEN FILE
     F*       90      FILE ERROR
     F*       91      ERROR ON FILE OPEN
     F*       92      ERROR ON FILE CLOSE
     F*       93      EMPTY FILE
     F*       94      EOF
     F*       95      NRF
     F*       96      INVALID OPTION PASSED AS PARM
     F*       97      NO SELECTION
     F*       98      ERROR ON CALL
     F*
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDREGNL1IF  E           K        DISK                           UC
      * IN  - @A3REBX Region codes    (RGCD)
     FGPREGNL1IF  E           K        DISK                           UC                      CSD031
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
      ************************************************************
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************        S01230
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS
     I                                        1  50 @MG
      *
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     I********D@FMT     E DSSDREGND0                                      S01230
     ID@FMT     E DSSDREGNPD                                              S01230
      * @A3REBX - Region code record format data structure
      *
     IP@FMT     E DSDSFDY                                                 S01230
     I* EXTERNAL DATA STRUCTURE TO HOLD THE OUTGOING RECORD FORMAT        S01230
     I* OF THE FILE.                                                      S01230
      *                                                                   S01230
     ISCSARD    E DSSCSARDPD                                                                 CSD031
      ** Switchable features file                                                            CSD031
      *                                                                                      CSD031
     I********P@FMT       DS                                              S01230
     I********                                1  94 P@DMMY                S01230
      *
     IP@RGCD      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
      *
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@RGCD  2        I:REGION CODE
     C                     PARM           P@FMT            O:Format
      *****************************************************
      *MAIN
      *
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *                                                                                       CSD031
      ** Check if Enhanced Centralised Static Data is installed                               CSD031
      *                                                                                       CSD031
     C                     CALL 'AOSARDR0'                                                    CSD031
     C                     PARM *BLANKS   PRTCD   7                                           CSD031
     C                     PARM '*VERIFY' POPTN   7                                           CSD031
     C                     PARM 'CSD031'  PSAR    6                                           CSD031
     C           SCSARD    PARM SCSARD    P@FMT                                               CSD031
      *                                                                                       CSD031
     C           PRTCD     IFEQ *BLANKS                                                       CSD031
     C                     MOVE 'Y'       CSD031  1                                           CSD031
     C                     ELSE                                                               CSD031
     C                     MOVE 'N'       CSD031                                              CSD031
     C                     ENDIF                                                              CSD031
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C           CSD031    IFEQ 'Y'                                                           CSD031
     C           *INU1     ANDEQ*ON                                                           CSD031
     C                     OPEN GPREGNL1                                                      CSD031
     C                     ELSE                                                               CSD031
     C                     OPEN SDREGNL1               91    Open file
     C                     ENDIF                                                              CSD031
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PROGRAM
     C           CSD031    IFEQ 'Y'                                                           CSD031
     C           *INU1     ANDEQ*ON                                                           CSD031
     C                     CLOSEGPREGNL1                                                      CSD031
     C                     ELSE                                                               CSD031
     C                     CLOSESDREGNL1               92    Close file
     C                     ENDIF                                                              CSD031
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST RECORD
     C           CSD031    IFEQ 'Y'                                                           CSD031
     C           *INU1     ANDEQ*ON                                                           CSD031
     C           *LOVAL    SETLLSDREGND0                                                      CSD031
     C                     ELSE                                                               CSD031
     C           *LOVAL    SETLL@A3REBX              9390    Set file
     C                     ENDIF                                                              CSD031
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT RECORD
     C           CSD031    IFEQ 'Y'                                                           CSD031
     C           *INU1     ANDEQ*ON                                                           CSD031
     C                     READ SDREGND0               9094                                   CSD031
     C                     ELSE                                                               CSD031
     C                     READ @A3REBX                9094  Read record
     C                     ENDIF                                                              CSD031
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST RECORD
     C           CSD031    IFEQ 'Y'                                                           CSD031
     C           *INU1     ANDEQ*ON                                                           CSD031
     C           *HIVAL    SETGTSDREGND0               90                                     CSD031
     C                     ELSE                                                               CSD031
     C           *HIVAL    SETGT@A3REBX                90    Set file
     C                     ENDIF                                                              CSD031
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.RECORD
     C           CSD031    IFEQ 'Y'                                                           CSD031
     C           *INU1     ANDEQ*ON                                                           CSD031
     C                     READPSDREGND0               9094                                   CSD031
     C                     ELSE                                                               CSD031
     C                     READP@A3REBX                9094  Read record
     C                     ENDIF                                                              CSD031
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEYED ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR            SS
     C           W@SL01    IFEQ '?'                        IF 8 SELECT
     C           W@SL02    OREQ '?'                         OR
     C*********************CALL 'SDKCSRR'              98    <SELECT>     S01230
     C                     CALL 'SD0191S'              98    <SELECT>     S01230
     C                     PARM *BLANK    P@RTC2  7          B:Return code
     C                     PARM *BLANK    P@RGCD             B:Key
     C           P@RTC2    COMP *BLANK               97      No selection
     C           *IN97     CABEQ'1'       T@SKIP             No selection
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C                     END                             FI 8
     C           CSD031    IFEQ 'Y'                                                           CSD031
     C           *INU1     ANDEQ*ON                                                           CSD031
     C           P@RGCD    CHAINSDREGND0             9590                                     CSD031
     C                     ELSE                                                               CSD031
     C           P@RGCD    CHAIN@A3REBX              9590    Get record
     C                     ENDIF                                                              CSD031
     C           *IN95     CABEQ'1'       T@SKIP             No record found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parameter error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MESSAGE
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No record found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MESSAGE>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Program name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Message file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA             I:Message data
     C                     PARM '*DIAG'   P@MGTP  7          I:Message type
     C                     END                             FI 9
      *
      * Caller requests DB error handling
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF 10
     C           P@RTCD    ANDNE*BLANK                      AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI 10
      *
     C           P@OPTN    IFNE '*VERIFY'                  IF 11
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C           P@RTCD    ANDNE'*OPTION'                   AND
     C                     MOVELD@FMT     P@FMT
     C                     END                             FI 11
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDREGNPD  AOREGNR0  Region codes
** CPY@     : Copyright notice for inclusion in all programs           grams
(c) Misys International Banking Systems Ltd. 2001
