     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Risk Management Details     *RDR')
      *===============================================================*
      *                                                               *
      *  Midas - ACCESS PROGRAMS.                                     *
      *                                                               *
      *  AORISKR0 - READ ACCESS TO INTEREST RISK MANAGEMENT ICD.      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMENDMENT  CRM001        DATE  22FEB95                  *
      *  PREV AMENDMENT  S01393        DATE  18SEP92                  *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRM001 - Interest Risk Management changes, recompiled.       *
      *  S01393 - Upgrade Strategic Risk Management to R10            *
      *                                                               *
      *****************************************************************
      /EJECT
      *===============================================================*
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  01 - First run indicator (off on first run)                  *
      *  90 - File error                                              *
      *  91 - Open error                                              *
      *  92 - Close error                                             *
      *  93 - Empty file                                              *
      *  96 - Option error                                            *
      *                                                               *
      *===============================================================*
      /EJECT
      *===============================================================*
      *
     FSDRISKL1IF  E           K        DISK                           UC
      *
      * Input Format - SDRISKRI - Risk Management ICD Retreival Index
      *
     E*===============================================================*
      /EJECT
      *===============================================================*
      *
     E                    MSG     1   1 50
      * Error Message Data Array
      *
     E                    CPY     1   1 80
      * Copyright Statement Array
      *
     I*===============================================================*
      /EJECT
      *===============================================================*
      *
     IPMSGDA      DS
      * Error Message Data Data Structure
     I                                        1  50 MSG
      *
     IPGMDS      SDS
      * Program Status Data Structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     IDFMT      E DSSDRISKPD
      * Risk Management ICD Record Format Data Structure
      *
     IPFMT      E DSDSFDY                                                 S01393
      * External Data structure to hold the outgoing record format        S01393
      * of the file.                                                      S01393
      *
      *
     IDSCPY       DS
      * Copyright Statement Data Structure
     I                                        1  80 CPY
      *
     C*===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *  MAIN  -  MAIN PROCESSING ROUTINE                             *
      *                                                               *
      *===============================================================*
      *
      *
      * Define inter program parameters - Return Code, Option and
      * Format
      *
     C           *ENTRY    PLIST
     C           WRTCD     PARM           PRTCD   7
     C                     PARM           POPTN   7
     C***********          PARM           DFMT                            S01393
     C                     PARM DFMT      PFMT                            S01393
      *
      *
      * Initialise error indicators
      *
     C                     SETOF                     909192
     C                     SETOF                     9396
      *
      *
      * If a valid option has been requested
      *
     C           POPTN     IFEQ '*KEY   '
     C           POPTN     OREQ '*FIRST '
     C           POPTN     OREQ '*LAST  '
     C           POPTN     OREQ '*NEXT  '
     C           POPTN     OREQ '*PREV  '
     C           POPTN     OREQ '*FREE  '
     C           POPTN     OREQ '*SAME  '
     C           POPTN     OREQ '*VERIFY'
      *
      *
      * If first call to program, retrieve RM ICD record
      * If an error occurs, set on appropriate error indicator
      *
     C           *IN01     IFEQ '0'
     C                     SETON                     01
     C                     OPEN SDRISKL1               91
     C           *IN91     CABEQ'1'       TSKIP          LR
     C           *LOVAL    SETLLSDRISKRI             9390
     C  N93                READ SDRISKRI                 93
     C                     CLOSESDRISKL1               92
     C           *IN93     CABEQ'1'       TSKIP
     C           *IN90     CABEQ'1'       TSKIP          LR
     C           *IN92     CABEQ'1'       TSKIP          LR
     C                     SETON                     LR
     C                     END
      *
      *
      * Else, if an invalid option has been requested set on
      * the invalid option indicator
      *
     C                     ELSE
     C                     SETON                     96
     C                     END
      *
      *
      *        -----------------
     C           TSKIP     TAG                             ++ TSKIP ++
      *        -----------------
      *
      *
      * Set up appropriate Return Code, depending on error
      * indicator settings
      *
     C                     MOVE *BLANK    PRTCD
     C   90                MOVE '*ERROR ' PRTCD
     C   91                MOVE '*OPEN  ' PRTCD
     C   92                MOVE '*CLOSE ' PRTCD
     C   93                MOVE '*EMPTY ' PRTCD
     C   96                MOVE '*OPTION' PRTCD
      *
      *
      * If caller has requested message to be sent to program queue
      * and an error has occurred, send appropriate error message
      *
     C           WRTCD     IFEQ '*MSG   '
     C           PRTCD     ANDNE*BLANK
      *
     C   90                MOVE 'ACO1001' PMGID
     C   91                MOVE 'ACO1002' PMGID
     C   92                MOVE 'ACO1003' PMGID
     C   93                MOVE 'ACO1004' PMGID
     C   96                MOVE 'ACO1007' PMGID
      *
     C                     CALL 'ZASNMG'
     C                     PARM *BLANK    WRTCD   7
     C                     PARM PGNAME    PPGNM  10
     C                     PARM '*PRV'    PDEST   5
     C                     PARM           PMGID   7
     C                     PARM *BLANK    PMSGF  10
     C                     PARM *BLANK    PMGKY   4
     C                     PARM           PMGDA  50
     C                     PARM '*DIAG'   PMGTP   7
      *
     C                     END
      *
      *
      * If caller has requested that the access program should
      * handle database errors and one has occurred, call Database
      * Error Control program
      *
     C           WRTCD     IFEQ '*DBERR'
     C           PRTCD     ANDNE*BLANK
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END
      *
      *
      * Return to caller
      *
     C                     RETRN
      *
      *===============================================================*
      /EJECT
**
SDRISKPD  AORISKR0  Risk Management ICD
**
(c) Misys International Banking Systems Ltd. 2001
