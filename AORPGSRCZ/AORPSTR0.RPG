     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Access Program for BSPL Sets')
     F********************************************************************
     F*                                                                  *
     F*  Midas ACCESS OBJECTS
     F*                                                                  *
     F*     AORPSTR0 - REPORTING STRUCTURES                              *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     LAST AMEND NO. 000000             DATE 00/00/00              *
     F*                                                                  *
      ************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSDRPSTL0IF  E           K        DISK                           UC
      * IN  - @ADREAI Reporting sets
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
      ************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS
      * Program Message data structure
     I                                        1  50 @MG
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     ID@FMT     E DSSDRPSTPD
      * @ADREAI - Reporting sets record format data structure
      *
     IP@FMT     E DSDSFDY
     I* External Data structure to hold the outgoing record format
     I* of the file.
      *
     IP@RSNO      DS
      * Key field data structure
     I                                        1   1 W@SL01
      *
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@RSNO  1        I:Key field
     C                     PARM           P@FMT            O:FORMAT
      *****************************************************
      *MAIN
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     OPEN SDRPSTL0               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     SETON                     01      Set ind.
     C           P@RTCD    CABEQ'*OPEN  ' T@SKIP             Open only
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PROGR
     C                     CLOSESDRPSTL0               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     GOTO T@SKIP                       Return
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST RECORD
     C           *LOVAL    SETLL@ADREAI              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT RECORD
     C                     READ @ADREAI                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST RECORD
     C           *HIVAL    SETGT@ADREAI                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.RECORD
     C                     READP@ADREAI                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEYED ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           W@SL01    IFEQ '?'                        IF 8 SELECT
     C                     CALL 'SD1310S'              98    <SELECT>
     C                     PARM *BLANK    P@RTC2  7          B:Return code
     C                     PARM *BLANK    P@RSNO             B:Key
     C           *IN98     CABEQ'1'       T@SKIP             Program error
     C           P@RTC2    CABNE*BLANK    T@SKIP     9797    No selection
     C                     END                             FI 8
     C           P@RSNO    CHAIN@ADREAI              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No record found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 9 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parameter er
     C                     END                             FI 9
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Call error
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 10 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO0001' P@MGID             File error
     C   91                MOVE 'ACO0002' P@MGID             Open error
     C   92                MOVE 'ACO0003' P@MGID             Close error
     C   93                MOVE 'ACO0004' P@MGID             Empty file
     C   94                MOVE 'ACO0005' P@MGID             End of file
     C   95                MOVE 'ACO0006' P@MGID             No record found
     C   96                MOVE 'ACO0007' P@MGID             Option error
     C   97                MOVE 'ACO0009' P@MGID             No selection
     C   98                MOVE 'ACO0010' P@MGID             Call error
     C                     CALL 'ZASNMG'               99    <SEND MESSAGE>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Program name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Message file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM *BLANK    P@MGDA 50          I:Message data
     C                     PARM '*DIAG'   P@MGTP  7          I:Message type
     C                     END                             FI 10
      *
      * Caller Requests DB Error Handling
      *
     C           W@RTCD    IFEQ '*DBERR'                   IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
     C           P@OPTN    IFNE '*VERIFY'
     C           P@OPTN    ANDNE'*FREE'
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SDRPSTPD  AORPSTR0  REPORTING SETS
