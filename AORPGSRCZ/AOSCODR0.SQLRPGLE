     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2003')
      *****************************************************************
/**** *  RPGBASEMOD                                                   *                     MD056812
/*STD *  RPGSQLMOD                                                    *                     MD056812
/*EXI *  CLOSQLCSR(*ENDMOD)                                           *                     MD056812
/*EXI *  TEXT('Midas AO SWIFT Status/Reason Codes Access Object')
      *****************************************************************
      *                                                               *
      *  Midas - Access Objects module                                *
      *                                                               *
      *  AOSCODR0 - SWIFT Status/Reason Codes                         *
      *                                                               *
      *  Function:  This module allows users to access data in        *
      *             AOSCODPD                                          *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD056812           Date 20Sep20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSE039  *CREATE    Date 30Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056812  - Deliverable Data Split for SDSCODPD              *
      *  MD046248 - Finastra Rebranding                               *
      *  CSE039 - Automatic Settlement of Trades                      *
      *                                                               *
      *****************************************************************
      *                                                               *
      *        D F C H L   FUNCTION INDICATOR                         *
      *                                                               *
      *        01     INITIALISATION                                  *
      *        90     FILE ERROR                                      *
      *        91     FILE OPEN ERROR                                 *
      *        92     FILE CLOSE ERROR                                *
      *        93     FILE EMPTY                                      *
      *        94     END OF FILE                                     *
      *        95     NO RECORD FOUND                                 *
      *        96     OPTION ERROR                                    *
      *        97     NO SELECTION                                    *
      *        98     ERROR ON CALL OF SELECTION MODULE (SD180000)    *
      *                                                               *
      *****************************************************************
      /EJECT
     F*FSTART
      *****************************************************************
     F*SDSCODL0* IF   E           K DISK    USROPN                                          MD056812
      * IN  - SDSCODD0
      *****************************************************************
      /EJECT
     D*ESTART
      *****************************************************************
      *****************************************************************
      /EJECT
     D*ISTART
      *****************************************************************
      *
     D PGMDS          SDS
      * Program Status data structure
     D  PGNAME                 1     10
     D  PGLIBR                81     90
     D  PGJBNM               244    253
     D  PGUSER               254    263
     D  PGJBNO               264    269  0
      *
     D P@MGDA          DS
     D  @MG                    1     50
     D                                     DIM(1) CTDATA PERRCD(1)
      *
     D*D@FMT****     E DS                  EXTNAME(SDSCODPD)                                MD056812
     D D@FMT         E DS                  EXTNAME(SDSCOJW0)                                MD056812
      * SDSCODD0 - Input file record layout
      *
     D P@FMT         E DS                  EXTNAME(DSFDY)
      * External Data structure to hold the outgoing record format
      * of the file.
      *
     D X@CODE          S              4A                                                    MD056812
     D X@CDTP          S              1A                                                    MD056812
     D X@CDDP          S              4A                                                    MD056812
      *****************************************************************
      /EJECT
     C*CSTART
      *****************************************************************
     C     *ENTRY        PLIST
     C     W@RTCD        PARM                    P@RTCD            7            B:Return code
     C                   PARM                    P@OPTN            7            I:Option
     C                   PARM                    P@CODE            4            I:S/R Code
     C                   PARM                    P@CDTP            1            I:Code Type
     C                   PARM                    P@CDDP            4            I:Depend. Code
     C                   PARM                    P@FMT                          O:Format
      *****************************************************************
     C     KSCODL0       KLIST
     C                   KFLD                    P@CODE
     C                   KFLD                    P@CDTP
     C                   KFLD                    P@CDDP
      *****************************************************************
      *MAIN
      *
     C     P@OPTN        IFEQ      '*SAME   '
     C     *IN01         COMP      '0'                                    91
     C                   GOTO      T@SKIP
     C                   END
      *
     C                   SETOFF                                       909192     ¦ Reset
     C                   SETOFF                                       939495     ¦ error
     C                   SETOFF                                       969798     ¦ indicators
      *
      * First call - open file :
      *
     C     *IN01         IFEQ      '0'                                          IF 1 FIRST TIME
     C                   SETON                                        01          Set ind.
     C**********         OPEN      SDSCODL0                             91        Open file MD056812
     C     *IN91         CABEQ     '1'           T@SKIP                   LR      Open error
     C                   END                                                    FI 1
      *
      * Last call - close file and terminate program :
      *
     C     P@OPTN        IFEQ      '*FREE  '                                    IF 2 FREE PGM
     C**********         CLOSE     SDSCODL0                             92        Close fileMD056812
     C     *IN92         CABEQ     '1'           T@SKIP                   LR      Close error
     C                   SETON                                        LR          Set ind.
     C                   END                                                    FI 2
      *
      * Caller requests first record :
      *
     C     P@OPTN        IFEQ      '*FIRST '                                    IF 3 FIRST REC
     C******LOVAL        SETLL     SDSCODD0                           9390        Set file  MD056812
     C     *IN93         CABEQ     '1'           T@SKIP                           Empty file
     C     *IN90         CABEQ     '1'           T@SKIP                   LR      File error
     C                   MOVE      '*NEXT  '     P@OPTN                           Force *Next
     C                   END                                                    FI 3
      *
      * Caller requests next record :
      *
     C     P@OPTN        IFEQ      '*NEXT  '                                    IF 4 NEXT REC
     C**********         READ      SDSCODD0                             9094      Read recorMD056812
     C     *IN94         CABEQ     '1'           T@SKIP                           End of file
     C     *IN90         CABEQ     '1'           T@SKIP                   LR      File error
     C                   END                                                    FI 4
      *
      * Caller requests last record :
      *
     C     P@OPTN        IFEQ      '*LAST  '                                    IF 5 LAST REC
     C******HIVAL        SETGT     SDSCODD0                             90        Set file  MD056812
     C     *IN90         CABEQ     '1'           T@SKIP                   LR      File error
     C                   MOVE      '*PREV  '     P@OPTN                           Force *Prev
     C                   END                                                    FI 5
      *
      * Caller requests previous record :
      *
     C     P@OPTN        IFEQ      '*PREV  '                                    IF 6 PREV.REC
     C**********         READP     SDSCODD0                             9094      Read recorMD056812
     C     *IN94         CABEQ     '1'           T@SKIP                           End of file
     C     *IN90         CABEQ     '1'           T@SKIP                   LR      File error
     C                   END                                                    FI 6
      *
      * Caller requests record indicated by key :
      *
     C     P@OPTN        IFEQ      '*KEY   '                                    IF 7 KEY ACCESS
     C     P@OPTN        OREQ      '*VERIFY'                                     OR         ESS
      *
     C                   IF        %Scan('?':P@CODE) > 0                        IF 8 SELECT
     C                   CALLB     'SD180000'                           98        <SELECT>     S0122
     C                   PARM      *BLANK        P@RTC2            7              B:Return code
     C                   PARM      *BLANKS       P@CODE                           B:S/R Code
     C                   PARM                    P@CDTP                           O:Code Type
     C                   PARM                    P@CDDP                           B:Depend. Code
     C     P@RTC2        COMP      *BLANK                             97          No selection
     C     *IN97         CABEQ     '1'           T@SKIP                           No selection
     C     *IN98         CABEQ     '1'           T@SKIP                           Program error
     C                   END                                                    FI 8
      *
     C*****KSCODL0       CHAIN     SDSCODD0                           9590        Get recordMD056812
     C                   eval      X@CODE = P@CODE                                          MD056812
     C                   eval      X@CDTP = P@CDTP                                          MD056812
     C                   eval      X@CDDP = P@CDDP                                          MD056812
     C/EXEC SQL                                                                             MD056812
     C+ SELECT *                                                                            MD056812
     C+ into :D@FMT                                                                         MD056812
     C+ from SDSCOJW0                                                                       MD056812
     C+ where SRCODE = :X@CODE and SRCDTP = :X@CDTP and SRCDDP = :X@CDDP                    MD056812
     C/END-EXEC                                                                             MD056812
     C                   Setoff                                       9095                  MD056812
     C                   If        SQLCODE = 100                                            MD056812
     C                   SETON                                        95                    MD056812
     C                   ENDIF                                                              MD056812
     C                   If        SQLCODE <> 100 and SQLCODE <> 0                          MD056812
     C                   SETON                                        90                    MD056812
     C                   ENDIF                                                              MD056812
                                                                                            MD056812
     C     *IN95         CABEQ     '1'           T@SKIP                           No rec found
     C     *IN90         CABEQ     '1'           T@SKIP                   LR      File error
     C                   END                                                    FI 7
      *
     C     P@OPTN        IFNE      '*FREE  '                                    IF 8 NOT FREE PGM
     C     P@OPTN        ANDNE     '*NEXT  '                                     AND NOT NEXT REC
     C     P@OPTN        ANDNE     '*PREV  '                                     AND NOT PREV.REC
     C     P@OPTN        ANDNE     '*KEY   '                                     AND NOT KEY ACCESS
     C     P@OPTN        ANDNE     '*VERIFY'                                     AND NOT KEY VERIF.
     C                   SETON                                          96        Parm error
     C                   END                                                    FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C     T@SKIP        TAG                                                    +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                   MOVE      *BLANK        P@RTCD                         No Errors
     C   90              MOVE      '*ERROR '     P@RTCD                         File error
     C   91              MOVE      '*OPEN  '     P@RTCD                         Open error
     C   92              MOVE      '*CLOSE '     P@RTCD                         Close error
     C   93              MOVE      '*EMPTY '     P@RTCD                         Empty file
     C   94              MOVE      '*EOF   '     P@RTCD                         End of file
     C   95              MOVE      '*NRF   '     P@RTCD                         No rec found
     C   96              MOVE      '*OPTION'     P@RTCD                         Option error
     C   97              MOVE      '*NOSEL '     P@RTCD                         No selection
     C   98              MOVE      '*CALL  '     P@RTCD                         Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C     W@RTCD        IFEQ      '*MSG   '                                    IF 9 SEND MSG
     C     P@RTCD        ANDNE     *BLANK                                        AND ERROR
     C   90              MOVE      'ACO1001'     P@MGID                           File error
     C   91              MOVE      'ACO1002'     P@MGID                           Open error
     C   92              MOVE      'ACO1003'     P@MGID                           Close error
     C   93              MOVE      'ACO1004'     P@MGID                           Empty file
     C   94              MOVE      'ACO1005'     P@MGID                           End of file
     C   95              MOVE      'ACO1006'     P@MGID                           No rec found
     C   96              MOVE      'ACO1007'     P@MGID                           Option error
     C   97              MOVE      'ACO1009'     P@MGID                           No selection
     C   98              MOVE      'ACO1010'     P@MGID                           Call error
     C                   CALLB     'ZASNMG'                                       <SEND MSG>
     C                   PARM      *BLANK        W@RTCD            7              B:Return code
     C                   PARM      PGNAME        P@PGNM           10              I:Pgm name
     C                   PARM      '*PRV'        P@DEST            5              I:Destination
     C                   PARM                    P@MGID            7              I:Message ID
     C                   PARM      *BLANK        P@MSGF           10              I:Msg file
     C                   PARM      *BLANK        P@MGKY            4              O:Message key
     C                   PARM                    P@MGDA           50              I:Msg data
     C                   PARM      '*DIAG'       P@MGTP            7              I:Msg type
     C                   END                                                    FI 9
      *
      * Caller Requests DB Error Handling
      *
     C     W@RTCD        IFEQ      '*DBERR'
     C     P@RTCD        ANDNE     *BLANK
     C                   DUMP
     C                   CALLB     'DBERRCTL'
     C                   END
      *
      * Return the current record
      *
     C     P@OPTN        IFNE      '*VERIFY'
     C     P@OPTN        ANDNE     '*FREE'
     C     P@RTCD        ANDNE     '*OPTION'
     C                   MOVEL     D@FMT         P@FMT
     C                   END
      *
      * Return to caller
      *
     C                   RETURN
      *
      *ENDMAIN
      *****************************************************************
      /EJECT
      *****************************************************************
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *****************************************************************
      *
     C     *PSSR         BEGSR
      *
     C     @RUN          IFEQ      *BLANK
     C                   MOVE      'Y'           @RUN              1
     C                   DUMP
     C                   END
      *
     C                   MOVE      '*ERROR '     P@RTCD
      *
     C                   SETON                                        U7U8LR
     C                   RETURN
      *
     C                   ENDSR
      *
      *****************************************************************
**CTDATA @MG
SDSCODPD  AOSCODR0  SWIFT Status/Reason Codes
