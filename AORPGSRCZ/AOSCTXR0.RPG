     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO SE Country Tax Rates')
     F********************************************************************
     F*                                                                  *
     F*  Midas  ACCESS OBJECTS
     F*                                                                  *
     F*     AOSCTXR0 - SE COUNTRY TAX RATES                              *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     LAST AMEND NO. S01447              DATE 05OCT93              *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
     F*     S01447 - New program for SE Withholding Tax Change           *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
     F*        D F C H L   FUNCTION INDICATOR                            *
     F*                                                                  *
     F*        01     INITIALISATION                                     *
     F*        90     FILE ERROR                                         *
     F*        91     FILE OPEN ERROR                                    *
     F*        92     FILE CLOSE ERROR                                   *
     F*        93     FILE EMPTY                                         *
     F*        94     END OF FILE                                        *
     F*        95     NO RECORD FOUND                                    *
     F*        96     OPTION ERROR                                       *
     F*        97     NO SELECTION                                       *
     F*        98     ERROR ON CALL OF SELECTION PROGRAM(SDCZSRR)        *
     F*        99     ERROR ON CALL ZASNMG                               *
     F*                                                                  *
     F********************************************************************
      /EJECT
     F*FSTART
      ************************************************************
     FSECNTRL0IF  E           K        DISK                           UC
      * IN  - SECNTRD0  COUNTRY TAX RATES  (TXCTTA, TXTABD)
      ************************************************************
      /EJECT
     E*ESTART
      ************************************************************
     E                    @MG     1   1 50
      ************************************************************
     E* Description     : Copyright notice for inclusion in all programs
     E*
     E********************************************************************
     E                    CPY@    1   1 80               Copyright     arr
      ****************************************************************
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS
     I                                        1  50 @MG
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     ID@FMT     E DSSECNTRPD
      * SECNTRD0 - Country Tax Rates record format data structure
      *
     IP@FMT     E DSDSSDY
     I*
     I* EXTERNAL DATA STRUCTURE TO HOLD THE OUTGOING RECORD FORMAT OF
     I* THE FILE
     I*
     IP@CTTA      DS
      * Key slicing data structure (for Record Selection)
     I                                        1   1 W@SL01
     I                                        2   2 W@SL02
     I*
     I* Description     : Copyright notice for inclusion in all programs
     I*
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@CTTA  2        I:Key field
     C                     PARM           P@TABD  10       I:Key field
     C                     PARM           P@FMT            O:Format
      *****************************************************
      *MAIN
     C           P@OPTN    IFEQ '*SAME '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     969798 ¦ indicators
     C                     SETOF                     505152 ¦ error
     C*
     C* KEY FIELD LIST
     C*
     C           K@KEY     KLIST
     C                     KFLD           P@CTTA
     C                     KFLD           P@TABD
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SECNTRL0               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSESECNTRL0               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLLSECNTRD0             9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ SECNTRD0               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGTSECNTRL0               90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READPSECNTRL0               9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEY ACCESS
     C           P@OPTN    OREQ '*VERIFY'                   OR
     C           K@KEY     CHAINSECNTRD0             9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No rec found
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parm error
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
     C   97                MOVE '*NOSEL ' P@RTCD           No selection
     C   98                MOVE '*CALL  ' P@RTCD           Called Pgm.err.
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No rec found
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C   97                MOVE 'ACO1009' P@MGID             No selection
     C   98                MOVE 'ACO1010' P@MGID             Call error
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg file
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          I:Msg data
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg type
     C                     END                             FI 9
      *
      * Caller Requests DB Error Handling
      *
     C           W@RTCD    IFEQ '*DBERR'                   IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
     C           P@OPTN    IFNE '*VERIFY'
     C           P@OPTN    ANDNE'*FREE'
     C           P@RTCD    ANDNE'*OPTION'
     C                     MOVELD@FMT     P@FMT
     C                     END
      *
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
SECNTRD0  AOSCTXR0  Country Tax Rates
** CPY@     : Copyright notice for inclusion in all programs           grams
(c) Misys International Banking Systems Ltd. 2001
