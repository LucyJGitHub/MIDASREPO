     H        1                                                           S01230
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Stopped cheques             *VFY')            *
     T* Access Verify: Stopped cheques
      *
     H*  SYSTEM  : ACCESS OBJECTS
     H* ANALYST     : J L Blenkinsop
     H* DATE        : June 1988
     H* DESCRIPTION : Verify stopped cheques using Access Object
      *
     M* MAINTENANCE : *NONE
      *
     N* NOTES       : Use format: *NONE
     N*
     N*               Files used: STOPCL0  (CNUM¦CCY¦ACOD¦ACSQ)     1
     N*                           STOPCL1  (ACNO)                   2
     N*
     N*               Select pgm: *NONE
     N*
     N*               Parameters: RTCD B Return Code           7A
     N*                           AMNT I Amount             13,0P
     N*                           CHQN I Account code          4A
     N*                           RETL I Retail account       10A   2
     N*                           CNUM I Customer number       6A   1
     N*                           CUCD I Currency code         3A   1
     N*                           ACCD I Account code          4A   1
     N*                           ACSQ I Account sequence      2A   1
     N*
     N*               RTCD values: I Blanks  Normal operation
     N*                            I *MSG    Normal; send message
     N*                            I *OPEN   Open files & return
     N*                            I *FREE   Close files & return
     N*
     N*                            O Blanks  No stopped cheque found
     N*                            O *STOP   Stopped cheque found
     N*                            O *ERROR  File error
     N*                            O *OPEN   Open error
     N*                            O *CLOSE  Close error
     N*                            O *SELECT Amount and Cheque No.invalid
     N*                            O *KEY    Invalid key
      *
     ******************************************************************
     F*                                                               *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR783021           Date 08Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 BUG27045A          Date 13Apr10               *
      *                 BUG27045           Date 18Feb10               *
      *                 CAP201             Date 09Dec09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 126299             Date 27FEB98               *
     F*                 059502             Date 29NOV93               *
     F*                 S01314             DATE 20FEB91               *
     F*                 LN0368             DATE 13JUN90               *
     F*                 S01117             DATE 02APR90               *
     F*                 S01230             DATE 06NOV89               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR783021 - System does not allow to process revoked stop     *
      *             payment cheque. Amended program to consider a     *
      *             cheque record which was logically deleted         *
      *             (RECI = '*') from Stopped Cheques File. Applied   *
      *             fix for GEMS 255508. (Child: AR783022)            *
      *  BUG27045A - Override the fix of Bug 27045 (Recompile)        *
      *  BUG27045 - Amend via SOAP removes Document Linkage           *
      *             (Recompile)                                       *
      *  CAP201 - Enhance Stopped Cheques processing for              *
      *           Teller Interfaces (Recompile)                       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  126299 - Correction to 059502 Incorporation.                 *
     F*  059502 - If a cheque number is present, validate using this  *
     F*           value, not just amount. (052564)                    *
     F*  S01314 - CHANGES MADE DURING R10 PTF INCORPORATION           *
     F*  LN0368 - Remvove CRT PAR because they were defined twice     *
     F*  S01230 - Field Name Changes                                  *
     F*  S01117 - Release 10 Changes - Branch Code Now a Key          *
     F*                                                               *
     F*****************************************************************
      /EJECT                                                              S01230
     F*FSTART                                                             S01230
     F*****************************************************************   S01230
     FSTOPCL0 IF  E           K        DISK                           UC
     F            STOPCSDF                          KRENAME@STPL0
      * IN  - STOPCSDF Stopped cheques (CNUM¦CCY¦ACOD¦ACSQ)
      *...........................................................
     FSTOPCL1 IF  E           K        DISK                           UC
     F            STOPCSDF                          KRENAME@STPL1
      * IN  - STOPCSDF Stopped cheques (ACNO)
      ************************************************************
      /EJECT                                                              S01230
     E*ESTART                                                             S01230
      ************************************************************        S01230
     E                    @MG     1   1 50                                S01230
      ************************************************************        S01230
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      ************************************************************        S01230
      /EJECT
     I*ISTART
      ************************************************************
     IP@MGDA      DS                                                      S01230
     I                                        1  50 @MG                   S01230
     IPGMDS      SDS
      * Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
      *
     IP@RETL      DS
      * Retail key character->numeric conversion data structure
     I                                        1  100W@RETL
      *
     I*P@CNUM      DS                                                     S01230
     IP@CUST      DS                                                      S01230
      * Customer number key character->numeric conversion data structure
     I*************************************   1   60W@CNUM                S01230
     I**********                              1   60W@CUST                             S01230 CSD027
     I                                        1   6 W@CUST                                    CSD027
      *
     IP@ACCD      DS
      * Account code key character->numeric conversion data structure
     I**********                              1   40W@ACCD                                    CGL029
     I                                        1  100W@ACCD                                    CGL029
      *
     IP@ACSQ      DS
      * Account sequence key character->numeric conversion data structure
     I                                        1   20W@ACSQ
      *
     IP@CHQN      DS
      * Cheque number selection character->numeric conversion data structure
     I                                        1   80W@CHQN
      *
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
      ************************************************************
      /EJECT
     C*CSTART
      *****************************************************
     C           *ENTRY    PLIST
     C                     PARM           P@RTCD  7        B:Return code
     C                     PARM           P@AMNT 130       I:Amount verify
     C                     PARM           P@CHQN  8        I:Cheque verify
     C                     PARM           P@RETL 10        I:Retail key
     C*********************PARM           P@CNUM  6        I:Key field 1  S01230
     C                     PARM           P@CUST  6        I:Key field 1  S01230
     C                     PARM           P@CUCD  3        I:Key field 2
     C**********           PARM           P@ACCD  4        I:Key field 3                      CGL029
     C                     PARM           P@ACCD 10        I:Key field 3                      CGL029
     C                     PARM           P@ACSQ  2        I:Key field 4
     C                     PARM           P@BRCA  3        I:Key field 4  S01117
      *****************************************************
      *MAIN
     C           K@L0      KLIST
     C*********************KFLD           W@CNUM                          S01230
     C                     KFLD           W@CUST                          S01230
     C                     KFLD           P@CUCD
     C                     KFLD           W@ACCD
     C                     KFLD           W@ACSQ
     C                     KFLD           P@BRCA                          S01117
      *                                                                   S01230
     C           P@CHQN    IFEQ *BLANK                                    S01230
     C                     Z-ADD0         W@CHQN                          S01230
     C                     END                                            S01230
      *                                                                   S01230
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     519394 ¦ indicators
     C                     SETOF                     030405 ¦
      *
      * First call - open files :
      *
     C           *IN01     IFEQ '0'                        IF INITIALISE
     C                     OPEN STOPCL0                93    Open file
     C           *IN93     CABEQ'1'       T@SKIP         LR  Open error
     C                     OPEN STOPCL1                93    Open file
     C           *IN93     CABEQ'1'       T@SKIP         LR  Open error
     C                     SETON                     01      Set ind.
     C           P@RTCD    CABEQ'*OPEN  ' T@SKIP             Open only
     C                     END                             FI
      *
      * Closedown request - close files :
      *
     C           P@RTCD    IFEQ '*CLOSE '                  IF CLOSEDOWN
     C                     CLOSESTOPCL0                94    Close file
     C           *IN94     CABEQ'1'       T@SKIP         LR  Close error
     C                     CLOSESTOPCL1                94    Close file
     C           *IN94     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                         LR  Free program
     C                     GOTO T@SKIP                       Exit
     C                     END                             FI
      *
      * Check selection required :
      *
     C           P@CHQN    IFEQ *BLANK                     IF   NO CHEQUE
     C           P@AMNT    ANDEQ*ZERO                       AND NO AMOUNT
     C                     SETON                     91      Selection err
     C                     GOTO T@SKIP                       Exit program
     C                     END                             FI
      *
      * Check key required :
      *
     C           P@RETL    IFNE *BLANK                     IF RETAIL KEY
     C                     SETON                     51      Retail key
     C                     END                             FI
      *
     C***********P@CNUM    IFNE *BLANK                     IF FULL A/C    S01230
     C           P@CUST    IFNE *BLANK                     IF FULL A/C    S01230
     C           P@CUCD    ANDNE*BLANK                      AND
     C           P@ACCD    ANDNE*BLANK                      AND
     C           P@ACSQ    ANDNE*BLANK                      AND
     C           P@BRCA    ANDNE*BLANK                      AND           S01117
     C                     SETOF                     51      Full key
     C                     ELSE                            EL NOT FULL A/C
     C           *IN51     CABEQ'0'       T@SKIP         92  Key error
     C                     END                             FI
      *
      * Set to stopped cheque group :
      *
     C  N51      K@L0      SETLL@STPL0               90  03=Account found
     C   51      W@RETL    SETLL@STPL1               90  03=Account found
     C           *IN03     CABEQ'0'       T@SKIP           No stops
     C           *IN90     CABEQ'1'       T@SKIP         LRFile error
      *
      * Read to find stopped cheque match with selection :
      *
     C           *IN05     DOUEQ'1'                        DO TILL FOUND
     C  N51      K@L0      READE@STPL0                 9004  Get record
     C   51      W@RETL    READE@STPL1                 9004  Get record
     C           *IN04     CABEQ'1'       T@SKIP             No record
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C           FCHQ      IFNE *ZERO                      IF CHEQUE SEL.
     C           RECI      ANDNE'*'                                                         AR783021
     C           W@CHQN    ANDNE*ZERO                      AND
     C           W@CHQN    CABEQFCHQ      T@SKIP         05  Found by chq.
     C           TCHQ      IFNE *ZERO                      IF CHEQUE RANGE
     C           W@CHQN    ANDGEFCHQ                       AND
     C           W@CHQN    ANDLETCHQ                       AND
     C           RECI      ANDNE'*'                                                         AR783021
     C                     SETON                         05  Found in rng.
     C                     GOTO T@SKIP                       Exit loop
     C                     END                             FI
     C                     END                             FI
     C           P@AMNT    IFNE *ZERO                      IF AMOUNT
     C           FCHQ      IFEQ *ZERO                                     059502
     C           RECI      ANDNE'*'                                                         AR783021
     C           W@CHQN    OREQ *ZERO                                     126299
     C           RECI      ANDNE'*'                                                         AR783021
     C           P@AMNT    CABEQSTPA      T@SKIP         05  Found by amt.
     C                     END                                            059502
     C                     END                             FI
     C                     END                             OD
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   05                MOVE '*STOP  ' P@RTCD           Stopped cheque
     C   90                MOVE '*ERROR ' P@RTCD           Error
     C   91                MOVE '*SELECT' P@RTCD           Select error
     C   92                MOVE '*KEY   ' P@RTCD           Key error
     C   93                MOVE '*OPEN  ' P@RTCD           Open error
     C   94                MOVE '*CLOSE ' P@RTCD           Close error
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF   SEND MSG
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C***05                MOVE 'ACO0051' P@MGID             Cheque stop  S01314
     C***90                MOVE 'ACO0001' P@MGID             File error   S01314
     C***91                MOVE 'ACO0052' P@MGID             Select error S01314
     C***92                MOVE 'ACO0008' P@MGID             Key error    S01314
     C***93                MOVE 'ACO0002' P@MGID             Open error   S01314
     C***94                MOVE 'ACO0003' P@MGID             Close error  S01314
     C   05                MOVE 'ACO1051' P@MGID             Cheque stop  S01314
     C   90                MOVE 'ACO1001' P@MGID             File error   S01314
     C   91                MOVE 'ACO1052' P@MGID             Select error S01314
     C   92                MOVE 'ACO1008' P@MGID             Key error    S01314
     C   93                MOVE 'ACO1002' P@MGID             Open error   S01314
     C   94                MOVE 'ACO1003' P@MGID             Close error  S01314
     C                     CALL 'ZASNMG'               99    <SEND MSG.>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Pgm.name
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Msg.ID
     C                     PARM *BLANK    P@MSGF 10          I:Msg.file
     C                     PARM *BLANK    P@MGKY  4          O:Msg.key
     C*********************PARM *BLANK    P@MGDA 50          I:Msg.data   S01230
     C                     PARM           P@MGDA 50          I:Msg.data   S01230
     C                     PARM '*DIAG'   P@MGTP  7          I:Msg.type
     C                     END                             FI
      *                                                                   S01230
      * Caller Requests DB Error Handling                                 S01230
      *                                                                   S01230
     C           W@RTCD    IFEQ '*DBERR'                   IF             S01230
     C           P@RTCD    ANDNE*BLANK                       AND          S01230
     C                     DUMP                                           S01230
     C                     CALL 'DBERRCTL'                                S01230
     C                     END                             FI             S01230
      *                                                                   S01230
      * Return to caller :
      *
     C                     RETRN
      *
      *****************************************************
      *ENDMAIN
**
STOPCPD  AOSTOPV0  Verify Stopped Cheques
** CPY@     : Copyright notice for inclusion in all programs           grams
(c) Finastra International Limited 2001
