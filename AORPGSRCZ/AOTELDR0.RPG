     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AO Teller Identification Access Object')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Access Objects                                   *
     F*                                                               *
     F*  AOTELDR0 - Read Access to Teller Identification Details      *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. CRT001  *CREATE    DATE 28FEB95               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  CRT001 - Created for Retail Teller System                    *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F* ID F  C  H  L  FUNCTION OF INDICATORS                         *
     F*                                                               *
     F*       01       OFF on the first run                           *
     F*       90       File error                                     *
     F*       91       OPEN error                                     *
     F*       92       CLOSE error                                    *
     F*       93       Empty file                                     *
     F*       94       EOF
     F*       95       No Record Found
     F*       96       Parameter Error
     F*       LR       Last Record
     F*                                                               *
     F*****************************************************************
     F*
     FSDTELDL1IF  E           K        DISK                           UC
     F** RTS Teller Identification File Retrieval
     F*
     F*****************************************************************
     F*
     E/EJECT
     E*
     E                    @MG     1   1 50
      ************************************************************
     E* Description     : Copyright notice for inclusion in all programs
     E*
     E********************************************************************
     E*
     E                    CPY@    1   1 80               Copyright     array
     E*
     I/EJECT
     I*
     IP@MGDA      DS
     I                                        1  50 @MG
     I*
     IPGMDS      SDS
     I* Program Status data structure
     I                                        1  10 PGNAME
     I                                       81  90 PGLIBR
     I                                      244 253 PGJBNM
     I                                      254 263 PGUSER
     I                                      264 2690PGJBNO
     I*
     ID@FMT     E DSSDTELDPD
     I** @BTREE4 - Teller ID Record Format Data Structure
     I*
     IP@FMT     E DSDSFDY
     I** External Data structure to hold the outgoing record format of
     I** the file
     I*
     IA@CPY       DS
     I** Copyright array - Copyright notice for inclusion in all
     I** programs
     I                                        1  80 CPY@
     I*
     C/EJECT
     C*================================================================
     C*  P R O G R A M   S T A R T                                    *
     C*================================================================
     C*
     C** Receive entry parameters
     C*
     C           *ENTRY    PLIST
     C           W@RTCD    PARM           P@RTCD  7        B:Return code
     C                     PARM           P@OPTN  7        I:Option
     C                     PARM           P@TLID  3        I:Key field
     C                     PARM D@FMT     P@FMT            O:Format
      *
      * Access Option *SAME input
      *
     C           P@OPTN    IFEQ '*SAME  '
     C           *IN01     COMP '0'                      91
     C                     GOTO T@SKIP
     C                     END
      *
      *MAIN
     C                     SETOF                     909192 ¦ Reset
     C                     SETOF                     939495 ¦ error
     C                     SETOF                     96     ¦ indicators
      *
      * First call - open file :
      *
     C           *IN01     IFEQ '0'                        IF 1 FIRST TIME
     C                     SETON                     01      Set ind.
     C                     OPEN SDTELDL1               91    Open file
     C           *IN91     CABEQ'1'       T@SKIP         LR  Open error
     C                     END                             FI 1
      *
      * Last call - close file and terminate program :
      *
     C           P@OPTN    IFEQ '*FREE  '                  IF 2 FREE PGM
     C                     CLOSESDTELDL1               92    Close file
     C           *IN92     CABEQ'1'       T@SKIP         LR  Close error
     C                     SETON                     LR      Set ind.
     C                     END                             FI 2
      *
      * Caller requests first record :
      *
     C           P@OPTN    IFEQ '*FIRST '                  IF 3 FIRST REC
     C           *LOVAL    SETLL@TELDL1              9390    Set file
     C           *IN93     CABEQ'1'       T@SKIP             Empty file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*NEXT  ' P@OPTN             Force *Next
     C                     END                             FI 3
      *
      * Caller requests next record :
      *
     C           P@OPTN    IFEQ '*NEXT  '                  IF 4 NEXT REC
     C                     READ @TELDL1                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 4
      *
      * Caller requests last record :
      *
     C           P@OPTN    IFEQ '*LAST  '                  IF 5 LAST REC
     C           *HIVAL    SETGT@TELDL1                90    Set file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     MOVE '*PREV  ' P@OPTN             Force *Prev
     C                     END                             FI 5
      *
      * Caller requests previous record :
      *
     C           P@OPTN    IFEQ '*PREV  '                  IF 6 PREV.REC
     C                     READP@TELDL1                9094  Read record
     C           *IN94     CABEQ'1'       T@SKIP             End of file
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 6
      *
      * Caller requests record indicated by key :
      *
     C           P@OPTN    IFEQ '*KEY   '                  IF 7 KEYED ACC
     C           P@OPTN    OREQ '*VERIFY'                    OR
     C           P@TLID    CHAIN@TELDL1              9590    Get record
     C           *IN95     CABEQ'1'       T@SKIP             No record fnd
     C           *IN90     CABEQ'1'       T@SKIP         LR  File error
     C                     END                             FI 7
      *
     C           P@OPTN    IFNE '*KEY   '                  IF 8 NOT KEYED
     C           P@OPTN    ANDNE'*NEXT  '                   AND NOT SEQ.
     C           P@OPTN    ANDNE'*PREV  '                   AND
     C           P@OPTN    ANDNE'*VERIFY'                   AND
     C           P@OPTN    ANDNE'*FREE  '                   AND
     C                     SETON                       96    Parameter err
     C                     END                             FI 8
      *
      * No record found or End of file - return error code :
      *          _____________
     C           T@SKIP    TAG                             +++ SKIP +++
      *          ~~~~~~~~~~~~~
     C                     MOVE *BLANK    P@RTCD           No Errors
     C   90                MOVE '*ERROR ' P@RTCD           File error
     C   91                MOVE '*OPEN  ' P@RTCD           Open error
     C   92                MOVE '*CLOSE ' P@RTCD           Close error
     C   93                MOVE '*EMPTY ' P@RTCD           Empty file
     C   94                MOVE '*EOF   ' P@RTCD           End of file
     C   95                MOVE '*NRF   ' P@RTCD           No record found
     C   96                MOVE '*OPTION' P@RTCD           Option error
      *
      * Caller requests message sent to program queue :
      *
     C           W@RTCD    IFEQ '*MSG   '                  IF 9 SEND MSGM
     C           P@RTCD    ANDNE*BLANK                      AND ERROR
     C   90                MOVE 'ACO1001' P@MGID             File error
     C   91                MOVE 'ACO1002' P@MGID             Open error
     C   92                MOVE 'ACO1003' P@MGID             Close error
     C   93                MOVE 'ACO1004' P@MGID             Empty file
     C   94                MOVE 'ACO1005' P@MGID             End of file
     C   95                MOVE 'ACO1006' P@MGID             No record fnd
     C   96                MOVE 'ACO1007' P@MGID             Option error
     C                     CALL 'ZASNMG'                     <SEND MSG>
     C                     PARM *BLANK    W@RTCD  7          B:Return code
     C                     PARM PGNAME    P@PGNM 10          I:Program nam
     C                     PARM '*PRV'    P@DEST  5          I:Destination
     C                     PARM           P@MGID  7          I:Message ID
     C                     PARM *BLANK    P@MSGF 10          I:Message fil
     C                     PARM *BLANK    P@MGKY  4          O:Message key
     C                     PARM           P@MGDA 50          I:Message dta
     C                     PARM '*DIAG'   P@MGTP  7          I:Message tpe
     C                     END                             FI 9
      *
      * Caller Requests DB Error Handling
      *
     C           W@RTCD    IFEQ '*DBERR '                  IF
     C           P@RTCD    ANDNE*BLANK                       AND
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     END                             FI
      *
     C           P@OPTN    IFNE '*VERIFY'                  IF 10
     C           P@OPTN    ANDNE'*FREE  '                    AND
     C           P@RTCD    ANDNE'*OPTION'                    AND
     C                     MOVELD@FMT     P@FMT            FI 10
     C                     END
     C*
     C** Return to caller
     C*
     C                     RETRN
     C*
     C*****************************************************************
**
SDTELDPD  AOTELDR0  Teller Identification Details
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
