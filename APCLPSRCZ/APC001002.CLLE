/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas AP Transaction Transferred to PEA')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - APIs                                                */
/*                                                                   */
/*       APC001002 - Midas AP Transactions Transferred to PEA        */
/*                                                                   */
/*       (c) Finastra International Limited 2012                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CSC054  *CREATE    Date 23Feb12              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSC054 - Period End Adjustments                             */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PSEQ &LEVL &PENTY)
 
             DCL        VAR(&PSEQ) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LEVL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PENTY) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PEAIND) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MPHAS)  TYPE(*CHAR)  LEN(1)
 
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
 
/*/COPY SDCPYSRC,SDSVALDCL  */
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2012')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
             SNDPGMMSG  MSG('APC001002 - Midas AP Transaction   +
                          Transferred Report') TOMSGQ(MRUNQ)
 
/** Get PEA Indicator */
 
             CALL       PGM(AOSVALR0) PARM(&RSVALRTNC +
                          'PEAIndicator' &SVAL1 +
                          &SVALK2 &SVAL2        +
                          &SVALK3 &SVAL3 &SVALK4 &SVAL4 +
                          &SVALK5 &SVAL5 &SVALK6 &SVAL6 +
                          &SVALK7 &SVAL7 &SVALK8 &SVAL8 +
                          &SVALK9 &SVAL9 &SVALK10 &SVAL10)
 
             CHGVAR     VAR(&PEAIND) VALUE(%SST(&SVAL1 1 1))
 
/** Initialise job switches and create data area */
 
             CHGJOB     SWS(XXXXXX00)
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
 
             RTVDTAARA  DTAARA(MPHAS) RTNVAR(&MPHAS)
 
/** End program if run in PEA System input cycle */
 
             IF         COND((&MPHAS = 'A') *AND (&PEAIND = 'Y')) +
                        THEN(DO)
             GOTO       CMDLBL(END)
             ENDDO
 
/** Call Report Program */
 
             CALL       PGM(AP001002) PARM(&PSEQ &LEVL &PENTY)
 
/** Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             GOTO       CMDLBL(END)
 
/** Abnormal termination */
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          APC001002 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
             ENDPGM
