      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AP API reporting prompt')
      *****************************************************************
      *                                                               *
      *  Midas - API                                                  *
      *                                                               *
      *  DSPF/AP0006DF - API Reporting Prompt                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 22Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP002  *CREATE    Date 18Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing changes                                   *
      *  CAP002 - Conversion of transaction inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      ********************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
 
      **  Display format.
     A          R AP0006F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CA03(03 'Exit.')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A            USER          10A  O  1  2TEXT('*USER')
     A                                  1 14'AP0006'
     A            BJMRDT         7A  O  1 72TEXT('Midas Run Date')
     A            JOB           10A  O  2  2TEXT('*JOB')
     A                                  2 28'API Reporting Prompt'
     A                                      DSPATR(HI)
     A                                  2 71TIME
 
     A                                  6 28'Sequence of Report.:'
     A            S1RSEQ         1A  B  6 52OVRDTA
     A N40                                  DSPATR(UL HI)
     A  40                                  DSPATR(RI PC)
 
     A                                  8 28'Select 1 for arrival sequence'
     A                                  9 28'Select 2 for sender reference'
     A                                 10 28'Select 3 for Midas module/deal no'
 
     A                                 13 28'Input X to Select Type:'
     A                                 14 28'Leave blank for all deals.'
     A                                 16 28'Insertions.........:'
     A            S1INSE         1A  B 16 52OVRDTA
     A N41                                  DSPATR(UL HI)
     A  41                                  DSPATR(RI PC)
     A                                 17 28'Amendments.........:'
     A            S1AMDS         1A  B 17 52OVRDTA
     A N42                                  DSPATR(UL HI)
     A  42                                  DSPATR(RI PC)
     A                                 18 28'Deletions..........:'
     A            S1DELS         1A  B 18 52OVRDTA
     A N43                                  DSPATR(UL HI)
     A  43                                  DSPATR(RI PC)
     A                                 19 28'Authorisations.....:'
     A            S1AUTH         1A  B 19 52OVRDTA
     A N44                                  DSPATR(UL HI)
     A  44                                  DSPATR(RI PC)
 
     A                                 23  2'F3=Exit'
 
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
 
      *----------------------------------------------------------------=========
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86
     AO 88                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
