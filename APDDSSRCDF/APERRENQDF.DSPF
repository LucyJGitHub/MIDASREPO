/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas AP API errors enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - API Interface Module                                 *
     A*                                                               *
     A*  APERRENQDF  - API ERRORS ENQUIRY                             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CAP004  *CREATE    Date 03Oct98               *
     A*  Prev Amend No.                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP004 - APIs phase 3.                                       *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R APERRENQS1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDLDES        46A     5  7
     A  95                                  DSPATR(HI)
     A            DDAMNT         7A     5 54
     A                                      DSPATR(HI)
     A            DDETXT        14A     5 63TEXT('"ERROR"')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SCREEN CONTROL                                                   *
     A* -------------------------------------------------------------------*
     A*
     A          R APERRENQS0                TEXT('SCREEN CONTROL')
     A                                      CF03
     A                                      SFLCTL(APERRENQS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'APERRENQ'
     A                                  1 33'API ERRORS ENQUIRY'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A            DDSFRN         4  0H      SFLRCDNBR
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R APERRENQF1                TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
