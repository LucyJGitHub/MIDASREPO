      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AP Automatic trade number maintenance')
     A****************************************************************
     A*                                                              *
     A*  Midas - API Interface Module                                **
     A*                                                              *
     A*   DSPF/APTRDNODF - TRADE NUMBER MAINTENANCE SCREEN.          *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CAP040             Date 29JUN00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. CAP006  *CREATE    Date 23JAN99               *
     A*                                                              *
     A*                                                              *
     A****************************************************************
     A*                                                               *
     A*  CAP040 - Addition of SD customer numbers to automatic        *
     A*           generation                                          *
     A*  CAP006 - APIs phase 4.                                       *
     A*****************************************************************
     A**  INDICATOR FUNCTION SUMMARY
     A*
     A*       10       SUBFILE END CONDITIONING INDICATOR
     A*
     A*       11       SCREEN BUILD 1 - INITIAL SCREEN.
     A*       12       SCREEN BUILD 2 - ENQUIRE SCREEN.
     A*       13       SCREEN BUILD 3 - AMEND SCREEN.
     A*
     A*       20       INPUT ERROR ON SCREEN BUILD 1 - ACTION CODE.
     A*       21       INPUT ERROR ON SCREEN BUILD 3 - RESERVE DEAL NO.
     A*
     A*       KA       CA03 INDICATOR
     A*       KB       CA12 INDICATOR
     A*
     A*       80
     A*       |        USED IN STANDARD SUBROUTINES
     A*       89
     A*
     A*          U7    SET ON WITH U8 IF A DATABASE ERROR OCCURS.
     A*          U8    FILE OUT OF BALANCE IF ON ON ITS OWN.
     A*
     A*****************************************************************
     A*            CA03 - Exit program
     A                                      CA03
     A*            CA12 - Return to previous Screen.
     A                                      CA12
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A*
     A**  MESSAGE SUBFILE FORMAT
     A          R APTRDNOD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A*
     A**  MAIN SCREEN FORMAT - ALSO CONTROLS MESSAGE SUBFILE FORMAT
     A          R APTRDNOD1                 TEXT('SUBFILE CONTROL FMT')
     A                                      SFLCTL(APTRDNOD0)
     A                                      SFLSIZ(4)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  10                                  SFLEND
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A*
     A                                      PUTOVR
     A                                      BLINK
     A                                      OVERLAY
     A*
     A            PGMQ                      SFLPGMQ
     A*
     A                                  1  2'Ref:'
     A                                  1 10'APTRDNOMNT'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0O  1 63EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A                                  1 72'WID:'
     A            DDWID     R     A  O  1 77REFFLD(WSID)
     A*
     A                                  3  2'Action'
     A            DDACTN         1A  B  3 10
     A                                      OVRDTA
     A  11                                  DSPATR(UL)
     A  11
     AO 14                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A N11                                  DSPATR(PR)
     A                                  3 12'( ,A, ,E)'
      *                                                    /Start of CAP040/
     A*************                     3 28'TRADE NUMBER MAINTENANCE'    CAP040
 
      ** Hidden field to hold the message ID (Screen heading)
     A            SCNHEAD        7A  H
     A            DDSCNHEAD     59A  O  3 23MSGID(&SCNHEAD ZZGBMSGF)
      *                                                      /End of CAP040/
 
     A  30                              5 30'FUTURES AND OPTIONS'
     A  31                              5 32'SECURITY TRADES'
     A  32                              5 32'DEPOT MOVEMENTS'
     A*
     A                                 10 49'CURRENT'
     A                                      OVRATR
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(ND)
     A                                 10 62'DESIRED'
     A                                      OVRATR
       N11
     AAN12                                  DSPATR(UL)
     A  11
     AO 12                                  DSPATR(ND)
 
      *                                                    /Start of CAP040/
     A***********                      12  3'Next Automatic Trade Number to'
     A***********                           OVRATR
     A**11*******                           DSPATR(ND)
     A***********                      12 34'be used:'
     A***********                           OVRATR
     A**11*******                           DSPATR(ND)
 
      ** Hidden field to hold the message ID ('Next automatic number to
      ** be used')
     A            NXTAUTOUSE     7A  H
     A            DDAUTOUSE     44A  O 12  3MSGID(&NXTAUTOUSE ZZGBMSGF)
     A                                      OVRATR
     A  11                                  DSPATR(ND)
      *                                                      /End of CAP040/
 
     A            DDNDLN    R        O 12 49REFFLD(DLNO)
     A N11                                  OVRDTA
     A  11                                  OVRATR
     A  11                                  DSPATR(ND)
     A*
 
      *                                                    /Start of CAP040/
     A***********                      14  3'Manual Trade Range starts at'
     A***********                           OVRATR
     A**11*******                           DSPATR(ND)
     A***********                      14 32'Trade No.:'
     A***********                           OVRATR
     A**11*******                           DSPATR(ND)
 
      ** Hidden field to hold the message ID ('Manual range starts at')
     A            RNGSTRT        7A  H
     A            DDRNGSTRT     44A  O 14  3MSGID(&RNGSTRT ZZGBMSGF)
     A                                      OVRATR
     A  11                                  DSPATR(ND)
      *                                                      /End of CAP040/
 
     A            DDRDLN    R        O 14 49REFFLD(DLNO)
     A N11                                  OVRDTA
     A  11                                  OVRATR
     A  11                                  DSPATR(ND)
     A**********                        9 41'Reserve'
     A**********                            OVRATR
     A**11
     A*O*12                                  DSPATR(ND)
     A            DDARDN    R     Y  B 14 62REFFLD(DLNO)
     A N11
     AAN12                                  OVRDTA
     A  11
     AO 12                                  OVRATR
     A  11
     AO 12                                  DSPATR(ND)
     A  13                                  DSPATR(UL PC)
     A N13                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A*
 
      *                                                    /Start of CAP040/
     A*************                    16  3'Next Automatic Trade Number to'
     A*************                         OVRATR
     A**11*********                         DSPATR(ND)
     A*************                    16 34'be generated:'
     A*************                         OVRATR
     A**11*********                         DSPATR(ND)
 
      ** Hidden field to hold the message ID ('Next automatic number to
      ** be generated')
     A            NXTAUTOGEN     7A  H
     A            DDAUTOGEN     44A  O 16  3MSGID(&NXTAUTOGEN ZZGBMSGF)
     A                                      OVRATR
     A  11                                  DSPATR(ND)
      *                                                      /End of CAP040/
 
     A            DDTNBF    R        O 16 49REFFLD(DLNO)
     A N11                                  OVRDTA
     A  11                                  OVRATR
     A  11                                  DSPATR(ND)
     A*
     A** COMMAND KEY OUTPUT FORMAT
     A          R APTRDNOD2                 TEXT('FORMAT TO OUTPUT+
     A                                       CMD KEY DESCRIPTIONS')
     A                                      BLINK
     A                                      PUTOVR
     A                                      OVERLAY
     A                                 24  2'F3=Exit   F12=Previous  '
