      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas AP List of JE Valid Errors - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - API Module                                           *
      *                                                               *
      *  PRTF/AP0007AU - List of JE Valid Errors - Audit              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. 186206  *CREATE    Date 26Oct00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  186206 - Journal Entry API fixes. Produce only one dump      *
      *           per JE batch. Generate JE Valid errors report.      *
      *           Prevent record locking error in JE database         *
      *           updater.                                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------------------*
      ** Report Header Record Format **
      *-------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AP0007AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'JOURNAL ENTRY +
     A                                       VALID ERRORS - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    42'--------------+
     A                                       --------------------'
     A                                      SPACEA(3)
      *
      *----------------*
      ** File Control **
      *----------------*
      *
     A          R CTRLAU
     A                                    60'CALCULATED'
     A                                      SPACEA(1)
     A                                    60'----------'
     A                                      SPACEA(3)
      *
     A                                     6'JE Headers in error from +
     A                                      DBU function - PF/GLVEBITHPD'
     A            RCOUNTH        5  0     62EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'-------------------------+
     A                                      ----------------------------'
     A                                      SPACEA(2)
      *
     A                                     6'JE Items in error from +
     A                                      DBU function - PF/GLVEBITPPD'
     A            RCOUNTP        5  0     62EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'-----------------------+
     A                                      ----------------------------'
     A                                      SPACEA(2)
      *
      *--------------*
      ** No Details **
      *--------------*
      *
     A          R NODTAU
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *------------------*
      ** Database Error **
      *------------------*
      *
     A          R DBERAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
