      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AP List of JE Valid Errors')
      *****************************************************************
      *                                                               *
      *  Midas - API Module                                           *
      *                                                               *
      *  PRTF/AP0007P1 - List of JE Valid Errors                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. 186206  *CREATE    Date 26Oct00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  186206 - Journal Entry API fixes. Produce only one dump      *
      *           per JE batch. Generate JE Valid errors report.      *
      *           Prevent record locking error in JE database         *
      *           updater.                                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------------------*
      ** Report Header Record Format **
      *-------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AP0007P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'JOURNAL ENTRY +
     A                                      VALID ERRORS'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'--------------+
     A                                      ------------'
     A                                      SPACEA(2)
      *
      *------------------------*
      ** Detail Record Format **
      *------------------------*
      *
     A          R DTLHP1                    TEXT('JE Headers')
     A                                      SPACEB(1)
      *
     A                                     6'Journal Entry Header:'
     A                                      SPACEA(1)
      *
     A                                     6'Batch Number ........:'
     A            RBTNB          3        29TEXT('Batch Number')
     A                                    55'Front Office ID:'
     A            RFRNT         20        72TEXT('Front Office ID')
     A                                      SPACEA(1)
      *
     A                                     6'Branch ..............:'
     A            RBRCA          3        29TEXT('Branch')
     A                                    55'Description ...:'
     A            RBDES         30        72TEXT('Batch Description')
     A                                      SPACEA(1)
      *
     A                                     6'Department Code .....:'
     A            RDEPT          3        29TEXT('Department')
     A                                      SPACEA(1)
      *
     A                                     6'Multiple Branch Batch:'
     A            RMBRB          1        29TEXT('Mult. Branch Batch')
     A                                      SPACEA(1)
      *
     A                                     6'Terminal ............:'
     A            RWSID         10        29TEXT('Workstation ID')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ** Detail Record Format 2 **
      *--------------------------*
      *
     A          R DTLPP1                    TEXT('JE Items')
     A                                      SPACEB(1)
      *
     A                                     6'Journal Entry Posting'
     A            RBINB          3  0     28TEXT('Batch Item no.')
     A                                      EDTCDE(4)
     A                                    31':'
     A                                      SPACEA(1)
      *
     A                                     6'Batch Number:'
     A            RBTNB          3        20TEXT('Batch Number')
     A                                    55'Front Office ID:'
     A            RFRNT         20        72TEXT('Front Office ID')
     A                                      SPACEA(1)
      *
     A                                     6'Account Number:'
     A            RACID         10        22TEXT('Account Identifier')
     A            RCYCD          3        33TEXT('Currency Code')
     A            RACCD          4        37TEXT('Account Code')
     A            RACSN          2        42TEXT('Acc. Seq. no.')
     A            RIBCA          3        45TEXT('Input Branch')
     A                                    50'Amount:'
     A            RFMPA         14        58TEXT('Posting Amount')
     A                                    74'DR/CR:'
     A            RDCIN          1        81TEXT('Debit/Credit indicator')
     A                                    84'Narrative:'
     A            RNRTV         30        95TEXT('Narrative')
     A                                      SPACEA(1)
      *
     A                                     6'Type:'
     A            RTRTY          5        12TEXT('Transaction type')
     A                                    19'Sub Type:'
     A            RTSTY         10        29TEXT('Transaction subtype')
     A                                    41'Value date:'
     A            RVDDS          6        53TEXT('Value Date')
     A                                    61'Cheque:'
     A            RCQNB          8        69TEXT('Cheque number')
     A                                      SPACEA(1)
      *
     A                                     6'Swift:'
     A            RSWCR         16        13TEXT('Swift Common Ref.')
     A                                    31'Profit centre:'
     A            RPRCN          4        46TEXT('Profit Centre')
     A                                    52'Associated customer:'
     A            RACNB          6        73TEXT('Assoc. Cust. no.')
     A                                      SPACEA(1)
      *
     A                                     6'Book Code:'
     A            RBKCD          2        17TEXT('Book Code')
     A                                    21'Void:'
     A            RRVDI          1        27TEXT('Reverse/Void indicator')
     A                                    30'No. Items:'
     A            RNITM          5  0     41TEXT('Number of Items')
     A                                      EDTCDE(4)
     A                                    48'O''ride:'
     A            RRINO          1        56TEXT('Retail ind.: Override')
     A                                      SPACEA(1)
      *
     A                                     6'Original Ccy/Amount:'
     A            ROCCY          3        27TEXT('Original Currency')
     A            RFMOA         16        31TEXT('Original Amount')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ** Detail Record Format 3 **
      *--------------------------*
      *
     A          R DTLZP1                    TEXT('5 Blanks lines')
     A                                      SPACEB(1)
     A                                     1' '
     A                                      SPACEA(2)
      *
      *------------------*
      ** Report Trailer **
      *------------------*
      *
     A          R TRLP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
