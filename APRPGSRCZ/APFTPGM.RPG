     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FT Trigger Pgm for OTPAYDD/CQPACDD/NTRANDD')
      *****************************************************************
      *                                                               *
      *  Midas - Fund Transfer Module                                 *
      *                                                               *
      *  APFTPGM - FT Trigger Pgm to check if pa/rcv is needed        *
      *                                                               *
      *  Called By: APFTTRIG - FT Trigger Pgm                         *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD058934           Date 28Sep21               *
      *  Prev Amend No. CSW220             Date 09Mar20               *
      *                 MD046248           Date 27Oct17               *
      *                 CAP212             Date 15Sep14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG2692  *CREATE   Date 18May04               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058934 - Recompile due to change in MEINFTPD file.         *
      *  CSW220 - SWIFT Changes 2020                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP212 - FT Nostro Transfer API (Recompile)                  *
      *  BUG2692 - Batch submit of confirmations programs             *
      *                                                               *
      *****************************************************************
      *
     FOTPAY1  IF  E           K        DISK         KINFSR *PSSR
     FCQPAC1  IF  E           K        DISK         KINFSR *PSSR
     FNTRAN1  IF  E           K        DISK         KINFSR *PSSR
     FMEINFTL1IF  E           K        DISK         KINFSR *PSSR                              CSW220
      *
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *                                                                                       CSW220
      ** DECLARE SWIFTACTV CONSTANT                                                           CSW220
      *                                                                                       CSW220
     I              'SWIFTACTV'           C         SWIFT                                     CSW220
      *                                                                                       CSW220
      *
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C           *ENTRY    PLIST
     C                     PARM           RETURN  7
      *
     C                     MOVEL*BLANKS   RETURN
      *
      ** If at least 1 record found, return = *YES
      ** If not record found, return = *NO
      ** If error is found, return = *ERROR
      *
     C           *LOVAL    SETLLOTPAYDDF
     C                     READ OTPAYDDF                 10
      *
      ** If not found, read next file
      *
     C           *IN10     IFEQ '0'
     C                     MOVEL'*YES   ' RETURN
     C                     ELSE
      *
     C           *LOVAL    SETLLCQPACDDF
     C                     READ CQPACDDF                 11
      *
      ** If not found, read next file
      *
     C           *IN11     IFEQ '0'
     C                     MOVEL'*YES   ' RETURN
     C                     ELSE
      *
     C           *LOVAL    SETLLNTRANDDF
     C                     READ NTRANDDF                 12
      *
      ** If not found, return  is *NO
      *
     C           *IN12     IFEQ '0'
     C                     MOVEL'*YES   ' RETURN
     C                     ELSE
     C                     MOVEL'*NO    ' RETURN
      *                                                                                       CSW220
      ** Determine if Swift 2020 is activated                                                 CSW220
      *                                                                                       CSW220
     C                     CALL SWIFT                                                         CSW220
     C                     PARM 'CSW220'  PSWIFT  6                                           CSW220
     C                     PARM *BLANKS   PRTCD   7                                           CSW220
      *                                                                                       CSW220
     C           PRTCD     IFEQ 'CSWACTV'                                                     CSW220
     C                     MOVE 'Y'       CSW220  1                                           CSW220
     C                     ELSE                                                               CSW220
     C                     MOVE 'N'       CSW220                                              CSW220
     C                     ENDIF                                                              CSW220
      *                                                                                       CSW220
     C           CSW220    IFEQ 'Y'                                                           CSW220
     C           *LOVAL    SETLL@INFTPD                                                       CSW220
     C                     READ @INFTPD                  13                                   CSW220
     C           *IN13     IFEQ '0'                                                           CSW220
     C                     MOVEL'*YES   ' RETURN                                              CSW220
     C                     ENDIF                                                              CSW220
     C                     ENDIF                                                              CSW220
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C                     DUMP
     C                     MOVE '*ERROR ' RETURN
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2004
