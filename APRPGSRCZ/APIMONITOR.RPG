     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas API Server Monitor Program')
      *****************************************************************
      *                                                               *
      *  Midas - API Interface Module                                 *
      *                                                               *
      *  APIMONITOR - Midas API Server Monitor Program                *
      *                                                               *
      *  Function:  This program controls the submission and removal  *
      *             of APIINPUT programs from the Midas subsystem.    *
      *                                                               *
      *  Called By: mmCnnnn - (program name)                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK025             Date 11May06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 XNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK025 - PAckaging changes for MidasPlus 1.3.                *
      *                                                               *
      *****************************************************************
      *
      ** File to store active job details
     FAPIJOBL0UF  E           K        DISK
      *
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E                    CMD     1   1 14
      ** Array containing Delay job command
      *
     E                    SELS        1  9 0
      ** Array of selectors (properties of queue to inquire after)
      *
     E                    IAV         1  9 0
      ** Output from queue inquiry
      *
      ********************************************************************
      *
      ***MQI*named*constants*                                                                 CPK025
     I*COPY*QRPGSRC,CMQR*                                                                     CPK025
     I/COPY QMQM/QRPGSRC,CMQR                                                                 CPK025
      *
      ** Object Descriptor
     IMQOD        DS
     I*COPY*QRPGSRC,CMQODR*                                                                   CPK025
     I/COPY QMQM/QRPGSRC,CMQODR                                                               CPK025
      *
      ** Message Descriptor
     IMQMD        DS
     I*COPY*QRPGSRC,CMQMDR*                                                                   CPK025
     I/COPY QMQM/QRPGSRC,CMQMDR                                                               CPK025
      *
      ** Get message options
     IMQGMO       DS
     I*COPY*QRPGSRC,CMQGMOR*                                                                  CPK025
     I/COPY QMQM/QRPGSRC,CMQGMOR                                                              CPK025
      *
      ** Put message options
     IMQPMO       DS
     I*COPY*QRPGSRC,CMQPMOR*                                                                  CPK025
     I/COPY QMQM/QRPGSRC,CMQPMOR                                                              CPK025
      *
      ** Queue name
     I              'MIDAS.INPUT.QUEUE'   C         QIN
      ********************************************************************
      *
      *   Main Processing
      *
      ********************************************************************
      *
     C                     EXSR OPENQ
      *
      ** Loop until job terminated
      *
     C           *IN90     DOWNE*ON
      *
     C                     EXSR DEPTH
     C                     EXSR ACTIVE
     C           NOACT     DOWLTNOREQ
     C                     EXSR START
     C                     ENDDO
      *
      ** Shut down one job at a time to prevent overreaction
      *
     C           NOACT     IFGT NOREQ
     C                     EXSR STOP
     C                     ENDIF
      *
      ** Shutdown processing
      *
     C                     SHTDN                     90
      *
      ** Delay job
      *
     C           *IN90     IFEQ *OFF
     C                     Z-ADD14        LEN    155
     C                     CALL 'QCMDEXC'
     C                     PARM           CMD    14
     C                     PARM           LEN
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           NOACT     DOWGT0
     C                     EXSR STOP
     C                     ENDDO
     C                     EXSR CLOSEQ
      *
     C                     SETON                     LR
     C                     RETRN
      ********************************************************************
      *
      *   Inquire on current queue depth, and judge required number
      *   of processing programs
      *
      ********************************************************************
     C           DEPTH     BEGSR
      *
      ** Reset arguments
      *
     C                     Z-ADDIACDEP    SELS,1
     C                     Z-ADD1         IACNT
     C                     Z-ADD0         CACNT
      *
      ** Inquire about number of messages on queue
      *
     C                     Z-ADDMQINQ     CID
     C                     CALL 'QMQM'
     C                     PARM           CID     90
     C                     PARM           HCONN   90
     C                     PARM           HINQ    90
     C                     PARM           SELCNT
     C                     PARM           SELS
     C                     PARM           IACNT   90
     C                     PARM           IAV
     C                     PARM           CACNT   90
     C                     PARM           CAV    20
     C                     PARM           CCODE   90
     C                     PARM           REASON  90
      *
      ** Error processing
      *
     C           REASON    IFNE RCNONE
     C                     ENDIF
      *
      ** Judge required number of input programs
      *
     C                     Z-ADD1         NOREQ
     C                     SELEC
     C           IAV,1     WHGT 1000
     C                     ADD  8         NOREQ
     C           IAV,1     WHGT 100
     C                     ADD  4         NOREQ   30
     C           IAV,1     WHGT 40
     C                     ADD  3         NOREQ
     C           IAV,1     WHGT 10
     C                     ADD  2         NOREQ
     C                     ENDSL
      *
     C                     ENDSR
      ********************************************************************
      *
      *   Check number of jobs active, remove dead entries from file
      *
      ********************************************************************
     C           ACTIVE    BEGSR
     C                     Z-ADD0         NOACT
     C                     SETOF                     91
     C           *LOVAL    SETLLJOBRCD
      *
      ** Loop to read all records in active jobs file
      *
     C           *IN91     DOWEQ*OFF
     C                     READ JOBRCD                   91
     C           *IN91     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
      *
      ** Call to check if the expected program is active
      *
     C                     CALL 'APICHKSR'
     C                     PARM           NAME   10
     C                     PARM           USER   10
     C                     PARM           NUMBER  6
     C                     PARM           RETURN  1
      *
      ** If yes, count job
      ** If no, remove record from active jobs file
      *
     C           RETURN    IFEQ '0'
     C                     ADD  1         NOACT   30
     C                     ENDIF
     C           RETURN    IFEQ '1'
     C                     DELETJOBRCD
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      ********************************************************************
      *
      *   Submit processing to batch, add details to jobs file
      *
      ********************************************************************
     C           START     BEGSR
      *
      ** Submit job
      *
     C                     CALL 'APISTART'
     C                     ADD  1         NOACT
      *
     C                     ENDSR
      ********************************************************************
      *
      *   Send single 'SHUTDOWN' message to queue
      *
      ********************************************************************
     C           STOP      BEGSR
      *
      ** Set message format to String (to enable ASCII to EBCDIC)
      *
     C                     MOVELFMSTR     MDFMT
      *
      ** Put message (default put message options)
      *
     C                     Z-ADDMQPUT     CID
     C                     CALL 'QMQM'
     C                     PARM           CID     90
     C                     PARM           HCONN   90
     C                     PARM           HINQ    90
     C                     PARM           MQMD
     C                     PARM           MQPMO
     C                     PARM           BUFLEN  90
     C                     PARM           BUFFER  8
     C                     PARM           CCODE   90
     C                     PARM           REASON  90
      *
      ** Error processing
      *
     C           REASON    IFNE RCNONE
     C                     ELSE
     C                     SUB  1         NOACT
     C                     ENDIF
      *
     C                     ENDSR
      ********************************************************************
      *
      *   Open queue for inquiry and input
      *
      ********************************************************************
     C           OPENQ     BEGSR
      *
      ** Open options: INQUIRE and FAIL-IF-QUIESCING
      *
     C           OOINQ     ADD  OOFIQ     OPTS
     C                     ADD  OOOUT     OPTS
      *
      ** Open queue
      *
     C                     Z-ADDMQOPEN    CID
     C                     CALL 'QMQM'
     C                     PARM           CID     90
     C                     PARM           HCONN   90
     C                     PARM           MQOD
     C                     PARM           OPTS    90
     C                     PARM           HINQ    90
     C                     PARM           OCODE   90
     C                     PARM           REASON  90
      *
      ** Error processing
      *
     C           REASON    IFNE RCNONE
     C                     ENDIF
      *
     C                     ENDSR
      ********************************************************************
      *
      *   Close queue
      *
      ********************************************************************
     C           CLOSEQ    BEGSR
      *
      ** Close options: NONE
      *
     C                     Z-ADDCONONE    OPTS
      *
      ** Close queue
      *
     C                     Z-ADDMQCLOS    CID
     C                     CALL 'QMQM'
     C                     PARM           CID     90
     C                     PARM           HCONN   90
     C                     PARM           HINQ    90
     C                     PARM           OPTS    90
     C                     PARM           CCODE   90
     C                     PARM           REASON  90
      *
      ** Error processing
      *
     C           REASON    IFNE RCNONE
     C                     ENDIF
      *
     C                     ENDSR
      ********************************************************************
      *
      *   Initialisation subroutine
      *
      ********************************************************************
     C           *INZSR    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Apply queue name to object descriptor
      *
     C                     MOVELQIN       ODON   48
      *
      ** Use default connection handle (implicit connection)
      *
     C                     Z-ADDHCDEFH    HCONN
      *
      ** Number of attributes to be returned = 1 (current depth)
      *
     C                     Z-ADD1         SELCNT  90
      *
      ** Shutdown message
      *
     C                     Z-ADD8         BUFLEN
     C                     MOVEL'SHUTDOWN'BUFFER
      *
     C                     ENDSR
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
**  CMD      - Commands for QCMDEXC
DLYJOB DLY(10)
