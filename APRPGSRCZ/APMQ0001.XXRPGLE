     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2012')
      *****************************************************************
/*S*D****RPGBASEMOD****************************************************                     MD019854
      *****************************************************************
      *                                                               *
      *  Midas - Application Programming Interfaces module            *
      *                                                               *
      *  APMQ0001 - T_APIMSG Purging Program                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. MD019854 *REDUNDANTDate 03Apr13               *
      *  Prev Amend No. AR574027 *CREATE   Date 20Dec12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD019854 - APCMQ0001 and APMQ0001 should be made redundant   *
      *  AR574027 - T_APIMSG Purging program.                         *
      *             (Child: AR574028)                                 *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   98    - Read indicator for SDSVALPD                         *
      *   99    - Read until EOF of T_APIMSG                          *
      *                                                               *
      *****************************************************************
     FSDSVALL0  IF   E           K DISK    INFSR(*PSSR)
      ** System Value
      *
     FAPIMSGL0  UF   E           K DISK    INFSR(*PSSR)
      ** API MQ Messages
      *
      *****************************************************************
      ** Data Structures and Variables                               **
      *****************************************************************
      *
     DMidasRefDate     S              5P 0
     DMidasDate        S              5P 0
      *
     D APMSGICD        C                   CONST('PurgeDaysOfAP_MQ_MSG')
      *
     D                 DS
     D YYYYMMDD                1      8
     D Year                    1      4
     D Month                   5      6
     D Day                     7      8
      *
     D LDA           E DS           256    EXTNAME(LDA) DTAARA(LDA)
      *
     D DSFDY         E DS                  EXTNAME(DSFDY)
      *
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
      *
      *****************************************************************
      * MAIN PROCESSING
      *****************************************************************
      *
     C                   EXSR      INIT
      *
     C                   EXSR      MAIN
      *
     C                   EVAL      *INLR = *ON
      *
      *****************************************************************
      * INIT
      *****************************************************************
     C     INIT          BEGSR
      *
      ** Retrieve RUNDATE from SDBANKPD
      *
     C                   CALL      'AOBANKR0'
     C                   PARM      *BLANKS       @RTCD             7
     C                   PARM      '*FIRST '     @OPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
      *
      * Database Error
      *
     C     @RTCD         IFNE      *BLANKS
     C                   MOVEL     'SDBANKPD'    DBFILE
     C                   Z-ADD     1             DBASE
     C                   MOVEL     @OPTN         DBKEY
     C                   EXSR      *PSSR
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
      * INIT
      *****************************************************************
     C     MAIN          BEGSR
      *
      ** Retrieve the ICD Settings
      *
     C     APMSGICD      CHAIN     SDSVALL0                           98
     C     *IN98         IFEQ      '1'
     C                   MOVEL     'SDSVALPD'    DBFILE
     C                   Z-ADD     2             DBASE
     C                   MOVEL     @OPTN         DBKEY
     C                   EXSR      *PSSR
     C                   ENDIF
      *
      ** Subtract the ICD Setting from the Rundate.
      *
     C                   EVAL      MidasRefDate = BJRDNB - %INT(GIVAL)
      *
     C     *LOVAL        SETLL     T_APIMSG
     C                   READ      T_APIMSG                               99
      *
     C     *IN99         DOWEQ     '0'
      *
      ** Convert the Timestamp to YYYYMMDD format for MIDAS Day conversion
      *
     C                   EXTRCT    MQTMST:*Y     Year
     C                   EXTRCT    MQTMST:*M     Month
     C                   EXTRCT    MQTMST:*D     Day
      *
     C                   CALLB     'ZDATE10'
     C                   PARM                    YYYYMMDD
     C                   PARM      *ZERO         MidasDate
      *
     C     MidasDate     IFLT      MidasRefDate
     C     MQSTAT        ANDEQ     'P'
      *
     C                   DELETE    T_APIMSG
      *
     C                   ENDIF
      *
     C                   READ      T_APIMSG                               99
      *
     C                   ENDDO
      *
     C                   ENDSR
      *****************************************************************
      * *PSSR                                                         *
      *****************************************************************
     C     *PSSR         BEGSR
      *
     C                   MOVE      *ON           *INU7
     C                   MOVE      *ON           *INU8
     C                   MOVE      *ON           *INLR
     C                   DUMP
      *
     C                   RETURN
      *
     C                   ENDSR
