/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas AS Hedge effectiveness assessment FH trans')    */
/*********************************************************************/
/*                                                                   */
/*       Midas    - Accounting Standards Module                      */
/*                                                                   */
/*       ASC000004 - Hedge Effectiveness Assessment FH Transaction   */
/*                   Control                                         */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CAS006             Date 21Jan03              */
/* Midas Release 4.01 -----------------------------------------------*/
/*                      CAS002  *CREATE    Date 14Jan02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CAS006 - Hedge Accounting Phase B                           */
/*       CAS002 - Hedge Strategy/Linkage                             */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&LDA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PRTCD)  TYPE(*CHAR) LEN(7)                                   /*CAS006*/
             DCL        VAR(&POPTN)  TYPE(*CHAR) LEN(7)                                   /*CAS006*/
             DCL        VAR(&PSARD)  TYPE(*CHAR) LEN(6)                                   /*CAS006*/
             DCL        VAR(&SCSARD) TYPE(*CHAR) LEN(200)                                 /*CAS006*/
             DCL        VAR(&CAS006) TYPE(*CHAR) LEN(1) VALUE('N')                        /*CAS006*/
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('ASC000004 - Hedge Effectiveness Assessment +
                          FH Transaction Control') TOMSGQ(MRUNQ)
 
             CHGJOB     SWS(00000000)
 
/** Setup system prefix variable from SDSTAT data area */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
                                                                                          /*CAS006*/
/* Check if CAS006 is enabled. */                                                         /*CAS006*/
                                                                                          /*CAS006*/
             CHGVAR     VAR(&PRTCD) VALUE('       ')                                      /*CAS006*/
             CHGVAR     VAR(&POPTN) VALUE('*VERIFY')                                      /*CAS006*/
             CHGVAR     VAR(&PSARD) VALUE('CAS006')                                       /*CAS006*/
             CALL       PGM(AOSARDR0) PARM(&PRTCD &POPTN &PSARD +
                          &SCSARD)                                                        /*CAS006*/
             IF         COND(&PRTCD *EQ '       ') +
                  THEN(CHGVAR VAR(&CAS006) VALUE('Y'))                                    /*CAS006*/
 
/** Call program CB0160 to get value of restart indicator */
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
 
/** If it is NOT a restart */
 
             IF         COND(&STAT *NE 'Y') THEN(DO)
             CPYF       FROMFILE(ASHGLKPD) TOFILE(&DPLIB/XASHGLKPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  IF         COND(&CAS006 *EQ 'Y') THEN(DO)                               /*CAS006*/
                     CPYF    FROMFILE(ASFHTRPD) TOFILE(&DPLIB/XASFHTRPD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)                               /*CAS006*/
                     MONMSG  MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS006*/
                  ENDDO                                                                   /*CAS006*/
 
             ENDDO
 
/** If it is a restart */
 
             IF         COND(&STAT *EQ 'Y') THEN(DO)
             CPYF       FROMFILE(XASHGLKPD) TOFILE(ASHGLKPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(ASHGLKPD))
                  IF         COND(&CAS006 *EQ 'Y') THEN(DO)                               /*CAS006*/
                     CPYF    FROMFILE(XASFHTRPD) TOFILE(ASFHTRPD) +
                             MBROPT(*REPLACE)                                             /*CAS006*/
                     MONMSG  MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS006*/
                  ENDDO                                                                   /*CAS006*/
             ENDDO
 
             CHGVAR     VAR(&STAT) VALUE('Y')
 
/** Call CB0150 to update restart indicator */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/** Call Hedge Effectiveness Assessment FH Transactions */
 
             CALL       PGM(AS000004)
 
/** If no error occured in the program */
 
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
 
/** Call CB0150 to update restart indicator */
 
             CHGVAR     VAR(&STAT) VALUE('N')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
             DLTF       FILE(XASHGLKPD)
             MONMSG     MSGID(CPF2105)
                  IF         COND(&CAS006 *EQ 'Y') THEN(DO)                               /*CAS006*/
                     DLTF    FILE(XASFHTRPD)                                              /*CAS006*/
                     MONMSG  MSGID(CPF2105)                                               /*CAS006*/
                  ENDDO                                                                   /*CAS006*/
             ENDDO
 
/** Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                ROLLBACK
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
             ENDDO
 
             GOTO       CMDLBL(END)
 
/** Abnormal termination - batch job */
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          ASC000004 ended abnormally - see job log')   +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
 
             ENDPGM
