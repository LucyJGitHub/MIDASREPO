/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas AS Forecast Transactions Update')               */
/*********************************************************************/
/*                                                                   */
/*       Midas    - Accounting Standards Module                      */
/*                                                                   */
/*       ASC000013 - Forecast Transactions Update                    */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CAS006  *CREATE    Date 21Jan03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CAS006 - Hedge Accounting Phase B                           */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(7) VALUE('  DMLIB')
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('ASC000013 - Forecast Transactions Update') +
                          TOMSGQ(MRUNQ)
 
             CHGJOB     SWS(XXXXXX00)
 
/** Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DMLIB 1 2)) VALUE(&SYSID)
 
/** Call program CB0160 to get value of restart indicator */
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
 
/** If it is not a restart */
 
             IF         COND(&STAT *NE 'Y') THEN(DO)
                  DLTF       FILE(XASFHTRPD)
                  MONMSG     MSGID(CPF2105)
 
/** Create a backup for ASFHTRPD */
 
                  CPYF       FROMFILE(ASFHTRPD) TOFILE(&DPLIB/XASFHTRPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
             ENDDO
 
/** If it is a restart */
 
             IF         COND(&STAT *EQ 'Y') THEN(DO)
                  CPYF       FROMFILE(XASFHTRPD) TOFILE(&DMLIB/ASFHTRPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
             ENDDO
 
             CHGVAR     VAR(&STAT) VALUE('Y')
 
/** Call CB0150 to update restart indicator */
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/** Call Midas AS Forecast Transactions Update */
 
             CALL       PGM(AS000013)
 
/** If no error occured in the program */
 
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
 
/* Call CB0150 to update restart indicator */
 
                  CHGVAR     VAR(&STAT) VALUE('N')
                  CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
                  DLTF       FILE(XASFHTRPD)
                  MONMSG     MSGID(CPF2105)
             ENDDO
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                  RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                  SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOMSGQ(MOPERQ MRUNQ)
                  CPYF       FROMFILE(&DPLIB/XASFHTRPD) TOFILE(ASFHTRPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                        ASC000013 ended abnormally - see job log') +
                        TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
 
             ENDPGM
