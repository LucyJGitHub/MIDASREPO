/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas AS Historic Market Data Collection program')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - Accounting Standard Module                          */
/*                                                                   */
/*       ASC000019 - Midas AS Historic Market Data Collection        */
/*                                                                   */
/*       (c) Finastra International Limited 2003                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CLE134             Date 01Aug12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CAS008             Date 11May04              */
/*                      CAS006  *CREATE    Date 21Jan03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CLE134 - Past Due Call Loan Processing (Recompile)          */
/*       CAS008 - Delayed Hedge Assessment                           */
/*       CAS006 - Hedge Accounting Phase B                           */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PCOMPNM &PSEQ)
 
             DCL        VAR(&PCOMPNM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&PSTAT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(7) VALUE('  DMLIB')
             DCL        VAR(&PRTCD)  TYPE(*CHAR) LEN(7)                                   /*CAS008*/
             DCL        VAR(&POPTN)  TYPE(*CHAR) LEN(7)                                   /*CAS008*/
             DCL        VAR(&PSARD)  TYPE(*CHAR) LEN(6)                                   /*CAS008*/
             DCL        VAR(&SCSARD) TYPE(*CHAR) LEN(200)                                 /*CAS008*/
             DCL        VAR(&CAS008) TYPE(*CHAR) LEN(1) VALUE('N')                        /*CAS008*/
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2003')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('ASC000019 - AS Historic Market Data +
                          Collection Control') TOMSGQ(MRUNQ)
 
             CHGJOB     SWS(XXXXXX00)
 
/** Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DMLIB 1 2)) VALUE(&SYSID)
                                                                                          /*CAS008*/
/* Check if CAS008 is enabled. */                                                         /*CAS008*/
                                                                                          /*CAS008*/
             CHGVAR     VAR(&PRTCD) VALUE('       ')                                      /*CAS008*/
             CHGVAR     VAR(&POPTN) VALUE('*VERIFY')                                      /*CAS008*/
             CHGVAR     VAR(&PSARD) VALUE('CAS008')                                       /*CAS008*/
             CALL       PGM(AOSARDR0) PARM(&PRTCD &POPTN &PSARD +
                          &SCSARD)                                                        /*CAS008*/
             IF         COND(&PRTCD *EQ '       ') +
                  THEN(CHGVAR VAR(&CAS008) VALUE('Y'))                                    /*CAS008*/
 
/* Call program CB0160 to get value of restart indicator */
 
             CALL       PGM(CB0160) PARM(&PCOMPNM &PSEQ &PSTAT)
 
/** If this is not a restart */
 
             IF         COND(&PSTAT *NE 'Y') THEN(DO)
 
                  DLTF       FILE(&DPLIB/XASDLHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(&DPLIB/XASLEHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(&DPLIB/XASSEHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(&DPLIB/XASHGLKPD)
                  MONMSG     MSGID(CPF2105)
                                                                                          /*CAS008*/
             IF         COND(&CAS008 *EQ 'Y') THEN(DO)                                    /*CAS008*/
                  DLTF       FILE(&DPLIB/XIRSNHVPD)                                       /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  DLTF       FILE(&DPLIB/XRXSNHVPD)                                       /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  DLTF       FILE(&DPLIB/XFRANHVPD)                                       /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  DLTF       FILE(&DPLIB/XDLNHVLPD)                                       /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  CPYF       FROMFILE(IRSNHVPD) TOFILE(&DPLIB/XIRSNHVPD) +
                             CRTFILE(*YES)                                                /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(RXSNHVPD) TOFILE(&DPLIB/XRXSNHVPD) +
                             CRTFILE(*YES)                                                /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(FRANHVPD) TOFILE(&DPLIB/XFRANHVPD) +
                             CRTFILE(*YES)                                                /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(DLNHVLPD) TOFILE(&DPLIB/XDLNHVLPD) +
                             CRTFILE(*YES)                                                /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  ENDDO                                                                   /*CAS008*/
 
/** Create a back-up for ASDLHMPD, ASLEHMPD, ASSEHMPD and ASHGLKPD */
 
                  CPYF       FROMFILE(ASDLHMPD) TOFILE(&DPLIB/XASDLHMPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(ASLEHMPD) TOFILE(&DPLIB/XASLEHMPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(ASSEHMPD) TOFILE(&DPLIB/XASSEHMPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(ASHGLKPD) TOFILE(&DPLIB/XASHGLKPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
             ENDDO
 
/** If it is a restart */
 
             IF         COND(&PSTAT *EQ 'Y') THEN(DO)
                  CPYF       FROMFILE(XASDLHMPD) TOFILE(&DMLIB/ASDLHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(XASLEHMPD) TOFILE(&DMLIB/ASLEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(XASSEHMPD) TOFILE(&DMLIB/ASSEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(XASHGLKPD) TOFILE(&DMLIB/ASHGLKPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                                                                                          /*CAS008*/
             IF         COND(&CAS008 *EQ 'Y') THEN(DO)                                    /*CAS008*/
                  CPYF       FROMFILE(XIRSNHVPD) TOFILE(&DMLIB/IRSNHVPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(XRXSNHVPD) TOFILE(&DMLIB/RXSNHVPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(XFRANHVPD) TOFILE(&DMLIB/FRANHVPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(XDLNHVLPD) TOFILE(&DMLIB/DLNHVLPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
             ENDDO                                                                        /*CAS008*/
             ENDDO
 
             CHGVAR     VAR(&PSTAT) VALUE('Y')
 
/** Call CB0150 to update restart indicator */
 
             CALL       PGM(CB0150) PARM(&PCOMPNM &PSEQ &PSTAT)
 
 
/* Call Historic Market Data Collection Program */
             IF         COND(&CAS008 *EQ 'Y') THEN(DO)                                    /*CAS008*/
                        CALL       PGM(AS000025)                                          /*CAS008*/
                        CALL       PGM(AS000026)                                          /*CAS008*/
             ENDDO                                                                        /*CAS008*/
 
             CALL       PGM(AS000019)
 
/** If no error occured in the program */
 
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
 
/** Call CB0150 to update restart indicator */
 
                  CHGVAR     VAR(&PSTAT) VALUE('N')
                  CALL       PGM(CB0150) PARM(&PCOMPNM &PSEQ &PSTAT)
                  DLTF       FILE(XASDLHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(XASLEHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(XASSEHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(XASHGLKPD)
                  MONMSG     MSGID(CPF2105)
                                                                                          /*CAS008*/
                  IF         COND(&CAS008 *EQ 'Y') THEN(DO)                               /*CAS008*/
                  DLTF       FILE(XIRSNHVPD)                                              /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  DLTF       FILE(XRSXNHVPD)                                              /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  DLTF       FILE(XFRANHVPD)                                              /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  DLTF       FILE(XDLNHVLPD)                                              /*CAS008*/
                  MONMSG     MSGID(CPF2105)                                               /*CAS008*/
                  ENDDO                                                                   /*CAS008*/
             ENDDO
 
/** Database error processing */
 
             IF            COND(%SWITCH(XXXXXX11)) THEN(DO)
                  RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                  SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOMSGQ(MOPERQ MRUNQ)
                  CPYF       FROMFILE(XASDLHMPD) TOFILE(&DMLIB/ASDLHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(XASLEHMPD) TOFILE(&DMLIB/ASLEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(XASSEHMPD) TOFILE(&DMLIB/ASSEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(XASHGLKPD) TOFILE(&DMLIB/ASHGLKPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                                                                                          /*CAS008*/
                  IF         COND(&CAS008 *EQ 'Y') THEN(DO)                               /*CAS008*/
                  CPYF       FROMFILE(XIRSNHVPD) TOFILE(&DMLIB/IRSNHVPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(XRXSNHVPD) TOFILE(&DMLIB/RXSNHVPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(XFRANHVPD) TOFILE(&DMLIB/FRANHVPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  CPYF       FROMFILE(XDLNHVLPD) TOFILE(&DMLIB/DLNHVLPD) +
                             MBROPT(*REPLACE)                                             /*CAS008*/
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)                               /*CAS008*/
                  ENDDO                                                                   /*CAS008*/
             ENDDO
 
             GOTO       CMDLBL(END)
 
/** Abnormal termination */
 
ABNOR:       CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                        ASC000019 ended abnormally - see job log') +
                        TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:         ENDPGM
