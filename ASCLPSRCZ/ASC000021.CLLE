/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI    TEXT('Midas AS Historic Market Data Housekeeping Control')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Accounting Standard Module                          */
/*                                                                   */
/*       ASC000021 - Midas AS Historic Market Data Housekeeping      */
/*                   Control                                         */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2003           */
/*                                                                   */
/*       Last Amend No. CLE134             Date 01Aug12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. 228542             Date 05Aug04              */
/*                      CAS006  *CREATE    Date 21Jan03              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CLE134 - Past Due Call Loan Processing (Recompile)          */
/*       228542 - RGZPFM has changed at R5V3                         */
/*       CAS006 - Hedge Accounting Phase B                           */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PCOMPNM &PSEQ)
 
             DCL        VAR(&PCOMPNM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&PSTAT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2003')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('ASC000021 - AS Historic Market Data +
                          Housekeeping Control') +
                          TOMSGQ(MRUNQ) MSGTYPE(*INFO)
 
             CHGJOB     SWS(XXXXXX00)
 
/* Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
 
/* Call program CB0160 to get value of restart indicator */
 
             CALL       PGM(CB0160) PARM(&PCOMPNM &PSEQ &PSTAT)
 
/* If restart, restore ASDLHMPD, ASLEHMPD and ASSEHMPD from */
/* XASDLHMPD, XASLEHMPD and XASSEHMPD respectively. */
 
             IF         COND(&PSTAT *EQ 'Y') THEN(DO)
                  CPYF       FROMFILE(&DPLIB/XASDLHMPD) TOFILE(ASDLHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(&DPLIB/XASLEHMPD) TOFILE(ASLEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(&DPLIB/XASSEHMPD) TOFILE(ASSEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
             ENDDO
 
/* If this is not a restart */
 
             IF         COND(&PSTAT *NE 'Y') THEN(DO)
 
                  DLTF       FILE(&DPLIB/XASDLHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(&DPLIB/XASLEHMPD)
                  MONMSG     MSGID(CPF2105)
                  DLTF       FILE(&DPLIB/XASSEHMPD)
                  MONMSG     MSGID(CPF2105)
 
/* Back-up ASDLHMPD, ASLEHMPD and ASSEHMPD to XASDLHMPD, */
/* XASLEHMPD and XASSEHMPD respectively. */
 
                  CPYF       FROMFILE(ASDLHMPD) TOFILE(&DPLIB/XASDLHMPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(ASLEHMPD) TOFILE(&DPLIB/XASLEHMPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(ASSEHMPD) TOFILE(&DPLIB/XASSEHMPD) +
                             CRTFILE(*YES)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
 
/* Call CB0150 with Status Parameter set to 'Y' */
 
                  CALL       PGM(CB0150) PARM(&PCOMPNM &PSEQ 'Y')
 
             ENDDO
 
/* Call AS000021 */
 
             CALL       PGM(AS000021)
 
/* If error occured in the program, restore data files then */
/* delete back-up files. Report error message as necessary. */
 
             IF            COND(%SWITCH(XXXXXX11)) THEN(DO)
                  CPYF       FROMFILE(&DPLIB/XASDLHMPD) TOFILE(ASDLHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(&DPLIB/XASLEHMPD) TOFILE(ASLEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
                  CPYF       FROMFILE(&DPLIB/XASSEHMPD) TOFILE(ASSEHMPD) +
                             MBROPT(*REPLACE)
                  MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
 
                  RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                  SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
                  GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* If no error occured in the program, reorganize historic */
/* data files and delete back-up data files. */
 
             CALL       PGM(CB0150) PARM(&PCOMPNM &PSEQ 'N')
 
/**********  RGZPFM     FILE(ASDLHMPD)                                                    /*228542*/
/**********  MONMSG     MSGID(CPF2995 CPF2981)                                            /*228542*/
             CALL       PGM(SCC000060) PARM('ASDLHMPD' '*FIRST')                          /*228542*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                  /*228542*/
                                                                                          /*228542*/
/**********  RGZPFM     FILE(ASLEHMPD)                                                    /*228542*/
/**********  MONMSG     MSGID(CPF2995 CPF2981)                                            /*228542*/
             CALL       PGM(SCC000060) PARM('ASLEHMPD' '*FIRST')                          /*228542*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                  /*228542*/
                                                                                          /*228542*/
/**********  RGZPFM     FILE(ASSEHMPD)                                                    /*228542*/
/**********  MONMSG     MSGID(CPF2995 CPF2981)                                            /*228542*/
             CALL       PGM(SCC000060) PARM('ASSEHMPD' '*FIRST')                          /*228542*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))                                                  /*228542*/
 
             DLTF       FILE(XASDLHMPD)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(XASLEHMPD)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(XASSEHMPD)
             MONMSG     MSGID(CPF2105)
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
ABNOR:       CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                        ASC000021 ended abnormally - see job log') +
                        TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:         ENDPGM
