/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas Average Spot Rates Take On Processing')         */
/*********************************************************************/
/*                                                                   */
/*       Midas - IAS21 Module                                        */
/*                                                                   */
/*       ASC002401 - Midas Average Spot Rates Take On Processing     */
/*                                                                   */
/*       (c) Finastra International Limited 2005                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CAS014  *CREATE    Date 10Aug05              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CAS014 - IAS21 The Effects of Changes in Foreign Exchange   */
/*                Rates                                              */
/*                                                                   */
/*********************************************************************/
             PGM
 
/* Declaration of variables */
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2005')
 
             DCL        VAR(&STAT)    TYPE(*CHAR) LEN(1)   VALUE(' ')
             DCL        VAR(&TOLIB)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRE)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&SDSTAT)  TYPE(*CHAR) LEN(256)
             DCL        VAR(&PRTCD)   TYPE(*CHAR) LEN(7)   VALUE(' ')
             DCL        VAR(&PERMS)   TYPE(*CHAR) LEN(50)  VALUE(' ')
             DCL        VAR(&PFULLCK) TYPE(*CHAR) LEN(7)   VALUE('Y')
             DCL        VAR(&PZONE)   TYPE(*CHAR) LEN(10)  VALUE(' ')
             DCL        VAR(&PSHTC)   TYPE(*CHAR) LEN(4)   VALUE(' ')
             DCL        VAR(&PRDNB)   TYPE(*DEC)  LEN(5 0) VALUE(0)
             DCL        VAR(&PDNWD)   TYPE(*DEC)  LEN(5 0) VALUE(0)
             DCL        VAR(&PBCCY)   TYPE(*CHAR) LEN(3)   VALUE(' ')
             DCL        VAR(&PNJOB)   TYPE(*DEC)  LEN(1 0) VALUE(0)
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Create data area LDA */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
/* Getting the DP Library for back-up*/
 
             RTVDTAARA  DTAARA(SDSTAT) RTNVAR(&SDSTAT)
             CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
             CHGVAR     VAR(&TOLIB) VALUE(&PRE *TCAT 'DPLIB')
 
/* Delete residual backup files. */
 
             DLTF       FILE(XGPAVSRPD)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(XGPAVSRPA)
             MONMSG     MSGID(CPF2105)
 
/* Create today's backup. */
 
             CPYF       FROMFILE(GPAVSRPD) TOFILE(&TOLIB/XGPAVSRPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
 
             CPYF       FROMFILE(GPAVSRPA) TOFILE(&TOLIB/XGPAVSRPA) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)
 
 
/* Midas Global Average Spot Rate Teke On Processing */
 
             CALL       PGM(AS002401)
 
/* Midas Standard Error Processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                             CMDLBL(ABNOR))
 
 
/* Retrieve Current Zone */
 
             CALL       PGM(GOGETZONE) PARM(&PRTCD &PERMS &PFULLCK +
                          &PZONE &PSHTC &PRDNB &PDNWD &PBCCY &PNJOB)
 
/* Midas Standard Error Processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                             CMDLBL(ABNOR))
 
/* Error Processing */
 
             IF         COND(&PERMS *NE ' ') THEN(GOTO +
                             CMDLBL(ABNOR))
 
/* Copy data for for dropping of records */
 
             CPYF       FROMFILE(GPAVSRPD) TOFILE(&TOLIB/WGPAVSRPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES) +
                          INCREL((*IF ASZONE *NE &PZONE)) FMTOPT(*NONE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(WGPAVSRPD))
 
/* Copy back data to original file */
 
             CPYF       FROMFILE(&TOLIB/WGPAVSRPD) TOFILE(GPAVSRPD) +
                          MBROPT(*REPLACE) FMTOPT(*NONE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) EXEC(CLRPFM +
                          FILE(GPAVSRPD))
 
/* Delete file */
 
             DLTF       FILE(&TOLIB/WGPAVSRPD)
 
/* Clears zone period file before any creattion of periodic records */
 
             CLRPFM     GPEZPDPD
/* Midas Zone Average Spot Rate Periodic Dates Extract */
 
             CALL       PGM(AS002402)
 
/* Midas Standard Error Processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                             CMDLBL(ABNOR))
 
/* Midas Zone Average Spot Rate Take On Processing */
 
             CALL       PGM(AS002403)
 
/* Midas Standard Error Processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNOR))
 
/* Delete backup file. */
 
             DLTF       FILE(XGPAVSRPD)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(XGPAVSRPA)
             MONMSG     MSGID(CPF2105)
 
/* Normal program termination */
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          ASC002401 ended abnormally - see joblog +
                          ') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 
/* End program */
 
 END:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited +
                          2005.')
 
             ENDPGM
