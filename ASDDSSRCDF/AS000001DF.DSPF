      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AS Hedge linkages mnt/enq/sel screen')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  AS000001DF - Hedge Linkages Mnt/Enq/Sel Screen               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS008             Date 11May04               *
      *  Prev Amend No. CAS006             Date 21Jan03               *
      *                 212430             Date 20Nov02               *
      *                 212426             Date 17Oct02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS008 - Delayed Hedge Assessment                            *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  212430 - Various errors on deleted transaction and deleted   *
      *           linkages                                            *
      *  212426 - Two F3s are shown on screen of Hedge Linkage Enquiry*
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
 
      *-------------------------------------------------------------------*
      ** Screen 1 - Subfile screen 1                                      *
      *-------------------------------------------------------------------*
 
     A          R AS000001S1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A  93                                  SFLNXTCHG
 
     A            #1SEL          1A  B  8  5TEXT('SELECT OPTION')
     A N31N73                               DSPATR(UL HI)
     A  31                                  DSPATR(RI PC)
     A  73                                  DSPATR(ND PR)
 
     A            #1HEDI         6A  O  8  9TEXT('Hedge ID')
     A                                      DSPATR(HI)
 
     A            #1HGST        10A  O  8 26TEXT('Hedged Strategy Ref.')
     A                                      DSPATR(HI)
 
     A            #1EFFD         7A  O  8 48DSPATR(HI)
     A                                      TEXT('Effective Date')
 
     A            #1STS         12A  O  8 66DSPATR(HI)
     A  68                                  DSPATR(RI)
 
     A            #1LCD          5S 0H
 
      *-------------------------------------------------------------------*
      **  Screen 1 - Screen Control for Subfile Screen 1                  *
      *-------------------------------------------------------------------*
 
     A          R AS000001C1                TEXT('Subfile Control.')
     A                                      CF03
     A  82                                  CF05
     A  83                                  CF09
     A                                      SFLCTL(AS000001S1)
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  95                                  SFLEND
     A  91                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N95                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
 
     A            #1SFRN         4S 0H      SFLRCDNBR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'AS000001S1'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
 
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 30'Hedge Linkage'
     A                                      DSPATR(HI)
     A  28N29N30                        2 44'Maintenance'
     A                                      DSPATR(HI)
     A N28 29N30                        2 44'Enquiry'
     A                                      DSPATR(HI)
     A N28N29 30                        2 44'Selection'
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  4  2'Action'
     A                                  4  9'Hedge ID'
     A                                  4 26'Hedge Strategy'
     A                                  4 48'Effective Date'
     A                                  4 66'Status'
 
     A            #1HEDR         6A  B  6  9TEXT('Hedge ID')
     A N73                                  DSPATR(HI UL)
     A  73                                  DSPATR(ND PR)
 
      *-------------------------------------------------------------------*
      ** Screen 2 - New Hedge Lingkage Screen                             *
      *-------------------------------------------------------------------*
 
     A          R AS000001F1                TEXT('New Hedge Linkage Screen')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY PUTOVR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'AS000001F1'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
 
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 35'New Hedge Linkage'
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  5  2'Hedge ID:'
     A            #1HEDG         6A  B  5 20TEXT('Hedge ID')
     A N32                                  DSPATR(HI UL)
     A  32                                  DSPATR(RI PC)
     A                                      OVRATR
 
      *-------------------------------------------------------------------*
      ** Screen 3 - Hedge Lingkage Main Detail Screen                     *
      *-------------------------------------------------------------------*
 
     A          R AS000001F2                TEXT('Detail screen')
     A                                      CF03
     A                                      CF12
     A  62                                  CF10
     A                                      OVERLAY PUTOVR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'AS000001F2'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
 
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 30'Hedge Linkage Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  4  2'Hedge ID.............:'
     A            #2HEDG         6A  O  4 25TEXT('Hedge ID')
     A                                      DSPATR(HI)
 
     A                                  6  2'Hedge Strategy............:'
     A            #2HGST        10A  B  6 30TEXT('Hedge Strategy')
     A N29N33
     AAN61N62                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A  29
     AO 61
     AO 62                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
 
     A                                  8  2'Effective Date............:'
     A            #2EFFD         6A  B  8 30TEXT('Effective Date')
     A N29N34
     AAN62                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A  29
     AO 62                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
 
     A                                  8 42'Input Date...............:'
     A            #2INDT         6A  B  8 69TEXT('Input Date')
     A N29N35
     AAN61N62                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A  29
     AO 61
     AO 62                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **Start of change CAS008
     A  84                              9  2'Delayed Assessment Date...:'
     A  84        #2DASD         6A  B  9 30TEXT('Delayed Assessment Date')
     A                                      DSPATR(PR HI)
      **End of change CAS008
 
     A                                 10  2'Functional Currency Offset:'
      **Start of change CAS006
      *
      ** Func CCY should always "not show", reserve for future use
      *
     A                                      DSPATR(ND)
      **End of change CAS006
     A            #2FCOA         1A  B 10 30TEXT('Functional Currency Offset')
      **Start of change CAS006
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
      **End of change CAS006
     A N29N36
     A N62                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
      **Start of change CAS006
     A**29******                                                                              CAS006
     A*O*62*****                            DSPATR(PR)                                        CAS006
      **End of change CAS006
     A                                      OVRATR OVRDTA
 
     A                                 12  2'Investment Category.......:'
      **Start of change CAS006
      *
      ** Inv Category should always "not show", reserve for future use
      *
     A                                      DSPATR(ND)
      **End of change CAS006
     A            #2ICAT         1A  B 12 30TEXT('Investment Category')
      **Start of change CAS006
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
      **End of change CAS006
     A N29N37
     AAN62                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
      **Start of change CAS006
     A**29******                                                                              CAS006
     A*O*62*****                            DSPATR(PR)                                        CAS006
      **End of change CAS006
     A                                      OVRATR OVRDTA
 
     A                                 14  2'Start Date................:'
     A            #2STDT         6A  B 14 30TEXT('Start Date')
     A N29N38
     AAN62                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A  29
     AO 62                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
 
     A                                 14 42'End Date.................:'
     A            #2ENDT         6A  B 14 69TEXT('End Date')
     A N29N39
     AAN62                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A  29
     AO 62                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **Start of change CAS006
      *
      ** Data type should always "not show", reserve for future use
      *
     A  53                             15 52'Data Type...........:'
     A                                      DSPATR(ND)
     A            #2DTTP         1A  B 15 74TEXT('Data Type')
     A                                      DSPATR(ND)
     A N29N74
     AAN62 53N54                            DSPATR(UL)
     A N74                                  DSPATR(HI)
     A  74                                  DSPATR(RI PC)
     A                                      DSPATR(PR)
     A                                      OVRATR OVRDTA
      **End of change CAS006
 
     A  69                             16  2'Hedge Derivative Deals.......+
     A                                       ...............:'
     A            #2DVDL         1A  B 16 49TEXT('Hedge Derivative Deals')
     A N69                                  DSPATR(ND)
     A N29N40
     AAN61N62 69                            DSPATR(UL)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A  29
     AO 61
     AO 62
     AON69                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **Start of change CAS006
     A  53                             16 52'Data Points Required:'
     A            #2DPTS         4A  B 16 74TEXT('Data Points +
     A                                      Required')
     A N53                                  DSPATR(ND)
     A N29N75
     AAN62 53N54                            DSPATR(UL)
     A N75                                  DSPATR(HI)
     A  75                                  DSPATR(RI PC)
     A  29
     AO 54
     AO 62
     AON53                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **End of change CAS006
 
     A  70                             17  2'Hedge Futures Trades/Position+
     A                                       ...............:'
     A            #2FTPO         1A  B 17 49TEXT('Hedge Futures Trades/+
     A                                            Position')
     A N70                                  DSPATR(ND)
     A N29N41
     AAN61N62 70                            DSPATR(UL)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A  29
     AO 61
     AO 62
     AON70                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **Start of change CAS006
     A  53                             17 52'Data Collection Freq:'
     A            #2DFRQ         1A  B 17 74TEXT('Data Collection +
     A                                      Frequency')
     A N53                                  DSPATR(ND)
     A N29N76
     AAN62 53N54                            DSPATR(UL)
     A N76                                  DSPATR(HI)
     A  76                                  DSPATR(RI PC)
     A  29
     AO 54
     AO 62
     AON53                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
 
     A  53                             18 52'Day of the Week.....:'
     A            #2WDAY         3A  B 18 74TEXT('Day of the Week')
     A N53                                  DSPATR(ND)
     A N29N77
     AAN62 53N54                            DSPATR(UL)
     A N77                                  DSPATR(HI)
     A  77                                  DSPATR(RI PC)
     A  29
     AO 54
     AO 62
     AON53                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **End of change CAS006
 
     A  71                             19  2'Hedged Deals/Loans/Forecast T+
     A                                       ransactions....:'
     A            #2HGDL         1A  B 19 49TEXT('Hedged Deals/Loans/For+
     A                                            ecast Transactions')
     A N71                                  DSPATR(ND)
     A N29N42
     AAN61N62 71                            DSPATR(UL)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A  29
     AO 61
     AO 62
     AON71                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **Start of change CAS006
     A  53                             19 52'Day of the Month....:'
     A            #2MNTH         3A  B 19 74TEXT('Day of the Month')
     A N53                                  DSPATR(ND)
     A N29N78
     AAN62 53N54                            DSPATR(UL)
     A N78                                  DSPATR(HI)
     A  78                                  DSPATR(RI PC)
     A  29
     AO 54
     AO 62
     AON53                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **End of change CAS006
 
     A  72                             20  2'Hedged Security Reference....+
     A                                       ...............:'
     A            #2SECR         1A  B 20 49TEXT('Hedged Security Reference')
     A N72                                  DSPATR(ND)
     A N29N43
     AAN61N62 72                            DSPATR(UL)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A  29
     AO 61
     AO 62
     AON72                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **Start of change CAS006
     A  53                             20 52'Next Data Collection:'
     A            #2NDTA         6A  B 20 74TEXT('Next Data +
     A                                      Collection Date')
     A N53                                  DSPATR(ND)
     A N29N79
     AAN62 53N54                            DSPATR(UL)
     A N79                                  DSPATR(HI)
     A  79                                  DSPATR(RI PC)
     A  29
     AO 54
     AO 62
     AON53                                  DSPATR(PR)
     A                                      OVRATR OVRDTA
      **End of change CAS006
 
      *-------------------------------------------------------------------*
      ** Screen 4 - Subfile screen 2                                      *
      *-------------------------------------------------------------------*
 
     A          R AS000001S2                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A  93                                  SFLNXTCHG
 
     A            #2ACT          1A  B  8  2
      **Start of change 212430
     A N55
      **End of change 212430
     A N44 61                               DSPATR(UL HI)
     A  44 61                               DSPATR(RI PC)
      **Start of change 212430
     A*N61******                            DSPATR(ND PR)                                     212430
     A  55N44
     AON61                                  DSPATR(PR)
     A N61                                  DSPATR(ND)
      **End of change 212430
 
      **Start of change 212430
     A            #2LDEL         1A  O  8  5
      **End of change 212430
     A            #2DMOD         2A  B  8  7
      **Start of change 212430
     A N56
      **End of change 212430
     A N45N29N62                            DSPATR(UL)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A  29
      **Start of change 212430
     AO 56
      **End of change 212430
     AO 62                                  DSPATR(PR)
      **Start of change 212430
     A            #2DDEL         1A  O  8 13
     A                                      DSPATR(ND)
      **End of change 212430
 
     A            #2DDNO         6A  B  8 15
      **Start of change 212430
     A N56
      **End of change 212430
     A N46N29N62                            DSPATR(UL)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A  61 64N65                            DSPATR(ND)
     A  61 64N65
     AO 29
      **Start of change 212430
     AO 56
      **End of change 212430
     AO 62                                  DSPATR(PR)
 
     A            #2DFPO        10A  B  8 26
      **Start of change 212430
     A*N29N62***                                                                              212430
     A N29N62N56
      **End of change 212430
     AAN47 64N65                            DSPATR(UL)
     A N47 64N65                            DSPATR(HI)
     A  47 64N65                            DSPATR(RI PC)
     A  63
     AO 64 65                               DSPATR(ND)
     A  29
     AO 62
     AO 63
      **Start of change 212430
     AO 56
      **End of change 212430
     AO 64 65                               DSPATR(PR)
 
     A            #2DBKC         2A  B  8 41
     A N29N62
     AAN48 64N65                            DSPATR(UL)
     A N48 64N65                            DSPATR(HI)
     A  48 64N65                            DSPATR(RI PC)
     A  63
     AO 64 65                               DSPATR(ND)
     A  29
     AO 62
     AO 63
     AO 64 65                               DSPATR(PR)
 
     A            #2BRCA         3A  B  8 48
     A N29N62
     AAN49 64N65                            DSPATR(UL)
     A N49 64N65                            DSPATR(HI)
     A  49 64N65                            DSPATR(RI PC)
     A  63
     AO 64 65                               DSPATR(ND)
     A  29
     AO 62
     AO 63
     AO 64 65                               DSPATR(PR)
 
     A            #2PCAL         1A  B  8 56
     A N29N62
     AAN50 64N65                            DSPATR(UL)
     A N50 64N65                            DSPATR(HI)
     A  50 64N65                            DSPATR(RI PC)
     A  63
     AO 64 65                               DSPATR(ND)
     A  29
     AO 62
     AO 63
     AO 64 65                               DSPATR(PR)
 
     A            #2DPCT         7A  B  8 62
      **Start of change 212430
     A N57
      **End of change 212430
     A N51N29N62                            DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  29
      **Start of change 212430
     AO 57
      **End of change 212430
     AO 62                                  DSPATR(PR)
 
     A            #2ACTP         1A  H
      **Start of change 212430
     A            #2LDELP        1A  H
      **End of change 212430
     A            #2DMODP        2A  H
      **Start of change 212430
     A            #2DDELP        1A  H
      **End of change 212430
     A            #2DDNOP        6A  H
     A            #2DFPOP       10A  H
     A            #2DBKCP        2A  H
     A            #2BRCAP        3A  H
     A            #2PCALP        1A  H
     A            #2DPCTP        7A  H
 
      *-------------------------------------------------------------------*
      **  Screen 4 - Screen Control for Subfile Screen 2                  *
      *-------------------------------------------------------------------*
 
     A          R AS000001C2                TEXT('Subfile Control 2.')
     A                                      CF03
     A                                      CF12
     A  62                                  CF10
     A                                      SFLCTL(AS000001S2)
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  95                                  SFLEND
     A  91                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N95                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
 
     A            #2SFRN         4S 0H      SFLRCDNBR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'AS000001S2'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
 
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A  63N64                           2 30'   Hedge Derivative Deals    '
     A                                      DSPATR(HI)
     A N63 64                           2 30'Hedge Futures Trade/Positions'
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  3  2'Hedge ID: '
     A            #2HEDG         6A  O  3 13
     A                                      DSPATR(HI)
 
     A                                  5  6'Midas'
     A N61N62N29
     AO 63
     AO 64 65                           5 14'Deal/Trade'
     A N63N65
     AO 64N65                           5 26'Futures/'
     A N63N65
     AO 64N65                           5 40'Book'
     A N63N65
     AO 64N65                           5 47'Branch'
     A N63N65
     AO 64N65                           5 55'Put/'
 
     A  61                              6  1'Del'
     A                                  6  6'Module'
     A N61N62N29
     AO 63
     AO 64 65                           6 15'Number'
     A N63N65
     AO 64N65                           6 26'Options'
     A N63N65
     AO 64N65                           6 40'Code'
     A N63N65
     AO 64N65                           6 48'Code'
     A N63N65
     AO 64N65                           6 55'Call'
     A                                  6 61'Percentage'
 
      *-------------------------------------------------------------------*
      ** Screen 5 - Subfile screen 3                                      *
      *-------------------------------------------------------------------*
 
     A          R AS000001S3                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A  93                                  SFLNXTCHG
 
     A            #3HACT         1A  B  8  2
      **Start of change 212430
     A N55
      **End of change 212430
     A N44 61                               DSPATR(UL HI)
     A  44 61                               DSPATR(RI PC)
      **Start of change 212430
     A*N61******                            DSPATR(ND PR)                                     212430
     A  55N44
     AON61                                  DSPATR(PR)
     AON61                                  DSPATR(ND)
      **End of change 212430
 
      **Start of change 212430
     A            #3LDEL         1A  O  8  6
      **End of change 212430
     A            #3HMOD         2A  B  8  8
      **Start of change 212430
     A N56
      **End of change 212430
     A N45N29N62                            DSPATR(UL)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A  29
      **Start of change 212430
     AO 56
      **End of change 212430
     AO 62                                  DSPATR(PR)
      **Start of change 212430
     A            #3HDEL         1A  O  8 13
     A                                      DSPATR(ND)
      **End of change 212430
 
     A            #3HDNO         6A  B  8 15
      **Start of change 212430
     A*N29N62***                                                                              212430
     A N29N62N56
      **End of change 212430
     AAN46 66                               DSPATR(UL)
     A N46 66                               DSPATR(HI)
     A  46 66                               DSPATR(RI PC)
     A N66                                  DSPATR(ND)
     A  29
     AO 62
      **Start of change 212430
     AO 56
      **End of change 212430
     AON66                                  DSPATR(PR)
 
     A            #3HSEC        10A  B  8 27
      **Start of change 212430
     A*N29N62***                                                                              212430
     A N29N62N56
      **End of change 212430
     AAN47 67                               DSPATR(UL)
     A N47 67                               DSPATR(HI)
     A  47 67                               DSPATR(RI PC)
     A N67                                  DSPATR(ND)
     A  29
     AO 62
      **Start of change 212430
     AO 56
      **End of change 212430
     AON67                                  DSPATR(PR)
 
     A            #3HBKC         2A  B  8 40
     A N29N62
     AAN48 67                               DSPATR(UL)
     A N48 67                               DSPATR(HI)
     A  48 67                               DSPATR(RI PC)
     A N67                                  DSPATR(ND)
     A  29
     AO 62
     AON67                                  DSPATR(PR)
 
     A            #3BRCA         3A  B  8 46
     A N29N62
     AAN49 67                               DSPATR(UL)
     A N49 67                               DSPATR(HI)
     A  49 67                               DSPATR(RI PC)
     A N67                                  DSPATR(ND)
     A  29
     AO 62
     AON67                                  DSPATR(PR)
 
     A            #3MKTI         1A  B  8 54
     A N29N62
     AAN52 67                               DSPATR(UL)
     A N52 67                               DSPATR(HI)
     A  52 67                               DSPATR(RI PC)
     A N67                                  DSPATR(ND)
     A  29
     AO 62
     AON67                                  DSPATR(PR)
 
     A            #3BHTV         1A  B  8 62
     A N29N62
     AAN50 67                               DSPATR(UL)
     A N50 67                               DSPATR(HI)
     A  50 67                               DSPATR(RI PC)
     A N67                                  DSPATR(ND)
     A  29
     AO 62
     AON67                                  DSPATR(PR)
 
     A            #3HPCT         7A  B  8 71
      **Start of change 212430
     A N57
      **End of change 212430
     A N51N29N62                            DSPATR(UL)
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  29
      **Start of change 212430
     AO 57
      **End of change 212430
     AO 62                                  DSPATR(PR)
 
     A            #3HACTP        1A  H
      **Start of change 212430
     A            #3LDELP        1A  H
      **End of change 212430
     A            #3HMODP        2A  H
      **Start of change 212430
     A            #3HDELP        1A  H
      **End of change 212430
     A            #3HDNOP        6A  H
     A            #3HSECP       10A  H
     A            #3HBKCP        2A  H
     A            #3BRCAP        3A  H
     A            #3MKTIP        1A  H
     A            #3BHTVP        1A  H
     A            #3HPCTP        7A  H
 
      *-------------------------------------------------------------------*
      **  Screen 5 - Screen Control for Subfile Screen 3                  *
      *-------------------------------------------------------------------*
 
     A          R AS000001C3                TEXT('Subfile Control 2.')
     A                                      CF03
     A                                      CF12
     A  62                                  CF10
     A                                      SFLCTL(AS000001S3)
     A  92                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  95                                  SFLEND
     A  91                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N95                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
 
     A            #3SFRN         4S 0H      SFLRCDNBR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'AS000001S3'
     A            #1RUNA    R        O  1 73REFFLD(RUNA)
 
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A  66N67                           2 30'Hedged Deals/Loans/Forecast'
     A                                      DSPATR(HI)
     A N66 67                           2 30'     Hedged Securities     '
     A                                      DSPATR(HI)
     A                                  2 72TIME
 
     A                                  3  2'Hedge ID: '
     A            #2HEDG         6A  O  3 13
     A                                      DSPATR(HI)
 
     A                                  5  6'Midas'
     A  66                              5 13'Deal/Loan/FH'
     A  67                              5 27'Security'
     A  67                              5 39'Book'
     A  67                              5 45'Branch'
     A  67                              5 53'Mkt'
     A  67                              5 58'Trade/Value'
 
     A  61                              6  1'Del'
     A                                  6  6'Module'
     A  66                              6 15'Number'
     A  67                              6 27'Short Name'
     A  67                              6 39'Code'
     A  67                              6 46'Code'
     A  67                              6 53'Ind'
     A  67                              6 58'Date Basis'
     A                                  6 70'Percentage'
 
      *-------------------------------------------------------------------*
      ** Select screen footer                                             *
      *-------------------------------------------------------------------*
 
     A          R AS000001F3                TEXT('Select screen footer')
 
      **Start of change 212430
     A  60                             22 27'''*'' - Deleted Hedge/d Item'
      **End of change 212430
     A  80                             23  2'F3=Exit F12=Previous'
     A N80 73                          23  2'F3=Exit'
     A  29                             23  2'F3=Exit'
     A  30N73                          23  2'1=Select request  F3=Exit'
     A N80 81                          23  2'A=Amend D=Delete'
     A N80N73N30                       23 19'E=Enquire'
      **Start of change 212426
     A N29
      **End of change 212426
     A N80N73N30                       23 29'F3=Exit'
     A N80 82                          23 37'F5=Refresh'
     A N80 83N73                       23 48'F9=Go to Add Mode'
     A N80 83 73                       23 10'F9=Go to Add Mode'
     A  80 62                          23 48'F10=Confirm Delete'
 
      *-------------------------------------------------------------------*
      ** MESSAGE SUBFILE FORMAT                                           *
      *-------------------------------------------------------------------*
 
     A          R AS000001S4                TEXT('Program message subfile')
     A                                      SFL SFLMSGRCD(24)
     A            #4SFKY                    SFLMSGKEY
     A            #4PGMQ                    SFLPGMQ
 
      *-------------------------------------------------------------------*
      ** MESSAGE SUBFILE CONTROL                                          *
      *-------------------------------------------------------------------*
 
     A          R AS000001C4                TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(AS000001S4)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP
     A N96                                  SFLDSPCTL
     A  97                                  SFLEND
     A            #4PGMQ                    SFLPGMQ
