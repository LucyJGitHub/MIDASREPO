      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AS Forecast transactions')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  AS000002DF - Midas AS Forecast Transaction Maintenance       *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
     A
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header
      *
     A          R AS000002F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A  52N54N55
     AO 53N54N55                        1 13'AS000002C0'
     A  54N52N53
     AO 55N52N53                        1 13'AS000002F1'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 25'Forecast Transaction Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Screen 1 Subfile Record Format
      *
     A          R AS000002S0                SFL
     A  31                                  SFLNXTCHG
      *
     A            #0SELE         1A  B  8  3TEXT('Action Code')
     A                                      DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL)
      *
     A            #0TRID         6Y 0O  8  5TEXT('Trans ID')
     A                                      DSPATR(HI)
     A            #0SUBT         2A  O  8 13TEXT('Trans Subtype')
     A                                      DSPATR(HI)
     A            #0CCY          3A  O  8 17TEXT('Trans Currency')
     A                                      DSPATR(HI)
     A            #0ERIN         1A  O  8 26TEXT('Expenditure/Receipt')
     A                                      DSPATR(HI)
     A            #0AMNT        14A  O  8 34TEXT('Trans Amount')
     A                                      DSPATR(HI)
     A            #0INDT         7A  O  8 52TEXT('Input Date')
     A                                      DSPATR(HI)
     A            #0MDAT         7A  O  8 60TEXT('Maturity Date')
     A                                      DSPATR(HI)
     A            #0STAT        12A  O  8 68TEXT('Status')
     A  38                                  DSPATR(RI)
     A                                      DSPATR(HI)
      *
      ** Subfile Control
      *
     A          R AS000002C0                SFLCTL(AS000002S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  34                                  SFLCLR
     A N34                                  SFLDSPCTL
     A N34 35                               SFLDSP
     A N34 35 36                            SFLEND(*MORE)
     A N36                                  ROLLUP(27)
     A N40                                  CF05
     A N26 29                               CF09
      *
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4  5'Trans.'
     A                                  5  6'ID'
     A            #0RTRI         6Y 0B  7  5
     A                                      EDTCDE(Z)
     A                                      DSPATR(UL HI)
     A                                  4 12'Sub-'
     A                                  5 12'Type'
     A                                  5 17'CCY'
     A                                  4 21'Expenditure/'
     A                                  5 23'Receipt'
     A                                  5 39'Amount'
     A                                  4 52'Input'
     A                                  5 52'Date'
     A                                  4 60'Maturity'
     A                                  5 61'Date'
     A                                  4 71'Status'
      *
      ** Details
      *
     A          R AS000002F1                PUTOVR
     A                                      OVERLAY
     A  55                                  CF10
     A                                      CF12
      *
     A                                  5  2'Transaction ID.........:'
     A            #1TRID         6Y 0B  5 30
     A                                      OVRDTA
     A N39 32                               DSPATR(UL HI)
     A N39N32                               DSPATR(PR)
     A  39                                  DSPATR(RI)
      *
     A N32                              6  2'Branch.................:'
     A            #1BRCA         3A  B  6 30
     A                                      OVRDTA
     A N26 41                               DSPATR(RI PC)
     A N26N41                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A N32 60                           7  2'Module ID..............:'
     A            #1MOID         2A  B  7 30
     A                                      OVRDTA
     A N26 61                               DSPATR(RI PC)
     A N26N61                               DSPATR(UL HI)
     A  26
     AO 32
     AON60                                  DSPATR(PR)
     A  32
     AON60                                  DSPATR(ND)
     A*N32******                        7  2'Description............:'                        CAS006
     A**********  #1DESC        30A  B  7 30                                                  CAS006
     A N32                              8  2'Description............:'
     A            #1DESC        30A  B  8 30
      **End of change CAS006
     A                                      OVRDTA
     A     42                               DSPATR(RI PC)
     A N26N42
     AO 26 28N42                            DSPATR(UL HI)
     A  26N28N42
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A*N32******                        8  2'Transaction Subtype....:'                        CAS006
     A**********  #1SUBT         2A  B  8 30                                                  CAS006
     A N32                              9  2'Transaction Subtype....:'
     A            #1SUBT         2A  B  9 30
      **End of change CAS006
     A                                      OVRDTA
     A N26 43                               DSPATR(RI PC)
     A N26N43                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A*N32******                        9  2'Input Date.............:'                        CAS006
     A*N33******  #1INDT         6Y 0B  9 30                                                  CAS006
     A N32                             10  2'Input Date.............:'
     A N33        #1INDT         6A  B 10 30
      **End of change CAS006
     A                                      OVRDTA
     A N26 44                               DSPATR(RI PC)
     A N26N44                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A**33******  #1INDA         7   B  9 30                                                  CAS006
     A  33        #1INDA         7   B 10 30
      **End of change CAS006
     A                                      OVRDTA
     A  33                                  DSPATR(PR)
     A N33                                  DSPATR(UL)
      *
      **Start of change CAS006
     A N32 60                          11  2'Start Date.............:'
     A N33        #1SDAT         6A  B 11 30
     A                                      OVRDTA
     A N26 62                               DSPATR(RI PC)
     A N26N62                               DSPATR(UL HI)
     A  26
     AO 32
     AON60                                  DSPATR(PR)
     A  32
     AON60                                  DSPATR(ND)
      *
     A  33        #1SDAA         7   B 11 30
     A                                      OVRDTA
     A  33
     AON60                                  DSPATR(PR)
     A N60                                  DSPATR(ND)
     A N33                                  DSPATR(UL)
      *
     A*N32******                       10  2'Maturity Date..........:'                        CAS006
     A*N33******  #1MDAT         6Y 0B 10 30                                                  CAS006
     A N32                             12  2'Maturity Date..........:'
     A N33        #1MDAT         6A  B 12 30
      **End of change CAS006
     A                                      OVRDTA
     A N26 45                               DSPATR(RI PC)
     A N26N45                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A**33******  #1MDAA         7   B 10 30                                                  CAS006
     A  33        #1MDAA         7   B 12 30
      **End of change CAS006
     A                                      OVRDTA
     A  33                                  DSPATR(PR)
     A N33                                  DSPATR(UL)
      *
      **Start of change CAS006
     A*N32******                       11  2'Expenditure/Receipt....:'                        CAS006
     A**********  #1ERIN         1A  B 11 30                                                  CAS006
     A N32                             13  2'Expenditure/Receipt....:'
     A            #1ERIN         1A  B 13 30
      **End of change CAS006
     A                                      OVRDTA
     A N26 46                               DSPATR(RI PC)
     A N26N46                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A*N32******                       12  2'Currency...............:'                        CAS006
     A**********  #1CCY          3A  B 12 30                                                  CAS006
     A N32                             14  2'Currency...............:'
     A            #1CCY          3A  B 14 30
      **End of change CAS006
     A                                      OVRDTA
     A N26 47                               DSPATR(RI PC)
     A N26N47                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A*N32******                       13  2'Amount.................:'                        CAS006
     A**********  #1AMNT        14A  B 13 30                                                  CAS006
     A N32                             15  2'Amount.................:'
     A            #1AMNT        14A  B 15 30
      **End of change CAS006
     A                                      OVRDTA
     A N26 48                               DSPATR(RI PC)
     A N26N48                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
      *
      **Start of change CAS006
     A*N32******                       14  2'Revaluation Yield Curve:'                        CAS006
     A**********  #1YLDC         5A  B 14 30                                                  CAS006
     A N32N60                          16  2'Revaluation Yield Curve:'
     A            #1YLDC         5A  B 16 30
      **End of change CAS006
     A                                      OVRDTA
     A N26 49                               DSPATR(RI PC)
     A N26N49                               DSPATR(UL HI)
     A  26
     AO 32                                  DSPATR(PR)
      **Start of change CAS006
     A**32******                            DSPATR(ND)                                        CAS006
     A  60
     AO 32                                  DSPATR(ND)
     A N32 60                          17  2'Underlying Ref. Number.:'
     A            #1UREF         6A  B 17 30
     A                                      OVRDTA
     A     63                               DSPATR(RI PC)
     A N26N63
     AO 26N63 64                            DSPATR(UL HI)
     A  26N63N64
     AO 32
     AON60                                  DSPATR(PR)
     A  32
     AON60                                  DSPATR(ND)
      **End of change CAS006
      *
      ** Footer
      *
     A          R AS000002F2                OVERLAY PUTOVR
     A                                      OVRATR OVRDTA
     A  52N40                          23  2'A=Amend D=Delete E=Enquire +
     A                                      F3=Main Menu F5=Refresh +
     A                                      F9=Go to Add Mode'
     A  53N40                          23  2'E=Enquire +
     A                                      F3=Main Menu F5=Refresh'
     A  54                             23  2'F3=Main Menu  F12=Previous'
     A  55                             23  2'F3=Main Menu  F10=Delete +
     A                                       F12=Previous'
     A  52 40                          23  2'F3=Main Menu F9=Go to Add Mode'
     A  53 40                          23  2'F3=Main Menu'
      *
      ** Message Subfile
      *
     A          R AS000002S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R AS000002C1                TEXT('Program messages')
     A                                      SFLCTL(AS000002S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
