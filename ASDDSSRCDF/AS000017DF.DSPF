/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AS Historic Market Data Maint (Screen 1)')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  DSPF/AS000017DF - Midas AS Historic Market Data Mnt/Enq/Rpt  *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      *
      ** Message subfile format
      *
     A          R AS000017S0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R AS000017C0                TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      SFLCTL(AS000017S0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            #0PGMQ                    SFLPGMQ
      *
      ** Transaction number subfile
      *
     A          R AS000017S1                SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A  04                                  SFLNXTCHG
     A            #1SELT         1A  B  6  4CHECK(ER)
     A N92                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI PC)
     A  93                                  DSPATR(ND PR)
     A            #1HEDI         6S 0O  6 16DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #1HGST        10A  O  6 32DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            #1EFFD         7A  O  6 57DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
      ** Transaction number subfile control
      *
     A          R AS000017C1                SFLCTL(AS000017S1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'AS000017S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #1WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 25'Historic Market Data +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Select'
     A                                  4 15'Hedge ID'
     A                                  4 30'Hedge Strategy'
     A                                  4 55'Effective Date'
     A            #1TRNN         6A  B  5 16CHECK(ER)
     A N05                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(RI PC)
     A  93                                  DSPATR(ND PR)
      *
      ** Select screen footer
      *
     A          R AS000017F0                TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'1=Select  +
     A                                       F3=Main Menu '
     A            TOGLTEXT      30   O 24 33OVRDTA
