      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AS Historic market data maint (screen 2)')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  DSPF/AS000018DF - Midas AS Historic Market Data Maintenance  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 225618             Date 30Mar04               *
      *  Prev Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  225618 - Change of 'Net Present Value' to 'Fair Value'.      *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      *
      ** Message subfile format
      *
     A          R AS000018S0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message subfile control
      *
     A          R AS000018C0                TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF05
     A                                      CF10
     A                                      CF12
     A                                      SFLCTL(AS000018S0)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            #0PGMQ                    SFLPGMQ
      *
      ** Subfile for DL and LE
      *
     A          R AS000018S1                SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A  04                                  SFLNXTCHG
     A            #1DELT         1A  B  9  5
     A N90                                  DSPATR(HI)
     A N87                                  DSPATR(UL)
     A  90                                  DSPATR(RI PC)
     A  87                                  DSPATR(PR)
     A            #1VALD         7A  B  9 10
     A N92                                  DSPATR(HI)
     A N88                                  DSPATR(UL)
     A  92                                  DSPATR(RI PC)
     A  88                                  DSPATR(PR)
     A            #1DAMT        21A  B  9 22
     A N91                                  DSPATR(HI)
     A N93                                  DSPATR(UL)
     A  91                                  DSPATR(RI PC)
     A  93                                  DSPATR(PR)
     A            #1IAMT        21A  B  9 51
     A N89                                  DSPATR(HI)
     A N93                                  DSPATR(UL)
     A  89                                  DSPATR(RI PC)
     A  93                                  DSPATR(PR)
     A            #1DTAT         1A  H
     A            #1FRFL         5N 0H
      *
      ** Subfile control for DL and LE
      *
     A          R AS000018C1                SFLCTL(AS000018S1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF05
     A                                      CF10
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'AS000018S1'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #1WID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 25'Historic Market Data +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  3'Hedge Derivative Transaction -
     A                                      No.: '
     A            #1HDNO         6A  O  4 40DSPATR(HI)
     A N22                              5  3'Hedged Item Transaction No.:'
     A N22        #1HINO         6A  O  5 40DSPATR(HI)
      *
     A  22                              5  3'Hedged Security Short Name:'
     A  22        #1FPSE        10A  O  5 31DSPATR(HI)
     A  22                              5 42'Branch'
     A  22        #1BRCA         3A  O  5 49DSPATR(HI)
     A  22                              5 53'Book'
     A  22        #1BKCD         2A  O  5 58DSPATR(HI)
     A  22                              5 61'Mkt Ind'
     A  22        #1MKIN         1A  O  5 69DSPATR(HI)
     A  22                              5 71'Trd/Val'
     A  22        #1BHTV         1A  O  5 79DSPATR(HI)
      *
      *
     A                                  7 22'Hedge Derivative'
     A                                  7 51'Hedged Item'
     A                                  8  4'Del'
     A                                  8 10'Value Date'
     A**Start of change 225618
     A**10******                        8 22'Net Present Value            +                   225618
     A**********                            Net Present/Market Value'                         225618
     A  10                              8 22'Fair Value                   +
     A                                      Fair Value'
     A**11******                        8 22'Market Value Change          +                   225618
     A**********                            Market Value Change'                              225618
     A  11                              8 22'Fair Value Change            +
     A                                      Fair Value Change'
     A**End of change 225618
     A  12                              8 22'Int Rate/Mkt Prices/Id+
     A                                      x Val  Int Rate/Mkt Prices/Idx +
     A                                      Val'
      *
      ** Select screen footer
      *
     A          R AS000018F0
     A                                      TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF05
     A                                      CF10
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'Rollup/Rolldown for further -
     A                                      records'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'F3=Main Menu  F5=Refresh  -
     A                                      F10=Accept  F12=Previous'
     A            TOGLTEXT      25   O 24 55OVRDTA
