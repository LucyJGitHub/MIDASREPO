      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AS Historic Market Data Report prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standard Module                           *
      *                                                               *
      *  DSPF/AS000020DF - Midas AS Historic Market Data Report       *
      *                    Prompt                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A N13                                  CA05
     A  13                                  CA06
     A                                      MSGLOC(24)
      *
     A          R AS000020F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      OVERLAY
      *
     A            #0USER        10A  O  1  2TEXT('User Id')
     A                                  1 13'AS000020DF'
     A                                  1 28'M I D A S  Banking System'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUND         7A  O  1 73
     A            #0WSID        10A  O  2  2TEXT('Workstation Id')
     A                                  2 27'Historic Market Data Report'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  8  9'This function will produce a-
     A                                       report of a hedge linkage''s'
     A                                  9  9'collected historic market data.'
     A                                 11  9'Enter a Hedge Linkage ID to-
     A                                       process a specific linkage or'
     A                                 12  9'leave it blank to process all link-
     A                                      ages in the system.'
     A                                 15 21'Hedge Linkage ID:'
     A            #0HEDI         6   B 15 41
     A  10                                  DSPATR(HI)
     A  11                                  DSPATR(PC)
     A  13                                  DSPATR(PR)
     A  20                                  DSPATR(RI)
     A N13                             22  3'F3=Exit  F5=Refresh  F12=Previous'
     A  13                             22  3'F3=Exit   F6=Confirm Report   +
     A                                      F12=Previous'
      *
     A          R AS000020S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R AS000020C0                TEXT('Program messages')
     A                                      SFLCTL(AS000020S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
      *
