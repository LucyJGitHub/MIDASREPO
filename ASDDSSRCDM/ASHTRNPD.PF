      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas AS Hedge transactions file')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PF/ASHTRNPD - Midas AS Hedge Transactions File               *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAS005             Date 16Dec02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
      *
     A          R ASHTRND0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            FSHEDI         6S 0       TEXT('Hedge ID Number')
     A                                      COLHDG('Hedge' +
     A                                             'ID Number')
     A            FSDHIN         1A         TEXT('Deriv/Hedged Indicator')
     A                                      COLHDG('Deriv/Hedged' +
     A                                             'Indicator')
     A            FSMOD          2A         TEXT('Module (DL, LE, FH, SE, FF)')
     A                                      COLHDG('Module')
      **Start of change CLE148
     A**********  FSDLNO         6S 0       TEXT('Deal/Loan/FH Number')                       CLE148
     A            FSDLNO         6A         TEXT('Deal/Loan/FH Number')
      **End of change CLE148
     A                                      COLHDG('Deal/Loan' +
     A                                             'FH Number')
     A            FSFPSE        10A         TEXT('F & O Position/Security')
     A                                      COLHDG('F & O Position/' +
     A                                             'Security')
     A            FSPCTG         6P 2       TEXT('Percentage')
     A                                      COLHDG('Percentage')
     A            FSBKCD         2A         TEXT('Book Code')
     A                                      COLHDG('Book' +
     A                                             'Code')
     A            FSBRCA         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' +
     A                                             'Code')
     A            FSPCAL         1A         TEXT('Put/Call')
     A                                      COLHDG('Put/' +
     A                                             'Call')
     A            FSBHTV         1A         TEXT('Trade/Value Date Basis')
     A                                      COLHDG('Trade/Value' +
     A                                             'Date Basis')
     A            FSMKTI         1A         TEXT('Market Indicator')
     A                                      COLHDG('Market' +
     A                                             'Indicator')
      **Start of change CAS004
     A            FSHTID         1A         TEXT('Hedge Transaction +
     A                                            Record ID')
     A                                      COLHDG('Hedge Transaction' +
     A                                             'Record ID')
     A            FSHTCT         1A         TEXT('Last Change Type')
     A                                      COLHDG('Last Change' +
     A                                             'Type')
     A            FSHTCD         5P 0       TEXT('Last Change Date')
     A                                      COLHDG('Last Change' +
     A                                             'Date')
     A            FSEFVP        13P 0       TEXT('Effective Fair Value +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Effective Fair Value' +
     A                                            'Hedge Unrealized' +
     A                                            'Profit/Loss Posted')
     A            FSHECP        13P 0       TEXT('Effective Cash Flow +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Effective Cash Flow' +
     A                                            'Hedge Unrealized' +
     A                                            'Profit/Loss Posted')
     A            FSHXFP        13P 0       TEXT('Ineffective Fair Value +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Ineffective Fair' +
     A                                            'Value Hedge' +
     A                                            'Unrealized PL Posted')
     A            FSHXCP        13P 0       TEXT('Ineffective Cash Flow +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Ineffective Cash' +
     A                                            'Flow Hedge' +
     A                                            'Unrealized PL Posted')
      **End of change CAS004
      **Start of change CAS005
     A            FSHTFP        13P 0       TEXT('Fair Value Hedge in +
     A                                           Trading Status Unrealized +
     A                                           Profit/Loss Posted')
     A                                      COLHDG('Fair Value Hedge in' +
     A                                          'Trading Status' +
     A                                          'Unrealised P/L')
     A            FSHTCP        13P 0       TEXT('Cash Flow Hedge in +
     A                                           Trading Status Unrealized +
     A                                           Profit/Loss Posted')
     A                                      COLHDG('Cash Flow Hedge in' +
     A                                          'Trading Status' +
     A                                          'Unrealised P/L')
     A            FSHSID         1A         TEXT('Hedged Side Indicator')
     A                                      COLHDG('Hedged Side' +
     A                                          'Indicator')
      **End of change CAS005
