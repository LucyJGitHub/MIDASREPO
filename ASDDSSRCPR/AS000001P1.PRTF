      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AS Hedge linkage detail report')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS000001P1 - Hedge Linkage Detail Report                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006             Date 21Jan03               *
      *  Prev Amend No. 212430             Date 20Nov02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  212430 - Various errors on deleted transaction and deleted   *
      *           linkages                                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A                                      INDARA
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE AS000001P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A            RTITLE        53        37TEXT('Audit/Request Title')
     A                                      SPACEA(1)
     A            RUNDLN        53        37TEXT('Underline')
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                      SPACEB(003)
     A                                     2'Hedge ID..................:'
     A            RHEDI          6        +2
     A            RACT           7       100
     A            RACTION       10        +2
     A                                      SPACEA(002)
     A                                    10'Hedge Strategy............:'
     A            RHGST         10        +2
     A                                      SPACEA(001)
     A                                    10'Effective Date............:'
     A            REFFD          7        +2
      **Start of change CAS006
     A**********                            SPACEA(001)                                       CAS006
     A N21                                  SPACEA(001)
     A  21                                55'Data Points Required......:'
     A  21        RDPTS          3        +2
     A  21                                  SPACEA(001)
      **End of change CAS006
     A                                    10'Input Date................:'
     A            RINDT          7        +2
      **Start of change CAS006
     A**********                            SPACEA(001)                                       CAS006
     A N21                                  SPACEA(001)
     A  21                                55'Data Collection Frequency.:'
     A  21        RDFRQ          1        +2
     A  21                                  SPACEA(001)
      *
      ** Field RFCOA might still be used in the future
      *
     A**********                          10'Functional Currency Offset:'                     CAS006
     A**********  RFCOA          1        +2                                                  CAS006
     A**********                            SPACEA(001)                                       CAS006
     A*N21******                            SPACEA(001)                                       CAS006
     A  21                                55'Day of the Week...........:'
     A  21        RWDAY          3        +2
     A  21                                  SPACEA(001)
      *
      ** Field RICAT might still be used in the future
      *
     A**********                          10'Investment Category.......:'                     CAS006
     A**********  RICAT          1        +2                                                  CAS006
     A**********                            SPACEA(001)                                       CAS006
     A*N21******                            SPACEA(001)                                       CAS006
     A  21                                55'Day of the Month..........:'
     A  21        RMNTH          2        +2
     A  21                                  SPACEA(001)
      **End of change CAS006
     A                                    10'Start Date................:'
     A            RSTDT          7        +2
      **Start of change CAS006
     A**********                            SPACEA(001)                                       CAS006
     A N21                                  SPACEA(001)
     A  21                                55'Next Data Collection Date.:'
     A  21        RNDTA          7        +2
     A  21                                  SPACEA(001)
      **End of change CAS006
     A                                    10'End Date..................:'
     A            RENDT          7        +2
      *
      *
     A          R DETAIL2
     A                                      SPACEB(002)
     A                                    10'Hedge Derivatives'
     A                                    39'Percentage'
      *
     A          R DETAIL2A
     A                                      SPACEB(001)
      **Start of change 212430
     A            RDDEL          1        15
      **End of change 212430
     A  04        RDMOD          2        17
     A  04        RDDNO          6        20
     A N04        RFPNO         10        20
     A            RDPCT          7        43
      *
      *
     A          R DETAIL3
     A                                      SPACEB(002)
     A                                    10'Hedged Deal/Loan/Security'
     A                                    39'Percentage'
      *
     A          R DETAIL3A
     A                                      SPACEB(001)
      **Start of change 212430
     A            RHDEL          1        15
      **End of change 212430
     A  05        RHMOD          2        17
     A  05        RHDNO          6        20
     A N05        RHSEC         10        20
     A            RHPCT          7        43
      *****************************************************************
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                      SPACEB(3)
     A                                    46'Number of Records +
     A                                      Printed..:'
     A            RTOTAL         5  0     +2EDTCDE(4)
     A                                      SPACEA(002)
     A  11                                49'***  NO DETAILS TO +
     A                                      REPORT  ***'
     A  11                                  SPACEA(002)
      *
     A                                    54'***  END OF REPORT  ***'
