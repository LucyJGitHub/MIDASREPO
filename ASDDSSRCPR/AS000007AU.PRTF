      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas AS Daily file maintenance/reorganisation')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS000007AU - Daily File Maintenance/Reorganization for  *
      *                    Hedge Strategy, Linkages and Transaction   *
      *                    Files - AUDIT                              *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header Record Format
      *
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS000007AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    32'DAILY FILE MAINTENANCE/+
     A                                       REORGANIZATION - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    32'------------------------+
     A                                      -------------------'
     A                                      SPACEA(3)
      *
      **  File Control
      *
     A          R FILCON
     A                                    69'CALCULATED'
     A                                      SPACEA(1)
     A                                    69'----------'
     A                                      SPACEA(3)
      *
     A                                     6'TOTAL NUMBER OF HEDGE +
     A                                      STRATEGY DETAILS BEFORE UPDATE:'
     A            WCOUNTER1      9  0     70EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF HEDGE STRATEGY +
     A                                      DETAILS DROPPED TODAY:'
     A            WCOUNTER2      9  0     70EDTCDE(3) SPACEA(2)
      *
     A                                     6'TOTAL NUMBER OF HEDGE +
     A                                      LINKAGE DETAILS BEFORE UPDATE:'
     A            WCOUNTER3      9  0     70EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF HEDGE LINKAGE +
     A                                      DETAILS DROPPED TODAY:'
     A            WCOUNTER4      9  0     70EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF HEDGE LINKAGE +
     A                                      TRANSACTIONS DROPPED TODAY:'
     A            WCOUNTER5      9  0     70EDTCDE(3) SPACEA(2)
      *
     A                                     6'TOTAL NUMBER OF FORECAST +
     A                                      TRANSACTION DETAILS BEFORE UPDATE:'
     A            WCOUNTER6      9  0     70EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF FORECAST TRANSACTION +
     A                                      DETAILS DROPPED TODAY:'
     A            WCOUNTER7      9  0     70EDTCDE(3) SPACEA(2)
      *
      **  No Details
      *
     A          R EOREPT                    SPACEB(3)
     A  26                                52'*** NO DETAILS TO REPORT ***'
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      **  Database Error
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
