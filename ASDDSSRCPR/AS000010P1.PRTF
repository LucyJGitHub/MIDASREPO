      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas AS DL A/C Key Generation Pgm - Printer File')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS000010P1 - Dealing Account Key Generation Program     *
      *                    (DETAIL)                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAS004  *CREATE    Date 26Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS004 - Hedge Accounting Phase A                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS000010P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'DEALING HEDGING ACCOUNT +
     A                                      KEY GENERATION'
     A                                    +2'- UP TO'
     A            REVCD          7A       +1
     A                                      SPACEA(1)
      *
     A                                    36'----------------------+
     A                                      -------------------------+
     A                                      --------'
     A                                      SPACEA(2)
      *
     A  03                                 5'BRANCH: '
     A  03        A8BRCD    R             +1REFFLD(BRCA)
     A  03        A8BRNM    R             +2REFFLD(BRNM)
     A  03                                  SPACEA(2)
      *
      ** Sub-Heading:
      *
     A  02                                 5'Customer'
     A  02                                20'Deal No.'
     A                                    33'Account Key'
     A                                    56'Amount'
     A                                    72'Currency'
     A                                    86'Rev Ind'
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETAIL
      *
     A                                      SPACEA(1)
     A  02        RCNUM          6A        5
     A  02        RDEAL          6A       20
      **Start of change CDL038
     A            RAKEY         14A       31                                                  CDL038
     A************RAKEY*********10A       33                                                  CDL038
      **End of change CDL038
     A            RAMNT         22A       45
     A            RCURR          3A       75
     A            RREVI          1A       89
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                      SPACEB(1)
     A                                    50'Deal Account Keys Generated '
     A            RNOREC         5A       80
     A                                      SPACEA(1)
     A  03                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N03                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
