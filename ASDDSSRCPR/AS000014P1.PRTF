      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas AS Forecast Trans. A/C Key Gen. - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS000014P1  - Forecast Transactions Account Key         *
      *                     Generation - Detail                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS000014P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    35'FORECAST TRANSACTIONS +
     A                                      A/C KEY GENERATION'
     A                                    +2'- UP TO'
     A            REVCD          7A       +1
     A                                      SPACEA(1)
      *
     A                                    35'----------------------+
     A                                      ------------------+
     A                                      -----------------'
     A                                      SPACEA(2)
      *
     A  37                                45'BRANCH -'
     A  37        A8BRCD    R             54REFFLD(BRCA)
     A  37        A8BRNM    R             59REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A  37                                45'-----------------+
     A                                      ---------------'
     A                                      SPACEA(2)
      *
      ** Sub-Heading:
      *
     A                                     5'Customer'
     A                                    20'Deal No.'
     A                                    33'Account Key'
     A                                    56'Amount'
     A                                    72'Currency'
     A                                    86'Module'
     A                                   101'Event'
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETAIL
      *
     A                                      SPACEA(1)
     A            RCNUM          6A        5
     A            RTRAN          6A       20
     A            RAKEY         10A       33
     A            RAMNT         22A       45
     A            RCURR          3A       75
     A            RMOD           3A       88
     A            REVNT         13A       97
      *
      ** Report Trailer
      *
     A          R TRAILP1                   SPACEB(2)
     A                                    40'Forecast Transaction A/C +
     A                                       Keys Generated '
     A            RNOREC         5A       80
     A                                      SPACEA(1)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
