      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas AS Hedge SE A/C Key Generation Rpt - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS000016P1 - Hedge SE Account Key Generation Program    *
      *                    (DETAIL)                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS000016P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'SECURITIES HEDGING ACCOUNT +
     A                                      KEY GENERATION'
     A                                    +2'- UP TO'
     A            REVCD          7A       +1
     A                                      SPACEA(1)
      *
     A                                    34'-------------------------+
     A                                      -------------------------+
     A                                      --------'
     A                                      SPACEA(2)
      *
     A  37                                 5'BRANCH: '
     A  37        A8BRCD    R             +2REFFLD(BRCA)
     A  37        A8BRNM    R             +2REFFLD(BRNM)
     A  37                                  SPACEA(2)
      *
      ** Sub-Heading:
      *
     A  02                                 5'Customer'
     A  02                                18'Security'
     A  02                                32'Book'
     A  02                                41'Market'
     A  02                                51'Basis'
     A                                    65'Account Key'
     A                                    99'Amount'
     A                                   111'Currency'
     A                                   126'Rev Ind'
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETAIL
      *
     A                                      SPACEA(1)
     A  02        RCNUM          6A        5
     A  02        RSECU         10A       18
     A  02        RBOOK          2A       33
     A  02        RMARK          1A       43
     A  02        RTRVL          1A       53
     A            RAKEY         20A       60
     A            RAMNT         22A       86
     A            RCURR          3A      114
     A            RREVI          1A      129
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                      SPACEB(1)
     A                                    50'Security Account Keys Generated '
     A            RNOREC         5A       83
     A                                      SPACEA(1)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
