      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AS Todays effective SE hedges report')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001004P1 - Midas AS Hedging of SE Effective Report    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. 225293             Date 26Feb04               *
      *                 CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  225293 - Report is showing Hedge Effectiveness Percentage of *
      *           linkages not yet assessed                           *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format 1
      *
     A          R HEADR1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE AS001004P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
     A  25                                38'SE EFFECTIVE HEDGES REPORT - -
     A                                      FAIR VALUE'
      *
     A  25                                +2'AS OF'
     A  25        OEVCD1         7A       +1
     A  25                                  SPACEA(1)
      *
     A  26                                38'SE EFFECTIVE HEDGES REPORT - -
     A                                      FOREIGN CURRENCY'
      *
     A  26                                +2'AS OF'
     A  26        OEVCD2         7A       +1
     A  26                                  SPACEA(1)
      *
     A  27                                38'SE EFFECTIVE HEDGES REPORT - -
     A                                      CASH FLOW'
      *
     A  27                                +2'AS OF'
     A  27        OEVCD3         7A       +1
     A  27                                  SPACEA(1)
      *
     A  25                                38'---------------------------
     A                                      ----------------------------'
     A  25                                  SPACEA(2)
     A  26                                38'------------------------------
     A                                      -------------------------------'
     A  26                                  SPACEA(2)
     A  27                                38'---------------------------
     A                                      ---------------------------'
     A  27                                  SPACEA(2)
     A  37                                 1'BRANCH: '
     A  37        O1BRCD    R             +2REFFLD(BRCA)
     A  37        O1BRNM    R             +2REFFLD(BRNM)
     A  37                                  SPACEA(2)
     A                                     1'STRATEGY:'
     A                                      UNDERLINE
     A            O1HESN        10A       +2
     A            O1HELD        35A       +2
     A                                      SPACEA(003)
      *
     A                                     1'BOOK'
     A                                     6'DERIVATIVE/'
     A                                    18'CUSTOMER'
     A                                    27'VALUE'
     A                                    33'MATURITY'
     A                                    63'INTEREST RATE/'
     A N40                                79'FISCAL YEAREND'
     A N40                               102'UNRL P/L'
     A                                      SPACEA(001)
     A  40                               105'OCI'
     A                                      SPACEA(001)
      *
     A                                     1'CODE'
     A                                     6'HEDGED ITEM'
     A                                    20'NO.'
     A                                    27'DATE'
     A                                    33'DATE'
     A                                    41'CCY'
     A                                    50'PRINCIPAL'
     A                                    63'AVERAGE PRICE'
     A N40                                82'UNRL P/L'
     A N40                               101'AS OF TODAY'
     A  40                               101'AS OF TODAY'
     A                                      SPACEA(001)
     A N40                               123'CHANGE'
     A                                      SPACEA(001)
      *
     A N40                                81'IN BASE CCY'
     A N40                               101'IN BASE CCY'
     A  40                               101'IN BASE CCY'
     A                                      SPACEA(001)
     A N40                               121'IN BASE CCY'
     A                                      SPACEA(001)
      *
     A                                     1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------+
     A                                       -----------------------------'
     A                                      SPACEA(001)
      *
      ** Hedge Id Detail Header
      *
     A          R DETAIL1H
     A                                      SPACEB(001)
     A                                     2'HEDGE NO.:'
     A                                      UNDERLINE
     A            OHEDI          6A       14
     A                                      SPACEA(001)
      *
      ** Hedge Id Details
      *
     A          R DETAIL1
     A                                      SPACEB(001)
     A            OBKC           2A        2
     A            ODLNO         10A        5
     A            ODTYP          2A       12
     A            ODASH          1A       14
     A            ODLST          2A       15
     A            OCNUM          6A       18
     A            OVALD          7A       25
     A            OMATD          7A       33
     A            OCCY      R             41REFFLD(CCY)
     A            OPRIN         18A       45
     A            OINTR         12A       63
     A N40        OYUPL         18A       76
     A            OURPL         18A       95
     A N40        ODIFF         18A      114
      *
      ** Derivative Hedge Sub-Total Record Format
      *
     A          R LINKTOD1
     A                                    39'             DERIVATIVE SUB-TOTAL'
     A N40        ODYUPL        18A       76
     A            ODURPL        18A       95
     A N40        ODDIFF        18A      114
     A                                   130' '
     A                                      SPACEA(001)
      *
      ** Hedged Sub-Total Record Format
      *
     A          R LINKTOH1
     A                                    39'                 HEDGED SUB-TOTAL'
     A N40        OHYUPL        18A       76
     A            OHURPL        18A       95
     A N40        OHDIFF        18A      114
      *
      **  Hedge ID Total Record Format
      *
     A          R LINKTOT1
     A                                      SPACEB(001)
     A                                    39'                      HEDGE TOTAL'
     A N40        OTYUPL        18A       76
     A            OTURPL        18A       95
     A N40        OTDIFF        18A      114
      *
      ** Change Percentage
      *
     A          R PERCENT1
     A                                      SPACEB(001)
     A                                    39'HEDGE EFFECTIVENESS AS OF '
     A            OLASDT         7A       65
     A            OCHGPC         7A       85
      **Start of change 225293
     A**********                          92 '%'                                              225293
     A N24                                92 '%'
      **End of change 225293
     A                                      SPACEA(001)
      *
      ** Dotted Line
      *
     A          R DOTTED1
     A                                      SPACEB(001)
     A                                     1'..................................+
     A                                       ..................................+
     A                                       ..................................+
     A                                       .............................'
     A                                      SPACEA(001)
      *
      ** Strategy Total Record Format (Derivative)
      *
     A          R HSENTOD1
     A                                    39'        STRATEGY DERIVATIVE TOTAL'
     A N40        YUPLD1        18A       76
     A            URPLD1        18A       95
     A N40        DIFFD1        18A      114
      *
      **  Strategy Total Record Format (Hedged)
      *
     A          R HSENTOH1
     A                                      SPACEB(001)
     A                                    39'            STRATEGY HEDGED TOTAL'
     A N40        YUPLH1        18A       76
     A            URPLH1        18A       95
     A N40        DIFFH1        18A      114
      *
      **  Strategy Total Record Format (Total)
      *
     A          R HSENTON1
     A                                      SPACEB(001)
     A                                    39'               STRATEGY NET TOTAL'
     A N40        YUPLN1        18A       76
     A            URPLN1        18A       95
     A N40        DIFFN1        18A      114
      *
      ** Product Type Total Record Format (Derivative)
      *
     A          R PRTPTOD1
     A        25                          39'      FAIR VALUE DERIVATIVE TOTAL'
     A        26                          39'FOREIGN CURRENCY DERIVATIVE TOTAL'
     A        27                          39'       CASH FLOW DERIVATIVE TOTAL'
     A N40        YUPLD2        18A       76
     A            URPLD2        18A       95
     A N40        DIFFD2        18A      114
      *
      **  Product Type Total Record Format (Hedged)
      *
     A          R PRTPTOH1
     A                                      SPACEB(001)
     A        25                          39'          FAIR VALUE HEDGED TOTAL'
     A        26                          39'    FOREIGN CURRENCY HEDGED TOTAL'
     A        27                          39'           CASH FLOW HEDGED TOTAL'
     A N40        YUPLH2        18A       76
     A            URPLH2        18A       95
     A N40        DIFFH2        18A      114
      *
      ** Product Type Total Record Format (Total)
      *
     A          R PRTPTON1
     A                                      SPACEB(001)
     A        25                          39'             FAIR VALUE NET TOTAL'
     A        26                          39'       FOREIGN CURRENCY NET TOTAL'
     A        27                          39'              CASH FLOW NET TOTAL'
     A N40        YUPLN2        18A       76
     A            URPLN2        18A       95
     A N40        DIFFN2        18A      114
      *
      **  Branch Total Record Format (Derivative)
      *
     A          R BRCATOD1
     A                                    39'          BRANCH DERIVATIVE TOTAL'
     A N40        YUPLD3        18A       76
     A            URPLD3        18A       95
     A N40        DIFFD3        18A      114
      *
      ** Branch Total Record Format (Hedged)
      *
     A          R BRCATOH1
     A                                      SPACEB(001)
     A                                    39'              BRANCH HEDGED TOTAL'
     A N40        YUPLH3        18A       76
     A            URPLH3        18A       95
     A N40        DIFFH3        18A      114
      *
      **  Branch Total Record Format (Total)
      *
     A          R BRCATON1
     A                                      SPACEB(001)
     A                                    39'                 BRANCH NET TOTAL'
     A N40        YUPLN3        18A       76
     A            URPLN3        18A       95
     A N40        DIFFN3        18A      114
      *
      **  Report Trailer
      *
     A          R TRAILP1
     A                                      SPACEB(003)
     A  37                                46'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(001)
     A  37        O1BRCD    R             +1REFFLD(BRCA)
     A  37                                +2'***'
      *
     A N37                                51'***  END OF REPORT  ***'
      *
     A          R BLANK1
      *
     A                                      SPACEB(001)
     A                                     1' '
