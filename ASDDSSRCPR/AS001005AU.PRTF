      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas AS Todays expired hedges report audit')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001005AU - Today's Expired Hedges Report - Audit      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS001005AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    37'TODAY''S EXPIRED HEDGES REPORT +
     A                                        AS OF'
      **Start of change CAS004
     A**********                            UNDERLINE                                         CAS004
     A**********  ORPTDT    R             +0REFFLD(MRDT)                                      CAS004
     A**********                            UNDERLINE                                         CAS004
     A            ORPTDT    R             +1REFFLD(MRDT)
      **End of change CAS004
     A                                    +0' - AUDIT REPORT'
      **Start of change CAS004
     A**********                            UNDERLINE                                         CAS004
     A                                      SPACEA(1)
     A                                    37'--------------------------
     A                                      ---------------------------
     A                                      -------'
      **End of change CAS004
     A                                      SPACEA(3)
     A                                    85'CALCULATED'
     A                                      SPACEA(1)
     A                                    85'----------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R TOTAUD
     A                                     6'TOTAL NO. OF HEDGE STRATEGY +
     A                                      RECORDS PROCESSED TODAY'
     A            SCOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'TOTAL NO. OF HEDGE LINKAGE +
     A                                      RECORDS FLAGGED AS EXPIRED TODAY'
     A            ECOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
