      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AS Todays expired hedges report')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001005P1 - Todays' Expired Hedges Report - Detail     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R AS001005D1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS001005P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A  25                                40'TODAY''S EXPIRED HEDGES REPORT - -
     A                                      FAIR VALUE'
      **Start of change CAS004
     A**********                            UNDERLINE                                         CAS004
      **End of change CAS004
      *
     A  26                                40'TODAY''S EXPIRED HEDGES REPORT - -
     A                                      FOREIGN CURRENCY'
      **Start of change CAS004
     A**********                            UNDERLINE                                         CAS004
      **End of change CAS004
      *
     A  27                                40'TODAY''S EXPIRED HEDGES REPORT - -
     A                                      CASH FLOW'
      **Start of change CAS004
     A**********                            UNDERLINE                                         CAS004
     A**37******                          88'BRANCH -'                                        CAS004
     A**37******  PBRCD     R             97REFFLD(BRCA)                                      CAS004
     A**37******  PBRNM     R            101REFFLD(BRNM)                                      CAS004
      *
     A**********                         132' '                                               CAS004
     A**********                            SPACEA(3)                                         CAS004
     A                                    +2'AS OF'
     A            PEVCD          7A       +1
     A                                      SPACEA(1)
     A  25                                40'----------------------------------
     A                                      -----------------------'
     A  25                                  SPACEA(2)
     A  26                                40'----------------------------------
     A                                      ------------------------------'
     A  26                                  SPACEA(2)
     A  27                                40'----------------------------------
     A                                      ------------------------'
     A  27                                  SPACEA(2)
      *
     A  37                                 1'BRANCH: '
     A  37        PBRCD     R             +2REFFLD(BRCA)
     A  37        PBRNM     R             +2REFFLD(BRNM)
     A  37                                  SPACEA(2)
      **End of change CAS004
      *
     A                                     1'STRATEGY:'
     A                                      UNDERLINE
     A            PHESN         10A       +2
     A            PHELD         35A       +2
     A                                      SPACEA(2)
      *
     A                                     6'HEDGE '
     A                                    +2'      '
     A                                    +2'DERIVATIVE/'
     A                                    +2'BOOK'
     A                                    +2'TYPE/  '
     A                                    +2'CUSTOMER'
     A                                    +2'VALUE  '
     A                                    +2'MATURITY'
     A                                    +2'CURRENCY'
     A                                    +2'        PRINCIPAL'
     A                                    +3'       INTEREST'
      *
     A                                      SPACEA(1)
      *
     A                                     6'NUMBER'
     A                                    +2'MODULE'
     A                                    +2'HEDGED ITEM'
     A                                    +2'CODE'
     A                                    +2'SUBTYPE'
     A                                    +2'NUMBER  '
     A                                    +2'DATE   '
     A                                    +2'DATE    '
     A                                    +2'CODE    '
     A                                    +2'           AMOUNT'
     A                                    +3'           RATE'
     A                                      SPACEA(1)
      *
     A                                     6'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------'
      *
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R AS001005D2
     A                                      SPACEB(001)
     A            PHEDI          6A        6
     A            PMOD           2A       +4
     A            PDLNO         10A       22
     A            PBKC           2A       36
     A            PDTYP     R             41REFFLD(DTYP)
     A            PDASH          1        +0
     A            PDLST     R             +0REFFLD(DLST)
     A            PCNUM          6A       50
     A            PVALD          7A       60
     A            PMATD          7A       69
     A            PCCY      R             79REFFLD(CCY)
     A            PPRIN         18A       91
     A            PINTR         12A      112
      *-------------------------*
     A          R AS001005D3
      *-------------------------*
     A                                      SPACEB(001)
     A**************************************************************************
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R AS001005D4
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(1)
     A  37        PBRCD     R             +1REFFLD(BRCA)
     A  37                                +2'***'
      *
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(1)
