      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AS Hedges with Suspect Items Report')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001006P1 - Hedges with Suspect Items - Detail         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 226267  *CREATE    Date 23Feb04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  226267 - Hedges with Suspect Items                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R AS001006D1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS001006P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A  25                                40'WARNING - HEDGES WITH SUSPECT ITE-
     A                                      MS - FAIR VALUE'
     A                                      SPACEA(1)
      *
     A  26                                40'WARNING - HEDGES WITH SUSPECT ITE-
     A                                      MS - FOREIGN CURRENCY'
     A                                      SPACEA(1)
      *
     A  27                                40'WARNING - HEDGES WITH SUSPECT ITE-
     A                                      MS - CASH FLOW'
     A                                      SPACEA(1)
     A  25                                40'----------------------------------
     A                                      ---------------'
     A  25                                  SPACEA(2)
     A  26                                40'----------------------------------
     A                                      ---------------------'
     A  26                                  SPACEA(2)
     A  27                                40'----------------------------------
     A                                      --------------'
     A  27                                  SPACEA(2)
      *
     A  37                                 1'BRANCH: '
     A  37        PBRCD     R             +2REFFLD(BRCA)
     A  37        PBRNM     R             +2REFFLD(BRNM)
     A  37                                  SPACEA(2)
      *
     A                                     1'STRATEGY:'
     A                                      UNDERLINE
     A            PHESN         10A       +2
     A            PHELD         35A       +2
     A                                      SPACEA(2)
      *
     A                                     6'HEDGE '
     A                                    +2'      '
     A                                    +2'DERIVATIVE/'
     A                                    +2'BOOK'
     A                                    +2'CUSTOMER'
     A                                    +2'VALUE  '
     A                                    +2'MATURITY'
     A                                    +2'CURRENCY'
     A                                    +2'      PRINCIPAL'
     A                                    +3'     INTEREST'
      *
     A                                      SPACEA(1)
      *
     A                                     6'NUMBER'
     A                                    +2'MODULE'
     A                                    +2'HEDGED ITEM'
     A                                    +2'CODE'
     A                                    +2'NUMBER  '
     A                                    +2'DATE   '
     A                                    +2'DATE    '
     A                                    +2'CODE    '
     A                                    +2'         AMOUNT'
     A                                    +3'         RATE'
     A                                    +3'STATUS       '
     A                                      SPACEA(1)
      *
     A                                     6'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------+
     A                                       ---------------------'
      *
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R AS001006D2
     A                                      SPACEB(001)
     A            PHEDI          6A        6
     A            PMOD           2A       +4
     A            PDLNO         10A       22
     A            PBKC           2A       36
     A            PCNUM          6A       41
     A            PVALD          7A       51
     A            PMATD          7A       60
     A            PCCY      R             70REFFLD(CCY)
     A            PPRIN         18A       77
     A            PINTR         12A       99
     A            PSTAT         15A      114
      *
      *-------------------------*
      ***  Blank Line Format    *
      *-------------------------*
      *
     A          R AS001006D3
     A                                      SPACEB(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R AS001006D4
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(1)
     A  37        PBRCD     R             +1REFFLD(BRCA)
     A  37                                +2'***'
      *
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(1)
