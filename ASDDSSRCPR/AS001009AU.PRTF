      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas AS Derivatives w/o hedged trading audit')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standard Module                           *
      *                                                               *
      *  PRTF/AS001009AU - Derivatives without Hedged (Trading)       *
      *                     - Audit Report                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS001009AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    18'HEDGE DERIVATIVES WITH NO -
     A                                      HEDGED ITEMS (TRADING) +
     A                                        AS OF'
      **Start of change CAS004
     A**********                            UNDERLINE                                         CAS004
     A**********  ORPTDT    R             +0REFFLD(MRDT)                                      CAS004
     A**********                            UNDERLINE                                         CAS004
      **End of change CAS004
     A            ORPTDT    R             +1REFFLD(MRDT)
     A                                    +0' - AUDIT REPORT'
      **Start of change CAS004
     A**********                            UNDERLINE                                         CAS004
     A                                      SPACEA(1)
     A                                    18'---------------------------
     A                                      ----------------------------
     A                                      ------------------------'
      **End of change CAS004
     A                                      SPACEA(3)
     A                                    85'CALCULATED'
     A                                      SPACEA(1)
     A                                    85'----------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R TOTAUD
     A                                     6'TOTAL NO. OF HEDGE STRATEGY +
     A                                      RECORDS PROCESSED'
      *
     A            SCOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'TOTAL NO. OF HEDGE LINKAGE +
     A                                      RECORDS PROCESSED'
      *
     A            LCOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *====================================
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
