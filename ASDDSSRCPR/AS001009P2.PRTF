      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas AS Hedge deriv w/no hedged items - partial')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standard Module                           *
      *                                                               *
      *  PRTF/AS001009P2 - Hedge Derivatives with No Hedged Items     *
      *                     (Partial)- Detail Report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS002  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAS002 - Hedge Strategy/Linkage                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *----------------------------------*
      ***  Report Header Record Format 1 *
      *----------------------------------*
      *
     A          R HEADR2                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE AS001009P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
      ** Start of change CAS004
     A**25******                          18'PARTIAL HEDGE DERIVATIVES -                      CAS004
     A**********                            (TRADING PORTION) - -                             CAS004
      **********                            FAIR VALUE'                                       CAS004
     A**********                            UNDERLINE                                         CAS004
     A**26******                          18'PARTIAL HEDGE DERIVATIVES -                      CAS004
     A**********                            (TRADING PORTION) - -                             CAS004
      **********                            FOREIGN CURRENCY'                                 CAS004
     A**********                            UNDERLINE                                         CAS004
     A**27******                          18'PARTIAL HEDGE DERIVATIVES -                      CAS004
     A**********                            (TRADING PORTION) - -                             CAS004
      **********                            CASH FLOW'                                        CAS004
     A**********                            UNDERLINE                                         CAS004
     A  25                                27'PARTIAL HEDGE DERIVATIVES -
     A                                      (TRADING PORTION) - -
     A                                      FAIR VALUE'
     A  26                                27'PARTIAL HEDGE DERIVATIVES -
     A                                      (TRADING PORTION) - -
     A                                      FOREIGN CURRENCY'
     A  27                                27'PARTIAL HEDGE DERIVATIVES -
     A                                      (TRADING PORTION) - -
     A                                      CASH FLOW'
      *
     A                                    +2'AS OF'
     A            OEVCD          7A       +1
     A                                      SPACEA(2)
     A  25                                27'---------------------------
     A                                      ---------------------
     A                                      -------------------------'
     A  25                                  SPACEA(2)
     A  26                                27'---------------------------
     A                                      --------------------------
     A                                      --------------------------'
     A  26                                  SPACEA(2)
     A  27                                27'---------------------------
     A                                      --------------------------
     A                                      -------------------'
     A  27                                  SPACEA(2)
      *
     A**37******                          88'BRANCH -'                                        CAS004
     A**37******  O1BRCD    R             97REFFLD(BRCA)                                      CAS004
     A**37******  O1BRNM    R            101REFFLD(BRNM)                                      CAS004
     A**********                         132' '                                               CAS004
     A**********                            SPACEA(003)                                       CAS004
     A  37                                 1'BRANCH: '
     A  37        O1BRCD    R             +2REFFLD(BRCA)
     A  37        O1BRNM    R             +2REFFLD(BRNM)
     A  37                                  SPACEA(2)
      **End of change CAS004
      *
     A                                     1'STRATEGY:'
     A                                      UNDERLINE
     A            O1HESN        10A       +2
     A            O1HELD        35A       +2
     A                                      SPACEA(003)
      *
     A                                     1'BOOK'
     A                                     6'DERIVATIVE/'
     A                                    18'CUSTOMER'
     A                                    27'VALUE'
     A                                    33'MATURITY'
     A                                    66'INTEREST'
     A                                    79'FISCAL YEAREND'
     A                                   102'UNRL P/L'
     A                                      SPACEA(001)
      *
     A                                     1'CODE'
     A                                     6'HEDGED ITEM'
     A                                    20'NO.'
     A                                    27'DATE'
     A                                    33'DATE'
     A                                    41'CCY'
     A                                    50'PRINCIPAL'
     A                                    68'RATE'
     A                                    82'UNRL P/L'
     A                                   101'AS OF TODAY'
     A                                   123'CHANGE'
     A                                      SPACEA(001)
      *
     A                                    81'IN BASE CCY'
     A                                   101'IN BASE CCY'
     A                                   121'IN BASE CCY'
     A                                      SPACEA(001)
      *
     A                                     1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------+
     A                                       -----------------------------'
     A                                      SPACEA(001)
     A**************************************************************************
      *------------------------------*
      ***  Hedge Id Detail Header  ***
      *------------------------------*
     A          R DETAIL2H
     A                                      SPACEB(001)
     A                                     2'HEDGE NO.:'
     A                                      UNDERLINE
     A            OHEDI          6A       14
     A                                      SPACEA(001)
      *
      *------------------------------*
      ***     Hedge Id Details     ***
      *------------------------------*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            OBKC           2A        2
     A            ODLNO         10A        5
     A            ODTYP          2A       12
     A            ODASH          1A       14
     A            ODLST          2A       15
     A            OCNUM          6A       18
     A            OVALD          7A       25
     A            OMATD          7A       33
     A            OCCY      R             41REFFLD(CCY)
     A            OPRIN         18A       45
     A            OINTR         12A       63
     A            OYUPL         18A       76
     A            OURPL         18A       95
     A            ODIFF         18A      114
      *-----------------------------------------*
      ***  Deriv. Hedge Sub-Total Record Format *
      *-----------------------------------------*
     A          R LINKTOD2
     A                                    39'             DERIVATIVE SUB-TOTAL'
     A            ODYUPL        18A       76
     A            ODURPL        18A       95
     A            ODDIFF        18A      114
      *-------------------------*
      ***   Dotted Line       ***
      *-------------------------*
     A          R DOTTED2
     A                                      SPACEB(001)
     A                                     1'..................................+
     A                                       ..................................+
     A                                       ..................................+
     A                                       .............................'
     A                                      SPACEA(001)
      *-----------------------------------------*
      ***  Strategy Total Record Format (Deriv.)*
      *-----------------------------------------*
     A          R HSENTOD2
     A                                    39'        STRATEGY DERIVATIVE TOTAL'
     A            YUPLD1        18A       76
     A            URPLD1        18A       95
     A            DIFFD1        18A      114
     A                                      SPACEA(001)
      *--------------------------------------------*
      ***  Product Type Total Record Format (Deriv.)
      *--------------------------------------------*
     A          R PRTPTOD2
     A        25                          39'      FAIR VALUE DERIVATIVE TOTAL'
     A        26                          39'FOREIGN CURRENCY DERIVATIVE TOTAL'
     A        27                          39'       CASH FLOW DERIVATIVE TOTAL'
     A            YUPLD2        18A       76
     A            URPLD2        18A       95
     A            DIFFD2        18A      114
     A                                      SPACEA(001)
      *--------------------------------------*
      ***  Branch Total Record Format (Deriv.)
      *--------------------------------------*
     A          R BRCATOD2
     A                                    39'          BRANCH DERIVATIVE TOTAL'
     A            YUPLD3        18A       76
     A            URPLD3        18A       95
     A            DIFFD3        18A      114
     A                                      SPACEA(001)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
     A          R TRAILP2
     A                                      SPACEB(003)
     A  37                                46'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(001)
     A  37        O1BRCD    R             +1REFFLD(BRCA)
     A  37                                +2'***'
      *
     A N37                                51'***  END OF REPORT  ***'
      *-------------------------*
     A          R BLANK2
      *-------------------------*
     A                                      SPACEB(001)
     A                                     1' '
     A                                      SPACEA(001)
