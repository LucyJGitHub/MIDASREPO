      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AS Historic market data detail report')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001020P1 - Historic Market Data Detail Report         *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 225618             Date 30Mar04               *
      *                 CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  225618 - Change of 'Net Present Value' to 'Fair Value'.      *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A                                      INDARA
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE AS001020P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'HISTORIC MARKET DATA +
     A                                      LISTING'
     A                                      SPACEA(1)
     A                                    50'--------------------+
     A                                      --------'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL1
      *
     A                                      SPACEB(3)
     A                                     2'Hedge Strategy............:'
     A            RHGST         10        +2
     A                                      SPACEA(1)
     A                                     2'Hedge ID..................:'
     A            RHEDI          6        +2
     A                                    83'Effective Date............:'
     A            REFFD          7        +2
     A                                      SPACEA(2)
     A                                     2'Hedge Derivative Transaction +
     A                                      Number:'
     A            RDRVT          6        +2
      *
     A                                      SPACEA(1)
     A  21                                 2'Hedged Deal Number:'
     A  22                                 2'Hedged Loan Number:'
     A  21
     A  21
     AO 22        RDLNO          6        +2
      *
     A  23                                 2'Hedged Security Shortname:'
     A  23        RSESN         10        +2
     A  23                                +3'Branch Code:'
     A  23        RBRCA          3        +2
     A  23                                +3'Book Code:'
     A  23        RBKCD          2        +2
     A  23                                +3'Market Indicator:'
     A  23        RMKTI          1        +2
     A  23                                +3'Trade/Value Date Basis:'
     A  23        RBHTV          1        +2
      *
     A          R DETAIL2
      *
     A                                      SPACEB(2)
     A                                    41'Hedge Derivative'
     A                                    78'Hedged Item'
      *
     A                                      SPACEA(1)
     A                                    15'Value Date'
     A**Start of change 225618
     A**24******                          45'Net Present Value'                               225618
     A**25******                          38'Market Value Change'                             225618
     A  24                                48'Fair Value'
     A  25                                38'Fair Value Change'
     A**End of change 225618
     A  26                                35'Interest Rates/Market Price'
     A**Start of change 225618
     A**24******                          45'Net Present Value'                               225618
     A**25******                          38'Market Value Change'                             225618
     A  24                                84'Fair Value'
     A  25                                70'Fair Value Change'
     A**End of change 225618
     A  26                                70'Interest Rates/Market Price'
      *
     A          R DETAIL3
      *
     A                                      SPACEB(1)
     A            RVDAT         10        15
     A            RDMTV         21        36
     A            RHMTV         21        68
      *
      *
     A          R DETAIL4
      *
     A                                      SPACEB(2)
     A                                    34'*** NO MARKET DATA COLLECTED ***'
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A                                      SPACEB(3)
     A                                    51'***  END OF REPORT  ***'
