      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas AS Todays Regression Analysis Effectiveness')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001023AU - Hedge Effectiveness Audit Report using     *
      *                    Regression Analysis                        *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE AS001023AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    37'HEDGE EFFECTIVENESS REPORT +
     A                                        AS OF'
     A            ORPTDT    R             +1REFFLD(MRDT)
     A                                    +0' - AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    37'----------------------------
     A                                      ----------------------------'
     A                                      SPACEA(3)
     A                                    85'CALCULATED'
     A                                      SPACEA(1)
     A                                    85'----------'
     A                                      SPACEA(3)
      *
      ** File Control
      *
     A          R TOTAUD
     A                                     6'TOTAL NO. OF HEDGE STRATEGY +
     A                                      RECORDS PROCESSED'
      *
     A            SCOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'TOTAL NO. OF HEDGE LINKAGE +
     A                                      RECORDS PROCESSED'
      *
     A            LCOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'TOTAL NO. OF EFFECTIVE HEDGE +
     A                                       LINKAGE RECORDS'
      *
     A            ECOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'TOTAL NO. OF INEFFECTIVE HEDGE +
     A                                       LINKAGE RECORDS'
      *
     A            ICOUNT         7  0     87EDTCDE(3)
     A                                      SPACEA(2)
      *
      ** No Details
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
