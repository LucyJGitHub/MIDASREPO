      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AS Todays regression analysis - ineffective')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001023P2 - Midas AS Ineffective/Discontinued Hedges   *
      *                    Report using Regression Analysis           *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 225618             Date 30Mar04               *
      *                 225181             Date 20Feb04               *
      *                 CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  225618 - Change of 'Net Present Value' to 'Fair Value'.      *
      *  225181 - Dash (-) overlaps security short name on hedge      *
      *           reports                                             *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format 1
      *
     A          R HEADR2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE AS001023P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
      *
     A  25                                18'INEFFECTIVE/DISCONTINUED -
     A                                      HEDGES REPORT USING REGRESSION -
     A                                      ANALYSIS - FAIR VALUE'
     A  25                                +2'AS OF'
     A  25        OEVCD1         7A       +1
     A  25                                  SPACEA(1)
      *
     A  26                                18'INEFFECTIVE/DISCONTINUED -
     A                                      HEDGES REPORT USING REGRESSION -
     A                                      ANALYSIS - FOREIGN CURRENCY'
     A  26                                +2'AS OF'
     A  26        OEVCD2         7A       +1
     A  26                                  SPACEA(1)
      *
     A  27                                18'INEFFECTIVE/DISCONTINUED -
     A                                      HEDGES REPORT USING REGRESSION -
     A                                      ANALYSIS - CASH FLOW'
     A  27                                +2'AS OF'
     A  27        OEVCD3         7A       +1
     A  27                                  SPACEA(1)
      *
     A  25                                18'--------------------------
     A                                      --------------------------------
     A                                      ----------------------
     A                                      ---------------'
     A  25                                  SPACEA(2)
     A  26                                18'--------------------------
     A                                      --------------------------------
     A                                      ----------------------------
     A                                      ---------------'
     A  26                                  SPACEA(2)
     A  27                                18'--------------------------
     A                                      --------------------------------
     A                                      ---------------------
     A                                      ---------------'
     A  27                                  SPACEA(2)
      *
     A  37                                 1'BRANCH: '
     A  37        O1BRCD    R             +2REFFLD(BRCA)
     A  37        O1BRNM    R             +2REFFLD(BRNM)
     A  37                                  SPACEA(2)
      *
     A                                     1'STRATEGY:'
     A                                      UNDERLINE
     A            O1HESN        10A       +2
     A            O1HELD        35A       +2
     A                                      SPACEA(003)
      *
     A                                     1'BOOK'
     A                                     6'DERIVATIVE/'
     A                                    18'CUSTOMER'
     A                                    27'VALUE'
      **Start of change 225181
     A***********                         33'MATURITY'                                        225181
     A***********                         66'INTEREST'                                        225181
     A                                    34'MATURITY'
     A                                    67'INTEREST'
      **End of change 225181
     A                                    76'COEFFICIENT OF'
     A                                    97'T-STATISTIC/'
     A                                   125'SLOPE'
     A                                      SPACEA(001)
      *
     A                                     1'CODE'
     A                                     6'HEDGED ITEM'
     A                                    20'NO.'
     A                                    27'DATE'
      **Start of change 225181
     A***********                         33'DATE'                                            225181
     A***********                         41'CCY'                                             225181
     A***********                         50'PRINCIPAL'                                       225181
     A***********                         68'RATE'                                            225181
     A                                    34'DATE'
     A                                    42'CCY'
     A                                    51'PRINCIPAL'
     A                                    69'RATE'
      **End of change 225181
     A                                    76'DETERMINATION'
     A                                    97'CRITICAL TVAL'
     A                                   122'COEFFICIENT'
     A                                      SPACEA(001)
      *
     A                                     1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------+
     A                                       -----------------------------'
     A                                      SPACEA(001)
      *
      ** Hedge Id Detail Header
      *
     A          R DETAIL2H
     A                                      SPACEB(001)
     A                                     2'HEDGE NO.:'
     A                                      UNDERLINE
     A            OHEDI          6A       14
     A N11        ORSQR         12A       76
     A N11        OTSTA         21A       92
     A N11        OSLCO         18A      115
     A N11                                  SPACEA(001)
     A N11        OTVAL         11A      102
     A  11                                85'***  Insufficient data +
     A                                       to be regressed.  ***'
     A                                      SPACEA(001)
      *
      ** Hedge Id Details
      *
     A          R DETAIL2
     A                                      SPACEB(002)
     A            OBKC           2A        2
     A            ODLNO         10A        5
      **Start of change 225181
     A**********  ODTYP          2A       12                                                  225181
     A**********  ODASH          1A       14                                                  225181
     A**********  ODLST          2A       15                                                  225181
     A**********  OCNUM          6A       18                                                  225181
     A**********  OVALD          7A       25                                                  225181
     A**********  OMATD          7A       33                                                  225181
     A**********  OCCY      R             41REFFLD(CCY)                                       225181
     A**********  OPRIN         18A       45                                                  225181
     A**********  OINTR         12A       63                                                  225181
     A**********                          79'VALUE DATE'                                      225181
     A**********                          94'NET PRESENT/MKT VAL'                             225181
     A            ODTYP          2A       13
     A            ODASH          1A       15
     A            ODLST          2A       16
     A            OCNUM          6A       19
     A            OVALD          7A       26
     A            OMATD          7A       34
     A            OCCY      R             42REFFLD(CCY)
     A            OPRIN         18A       46
     A            OINTR         12A       64
     A                                    80'VALUE DATE'
     A**Start of change 225618
     A**********                          95'NET PRESENT/MKT VAL'                             225618
     A                                   103'FAIR VALUE'
     A**End of change 225618
      **End of change 225181
      *
      ** Historic Market Data - Hedge Derivative
      *
     A          R DETAIL2A
      *
     A                                      SPACEB(001)
     A            RVDAT         10        78
     A            RDMTV         21        92
      *
      ** Historic Market Data - Hedge Item
      *
     A          R DETAIL2B
      *
     A                                      SPACEB(001)
     A            RVDAT         10        78
     A            RHMTV         21        92
      *
      ** Dotted Line
      *
     A          R DOTTED2
     A                                      SPACEB(001)
     A                                     1'..................................+
     A                                       ..................................+
     A                                       ..................................+
     A                                       .............................'
     A                                      SPACEA(001)
      *
      ** Report Trailer
      *
     A          R TRAILP2
     A                                      SPACEB(003)
     A  37                                46'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(001)
     A  37        O1BRCD    R             +1REFFLD(BRCA)
     A  37                                +2'***'
      *
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(001)
      *
     A          R BLANK2
      *
     A                                      SPACEB(001)
     A                                     1' '
     A                                      SPACEA(001)
