      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas AS Unreplaced Forecast Xaction Report Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001024AU - Midas Unreplaced Forecast Transactions     *
      *                    Audit Report                               *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS006  *CREATE    Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(3)
      *
     A                                     2'REFERENCE AS001024AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                   101'TIME'
     A                                      SPACEB(1)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                    41'UNREPLACED FORECAST TRANSACTIONS +
     A                                      AUDIT REPORT'
     A                                      SPACEB(1)
     A                                    41'---------------------------------+
     A                                      ------------'
     A                                      SPACEB(1)
      *
     A          R TOTAUD                    TEXT('Final totals             ')
     A                                      SPACEB(2)
      *
     A                                    64'Audit Control'
     A                                    53'No of Forecast Transactions..:'
     A                                      SPACEB(2)
     A            RFRTNO         5  0     84TEXT('No of Forecast Transactions')
     A                                      EDTCDE(3)
     A                                    53'No of Hedge Items............:'
     A                                      SPACEB(1)
     A            RHEDGE         5  0     84TEXT('No of Hedge Items Processed')
     A                                      EDTCDE(3)
     A                                    53'No of Derivatives............:'
     A                                      SPACEB(1)
     A            RFRACT         5  0     84TEXT('No of Derivatives Processed')
     A                                      EDTCDE(3)
      *
     A          R NODTLS                    TEXT('No Records Found')
     A                                      SPACEB(2)
      *
     A                                    57'*** NO DETAILS TO REPORT ***'
      *
     A          R ENDRPRT                   TEXT('No Records Found')
      *
     A                                      SPACEB(3)
     A                                    60'*** END OF REPORT ***'
      *
     A          R DBERROR                   TEXT('Database Error')
     A                                      SPACEB(4)
      *
     A                                    45'DATABASE ERROR '
     A            DBASE          3  0     60
     A                                    64'IN PROGRAM'
     A            DBPGM         10A       75
     A                                      SPACEA(2)
     A                                    45'FILE '
     A            DBFILE         8A       50
     A                                    65', KEY '
     A            DBKEY         29A       67
      *
