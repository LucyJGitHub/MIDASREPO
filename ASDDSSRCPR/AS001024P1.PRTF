      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas AS Unreplaced Forecast Xaction Report')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS001024P1 - Midas Unreplaced Forecast Transactions     *
      *                    Report                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS006 *CREATE     Date 21Jan03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS006 - Hedge Accounting Phase B                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A                                      INDARA
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     1'REFERENCE AS001024P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'UNREPLACED FORECAST TRANSACTIONS +
     A                                      REPORT'
     A                                      SPACEA(1)
     A                                    43'---------------------------------+
     A                                      ------'
     A                                      SPACEA(1)
      *
     A          R DETAIL1
      *
     A                                      SPACEB(003)
     A                                     2'Forecast Transaction ID...:'
     A            RTRID          6        30
     A            RDESC         30        40
     A                                      SPACEA(001)
     A                                     2'Maturity Date.............:'
     A            RMDAT          7        30
     A                                    40'Module ID.............:'
     A            RMOD           2        64
     A                                    70'Underlying Reference No...:'
     A  61                                98'** NOT DEFINED **'
     A N61        RUREF          6        98
      *
     A          R DETAIL1A
     A                                      SPACEB(002)
     A                                    10'Hedge ID...:'
     A            RHGID          6        23
      *
     A          R DETAIL2
     A                                      SPACEB(002)
     A                                    15'Hedge Item'
     A                                    45'Percentage'
      *
     A          R DETAIL2A
     A                                      SPACEB(001)
     A            RDMOD          2        20
     A            RDDNO          6        32
     A            RPCTG          7        48
      *
      *
     A          R DETAIL3
     A                                      SPACEB(002)
     A                                    15'Hedge Derivatives'
     A                                    45'Percentage'
      *
     A          R DETAIL3A
     A                                      SPACEB(001)
     A            RDMOD          2        20
     A            RDDNO          6        32
     A            RPCTG          7        48
      *
     A          R NODETL
     A                                      SPACEB(3)
     A                                    49'***  NO DETAILS TO +
     A                                      REPORT  ***'
      *
     A          R TRAILP1
     A                                      SPACEB(2)
      *
     A                                    54'***  END OF REPORT  ***'
