      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Global Average Spot Rates Report - Global')
      *****************************************************************
      *                                                               *
      *  Midas - IAS21 Module                                         *
      *                                                               *
      *  PRTF/AS002400P1 - Midas Global Average Spot Rates Report     *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS014  *CREATE    Date 10Aug05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS014 - IAS21 The Effects of Changes in Foreign Exchange    *
      *           Rates                                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
      *
     A                                      SKIPB(001)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE AS002400P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    51
     A                                      'TODAYS GLOBAL AVERAGE SPOT RATES'
     A                                      SPACEB(002)
     A                                     6
     A                                      'GLOBAL'
     A                                      SPACEB(002)
     A                                    21
     A                                      'Average Spot Rate'
     A                                      SPACEB(002)
     A                                    45
     A                                      'Period'
     A                                    62
     A                                      'Period'
     A                                    77
     A                                      'Period'
     A                                    93
     A                                      'Total'
     A                                   108
     A                                      'Next Period'
     A                                     6
     A                                      'Currency'
     A                                      SPACEB(001)
     A                                    25
     A                                      'To Date'
     A                                    44
     A                                      'Frequency'
     A                                    60
     A                                      'Start Date'
     A                                    76
     A                                      'End Date'
     A                                    91
     A                                      'Spot Days'
     A                                   108
     A                                      'Start Date'
      *
      *---------------------------*
      ***  Detail Record Format   *
      *---------------------------*
      *
     A          R DETAIL1
     A                                      SPACEB(001)
     A            RCUR1          3A  O     8
     A            RAVE1         20S 9O    19
     A                                      EDTCDE(4)
     A            RFRQ1          2A  O    47
     A            RSTR1          7A  O    61
     A            REND1          7A  O    76
     A            RTSPT1         5S 0O    93
     A                                      EDTCDE(4)
     A            RNXT1          7A  O   111
      *
      *--------------------------*
      ***  Report Trailer Format *
      *--------------------------*
      *
     A          R ENDREP1
     A                                      SPACEB(001)
     A                                    53
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
