      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Global Average Spot Rates Take On Processing')
      *****************************************************************
      *                                                               *
      *  Midas - IAS21 Module                                         *
      *                                                               *
      *  PRTF/AS002401P1 - Midas Global Average Spot Rates            *
      *                    Take On Processing                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS014  *Create    Date 10Aug05               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS014 - IAS21 The Effects of Changes in Foreign Exchange    *
      *           Rates                                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **********************************
      ***  Report Header Record Format *
      **********************************
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
      *
     A                                      SKIPB(001)
     A                                      SPACEA(001)
     A                                     6
     A                                      'REFERENCE AS002401P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    39
     A                                      'INITIALISED GLOBAL CURRENCIES AVER-
     A                                      AGE SPOT RATES'
     A                                      SPACEB(002)
     A                                     6
     A                                      'GLOBAL'
     A                                      SPACEB(002)
     A                                    21
     A                                      'Average Spot Rate'
     A                                      SPACEB(002)
     A                                    45
     A                                      'Period'
     A                                    62
     A                                      'Period'
     A                                    77
     A                                      'Period'
     A                                    91
     A                                      'Next Period'
     A                                   108
     A                                      'Change'
     A                                     6
     A                                      'Currency'
     A                                      SPACEB(001)
     A                                    25
     A                                      'To Date'
     A                                    44
     A                                      'Frequnecy'
     A                                    60
     A                                      'Start Date'
     A                                    76
     A                                      'End Date'
     A                                    91
     A                                      'Start Date'
     A                                   109
     A                                      'Type'
      *
      **********************************
      ***  Report Detail Record Format *
      **********************************
      *
     A          R DETAIL1
     A                                      SPACEB(001)
     A            RCUR           3A  O     8
     A            RAVE          20S 9O    19
     A                                      EDTCDE(4)
     A            RFRQ           2A  O    47
     A            RSTR           7A  O    61
     A            REND           7A  O    77
     A            RNXT           7A  O    93
     A            RTYP           1A  O   111
      *
      **********************************
      ***  End of Report Record Format *
      **********************************
      *
     A          R ENDREP1
     A                                      SPACEB(001)
     A                                    53
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(001)
