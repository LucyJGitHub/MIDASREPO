/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Zone Ave Spot Rates TakeOn Procssg Report')
      *****************************************************************
      *                                                               *
      *  Midas - Accounting Standards Module                          *
      *                                                               *
      *  PRTF/AS002403P1 - Midas Zone Average Spot Rates Take On      *
      *                    Processing Report                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS014  *Create    Date 10Aug05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS014 - IAS21 The Effects of Changes in Foreign Exchange    *
      *           Rates                                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE AS002403P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'INITIALISED ZONE AVERAGE SPOT RATE-
     A                                      S'
     A                                      SPACEA(1)
      *
     A                                    46'-----------------------------------
     A                                      -'
     A                                      SPACEA(3)
     A                                     2'Zone:'
     A            RZONE         10A  O     8TEXT('Zone')
     A                                      SPACEA(3)
     A                                    17'Average Spot Rate'
     A                                    43'Period'
     A                                    60'Period'
     A                                    79'Period'
     A                                    96'Next Period'
     A                                   116'Change'
     A                                      SPACEA(1)
     A                                     2'Currency'
     A                                    21'To Date'
     A                                    42'Frequency'
     A                                    58'Start Date'
     A                                    78'End Date'
     A                                    97'Start Date'
     A                                   117'Type'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format   *
      *---------------------------*
      *
     A          R DETAIL1
     A            RCUR           3A  O     4TEXT('Currency')
     A            RAVE          20S 9O    14TEXT('Ave Spot Rate')
     A                                      EDTCDE(4)
     A            RFREQ          2A  O    45TEXT('Frequency')
     A            RSTR           7A  O    59TEXT('Start Date')
     A            REND           7A  O    78TEXT('End Date')
     A            RNXT           7A  O    98TEXT('Next Start Date')
     A            RCHG           1A  O   118TEXT('Change Type')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Report Trailer Format *
      *--------------------------*
      *
     A          R TRAILP1
     A                                     2'Total No. of Records Initialised:'
     A                                      SPACEB(3)
     A            RTOT           8S 0O    36TEXT('Total')
     A                                      EDTCDE(3)
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
