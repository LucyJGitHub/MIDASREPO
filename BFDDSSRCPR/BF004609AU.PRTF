      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas COB Extraction of Teller Transactions Audit')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/BF004609AU - Midas COB Extraction of Teller             *
      *                    Transactions Record Audit                  *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CRE072  *CREATE    Date 30Jun11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE072 - BankFusion Midas Teller Related Changes             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Report Header Record Format
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BF004609AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'COB EXTRACTION OF TELLER +
     A                                      TRANSACTIONS - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    42'-------------------------+
     A                                      --------------------'
     A                                      SPACEA(3)
      *
      ** File Control
      *
     A          R CONTROL
      *
     A                                    20'Total no. of records +
     A                                      extracted from RATM'
     A            RBFRTM         5  0     70EDTCDE(3)
     A                                      SPACEA(1)
     A                                    20'Total no. of records +
     A                                      extracted from OPAY'
     A            RBFOPY         5  0     70EDTCDE(3)
     A                                      SPACEA(1)
      *
      ** No Details
      *
     A          R NODTLS
     A                                    49'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      ** Database Error
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
