      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas COB Extraction of Teller Transactions')          *
      *****************************************************************
      *                                                               *
      *  Midas - BankFusion Module                                    *
      *                                                               *
      *  BF004609P1 - Midas COB Extraction of Teller Transactions     *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CRE072  *CREATE    Date 30Jun11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE072 - BankFusion Midas Teller Related Changes             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BF004609P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'COB EXTRACTION OF TELLER +
     A                                      TRANSACTIONS'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    45'-------------------------+
     A                                      ------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     6'Midas Transaction'
     A                                    25'Teller Transaction'
     A                                    47'Front Office ID'
     A                                    72'API'
     A                                    83'Type'
     A                                    88'Subtype'
     A                                    96'Action'
     A                                   112'Timestamp'
     A                                      SPACEA(1)
      *
     A                                    10'Reference'
     A                                    29'Reference'
     A                                      SPACEA(1)
     A                                     6'-----------------'
     A                                    24'--------------------'
     A                                    45'--------------------'
     A                                    66'----------------'
     A                                    83'----'
     A                                    88'-------'
     A                                    96'------'
     A                                   103'--------------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RMREF         15         6
     A            RTREF         20        24
     A            RFRNT         20        45
     A            RAPI          16        66
     A            RTTYP          2        83
     A            RSTYP          5        88
     A            RCHTP          6        96
     A            RTMST         23       103
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
