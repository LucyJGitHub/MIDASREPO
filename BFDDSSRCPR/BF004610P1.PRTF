      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Purge BF Teller Journal Extension')              *
      *****************************************************************
      *                                                               *
      *  Midas - BankFusion Module                                    *
      *                                                               *
      *  BF004610P1 - Purge BF Midas Teller Journal Extension         *
      *               Records                                         *
      *                                                               *
      *  (c) Finastra International Limited 2010                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CRE071  *CREATE    Date 05Oct10               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE071 - BankFusion Midas Teller CBS related changes         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2010')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BF004610P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'PURGED BF TELLER JOURNAL EXTENSION'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    50'--------------------+
     A                                      -------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    12'Teller Transaction'
     A                                    41'Front Office ID'
     A                                    75'API Name'
     A                                   100'Stage'
     A                                      SPACEA(1)
      *
     A                                    12'Reference'
     A                                    41'---------------'
     A                                    75'--------'
     A                                   100'-----'
     A                                      SPACEA(1)
     A                                    12'------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RTTRE         22        12
     A            RFOID         22        41
     A            RAPI           6        75
     A            RSTAGE         3       100
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
