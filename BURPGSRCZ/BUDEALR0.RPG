     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BU Background Update of Citydealer DL ICD')
      *****************************************************************
      *                                                               *
      *  Midas - Background Updates
      *                                                               *
      *  BUDEALR0 - Background Update of Dealing ICD for Citydealer   *
      *                                                               *
      *  Function:  This program is called by DL0003 whenever the     *
      *  (I/C Term) Citydealer Transmission Delay Period has expired  *
      *             in order to update the details with the date and  *
      *             time of transmission for the Citydealer trans-    *
      *             mission which has just been requested.            *
      *             This program is also called by DL0002 and DL0008. *
      *                                                               *
      *  Called By: DL0002, DL0003 and DL0008.                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCM002 *CREATE     DATE 04SEP95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCM002 - Midas/Citydealer Interface (Phases V and VI)        *
      *                                                               *
      *****************************************************************
     FSDDEALL0UF  E           K        DISK         KINFSR *PSSR
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N    O F    I N D I C A T O R S                *
     F*                                                               *
     F*  10         Chain Fails for LF/SDDEALL0                       *
     F*  U7-U8      Database Error                                    *
     F*  LR         Exit program                                      *
     F*                                                               *
     F*****************************************************************
      /TITLE SUBROUTINE INDEX
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E    I N D E X                             *
     F*                                                               *
     F*  Main    -  Main processing                                   *
     F*  *PSSR   -  Program exception error routine                   *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E*
     E** Array containing Copyright statement
     E*
      /SPACE 3
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Data Structure for Message Data parameter
      *
     I            DS
     I                                        1 256 PMSGD
     I                                        1   8 DBFILE
     I                                        9  37 DBKEY
     I                                       38  47 DBPGM
     I                                       48  50 DBASE
     I*
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     IDSFDY     E DSDSFDY
      ** Short Data Structure
      *
      /EJECT
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C** Access the Bank Details.
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANKS
     C*
     C                     MOVEL'001'     DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     MOVEL'BUDEALR0'DBPGM
     C*
     C** Send database error message to MOPERQ MRUNQ
     C*
     C                     CALL 'DLC0004'
     C                     PARM 'MIDAS'   PMSGF  10
     C                     PARM 'MEM0011' PMSGID  7
     C                     PARM           PMSGD
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C** Get the current time.
     C*
     C                     TIME           WTIME   60
     C                     MOVEL'DEAL'    WKEY   10
     C*
     C** Access the Dealing ICD Details File.
     C*
     C           WKEY      CHAINSDDEALL0             10
     C*
     C** If record is found, update the file.
     C*
     C           *IN10     IFEQ *OFF
     C                     Z-ADDBJRDNB    BNLTMD
     C                     MOVELWTIME     BNLTMT
     C                     UPDAT@BNREEH
     C                     ELSE
     C*
     C                     MOVEL'002'     DBASE            ***************
     C                     MOVEL'SDDEALPD'DBFILE           *DB ERROR 002 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     MOVEL'BUDEALR0'DBPGM
     C*
     C** Send database error message to MOPERQ MRUNQ
     C*
     C                     CALL 'DLC0004'
     C                     PARM 'MIDAS'   PMSGF  10
     C                     PARM 'MEM0011' PMSGID  7
     C                     PARM           PMSGD
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     MOVE '1'       *INLR
      /EJECT
     C*
     C*****************************************************************
     C*                                                               *
     C* *PSSR  - Program exception error routine                      *
     C*          Called automatically if a program error occurs,      *
     C*          or directly by the program code using EXSR.          *
     C*          This subroutine DUMPs the program just once.         *
     C*                                                               *
     C* Called by: Anywhere                                           *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR SR **
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C*****************************************************************
**  CPY@
(c) Finastra International Limited 2001
