      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BU Bgnd updt new C/pty Nostro(SDCNSTPD)')
     F********************************************************************
     F*                                                                  *
     F*  Midas - STANDING DATA MODULE
     F*                                                                  *
     F*     CPNOSTRO - BACKGROUND UPDATE OF COUNTERPARTY NOSTRO FILE     *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. LN0583             Date 02Aug90               *
      *                 LN0565             Date 30Jul90               *
     F*                    S01230             DATE 07/11/89              *S01230
     F*                                                                  *
     F********************************************************************
      *  MD046248 - Finastra Rebranding                               *
     F*     LN0583 - SYNON FILE CONTROLS UPDATED TO REFLECT DATA         *
     F*              IN SDCNSTPD                                         *
     F*     LN0565 - COPYRIGHT AND TITLES UPDATE FOR RELEASE 10          *
     F*     S01230 - STANDARDISE PROGRAMS                                *S01230
     F********************************************************************S01230
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       30      CHAIN CPNOSTRO FAILED/ERROR                        *
     F*       31      ERROR ON CPNOSTRO FILE                             *
     F*                                                                  *
     F********************************************************************
     FSDCNSTL0UF  E           K        DISK                      A
     F                                              KCOMIT
     FSDFCTLL0UF  E           K        DISK                               LN0583
     F                                              KCOMIT                LN0583
      /EJECT
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
     E********************************************************************S01230
      /EJECT                                                              S01230
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*
      /EJECT
     C*
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7        RETURN CODE
     C                     PARM           WCPSH   8        CP NOSTRO CODE
     C                     PARM           WCPNM  35        CP NOSTRO NAME
     C                     PARM           WCPTN  20        CP NOSTRO TOWN E
     C                     PARM           WCSN1   7        CUS SHORTNAME1
     C                     PARM           WCSN2   3        CUS SHORTNAME2
     C                     PARM           WLCD    50       LAST CHANGE DATE
     C                     PARM           WCHTP   1        MIDAS CHANGE TP
     C*
      /EJECT
     C****************************************************************
     C*
     C*   M A I N L I N E   P R O G R A M
     C*
     C****************************************************************
     C*
     C* IF RECORD HAS BEEN INSERTED, CHAIN TO CP NOSTRO TO CHECK THAT
     C* RECORD DOES NOT ALREADY EXIST ON THIS FILE
     C*
     C           WCHTP     IFEQ 'I'
     C           WCPSH     CHAINSDCNSTL0             30    CHAIN FAILED
     C*
     C* IF RECORD DOES NOT EXIST
     C* THEN MOVE PARAMETERS RECEIVED INTO FILE FIELDS AND WRITE
     C* A NEW RECORD
     C*
     C           *IN30     IFEQ '1'
     C*
     C*********************MOVE WCPSH     AWARCD                          S01230
     C                     MOVE WCPSH     AWCPNC                          S01230
     C*********************MOVE WCPNM     AWAWTX                          S01230
     C                     MOVE WCPNM     AWCPNM                          S01230
     C*********************MOVE WCPTN     AWAXTX                          S01230
     C                     MOVE WCPTN     AWCNTN                          S01230
     C*********************MOVE WCSN1     AWAYTX                          S01230
     C                     MOVE WCSN1     AWCSN1                          S01230
     C*********************MOVE WCSN2     AWAZTX                          S01230
     C                     MOVE WCSN2     AWCSN2                          S01230
     C*********************MOVE WCHTP     AWADST                          S01230
     C                     MOVE WCHTP     AWTYLC                          S01230
     C*********************Z-ADDWLCD      AWAAVA                          S01230
     C                     Z-ADDWLCD      AWLCD                           S01230
     C*
     C* IF RECORD FOUND ON FILE ,  SET ERROR
     C*
     C                     ELSE
     C*
     C                     SETON                     31
     C                     END
     C*
     C           *IN30     IFEQ '1'
     C                     WRITE@AWREDM                31   ERROR
     C                     END
     C*
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT AN INSERT
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN CHANGED, RETRIEVE RECORD FROM FILE
     C*
     C           WCHTP     IFEQ 'A'
     C           WCPSH     CHAINSDCNSTL0             30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, MOVE RECEIVED PARAMETERS INTO FILE FIELDS
     C* AND UPDATE
     C*
     C           *IN30     IFEQ '0'
     C*********************MOVE WCPSH     AWARCD                          S01230
     C                     MOVE WCPSH     AWCPNC                          S01230
     C*********************MOVE WCPNM     AWAWTX                          S01230
     C                     MOVE WCPNM     AWCPNM                          S01230
     C*********************MOVE WCPTN     AWAXTX                          S01230
     C                     MOVE WCPTN     AWCNTN                          S01230
     C*********************MOVE WCSN1     AWAYTX                          S01230
     C                     MOVE WCSN1     AWCSN1                          S01230
     C*********************MOVE WCSN2     AWAZTX                          S01230
     C                     MOVE WCSN2     AWCSN2                          S01230
     C*********************MOVE WCHTP     AWADST                          S01230
     C                     MOVE WCHTP     AWTYLC                          S01230
     C*********************Z-ADDWLCD      AWAAVA                          S01230
     C                     Z-ADDWLCD      AWLCD                           S01230
     C                     UPDAT@AWREDM                30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE, SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C*
     C* RECORD IS NOT A CHANGED RECORD
     C*
     C                     END
     C*
     C* IF RECORD IS TO BE DELETED FROM OLD FILE, DELETE IT FROM
     C* NEW FILE IF IT EXISTS
     C*
     C           WCHTP     IFEQ 'D'
     C           WCPSH     CHAINSDCNSTL0             30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, DELETE RECORD FROM THE NEW FILE
     C*
     C           *IN30     IFEQ '0'
     C                     DELET@AWREDM                31  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND, TAKE NO FURTHER ACTION
     C*
     C           *IN30     IFEQ '1'
     C                     GOTO END
     C                     END
     C*
     C* IF ERROR OCCURRED ON DELETE SET ERROR PARAMETER
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C**  UPDATE SYNON FILE CONTROLS                                      LN0583
     C*                                                                   LN0583
     C                     MOVEL'SDCNSTPD'KEY    10                       LN0583
     C           KEY       CHAIN@AHREAU              30                   LN0583
     C*                                                                   LN0583
     C           *IN30     IFEQ '1'                                       LN0583
     C                     MOVE '*ERROR*' P0RTN                           LN0583
     C                     ELSE                                           LN0583
     C*                                                                   LN0583
     C           WCHTP     IFEQ 'I'                                       LN0583
     C                     ADD  1         AHNOIN                          LN0583
     C                     ADD  1         AHNORC                          LN0583
     C                     END                                            LN0583
     C*                                                                   LN0583
     C           WCHTP     IFEQ 'A'                                       LN0583
     C                     ADD  1         AHNOAM                          LN0583
     C                     END                                            LN0583
     C*                                                                   LN0583
     C           WCHTP     IFEQ 'D'                                       LN0583
     C                     ADD  1         AHNODL                          LN0583
     C                     SUB  1         AHNORC                          LN0583
     C                     END                                            LN0583
     C*                                                                   LN0583
     C                     UPDAT@AHREAU                31                 LN0583
     C*                                                                   LN0583
     C           *IN31     IFEQ '1'                                       LN0583
     C                     MOVE '*ERROR*' P0RTN                           LN0583
     C                     END                                            LN0583
     C*                                                                   LN0583
     C                     END                                            LN0583
     C*
     C           END       TAG
     C                     SETON                     LR
     C                     RETRN
     C*
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
