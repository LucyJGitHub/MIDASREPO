      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BU Update BOOKDA Execute User Program')
     F********************************************************************
     F*                                                                  *
     F*  Midas STANDING DATA MODULE                                   S01230
     F*                                                                  *
     F*     SDBOOKDD - BACKGROUND UPDATE OF BOOKD FILE                   *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *     Last Amend No. CSE006             DATE 11May99            *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     Prev Amend No. S01230             DATE 07/11/89              *S01230
     F*                                                                  *
     F********************************************************************
     F*  CSE006 - Repurchase Agreement (REPOS) enhancement.           *
     F*           New field will be displayed                         *
     F*     S01230 - STANDARDISE PROGRAMS                                *S01230
     F********************************************************************S01230
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       30      CHAIN BOOKD FAILED/ERROR                           *
     F*       31      ERROR ON BOOKD  FILE                               *
     F*       32      CHAIN BOOKDA FAILED/ERROR                          *
     F*                                                                  *
     F********************************************************************
     FBOOKD   UF  E           K        DISK         KCOMIT       A
     FBOOKDA  UF  E           K        DISK         KCOMIT
      /EJECT
     E* Description     : Copyright notice for inclusion in all programs
     E*
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
     E********************************************************************S01230
      /EJECT                                                              S01230
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*                                                                   S01230
      /EJECT                                                              S01230
     C*
     C*  DEFINE KEY TO BOOKD FILE
     C*
     C           KEY       KLIST
     C                     KFLD           BOKC
     C*
     C*
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7        RETURN CODE
     C                     PARM           WBOKC   2        BOOK CODE
     C                     PARM           WBOKD  30        BOOK DESC.
     C                     PARM           WHDGI   1        HEDGE IND.
     C                     PARM           WAUCI   1        AUTOCLOSURE IND
     C                     PARM           WBOCC   1        CONSOL. CODE
     C                     PARM           WSTAD   1        ST AMORT. DISC.
     C                     PARM           WSTRM   1        REVAL MARK/MKT.
     C                     PARM           WSTEP   30       ST EXPOSURE %
     C                     PARM           WINAK   1        INCLUDE IN A/C
     C                     PARM           WCHTP   1        LAST CHANGE TYPE
     C                     PARM           WLCD    50       LAST CHANGE DATE
     C                     PARM           WBYSL   1        Buy-Sell Book  CSE006
     C*
      /EJECT
     C****************************************************************
     C*
     C*   M A I N L I N E   P R O G R A M
     C*
     C****************************************************************
     C*
     C*
     C* IF RECORD HAS BEEN INSERTED, CHAIN TO BOOKD
     C* RECORD DOES NOT ALREADY EXIST ON THIS FILE
     C*
     C           WCHTP     IFEQ 'I'
     C                     MOVE WBOKC     BOKC
     C           KEY       CHAINBOOKDDF              30    CHAIN FAILED
     C*
     C* IF RECORD DOES NOT EXIST
     C* THEN MOVE PARAMETERS RECIEVED INTO FILE FIELDS AND WRITE
     C* A NEW RECORD
     C*
     C           *IN30     IFEQ '1'
     C*
     C*  IF IT DOES EXIST AS DELETED (RECI *)
     C*  THEN MOVE PARAMETERS RECEIVED INTO FILE FIELDS AND UPDATE
     C*  OLD RECORD
     C*
     C           *IN30     OREQ '0'
     C           RECI      ANDEQ'*'
     C                     MOVE 'D'       RECI
     C                     MOVE WBOKC     BOKC
     C                     MOVE WBOKD     BOKD
     C                     MOVE WHDGI     HDGI
     C                     Z-ADDWLCD      LCD
     C                     MOVE WCHTP     CHTP
     C                     MOVE WAUCI     AUCI
     C                     MOVE WBOCC     BOCC
     C                     MOVE WSTAD     STAD
     C                     MOVE WSTRM     STRM
     C                     MOVE WINAK     INAK
     C                     Z-ADDWSTEP     STEP
     C                     MOVE WBYSL     BYSL                            CSE006
     C*
     C* IF RECORD FOUND ON FILE AND IS NOT A DELETED RECORD SET ERROR
     C*
     C                     ELSE
     C*
     C                     SETON                     31
     C                     END
     C*
     C           *IN30     IFEQ '1'
     C                     WRITEBOOKDDF                31  ERROR
     C                     END
     C*
     C           *IN30     IFEQ '0'
     C           *IN31     ANDEQ'0'
     C                     UPDATBOOKDDF                31  ERROR
     C                     END
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT AN INSERT
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN CHANGED, RETRIEVE RECORD FROM FILE
     C*
     C           WCHTP     IFEQ 'A'
     C                     MOVE WBOKC     BOKC
     C           KEY       CHAINBOOKDDF              30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, MOVE RECEIVED PARAMETERS INTO FILE FIELDS
     C* AND UPDATE
     C*
     C           *IN30     IFEQ '0'
     C                     MOVE WBOKC     BOKC
     C                     MOVE WBOKD     BOKD
     C                     MOVE WHDGI     HDGI
     C                     Z-ADDWLCD      LCD
     C                     MOVE WCHTP     CHTP
     C                     MOVE WAUCI     AUCI
     C                     MOVE WBOCC     BOCC
     C                     MOVE WSTAD     STAD
     C                     MOVE WSTRM     STRM
     C                     MOVE WINAK     INAK
     C                     Z-ADDWSTEP     STEP
     C                     MOVE WBYSL     BYSL                            CSE006
     C                     UPDATBOOKDDF                30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT A CHANGED RECORD
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN DELETED FROM THE NEW FILE, RETRIEVE IT
     C* FROM BOOKD
     C*
     C           WCHTP     IFEQ 'D'
     C                     MOVE WBOKC     BOKC
     C           KEY       CHAINBOOKDDF              30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, UPDATE RELEVANT FIELDS (WE DON'T ACTUALLY
     C* DELETE ANY RECORDS FROM THE OLD FILES)
     C*
     C           *IN30     IFEQ '0'
     C                     MOVE '*'       RECI
     C                     MOVE WCHTP     CHTP
     C                     Z-ADDWLCD      LCD
     C                     UPDATBOOKDDF                30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS HAVE OCCURRED ON STDSIDD UPDATE
     C*
     C           P0RTN     IFEQ *BLANKS
     C*
     C*  READ HEADER RECORD
     C*
     C                     READ BOOKDAF                  32  EOF
     C                     END
     C*
     C*
     C* IF NO ERRORS
     C*
     C           *IN32     IFEQ '0'
     C*
     C* UPDATE AS APPROPRIATE THE NUMBER OF INSERTS,AMENDS OR DELETES
     C* ON THE TRAILER FILE
     C*
     C           WCHTP     IFEQ 'I'
     C                     ADD  1         NINS
     C                     END
     C*
     C           WCHTP     IFEQ 'A'
     C                     ADD  1         NAMD
     C                     END
     C*
     C           WCHTP     IFEQ 'D'
     C                     ADD  1         NDEL
     C                     END
     C*
     C                     UPDATBOOKDAF                32    ERROR
     C*
     C                     END
     C*
     C* CHECK FOR ANY ERRORS AND SET ERROR PARAMETER FOR RETURN TO
     C* NEW FILE MAINTENANCE PROGRAM
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
** CPY@     : Copyright notice for inclusion in all programs
(c) Misys International Banking Systems Ltd. 2001
