      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BU Loan Types/Sub-types')
     F********************************************************************
     F*                                                                  *
     F*  Midas  STANDING DATA MODULE
     F*                                                                  *
     F*     TABLET6 - BACKGROUND UPDATE OF TABLET6 FILE                  *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 242069             Date 18Sep06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No.S01230              Date 07Nov89               *
      *                S01229              Date 20Oct89               *
     F*                    E17596             DATE 20/03/89              *
     F*                                                                  *
     F********************************************************************
     F********************************************************************S01230
      *  242069 - Loan Class should be part of key field for TABLET6     *
     F*     S01230  - STANDARDISE PROGRAM                                *S01230
     F*     S01229  - SYNON FIELD NAME CHANGES                           *S01229
     F*     E17596  - RECOMPILED FOR NEW VERSION OF TABLETXL             *
     F********************************************************************
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       30      CHAIN TABLET6 FAILED/ERROR                         *
     F*       31      ERROR ON TABLET6 FILE                              *
     F*       32      CHAIN TABLETX FAILED/ERROR                         *
     F*                                                                  *
     F********************************************************************
     FTABLET6 UF  E           K        DISK                      A
     F                                              KCOMIT
     FTABLETA UF  E           K        DISK
     F                                              KCOMIT
     FTABLETXLUF  E           K        DISK
     F                                              KCOMIT
      /EJECT                                                              S01230
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
     E********************************************************************S01230
      /EJECT
     I*
     I* RENAME RECORD ID ON HEADER FILE TO AVOID OVERWRITING
     I*
     ITABLETAF
     I              RECT                            RECTA
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*                                                                   S01230
     ISCSARD    E DSSCSARDPD                                                                  242069
      **  Switchable Features File                                                            242069
      *                                                                                       242069
     IDSFDY     E DSDSFDY                                                                     242069
      ** Short data structure for access objects                                              242069
      *                                                                                       242069
     ILDA        UDS                            256                                           242069
      ** Local database data area                                                             242069
     I                                      134 141 DBFILE                                    242069
     I                                      142 170 DBKEY                                     242069
     I                                      171 180 DBPGM                                     242069
     I                                      181 1830DBASE                                     242069
     I                                      251 2560ZFSIZE                                    242069
      *                                                                                       242069
      /EJECT
     C*
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7        RETURN CODE
     C*********************PARM           WLTYP   2        LOAN TYPE      S01229
     C                     PARM           WLNTY   2        LOAN TYPE      S01229
     C*********************PARM           WSUTP   2        SUB-TYPE       S01229
     C                     PARM           WLNST   2        SUB-TYPE       S01229
     C                     PARM           WLNCL   4                                           242069
     C*********************PARM           WNARR  30        NARRATIVE      S01229
     C                     PARM           WLNTD  30        NARRATIVE      S01229
     C*********************PARM           WPTYP   20       PROC. TYPE     S01229
     C                     PARM           WLNPT   20       PROC. TYPE     S01229
     C*********************PARM           WCHTP   1        LAST CHANGE TYPS01229
     C                     PARM           WTYLC   1        LAST CHANGE TYPS01229
     C                     PARM           WLCD    50       LAST CHANGE DATE
     C*
      /EJECT
     C*
     C           KEY       KLIST
     C*********************KFLD           WLTYP                           S01229
     C                     KFLD           WLNTY                           S01229
     C*********************KFLD           WSUTP                           S01229
     C                     KFLD           WLNST                           S01229
     C                     KFLD           WLNCL                                               242069
      *                                                                                       242069
     C           *NAMVAR   DEFN           LDA                                                 242069
      *                                                                                       242069
      ** Check if CLE042 is switched on                                                       242069
      *                                                                                       242069
     C                     MOVE 'N'       CLE042  1                                           242069
     C                     CALL 'AOSARDR0'                                                    242069
     C                     PARM *BLANKS   @RTCD   7                                           242069
     C                     PARM '*VERIFY' @OPTN   7                                           242069
     C                     PARM 'CLE002'  @SARD   6                                           242069
     C           SCSARD    PARM SCSARD    DSFDY                                               242069
      *                                                                                       242069
     C           @RTCD     IFEQ *BLANKS                                                       242069
     C                     MOVE 'Y'       CLE042                                              242069
     C                     ENDIF                                                              242069
      *                                                                                       242069
     C           @RTCD     IFNE *BLANKS                                                       242069
     C           @RTCD     ANDNE'*NRF   '                                                     242069
      *                                                                                       242069
      ** Call to program ended in error                                                       242069
      *                                                                                       242069
     C           *LOCK     IN   LDA                                                           242069
     C                     MOVEL'CLE042'  DBKEY                                               242069
     C                     MOVE 'SCSARDPD'DBFILE                                              242069
     C                     Z-ADD1         DBASE                                               242069
     C                     MOVEL'TABLET6' DBPGM                                               242069
     C                     OUT  LDA                                                           242069
     C                     SETON                     U7U8LR                                   242069
     C                     EXSR *PSSR                                                         242069
     C                     ENDIF                                                              242069
     C*
      /EJECT
     C****************************************************************
     C*
     C*   M A I N L I N E   P R O G R A M
     C*
     C****************************************************************
     C*
     C* IF RECORD HAS BEEN INSERTED, CHAIN TO TABLET6 TO CHECK THAT
     C* RECORD DOES NOT ALREADY EXIST ON THIS FILE
     C*
     C***********WCHTP     IFEQ 'I'                                       S01229
     C           WTYLC     IFEQ 'I'                                       S01229
     C           KEY       CHAINTABLET6              30    CHAIN FAILED
     C*
     C* IF RECORD DOES NOT EXIST
     C* THEN MOVE PARAMETERS RECEIVED INTO FILE FIELDS AND WRITE
     C* A NEW RECORD
     C*
     C           *IN30     IFEQ '1'
     C*
     C* IF IT DOES EXIST AS DELETED(RECI *)
     C* THEN MOVE PARAMETERS RECEIVED INTO FILE FIELDS AND UPDATE
     C* OLD RECORD
     C           *IN30     OREQ '0'
     C           RECI      ANDEQ'*'
     C                     MOVE 'D'       RECI
     C                     Z-ADD43        RECT
     C*********************MOVE WLTYP     LTYP                            S01229
     C                     MOVE WLNTY     LTYP                            S01229
     C*********************MOVE WSUTP     SUTP                            S01229
     C                     MOVE WLNST     SUTP                            S01229
     C           CLE042    IFEQ 'Y'                                                           242069
     C                     MOVE WLNCL     LNCL                                                242069
     C                     ENDIF                                                              242069
     C*********************MOVE WNARR     NARR                            S01229
     C                     MOVE WLNTD     NARR                            S01229
     C*********************MOVE WPTYP     PTYP                            S01229
     C                     MOVE WLNPT     PTYP                            S01229
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C*
     C* IF RECORD FOUND ON FILE AND IS NOT A DELETED RECORD SET ERROR
     C*
     C                     ELSE
     C*
     C                     SETON                     31
     C                     END
     C*
     C           *IN30     IFEQ '1'
     C                     WRITETABLET6F               31  ERROR
     C                     END
     C*
     C           *IN30     IFEQ '0'
     C           *IN31     ANDEQ'0'
     C                     UPDATTABLET6F               31  ERROR
     C                     END
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT AN INSERT
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN CHANGED, RETRIEVE RECORD FROM FILE
     C*
     C***********WCHTP     IFEQ 'A'                                       S01229
     C           WTYLC     IFEQ 'A'                                       S01229
     C           KEY       CHAINTABLET6F             30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, MOVE RECEIVED PARAMETERS INTO FILE FIELDS
     C* AND UPDATE
     C*
     C           *IN30     IFEQ '0'
     C*********************MOVE WLTYP     LTYP                            S01229
     C                     MOVE WLNTY     LTYP                            S01229
     C*********************MOVE WSUTP     SUTP                            S01229
     C                     MOVE WLNST     SUTP                            S01229
     C           CLE042    IFEQ 'Y'                                                           242069
     C                     MOVE WLNCL     LNCL                                                242069
     C                     ENDIF                                                              242069
     C*********************MOVE WNARR     NARR                            S01229
     C                     MOVE WLNTD     NARR                            S01229
     C*********************MOVE WPTYP     PTYP                            S01229
     C                     MOVE WLNPT     PTYP                            S01229
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C                     UPDATTABLET6F               30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT A CHANGED RECORD
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN DELETED FROM THE NEW FILE, RETRIEVE IT
     C* FROM TABLET6
     C*
     C***********WCHTP     IFEQ 'D'                                       S01229
     C           WTYLC     IFEQ 'D'                                       S01229
     C           KEY       CHAINTABLET6F             30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, UPDATE RELEVANT FIELDS (WE DON'T ACTUALLY
     C* DELETE ANY RECORDS FROM THE OLD FILES)
     C*
     C           *IN30     IFEQ '0'
     C                     MOVE '*'       RECI
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C                     UPDATTABLET6F               30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS HAVE OCCURRED ON TABLET6 UPDATE
     C*
     C           P0RTN     IFEQ *BLANKS
     C*
     C* RETRIEVE OLD TRAILER FILE RECORD
     C*
     C           '43'      CHAINTABLETXF             32    CHAIN FAILED
     C*
     C* IF CHAIN TO FILE SUCCESSFUL
     C*
     C           *IN32     IFEQ '0'
     C*
     C* IF FIRST ACCESS TO THE FILE THEN GET HEADER RECORD
     C*
     C           NINS      ANDEQ0
     C           NAMD      ANDEQ0
     C           NDEL      ANDEQ0
     C*
     C                     READ TABLETAF                 32  EOF
     C*
     C* IF HEADER RECORD FOUND THEN SET PRINT INDICATOR AND UPDATE
     C*
     C           *IN32     IFEQ '0'
     C                     ADD  1         ILTS
     C                     UPDATTABLETAF               32    ERROR
     C*
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS
     C*
     C           *IN32     IFEQ '0'
     C*
     C* UPDATE AS APPROPRIATE THE NUMBER OF INSERTS,AMENDS OR DELETES
     C* ON THE TRAILER FILE
     C*
     C***********WCHTP     IFEQ 'I'                                       S01229
     C           WTYLC     IFEQ 'I'                                       S01229
     C                     ADD  1         NINS
     C                     END
     C*
     C***********WCHTP     IFEQ 'A'                                       S01229
     C           WTYLC     IFEQ 'A'                                       S01229
     C                     ADD  1         NAMD
     C                     END
     C*
     C***********WCHTP     IFEQ 'D'                                       S01229
     C           WTYLC     IFEQ 'D'                                       S01229
     C                     ADD  1         NDEL
     C                     END
     C*
     C                     UPDATTABLETXF               32    ERROR
     C*
     C                     END
     C*
     C* CHECK FOR ANY ERRORS AND SET ERROR PARAMETER FOR RETURN TO
     C* NEW FILE MAINTENANCE PROGRAM
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
      ****************************************************************                        242069
      *                                                              *                        242069
      * *PSSR  - Subroutine to handle abnormal error conditions      *                        242069
      *                                                              *                        242069
      ****************************************************************                        242069
      *                                                                                       242069
     C           *PSSR     BEGSR                                                              242069
      *                                                                                       242069
     C           WRUN      IFEQ *BLANKS                                                       242069
     C                     MOVE 'Y'       WRUN    1                                           242069
     C                     DUMP                                                               242069
     C                     END                                                                242069
      *                                                                                       242069
     C                     ENDSR                                                              242069
      ****************************************************************                        242069
     C*
** CPY@     : Copyright notice for inclusion in all programs           grams
(c) Misys International Banking Systems Ltd. 2001
