      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BU Lcl Inst. Cde BackGnd Upd')
     F********************************************************************
     F*                                                                  *
     F*  Midas  STANDING DATA MODULE
     F*                                                                  *
     F*     TABLETO - BACKGROUND UPDATE OF TABLETO FILE                  *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*     LAST AMEND NO. S01230             DATE 07/11/89              *S01230
     F*     PREV AMEND NO. S01229             DATE 20/10/89              *S01229
     F*                    E17596             DATE 20/03/89              *
     F*                                                                  *
     F********************************************************************
     F*     S01230  - STANDARDISE PROGRAMS                               *S01230
     F*     E17596  - RECOMPILED FOR NEW VERSION OF TABLETXL             *
     F*     S01229  - SYNON FIELD NAME CHANGES                           *S01229
     F********************************************************************
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       30      CHAIN TABLETO FAILED/ERROR                         *
     F*       31      ERROR ON TABLETO FILE                              *
     F*       32      CHAIN TABLETX FAILED/ERROR                         *
     F*                                                                  *
     F********************************************************************
     FTABLETO UF  E           K        DISK                      A
     F                                              KCOMIT
     FTABLETA UF  E           K        DISK
     F                                              KCOMIT
     FTABLETXLUF  E           K        DISK
     F                                              KCOMIT
      /EJECT
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
      /EJECT                                                              S01230
     I*
     I* RENAME RECORD ID ON HEADER FILE TO AVOID OVERWRITING
     I*
     ITABLETAF
     I              RECT                            RECTA
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
      /EJECT
     C*
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7        RETURN CODE
     C*********************PARM           WLINS   2        L.I. CODE      S01229
     C                     PARM           WLINC   2        L.I. CODE      S01229
     C*********************PARM           WLISN  30        L.I. NAME      S01229
     C                     PARM           WLINM  30        L.I. NAME      S01229
     C*********************PARM           WCHTP   1        LAST CHANGE TYPS01229
     C                     PARM           WTYLC   1        LAST CHANGE TYPS01229
     C                     PARM           WLCD    50       LAST CHANGE DATE
     C           *LIKE     DEFN LINS      KLINS
     C*
      /EJECT
     C****************************************************************
     C*
     C*   M A I N L I N E   P R O G R A M
     C*
     C****************************************************************
     C*
     C* IF RECORD HAS BEEN INSERTED, CHAIN TO TABLETO TO CHECK THAT
     C* RECORD DOES NOT ALREADY EXIST ON THIS FILE
     C*
     C***********WCHTP     IFEQ 'I'                                       S01229
     C           WTYLC     IFEQ 'I'                                       S01229
     C*********************MOVE WLINS     KLINS                           S01229
     C                     MOVE WLINC     KLINS                           S01229
     C           KLINS     CHAINTABLETO              30    CHAIN FAILED
     C*
     C* IF RECORD DOES NOT EXIST
     C* THEN MOVE PARAMETERS RECIEVED INTO FILE FIELDS AND WRITE
     C* A NEW RECORD
     C*
     C           *IN30     IFEQ '1'
     C*
     C* IF IT DOES EXIST AS DELETED(RECI *)
     C* THEN MOVE PARAMETERS RECIEVED INTO FILE FIELDS AND UPDATE
     C* OLD RECORD
     C*
     C           *IN30     OREQ '0'
     C           RECI      ANDEQ'*'
     C                     MOVE 'D'       RECI
     C                     Z-ADD60        RECT
     C*********************MOVE WLINS     LINS                            S01229
     C                     MOVE WLINC     LINS                            S01229
     C*********************MOVE WLISN     LISN                            S01229
     C                     MOVE WLINM     LISN                            S01229
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C*
     C* IF RECORD FOUND ON FILE AND IS NOT A DELETED RECORD SET ERROR
     C*
     C                     ELSE
     C*
     C                     SETON                     31
     C                     END
     C*
     C           *IN30     IFEQ '1'
     C                     WRITETABLETOF               31   ERROR
     C                     END
     C*
     C           *IN30     IFEQ '0'
     C           *IN31     ANDEQ'0'
     C                     UPDATTABLETOF               31   ERROR
     C                     END
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT AN INSERT
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN CHANGED, RETRIEVE RECORD FROM FILE
     C*
     C***********WCHTP     IFEQ 'A'                                       S01229
     C           WTYLC     IFEQ 'A'                                       S01229
     C*********************MOVE WLINS     KLINS                           S01229
     C                     MOVE WLINC     KLINS                           S01229
     C           KLINS     CHAINTABLETOF             30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, MOVE RECEIVED PARAMETERS INTO FILE FIELDS
     C* AND UPDATE
     C*
     C           *IN30     IFEQ '0'
     C*********************MOVE WLINS     LINS                            S01229
     C                     MOVE WLINC     LINS                            S01229
     C*********************MOVE WLISN     LISN                            S01229
     C                     MOVE WLINM     LISN                            S01229
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C                     UPDATTABLETOF               30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT A CHANGED RECORD
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN DELETED FROM THE NEW FILE, RETRIEVE IT
     C* FROM TABLETO
     C*
     C***********WCHTP     IFEQ 'D'                                       S01229
     C           WTYLC     IFEQ 'D'                                       S01229
     C*********************MOVE WLINS     KLINS                           S01229
     C                     MOVE WLINC     KLINS                           S01229
     C           KLINS     CHAINTABLETOF             30    CHAIN FAILED
     C*
     C* IF RECORD FOUND, UPDATE RELEVANT FIELDS (WE DON'T ACTUALLY
     C* DELETE ANY RECORDS FROM THE OLD FILES)
     C*
     C           *IN30     IFEQ '0'
     C                     MOVE '*'       RECI
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C                     UPDATTABLETOF               30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS HAVE OCCURRED ON TABLETO UPDATE
     C*
     C           P0RTN     IFEQ *BLANKS
     C*
     C* RETRIEVE OLD TRAILER FILE RECORD
     C*
     C           '60'      CHAINTABLETXF             32    CHAIN FAILED
     C*
     C* IF CHAIN TO FILE SUCCESSFUL
     C*
     C           *IN32     IFEQ '0'
     C*
     C* IF FIRST ACCESS TO THE FILE THEN GET HEADER RECORD
     C*
     C           NINS      ANDEQ0
     C           NAMD      ANDEQ0
     C           NDEL      ANDEQ0
     C*
     C                     READ TABLETAF                 32  EOF
     C*
     C* IF HEADER RECORD FOUND THEN SET PRINT INDICATOR AND UPDATE
     C*
     C           *IN32     IFEQ '0'
     C                     ADD  1         ILIS
     C                     UPDATTABLETAF               32    ERROR
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS
     C*
     C           *IN32     IFEQ '0'
     C*
     C* UPDATE AS APPROPRIATE THE NUMBER OF INSERTS,AMENDS OR DELETES
     C* ON THE TRAILER FILE
     C*
     C***********WCHTP     IFEQ 'I'                                       S01229
     C           WTYLC     IFEQ 'I'                                       S01229
     C                     ADD  1         NINS
     C                     END
     C*
     C***********WCHTP     IFEQ 'A'                                       S01229
     C           WTYLC     IFEQ 'A'                                       S01229
     C                     ADD  1         NAMD
     C                     END
     C*
     C***********WCHTP     IFEQ 'D'                                       S01229
     C           WTYLC     IFEQ 'D'                                       S01229
     C                     ADD  1         NDEL
     C                     END
     C*
     C                     UPDATTABLETXF               32    ERROR
     C*
     C                     END
     C*
     C* CHECK FOR ANY ERRORS AND SET ERROR PARAMETER FOR RETURN TO
     C* NEW FILE MAINTENANCE PROGRAM
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
** CPY@     : Copyright notice for inclusion in all programs           graS01230
(c) Misys International Banking Systems Ltd. 2001
