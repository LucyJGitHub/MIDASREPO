      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BU Telex module BackGnd Upd')
     F********************************************************************
     F*                                                                  *
     F*  Midas  STANDING DATA MODULE
     F*                                                                  *
     F*     TABMTLX - BACKGROUND UPDATE OF TABTB11 TABLETA & TABLETX     *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01230             Date 07/11/89              *
     F*                 S01229             Date 20/10/89              *S01229
     F*                    E17596             DATE 20/03/89              *
     F*                                                                  *
     F********************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*     S01230 - STANDARDISE PROGRAM                                 *S01230
     F*     S01229 - SYNON FIELD NAME CHANGES                            *S01229
     F*     E17596 - RECOMPILED FOR NEW VERSION OF TABLETXL              *
     F********************************************************************
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*                                                                  *
     F*       30      CHAIN TABTB11 FAILED/ERROR                         *
     F*       31      ERROR ON TABTB11 FILE                              *
     F*       32      CHAIN TABLETX FAILED/ERROR                         *
     F*                                                                  *
     F********************************************************************
     FTABTB11 UF  E           K        DISK
     F                                              KCOMIT
     FTABLETA UF  E           K        DISK
     F                                              KCOMIT
     FTABLETXLUF  E           K        DISK
     F                                              KCOMIT
      /EJECT
     E* Description     : Copyright notice for inclusion in all progra    S01230
     E*                                                                   S01230
     E********************************************************************S01230
     E                    CPY@    1   1 80               Copyright     arrS01230
     E********************************************************************S01230
      /EJECT                                                              S01230
     I*
     I* RENAME RECORD ID ON HEADER FILE TO AVOID OVERWRITING
     I*
     ITABLETAF
     I              RECT                            RECTA
     I*                                                                   S01230
     I* Description     : Copyright notice for inclusion in all programs  S01230
     I*                                                                   S01230
     IA@CPY       DS                                                      S01230
     I* Copyright array                                                   S01230
     I                                        1  80 CPY@                  S01230
     I*                                                                   S01230
      /EJECT
     C*
     C           *ENTRY    PLIST
      *
     C                     PARM           WLCD    50       LAST CHANGE DATE
     C*********************PARM           WCHTP   1        LAST CHANGE TYPS01229
     C                     PARM           WTYLC   1        LAST CHANGE TYPS01229
     C                     PARM           WVPMS   1        VERIFY PAYMENTS
     C                     PARM           WDFLG   1        DROP MSGS
     C                     PARM           P0RTN   7        RETURN CODE
     C*
      /EJECT
     C****************************************************************
     C*
     C*   M A I N L I N E   P R O G R A M
     C*
     C****************************************************************
     C*
     C* IF RECORD HAS BEEN CHANGED, RETRIEVE RECORD FROM FILE
     C*
     C***********WCHTP     IFEQ 'A'                                       S01229
     C           WTYLC     IFEQ 'A'                                       S01229
     C                     READ TABTB11F                 30NO RECORDS
     C*
     C* IF RECORD FOUND, MOVE RECEIVED PARAMETERS INTO FILE FIELDS
     C* AND UPDATE
     C*
     C           *IN30     IFEQ '0'
     C                     Z-ADD1         RECT
     C                     Z-ADD11        RCDE
     C*
     C           WVPMS     IFEQ 'Y'
     C                     BITON'1'       VPMS
     C                     ELSE
     C                     BITOF'1'       VPMS
     C                     END
     C*
     C           WDFLG     IFEQ 'Y'
     C                     BITON'1'       MSDR
     C                     ELSE
     C                     BITOF'1'       MSDR
     C                     END
     C*
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C                     UPDATTABTB11F               30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C* RECORD IS NOT A CHANGED RECORD
     C*
     C                     END
     C*
     C* IF RECORD HAS BEEN DELETED FROM THE NEW FILE, RETRIEVE IT
     C* FROM TABTB11
     C*
     C***********WCHTP     IFEQ 'D'                                       S01229
     C           WTYLC     IFEQ 'D'                                       S01229
     C                     READ TABTB11F                 30NO RECORDS
     C*
     C* IF RECORD FOUND, UPDATE RELEVANT FIELDS (WE DON'T ACTUALLY
     C* DELETE ANY RECORDS FROM THE OLD FILES)
     C*
     C           *IN30     IFEQ '0'
     C*********************MOVE WCHTP     CHTP                            S01229
     C                     MOVE WTYLC     CHTP                            S01229
     C                     Z-ADDWLCD      LCD
     C                     MOVE *BLANK    VPMS
     C                     UPDATTABTB11F               30  ERROR
     C                     END
     C*
     C* IF RECORD NOT FOUND OR ERROR OCCURRED ON UPDATE SET ERROR
     C* PARAMETER
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS HAVE OCCURRED ON TABTB11 UPDATE
     C*
     C           P0RTN     IFEQ *BLANKS
     C*
     C* RETRIEVE OLD TRAILER FILE RECORD
     C*
     C           '01'      CHAINTABLETXF             32    CHAIN FAILED
     C*
     C* IF CHAIN TO FILE SUCCESSFUL
     C*
     C           *IN32     IFEQ '0'
     C*
     C* IF FIRST ACCESS TO THE FILE THEN GET HEADER RECORD
     C*
     C           NINS      ANDEQ0
     C           NAMD      ANDEQ0
     C           NDEL      ANDEQ0
     C*
     C                     READ TABLETAF                 32  EOF
     C*
     C* IF HEADER RECORD FOUND THEN SET PRINT INDICATOR AND UPDATE
     C*
     C           *IN32     IFEQ '0'
     C                     ADD  1         INSC
     C                     UPDATTABLETAF               32    ERROR
     C                     END
     C*
     C                     END
     C*
     C* IF NO ERRORS
     C*
     C           *IN32     IFEQ '0'
     C*
     C* UPDATE AS APPROPRIATE THE NUMBER OF INSERTS,AMENDS OR DELETES
     C* ON THE TRAILER FILE
     C*
     C***********WCHTP     IFEQ 'A'                                       S01229
     C           WTYLC     IFEQ 'A'                                       S01229
     C                     ADD  1         NAMD
     C                     END
     C*
     C***********WCHTP     IFEQ 'D'                                       S01229
     C           WTYLC     IFEQ 'D'                                       S01229
     C                     ADD  1         NAMD
     C                     END
     C*
     C                     UPDATTABLETXF               32    ERROR
     C*
     C                     END
     C*
     C* CHECK FOR ANY ERRORS AND SET ERROR PARAMETER FOR RETURN TO
     C* NEW FILE MAINTENANCE PROGRAM
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE '*ERROR*' P0RTN
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
** CPY@     : Copyright notice for inclusion in all programs           grams
(c) Finastra International Limited 2001
