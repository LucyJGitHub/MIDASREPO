/*STDA*  DSPFBASE
/*EXI *  TEXT('BoE ICD Maintenance')
     A**************************************************************************
     A*
     A*  Midas - Bank of England
     A*
     A*  DSPF/BY0010DF - BoE ICD Maintenance
     A*
     A*  (c) Misys International Banking Systems Ltd.1999
     A*
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. LLN018             Date 17Apr01
     A*  Prev Amend No. LLN016             Date 10May00
     A*                 LLN014             Date 19Jan00
     A*                 LLN012   *CREATE   Date 20Apr99
     A*
     A*=========================================================================
     A*
     A*  Amendment Details
     A*  ~~~~~~~~~~~~~~~~~~
     A*
     A*  LLN018 - Upgrade BoE to APIs                                 *
     A*  LLN016 - Upgrade BoE to DBAR3                                *
     A*  LLN014 - Negotiable Assets Face Value Required
     A*  LLN012 - Financial Services Authority - Liquidity Report
     A*
     A**************************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Redisplay')
     A                                      PRINT
      *
      ** Display Screen
     A          R BY0010F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1999')
     A                                      OVERLAY
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  1 20'BANK OF ENGLAND ICD MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 57TIME
     A                                  1 67'WID'
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
      *
     A                                 10  9'Extract Cleared Balances for -
                                            Retail Accounts (Y/N) ......:'
     A            L#CLLD         1A  B 10 70TEXT('Flag')
     A                                      DSPATR(HI)
     A N50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  49
     AO 50                                  DSPATR(PC)
      *
     A                                 12  9'Create BT Extract File daily -
                                            (Y/N) ......................:'
     A            L#BTEX         1A  B 12 70TEXT('Flag')
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(RI PC)
      *
      ** Following Lines added for LLN014
     A                                 14  9'Negotiable Assets Face Value -
                                            required (Y/N)..............:'
     A            L#FACE         1A  B 14 70TEXT('Negotiable Assets Face Value +
                                            Indicator')
     A                                      DSPATR(HI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(RI PC)
      ** End of change for LLN014
      ** Following Lines added for LLN018
     A                                 16  9'Use Standard Industry Code field +
                                            in B of E (Y/N).........:'
     A            L#DICD         1A  B 16 70TEXT('Use Standard Industry Code  +
                                            field in Bank of England indicator')
     A                                      DSPATR(HI)
     A N53                                  DSPATR(UL)
     A  53                                  DSPATR(RI PC)
      *
     A                                 18  9'Use Standard Institution Code fie+
                                            ld in B of E (Y/N)......:'
     A            L#DINC         1A  B 18 70TEXT('Use Standard Industitution +
                                            Code field in Bank of England ind')
     A                                      DSPATR(HI)
     A N54                                  DSPATR(UL)
     A  54                                  DSPATR(RI PC)
      ** End of change for LLN018
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
      *
      ** Message subfile
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            L#PGMQ                    SFLPGMQ
      *
      ** Message subfile control
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            L#PGMQ                    SFLPGMQ
