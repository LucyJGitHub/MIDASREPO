/*STDA*  DSPFBASE
/*EXI *  TEXT('Midas BoE Account Extract Details Maintenance')
     A*****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  DSPF/BY1010DF - BoE Account Extract Details Maintenance      *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN022             Date 24Jan05               *
      *                 LLN021             Date 02Sep03               *
      *                 LLN016             Date 10May00               *
      *                 LLN016             Date 10May00               *
      *                 122382  *CREATE    Date 03Oct97               *
      *                                                               *
      *===============================================================*
      *                                                               *
      *  Amendment Details                                            *
      *  ~~~~~~~~~~~~~~~~~~                                           *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN022 - BOE Upgrade to MidasPlus                            *
      *  LLN021 - BoE Profit and Loss return                          *
      *  LLN016 - Upgrade BoE to DBAR3                                *
      *  122382 - Msg SubFile Indicator for end of Subfile is Displayed
      *                                                               *
     A**************************************************************************
     A*%%TS  SD  19970313  151440  PA_USR      REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(03 'Exit')
     A                                      PRINT
      **Start of change LLN022
     A**********                            HELP
     A**********                            HLPDOC(H035@000 BYINS ABSENG/DBAR4)
      **End of change LLN022
      *
      ** Initial Display Screen
     A          R BY1010F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A                                      OVERLAY
      **Start of change LLN022
     A**********H                           HLPDOC(H035@005 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(04 09 04 32)
     A**********H                           HLPDOC(H035@010 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(05 09 05 32)
     A**********H                           HLPDOC(H035@015 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(11 11 11 54)
     A**********H                           HLPDOC(H035@020 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(12 24 12 54)
     A**********H                           HLPDOC(H035@025 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(13 24 13 54)
     A**********H                           HLPDOC(H035@030 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(15 11 15 54)
     A**********H                           HLPDOC(H035@035 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(16 11 16 51)
     A**********H                           HLPDOC(H035@040 BYINS ABSENG/DBAR4)
     A**********                            HLPARA(17 11 17 51)
      **End of change LLN022
      *
     A                                  1 15'BANK OF ENGLAND RETURNS ACCOUNT EX-
     A                                      TRACT'
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   B  3 28TEXT('Action Code')
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI)
     A  49
     AO 50                                  DSPATR(PC)
     A                                  4  9'Account Code'
      **Start of change LLN022
     A**********  L#ACOD         4   B  4 28TEXT('Account Code')                              LLN022
     A            L#ACOD        10   B  4 28TEXT('Account Code')
      **End of change LLN022
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                  5  9'Institution Code'
     A            L#LINS         2   B  5 28TEXT('Institution Code')
     A N52                                  DSPATR(UL HI)
     C  52                                  DSPATR(RI)
     A  52
     AO 35                                  DSPATR(PC)
     A** LLN021 Start                                                                         LLN021
     A  66                              6  9'Industry Type'                                   LLN021
     A  66        L#MIND         3   B  6 28TEXT('Industry Type')                             LLN021
     A N59                                  DSPATR(UL HI)                                     LLN021
     C  59                                  DSPATR(RI)                                        LLN021
     A  59                                                                                    LLN021
     AO 35                                  DSPATR(PC)                                        LLN021
     A** LLN021 End                                                                           LLN021
     A                                 23  2'F3=Exit'
      *
      ** Detail Screen
     A          R BY1010F2
     A*%%TS  SD  19970313  151440  PA_USR      REL-V3R1M0  5763-PW1
     A                                      CA12(12 'Previous screen')
     A  33                                  CA10(10 'Delete')
     A                                      OVERLAY
     A                                  1 15'BANK OF ENGLAND RETURNS ACCOUNT EX-
     A                                      TRACT'
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   O  3 29TEXT('Action Code')
     A                                  4  9'Account Code'
      **Start of change LLN022
     A**********  L#ACOD         4   O  4 29TEXT('Account Code')                              LLN022
     A            L#ACOD        10   O  4 29TEXT('Account Code')
      **End of change LLN022
     A                                  5  9'Institution Code'
     A            L#LINS         2   O  5 29TEXT('Institution Code')
     A** LLN021 Start                                                                         LLN021
     A  66                              6  9'Industry Type'                                   LLN021
     A  66        L#MIND         3   O  6 29TEXT('Industry Type')                             LLN021
      *                                                                                       LLN021
     A** LLN021 End                                                                           LLN021
     A                                 10 11'Product Code - Debit..........:'
     A            L#PCDR         4   B 10 50TEXT('Product Code - Debit')
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A                                 11 24'- Credit.........:'
     A            L#PCCR         4   B 11 50TEXT('Product Code - Credit')
     A N54                                  DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A                                 12 24'- Supplementary..:'
     A            L#PCSP         4   B 12 50TEXT('Product Code - Supplementary')
     A N55                                  DSPATR(UL HI)
     A  55                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A                                 14 11'Overdue.......................:'
     A            L#ODIN         1   B 14 50TEXT('Overdue Indicator')
     A N56                                  DSPATR(UL HI)
     A  56                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A                                 15 11'Currency Override.............:'
     A            L#CCYI         1   B 15 50TEXT('Currency Override')
     A N57                                  DSPATR(UL HI)
     A  57                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A                                 16 11'Maturity Category.............:'
     A            L#MCAT         1   B 16 50TEXT('Maturity Category')
     A N58                                  DSPATR(UL HI)
     A  58                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A** LLN021 Start                                                                         LLN021
     A  66                             18 11'PL Product Code'                                 LLN021
     A  66                             18 27'- Debit.......:'                                 LLN021
      *                                                                                       LLN021
     A  66        L#PLDR         4   B 18 50TEXT('Debit Entries')                             LLN021
     A N60                                  DSPATR(UL HI)                                     LLN021
     A  60                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             19 27'- Credit......:'                                 LLN021
     A  66        L#PLCR         4   B 19 50TEXT('Credit Entries')                            LLN021
     A N61                                  DSPATR(UL HI)                                     LLN021
     A  61                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             20 27'- Fee Type....:'                                 LLN021
     A  66        L#PLFT         2   B 20 50TEXT('Fee Type')                                  LLN021
     A N62                                  DSPATR(UL HI)                                     LLN021
     A  62                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             21 27'- Type of Provision:'                            LLN021
     A  66        L#PLTP         1   B 21 50TEXT('Type of Prov')                              LLN021
     A N63                                  DSPATR(UL HI)                                     LLN021
     A  63                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
     A** LLN021 End                                                                           LLN021
     A                                 23  2'F3 = Exit'
     A                                 23 14'F12 = Previous'
     A  33                             23 50'F10 = Delete'
      *
      ** Message subfile
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            L#PGMQ                    SFLPGMQ
      *
      ** Message subfile control
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A** 122382 Start                                                     122382
     A**25********                          SFLEND                        122382
     A  25
     AON25                                  SFLEND
     A** 122382 End                                                       122382
     A            L#PGMQ                    SFLPGMQ
