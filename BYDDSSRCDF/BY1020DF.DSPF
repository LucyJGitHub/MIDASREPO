/*STDA*  DSPFBASE
/*EXI *  TEXT('Midas BoE Deal Extract Details Maintenance')
     A*%%TS  SD  19970313  150356  PA_USR      REL-V3R1M0  5763-PW1   *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  DSPF/BY1020DF - BoE Deal Extract Details Maintenance         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd 2005             *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN021             Date 02Sep03               *
      *  Prev Amend No. LLN020             Date 13May02               *
      *                 LLN016             Date 10May00               *
      *                 122382  *CREATE    Date 03OCT97               *
      *                                                               *
      *===============================================================*
      *                                                               *
      *  Amendment Details                                            *
      *  ~~~~~~~~~~~~~~~~~~                                           *
      *  LLN021 - BoE Profit and Loss return                          *
      *  LLN020 - Upgrade BoE to Midas Release 4                      *
      *  LLN016 - Upgrade BoE to DBAR3                                *
      *  122382 - Msg SubFile Indicator for end of Subfile is Displayed
      *                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(03 'Exit')
     A                                      PRINT
      *
      ** Initial display screen
     A          R BY1020F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1997')
     A                                      OVERLAY
      *
     A                                  1 15'BANK OF ENGLAND RETURNS DEALS EXTR-
     A                                      ACT  '
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   B  3 29TEXT('Action Code')
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI PC)
     A                                  4  9'Deal Type'
     A            L#DTYP         2   B  4 29TEXT('Deal Type')
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                  5  9'Deal Sub-Type'
     A            L#DLST         2   B  5 29TEXT('Deal Sub-Type')
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A                                  6  9'Institution Codes'
     A            L#LINS         2A  B  6 29TEXT('Institution Codes')
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI)
     A  52
     AO 35                                  DSPATR(PC)
     A** LLN021 Start
     A  66                              7  9'Industry Type'
     A  66        L#MIND         3   B  7 29TEXT('Industry Type')
     A N59                                  DSPATR(UL HI)
     C  59                                  DSPATR(RI)
     A  59
     AO 35                                  DSPATR(PC)
     A** LLN021 End
     A                                 23  2'F3 = Exit'
      *
      ** Detail screen
     A          R BY1020F2
     A*%%TS  SD  19970313  150356  PA_USR      REL-V3R1M0  5763-PW1
     A                                      CA12(12 'Previous screen')
     A  33                                  CA10(10 'Delete')
     A                                      OVERLAY
      *
     A                                  1 15'BANK OF ENGLAND RETURNS DEALS EXTR-
     A                                      ACT  '
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   O  3 29TEXT('Action Code')
     A                                  4  9'Deal Type'
     A            L#DTYP         2   O  4 29TEXT('Deal Type')
     A                                  5  9'Deal Sub-Type'
     A            L#DLST         2   O  5 29TEXT('Deal Sub-Type')
     A                                  6  9'Institution Codes'
     A            L#LINS         2A  O  6 29TEXT('Institution Codes')
     A** LLN021 Start
     A  66                              7  9'Industry Type'
     A  66        L#MIND         3   O  7 29TEXT('Industry Type')
     A** LLN021 End
     A                                 11 11'Product Code....................:'
     A            L#PCOD         4A  B 11 48TEXT('Product Code')
     A N54                                  DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A                                 13 11'Internal Time/Sight Deposit'
     A                                 14 11'Analysis Required...............:'
     A            L#ISTD         1A  B 14 50TEXT('Internal Time  +
     A                                      /Sight Deposit')
     A N55                                  DSPATR(UL HI)
     A  55                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A** LLN021 Start
     A  66                             16 11'PL Product Code - Debit.......:'
      *
     A  66        L#PLDR         4   B 16 48TEXT('Debit Entries')
     A N60                                  DSPATR(UL HI)
     A  60                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
      *
     A  66                             17 27'- Credit......:'
     A  66        L#PLCR         4   B 17 48TEXT('Credit Entries')
     A N61                                  DSPATR(UL HI)
     A  61                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
      *
     A  66                             18 27'- Tax.........:'
     A  66        L#PLTX         4   B 18 48TEXT('Tax')
     A N62                                  DSPATR(UL HI)
     A  62                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A** LLN021 End
     A                                 23  2'F3 = Exit'
     A                                 23 13'F12 = Previous'
     A  33                             23 50'F10 = Delete'
      *
      ** Message subfile
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            L#PGMQ                    SFLPGMQ
      *
      ** Message subfile control
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A** 122382 Start
     A**25********                          SFLEND                                            122382
     A  25
     AON25                                  SFLEND
     A** 122382 End
     A            L#PGMQ                    SFLPGMQ
