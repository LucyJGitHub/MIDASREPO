/*STDA*  DSPFBASE
/*EXI *  TEXT('Midas BoE Loan Extract Details Maintenance')
     A*%%TS  SD  19970313  144023  PA_USR      REL-V3R1M0  5763-PW1
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  DSPF/BY1030DF - BoE Loan Extract Details Maintenance         *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD057833           Date 18May21               *
      *  Prev Amend No. MD049900           Data 02Mar18               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 LLN021             Date 02Sep03               *
      *                 LLN020             Date 13May02               *
      *                 LLN016             Date 10May00               *
      *                 LLN012             Date 03Oct97               *
      *                 122382  *CREATE    Date 03Oct97               *
      *                                                               *
      *===============================================================*
      *                                                               *
      *  Amendment Details                                            *
      *  ~~~~~~~~~~~~~~~~~~                                           *
      *  MD057833 - BOE Returns Loans Extract Maintenance validation  *
      *             error.                                            *
      *             Do not validate and do not display the Loan Class *
      *             field if CLE042 is OFF.                           *
      *  MD049900 - Include loan class in validating loan type/stype  *
      *             combination                                       *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *  LLN020 - Upgrade BoE to Midas Release 4                      *
      *  LLN016 - Upgrade BoE to DBAR3                                *
      *  LLN012 - Additional BoE Changes (Patch B)                    *
      *  122382 - Msg SubFile Indicator for end of Subfile is Displayed
      *                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03 'Exit')
     A                                      PRINT
      *
      ** Initial Display Screen
     A          R BY1030F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A                                      OVERLAY
      *
     A                                  1 15'BANK OF ENGLAND RETURNS LOANS EXTR-
     A                                      ACT  '
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   B  3 28TEXT('Action Code')
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI PC)
     A                                  4  9'Loan Type'
     A            L#LTYP         2   B  4 28TEXT('Loan Type')
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                  5  9'Loan Sub-Type'
     A            L#LNST         2   B  5 28TEXT('Loan Sub-Type')
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
      **Start of change MD049900
      *
      ** Start of change MD057833
     A**********                        6  9'Loan Class'                                    MD057833
     A**********  L#LNCL         4   B  6 28TEXT('Loan Class')                              MD057833
     A  70                              6  9'Loan Class'
     A  70        L#LNCL         4   B  6 28TEXT('Loan Class')
      ** End of change MD057833
      *
     A N49                                  DSPATR(UL HI)
     A  49                                  DSPATR(RI PC)
     A**********                        6  9'Institution Code'                              MD049900
     A                                  7  9'Institution Code'
     A**********  L#LINS         2   B  6 28TEXT('Institution Code')                        MD049900
     A            L#LINS         2   B  7 28TEXT('Institution Code')
      **End of change MD049900
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI)
     A  53
     AO 35                                  DSPATR(PC)
     A** LLN021 Start
      **Start of change MD049900
     A**66******                        7  9'Industry Type'                                 MD049900
     A  66                              8  9'Industry Type'
     A**66******  L#MIND         3   B  7 28TEXT('Industry Type')                           MD049900
     A  66        L#MIND         3   B  8 28TEXT('Industry Type')
      **End of change MD049900
     A N59                                  DSPATR(UL HI)
     C  59                                  DSPATR(RI)
     A  59
     AO 35                                  DSPATR(PC)
     A** LLN021 End
     A                                 23  2'F3 = Exit'
      *
      ** Detail Screen
     A          R BY1030F2
     A*%%TS  SD  19970313  144023  PA_USR      REL-V3R1M0  5763-PW1
     A                                      CF12(12 'Previous screen')
     A  33                                  CF10(10 'Delete')
     A                                      OVERLAY
      *
     A                                  1 15'BANK OF ENGLAND RETURNS LOANS EXTR-
     A                                      ACT  '
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   O  3 28TEXT('Action Code')
     A                                  4  9'Loan Type'
     A            L#LTYP         2   O  4 28TEXT('Loan type')
     A                                  5  9'Loan Sub-Type'
     A            L#LNST         2   O  5 28TEXT('Loan Sub-Type')
      **Start of change MD049900
      *
      ** Start of change MD057833
     A**********                        6  9'Loan Class'                                    MD057833
      **********  L#LNCL         4   O  6 28TEXT('Loan Class')                              MD057833
     A  70                              6  9'Loan Class'
     A  70        L#LNCL         4   O  6 28TEXT('Loan Class')
      ** End of change MD057833
      *
     A**********                        6  9'Institution Code'                              MD049900
     A                                  7  9'Institution Code'
     A**********  L#LINS         2   O  6 28TEXT('Institution Code')                        MD049900
     A            L#LINS         2   O  7 28TEXT('Institution Code')
     A** LLN021 Start
     A**66******                        7  9'Industry Type'                                 MD049900
     A  66                              8  9'Industry Type'
     A**66******  L#MIND         3   O  7 28TEXT('Industry Type')                           MD049900
     A  66        L#MIND         3   O  8 28TEXT('Industry Type')
      **End of change MD049900
     A** LLN021 End
     A                                 11 11'Product Code...............:'
     A            L#PCOD         4   B 11 48TEXT('Product Code')
     A  32N54
     AO 31N54                               DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A** LLN012 Start
     A                                 12 11'Non Performing Loan........:'
     A            L#NPER         1A  B 12 48TEXT('Non Performing Loan')
     A  32N56
     AO 31N56                               DSPATR(UL HI)
     A  56                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A** LLN012 End
     A** LLN021 Start
     A  66                             14 11'PL Product Code - Credit...:'
      *
     A  66        L#PLCR         4   B 14 48TEXT('Credit Entries')
     A  32N60
     AO 31N60                               DSPATR(UL HI)
     A  60                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
      *
     A  66                             15 27'- Charges..:'
     A  66        L#PLCG         4   B 15 48TEXT('Charges')
     A  32N61
     AO 31N61                               DSPATR(UL HI)
     A  61                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
      *
     A  66                             16 27'- Tax......:'
     A  66        L#PLTX         4   B 16 48TEXT('Tax')
     A  32N62
     AO 31N62                               DSPATR(UL HI)
     A  62                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
      *
     A  66                             17 27'- Fee Type.:'
     A  66        L#PLFT         2   B 17 48TEXT('Fee Type')
     A  32N63
     AO 31N63                               DSPATR(UL HI)
     A  63                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A** LLN021 End
     A                                 23  2'F3 = Exit'
     A                                 23 13'F12 = Previous'
     A  33                             23 50'F10 = Delete'
      *
      ** Message subfile
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            L#PGMQ                    SFLPGMQ
      *
      ** Message subfile control
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A** 122382 Start
     A**25********                          SFLEND                                            122382
     A  25
     AON25                                  SFLEND
     A** 122382 End
     A            L#PGMQ                    SFLPGMQ
