/*STDA*  DSPFBASE
/*EXI *  TEXT('Midas BoE Securities Extract Details Maintenance')
     A*%%TS  SD  19970313  154941  PA_USR      REL-V3R1M0  5763-PW1
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  DSPF/BY1050DF - BoE Security Extract Details Maintenance     *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN021             Date 02Sep03               *
      *                 LLN020             Date 13May02               *
      *                 LLN016             Date 10May00               *
      *                 122382  *CREATE    Date 03Oct97               *
      *                                                               *
      *===============================================================*
      *                                                               *
      *  Amendment Details                                            *
      *  ~~~~~~~~~~~~~~~~~~                                           *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *  LLN020 - Upgrade BoE to Midas Release 4                      *
      *  122382 - Msg SubFile Indicator for end of Subfile is Displayed
      *                                                               *
      *****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(03 'Exit')
     A                                      PRINT
      *
      ** Initial Display Screen
     A          R BY1050F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A                                      OVERLAY
      *
     A                                  1 13'BANK OF ENGLAND RETURNS SECURITIES-
     A                                       EXTRACT'
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  2TEXT('Program name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   B  3 28TEXT('Action Code')
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI PC)
     A                                  4  9'Security Shortname'
     A            L#SESN        10   B  4 28TEXT('Security Shortname')
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                  5  9'Investment Type'
     A            L#INVT         3   B  5 28TEXT('Investment Type')
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A                                  6  9'Institution Code'
     A            L#LINS         2A  B  6 28TEXT('Institution Code')
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI PC)
     A** LLN021 Start                                                                         LLN021
     A  66                              7  9'Industry Type'                                   LLN021
     A  66        L#MIND         3   B  7 28TEXT('Industry Type')                             LLN021
     A N59                                  DSPATR(UL HI)                                     LLN021
     C  59                                  DSPATR(RI)                                        LLN021
     A  59                                                                                    LLN021
     AO 35                                  DSPATR(PC)                                        LLN021
     A** LLN021 End                                                                           LLN021
     A                                 23  2'F3 = Exit'
      *
      ** Detail Screen
     A          R BY1050F2
     A*%%TS  SD  19970313  154941  PA_USR      REL-V3R1M0  5763-PW1
     A                                      CA12(12 'Previous screen')
     A  33                                  CA10(10 'Delete')
     A                                      OVERLAY
      *
     A                                  1 13'BANK OF ENGLAND RETURNS SECURITIES-
     A                                       EXTRACT'
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  2TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   O  3 28TEXT('Action Code')
     A                                  4  9'Security Shortname'
     A            L#SESN        10   O  4 28TEXT('Security Shortname')
     A                                  5  9'Investment Type'
     A            L#INVT         3   O  5 28TEXT('Investment Type')
     A                                  6  9'Institution Code'
     A            L#LINS         2A  O  6 28TEXT('Institution Code')
     A** LLN021 Start                                                                         LLN021
     A  66                              7  9'Industry Type'                                   LLN021
     A  66        L#MIND         3   O  7 28TEXT('Industry Type')                             LLN021
     A** LLN021 End                                                                           LLN021
     A                                 11 11'Product Code..................:'
     A            L#PCOD         4A  B 11 47TEXT('Product Code')
     A N54                                  DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A  33
     AO 34                                  DSPATR(PR)
     A** LLN021 Start                                                                         LLN021
     A  66                             13 11'PL Product Code - Int. Rec....:'                 LLN021
      *                                                                                       LLN021
     A  66        L#PLIR         4   B 13 47TEXT('Accrued Interest Receivable')               LLN021
     A N60                                  DSPATR(UL HI)                                     LLN021
     A  60                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             14 27'- Int. Pay....:'                                 LLN021
     A  66        L#PLIP         4   B 14 47TEXT('Accrued Interest Payable')                  LLN021
     A N61                                  DSPATR(UL HI)                                     LLN021
     A  61                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             15 27'- Profit/Loss.:'                                 LLN021
     A  66        L#PLPL         4   B 15 47TEXT('Profit/Loss')                               LLN021
     A N62                                  DSPATR(UL HI)                                     LLN021
     A  62                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             16 27'- Tax.........:'                                 LLN021
     A  66        L#PLTX         4   B 16 47TEXT('Tax')                                       LLN021
     A N63                                  DSPATR(UL HI)                                     LLN021
     A  63                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             17 27'- Charges Received :'                            LLN021
     A  66        L#CCHG         4   B 17 49TEXT('Charges Received')                          LLN021
     A N64                                  DSPATR(UL HI)                                     LLN021
     A  64                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             18 27'- Received Fee Type:'                            LLN021
     A  66        L#CFTP         2   B 18 49TEXT('Received Fee Type')                         LLN021
     A N65                                  DSPATR(UL HI)                                     LLN021
     A  65                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             19 27'- Charges Paid ....:'                            LLN021
     A  66        L#DCHG         4   B 19 49TEXT('Charges Paid')                              LLN021
     A N68                                  DSPATR(UL HI)                                     LLN021
     A  68                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
      *                                                                                       LLN021
     A  66                             20 27'- Paid Fee Type....:'                            LLN021
     A  66        L#DFTP         2   B 20 49TEXT('Paid Fee Type')                             LLN021
     A N69                                  DSPATR(UL HI)                                     LLN021
     A  69                                  DSPATR(RI PC)                                     LLN021
     A  33                                                                                    LLN021
     AO 34                                  DSPATR(PR)                                        LLN021
     A** LLN021 End                                                                           LLN021
     A                                 23  2'F3 = Exit'
     A                                 23 13'F12 = Previous'
     A  33                             23 50'F10 = Delete'
      *
      ** Message subfile
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            L#PGMQ                    SFLPGMQ
      *
      ** Message subfile control
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A** 122382 Start                                                     122382
     A**25********                          SFLEND                        122382
     A  25
     AON25                                  SFLEND
     A** 122382 End                                                       122382
     A            L#PGMQ                    SFLPGMQ
