/*STDA*  DSPFBASE
/*EXI *  TEXT('Midas BoE Futures & Options Extract Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  DSPF/BY1070DF - BoE Futures & Options Details Maintenance    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN021  *CREATE    Date 23Oct03               *
      *                 XXXXXX             Date                       *
      *                                                               *
      *===============================================================*
      *                                                               *
      *  Amendment Details                                            *
      *  ~~~~~~~~~~~~~~~~~~                                           *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - *CREATE                                             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03 'Exit')
     A                                      PRINT
      *
      ** Initial Display Screen
     A          R BY1070F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A*
     A                                      OVERLAY
      *
     A                                  1 15'BANK OF ENGLAND FUTURES and OPTION-
     A                                      S EXTRACT'
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   B  3 28TEXT('Action Code')
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI PC)
     A                                  4  9'Processing Type...'
     A            L#PROC         1   B  4 28TEXT('Processing Type')
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                  5  9'Book Code.........'
     A            L#BOOK         2   B  5 28TEXT('Book Code')
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A**
     A                                  6  9'Market Centre.....'
     A            L#MKCC         2   B  6 28TEXT('Market Centre')
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI PC)
     A**
     A                                  7  9'Instrument Type...'
     A            L#INST         3   B  7 28TEXT('Instrument Type')
     A N54                                  DSPATR(UL HI)
     C  54                                  DSPATR(RI PC)
     A**
     A                                  8  9'Institution Type..'
     A            L#LINS         2   B  8 28TEXT('Institution Type')
     A N55                                  DSPATR(UL HI)
     C  55                                  DSPATR(RI PC)
     A**
     A                                  9  9'Industry Code.....'
     A            L#MIND         3   B  9 28TEXT('Industry Code')
     A N56                                  DSPATR(UL HI)
     C  56                                  DSPATR(RI PC)
     A**
     A                                 23  2'F3 = Exit'
      *
      ** Detail Screen
     A          R BY1070F2
     A                                      CF12(12 'Previous screen')
     A  33                                  CF10(10 'Delete')
     A                                      OVERLAY
      *
     A                                  1 15'BANK OF ENGLAND FUTURES and OPTION-
     A                                      S EXTRACT'
     A                                  1 57TIME
     A            L#WSID        10A  O  1 71TEXT('Workstation Id')
     A                                  1 67'WID'
     A            L#PGM         10A  O  1  3TEXT('Program Name')
     A                                  3  9'Action Code'
     A            L#ACTN         1   O  3 28TEXT('Action Code')
     A                                  4  9'Processing Type...'
     A            L#PROC         1   O  4 28TEXT('Processing Type')
     A                                  5  9'Book Code.........'
     A            L#BOOK         2   O  5 28TEXT('Book Code')
     A                                  6  9'Market Centre.....'
     A            L#MKCC         2   O  6 28TEXT('Market Centre')
     A                                  7  9'Instrument Type...'
     A            L#INST         3   O  7 28TEXT('Instrument Type')
     A                                  8  9'Institution Type..'
     A            L#LINS         2   O  8 28TEXT('Institution Type')
     A                                  9  9'Industry Code.....'
     A            L#MIND         3   O  9 28TEXT('Industry Code')
      **
     A                                 11 16'Accrued Interest Receivable:'
     A            L#ACIR         4  0B 11 48TEXT('Accr Int Receivable')
     A  66                                  DSPATR(PR)
     A                                 12 16'Accrued Interest Payable...:'
     A            L#ACIP         4  0B 12 48TEXT('Accr Int Payable')
     A  66                                  DSPATR(PR)
     A                                 13 16'Profit / Loss .............:'
     A            L#PRLS         4  0B 13 48TEXT('Profit/Loss')
     A  66                                  DSPATR(PR)
      *
     A                                 14 16'Charges Received ..........:'
     A            L#CCHG         4  0B 14 48TEXT('Charges Received')
     A  66                                  DSPATR(PR)
     A  61                                  DSPATR(RI PC)
      *
     A                                 15 16'Received Fee Type .........:'
     A            L#CFEE         2   B 15 48TEXT('Received Fee Type')
     A  66                                  DSPATR(PR)
     A  61
     AO 62                                  DSPATR(RI PC)
      *
     A                                 16 16'Charges Paid ..............:'
     A            L#DCHG         4  0B 16 48TEXT('Charges Paid')
     A  66                                  DSPATR(PR)
     A  63                                  DSPATR(RI PC)
      *
     A                                 17 16'Paid Fee Type .............:'
     A            L#DFEE         2   B 17 48TEXT('Paid Fee Type')
     A  66                                  DSPATR(PR)
     A  63
     AO 64                                  DSPATR(RI PC)
     A**
     A                                 23  2'F3 = Exit'
     A                                 23 13'F12 = Previous'
     A  33                             23 50'F10 = Delete'
      *
      ** Message subfile
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            L#PGMQ                    SFLPGMQ
      *
      ** Message subfile control
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A*                                                                   122382
     A  25
     AON25                                  SFLEND
     A            L#PGMQ                    SFLPGMQ
