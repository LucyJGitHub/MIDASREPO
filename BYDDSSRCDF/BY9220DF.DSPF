/*STDA*  DSPFBASE
/*EXI *  TEXT('Selection screen for product codes')
     A*%%TS  SD  19930415  134534  EB2LDR      REL-V2R1M0  5738-PW1
     A**************************************************************************
     A*
     A*  Midas - Bank of England
     A*
     A*  DSPF/BY9220DF - Selection screen for product codes
     A*
     A*  (c) Misys International Banking Systems Ltd. 1997
     A*
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. LLN016             Date 10May00
     A*  Prev Amend No. xxxxxx             Date DDMMMYY
     A*
     A*=========================================================================
     A*
     A*  Amendment Details
     A*  ~~~~~~~~~~~~~~~~~~
     A*
     A*  LLN016 - Upgrade BoE to DBAR3                                *
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
      *
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd 1997')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   B  8  3
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1PCOD         4      8  5TEXT('Product code')
     A                                      DSPATR(HI)
     A            #1PCNA        50      8 11TEXT('Product code narrative')
     A                                      DSPATR(HI)
     A            #1FXNT         1      8 66TEXT('Product code')
     A                                      DSPATR(HI)
     A            #1MMNT         1      8 72TEXT('Product code narrative')
     A                                      DSPATR(HI)
     A            #1LENT         1      8 78TEXT('Product code narrative')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd 1997')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2DPCD')
     A                                      SETOF(32 'Invalid: #2EYTX')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A                                  1  2'SELECT '
     A            ##JOB         10      1 49TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##AANA         7      1 60TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##TME          6  0   1 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  2 27'SELECT PRODUCT CODE'
     A                                      DSPATR(HI)
     A                                  4  4'Prod'
     A                                  4 30'Product'
     A                                  4 69'Netting:'
     A                                  5  4'Code'
     A                                  5 26'Code Narrative'
     A                                  5 65'FX'
     A                                  5 71'MM'
     A                                  5 77'LE'
     A            #2PCOD         4   B  6  5TEXT('Product code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2PCNA        50   B  6 11TEXT('Product code narrative')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd 1997')
     A
     A                                      OVERLAY PUTOVR
     A                                 22  2'1=Select request F3=Exit          -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd 1997')
     A
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd 1997')
     A
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(02) SFLSIZ(06)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
