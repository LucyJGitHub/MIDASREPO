/*STDA*  DSPFBASE
/*EXI *  TEXT('Selection screen for instrument type')
     A*%%TS  SD  19930415  134534  EB2LDR      REL-V2R1M0  5738-PW1
     A**************************************************************************
     A*
     A*  Midas - Bank of England
     A*
     A*  DSPF/BY9250DF - Selection screen for instrument type
     A*
     A*  (c) Finastra International Limited 1997
     A*
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Prev Amend No. LLN021  *create    Date 14Nov03
     A*                 xxxxxx             Date DDMMMYY
     A*
     A*=========================================================================
     A*
     A*  Amendment Details
     A*  ~~~~~~~~~~~~~~~~~~
     A*  MD046248 - Finastra Rebranding
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
      *
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1SEL          1   B  8  3
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1MIND         5      8  5TEXT('Instrument type')
     A                                      DSPATR(HI)
     A            #1INAR        20A     8 16TEXT('Instrument Name')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('(c) Finastra International +
     A                                      Limited 1997')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2DPCD')
     A                                      SETOF(32 'Invalid: #2EYTX')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A                                  1  2'SELECT '
     A            ##JOB         10      1 49TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##AANA         7      1 60TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##TME          6  0   1 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  2 27'SELECT INSTRUMENT CODE'
     A                                      DSPATR(HI)
     A                                  4  4'Instr.'
     A                                  4 16'Instrument Name'
     A                                  4 55'BOE'
     A                                  5  4'Code'
     A                                  5 55'Code'
     A            #2MIND         3   B  6  5TEXT('Instrument code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2INAR        20A  B  6 16TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Finastra International +
     A                                      Limited 1997')
     A
     A                                      OVERLAY PUTOVR
     A                                 22  2'1=Select request F3=Exit          -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('(c) Finastra International +
     A                                      Limited 1997')
     A
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('(c) Finastra International +
     A                                      Limited 1997')
     A
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(02) SFLSIZ(06)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
