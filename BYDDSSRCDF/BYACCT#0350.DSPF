     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas BoE Accounts Maintenance Window')
     A*****************************************************************
     A*                                                               *
     A*  Midas¦ABS - General Ledger Module                            *
     A*                                                               *
     A*  BYACCT#0 - Accounts Maintenance Window                       *
     A*                                                               *
     A*  (C) Copyright Midas-Kapiti International Ltd. 2000           *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. LLN020             Date 13MAY02               *
     A*  Prev Amend No. LLN016             Date 10MAY00               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  LLN020 - Upgrade BoE to Midas Release 4                      *
     A*  LLN016 - Upgrade BoE to DBAR3                                *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
     A          R WINDOWF                   TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
     A                                      WINDOW(&SWROW &SWCOL 13 50)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         50A  O  2  1OVRDTA
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
      *
     A                                  4  2'Netting Balance...................'
     A                                      OVRATR
     A            L#NETT         1A  B  4 37OVRDTA
     A N50N20                               DSPATR(HI UL)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  5  2'Deposit type......................'
     A                                      OVRATR
     A            L#DPTY         1A  B  5 37OVRDTA
     A N51N20                               DSPATR(HI UL)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  6  2'SAYE/Sharesave notice days......'
     A                                      OVRATR
     A            L#SNOD         3A  B  6 35OVRDTA
     A N52N20                               DSPATR(HI UL)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  7  2'Loan Type.........................'
     A                                      OVRATR
     A            L#LNTY         1A  B  7 37OVRDTA
     A N53N20                               DSPATR(HI UL)
     A  53N20                               DSPATR(RI)
     A  53N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  8  2'Loan Maturity Date...........'
     A                                      OVRATR
     A            L#LNMD         6A  B  8 32OVRDTA
     A N54N20                               DSPATR(HI UL)
     A  54N20                               DSPATR(RI)
     A  54N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  9  2'Loan Limit............'
     A                                      OVRATR
     A            L#LNLT        13A  B  9 25OVRDTA
     A N55N20                               DSPATR(HI UL)
     A  55N20                               DSPATR(RI)
     A  55N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *----------------------------------------------------------------
      *    To reduce the window size you may wish to split the command
      *    key display over two lines.
     A                                 11  2'F3=Main Menu'
     A                                 11 16'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(12)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      WINDOW(WINDOWF)
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
