     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas BoE Book codes Maintenance Window')
     A*****************************************************************
     A*                                                               *
     A*  Midas¦ABS - Standing Data Module                             *
     A*                                                               *
     A*  BYBOOK#0 - Book Codes Maintenance Window                     *
     A*                                                               *
     A*  (C) Copyright Midas-Kapiti International Ltd. 2000           *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. LLN020             Date 13May02               *
     A*  Prev Amend No. LLN016             Date 10May00               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  LLN020 - Upgrade BoE to Midas Release 4                      *
     A*  LLN016 - Upgrade BoE to DBAR3                                *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
     A          R WINDOWF                   TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      * Following line amended for LLN008
     A**                                    WINDOW(&SWROW &SWCOL 8 50)
     A                                      WINDOW(&SWROW &SWCOL 10 51)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         50A  O  2  1OVRDTA
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
      * Replace fields below with required fields for new window
     A                                  4  2'Banking/Trading (B/T)............'
     A                                      OVRATR
     A            L#BTIN         1A  B  4 36OVRDTA
     A N50N20                               DSPATR(HI UL)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
      *  Start of change for LLN008
     A                                  6  2'Trade/Value Date (T/V) ..........'
     A                                      OVRATR
     A            L#TVIN         1A  B  6 36OVRDTA
     A N51N20                               DSPATR(HI UL)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *  End of change for LLN008
      *----------------------------------------------------------------
      *    To reduce the window size you may wish to split the command
      *    key display over two lines.
     A                                  8  2'F3=Main Menu'
     A                                  8 16'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A* Following line amended for LLN008
     A**                                    SFL SFLMSGRCD(07)
     A                                      SFL SFLMSGRCD(09)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      WINDOW(WINDOWF)
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
