      *****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Customer Details Window')
      *****************************************************************
      *                                                               *
      *  Midas¦ABS - Standing Data Module                             *
      *                                                               *
      *  BYCUST#0 - Customer Details Window                           *
      *                                                               *
      *  (c) Finastra International Limited 2000                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN020             Date 13May02               *
      *                 199260             Date 23Oct01               *
      *                 LLN018             Date 17Apr01               *
      *                 LLN016  *CREATE    Date 10May00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN020 - Upgrade BoE to Midas Release 4                      *
      *  199260 - Change Help Tag references from R10.6 to DBAR3.     *
      *  LLN018 - Upgrade BoE to APIs and addition of 2 new fields    *
      *           on ICD.                                             *
      *  LLN016 - BoE Upgrade to DBAR3                                *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
      *
      *
     A          R WINDOWF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A                                      WINDOW(&SWROW &SWCOL 8 76)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         50A  O  1  1OVRDTA
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
      * Replace fields below with required fields for new window
     A                                  2  2'SIO Indicator...............'
     A            L#SIO          1A  B  2 31OVRDTA
     A N50N20                               DSPATR(HI UL)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  2 40'Housing Association.............'
     A            L#HAI          1A  B  2 73OVRDTA
     A N51N20                               DSPATR(HI UL)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  3  2'Ultimate Parent.............'
     A            L#ULP          6A  B  3 31OVRDTA
     A N52N20                               DSPATR(HI UL)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  3 40'Eligibility Indicator...........'
     A            L#EGID         1A  B  3 73OVRDTA
     A N53N20                               DSPATR(HI UL)
     A  53N20                               DSPATR(RI)
     A  53N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A**Start of LLN018
     A************                      4  2'Local Instintution Code.....'
     A                                  4  2'Local Institution Code......'
     A**End of LLN018
     A            L#LINS         2A  B  4 31OVRDTA
     A N54N20                               DSPATR(HI UL)
     A  54N20                               DSPATR(RI)
     A  54N20                               DSPATR(PC)
     A**Start of LLN020
     A**20******                            DSPATR(PR)                                        LLN020
     A  20
     AO 21                                  DSPATR(PR)
     A**End of LLN020
     A                                  4 40'Local Industry Code.............'
     A            L#LIND         3A  B  4 73OVRDTA
     A N55N20                               DSPATR(HI UL)
     A  55N20                               DSPATR(RI)
     A  55N20                               DSPATR(PC)
     A**Start of LLN020
     A**20******                            DSPATR(PR)                                        LLN020
     A  20
     AO 22                                  DSPATR(PR)
     A**End of LLN020
     A                                  5  2'Connected Counterparty......'
     A            L#CNTP         1A  B  5 31OVRDTA
     A N60N20                               DSPATR(HI UL)
     A  60N20                               DSPATR(RI)
     A  60N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *----------------------------------------------------------------
      *    To reduce the window size you may wish to split the command
      *    key display over two lines.
     A                                  6  2'F3=Main Menu'
     A                                  6 16'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(07)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(08)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      WINDOW(WINDOWF)
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
