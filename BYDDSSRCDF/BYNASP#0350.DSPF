      *****************************************************************                       LLN012
/*STDA*  DSPFBASE                                                    *
/*EXI *  TEXT('Negotiable Assets Purchase Window')
      *****************************************************************                       LLN012
      *                                                               *                       LLN012
      *  Midas - MM Dealer Module                                     *                       LLN012
      *                                                               *                       LLN012
      *  BYNASP#0 - Bank of England Negotiable Assets Purchased       *                       LLN012
      *             Window                                            *                       LLN012
      *                                                               *                       LLN012
      *  (c) Misys International Banking Systems Ltd. 2000            *                       LLN012
      *                                                               *                       LLN012
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN020             Date 13May02               *                       LLN012
      *  Prev Amend No. LLN016             Date 10May00               *                       LLN012
      *                                                               *                       LLN012
      *---------------------------------------------------------------*                       LLN012
      *                                                               *                       LLN012
      *  LLN020 - Upgrade BoE to Midas Release 4                      *                       LLN012
      *  LLN016 - Upgrade BoE to DBAR3                                *                       LLN012
      *                                                               *                       LLN012
      *****************************************************************                       LLN012
     A                                      DSPSIZ(24 80 *DS3)                                LLN012
     A                                      PRINT                                             LLN012
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
     A          R WINDOWF                                               +                     LLN012
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +                            LLN012
     A                                      1997')                                            LLN012
     A                                      WINDOW(&SWROW &SWCOL 8 40)
     A                                      USRRSTDSP                                         LLN012
     A                                      KEEP                                              LLN012
     A                                      OVERLAY                                           LLN012
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *----------------------------------------------------------------
     A                                  2  2'Guarantee Code ........:'
     A            L#GTEE         2A  B  2 27
     A N48N20                               DSPATR(HI UL)
     A  48N20                               DSPATR(RI)
     A  48N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  3  2'Qualifying Flag .......:'
     A            L#QUAL         1A  B  3 27
     A N47N20                               DSPATR(HI UL)
     A  47N20                               DSPATR(RI)
     A  47N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  4  2'Final Maturity Date....:'                        LLN012
     A            L#FMAT         6A  B  4 27                                                  LLN012
     A N49N20                               DSPATR(HI UL)
     A  49N20                               DSPATR(RI)
     A  49N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  6  2'F3=Exit F12=Previous'                            LLN012
     A*=========================================================================
     A          R ASSUMEF                                                                     LLN012
     A                                      ASSUME                                            LLN012
     A                                  1  3' '                                               LLN012
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(07)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      WINDOW(WINDOWF)
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
