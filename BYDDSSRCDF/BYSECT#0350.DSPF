     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Securities Details Maintenance Window')
     A*****************************************************************
      *                                                               *
      *  Midas¦ABS - Securities Trading Module                        *
      *                                                               *
      *  BYSECT#0 - Securities Details Maintenance Window             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2000            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN020             Date 13May02               *
      *  Prev Amend No. LLN018             Date 17Apr01               *
      *                 LLN016   *CREATE   Date 10May00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LLN020 - Upgrade BoE to Midas Release 4                      *
      *  LLN018 - Upgrade BoE to APIs and addition of 2 new fields    *
      *           on ICD.                                             *
      *  LLN016 - Upgrade BoE to DBAR3                                *
      *                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
     A          R WINDOWF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1999')
     A                                      WINDOW(&SWROW &SWCOL 7 30)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *----------------------------------------------------------------
      * Replace fields below with required fields for new window
      *
     A                                  1  9'BoE Details'                           LLN018
     A                                      DSPATR(HI)                              LLN018
     A                                  2  2'Guarantee Code ........:'
     A                                      OVRATR
     A            L#GTEE         2A  B  2 27OVRDTA
     A N51N20                               DSPATR(HI UL)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  3  2'Qualifying Flag .......:'
     A                                      OVRATR
     A            L#QUAL         1A  B  3 27OVRDTA
     A N50N20                               DSPATR(HI UL)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *----------------------------------------------------------------
      *    To reduce the window size you may wish to split the command
      *    key display over two lines.
     A                                  5  2'F3=Exit'
     A                                  5 11'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(06)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A                                      WINDOW(WINDOWF)
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
