      *****************************************************************
/*STD *  PFBASE
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Deals Extract Details File')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  PF/BYDLEXPP - Deals Extract Details File                     *
      *                                                               *
      *  (C) Midas Kapiti International Limited, 1997                 *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             DATE 24Aug03               *
      *  Prev Amend No. LLN021             DATE 24Aug03               *
      *                 LLN007             DATE 06MAR97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment  Details                                           *
      *  ------------------                                           *
      *  CDL038 - Extended Deal Sub Type                              *
      *  LLN021 - BoE Profit and Loss return                          *
      *  LLN007 - Yellow Book Changes, *CREATE                        *
      *                                                               *
      *****************************************************************
      *
     A          R BYDLEXD0                  TEXT('(c) Midas Kapiti +
     A                                      International Limited 1997')
     A            L2RECI         1A         TEXT('Record Id')
     A                                      COLHDG('Rec' 'Id')
     A            L2UTIM         6S 0       TEXT('Time of Update')
     A                                      COLHDG('Update' 'Time')
     A            L2LCD          5P 0       TEXT('Last Change Date')
     A                                      COLHDG('Last' 'Chg Date')
     A            L2CHTP         1A         TEXT('Type of Last Change')
     A                                      COLHDG('Last' 'Chg Type')
     A            L2DTYP         2A         TEXT('Deal Type')
     A                                      COLHDG('Deal Type')
     A            L2DLST         2A         TEXT('Deal Sub-Type')
     A                                      COLHDG('Deal' 'S-Type')
     A            L2LINS         2S 0       TEXT('Institution Code')
     A                                      COLHDG('Inst' 'Code')
      * LLN021 - Start                                                                        LLN021
     A            L2MIND         3A         TEXT('Industry code')                             LLN021
     A                                      COLHDG('Industry' 'Code')                         LLN021
      * LLN021 - End                                                                          LLN021
     A            L2PCOD         4S 0       TEXT('Product Code')
     A                                      COLHDG('Prod' 'Code')
     A            L2ISTD         1A         TEXT('Internal Sight/Time Deposit')
     A                                      COLHDG('Int S/T' 'Deposit')
      * LLN021 - Start                                                                        LLN021
     A            L2PLDR         4S 0       TEXT('PL Product Code - Debit')                   LLN021
     A                                      COLHDG('Prod Code' 'Debit')                       LLN021
     A            L2PLCR         4S 0       TEXT('PL Product Code - Credit')                  LLN021
     A                                      COLHDG('Prod Code' 'Credit')                      LLN021
     A            L2PLTX         4S 0       TEXT('PL Product Code - Tax')                     LLN021
     A                                      COLHDG('Prod Code' 'Tax' )                        LLN021
      * LLN021 - End                                                                          LLN021
     A*                                              Begin /CDL038/
     A            L2CLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
      *
     A          K L2DTYP
     A          K L2DLST
     A          K L2LINS
      * LLN021 - Start                                                                        LLN021
     A          K L2MIND                                                                      LLN021
      * LLN021 - End                                                                          LLN021
