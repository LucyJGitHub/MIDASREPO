      *****************************************************************
/*STD *  PFBASE
/*EXS *  PF_NM                                                        *
/*EXI *  TEXT('Extracted Details For Bank of England Reporting')      *
/*XAIA*  ADDPFM FILE(BYEXDTPP) MBR(BYEXACM)                           *                       LLN022
/*XAIA*  TEXT('Account Extract Details')                              *                       LLN022
/*XAIB*  ADDPFM FILE(BYEXDTPP) MBR(BYEXDLM)                           *                       LLN022
/*XAIB*  TEXT('Dealing Extract Details')                              *                       LLN022
/*XAIC*  ADDPFM FILE(BYEXDTPP) MBR(BYEXLEM)                           *                       LLN022
/*XAIC*  TEXT('Lending Extract Details')                              *                       LLN022
/*XAID*  ADDPFM FILE(BYEXDTPP) MBR(BYEXSEM)                           *                       LLN022
/*XAID*  TEXT('Securities Extract Details')                           *                       LLN022
/*XAIE*  ADDPFM FILE(BYEXDTPP) MBR(BYEXNTM)                           *                       LLN022
/*XAIE*  TEXT('Netted Items Details')                                 *                       LLN022
/*XAIF*  ADDPFM FILE(BYEXDTPP) MBR(BYEXFRM)                           *                       LLN022
/*XAIF*  TEXT('FRA/IRS Deals Extract Details')                        *                       LLN022
/*XAIG*  ADDPFM FILE(BYEXDTPP) MBR(BYEXFFM)                           *                       LLN022
/*XAIG*  TEXT('Futures & Options Extract Details')                    *                       LLN022
/*XAIH*  ADDPFM FILE(BYEXDTPP) MBR(BYEXTRM)                           *                       LLN022
/*XAIH*  TEXT('Securities Trades Extract Details')                    *                       LLN022
/*XAII*  ADDPFM FILE(BYEXDTPP) MBR(BYEXJCM)                           *                      BUG7238
/*XAII*  TEXT('Journal Entry and Amount Accruals Details')            *                      BUG7238
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  PF/BYEXDTPP - Extracted Details For Bank of England Reports  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 1997            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG23967           Date 14May09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 25Nov05               *
      *                 BUG7238            Date 18May05               *
      *                 LLN022A            Date 24Jan05               *
      *                 LLN012             Date 19Apr99               *
      *                 LLN007             Date 04Mar97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Note: This file must be created with members:                *
      *                                                               *
      *           BYEXACM - Account Extract Details                   *
      *           BYEXDLM - Dealing Extract Details                   *
      *           BYEXLEM - Lending Extract Details                   *
      *           BYEXSEM - Securities Extract Details                *
      *           BYEXNTM - Netted Items Details                      *
      *           BYEXFRM - FRA/IRS Deals Extract Details             *
      *           BYEXFFM - Futures and Options Extract Details       *
      *           BYEXTRM - Securities Trades Extract Details         *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment  Details                                           *
      *  ------------------                                           *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23967 - Add Local Industry Code Field.                    *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG7238- No detail records generated for extract files       *
      *           BYEXABPP, BYEXLRPP and BYEXPLPP                     *
      *  LLN022A - BOE Upgrade to MidasPlus                           *
      *  LLN012 - Financial Services Authority - Liquidity Report     *
      *  LLN007 - Yellow Book Changes, *CREATE                        *
      *                                                               *
      *****************************************************************
      *
      **Start of change BUG23967
     A                                      REF(MFRFL)
      **End of change BUG23967
     A          R BYEXDTD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1997')
     A            L8RTYP         1A         TEXT('Record type')
     A                                      COLHDG('Rec Type')
     A            L8PCOD         4S 0       TEXT('Product Code')
     A                                      COLHDG('Prod' 'Code')
      * Begin /CSD027/
     A**********  L8CUST         6S 0       TEXT('Customer Number')                           CSD027
     A            L8CUST         6A         TEXT('Customer Number')
      * End /CSD027/
     A                                      COLHDG(' Cust' 'Number')
     A            L8CYCD         3A         TEXT('Currency')
     A                                      COLHDG('Cur')
     A**Start of Change LLN022
     A**********  L8TRAN        10A         TEXT('Transaction Reference')                     LLN022
     A            L8TRAQ        10A         TEXT('Transaction Reference')
     A**End of change LLN022
     A                                      COLHDG('Transaction' 'Reference')
     A            L8BRCD         3A         TEXT('Branch Code')
     A                                      COLHDG('Bch' 'code')
     A            L8MMOD         2A         TEXT('Module Id')
     A                                      COLHDG('Mod' 'Id')
     A            L8DDAT         5P 0       TEXT('Deal Date')
     A                                      COLHDG('Deal' 'Date')
     A            L8VDAT         5P 0       TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
     A            L8MDAT         5P 0       TEXT('Maturity Date')
     A                                      COLHDG('Matur' 'Date')
     A            L8MCAT         1A         TEXT('Maturity Category')
     A                                      COLHDG('Mat' 'Dat')
     A            L8ISOC         2A         TEXT('ISO Country Code')
     A                                      COLHDG('ISO' 'CC')
     A            L8RISO         2A         TEXT('ISO Risk Country Code')
     A                                      COLHDG('ISO' 'RCC')
     A            L8LIND         3S 0       TEXT('Industry Code')
     A                                      COLHDG('Ind' 'Cod')
     A            L8LINS         2S 0       TEXT('Institution Code')
     A                                      COLHDG('Ins' 'Cd')
     A            L8CCYS         3A         TEXT('SWIFT Currency Code')
     A                                      COLHDG('SWIFT' 'CurCd')
     A            L8NNSI         1A         TEXT('Sterling/Currency Ind.')
     A                                      COLHDG('$/Cur' 'Ind')
     A            L8STGI         1A         TEXT('Currency Interpretation')
     A                                      COLHDG('Cur' 'Int')
     A            L8CUAS        15P 0       TEXT('Scaled Currency Amount')
     A                                      COLHDG('Scaled Currency' 'Amount')
     A            L8SGAM        15P 0       TEXT('Sterling Equivalent')
     A                                      COLHDG('Sterling' 'Equivalent')
     A            L8SIO          1A         TEXT('S form indicator')
     A                                      COLHDG(' S' 'Ind')
     A            L8INTB         1A         TEXT('Interest Bearing Indicator')
     A                                      COLHDG('IB' 'Ind')
     A            L8ODIN         1A         TEXT('Overdue Indicator')
     A                                      COLHDG('Ovd' 'Ind')
     A            L8CSSN        10A         TEXT('Customer Shortname')
     A                                      COLHDG('Customer' 'Shortname')
      * Begin /CSD027/
     A**********  L8PCNB         6S 0       TEXT('Customer Parent Number')                    CSD027
     A            L8PCNB         6A         TEXT('Customer Parent Number')
      * End /CSD027/
     A                                      COLHDG('CusPar' 'Number')
     A            L8MBSE         1A         TEXT('Mortgage Backed Security')
     A                                      COLHDG('Mort Back' 'Security')
     A            L8CPNR        11P 7       TEXT('Coupon Rate')
     A                                      COLHDG('Coupon' 'Rate')
     A            L8MKTS        15P 0       TEXT('Market Value Scaled Currency')
     A                                      COLHDG('Market Value' +
     A                                      'Scaled Currency')
     A            L8SMKV        15P 0       TEXT('Market Value Sterling')
     A                                      COLHDG('Market Value' 'Sterling')
     A            L8MKDC        15P 0       TEXT('Max DR position in period')
     A                                      COLHDG('Max DR pos in period')
     A            L8SMXD        15P 0       TEXT('Max DR position in Sterling')
     A                                      COLHDG('Max DR pos in Stg')
     A            L8MXDD         5P 0       TEXT('Last date of Max DR pos')
     A                                      COLHDG('Last date Max DR')
     A            L8MKCC        15P 0       TEXT('Max CR position in period')
     A                                      COLHDG('Max CR pos in period')
     A            L8SMXC        15P 0       TEXT('Max CR position in Sterling')
     A                                      COLHDG('Max CR pos in Stg')
     A            L8MXCD         5P 0       TEXT('Last date of Max CR pos')
     A                                      COLHDG('Last date Max CR')
     A            L8OPHW         1A         TEXT('Option Held/Written +
     A                                      Indicator')
     A                                      COLHDG('Opt Hld' '/Wrttn Ind')
     A            L8OPCS        15P 0       TEXT('Option Contract Price +
     A                                      in Sterling')
     A                                      COLHDG('Option Contract' +
     A                                      'Price Sterling')
     A            L8SOPC        15P 0       TEXT('Scaled Option +
     A                                      Contract Price Sterling')
     A                                      COLHDG('Scaled Option' +
     A                                      'Contract prc stg')
     A            L8OMPS        15P 0       TEXT('Scaled Option Margin +
     A                                      /Premium in currency')
     A                                      COLHDG('Scaled Opt Marg' +
     A                                      '/Premium in Cur')
     A            L8SOMP        15P 0       TEXT('Option Margin +
     A                                      /Premium in Sterling')
     A                                      COLHDG('Option Margin' +
     A                                      '/Premium in Stg')
     A            L8OTCX         1A         TEXT('OTC or Exchange +
     A                                      Traded Indicator')
     A                                      COLHDG('OTC or Exchg' 'Traded Ind')
     A            L8ONUD         1A         TEXT('Nature of +
     A                                      Underlying Derivative')
     A                                      COLHDG('Ntr of' 'Und Deriv')
     A            L8ONUP         1A         TEXT('Underlying Position')
     A                                      COLHDG('Und' 'Pos')
     A            L8CTYP         1S 0       TEXT('Type of Credit Given')
     A                                      COLHDG('Cred' 'Typ')
     A            L8DTYP         1S 0       TEXT('Type of Deposit Taken')
     A                                      COLHDG('Dep' 'Typ')
     A            L8DRVT         1S 0       TEXT('Derivative Production Type')
     A                                      COLHDG('Drv Pdtn' +
     A                                      'type')
     A            L8ELIG         1A         TEXT('Eligibility for Rediscount')
     A                                      COLHDG('elig' +
     A                                      'ReDscnt')
     A            L8NETI         1A         TEXT('Netted Amount')
     A                                      COLHDG('Net' 'Amt')
     A            L8RWCD         4S 0       TEXT('Risk Weighting')
     A                                      COLHDG('Risk' 'Weighting')
     A            L8HAIN         1A         TEXT('Housing Association +
     A                                      Indicator')
     A                                      COLHDG('Hsing' 'Assc Ind')
      ** LLN012 start
     A            L8GTEE         2A         TEXT('Guarantee Code')
     A                                      COLHDG('Guarantee' 'Code')
     A            L8QUAL         1A         TEXT('Qualifying Flag')
     A                                      COLHDG('Qualifying' 'Flag')
     A            L8CNTP         1A         TEXT('Connected Counterparty')
     A                                      COLHDG('Connected' 'Counterparty')
     A            L8BTEX         1A         TEXT('Include on BT Extract')
     A                                      COLHDG('Include on' 'BT Extract')
     A            L8LREX         1A         TEXT('Include on LR Extract')
     A                                      COLHDG('Include on' 'LR Extract')
     A            L8ETYP         1A         TEXT('Event Type')
     A                                      COLHDG('Event' 'Type')
      ** LLN012 end
     A**Start of Change LLN022
     A            L8TRAN        12A         TEXT('Transaction Reference')
     A                                      COLHDG('Transaction' 'Reference')
     A**End of change LLN022
      *
      **Start of change BUG23967
     A            L8LOIC    R               REFFLD(FTAG)
     A                                      TEXT('Local Industry Code - Alpha')
     A                                      COLHDG('Local Ind Code')
      **End of change BUG23967
