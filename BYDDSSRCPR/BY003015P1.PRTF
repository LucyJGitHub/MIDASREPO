      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Missing Account Extract Criteria Report')              *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY003015P1 - Missing Account Extract Criteria Report         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD049901*CREATE    Date 02Mar18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD049901 - Missing criteria reports are not generated        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited (2001)')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BY003015P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    49'MISSING ACCOUNT EXTRACT CRITERIA'
      *
     A                                      SPACEA(1)
     A                                    49'--------------------------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    12'Account Code'
     A                                    32'Account Code Name'
     A                                    65'Account Type'
     A                                    89'Account Section'
     A                                    12'------------'
     A                                      SPACEB(1)
     A                                    32'-----------------'
     A                                    65'------------'
     A                                    89'---------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RACCD         10        12
     A            RACCN         25        32
     A            RACTY          1        71
     A            RACSC          2        95
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
