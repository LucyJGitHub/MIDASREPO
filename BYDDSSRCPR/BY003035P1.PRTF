      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Missing Lending Extract Criteria Report')              *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY003015P1 - Missing Lending Extract Criteria Report         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD049901*CREATE    Date 02Mar18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD049901 - Missing criteria reports are not generated        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited (2001)')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BY003035P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    49'MISSING LENDING EXTRACT CRITERIA'
      *
     A                                      SPACEA(1)
     A                                    50'------------------------------'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    12'Loan Type'
     A                                    32'Sub Type'
     A                                    50'Description'
     A                                    89'Processing Type'
     A                                    12'---------'
     A                                      SPACEB(1)
     A                                    32'--------'
     A                                    50'-----------'
     A                                    89'---------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RLNTY          2        15
     A            RLNST          2        35
     A            RLNTD         30        50
     A            RLNPT          2  0     96
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
