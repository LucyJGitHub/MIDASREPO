      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Missing Securities Extract Criteria Report')           *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY003055P1 - Missing Securities Extract Criteria Report      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD049901*CREATE    Date 02Mar18               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD049901 - Missing criteria reports are not generated        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited (2001)')
      *
     A                                      SKIPB(2)
     A                                     6'REFERENCE BY003055P1'
     A                                      SPACEB(1)
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'MISSING SECURITIES EXTRACT -
     A                                      CRITERIA'
      *
     A                                      SPACEA(1)
     A                                    47'-----------------------------------
     A                                      -'
     A                                      SPACEA(3)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                    25'Invesment Type'
     A                                    47'Investment Name'
     A                                    83'Processing Type'
     A                                    25'-------------'
     A                                      SPACEB(1)
     A                                    47'---------------'
     A                                    83'---------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL1
     A            RINVT          3        31
     A            RINAM         20        47
     A            RPROT          1        91
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
