/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Account Extract - Securities Account Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY2010P5 - Account Extract/Validation - Securities Accounts  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 1997            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN022             Date 25Jan05               *
      *  Prev Amend No. 204929             Date 04Jul02               *
      *                 137888             Date 05Jun01               *
      *                 LLN012  *CREATE    Date 30Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  LLN022  - BOE Upgrade to Midas Plus.                         *
      *  204929  - Report hash currency total.                        *
      *  137888  - Report total of sterling equivalents on summary    *
      *  LLN012 BoE Liquidity Reporting Patch B.                      *
      *****************************************************************
      *
     A          R BY2010H5                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1997')
     A                                      SKIPB(03)
     A                                     6'Reference BY2010P5'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE5          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  21                                35'Account Extract As At'
     A N21                                32'Account Validation As At '
     A            L#RPDA         7A       57
     A                                    64' - Securities Account'
     A                                      SPACEA( 1)
     A  21                                35'----------------------'
     A N21                                32'-------------------------'
     A                                    57'-------'
     A                                    64'---------------------'
     A                                      SPACEA(2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
      **Start of change LLN022
     A**********                          52'Cust'                                            LLN022
     A                                    55'Cust'
     A**********                          57'Risk'                                            LLN022
     A                                    60'Risk'
     A**********                          64'Ind.'                                            LLN022
     A                                    67'Ind.'
     A**********                          70'Inst'                                            LLN022
     A                                    73'Inst'
     A**********                         113'Sterling'                                        LLN022
     A                                   116'Sterling'
      **End of change LLN022
      *
     A                                   126'Product'
     A                                      SPACEA(1)
      *
     A                                     1'Customer'
     A                                    30'Account'
      *
      **Start of change LLN022
     A**********                          52'Cnty'                                            LLN022
     A                                    55'Cnty'
     A**********                          57'Cnty'                                            LLN022
     A                                    60'Cnty'
     A**********                          64'Code'                                            LLN022
     A                                    67'Code'
     A**********                          70'Code'                                            LLN022
     A                                    73'Code'
     A**********                          77'Ccy'                                             LLN022
     A                                    80'Ccy'
     A**********                          92'Amount'                                          LLN022
     A                                    98'Amount'
     A**********                         111'Equivalent'                                      LLN022
     A                                   114'Equivalent'
      **End of change LLN022
      *
     A                                   127'Code'
     A                                      SPACEA(1)
     A*
     A*
     A*  Details
     A          R BY2010D5                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 2005')
     A  30        L#CUST         6A        1SPACEB(2)
     A  30        L#CRNM        20A        8SPACEA(1)
     A            L#CNUM         6A       30SPACEB(1)
     A                                    36'-'
     A            L#CYCD         3A       37
     A                                    40'-'
      *
      **Start of change LLN022
     A**********  L#ACOD         4A       41                                                  LLN022
     A            L#ACOD        10A       41
     A**********                          45'-'                                               LLN022
     A                                    51'-'
     A**********  L#ACSQ         2A       46                                                  LLN022
     A            L#ACSQ         2A       52
     A**********  L#LISO         2A       53                                                  LLN022
     A            L#LISO         2A       56
     A**********  L#RISO         2A       58                                                  LLN022
     A            L#RISO         2A       61
     A**********  L#LIND         3A       64                                                  LLN022
     A            L#LIND         3A       67
     A**********  L#LINS         2A       71                                                  LLN022
     A            L#LINS         2A       74
     A**********  L#CCYD         3A       77                                                  LLN022
     A            L#CCYD         3A       80
     A**********  L#CUAS        17A       82                                                  LLN022
     A            L#CUAS        17A       88
     A**********  L#SGAM        17A      105                                                  LLN022
     A            L#SGAM        17A      108
      **End of change LLN022
      *
     A            L#PCOD         4S 0    127
     A*
     A* Start of changes for LLN012.
     A*
     A          R BY2010I5                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                      SKIPB(03)
     A                                     1'Reference BY2010P5'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE5          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
     A*
     A                                    38'Account Extract As At'
     A            L#RPDA         7A       59
     A                                    66' - Securities Accounts'
     A                                    88' Summary'
     A                                      SPACEA( 1)
     A*
     A                                    38'----------------------'
     A                                    59'-------'
     A                                    66'----------------------'
     A                                    88'--------'
     A                                      SPACEA( 2)
     A*
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
     A*
     A                                    16'Securities Extract'
     A                                    38'Securities Extract'
     A                                    73'Accounts Extract'
     A                                    95'Accounts Extract'
     A                                      SPACEA(1)
     A*
     A                                     4'Ccy'
     A                                    20'Currency Total'
     A                                    37'Sterling Equivalent'
     A                                    75'Currency Total'
     A                                    92'Sterling Equivalent'
     A                                       SPACEA(2)
     A*
     A          R BY2010J5                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                       SPACEB(1)
     A            ZCYCD          3A        4
     A            ZCUAS         20A       14
     A            ZSGAM         20A       36
     A            ZSEC          20A       69
     A            ZSES          20A       91
     A  40                               114'***Discrepancy***'
     A*
     A**  Start of change for 137888
     A          R BY2010K5                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                       SPACEB(2)
     A**  Start of change for 204929
     A                                     4 'Hash'
     A                                       SPACEA(1)
     A**  End of change for 204929
     A                                     4 'Total'
     A**  Following line added for 204929
     A            L5TOTC        20A       14
     A            L5TOTS        20A       36
     A**  Following line added for 204929
     A            L5TOTB        20A       69
     A            L5TOTA        20A       91
     A  40                               114'***Discrepancy***'
     A**  End of change for 137888
     A*
     A* End of changes for LLN012.
     A*
     A          R BY2010E5                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                      SPACEB(3)
     A                                    50'***  No Details To Report  ***'
      *
      *
      *
     A          R BY2010Z5                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                      SPACEB(3)
     A                                    53'***  End of Report  ***'
      *
