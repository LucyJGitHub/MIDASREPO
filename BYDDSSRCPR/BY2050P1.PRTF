/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Securities Extract - Valid Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY2050P1 - Securities extract - Valid report                 *
      *                                                               *
      *  (c) Finastra International Limited 1997                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN012             Date 30Sep99               *
      *                 122556  *CREATE    Date 26Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN012 - BoE Liquidity Reporting Patch B.                    *
      *                                                               *
      *****************************************************************
      *
     A          R BY2050H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A                                      SKIPB(03)
     A                                     6'Reference BY2050P1'
     A            BJURPT        53A       39
     A                                   100'Date'
     A            BJMRDT         7A      105
     A                                   119'Page'
     A            PAGE1          4  0    124
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    38'Securities Extract As At '
     A            L#RPDA         7A       63
     A                                    70' - Valid Securities'
     A                                      SPACEA(1)
     A                                    38'-------------------------'
     A                                    63'-------'
     A                                    70'-------------------'
     A                                      SPACEA(2)
      *
     A  20                                 1'Branch'
     A  20        L#BRCD         3A        8
     A  20        L#BRNM        30A       12
     A  20                                  SPACEA(2)
      *
     A                                    23'Inv.'
     A                                    30'Book'
     A                                    42'Book'
      ** Start of changes for LLN012
     A**********                          52'Cust'                                            LLN012
     A**********                          57'Risk'                                            LLN012
     A**********                          64'Ind.'                                            LLN012
     A**********                          70'Inst'                                            LLN012
     A**********                         114'Sterling'                                        LLN012
     A**********                         126'Product'                                         LLN012
     A                                    49'Cust'
     A                                    54'Risk'
     A                                    59'Ind.'
     A                                    65'Inst'
     A                                   104'Sterling'
     A                                   114'Product'
      ** End of changes for LLN012
     A                                      SPACEA(1)
      *
     A                                     1'Issuer'
     A                                    10'Security'
     A                                    23'Type'
     A                                    30'Code'
     A                                    35'Mkt'
     A                                    42'Date'
      ** Start of changes for LLN012
     A**********                          52'Cnty'                                            LLN012
     A**********                          57'Cnty'                                            LLN012
     A**********                          64'Code'                                            LLN012
     A**********                          70'Code'                                            LLN012
     A**********                          77'Ccy'                                             LLN012
     A**********                          86'Book'Position'                                   LLN012
     A**********                         112'Equivalent'                                      LLN012
     A**********                         127'Code'                                            LLN012
     A                                    49'Cnty'
     A                                    54'Cnty'
     A                                    59'Code'
     A                                    65'Code'
     A                                    70'Ccy'
     A                                    80'Book Position'
     A                                   102'Equivalent'
     A                                   115'Code'
     A                                   121'BT'
     A                                   126'LR'
      ** End of changes for LLN012
      *
      *
      *  Details
     A          R BY2050D1                  TEXT('(c) +
     A                                      Finastra International +
     A                                      Limited 1997')
     A  30        L#CNUM         6A        1SPACEB(2)
     A  30        L#CRNM        20A        8SPACEA(1)
     A            L#SESN        10A       10SPACEB(1)
     A            L#SITP         3A       24
      ** Start of changes for LLN012
     A**********  L#BPBK         2A       32                                                  LLN012
     A            L#BPBK         2A       31
      ** End of changes for LLN012
     A            L#MKCD         1A       36
     A            L#VDAT         7A       41
      ** Start of changes for LLN012
     A**********  L#LISO         2A       53                                                  LLN012
     A**********  L#RISO         2A       58                                                  LLN012
     A**********  L#LIND         3A       64                                                  LLN012
     A**********  L#LINS         2A       71                                                  LLN012
     A**********  L#CYCD         3A       77                                                  LLN012
     A**********  L#CUAS        17A       82                                                  LLN012
     A**********  L#SGAM        17A      105                                                  LLN012
     A**********  L#PCOD         4S 0    127                                                  LLN012
     A            L#LISO         2A       50
     A            L#RISO         2A       55
     A            L#LIND         3A       59
     A            L#LINS         2A       66
     A            L#CYCD         3A       70
     A            L#CUAS        17A       77
     A            L#SGAM        17A       96
     A            L#PCOD         4S 0    115
     A            L#BTEX         1A      122
     A            L#LREX         1A      127
      ** End of changes for LLN012
      *
      *
      *  Invalid extract details
     A  41                                 2'Missing Extract Details'
     A                                      SPACEB(1)
      *
      *
     A          R BY2050E1                  TEXT('(c) +
     A                                      Finastra International +
     A                                      Limited 1997')
     A                                      SPACEB(02)
     A                                    50'***  No Details To Report  ***'
      *
      *
     A          R BY2050R1                  TEXT('(c) +
     A                                      Finastra International +
     A                                      Limited 1997')
     A                                      SKIPB(3)
     A                                     6'Reference BY2050P1'
     A            BJURPT        53A       39
     A                                   100'Date'
     A            BJMRDT         7A      105
     A                                   119'Page'
     A            PAGE           4  0    124
     A                                      EDTCDE(4)
     A                                      SPACEA( 2)
      *
      *
     A                                    38'Securities Extract As At '
     A            L#RPDA         7A       63
     A                                    70' - Currency Summary'
     A                                      SPACEA( 1)
     A                                    38'-------------------------'
     A                                    63'-------'
     A                                    70'-------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 6'Branch'
     A  20        L#BRCD         3A       13
     A  20        L#BRNM        30A       17
     A  20                                  SPACEA(2)
      *
     A                                     2'Currency'
     A                                    14'Currency'
     A                                    31'Total Assets'
     A                                    55'Total Assets'
     A                                    81'Total Liabilities'
     A                                   105'Total Liabilities'
     A                                      SPACEA( 1)
     A                                     4'Code'
     A                                    16'Name'
     A                                    32'(Currency)'
     A                                    52'(Sterling Equivalent)'
     A                                    84'(Currency)'
     A                                   103'(Sterling Equivalent)'
     A                                      SPACEA( 1)
      *
     A          R BY2050S1                  TEXT('(c) +
     A                                      Finastra International +
     A                                      Limited 1997')
     A                                      SPACEB( 1)
     A            L#CYCD         3A        4TEXT('Currency')
     A            L#CYNM        14A       12TEXT('Currency Name')
     A            L#TLAC        17A       29TEXT('Total Assets - Currency')
     A            L#TLAS        17A       52TEXT('Total Assets - Sterling')
     A            L#TLLC        17A       80TEXT('Total Liabilities - Currency')
     A            L#TLLS        17A      103TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY2050T1                  TEXT('(c) +
     A                                      Finastra International +
     A                                      Limited 1997')
     A                                      SPACEB(3)
     A                                     3'TotaL'
     A            L#TOTA        21A       48TEXT('Total Assets - Sterling')
     A            L#TOTL        21A       99TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY2050Z1                  TEXT('(c) +
     A                                      Finastra International +
     A                                      Limited 1997')
     A                                      SPACEB(2)
     A                                    53'***  End of Report  ***'
      *
