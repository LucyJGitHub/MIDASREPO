/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Securities Extract - Invalid Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England Returns Module                       *
      *                                                               *
      *  BY2050P2 - Securities Extract/Validation report              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 1997            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. nnnnnn           Date ddmmmyy                 *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment Details                                            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY2050H2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1997')
     A                                      SKIPB(03)
     A                                     6'Reference BY2050P2'
     A            BJURPT        53A       39
     A                                   100'Date'
     A            BJMRDT         7A      105
     A                                   119'Page'
     A            PAGE2          4  0    124
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A                                    38'Securities Extract As At '
     A            L#RPDA         7A       63
     A                                    70' - Invalid Securities'
     A                                      SPACEA( 1)
     A                                    38'-------------------------'
     A                                    63'-------'
     A                                    70'---------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 6'Branch'
     A  20        L#BRCD         3A       13
     A  20        L#BRNM        30A       17
     A  20                                  SPACEA(2)
      *
     A                                    28'Issuer'
     A                                    37'ISO'
     A                                    45'Risk'
     A                                    55'ISO'
     A                                    64'Ind.'
     A                                    69'Inst'
     A                                      SPACEA(1)
      *
     A                                     1'Issuer'
     A                                    28'Country'
     A                                    37'Code'
     A                                    45'Country'
     A                                    55'Code'
     A                                    64'Code'
     A                                    69'Code'
     A                                    80'Errors in Customer Details'
     A                                      SPACEA(1)
      *
     A                                     1'------'
     A                                    28'-------'
     A                                    37'----'
     A                                    45'-------'
     A                                    55'----'
     A                                    64'----'
     A                                    69'----'
     A                                    80'--------------------------'
      *
      *  Details
     A          R BY2050D2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A            L#CNUM         6A        1SPACEB(2)
     A            L#CRNM        20A        8
     A            L#COLC         2A       30
     A            L#LISO         2A       38
     A            L#CNCZ         2A       46
     A            L#RISO         2A       56
     A            L#LIND         3A       65
     A            L#LINS         2A       70
      *
      *
      *  Invalid Customer Details
     A  31                                80'ISO code for Customer +
     A                                      Country is invalid'
     A                                      SPACEA(1)
      *
     A  32                                80'ISO code for Country  +
     A                                      of Risk is invalid'
     A                                      SPACEA(1)
      *
     A  33                                80'Institution code is not a +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  34                                80'Industry code is not a  +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  35                                80'Institution code must be +
     A                                      for a UK resident'
     A                                      SPACEA(1)
      *
     A  36                                80'Institution code must be +
     A                                      for a non UK resident'
     A                                      SPACEA(1)
      *
     A  37                                80'Industry code must exist +
     A                                      if institution code is'
     A                                      SPACEA(1)
     A  37                                80'for UK non-monetary sector'
     A                                      SPACEA(1)
      *
      *
     A                                     1'Extract details'
     A                                      SPACEB(1)
     A                                    30'Inv.'
     A                                    35'Book'
     A                                    46'Value'
     A                                    56'Maturity'
     A                                   114'Sterling'
     A                                   126'Product'
     A                                      SPACEA(1)
      *
     A                                    16'Security'
     A                                    30'Type'
     A                                    35'Code'
     A                                    40 'Mkt'
     A                                    47'Date'
     A                                    57'Date'
     A                                    77'Ccy'
     A                                    86'Book position'
     A                                   112'Equivalent'
     A                                   127'Code'
     A                                      SPACEA(1)
      *
     A          R BY2050D3                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A            L#SESN        10A       16SPACEB(1)
     A            L#SITP         3A       31
     A            L#BPBK         2A       36
     A            L#MKCD         1A       41
     A            L#VDAT         7A       45
     A            L#MDAT         7A       56
     A            L#CYCD         3A       77
     A            L#CUAS        17A       82
     A            L#SGAM        17A      105
     A            L#PCOD         4S 0    127
      *
      *  Invalid extract details
     A  41                                10'No Extract Criteria'
     A                                      SPACEB(1)
      *
      *
      *  No Details to Report
     A          R BY2050E2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                      SPACEB(3)
     A                                    52'***  No Details To +
     A                                      Report  ***'
      *
      *  Database Error
     A          R BY2050F2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                       SPACEB(2)
     A                                    47'DATABASE ERROR'
     A            DBASER    R             62REFFLD(DBASE)
     A                                    66'IN PROGRAM'
     A            DBPGM     R             77
     A                                    47'FILE'
     A                                       SPACEB(2)
     A            DBFILE    R             52
     A                                    61'KEY'
     A            DBKEY     R             65
      *
      *
      *  End of Report
     A          R BY2050Z2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                      SPACEB(2)
     A                                    55'***  End of Report  ***'
      *
