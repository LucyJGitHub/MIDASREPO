/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas BoE Securities Trades Extract - Valid Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY2055P1 - Securities Trades Extract - Valid Report          *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN012  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  LLN012 - CREATE                                              *
      *           Financial Services Authority - Liquidity Report     *
      *****************************************************************
      *
     A          R BY2055H1                  TEXT('(C) Copyright +
     A                                      Midas Kapiti International+
     A                                       Limited 1999')
     A                                      SKIPB(03)
     A                                     6'Reference BY2055P1'
     A            BJURPT        53A       39
     A                                   100'Date'
     A            BJMRDT         7A      105
     A                                   119'Page'
     A            PAGE1          4  0    124
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    39'Securities Trades Extract'
     A                                    +1'As At'
     A            L#RPDA         7A       +1
     A                                    +1'- Valid Trades'
     A                                      SPACEA(1)
     A                                    39'-------------------------'
     A                                    +0'------'
     A                                    +0'--------'
     A                                    +0'---------------'
     A                                      SPACEA(2)
      *
     A  20                                 3'Branch'
     A  20        L#BRCD         3A       12
     A  20        L#BRNM        30A       16
     A  20                                  SPACEA(2)
      *
     A                                    18'Trade'
     A                                    26'Trade'
     A                                    37'Trade'
     A                                    46'Value'
     A                                    56'Cust'
     A                                    63'Risk'
     A                                    70'Ind.'
     A                                    76'Inst.'
     A                                    95'Currency'
     A                                   112'Sterling'
     A                                   123'Product'
     A                                      SPACEA(1)
      *
     A                                     3'Customer'
     A                                    18'Ref.'
     A                                    26'Type'
     A                                    37'Date'
     A                                    47'Date'
     A                                    56'Ctry'
     A                                    63'Ctry'
     A                                    70'Code'
     A                                    76'Code'
     A                                    82'Ccy'
     A                                    97'Amount'
     A                                   110'Equivalent'
     A                                   124'Code'
      *
      *
      *  Details
     A          R BY2055D1                  TEXT('(C) Copyright +
     A                                      Midas Kapiti International+
     A                                       Limited 1999')
     A  30        L#CNUM         6A        3SPACEB(2)
     A  30        L#CRNM        20A       11SPACEA(1)
     A            L#TRAN         6A       18SPACEB(1)
     A            L#TDTP         2A       28
     A            L#TDAT         7A       36
     A            L#VDAT         7A       46
     A            L#LISO         2A       57
     A            L#RISO         2A       64
     A            L#INDC         3A       71
     A            L#INSC         2A       77
     A            L#CYCD         3A       82
     A            L#CUAS        17A       87
     A            L#SGAM        17A      105
     A            L#PCOD         4S 0    124
      *
      *
      *  Invalid extract details
     A  41                                 2'Missing Extract Details'
     A                                      SPACEB(1)
      *
      *
     A          R BY2055E1                  TEXT('(C) Copyright +
     A                                      Midas Kapiti International+
     A                                       Limited 1999')
     A                                      SPACEB(02)
     A                                    50'***  No Details To Report  ***'
      *
     A          R BY2055Z1                  TEXT('(C) Copyright +
     A                                      Midas Kapiti International+
     A                                       Limited 1999')
     A                                      SPACEB(2)
     A                                    53'***  End of Report  ***'
      *
