/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas BoE Securities Trades Extract - Invalid Rpt.')   *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England Returns Module                       *
      *                                                               *
      *  BY2055P2 - Securities Trades Extract - Valid Report          *
      *                                                               *
      *  (C) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG7473            Date 03Jun15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 LLN012  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG7473- Trade reference can be a security shortname or      *
      *           trade number. Change the field from 6A to 10A.      *
      *  LLN012 - CREATE                                              *
      *           Financial Services Authority - Liquidity Report     *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY2055H2                  TEXT('(C) Copright +
     A                                      Finastra International +
     A                                      Limited 1999')
     A                                      SKIPB(03)
     A                                     6'Reference BY2055P2'
     A            BJURPT        53A       39
     A                                   100'Date'
     A            BJMRDT         7A      105
     A                                   119'Page'
     A            PAGE2          4  0    124
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A                                    38'Securities Trades Extract'
     A                                    +1'As At'
     A            L#RPDA         7A       +1
     A                                    +1' - Invalid Trades'
     A                                      SPACEA( 1)
     A                                    38'-------------------------'
     A                                    +0'------'
     A                                    +0'--------'
     A                                    +0'------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 3'Branch'
     A  20        L#BRCD         3A       12
     A  20        L#BRNM        30A       18
     A  20                                  SPACEA(2)
      *
     A                                    36'Cust'
     A                                    44'Risk'
     A                                    52'Ind.'
     A                                    60'Inst.'
     A                                      SPACEA(1)
      *
     A                                     3'Customer'
     A                                    36'Ctry'
     A                                    44'Ctry'
     A                                    52'Code'
     A                                    60'Code'
     A                                    69'Errors in Customer Details'
     A                                      SPACEA(1)
      *
      *  Details
     A          R BY2055D2                  TEXT('(C) Copyright +
     A                                      Finastra International +
     A                                       Limited 1999')
     A            L#CNUM         6A        3SPACEB(2)
     A            L#CRNM        20A       11
     A            L#CISO         2A       37
     A            L#LISO         2A       45
     A            L#INDC         3A       52
     A            L#INSC         2A       61
      *
      *  Invalid Customer Details
     A  31                                69'ISO code for Customer +
     A                                      Country is invalid'
     A                                      SPACEA(1)
      *
     A  32                                69'ISO code for Country  +
     A                                      of Risk is invalid'
     A                                      SPACEA(1)
      *
     A  33                                69'Institution code is not a +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  34                                69'Industry code is not a  +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  35                                69'Institution code must be +
     A                                      for a UK resident'
     A                                      SPACEA(1)
      *
     A  36                                69'Institution code must be +
     A                                      for a non UK resident'
     A                                      SPACEA(1)
      *
     A  37                                69'Industry code must exist +
     A                                      if institution code is'
     A                                      SPACEA(1)
     A  37                                69'for UK non-monetary sector'
     A                                      SPACEA(1)
      *
      *
     A                                    17'Extract details:'
     A                                      SPACEB(1)
     A                                    36'Trade'
     A                                    44'Trade'
     A                                    55'Trade'
     A                                    64'Value'
     A                                    86'Currency'
     A                                   104'Sterling'
     A                                   115'Product'
     A                                      SPACEA(1)
      *
     A                                    36'Ref.'
     A                                    44'Type'
     A                                    55'Date'
     A                                    64'Date'
     A                                    74'Ccy'
     A                                    88'Amount'
     A                                   102'Equivalent'
     A                                   116'Code'
     A                                      SPACEA(1)
      *
     A          R BY2055D3                  TEXT('(C) Copyright +
     A                                      Finastra International +
     A                                       Limited 1999')
      **Start of change BUG7473
     A**********  L#TRAN         6A       36SPACEB(1)                                        BUG7473
     A            L#TRAN        10A       32SPACEB(1)
      **Start of change BUG7473
     A            L#TDTP         2A       45
     A            L#TDAT         7A       54
     A            L#VDAT         7A       64
     A            L#CYCD         3A       74
     A            L#CUAS        17A       79
     A            L#SGAM        17A       97
     A            L#PCOD         4S 0    116
      *
      *  Invalid extract details
     A  41                                10'No Extract Criteria'
      *
      *
      *  No Details to Report
     A          R BY2055E2                  TEXT('(C) Copyright +
     A                                      Finastra International +
     A                                      Limited 1999')
     A                                      SPACEB(3)
     A                                    52'***  No Details To +
     A                                      Report  ***'
      *
      *  Database Error
     A          R BY2055F2                  TEXT('(C) Copyright +
     A                                      Finastra International +
     A                                      Limited 1999')
     A                                       SPACEB(2)
     A                                    47'DATABASE ERROR'
     A            DBASER    R             62REFFLD(DBASE)
     A                                    66'IN PROGRAM'
     A            DBPGM     R             77
     A                                    47'FILE'
     A                                       SPACEB(2)
     A            DBFILE    R             52
     A                                    61'KEY'
     A            DBKEY     R             65
      *
      *
      *  End of Report
     A          R BY2055Z2                  TEXT('(C) Copright +
     A                                      Finastra International +
     A                                      Limited 1999')
     A                                      SPACEB(2)
     A                                    55'***  End of Report  ***'
      *
