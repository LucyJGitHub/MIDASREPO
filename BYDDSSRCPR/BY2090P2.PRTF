/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('LR Maturity Data List Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY2090P2 - LR Maturity Data Report                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 1999            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN022             Date 24Jan05               *
      *  Prev Amend No. 167631             Date 07Oct99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment Details                                            *
      *  -----------------                                            *
      *                                                               *
      *  LLN022 - BOE Upgrade to Midas Plus.                          *
      *  167631 - Change Page Numbering field to PAGE2.               *
      *                                                               *
      *****************************************************************
      *
      **  Report Heading
     A          R BY2090H2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1999')
     A                                      SKIPB(03)
     A                                     1'Reference BY2090P2'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A**********  PAGE1          4  0    126                                                  167631
     A            PAGE2          4  0    126                                                  167631
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
      *
     A                                    50'LR Maturity Data Report'
     A                                      SPACEA(1)
     A                                    50'-----------------------'
     A                                      SPACEA(2)
      *
     A                                     1'Customer'
     A                                    25'Module'
     A                                    35'Transaction'
     A                                    51'Event'
     A                                    61'Report'
     A                                    90'Cashflow'
     A                                   110'Sterling'
     A                                   123'Product'
     A                                      SPACEA(1)
      *
     A                                     1'Report Name'
     A                                    27'Id.'
     A                                    35'Reference'
     A                                    52'Date'
     A                                    61'Category'
     A                                    75'Ccy'
     A                                    92'Amount'
     A                                   108'Equivalent'
     A                                   124'Code'
     A                                      SPACEA(2)
      *
      *
      *  Report Details
     A          R BY2090D2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1999')
     A            L#CRNM        20A        1
     A            L#MMOD         2A       27
      *
      **Start of change LLN022
     A**********  L#TRAN        10A       35                                                  LLN022
     A            L#TRAN        12A       35
      **End of change LLN022
      *
     A            L#MDAT         7A       50
     A            L#MCAT         7A       64
     A            L#CYCD         3A       75
     A            L#CUAS        17A       82
     A  31                                99'CR'
     A            L#SGAM        17A      102
     A  31                               119'CR'
     A            L#PCOD         4S 0    124SPACEA(1)
      *
      *
      **  No Details To Report
     A          R BY2090E2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                      SPACEB(3)
     A                                    50'***  No Details To Report  ***'
      *
      **  End of Report
     A          R BY2090Z2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                      SPACEB(3)
     A                                    53'***  End of Report  ***'
      *
