      *****************************************************************
/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('BoE Account Extract Details Print')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of Engalnd                                      *
      *                                                               *
      *  PRTF/BY3010P1 - Account Extract Details Print                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 1997            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN022             Date 25Jan05               *
      *  Prev Amend No. LLN021             Date 19Nov03               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  LLN022 - BOE Upgrade to Midas Plus.                          *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ***  Report Header Record Format ***
      *
     A          R BY3010H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1997')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BY3010P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'ACCOUNT EXTRACT CRITERIA'
     A                                    71'-'
     A            L#MODE         7        73TEXT('Run mode')
     A                                      SPACEA(1)
     A                                    46'------------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings ***
      *
     A                                     1'ACCOUNT'
      *
      **Start of change LLN022
     A**********                          11'INST.'                                           LLN022
     A                                    12'INST.'
      **End of change LLN022
      *
     A                                    19'INDY.'
     A                                    87'OVERDUE'
     A                                    98'CURRENCY'
     A                                   109'MATURITY'
     A                                      SPACEA(1)
     A                                     2'CODE'
      *
      **Start of change LLN022
     A**********                          11'CODE'                                            LLN022
     A                                    12'CODE'
      **End of change LLN022
      *
     A                                    19'CODE'
     A                                    27'PRODUCT CODE'
     A                                    86'INDICATOR'
     A                                    98'OVERRIDE'
     A                                   109'CATEGORY'
     A  40                               121'ACTION'
     A                                   132' '
     A                                      SPACEA(2)
      *
      ***  Detail Record Format ***
      *
     A          R BY3010D1
      *
      **Start of change LLN022
     A**********  L#ACOD         4  0      2TEXT('Account Code')                              LLN022
     A            L#ACOD        10  0      2TEXT('Account Code')
     A**********  L#LINS         2  0     12TEXT('Institution Code')                          LLN022
     A            L#LINS         2  0     13TEXT('Institution Code')
      **End of change LLN022
      *
     A            L#MIND         3A       20TEXT('Institution Code')
     A                                      SPACEA(1)
     A                                    19'Debit :'
     A            L#PCDR         4  0     27TEXT('Product code - Debit')
     A                                    32'-'
     A            L#DES1        50        34TEXT('Product code narrative')
     A            L#ODIN         1A       90TEXT('Overdue Indicator')
     A            L#CCYI         1A      101TEXT('Currency Override')
     A            L#MCAT         1A      112TEXT('Maturity Category')
     A  40        L#CHTP         8       120TEXT('Last Change Type')
                                         132' '
     A                                      SPACEA(1)
      *
     A                                    19'Credit:'
     A            L#PCCR         4  0     27TEXT('Product code - Credit')
     A                                    32'-'
     A            L#DES2        50        34TEXT('Product code narrative')
     A                                      SPACEA(1)
      *
     A                                    19'Supp  :'
     A            L#PCSP         4  0     27TEXT('Product code - Supplementary')
     A                                    32'-'
     A            L#DES3        50        34TEXT('Product code narrative')
     A                                      SPACEA(2)
      *                                                                                       LLN021
     A  88                                19'PL Dr :'                                         LLN021
     A  88        L#PLDR         4  0     27TEXT('PL Product code - Debit')                   LLN021
     A  88                                32'-'                                               LLN021
     A  88        L#DES4        50        34TEXT('Product code narrative')                    LLN021
     A  88                                  SPACEA(1)                                         LLN021
      *                                                                                       LLN021
     A  88                                19'PL Cr :'                                         LLN021
     A  88        L#PLCR         4  0     27TEXT('PL Product code - Credit')                  LLN021
     A  88                                32'-'                                               LLN021
     A  88        L#DES5        50        34TEXT('Product code narrative')                    LLN021
     A  88                                  SPACEA(2)                                         LLN021
      *
      *** Detail Record Format 2 ***
      *
     A          R BY3010D2
     A                                    20'NO DETAILS TO REPORT.'
      *
      ***  Report footer *
      *
     A          R BY3010F1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
