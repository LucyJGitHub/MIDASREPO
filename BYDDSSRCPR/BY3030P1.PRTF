      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('BoE Loans Extract Details Print')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of Engalnd                                      *
      *                                                               *
      *  PRTF/BY3030P1 - Loans Extract Details Print                  *
      *                                                               *
      *  (c) Finastra International Limited 1997                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN021             Date 19Nov03               *
      *                 LLN012             Date 30Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *  LLN012 - Additional BoE Changes (Patch B)                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ***  Report Header Record Format ***
      *
     A          R BY3030H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      1997')
     A                                      SKIPB(3)
     A                                     6'REFERENCE BY3030P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    39'LOANS EXTRACT +
     A                                      CRITERIA DETAILS'
     A                                    70'-'
     A            L#MODE         7A       72TEXT('Run mode')
     A                                      SPACEA(1)
     A                                    39'--------------+
                                            ----------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings ***
      *
     A                                    15'LOAN'
     A                                    23'SUB'
     A                                    32'INST.'
     A                                      SPACEA(1)
                                          15'TYPE'
     A                                    23'TYPE'
     A                                    32'CODE'
     A                                    40'PRODUCT CODE'
     A  40                               101'ACTION'
     A                                   109'NON PERF'                                        LLN012
     A                                   132' '
     A                                      SPACEA(2)
      *
      ***  Detail Record Format ***
      *
     A          R BY3030D1
     A            L#LTYP         2A       16TEXT('Loan Type')
     A            L#LNST         2A       24TEXT('Loan Sub-Type')
     A            L#LINS         2  0     33TEXT('Institution Code')
     A            L#PCOD         4  0     40TEXT('Product Code')
     A                                    45'-'
     A            L#DESC        50        47TEXT('Product code narrative')
     A  40        L#CHTP         8       100TEXT('Last Change Type')
     A*** LLN012 Start
     A            L#NPER         1       109TEXT('Non Performing Loan')
     A*** LLN012 End
                                         132' '
     A                                      SPACEA(1)
     A  87                                27'PL Credit :'                                     LLN021
     A  87                                  SPACEB(1)                                         LLN021
     A  87        L#PLCR         4  0     40TEXT('PL CR Product Code')                        LLN021
     A  87                                45'-'                                               LLN021
     A  87        L#DES2        50        47TEXT('PL CR Product code narrative')              LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                27'PL Charges:'                                     LLN021
     A  87        L3PLCG         4  0     40TEXT('PL Charges Product Code')                   LLN021
     A  87                                45'-'                                               LLN021
     A  87        L#DES3        50        47TEXT('PL Charges Prod. code narr.')               LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                27'PL Tax    :'                                     LLN021
     A  87        L#PLTX         4  0     40TEXT('PL Tax Product Code')                       LLN021
     A  87                                45'-'                                               LLN021
     A  87        L#DES4        50        47TEXT('PL Tax Prod. code narrative')               LLN021
     A  87                                  SPACEA(2)                                         LLN021
      *
      *** Detail Record Format 2 ***
      *
     A          R BY3030D2
     A                                    49'NO DETAILS TO REPORT.'
      *
      ***  Report Footer ***
      *
     A          R BY3030F1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
