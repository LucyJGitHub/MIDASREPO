      *****************************************************************
/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('BoE Facility Extract Details Print')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of Engalnd                                      *
      *                                                               *
      *  PRTF/BY3040P1 - Deals Extract Details Print                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 1997            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN021             Date 19Nov03               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ***  Report Header Record Format ***
      *
     A          R BY3040H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      1997')
     A                                      SKIPB(3)
     A                                     6'REFERENCE BY3040P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'FACILITY EXTRACT CRITERIA'
     A                                    72'-'
     A            L#MODE         7        74TEXT('Run mode')
     A                                      SPACEA(1)
     A                                    46'------------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings ***
      *
     A                                    20'FACILITY'
     A                                    31'INST.'
     A                                      SPACEA(1)
     A                                    22'TYPE'
     A                                    31'CODE'
     A                                    39'PRODUCT CODE'
     A  40                               100'ACTION'
     A                                   132' '
     A                                      SPACEA(2)
      *
      ***  Detail Record Format ***
      *
     A          R BY3040D1
     A            L#FCTP         3A       23TEXT('Facility Type')
     A            L#LINS         2  0     32TEXT('Institution Code')
     A            L#PCOD         4  0     39TEXT('Product Code')
     A                                    44'-'
     A            L#DESC        50        46TEXT('Product code narrative ')
     A  40        L#CHTP         8        99TEXT('Last Change Type')
     A                                   132' '
     A                                      SPACEA(1)
     A  87                                26'PL Charges:'                                     LLN021
     A  87                                  SPACEB(1)                                         LLN021
     A  87        L#PLCG         4  0     39TEXT('PL Charges Product Code')                   LLN021
     A  87                                44'-'                                               LLN021
     A  87        L#DES2        50        46TEXT('PL Charges Prod. code narr.')               LLN021
     A  87                                  SPACEA(2)                                         LLN021
      *
      *** Detail Record Format 2 ***
      *
     A          R BY3040D2
     A                                    20'NO DETAILS TO REPORT.'
      *
      ***  Report Footer *
      *
     A          R BY3040F1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
