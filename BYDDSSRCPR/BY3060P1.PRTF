      *****************************************************************
/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas BoE Composite Currencies Details Print')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of Engalnd                                      *
      *                                                               *
      *  PRTF/BY3060P1 - Composite Currencies Details File            *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems 2005       *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ***  Report Header Record Format ***
      *
     A          R BY3060H1                  TEXT('(C) Midas Kapiti +
     A                                      International Ltd 1997')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BY3060P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'COMPOSITE CURRENCIES +
     A                                      DETAILS'
     A                                    75'-'
     A            L#MODE         7        77TEXT('Run mode')
     A                                    85'REPORT'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'--------------------+
     A                                      -------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings ***
      *
     A                                     5'CURRENCY'
     A                                    28'CURRENCY PERCENTAGE'
     A                                    50'CURRENCY PERCENTAGE'
     A                                    73'CURRENCY PERCENTAGE'
     A                                    96'CURRENCY PERCENTAGE'
     A  40
     AA 41                               125'ACTION'
     A                                   132' '
     A                                      SPACEA(2)
      *
      ***  Detail Record Format ***
      *
     A          R BY3060D1
     A  41        L#CCCY         3A        5TEXT('Currency code')
     A  41                                 9'-'
     A  41        L#CCNM        14A       11TEXT('Currency name')
     A            L#CCY1         3A       30TEXT('Composite ccy 1')
     A            L#PCT1         5  2     38TEXT('Composite ccy % 1')
     A                                      EDTCDE(2)
     A            L#CCY2         3A       52TEXT('Composite ccy 2')
     A            L#PCT2         5  2     60TEXT('Composite ccy % 2')
     A                                      EDTCDE(2)
     A            L#CCY3         3A       75TEXT('Composite ccy 3')
     A            L#PCT3         5  2     84TEXT('Composite ccy % 3')
     A                                      EDTCDE(2)
     A            L#CCY4         3A       98TEXT('Composite ccy 4')
     A            L#PCT4         5  2    106TEXT('Composite ccy % 4')
     A                                      EDTCDE(2)
     A  40
     AA 41        L#CHTP         8       124TEXT('Last change type')
     A                                   132' '
     A                                      SPACEA(1)
      *
      *** Detail Record Format 2 ***
      *
     A          R BY3060D2
     A                                    49'NO DETAILS TO REPORT.'
      *
      *** Space before next record ***
      *
     A          R BY3060S1
     A                                     1' '
     A                                      SPACEA(1)
      *
      ***  Report Footer *
      *
     A          R BY3060F1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
