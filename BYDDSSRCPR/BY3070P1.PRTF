      *****************************************************************
/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas BoE Futures and Options Extract Print')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  PRTF/BY3070P1 - Futures and Options Extract Details Print    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN021  *CREATE    Date 08Jan04               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ***  Report Header Record Format ***
      *
     A          R BY3070H1                  TEXT('(C) Finastra +
     A                                      International Ltd 1997')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE BY3070P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'FUTURES & OPTIONS'
     A                                    58'EXTRACT CRITERIA'
     A                                    75'-'
     A            L#MODE         7        78TEXT('Run mode')
     A                                      SPACEA(1)
     A                                    40'------------------'
     A                                    58'----------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings ***
      *
     A                                    19'BOOK'
     A                                    25'MARKET'
     A                                    33'INSTR.'
     A                                    41'INST.'
     A                                    48'INDY.'
     A                                      SPACEA(2)
     A                                    19'CODE'
     A                                    25'CENTRE'
     A                                    33'TYPE'
     A                                    41'CODE'
     A                                    48'CODE'
     A  40                               102'ACTION'
     A                                   132' '
     A                                      SPACEA(2)
      *
      ***  Detail Record Format ***
      *
     A          R BY3070D1
     A            L#BOOK         2A       20TEXT('Book Code')
     A            L#MKCC         2A       26TEXT('Mkt Centre Code')
     A            L#INST         3A       34TEXT('Instrument type')
     A            L#LINS         2  0     42TEXT('Institution Code')
     A            L#MIND         3A       48TEXT('Industry Code')
     A  40        L#CHTP         8       101TEXT('Last Change Type')
     A                                   132' '
     A                                      SPACEA(1)
     A  87                                54'Accr. Int Receivable :'                          LLN021
     A  87        L#ACIR         4  0     78TEXT('Accr Int Receivable')                       LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                54'Accr. Int Payable    :'                          LLN021
     A  87        L#ACIP         4  0     78TEXT('Accr Int Payable')                          LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                54'Profit & Loss        :'                          LLN021
     A  87        L#PRLS         4  0     78TEXT('Profit and Loss')                           LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                54'Credit Charges       :'                          LLN021
     A  87        L#CCHG         4  0     78TEXT('Credit Charges')                            LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                54'Credit Fee Type      :'                          LLN021
     A  87        L#CFEE         2A       78TEXT('Credit Fee Type')                           LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                54'Debit Charges        :'                          LLN021
     A  87        L#DCHG         4  0     78TEXT('Debit Charges')                             LLN021
     A  87                                  SPACEA(1)                                         LLN021
     A  87                                54'Debit Fee Type       :'                          LLN021
     A  87        L#DFEE         4  0     78TEXT('Debit Fee Type')                            LLN021
     A  87                                  SPACEA(2)                                         LLN021
      *
      *** Detail Record Format 2 ***
      *
     A          R BY3070D2
     A                                    20'NO DETAILS TO REPORT.'
      *
      ***  Report Footer ***
      *
     A          R BY3070F1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
