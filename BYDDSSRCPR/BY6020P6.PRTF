/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Journal Extract - Invalid Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England Returns Module                       *
      *                                                               *
      *  BY6020P6 - Journal Extract/Validation - Invalid Entries      *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2005            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN022             Date 25Jan05               *
      *                 LLN021             Date 02Nov03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN022 - BOE Upgrade to Midas Plus.                          *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY6020H6                  TEXT('(C) Copright +
     A                                      Finastra IBS Ltd 2003')
     A                                      SKIPB(03)
     A                                     1'Reference BY6020P6'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE6          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  21                                41'Journal Extract '
     A N21                                38'Journal Validation '
     A                                    57'As At '
     A            L#RPDA         7A       63
     A                                    70' - Invalid Entries'
     A                                      SPACEA( 1)
     A  21                                41'-----------------'
     A N21                                38'-------------------'
     A                                    57'------'
     A                                    63'-------'
     A                                    70'------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                    30'Customer'
     A                                    41'ISO'
     A                                    49'Risk'
     A                                    59'ISO'
     A                                    66'Ind.'
     A                                    73'Inst'
     A                                      SPACEA(1)
      *
     A                                     1'Customer'
     A                                    30'Country'
     A                                    41'Code'
     A                                    49'Country'
     A                                    59'Code'
     A                                    66'Code'
     A                                    73'Code'
     A                                    80'Errors in Customer Details'
     A                                      SPACEA(1)
      *
     A                                     1'--------'
     A                                    30'-------'
     A                                    41'----'
     A                                    49'-------'
     A                                    59'----'
     A                                    66'----'
     A                                    73'----'
     A                                    80'--------------------------'
      *
      *  Details
     A          R BY6020D6                  TEXT('(C) Copyright +
     A                                      Finastra IBS Ltd 2003')
     A            L#CNUM         6A        1SPACEB(2)
     A            L#CRNM        20A        8
     A            L#COLC         2A       32
     A            L#LISO         2A       42
     A            L#CNCZ         2A       50
     A            L#RISO         2A       60
     A            L#LIND         3A       66
     A            L#LINS         2A       74
      *
      *
      *  Invalid Customer Details
     A  31                                80'ISO code for Customer +
     A                                      Country is invalid'
     A                                      SPACEA(1)
      *
     A  32                                80'ISO code for Country  +
     A                                      of Risk is invalid'
     A                                      SPACEA(1)
      *
     A  33                                80'Institution code is not a +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  34                                80'Industry code is not a +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  35                                80'Institution code must be +
     A                                      for a UK resident'
     A                                      SPACEA(1)
      *
     A  36                                80'Institution code must be +
     A                                      for a non UK resident'
     A                                      SPACEA(1)
      *
     A  37                                80'Industry code must exist +
     A                                      if institution code is'
     A                                      SPACEA(1)
     A  37                                80'for UK non-monetary sector'
     A                                      SPACEA(1)
      *
      * Following line forces a blank line to be printed on overflow
     A  30                                 1' '
     A                                      SPACEB(1)
      *
     A                                    10'Extract Details'
     A                                      SPACEB(1)
     A                                   114'Sterling'
     A                                   126'Product'
     A                                      SPACEA(1)
      *
     A                                    30'Account'
     A                                    77'Ccy'
     A                                    93'Amount'
     A                                   112'Equivalent'
     A                                   127'Code'
     A                                      SPACEA(1)
      *
     A          R BY6020D7                  TEXT('(C) Copyright +
     A                                      Finastra IBS Ltd 2003')
     A                                      SPACEB(1)
      *
      *  Could not find valid product code
     A  38                                 2'Prod. code not found'
      *
      *  Invalid extract details
     A  41                                 2'No Extract Criteria'
      *
     A            L#CNUM         6A       30
     A                                    36'-'
     A            L#CYCD         3A       37
     A                                    40'-'
      *
      **Start of change LLN022
     A**********  L#ACOD         4A       41                                                  LLN022
     A            L#ACOD        10A       41
     A**********                          45'-'                                               LLN022
     A                                    51'-'
     A**********  L#ACSQ         2A       46                                                  LLN022
     A            L#ACSQ         2A       52
      **End of change LLN022
      *
     A            L#CCYD         3A       77
     A            L#CUAS        17A       82
     A            L#SGAM        17A      105
     A            L#PCOD         4S 0    127
      *
      *
      *  No Details to Report
     A          R BY6020E6                  TEXT('(C) Copyright +
     A                                      Finastra IBS Ltd')
     A                                      SPACEB(3)
     A                                    52'***  No Details To +
     A                                      Report  ***'
      *
      *  Database Error
     A          R BY6020F6                  TEXT('(C) Copyright +
     A                                      Finastra IBS Ltd 2003')
     A                                       SPACEB(2)
     A                                    47'DATABASE ERROR'
     A            DBASER    R             62REFFLD(DBASE)
     A                                    66'IN PROGRAM'
     A            DBPGM     R             77
     A                                    47'FILE'
     A                                       SPACEB(2)
     A            DBFILE    R             52
     A                                    61'KEY'
     A            DBKEY     R             65
      *
      *
      *  End of Report
     A          R BY6020Z6                  TEXT('(C) Copright +
     A                                      Finastra IBS Ltd 2003')
     A                                      SPACEB(3)
     A                                    55'***  End of Report  ***'
      *
