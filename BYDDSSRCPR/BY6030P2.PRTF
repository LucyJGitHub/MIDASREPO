/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Deals PL Extract - Valid Deals Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6030P2 - PL Deals Extract/Validation Invalid Deals Report  *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN021             Date 13Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY6030H2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SKIPB(03)
     A                                     1'Reference BY6030P2'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE1          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  21                                41'PL Deal Extract '
     A N21                                38'PL Deal Validation '
     A                                    57'As At'
     A            L#RPDA         7A       63
     A                                    71'- Invalid Deals Report'
     A                                      SPACEA( 1)
     A  21                                41'----------------'
     A N21                                39'-------------------'
     A                                    57'------ '
     A                                    63'--------'
     A                                    71'--------------------'
     A                                      SPACEA( 2)
      *
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                    17'Deal'
     A                                    24'Deal'
     A                                    29'Deal/Val'
     A                                    38'Val/Mat'
     A                                    46'Customer'
     A                                    56'Risk'
     A                                    62'Ind.'
     A                                    67'Inst'
     A                                    74'Ccy'
     A                                    80'Currency'
     A                                   103'Sterling'
     A                                   117'Product'
     A                                      SPACEA(1)
      *
     A                                     1'Customer'
     A                                    16'Number'
     A                                    24'Type'
     A                                    31'Date'
     A                                    40'Date'
     A                                    46'Country'
     A                                    54'Country'
     A                                    62'Code'
     A                                    67'Code'
     A                                    74'Code'
     A                                    81'Amount'
     A                                   102'Equivalent'
     A                                   118'Code'
     A                                   125'BT'
     A                                   129'LR'
 
      *
      *
      *  Details
     A          R BY6030D3                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A  30        L#CNUM         6A        1SPACEB(2)
     A  30        L#CRNM        20A        8SPACEA(1)
     A            L#DLNO         6S 0     16SPACEB(1)
     A            L#DTYP         2A       23
     A                                    25'-'
     A            L#DLST         2A       26
     A            L#VDAT         7A       30
     A            L#MDAT         7A       38
     A            L#LISO         2A       48
     A            L#RISO         2A       56
     A            L#LIND         3A       62
     A            L#LINS         2A       68
     A            L#CYCD         3A       74
     A            L#CUAS        17A       79
     A            L#SGAM        17A       99
     A            L#PCOD         4S 0    119
     A            L#BTEX         1A      126
     A            L#LREX         1A      130
      *
     A          R BY6030E2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SPACEB(3)
     A                                    50'***  No Details To Report  ***'
      *
      *  Database Error
     A          R BY6030F2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                       SPACEB(2)
     A                                    47'Database Error'
     A            DBASER    R             62REFFLD(DBASE)
     A                                    66'In Program'
     A            DBPGM     R             77
     A                                    47'File'
     A                                       SPACEB(2)
     A            DBFILE    R             52
     A                                    61'Key'
     A            DBKEY     R             65
      *
      *
      *
     A          R BY6030Z2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SPACEB(3)
     A                                    53'***  End of Report  ***'
      *
