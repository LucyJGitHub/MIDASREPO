/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Loans PL Extract - Valid Loans Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6040P1 - PL Loans Extract/Validation - Valid Loans Report  *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 LLN021/03          Date 18Mar04               *
      *                 LLN021             Date 13Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  LLN021/034 - Detail lines over printing                      *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY6040H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(03)
     A                                     1'Reference BY6040P1'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE1          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  21                                41'PL Loan Extract '
     A N21                                38'PL Loan Validation '
     A                                    57'As At'
     A            L#RPDA         7A       63
     A                                    71'- Valid Loans Report'
     A                                      SPACEA( 1)
     A  21                                41'----------------'
     A N21                                39'-------------------'
     A                                    57'------ '
     A                                    63'--------'
     A                                    71'--------------------'
     A                                      SPACEA( 2)
      *
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                    15'Loan'
     A                                    22'Loan'
     A                                    29'Value'
     A                                    36'Maturity'
     A                                    46'Cust'
     A                                    51'Risk'
     A                                    57'Ind.'
     A                                    62'Inst'
     A                                   102'Sterling'
     A                                   116'Product'
     A                                      SPACEA(1)
      *
     A                                     1'Customer'
     A                                    15'Number'
     A                                    22'Type'
     A                                    30'Date'
     A                                    38'Date'
     A                                    46'Cnty'
     A                                    51'Cnty'
     A                                    57'Code'
     A                                    62'Code'
     A                                    69'Ccy'
     A                                    80'Amount'
     A                                   100'Equivalent'
     A                                   117'Code'
     A                                   124'BT'
     A                                   129'LR'
     A                                      SPACEA(1)
      *
      *  Details
     A          R BY6040D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A  30        L#CNUM         6A        1SPACEB(2)
     A  30        L#CRNM        20A        8SPACEA(1)
      * Start LLN021/034
     A*********** L#LNRF         6S 0     15
      **Start of change CLE148
     A**********  L#LNRF         6S 0     15SPACEB(1)                                         CLE148
     A            L#LNRF         6A       15SPACEB(1)
      **End of change CLE148
      * Start LLN021/034
     A            L#LTYP         2A       22
     A                                    24'-'
     A            L#SUTP         2A       25
     A            L#VDAT         7A       29
     A            L#MDAT         7A       37
     A            L#LISO         2A       47
     A            L#RISO         2A       52
     A            L#LIND         3A       57
     A            L#LINS         2A       63
     A            L#CYCD         3A       69
     A            L#CUAS        17A       73
     A            L#SGAM        17A       93
     A            L#PCOD         4S 0    117
     A            L#BTEX         1A      125
     A            L#LREX         1A      130
      *
     A          R BY6040E1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SPACEB(3)
     A                                    50'***  No Details To Report  ***'
      *
      *
     A          R BY6040R1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SKIPB(3)
     A                                     1'Reference BY6040P1'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE1          4  0    126
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
      *
     A                                    44'Loan Extract As At '
     A            L#RPDA         7A       63
     A                                    71'- Currency Summary'
     A                                      SPACEA( 1)
     A                                    44'-------------------'
     A                                    63'--------'
     A                                    71'--------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                     2'Currency'
     A                                    14'Currency'
     A                                    31'Total Assets'
     A                                    55'Total Assets'
     A                                    81'Total Liabilities'
     A                                   105'Total Liabilities'
     A                                      SPACEA( 1)
     A                                     4'Code'
     A                                    16'Name'
     A                                    32'(Currency)'
     A                                    52'(Sterling Equivalent)'
     A                                    84'(Currency)'
     A                                   103'(Sterling Equivalent)'
     A                                      SPACEA( 1)
      *
     A          R BY6040S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SPACEB( 1)
     A            L#CYCD         3A        4TEXT('Currency')
     A            L#CYNM        14A       12TEXT('Currency Name')
     A            L#TLAC        17A       29TEXT('Total Assets - Currency')
     A            L#TLAS        17A       52TEXT('Total Assets - Sterling')
     A            L#TLLC        17A       80TEXT('Total Liabilities - Currency')
     A            L#TLLS        17A      103TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY6040T1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SPACEB(3)
     A                                     4'Total:'
     A            L#TOTA        21A       48TEXT('Total Assets - Sterling')
     A            L#TOTL        21A       99TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY6040Z1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
     A                                      SPACEB(3)
     A                                    53'***  End of Report  ***'
      *
