/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Loans PL Extract - Invalid Loans Report')              *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6040P2 - PL Loans Extract/Validation Invalid Loans Report  *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems  2005      *
      *                                                               *
      *  Last Amend No. BUG7474            Date 03Jun15               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN021/028         Date 05Mar04               *
      *                 LLN021/014         Date 12Feb04               *
      *                 LLN021             Date 18Nov03               *
      *                 LLN021             Date 13Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  BUG7474    - BY6040P2 - No customer number in the report     *
      *  CLE148 - Alpha Loan Reference                                *
      *  LLN021/028 - Invalid Loans Report is overwriting details.    *
      *               Also Loan Reference + Amount not shown          *
      *  LLN021/014 - Output report as invalid                        *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY6040H2                  TEXT('(C) Copyright +
     A                                      Misys International Banking +
     A                                       Systems Limited 2003')
     A                                      SKIPB(03)
     A                                     1'Reference BY6040P2'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
      * Start of change BUG7474
     A**********  PAGE1          4  0    126                                                 BUG7474
     A            PAGE2          4  0    126
      * End of change BUG7474
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  21                                41'PL Loan Extract '
     A N21                                38'PL Loan Validation '
     A                                    57'As At'
     A            L#RPDA         7A       63
     A****************                    71'- Valid Loans Report'              LLN021
     A                                    71'- Invalid Loans Report'            LLN021
     A                                      SPACEA( 1)
     A  21                                41'----------------'
     A N21                                39'-------------------'
     A                                    57'------ '
     A                                    63'--------'
     A                                    71'--------------------'
     A                                      SPACEA( 2)
      *
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                    15'Loan'
     A                                    22'Loan'
     A                                    29'Value'
     A                                    36'Maturity'
     A                                    46'Cust'
     A                                    51'Risk'
     A                                    57'Ind.'
     A                                    62'Inst'
     A                                   102'Sterling'
     A                                   116'Product'
     A                                      SPACEA(1)
      *
     A                                     1'Customer'
     A                                    15'Number'
     A                                    22'Type'
     A                                    30'Date'
     A                                    38'Date'
     A                                    46'Cnty'
     A                                    51'Cnty'
     A                                    57'Code'
     A                                    62'Code'
     A                                    69'Ccy'
     A                                    80'Amount'
     A                                   100'Equivalent'
     A                                   117'Code'
     A                                   124'BT'
     A                                   129'LR'
     A                                      SPACEA(1)
      *
      *  Details
     A          R BY6040D3                  TEXT('(C) Copyright +
     A                                      Misys International Banking +
     A                                       Systems Limited 2003')
     A  30        L#CNUM         6A        1SPACEB(2)
     A  30        L#CRNM        20A        8SPACEA(1)
     A* Start LLN021
     A************L#LNRF         6S 0     15
      **Start of change CLE148
     A**********  L#LNRF         6S 0     15SPACEB(1)                                         CLE148
     A            L#LNRF         6A       15SPACEB(1)
      **End of change CLE148
     A* End   LLN021
     A            L#LTYP         2A       22
     A                                    24'-'
     A            L#SUTP         2A       25
     A            L#VDAT         7A       29
     A            L#MDAT         7A       37
     A            L#LISO         2A       47
     A            L#RISO         2A       52
     A            L#LIND         3A       57
     A            L#LINS         2A       63
     A            L#CYCD         3A       69
     A            L#CUAS        17A       73
     A            L#SGAM        17A       93
     A            L#PCOD         4S 0    117
     A            L#BTEX         1A      125
     A            L#LREX         1A      130
      *
     A          R BY6040E2                  TEXT('(C) Copyright +
     A                                      Misys International Banking +
     A                                       Systems Limited 2003')
     A                                      SPACEB(3)
     A                                    50'***  No Details To Report  ***'
      *
      *  Database Error
     A          R BY6040F2                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                       SPACEB(2)
     A                                    47'Database Error'
     A            DBASER    R             62REFFLD(DBASE)
     A                                    66'In Program'
     A            DBPGM     R             77
     A                                    47'File'
     A                                       SPACEB(2)
     A            DBFILE    R             52
     A                                    61'Key'
     A            DBKEY     R             65
      *
      *
      *
     A          R BY6040Z2                  TEXT('(C) Copyright +
     A                                      Misys International Banking +
     A                                       Systems Limited 2003')
     A                                      SPACEB(3)
     A                                    53'***  End of Report  ***'
      *
