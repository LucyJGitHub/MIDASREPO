/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Facilities PL Extract - Valid Facilities Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6050P1 - Facilities Extract/Validation - Valid Facilities  *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN021             Date 13Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     A          R BY6050H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SKIPB(03)
     A                                     1'Reference BY6050P1'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE1          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A  21                                38'Facilities Extract '
     A N21                                35'Facilities Validation '
     A                                    57'As At'
     A            L#RPDA         7A       63
     A                                    70' - Valid Facilities'
     A                                      SPACEA(1)
     A  21                                38'-------------------'
     A N21                                35'----------------------'
     A                                    57'-----'
     A                                    63'-------'
     A                                    70'-------------------'
     A                                      SPACEA(2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                    13'Facility'
     A                                    25'Start'
     A                                    35'Maturity'
     A                                    45'Cust'
     A                                    50'Risk'
     A                                    57'Ind.'
     A                                    63'Inst'
     A                                   107'Sterling'
     A                                   119'Product'
     A                                      SPACEA(1)
      *
     A                                     1'Customer'
     A                                    13'Type/Seq'
     A                                    26'Date'
     A                                    37'Date'
     A                                    45'Cnty'
     A                                    50'Cnty'
     A                                    57'Code'
     A                                    63'Code'
     A                                    70'Ccy'
     A                                    86'Amount'
     A                                   105'Equivalent'
     A                                   120'Code'
     A                                   127'BT'
     A                                   130'LR'
     A                                      SPACEA(1)
      *
      *
      *  Details
     A          R BY6050D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A  30        L#CNUM         6A        1SPACEB(1)
     A  30        L#CRNM        20A        8SPACEA(1)
     A            L#FACT         3S 0     13
     A                                    16'/'
     A            L#FCNO         2S       17
     A            L#VDAT         7A       24
     A            L#MDAT         7A       35
     A            L#LISO         2A       46
     A            L#RISO         2A       51
     A            L#LIND         3A       57
     A            L#LINS         2A       64
     A            L#CYCD         3A       70
     A            L#CUAS        17A       75
     A            L#SGAM        17A       98
     A            L#PCOD         4S 0    120
     A            L#BTEX         1A      127
     A            L#LREX         1A      130
     A*
     A                                      SPACEA(1)
      *
      *  Invalid extract details
     A  41                                 2'Missing Extract Details'
     A                                      SPACEA(1)
      *
      *
     A          R BY6050E1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SPACEB(3)
     A                                    50'***  No Details To Report  ***'
      *
      *
     A          R BY6050R1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SKIPB(3)
     A                                     1'Reference BY6050P1'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE           4  0    126
     A                                      EDTCDE(4)
     A                                      SPACEA( 2)
      *
     A                                    38'Facilities Extract '
     A                                    57'As At '
     A            L#RPDA         7A       63
     A                                    70' - Currency Summary'
     A                                      SPACEA(1)
     A                                    38'-------------------'
     A                                    57'----- '
     A                                    63'-------'
     A                                    70'-------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                     2'Currency'
     A                                    14'Currency'
     A                                    31'Total Assets'
     A                                    55'Total Assets'
     A                                    81'Total Liabilities'
     A                                   105'Total Liabilities'
     A                                      SPACEA( 1)
     A                                     4'Code'
     A                                    16'Name'
     A                                    32'(Currency)'
     A                                    52'(Sterling Equivalent)'
     A                                    84'(Currency)'
     A                                   103'(Sterling Equivalent)'
     A                                      SPACEA( 1)
      *
     A          R BY6050S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SPACEB( 1)
     A            L#CYCD         3A        4TEXT('Currency')
     A            L#CYNM        14A       12TEXT('Currency Name')
     A            L#TLAC        17A       29TEXT('Total Assets - Currency')
     A            L#TLAS        17A       52TEXT('Total Assets - Sterling')
     A            L#TLLC        17A       80TEXT('Total Liabilities - Currency')
     A            L#TLLS        17A      103TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY6050T1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SPACEB(3)
     A                                     3'Total'
     A            L#TOTA        21A       48TEXT('Total Assets - Sterling')
     A            L#TOTL        21A       99TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY6050Z1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SPACEB(3)
     A                                    53'***  End of Report  ***'
      *
