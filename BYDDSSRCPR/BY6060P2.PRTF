/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Securities PL Extract - Invalid Securities Report')    *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England Returns Module                       *
      *                                                               *
      *  BY6060P2 - PL Securities Extract/Validation - Invalid Report *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2003            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. BUG7612            Date 03Jun15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 LLN021             Date 03Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment Details                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG7612- Incorrect report title.                             *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY6060H2                  TEXT('(C) Copright +
     A                                      Finastra International Limited +
     A                                      Limited 2003')
     A                                      SKIPB(03)
     A                                     1'Reference BY6060P2'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE2          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
      * Start of change BUG7612
     A**21******                          38'Securities Extract '                            BUG7612
     A*N21******                          35'Securities Validation '                         BUG7612
     A  21                                35'PL Securities Extract '
     A N21                                32'PL Securities Validation '
      * End of change BUG7612
     A                                    57'As At'
     A            L#RPDA         7A       63
     A                                    70' - Invalid Securities Report'
     A                                      SPACEA( 1)
      * Start of change BUG7612
     A**21******                          38'-------------------'                            BUG7612
     A*N21******                          35'----------------------'                         BUG7612
     A  21                                35'----------------------'
     A N21                                32'-------------------------'
      * End of change BUG7612
     A                                    57'------ '
     A                                    63'-------'
     A                                    70'----------------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                    30'Issuer'
     A                                    41'ISO'
     A                                    49'Risk'
     A                                    59'ISO'
     A                                    66'Ind.'
     A                                    73'Inst'
     A                                      SPACEA( 1)
      *
     A                                     1'Issuer'
     A                                    30'Country'
     A                                    41'Code'
     A                                    49'Country'
     A                                    59'Code'
     A                                    66'Code'
     A                                    73'Code'
     A                                    80'Errors in Issuer Details'
     A                                      SPACEA( 1)
      *
     A                                     1'------'
     A                                    30'-------'
     A                                    41'----'
     A                                    49'-------'
     A                                    59'----'
     A                                    66'----'
     A                                    73'----'
     A                                    80'------------------------'
      *
      *  Details
     A          R BY6060D3                  TEXT('(C) Copyright +
     A                                      Finastra IBS Ltd 2003')
     A            L#CNUM         6A        1SPACEB(2)
     A            L#CRNM        20A        8
     A            L#COLC         2A       32
     A            L#LISO         2A       42
     A            L#CNCZ         2A       50
     A            L#RISO         2A       60
     A            L#LIND         3A       66
     A            L#LINS         2A       74
      *
      *
      *  Invalid Customer Details
     A  31                                80'ISO code for Customer +
     A                                      Country is invalid'
     A                                      SPACEA(1)
      *
     A  32                                80'ISO code for Country  +
     A                                      of Risk is invalid'
     A                                      SPACEA(1)
      *
     A  33                                80'Institution code is not a +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  34                                80'Industry code is not a +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  35                                80'Institution code must be +
     A                                      for a UK resident'
     A                                      SPACEA(1)
      *
     A  36                                80'Institution code must be +
     A                                      for a non UK resident'
     A                                      SPACEA(1)
      *
     A  37                                80'Industry code must exist +
     A                                      if institution code is'
     A                                      SPACEA(1)
     A  37                                80'for UK non-monetary sector'
     A                                      SPACEA(1)
      *
      * Following line forces a blank line to be printed on overflow
     A  30                                 1' '
     A                                      SPACEB(1)
      *
     A                                     5'Extract Details'
     A                                      SPACEB(2)
     A                                    29'Book'
     A                                    36'Security'
     A                                   114'Sterling'
     A                                   126'Product'
     A                                      SPACEA(1)
      *
     A                                    29'Code'
     A                                    36'Shortn.'
     A                                    76'Ccy'
     A                                    93'Amount'
     A                                   112'Equivalent'
     A                                   127'Code'
     A                                      SPACEA(1)
      *
     A          R BY6060D7                  TEXT('(C) Copyright +
     A                                      Finastra IBS Ltd 2003')
     A                                      SPACEB(1)
      *
      *  Could not find valid product code
     A  38                                 2'Prod. code not found'
      *
      *  Invalid extract details
     A  41                                 2'No Extract Criteria'
      *
     A            L#BHBK         2A       30
     A            L#BHSC        10A       35
     A            L#CCYD         3A       76
     A            L#CUAS        17A       82
     A            L#SGAM        17A      105
     A            L#PCOD         4S 0    127
      *
      *
      *
      *  No Details to Report
     A          R BY6060E2                  TEXT('(C) Copyright +
     A                                      Finastra International Limited +
     A                                      Limited 2003')
     A                                      SPACEB(03)
     A                                    52'***  No Details To +
     A                                      Report  ***'
      *
      *  Database Error
     A          R BY6060F2                  TEXT('(C) Copyright +
     A                                      Finastra International Limited +
     A                                      Limited 2003')
     A                                       SPACEB(2)
     A                                    47'Database Error'
     A            DBASER    R             62REFFLD(DBASE)
     A                                    66'In Program'
     A            DBPGM     R             77
     A                                    47'File'
     A                                       SPACEB(2)
     A            DBFILE    R             52
     A                                    61'Key'
     A            DBKEY     R             65
      *
      *
      *  End of Report
     A          R BY6060Z2                  TEXT('(C) Copright +
     A                                      Finastra International Limited +
     A                                      Limited 2003')
     A                                      SPACEB(3)
     A                                    55'***  End of Report  ***'
      *
