/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Futures/Options Books  Extract - Valid Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6070P1 - Futures/Options Extract Books - Valid Report      *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd 2003   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN021  *CREATE    Date 13Nov03               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details                                            *
      *                                                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *
      *****************************************************************
      *
     A          R BY6070H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SKIPB(03)
     A                                     6'Reference BY6070P1'
     A            BJURPT        53A       39
     A                                   100'Date'
     A            BJMRDT         7A      105
     A                                   119'Page'
     A            PAGE1          4  0    124
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A  21                                42'PL Fut./Opt. Extract  '
     A N21                                39'PL Fut./Opt. Validation  '
     A                                    +1'As At'
     A            L#RPDA         7A       +1
     A                                    +1'- Valid Books '
     A                                      SPACEA(1)
     A  21                                42'----------------------'
     A N21                                39'-------------------------'
     A                                    +0'------'
     A                                    +0'--------'
     A                                    +0'---------------'
     A                                      SPACEA(2)
      *
     A  20                                 3'Branch'
     A  20        L#BRCD         3A       12
     A  20        L#BRNM        30A       16
     A  20                                  SPACEA(2)
      *
     A                                    55'Cust'
     A                                    60'Risk'
     A                                    65'Ind.'
     A                                    70'Inst.'
     A                                    89'Currency'
     A                                   106'Sterling'
     A                                   117'Product'
     A                                      SPACEA(1)
      *
     A                                     3'Customer'
     A                                    55'Ctry'
     A                                    60'Ctry'
     A                                    65'Code'
     A                                    70'Code'
     A                                    76'Ccy'
     A                                    91'Amount'
     A                                   104'Equivalent'
     A                                   118'Code'
      *
      *  Details
     A          R BY6070D1                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A  30        L#CNUM         6A        3SPACEB(2)
     A  30        L#CRNM        20A       11SPACEA(1)
     A            L#LISO         2A       56SPACEB(1)
     A            L#RISO         2A       61
     A            L#INDC         3A       66
     A            L#INSC         2A       71
     A            L#CYCD         3A       76
     A            L#CUAS        17A       81
     A            L#SGAM        17A       99
     A            L#PCOD         4S 0    118
      *
      *
      *  Invalid extract details
     A  41                                 2'Missing Extract Details'
      *
      *
     A          R BY6070E1                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                      SPACEB(02)
     A                                    50'***  No Details To Report  ***'
      *
      *
     A          R BY6070R1                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                      SKIPB(3)
     A                                     1'Reference BY6070P1'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE1          4  0    126
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
      *
     A                                    40'Security Extract As At '
     A            L#RPDA         7A       63
     A                                    71'- Currency Summary'
     A                                      SPACEA( 1)
     A                                    40'-----------------------'
     A                                    63'--------'
     A                                    71'--------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
     A  20                                  SPACEA(2)
      *
     A                                     2'Currency'
     A                                    14'Currency'
     A                                    31'Total Assets'
     A                                    55'Total Assets'
     A                                    81'Total Liabilities'
     A                                   105'Total Liabilities'
     A                                      SPACEA( 1)
     A                                     4'Code'
     A                                    16'Name'
     A                                    32'(Currency)'
     A                                    52'(Sterling Equivalent)'
     A                                    84'(Currency)'
     A                                   103'(Sterling Equivalent)'
     A                                      SPACEA( 1)
      *
     A          R BY6070S1                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                      SPACEB( 1)
     A            L#CYCD         3A        4TEXT('Currency')
     A            L#CYNM        14A       12TEXT('Currency Name')
     A            L#TLAC        17A       29TEXT('Total Assets - Currency')
     A            L#TLAS        17A       52TEXT('Total Assets - Sterling')
     A            L#TLLC        17A       80TEXT('Total Liabilities - Currency')
     A            L#TLLS        17A      103TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY6070T1                  TEXT('(C) Copyright +
     A                                      Misys International Banking +
     A                                       Systems Limited 2003')
     A                                      SPACEB(3)
     A                                     4'Total:'
     A            L#TOTA        21A       48TEXT('Total Assets - Sterling')
     A            L#TOTL        21A       99TEXT('Total Liabilities - Sterling')
      *
      *
     A          R BY6070Z1                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                      SPACEB(2)
     A                                    53'***  End of Report  ***'
      *
