/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Futures/Options Trades Extract - Invalid Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England Returns Module                       *
      *                                                               *
      *  BY6075P2 - Futures/Options Trades Extract - Valid Report     *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd 2003   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN021  *CREATE    Date 07Nov03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment Details                                            *
      *                                                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R BY6075H2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SKIPB(03)
     A                                     6'Reference BY6075P2'
     A            BJURPT        53A       39
     A                                   100'Date'
     A            BJMRDT         7A      105
     A                                   119'Page'
     A            PAGE2          4  0    124
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  21                                38'Fut./Opt. Extract  '
     A N21                                35'Fut./Opt. Validation  '
     A                                    +1'As At'
     A            L#RPDA         7A       +1
     A                                    +1' - Invalid Trades'
     A                                      SPACEA( 1)
     A  21                                38'-------------------'
     A N21                                35'----------------------'
     A                                    +0'------'
     A                                    +0'--------'
     A                                    +0'------------------'
     A                                      SPACEA( 2)
      *
     A  20                                 3'Branch'
     A  20        L#BRCD         3A       12
     A  20        L#BRNM        30A       16
     A  20                                  SPACEA(2)
      *
     A                                    36'Cust'
     A                                    44'Risk'
     A                                    52'Ind.'
     A                                    60'Inst.'
     A                                      SPACEA(1)
      *
     A                                     3'Customer'
     A                                    36'Ctry'
     A                                    44'Ctry'
     A                                    52'Code'
     A                                    60'Code'
     A                                    69'Errors in Customer Details'
     A                                      SPACEA(1)
      *
      *  Details
     A          R BY6075D2                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A            L#CNUM         6A        3SPACEB(2)
     A            L#CRNM        20A       11
     A            L#CISO         2A       37
     A            L#LISO         2A       45
     A            L#INDC         3A       52
     A            L#INSC         2A       61
      *
      *  Invalid Customer Details
     A  31                                69'ISO code for Customer +
     A                                      Country is invalid'
     A                                      SPACEA(1)
      *
     A  32                                69'ISO code for Country  +
     A                                      of Risk is invalid'
     A                                      SPACEA(1)
      *
     A  33                                69'Institution code is not a +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  34                                69'Industry code is not a  +
     A                                      valid Bank of England code'
     A                                      SPACEA(1)
      *
     A  35                                69'Institution code must be +
     A                                      for a UK resident'
     A                                      SPACEA(1)
      *
     A  36                                69'Institution code must be +
     A                                      for a non UK resident'
     A                                      SPACEA(1)
      *
     A  37                                69'Industry code must exist +
     A                                      if institution code is'
     A                                      SPACEA(1)
     A  37                                69'for UK non-monetary sector'
     A                                      SPACEA(1)
      *
      *
     A                                    17'Extract details:'
     A                                      SPACEB(1)
     A                                    86'Currency'
     A                                   104'Sterling'
     A                                   115'Product'
     A                                      SPACEA(1)
      *
     A                                    74'Ccy'
     A                                    88'Amount'
     A                                   102'Equivalent'
     A                                   116'Code'
     A                                      SPACEA(1)
      *
     A          R BY6075D3                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A            L#CYCD         3A       74SPACEB(1)
     A            L#CUAS        17A       79
     A            L#SGAM        17A       97
     A            L#PCOD         4S 0    116
      *
      *  Could not find valid product code
     A  38                                 2'Prod. code not found'
      *
      *  Invalid extract details
     A  41                                 2'No Extract Criteria'
      *
      *
      *  No Details to Report
     A          R BY6075E2                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                      SPACEB(3)
     A                                    52'***  No Details To +
     A                                      Report  ***'
      *
      *  Database Error
     A          R BY6075F2                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                       SPACEB(2)
     A                                    47'DATABASE ERROR'
     A            DBASER    R             62REFFLD(DBASE)
     A                                    66'IN PROGRAM'
     A            DBPGM     R             77
     A                                    47'FILE'
     A                                       SPACEB(2)
     A            DBFILE    R             52
     A                                    61'KEY'
     A            DBKEY     R             65
      *
      *
      *  End of Report
     A          R BY6075Z2                  TEXT('(C) Copyright +
     A                                      Misys International Banking+
     A                                      Systems Limited 2003')
     A                                      SPACEB(2)
     A                                    55'***  End of Report  ***'
      *
