/*STD *  PRTFBASE
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('PL Consolidated Extract Report')
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6090P1 - PL Consolidated Extract Report                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 1999            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. LLN021             Date 01Dec03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment Details                                            *
      *  -----------------                                            *
      *                                                               *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
      **  Report Heading
     A          R BY6090H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SKIPB(03)
     A                                     1'Reference BY6090P1'
     A            BJURPT        53A       44
     A                                   105'Date'
     A            BJMRDT         7A      110
     A                                   121'Page'
     A            PAGE1          4  0    126
     A                                      EDTCDE(Z)
     A                                      SPACEA( 2)
      *
     A  20                                 1'Branch:'
     A  20        L#BRCD         3A       10
     A  20        L#BRNM        30A       14
      *
     A                                    50'PL Consolidated Extract Report'
     A                                      SPACEA(1)
     A                                    50'------------------------------'
     A                                      SPACEA(2)
      *
      *
     A                                     1'Customer'
     A                                    34'Mod.'
     A                                    39'Transaction'
     A                                    54'Event'
     A                                    61'Mat.'
     A                                    66'Ctry'
     A                                    72'Risk'
     A                                    78'Ind.'
     A                                    83'Inst'                                            178582
     A                                    98'Transaction'                                     178582
     A                                   122'Sterling'
     A                                      SPACEA(1)
      *
     A                                     1'Report Name'
     A                                    22'Town'
     A                                    34'Id.'
     A                                    39'Reference'
     A                                    54'Date'
     A                                    61'Cat.'
     A                                    66'Code'
     A                                    72'Ctry'
     A                                    78'Code'
     A                                    83'Code'                                            178582
     A                                    88'Ccy'                                             178582
     A                                   103'Amount'                                          178582
     A                                   120'Equivalent'
     A                                       SPACEA(1)
      *
      **  Product Code Heading
     A          R BY6090H2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1999')
     A                                      SPACEB(1)
     A                                     1'Product Code:'
     A            L#PCOD         4A       15SPACEA(1)
      *
      *
      *  Report Details
     A          R BY6090D1                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1999')
      *
     A            L#CRNM        20A        1SPACEB(1)
     A            L#CTWN        10A       22
     A            L#MMOD         2A       34
     A            L#TRAN        10A       39
     A            L#MDAT         6S 0     52EDTWRD('  /  /  ')
     A            L#MCAT         1A       62
     A            L#ISOC         2A       67
     A            L#RISO         2A       73
     A            L#LIND         3A       78
     A            L#LINS         2A       84                                                  178582
     A            L#CYCD         3A       88                                                  178582
     A            L#CUAS        18A       92                                                  178582
     A  31                               109'CR'                                              178582
     A            L#SGAM        18A      113                                                  178582
     A  31                               130'CR'                                              178582
      *
      *
      **  No Details To Report
     A          R BY6090E1                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                      SPACEB(3)
     A                                    50'***  No Details To Report  ***'
      *
      **  Total For Product Code
     A          R BY6090T1                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                      SPACEB(2)
     A                                    67'Total for Product Code'
     A            L#PCOD         4A       90
     A                                    94':'
     A            L#TOTE        21A      110                                                  178582
     A                                      SPACEA(2)
      *
      **  Total For Sterling/Non-Sterling
     A          R BY6090T2                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1999')
     A                                      SPACEB(2)
     A  33                                71'Total for Non-Sterling:'
     A N33                                75'Total for Sterling:'
     A                                    94':'
     A            L#TOTC        21A      110                                                  178582
     A                                      SPACEA(1)
      *
     A          R BY6090Z1                  TEXT('(c) +
     A                                      Misys International +
     A                                      Banking Systems Ltd. 1997')
     A                                      SPACEB(3)
     A                                    53'***  End of Report  ***'
      *
