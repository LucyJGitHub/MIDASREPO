     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BoE Facilities Extract/Validation')              *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY2040 - Facilities Extract/Validation                       *
      *                                                               *
      *  Called By: BYC2030 - Customer Lending Extract/Validation     *
      *                                                               *
      *  (c) Finastra International Limited 1998                      *
      *                                                               *
      *  Last Amend No. CLE172             Date 01Oct20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CSD027             Date 21Jan09               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 BUG7632            Date 18Jun05               *
      *                 LLN022             Date 24Jan05               *
      *                 232050             Date 23Feb05               *
      *                 LLN021             Date 23Nov03               *
      *                 LLN021             Date 21Oct03               *
      *                 202299             Date 29Jan02               *
      *                 LLN016             Date 12May00               *
      *                 175023             Date 15Feb99               *
      *                 168646             Date 08Oct99               *
      *                 LLN012             Date 01Oct99               *
      *                 LLN012             Date 23APR99               *
      *                 LLN010             Date 21Jul98               *
      *                 LLN009             Date 16Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment History                                            *
      *  -----------------                                            *
      *                                                               *
      *  CLE172 - LIBOR Deregulation - Lending (Recompile)            *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG7632- Customer missing in the report                      *
      *  LLN022 - BOE Upgrade to MidasPlus.                           *
      *  232050 - Write live records only in BY2040P1 and BY2040P2.   *
      *           Doesn't include matured deals in the calculation    *
      *           of totals. Ensures deals aren't reported under      *
      *           the wrong customer.                                 *
      *  LLN021 - Records for mature Securities should be output to   *
      *           PF/BYMTDTPP for reporting purposes                  *
      *  LLN021 - Account Extract Validation, add industry code to the*
      *           extract criteria for BYFCEXPP.                      *
      *  202299 - Facility number not displayed on extract.           *
      *  LLN016 - Upgrade BoE to DBAR3.                               *
      *  175023 - Clear of file format not working if no output       *
      *           fields in the file format. Also inlcude check       *
      *           to see if a sterling facility is flagged as a       *
      *           Euro facility.                                      *
      *  168646 - Key date for events/transactions must be compared   *
      *           less than or equal to LR Report Date.  System was   *
      *           missing all those equal to date.                    *
      *  LLN012 - Liquidity Reporting Patch B.                        *
      *  LLN012 - Financial Services Authority - Liquidity Report     *
      *  LLN010 - Bank of England EMU Changes                         *
      *           Note: Interim Release 6 must be installed for       *
      *                 this enhancement.                             *
      *  LLN009 - DBA2 Upgrade.                                       *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
      *  Indicator Usage                                              *
      *  ---------------                                              *
      *                                                               *
      *  *IN20  -   Multi-branching                                   *
      *  *IN31  -   Report error in customer details to BY2040P2      *
      *  *IN32  -   Report error in customer details to BY2040P2      *
      *  *IN33  -   Report error in customer details to BY2040P2      *
      *  *IN34  -   Report error in customer details to BY2040P2      *
      *  *IN35  -   Report error in customer details to BY2040P2      *
      *  *IN36  -   Report error in customer details to BY2040P2      *
      *  *IN37  -   Report error in customer details to BY2040P2      *
      *  *IN41  -   Extract criteria does not exist                   *
      *  *IN42  -   Product code not valid                            *
      *  *IN88  -   End of file indicator for reading SDBRCHL1        *
      *  *IN89  -   End of file indicator for reading FCLTYL1         *
      *  *IN90  -   General error indicator                           *
      *                                                               *
      *****************************************************************
      *
     FFCLTYL1 IF  E           K        DISK         KINFSR *PSSR
      *  Facilities by branch and facility reference - A record
      *
     FFCLTY   IF  E           K        DISK         KINFSR *PSSR
     F            FCLTYHHF                          KIGNORE
     F            FCLTYFMF                          KIGNORE
     F            FCLTYZZF                          KIGNORE
      *  Facilities by facility reference - (FCLTYFNF only)
      *
     FSDCURRL0IF  E           K        DISK         KINFSR *PSSR
      *  Currency details
      *
     FSDBRCHL1IF  E           K        DISK         KINFSR *PSSR
      *  Branches
      *
     F*DBRCHY9IF**E********** K        DISK         KINFSR *PSSR                             LLN016
     FBYBRCHY0IF  E           K        DISK         KINFSR *PSSR                             LLN016
      *  Branch Detail extension file
      *
     FBYRPDTPPIF  E                    DISK         KINFSR *PSSR      UC
      *  Bank of England Extract Date File
      *
     FBYFCEXPPIF  E           K        DISK         KINFSR *PSSR
      *  Security Extract Criteria
      *
     FLVENT   IF  E           K        DISK         KINFSR *PSSR          LLN012
     F            LVENTHHF                          KIGNORE               LLN012
     F            LVENTZZF                          KIGNORE               LLN012
      * Lending Events                                                    LLN012
      *
     FBYEXDTPPO   E           K        DISK                           UC
     F                                              KINFSR *PSSR
      *  Bank of England Extract File - (Uses member BYEXLEM)
     FBYMTDTPPO   E           K        DISK         KINFSR *PSSR      UC  LLN021
     F            BYEXDTD0                          KRENAMEBYMTDTP0
      *  Mature Bank of England Extract File - (Uses member BYEXLEM)      LLN021
      *
     FBY2040P1O   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOL1
      *  Bank of England Validation Report - Valid
      *
     FBY2040P2O   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOL2
     F*CLTYY9*IF**E****       K        DISK                         LLN010LLN016
     FBYFCLTY0IF  E           K        DISK                               LLN016
      *  Facilities details extension file                                LLN010
      *                                                                   LLN010
      *  Bank of England Validation Report - Invalid
      *
      /EJECT
      *
      *
     E                    @CPY    1   1 80
     E                    @CCY      200  3
     E                    @TAC      200 15 0             Total assets - ccy
     E                    @TAS      200 15 0             Total assets - sterling
     E                    @TLC      200 15 0             Total liabilities - ccy
     E                    @TLS      200 15 0             Total liabilities - STG
      *
      *
      ** Local data area for database error details
     ILDA       E DSLDA                         256
      *
      ** Data Structure using Bank file format
     ISDBANK    E DSSDBANKPD
      *
      ** Trading Data ICD
     ISDTRAD    E DSSDTRADPD
      *
      ** Data Structure using Currency Details
     ISDCURR    E DSSDCURRPD                200
      *
      ** General Ledger ICD
     ISDGELR    E DSSDGELRPD
     I              QQACCD                          QQACC1                                   LLN022
      *
      ** Data Structure for Customer Details
     ISDCUST    E DSSDCUSTPD
      *
      ** Data Structure for Bank of England Customer Details
     I*DCUS7****E*DSSDCUSTX7**                                                              LLN016
     IBYCUST    E DSBYCUSTX0                                                                LLN016
      *
      ** Data Structure for extra customer details returned by BY9110
      *       ULLISO  - ISO Code of customer's country of location
      *       ULRISO  - ISO Code of customer's country of citizenship
      *       ULRWCD  - Risk weighting
     ISDCUSX      DS
     I                                        1   2 ULLISO
     I                                        3   4 ULRISO
     I                                        5   80ULRWCD
      *
      * DS For Access Programs, Long Data Structure
     IDSSDY     E DSDSSDY
      *
      ** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM ULPGMN
     I                                      244 253 ULWSID
     I                                      254 263 ULUSER
      *
      * File information data structure (for database files only)
     IINFDS1    E DSY2I1DSP
      *
      *
      * Data structure for retrieval of data from DTAARA/RUNDAT
     IRUNDAT    E DSRUNDAT
      *
      *  File Information Data Structure for BY2040P1
     ISPOOL1      DS
     I                                    B 123 1240ULNUM1
     I                                    B 188 1890ULOVF1
     I                                    B 367 3680ULCLN1
      *
      *  File Information Data Structure for BY2040P2
     ISPOOL2      DS
     I                                    B 123 1240ULNUM2
     I                                    B 188 1890ULOVF2
     I                                    B 367 3680ULCLN2
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Main Processing                                               *
      *                                                               *
      * Calls: SR/#LMAIN - Main Processing                            *
      *        SR/#LTERM - Termination Processing                     *
      *                                                               *
      *****************************************************************
      *
      *
      *  Main Processing
     C                     EXSR #LMAIN
      *
      *
      *  Termination Processing
     C                     EXSR #LTERM
      *
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LMAIN - Main Processing                                   *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:      SR/#LBRCH - Branch Processing                     *
      *             SR/#LRSET - Reset Work Fields                     *
      *             SR/#LVALD - Validate Facility Details             *
      *             SR/#LFMTD - Format Details For Reporting/Extract  *
      *             SR/#LRPXT - Report/Extract Details                *
      *                                                               *
      *****************************************************************
     C           #LMAIN    BEGSR                           ** SR/#LDETL**
      *
      *
     C           *LOVAL    SETLLSDBRCHL1
     C                     READ SDBRCHL1                 88
     C           *IN88     DOWEQ*OFF
      *
      *  Branch processing
     C                     EXSR #LBRCH
      *
      *
      *  Process all facilities for the branch
     C           ULIBOE    IFEQ 'Y'
     C           ULBRCA    SETLLFCLTYL1
     C           ULBRCA    READEFCLTYL1                  89
     C           *IN89     DOWEQ*OFF
      *
      *  Process facility according to program mode
     C           ULMODE    IFEQ 'INTERIM'
     C           LCD       ANDEQBJRDNB
     C           ULMODE    OREQ 'EXTRACT'
     C           ULMODE    OREQ 'LIST   '
      *
      *  Process facility if start and end dates are valid, and
      *   facility commitment indicator is G = granted
     C           DTAP      IFLE L0RPDT
     C           DTEX      ANDGTL0RPDT
     C********** RECI      ANDEQ'D'                                         LLN021
     C           FCTI      ANDNE'G'
      *
      *  Reset work fields
     C                     EXSR #LRSET
      *                                                                      LLN021
      *
      *  Validate facility
     C                     EXSR #LVALD
      *
      *  Do not report/extract details for product code 9998
     C           ULPCOD    IFNE 9998
      *
      *  Format details for reporting and extract
     C                     EXSR #LFMTD
      *                                                                   LLN012
      ** Set-up Event Type                                                LLN012
     C                     MOVE 'P'       ULETYP  1                       LLN012
      *
      *  Report/extract details
     C                     EXSR #LRPXT
      *                                                                   LLN012
      *  Extract details of fees for facility                             LLN012
     C           LLN012    IFEQ 'Y'                                       LLN012
     C                     EXSR #LFEES                                    LLN012
     C                     ENDIF                                          LLN012
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C           ULBRCA    READEFCLTYL1                  89
     C                     ENDDO
      *
      *
     C                     ENDIF
     C                     READ SDBRCHL1                 88
     C                     ENDDO
      *
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LBRCH - Processing For New Branch                        *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:      SR/#LRPTT - Final Report Processing               *
      *                                                               *
      *****************************************************************
     C           #LBRCH    BEGSR                           ** SR/#LBRCH**
      *
      *  Reset flag
     C                     MOVE 'N'       ULIBOE           Include branch
     C                     MOVE A8BRCD    ULBRCA
      *
      *  If single branch all records must be extracted
     C           AGMBIN    IFEQ 'N'
     C                     MOVE 'Y'       ULIBOE
      *
      *  Multi-branch processing
     C                     ELSE
      *
      *  If files open report currency summaries and close files
     C           ULOPNF    IFEQ 'Y'
     C                     EXSR #LRPTT
     C                     ENDIF
      *
      *   Reset open flag
     C                     MOVE 'N'       ULOPNF
      *
      *   Check if new branch is to be included in report extract
     C********** ULBRCA    CHAINSDBRCHY9             90                                    LLN016
     C           ULBRCA    CHAINBYBRCHY0             90                                    LLN016
     C           *IN90     IFEQ *OFF
     C                     MOVELA8IBOE    ULIBOE
     C                     ENDIF
      *
      *   Include branch in Bank of England Extract
     C           ULIBOE    IFEQ 'Y'
      *
     C                     MOVE 'Y'       ULOPNF
     C                     OPEN BY2040P1
     C                     OPEN BY2040P2
      *
      *  RCF processing for printer file BY2040P1
     C                     Z-ADDULNUM1    ULSFNO
     C                     CALL 'ZSFILE'
     C                     PARM           ULRSEQ
     C                     PARM           ULBRCA
     C                     PARM 'BY2040P1'ULFNAM
     C                     PARM           ULSFNO
     C                     PARM           ULRTCD
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 01 *
     C                     MOVEL'ZSFILE  'DBFILE           ************
     C                     Z-ADD01        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *  RCF processing for printer file BY2040P2
     C                     Z-ADDULNUM2    ULSFNO
     C                     CALL 'ZSFILE'
     C                     PARM           ULRSEQ
     C                     PARM           ULBRCA
     C                     PARM 'BY2040P2'ULFNAM
     C                     PARM           ULSFNO
     C                     PARM           ULRTCD
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 02 *
     C                     MOVEL'ZSFILE  'DBFILE           ************
     C                     Z-ADD02        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  Write report headings to BY2040P1 and BY2040P2
     C                     MOVE L0RPDA    L#RPDA
     C                     MOVE A8BRCD    L#BRCD
     C                     MOVE A8BRNM    L#BRNM
     C                     WRITEBY2040H1
     C                     WRITEBY2040H2
      *
     C                     ENDIF
     C                     ENDIF
      *
      *
      *  Initialise currency summary arrays
     C                     Z-ADD0         @TAC              Tot assets ccy
     C                     Z-ADD0         @TAS              Tot assets STG
     C                     Z-ADD0         @TLC              Tot liab ccy
     C                     Z-ADD0         @TLS              Tot liab STG
      *                                                                                      BUG7632
      ** Reset previous customer work field when a change of branch occurs                   BUG7632
      ** to allow printing of 'Customer' in the report even the previous                     BUG7632
      ** record has the same customer                                                        BUG7632
      *                                                                                      BUG7632
     C**********           Z-ADD0         ULCUS1                                      BUG7632 CSD027
     C**********           Z-ADD0         ULCUS2                                      BUG7632 CSD027
     C                     MOVE *BLANKS   ULCUS1                                              CSD027
     C                     MOVE *BLANKS   ULCUS2                                              CSD027
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRSET - Reset Work Fields                                 *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRSET    BEGSR
      *
      *  Reset work fields
     C**********           Z-ADD0         ULCNUM           Customer                           CSD027
     C                     MOVE *BLANKS   ULCNUM           Customer                           CSD027
     C                     Z-ADD0         ULVDAT           Start date
     C                     Z-ADD0         ULMDAT           End date
     C                     Z-ADD0         ULCUAS           Currency amount
     C                     Z-ADD0         ULSGAM           Sterling equivalent
     C                     Z-ADD0         ULPCOD           Product code
     C                     MOVE *BLANK    ULCYCD           Currency
     C                     MOVE *BLANK    ULMCAT           Maturity category
     C                     MOVE *BLANK    ULFACT           Facility type
     C                     MOVE *BLANK    ULFCNO           Facility no.
     C                     MOVE *BLANK    ULLINS           Institution code
     C                     MOVE *BLANK    ULLIND           Industry code
      *
     C                     MOVEA'0000000' *IN,31
     C                     MOVEA'0'       *IN,41
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LVALD - Validate and output Details For Extract           *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LVALD    BEGSR                           ** SR/#LVALD**
      *
      *  Validate customer and retrieve details if customer has changed
     C**********           Z-ADDCNUM      ULCNUM                                              CSD027
     C                     MOVE CNUM      ULCNUM                                              CSD027
     C           ULCNUM    IFNE ULPCUS                     Previous customer
     C                     MOVE ULCNUM    ULCUST
     C                     CLEARSDCUST
     C***********          CLEARSDCUS7                                                       LLN016
     C                     CLEARBYCUST                                                       LLN016
     C                     CLEARSDCUSX
     C                     CALL 'BY9110'
     C                     PARM           ULCUST           Customer number
     C                     PARM           SDCUST           SDCUSTPD details
     C**********           PARM           SDCUS7           SDCUSTX7 details                  LLN016
     C                     PARM           BYCUST           SDCUSTX7 details                  LLN016
     C                     PARM           SDCUSX           Extra details
     C                     PARM 0         ULERR1
     C                     PARM *BLANK    ULRTCD           Return code
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 03 *
     C                     MOVEL'BY9110  'DBFILE           ************
     C                     Z-ADD03        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  Set report indicators (*IN31 to *IN37) with returned settings
     C           ULERR1    IFNE 0
     C                     MOVE 'N'       ULVALD           Invalid
     C                     ELSE
     C                     MOVE 'Y'       ULVALD           Valid
     C                     ENDIF
      *
      *  Save customer and set customer print flag
     C**********           Z-ADDULCNUM    ULPCUS           Previous customer                  CSD027
     C                     MOVE ULCNUM    ULPCUS           Previous customer                  CSD027
     C                     ENDIF
      *
      *
      *  Check extract criteria exists for facility
     C                     Z-ADDFACT      L4FCTP           Facility typ
     C                     MOVE BBLINC    L4LINS           Institution code
     C                     MOVE BBLICD    L4MIND           Industry                          LLN021
      *                                                                                      LLN021
      *  Check extract criteria exists using Industry code                                   LLN021
     C           K1FCEX    CHAINBYFCEXPP             90                                      LLN021
     C           *IN90     IFEQ *ON                                                          LLN021
     C                     MOVE *BLANKS   L4MIND                                             LLN021
     C                     END                                                               LLN021
      *                                                                                      LLN021
     C           *IN90     IFEQ *ON                                                          LLN021
     C           K1FCEX    CHAINBYFCEXPP             90
     C           *IN90     IFEQ *ON
     C                     Z-ADD0         L4LINS           Institution code
     C           K1FCEX    CHAINBYFCEXPP             90
     C                     ENDIF
     C                     ENDIF                                                             LLN021
     C*                                                                                      LLN021
     C           *IN90     IFEQ *ON
     C                     Z-ADD5000      ULPCOD
     C                     ELSE
     C                     Z-ADDL4PCOD    ULPCOD
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LFMTD - Format Work Fields For Reporting and Extract      *
      *                                                               *
      * Called by:  #LMAIN - Main Processing                          *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LFMTD    BEGSR                                       **
      *
      *  Get facility B record details
     C           K2FCLT    CHAINFCLTYFNF             90
      *
      *  Set all work fields for reporting and extract
     C                     Z-ADDSTDT      ULVDAT           Start date
     C                     Z-ADDEDDT      ULMDAT           End date
     C                     MOVE FCCY      ULCYCD           Facility currency
     C***********          MOVE BBLINC    ULLINS           Institution code                LLN016
     C***********          MOVE BBLICD    ULLIND           Industry code                   LLN016
     C                     MOVE LLINC     ULLINS           Institution code                LLN016
     C                     MOVE LLICD     ULLIND           Industry code                   LLN016
     C                     MOVE FACT      ULFACT           Facility type
     C                     MOVE FCNO      ULFCNO           Facility no.
     C           FAMT      SUB  OAM1      ULCUAS           Currency amount
     C           ULCUAS    IFLT 0
     C                     Z-ADD0         ULCUAS
     C                     ENDIF
      *
      * Get maturity category
     C                     CALL 'BY9060'
     C                     PARM           ULMDAT  50       Maturity date
     C                     PARM           ULMCAT  1        Maurity category
     C                     PARM           ULMPER  50
     C                     PARM           ULRTCD  1
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 04 *
     C                     MOVEL'BY9060  'DBFILE           ************
     C                     Z-ADD04        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Get currency detail
     C                     Z-ADD1         #
     C           ULCYCD    LOKUP@CCY,#                   90
     C           #         OCUR SDCURR
      *                                                                   LLN010
      **Facility*currency,*if*is*'in'*currency,*get*details*from    LLN010LLN016
      **the*extension*file*fcltyy9*                                 LLN010LLN016
      * Facility currency, if is 'in' currency, get details from          LLN016
      * the extension file BYFCLTY1                                       LLN016
     C**********           Z-ADDCNUM      KCNUM                                        LLN010 CSD027
     C                     MOVE CNUM      KCNUM                                               CSD027
     C                     Z-ADDFACT      KFACT                           LLN010
     C                     Z-ADDFCNO      KFCNO                           LLN010
     C***                  CLEARFCLTYD9                             LLN010175023
     C                     MOVE 'N'       LEURO                           175023
     C           A6INCY    IFEQ 'Y'                                       LLN010
     C           FCCY      OREQ BLCYCD                                    175023
     C********** KEY1      CHAINFCLTYD9              90             LLN010LLN016
     C           KEY1      CHAINBYFCLTD0             90                   LLN016
     C                     ENDIF                                          LLN010
      *
      * Convert currency amount to sterling
     C           ULCYCD    IFNE BLCYCD
     C                     CALL 'BY9070'
     C                     PARM ULCUAS    ULINAM           In amount
     C                     PARM ULCYCD    ULFCCY           From currency
     C                     PARM *ZERO     ULOTAM           Converted amount
     C                     PARM BLCYCD    ULTCCY           To currency
     C                     PARM *BLANK    ULRTCD           Return code
     C           ULRTCD    IFNE ' '
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 05 *
     C                     MOVEL'BY9070  'DBFILE           ************
     C                     Z-ADD05        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDULOTAM    ULSGAM
     C                     ELSE
     C                     Z-ADDULCUAS    ULSGAM
     C                     END
      *                                                                   LLN012
      * Set extract record flags for BT and LR return                     LLN012
     C                     MOVE 'Y'       ULBTEX  1                       LLN012
     C                     MOVE 'N'       ULLREX  1                       LLN012
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRPXT - Report/Extract Details                            *
      *                                                               *
      * Called by:  #LMAIN - Main Processing                          *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPXT    BEGSR                                       **
      *
      *  Details are valid
     C           ULVALD    IFEQ 'Y'
      *
      *  Report details to BY2040P1
     C           RECI      IFEQ 'D'                                                           232050
     C                     EXSR #LRPT1
     C                     ENDIF                                                              232050
      *
      *  Create extract record if extract mode
     C           ULMODE    IFEQ 'EXTRACT'
     C                     EXSR #LXTRC
     C                     ENDIF
      *
      *  Details are invalid - report details to BY2040P2
     C                     ELSE
     C           RECI      IFEQ 'D'                                                           232050
     C                     EXSR #LRPT2
     C                     ENDIF                                                              232050
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LXTRC - Create Extract Record                                *
      *                                                               *
      * Called by:  SR/#LRPXT - Report Extract Processing             *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LXTRC    BEGSR                           ** SR/#LXTRC**
      *
      *  Clear extract file format
     C                     CLEARBYEXDTD0
      *
      *  Set up detail fields
     C                     MOVE 'B'       L8RTYP           Record type
     C                     Z-ADDULPCOD    L8PCOD           Product code
     C**********           Z-ADDULCNUM    L8CUST           Customer                           CSD027
     C                     MOVE ULCNUM    L8CUST           Customer                           CSD027
     C                     MOVE ULCYCD    L8CYCD           Currency
     C********** 'F'       CAT  ULFACT    L8TRAN           Transaction      LLN021
     C           ULFACT    CAT  ULFCNO    @WORK5  5        Transaction      LLN021
     C           'F'       CAT  @WORK5    L8TRAN           Transaction      LLN021
     C***********          CAT  ULFCNO    L8TRAN                    202299  LLN021
     C***********L8TRAN    CAT  ULFCNO    L8TRAN                    202299  LLN021
     C                     MOVE ULBRCA    L8BRCD           Branch code
     C                     MOVE 'FC'      L8MMOD           Module id.
     C                     Z-ADDULVDAT    L8DDAT           Value date
     C                     Z-ADDULVDAT    L8VDAT           Value date
     C                     Z-ADDULMDAT    L8MDAT           Maturity date
     C                     MOVE ULMCAT    L8MCAT           Maturity category
     C                     MOVE ULLISO    L8ISOC           Customer country code
     C                     MOVE ULRISO    L8RISO           Risk country code
     C                     MOVE ULLIND    L8LIND           Industry code
     C                     MOVE ULLINS    L8LINS           Institution code
     C                     MOVE A6SWCY    L8CCYS           SWIFT Currency code
     C           A6SWCY    IFEQ BLCYCD
     C           MCCY      ANDNE'Y'
     C                     MOVE 'N'       L8NNSI           Sterling ccy ind.
     C                     ELSE
     C                     MOVE 'Y'       L8NNSI
     C                     ENDIF
     C                     Z-ADDULCUAS    L8CUAS           Currency amount
     C                     Z-ADDULSGAM    L8SGAM           Sterling equivalent
     C           MCCY      IFEQ 'Y'
     C                     MOVE 'M'       L8STGI           Sterling ind.
     C                     ELSE
     C                     MOVE *BLANK    L8STGI           Sterling ind.
     C                     ENDIF
     C                     MOVE BBCSSN    L8CSSN           Customer shortname
     C                     SELEC
     C           LUPAR     WHNE *BLANK
     C                     MOVE LUPAR     L8PCNB           Ultimate parent
     C           BBPCNB    WHNE *BLANK
     C                     MOVE BBPCNB    L8PCNB           Customer parent
     C                     OTHER
     C                     MOVE BBCUST    L8PCNB           Use customer no.
     C                     ENDSL
     C                     MOVE LELGF     L8ELIG           Eligibility flag
     C                     MOVE ULRWCD    L8RWCD           Risk weighting code
     C***********          MOVE LLENT     L8NETI           Netting indicator                 LLN016
     C                     MOVE LHASI     L8HAIN           Housing association
      *                                                                   LLN010
      *  Facility may be drawndown in 'in' currencies only                LLN010
     C           LEURO     IFEQ 'Y'                                       LLN010
     C                     MOVEL'E'       L8STGI                          LLN010
     C                     ENDIF                                          LLN010
      *                                                                   LLN012
      * Set extract record flags for BT and LR return                     LLN012
     C                     MOVE ULBTEX    L8BTEX                          LLN012
     C                     MOVE ULLREX    L8LREX                          LLN012
      *                                                                   LLN012
      ** Set-up other fields                                              LLN012
     C                     MOVE LCNTP     L8CNTP                          LLN012
     C                     MOVE '00'      L8GTEE           Null value     LLN012
     C                     MOVE '0'       L8QUAL           Null value     LLN012
     C                     MOVE ULETYP    L8ETYP                          LLN012
      *
      ** Check whether facility expired this month                           LLN021
      *
     C                     EXSR #MATUR                                       LLN021
      *
     C           @MATS     IFEQ 'Y'                                       LLN021
     C                     WRITEBYMTDTP0                                  LLN021
     C                     MOVE *BLANK    @MATS   1        Mature Indicator  LLN021
     C                     ELSE                                           LLN021
      *                                                                   LLN021
      *  Write record to extraction file
     C                     WRITEBYEXDTD0
     C                     ENDIF                                          LLN021
      *
      *
      *  Accumulate total assets and liabilities
     C           RECI      IFEQ 'D'                                                           232050
     C                     Z-ADD1         #
     C           ULCYCD    LOKUP@CCY,#                   90
     C           ULPCOD    IFGE 1100
     C           ULPCOD    ANDLE1960
     C                     ADD  ULCUAS    @TAC,#
     C                     ADD  ULSGAM    @TAS,#
     C                     ENDIF
     C           ULPCOD    IFGE 2100
     C           ULPCOD    ANDLE3990
     C                     ADD  ULCUAS    @TLC,#
     C                     ADD  ULSGAM    @TLS,#
     C                     ENDIF
     C                     ENDIF                                                              232050
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LRPT1 - Write Details of Valid LOANS to BY2040P1             *
      *                                                               *
      * Called by:  #LRPXT - Report Ectract Processing                *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPT1    BEGSR                           ** SR/#LRPT1**
      *
      *  Reset print indicator
     C                     MOVE *OFF      *IN30
      *
      *  Set up report fields
     C                     MOVELULCNUM    L#CNUM           Customer
     C                     CALL 'ZDATE2'                   Value date
     C                     PARM ULVDAT    ULDAYN
     C                     PARM           BJDFIN
     C                     PARM           ULDATE
     C           L#VDAT    PARM           ULDATA
     C           ULMDAT    IFNE 0
     C                     CALL 'ZDATE2'                   Maturity date
     C                     PARM ULMDAT    ULDAYN
     C                     PARM           BJDFIN
     C                     PARM           ULDATE
     C           L#MDAT    PARM           ULDATA
     C                     ELSE
     C                     MOVE *BLANKS   L#MDAT
     C                     ENDIF
     C                     MOVE ULFACT    L#FACT           facility typ
     C                     MOVE ULFCNO    L#FCNO           facility no.
     C                     MOVELULCYCD    L#CYCD           Currency
     C                     MOVE ULLISO    L#LISO           Customer country code
     C                     MOVE ULRISO    L#RISO           Risk country code
     C                     MOVE BBCOLC    L#COLC           Country of location
     C                     MOVE BBCNCZ    L#CNCZ           Country of citizen.
     C                     MOVE ULLIND    L#LIND           Industry code
     C                     MOVE ULLINS    L#LINS           Institution code
     C                     MOVE BBCRNM    L#CRNM           Customer report name
     C                     CALL 'ZSEDIT'
     C                     PARM ULCUAS    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Currency amount
     C                     MOVE ULAMTA    L#CUAS
     C                     CALL 'ZSEDIT'
     C                     PARM ULSGAM    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Sterling equivalent
     C                     MOVE ULAMTA    L#SGAM
     C                     MOVE ULPCOD    L#PCOD           Product code
      *
      *
      * Check if customer number and name to be printed
     C           ULCNUM    IFNE ULCUS1
     C                     MOVE *ON       *IN30            Print cust n
     C**********           Z-ADDULCNUM    ULCUS1                                              CSD027
     C                     MOVE ULCNUM    ULCUS1                                              CSD027
     C                     ENDIF
     C*
     C* Start of change for LLN012.
     C* Report BT and LR indicators on BY2010D1.
     C                     MOVELULBTEX    L#BTEX
     C                     MOVELULLREX    L#LREX
     C* End of change for LLN012.
     C*
     C*  Write details to report
     C           ULCLN1    IFGT ULOVF1                     Overflow
     C                     WRITEBY2040H1
     C                     MOVE *ON       *IN30            Print cust n
     C                     END
     C                     WRITEBY2040D1               90
      *
      *  Set flag to indicate details written
     C           ULRPT1    IFNE 'Y'
     C                     MOVE 'Y'       ULRPT1
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LRPT2 - Write Details of Invalid Deals To BY2040P2           *
      *                                                               *
      * Called by:  #LRPXT - Report Ectract Processing                *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPT2    BEGSR                           ** SR/#LRPT2**
      *
      *  Reset report indicators
     C                     MOVEA'0000000' *IN,31
     C                     MOVEA'0'       *IN,41
      *
      *  Set up report fields
     C**********           MOVELULCNUM    L#CNUM           Customer                           CSD027
     C                     MOVE ULCNUM    L#CNUM           Customer                           CSD027
     C                     CALL 'ZDATE2'                   Value date
     C                     PARM ULVDAT    ULDAYN
     C                     PARM           BJDFIN
     C                     PARM           ULDATE
     C           L#VDAT    PARM           ULDATA
     C           ULMDAT    IFNE 0
     C                     CALL 'ZDATE2'                   Maturity date
     C                     PARM ULMDAT    ULDAYN
     C                     PARM           BJDFIN
     C                     PARM           ULDATE
     C           L#MDAT    PARM           ULDATA
     C                     ELSE
     C                     MOVE *BLANKS   L#MDAT
     C                     ENDIF
     C                     MOVE ULFACT    L#FACT           facility typ
     C                     MOVE ULFCNO    L#FCNO           facility no.
     C                     MOVELULCYCD    L#CYCD           Currency
     C                     MOVE ULLISO    L#LISO           Customer country code
     C                     MOVE ULRISO    L#RISO           Risk country code
     C                     MOVE BBCOLC    L#COLC           Country of location
     C                     MOVE BBCNCZ    L#CNCZ           Country of citizen.
     C                     MOVE ULLIND    L#LIND           Industry code
     C                     MOVE ULLINS    L#LINS           Institution code
     C                     MOVE BBCRNM    L#CRNM           Customer report name
     C                     CALL 'ZSEDIT'
     C                     PARM ULCUAS    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Currency amount
     C                     MOVE ULAMTA    L#CUAS
     C                     CALL 'ZSEDIT'
     C                     PARM ULSGAM    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Sterling equivalent
     C                     MOVE ULAMTA    L#SGAM
     C                     MOVE ULPCOD    L#PCOD           Product code
      *
      * Check if customer number and name to be printed
     C           ULCNUM    IFNE ULCUS2
     C           ULERR1    ANDNE0
     C                     MOVE ULERR1    ULERR2
     C                     MOVEAULERR2    *IN,31
     C                     ENDIF
     C           ULPCOD    IFEQ 9999
     C                     MOVE *ON       *IN41
     C                     ENDIF
      *
      *  Determine no. of errors to be reported for overflow check
     C                     Z-ADD0         X       20
     C   31                ADD  1         X
     C   32                ADD  1         X
     C   33                ADD  1         X
     C   34                ADD  1         X
     C   35                ADD  1         X
     C   36                ADD  1         X
     C   37                ADD  1         X
      *
      *  Write details to report
     C           ULOVF2    SUB  ULCLN2    ULLDIF
     C                     SUB  X         ULLDIF
     C           ULLDIF    IFLE 5
     C           ULCNUM    ANDNEULCUS2
     C           ULLDIF    ORLT 0
     C           ULCNUM    ANDEQULCUS2
     C                     WRITEBY2040H2
     C                     WRITEBY2040D2
     C                     ELSE
     C           ULCNUM    IFNE ULCUS2
     C           ULBRCA    ORNE ULBRC2                                                        LLN022
     C                     WRITEBY2040D2               90
     C**********           Z-ADDULCNUM    ULCUS2                                              CSD027
     C                     MOVE ULCNUM    ULCUS2                                              CSD027
     C                     MOVE ULBRCA    ULBRC2                                              LLN022
     C                     ENDIF
     C                     ENDIF
     C                     WRITEBY2040D3               90
      *
      *  Set flag to indicate details written
     C           ULRPT2    IFNE 'Y'
     C                     MOVE 'Y'       ULRPT2
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRPTT - Final Reporting                                   *
      *                                                               *
      * Called by:  SR/#LBRCH - Branch Processing Routine             *
      *             SR/#LTERM - Termination Processing                *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           #LRPTT    BEGSR                           ** SR/#LRPTT**
      *
      *  No details to report - BY2040P1
     C           ULRPT1    IFNE 'Y'
     C                     WRITEBY2040E1               90
     C                     ENDIF
      *
      *  No details to report - BY2040P2
     C           ULRPT2    IFNE 'Y'
     C                     WRITEBY2040E2               90
     C                     ENDIF
      *
      *   Report currency summary if EXTRACT mode
     C           ULMODE    IFEQ 'EXTRACT'
     C           ULRPT1    ANDEQ'Y'
      *
      *   Write heading
     C                     WRITEBY2040R1
      *
      *   Write detail record for each currency
     C                     Z-ADD0         ULTOTA
     C                     Z-ADD0         ULTOTL
     C                     Z-ADD1         #
     C           @CCY,#    DOWNE*BLANK
     C           #         ANDLT200
     C           #         OCUR SDCURR
      *
     C           @TAC,#    IFNE 0
     C           @TLC,#    ORNE 0
     C                     MOVE A6CYCD    L#CYCD           Currency
     C                     MOVE A6CYNM    L#CYNM           Currency name
     C                     CALL 'ZSEDIT'
     C                     PARM @TAC,#    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - ccy
     C                     MOVE ULAMTA    L#TLAC
     C                     CALL 'ZSEDIT'
     C                     PARM @TAS,#    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - STG
     C                     MOVE ULAMTA    L#TLAS
     C                     CALL 'ZSEDIT'
     C                     PARM @TLC,#    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - ccy
     C                     MOVE ULAMTA    L#TLLC
     C                     CALL 'ZSEDIT'
     C                     PARM @TLS,#    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - STG
     C                     MOVE ULAMTA    L#TLLS
      *
      *   Report details
     C                     WRITEBY2040S1               90
     C           ULCLN1    IFGT ULOVF1                     Overflow
     C                     WRITEBY2040R1
     C                     ENDIF
      *
      *  Accumulate total assets and liabilities in sterling
     C                     ADD  @TAS,#    ULTOTA
     C                     ADD  @TLS,#    ULTOTL
     C                     ENDIF
      *
     C                     ADD  1         #
     C                     ENDDO
      *
      *
      *   Report overall asset and libilities totals
     C                     CALL 'ZSEDIT'
     C                     PARM ULTOTA    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - STG
     C                     MOVE ULAMTA    L#TOTA
     C                     CALL 'ZSEDIT'
     C                     PARM ULTOTL    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - STG
     C                     MOVE ULAMTA    L#TOTL
     C           ULCLN1    IFGT ULOVF1                     Overflow
     C                     WRITEBY2040R1
     C                     ENDIF
     C                     WRITEBY2040T1               90
      *
     C                     ENDIF
      *
      *
      *  Write end of report - BY2040P1
     C                     WRITEBY2040Z1               90
      *
      *  Write end of report - BY2040P2
     C                     WRITEBY2040Z2               90
      *
      *  Close files
     C                     CLOSEBY2040P1
     C                     CLOSEBY2040P2
      *
      *  Reset file open flag indicator and totals
     C                     MOVE 'N'       ULOPNF
     C                     MOVE 'N'       ULRPT1
     C                     MOVE 'N'       ULRPT2
     C                     Z-ADD0         PAGE1
     C                     Z-ADD0         PAGE2
      *
     C                     ENDSR
      /EJECT
      *****************************************************************   LLN012
      *                                                               *   LLN012
      * #LFEES - Extract Fees for Facility                            *   LLN012
      *                                                               *   LLN012
      * Called by:  None - Contains definitions only                  *   LLN012
      *                                                               *   LLN012
      * Calls: None                                                   *   LLN012
      *                                                               *   LLN012
      *                                                               *   LLN012
      *****************************************************************   LLN012
     C           #LFEES    BEGSR                           ** SR/#LFEES** LLN012
      *                                                                   LLN012
      *                                                                   LLN012
      * ******************* current facility where event date       LLN012168646
      * ******************* report extract date.                    LLN012168646
      * Read all events for current facility where event date             168646
      * is less than or equal the LR report extract date.                 168646
     C           K2FCLT    SETLLLVENTELF                                  LLN012
     C           K2FCLT    READELVENTELF                 87               LLN012
     C           *IN87     DOWEQ*OFF                                      LLN012
     C***********EDAT      ANDLTL0LRDT                              LLN012168646
     C           EDAT      ANDLEL0LRDT                                    168646
      *                                                                   LLN012
      * Only generate additional extract for event type 71F2              LLN012
     C           ETYP      IFEQ '71F2'                                    LLN012
      *                                                                   LLN012
      * Set up extract details...                                         LLN012
      * ...Project Code                                                   LLN012
     C                     Z-ADD1915      ULPCOD                          LLN012
      *                                                                   LLN012
      * ...Currency amount                                                LLN012
     C                     Z-ADDEAMT      ULCUAS                          LLN012
      *                                                                   LLN012
      * Reverse the sign if the event is an Outflow                       LLN012
     C           INOI      IFEQ 'O'                                       LLN012
     C                     MULT -1        ULCUAS                          LLN012
     C                     ENDIF                                          LLN012
      *                                                                   LLN012
      *  Convert amount to sterling                                       LLN012
     C           ULCYCD    IFEQ BLCYCD                                    LLN012
     C                     Z-ADDULCUAS    ULSGAM                          LLN012
     C                     ELSE                                           LLN012
     C                     CALL 'BY9070'                                  LLN012
     C                     PARM ULCUAS    ULINAM                          LLN012
     C                     PARM ULCYCD    ULFCCY                          LLN012
     C                     PARM *ZERO     ULOTAM                          LLN012
     C                     PARM BLCYCD    ULTCCY                          LLN012
     C                     PARM *BLANK    ULRTCD                          LLN012
     C           ULRTCD    IFNE ' '                                       LLN012
     C           *LOCK     IN   LDA                                       LLN012
     C                     MOVELULPGMN    DBPGM            ************   LLN012
     C                     MOVEL'*NONE'   DBKEY            * DBERR 16 *   LLN012
     C                     MOVEL'BY9070  'DBFILE           ************   LLN012
     C                     Z-ADD16        DBASE                           LLN012
     C                     OUT  LDA                                       LLN012
     C                     EXSR *PSSR                                     LLN012
     C                     ENDIF                                          LLN012
     C                     Z-ADDULOTAM    ULSGAM                          LLN012
     C                     ENDIF                                          LLN012
      *                                                                   LLN012
      * ...Maturity date                                                  LLN012
     C                     Z-ADDEDAT      ULMDAT                          LLN012
      *                                                                   LLN012
      * Set extract record flags for BT and LR return                     LLN012
     C                     MOVE 'N'       ULBTEX                          LLN012
     C                     MOVE 'Y'       ULLREX                          LLN012
      *                                                                   LLN012
      ** Set-up event type                                                LLN012
     C                     MOVE 'C'       ULETYP                          LLN012
      *                                                                   LLN012
      * Generate the extract                                              LLN012
     C                     EXSR #LRPXT                                    LLN012
     C                     ENDIF                                          LLN012
      *                                                                   LLN012
     C           K2FCLT    READELVENTELF                 87               LLN012
     C                     ENDDO                                          LLN012
      *                                                                   LLN012
     C                     ENDSR                                          LLN012
      /EJECT                                                              LLN012
      *****************************************************************
      *                                                               *
      * #LDEFN - Field Definition Routine                             *
      *                                                               *
      * Called by:  None - Contains definitions only                  *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *                                                               *
      *****************************************************************
     C           #LDEFN    BEGSR                           ** SR/#LDEFN**
      *
      *  Copyright statement
     C                     MOVEA@CPY      ULCPY@ 80
      *
      ** Define external data areas
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           RUNDAT
      *
      ** Define and initialise work fields
     C                     Z-ADD0         #       50       Array index
     C                     Z-ADD0         ULSFNO  60       Spool file no.
     C                     Z-ADD0         ULDAYN  50       Date - Midas day no.
     C                     Z-ADD0         ULDATE  60       Date - DDMMYY
     C                     Z-ADD0         ULERR1  70       Error indicators
     C                     Z-ADD0         ULTOTA 150       Total assets
     C                     Z-ADD0         ULTOTL 150       Total liabilities
     C                     Z-ADD0         ULMPER  50       Category period date
     C                     Z-ADD0         ULINAM 150       In amount - converted
     C                     Z-ADD0         ULOTAM 150       Out amount - convertd
     C                     Z-ADD0         ULLDIF  20       Line difference
     C                     MOVE A6INCY    A6INCY  1        Error indicators
     C                     MOVE 'N'       ULERRI  1        Error indicators
     C                     MOVE 'N'       ULVALD  1        Valid deal
     C                     MOVE 'N'       ULRPT1  1        Details reported
     C                     MOVE 'N'       ULRPT2  1        Details reported
     C                     MOVE 'N'       ULOPNF  1        Files opened
     C                     MOVE *BLANK    ULRTCD  1        Return code
     C                     MOVE *BLANK    ULBRCA  3        Branch code
     C                     MOVE *BLANK    ULBRC2  3        Branch code                        LLN022
     C                     MOVE *BLANK    P@RTCD  7        Return code
     C                     MOVE *BLANK    P@OPTN  7        Option
     C                     MOVE *BLANK    ULDATA  7        Date - DDMMMYY
     C                     MOVE *BLANK    ULLOCN  3        Location
     C                     MOVE *BLANK    ULAMTA 21        Edited amount field
     C                     MOVE *BLANK    ULECOD  1        Edit code
     C                     MOVE *BLANK    ULFNAM 10        File name
     C                     MOVE *BLANK    ULERR2  7        Error indicators
     C                     MOVE *BLANK    ULODIN  1        overdue ind tors
     C                     MOVE *BLANK    ULRISO  2        risk countrytors
     C                     MOVE *BLANK    UPLISO  2        Cust countrytors
     C                     MOVE *BLANK    UPRISO  2        Risk countrytors
     C                     MOVE *BLANK    WWRISO  2        work countrytors
     C                     MOVE *BLANK    ULCSNO  6        work cust notors
     C                     MOVE *BLANK    ULFACT  3        Facility type
     C                     MOVE *BLANK    ULFCNO  2        Facility no.
     C           *LIKE     DEFN CNUM      ULCNUM           Customer
     C           *LIKE     DEFN CNUM      ULPCUS           Previous customer
     C           *LIKE     DEFN CNUM      ULCUS1           Print customer 1
     C           *LIKE     DEFN CNUM      ULCUS2           Print customer 2
     C           *LIKE     DEFN FCCY      ULCYCD           Currency
     C           *LIKE     DEFN L8PCOD    ULPCOD           Product code
     C           *LIKE     DEFN L8CUAS    ULCUAS           Currency amount
     C           *LIKE     DEFN L8SGAM    ULSGAM           Sterling equivalent
     C           *LIKE     DEFN A8IBOE    ULIBOE           Include branch
     C           *LIKE     DEFN A6NBDP    ULNBDP           No. of decimal places
     C           *LIKE     DEFN A6NBDP    UL$NDP           Sterling dec. places
     C           *LIKE     DEFN BBCUST    ULCUST           Customer
     C           *LIKE     DEFN ULCUAS    ULAMNT           Amount field
     C           *LIKE     DEFN FCCY      ULFCCY           From currency
     C           *LIKE     DEFN FCCY      ULTCCY           To currency
     C********** *LIKE     DEFN BBLICD    ULLIND           Industry code                    LLN016
     C********** *LIKE     DEFN BBLINC    ULLINS           Institution code                 LLN016
     C           *LIKE     DEFN LLICD     ULLIND           Industry code                    LLN016
     C           *LIKE     DEFN LLINC     ULLINS           Institution code                 LLN016
     C           *LIKE     DEFN STDT      ULVDAT           Start date
      *
      *
      *  Key List Definitions
      *  ====================
      *
      *  Facilities extract criteria
     C           K1FCEX    KLIST
     C                     KFLD           L4FCTP           Facility typ
     C                     KFLD           L4LINS           Institution
     C                     KFLD           L4MIND           Industry code                      LLN021
      *
      *  Facilities key list
     C           K2FCLT    KLIST
     C                     KFLD           CNUM             Facility customer
     C                     KFLD           FACT             Facility type
     C                     KFLD           FCNO             Facility sequence no.
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
      *  Check if error has previously been reported
     C           ULERRI    IFNE 'Y'
     C                     MOVE 'Y'       ULERRI
     C                     MOVE 'Y'       ULRTCD
     C                     DUMP
     C                     ENDIF
      *
      *  Set on external indicators
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LTERM - Termination Processing                               *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LTERM    BEGSR                           ** SR/#LTERM**
      *
      *  End of report
     C           ULOPNF    IFEQ 'Y'
     C                     EXSR #LRPTT
     C                     ENDIF
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation Routine                       *
      *                                                               *
      * Called by:  At program initialisation                         *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR                           ** #INZSR SR**
      *                                                                   LLN010
      * Key list for the extension file                                   LLN010
     C           KEY1      KLIST                                          LLN010
     C**********           KFLD           KCNUM   60                                   LLN010 CSD027
     C                     KFLD           KCNUM   6                                           CSD027
     C                     KFLD           KFACT   30                      LLN010
     C                     KFLD           KFCNO   20                      LLN010
      *
     C           *ENTRY    PLIST
     C                     PARM           ULMODE  7        Run mode
     C                     PARM           ULRSEQ  5        Report sequence
     C                     PARM           ULRTCD           Return code
     C                     PARM           CUREOM  5                    LLN021
     C                     PARM           CURSOM  5                    LLN021
      *
      *  Set report indicator according to mode
     C           ULMODE    IFEQ 'EXTRACT'
     C                     MOVE *ON       *IN21
     C                     ELSE
     C                     MOVE *OFF      *IN21
     C                     ENDIF
      *
      *                                                                    LLN021
      *  Get beginning and end of month dates                              LLN021
      *                                                                    LLN021
     C                     MOVELCURSOM    CURSO   50                       LLN021
     C                     MOVELCUREOM    CUREO   50                       LLN021
      *                                                                    LLN021
      *  Get Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*FIRST ' P@OPTN
     C                     PARM           SDBANK
     C           P@RTCD    IFNE *BLANKS
     C           BJTYLC    OREQ 'D'                        ON-deleted
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 06 *
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     Z-ADD06        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ELSE
     C                     MOVE *OFF      *IN98
     C                     END
      *
      *
      *  Get General Ledger ICD Details
     C                     CALL 'AOGELRR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*FIRST ' P@OPTN
     C                     PARM           SDGELR
     C           P@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*FIRST'  DBKEY            * DBERR 07 *
     C                     MOVEL'SDGELRPD'DBFILE           ************
     C                     Z-ADD07        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  Get Trading Data ICD Record For GBP Corrency Code (BLCYCD)
     C                     CALL 'AOTRADR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*FIRST ' P@OPTN
     C                     PARM           SDTRAD
     C           P@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*FIRST'  DBKEY            * DBERR 08 *
     C                     MOVEL'SDTRADPD'DBFILE           ************
     C                     Z-ADD08        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                   LLN012
      ** Check switchable feature to see if LLN012 is on                  LLN012
     C                     MOVE 'N'       LLN012  1                       LLN012
     C                     CALL 'AOSARDR0'                                LLN012
     C                     PARM '       ' @RTCD   7                       LLN012
     C                     PARM '*VERIFY' @OPTN   7                       LLN012
     C                     PARM 'LLN012'  @SARD   6                       LLN012
     C           @RTCD     IFEQ *BLANKS                                   LLN012
     C                     MOVE 'Y'       LLN012                          LLN012
     C                     ENDIF                                          LLN012
      *
      *  Initialise work fields
     C                     EXSR #LDEFN
      *
      *
      *  Get the extract date
     C                     OPEN BYRPDTPP
     C                     READ BYRPDTPP                 90
     C           *IN90     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 09 *
     C                     MOVEL'BYRPDTPP'DBFILE           ************
     C                     Z-ADD09        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     CLOSEBYRPDTPP
      *
      *  Check if multi-branching
     C                     IN   RUNDAT
     C                     UNLCKRUNDAT
      *  Single branch:
     C           AGMBIN    IFNE 'Y'
     C                     MOVE 'Y'       ULOPNF
     C                     MOVE *OFF      *IN20
      *
      *  RCF processing for printer file BY2040P1
     C                     OPEN BY2040P1
     C                     Z-ADDULNUM1    ULSFNO
     C                     CALL 'ZSFILE'
     C                     PARM           ULRSEQ
     C                     PARM           ULBRCA
     C                     PARM 'BY2040P1'ULFNAM
     C                     PARM           ULSFNO
     C                     PARM           ULRTCD
      *
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 10 *
     C                     MOVEL'ZSFILE  'DBFILE           ************
     C                     Z-ADD10        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  RCF processing for printer file BY2040P2
     C                     OPEN BY2040P2
     C                     Z-ADDULNUM2    ULSFNO
     C                     CALL 'ZSFILE'
     C                     PARM           ULRSEQ
     C                     PARM           ULBRCA
     C                     PARM 'BY2040P2'ULFNAM
     C                     PARM           ULSFNO
     C                     PARM           ULRTCD
      *
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 11 *
     C                     MOVEL'ZSFILE  'DBFILE           ************
     C                     Z-ADD11        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  Write report headings to BY2040P1 and BY2040P2
     C                     MOVE L0RPDA    L#RPDA
     C                     WRITEBY2040H1
     C                     WRITEBY2040H2
      *
      *  Multi branching indicator
     C                     ELSE
     C                     MOVE *ON       *IN20
     C                     ENDIF
      *
      *  Load currency details
     C                     Z-ADD0         #
     C           *LOVAL    SETLLSDCURRL0
     C           *IN90     DOWEQ*OFF
     C           #         ANDLT200
     C                     ADD  1         #
     C           #         OCUR SDCURR
     C                     READ SDCURRL0                 90
     C           *IN90     IFEQ *OFF
     C                     MOVE A6CYCD    @CCY,#
     C                     ENDIF
     C                     ENDDO
      *
      *  Get decimal places for sterling currency code
     C                     Z-ADD1         #
     C           BLCYCD    LOKUP@CCY,#                   90
     C           *IN90     IFEQ *ON
     C           #         OCUR SDCURR
     C                     Z-ADDA6NBDP    UL$NDP
     C                     ELSE
     C           200       OCUR SDCURR
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*KEY   ' P@OPTN
     C                     PARM           BLCYCD
     C           SDCURR    PARM           DSSDY
     C           P@RTCD    IFNE *BLANK                     Error on call
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVELBLCYCD    DBKEY            * DBERR 12 *
     C                     MOVEL'SDCURRL0'DBFILE           ************
     C                     Z-ADD12        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    UL$NDP
     C                     ENDIF
      *
      *  Open extract file if EXTRACT mode
     C           ULMODE    IFEQ 'EXTRACT'
     C                     OPEN BYEXDTPP
     C                     OPEN BYMTDTPP                                  LLN021
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************   LLN021
      *                                                               *   LLN021
      * SR/#MATUR - Check Security matured this month                 *   LLN021
      *                                                               *   LLN021
      * Called by:  SR/#LSECD - Main Processing                       *   LLN021
      *                                                               *   LLN021
      * Calls:                                                        *   LLN021
      *                                                               *   LLN021
      *****************************************************************   LLN021
      *                                                                   LLN021
     C           #MATUR    BEGSR                           ** SR/#MATUR** LLN021
      *                                                                   LLN021
     C           RECI      IFEQ 'E'
      *                                                                      LLN021
     C           DTEX      IFNE 0                                         LLN021
      *                                                                   LLN021
     C           DTEX      IFLE CUREO                                      LLN021
     C           DTEX      ANDGECURSO                                      LLN021
     C                     MOVE 'Y'       @MATS                              LLN021
     C                     ENDIF                                             LLN021
      *                                                                      LLN021
     C                     ENDIF                                             LLN021
      *                                                                      LLN021
     C                     ENDIF                                             LLN021
      *                                                                      LLN021
     C                     ENDSR                                          LLN021
      /EJECT
      ********************************************************************
**  CPY@
(c) Finastra International Limited 1997
