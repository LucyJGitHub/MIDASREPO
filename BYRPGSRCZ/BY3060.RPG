     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BoE Composite Currencies Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY3060 - Composite Currencies Report                         *
      *                                                               *
      *  Function:  This program prints a report of Composite         *
      *             Currencies                                        *
      *                                                               *
      *  Called By: mmCnnnn - (program name)                          *
      *                                                               *
      *  (c) Finastra International Limited 1997                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. LLN021             Date 13Nov03               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *  Amendment Details:                                           *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN021 - Recompile due to file changes                       *
      *                                                               *
      *****************************************************************
      *
      * File description for files defined
     FBYCCCYPPUF  E           K        DISK         KINFSR *PSSR
     F                                              KINFDS INFDS1
     FBY3060P1O   E             50     PRINTER      KINFSR *PSSR
     F                                              KINFDS SPOOL
     FSDCURRL1IF  E           K        DISK         KINFSR *PSSR
      /EJECT
      **************************************************************************
      *                                                               *
      *  Indicators Used:                                             *
      *  ~~~~~~~~~~~~~~~                                              *
      * 40 - Print 'Action' heading in report                         *
      * 41 - Condition 'Currency' and 'Action' to print once per record
      * 50 - Page Overflow                                            *
      * 89 - READ BYCCCYPP                                            *
      * 90 - General error indicator                                  *
      *                                                               *
      **************************************************************************
     E                    CPY@    1   1 80               Copyright statement
     E                    CCYN       20  3               Currency array
     E                    PCTN       20  5               Percentage array
      *
      /SPACE 3
      ** Local data area for database error details
     ILDA       E DSLDA                         256
      *
      ** Data Structure using Bank file format
     ISDBANK    E DSSDBANKPD
      *
      ** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM ULPGM
     I                                      244 253 ULWSID
     I                                      254 263 ULUSER
      *
      ** File information data structure (for database files only)
     IINFDS1    E DSY2I1DSP
      *
      ** File information data structure (for printer files only)
     ISPOOL       DS
     I                                       83  92 ULSPFL
     I                                    B 123 1240ULSNUM
      *
      ** First DS for access programs, short data structure
     IDSFDY     E DSDSFDY
      *
      ** Second DS for access programs, long data structure
     IDSSDY     E DSDSSDY
      *
      *
      /EJECT
      *****************************************************************
      * Main Processing                                               *
      *                                                               *
      * Calls: SR/#LMAIN - Report Processing                          *
      *        SR/#LTERM - Termination Processing                     *
      *****************************************************************
      *
      *
      *  Report Processing
     C                     EXSR #LMAIN
      *
      *
      *  Termination Processing
     C                     EXSR #LTERM
      *
      *
      /EJECT
      *****************************************************************
      * #LMAIN - Report Processing                                    *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:                                                        *
      *****************************************************************
      *
     C           #LMAIN    BEGSR                           ** SR/#LMAIN**
      *
      **  Set indicators, based on run mode
     C                     READ BYCCCYPP                 89
     C           *IN89     DOWEQ*OFF
      *
      *   Process record according to run mode
     C           ULMODE    IFEQ 'LIST'
     C           ULMODE    OREQ 'AUDIT'
     C           L6LCD     ANDEQBJRDNB
     C           ULMODE    OREQ 'INTERIM'
     C           L6LCD     ANDEQBJRDNB
      *
      *   Print details
     C                     EXSR #LREPT
      *
      *   Insert a space before next record
     C                     WRITEBY3060S1
      *
      *   If run mode is AUDIT and record deleted
     C           ULMODE    IFEQ 'AUDIT'
     C           L6CHTP    ANDEQ'D'
     C           L6CCCY    DELETBYCCCYPP             90
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ BYCCCYPP                 89
     C                     ENDDO
      *
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LREPT - Prints report                                        *
      *                                                               *
      * Called by: #LMAIN                                             *
      *                                                               *
      * Calls:                                                        *
      *****************************************************************
      *
     C           #LREPT    BEGSR                           ** SR/#LREPT**
      *
      ** Format fields and reset indicators
     C                     EXSR #LFORM
      *
     C           X         DOWLE20
     C           CCYN,X    ANDNE*BLANKS
      *
      ** Find currency name
     C           L6CCCY    CHAINSDCURRL1             90
     C                     MOVE A6CYNM    L#CCNM
      *
      ** Reset 'Record Written' flag
     C                     MOVE *BLANKS   ULWRIT
      *
      ** Don't print CURRENCY and ACTION after first line of record
     C           X         IFGT 4
     C                     MOVE *OFF      *IN41
     C                     ENDIF
      *
      **  Sequentially print currencies as read from array
     C                     EXSR #LSEQP
      *
      **  Write new header on page overflow
     C           *IN50     IFEQ *ON
     C                     WRITEBY3060H1
     C                     MOVE *OFF      *IN50
     C                     ENDIF
      *
     C                     ADD  1         X
      *
     C                     ENDDO
      *
      ** Print details if not printed within loop
     C           ULWRIT    IFNE 'Y'
     C                     WRITEBY3060D1
     C                     MOVE 'Y'       ULWRIT
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      * #LFORM - Formats fields, resets indicators and counters       *
      *                                                               *
      * Called by:  #LREPT                                            *
      *                                                               *
      * Calls:      None                                              *
      *****************************************************************
      *
     C           #LFORM    BEGSR
      *
      ** Report Action Code for AUDIT and INTERIM
     C           ULMODE    IFEQ 'AUDIT  '
     C           ULMODE    OREQ 'INTERIM'
      *
     C                     SELEC
     C           L6CHTP    WHEQ 'I'
     C                     MOVE 'Inserted'L#CHTP
     C           L6CHTP    WHEQ 'A'
     C                     MOVE 'Amended 'L#CHTP
     C           L6CHTP    WHEQ 'D'
     C                     MOVE 'Deleted 'L#CHTP
     C                     ENDSL
      *
     C                     ENDIF
      *
      ** Move physical file fields into arrays
     C                     MOVE *BLANKS   CCYN
     C                     MOVE *BLANKS   PCTN
     C                     MOVEAL6CCYA    CCYN
     C                     MOVEAL6PCTA    PCTN
      *
      ** Move physical file field into printer file field
     C                     MOVE L6CCCY    L#CCCY
      *
      ** Reset counters
     C                     Z-ADD1         X
     C                     MOVE 0         COUNT
      *
      ** Reset indicator
     C                     MOVE *ON       *IN41
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      * #LSEQP - Sequentially prints currencies as read from array    *
      *                                                               *
      * Called by:  #LREPT                                            *
      *                                                               *
      * Calls:      None                                              *
      *****************************************************************
      *
     C           #LSEQP    BEGSR
      *
     C                     ADD  1         COUNT
     C                     SELEC
     C           COUNT     WHEQ 1
     C                     MOVE CCYN,X    L#CCY1
     C                     MOVE PCTN,X    L#PCT1
     C           COUNT     WHEQ 2
     C                     MOVE CCYN,X    L#CCY2
     C                     MOVE PCTN,X    L#PCT2
     C           COUNT     WHEQ 3
     C                     MOVE CCYN,X    L#CCY3
     C                     MOVE PCTN,X    L#PCT3
     C           COUNT     WHEQ 4
     C                     MOVE CCYN,X    L#CCY4
     C                     MOVE PCTN,X    L#PCT4
     C                     MOVE 0         COUNT            Reset count
     C                     WRITEBY3060D1                   Detail
     C                     MOVE 'Y'       ULWRIT           Record written?
     C                     EXSR #LRSET                     Reset fields
     C                     ENDSL
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * #LRSET - Reset print fields                                   *
      *                                                               *
      * Called by: SR/#LREPT - Report processing                      *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           #LRSET    BEGSR
      *
      ** Reset all fields
     C                     MOVE *BLANKS   L#CCY1
     C                     MOVE *BLANKS   L#PCT1
     C                     MOVE *BLANKS   L#CCY2
     C                     MOVE *BLANKS   L#PCT2
     C                     MOVE *BLANKS   L#CCY3
     C                     MOVE *BLANKS   L#PCT3
     C                     MOVE *BLANKS   L#CCY4
     C                     MOVE *BLANKS   L#PCT4
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LTERM - Termination Processing                               *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:                                                        *
      *****************************************************************
      *
     C           #LTERM    BEGSR                           ** SR/#LTERM**
      *
      ** If no records written, report this on print file
     C           ULWRIT    IFNE 'Y'
     C                     WRITEBY3060D2                   No details
     C                     ENDIF
      *
     C                     WRITEBY3060F1                   Footer
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * #LDEFN - Field Definition Routine                             *
      *                                                               *
      * Called by:  None - Contains definitions only                  *
      *                                                               *
      * Calls: None                                                   *
      *****************************************************************
      *
     C           #LDEFN    BEGSR                           ** SR/#LDEFN**
      *
      ** Define external data areas
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
      *
      ** Define and initialise work fields
     C                     Z-ADD0         ULSFNO  60       Spool file no.
     C                     MOVE *BLANKS   ULRTCD  1        Return code
     C                     MOVE *BLANKS   ULERRI  1        Pgm error indicator
     C                     MOVE *BLANKS   P@RTCD  7        Return code
     C                     MOVE *BLANKS   P@OPTN  7
     C                     MOVE *BLANKS   ULTIME  60       System time
     C                     MOVE *BLANKS   ULSF1   60
     C                     MOVE *BLANKS   ULPARM  3
     C                     MOVE *BLANKS   ULZERR  1        Error flag
     C                     MOVE *BLANKS   ULWRIT  1        Flag: record written?
     C                     MOVE *BLANKS   ULENTY  3
     C                     MOVEACPY@      ULCPY@ 80        Copyright
     C                     Z-ADD1         X       20
     C                     Z-ADD1         COUNT   10
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
      *  Check if error has previously been reported
     C           ULERRI    IFEQ *BLANK
     C                     MOVE 'Y'       ULERRI
     C                     MOVE 'Y'       ULRTCD
     C                     DUMP
     C                     ENDIF
      *
      *  Set on external indicators
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      * *INZSR - Program Initialisation Routine                       *
      *                                                               *
      * Called by:  At program initialisation                         *
      *                                                               *
      * Calls: None                                                   *
      *****************************************************************
      *
     C           *INZSR    BEGSR                           ** *INZSR SR**
      *
     C           *ENTRY    PLIST
     C                     PARM           ULMODE  7        Run mode
     C                     PARM           ULRSEQ  5        Report sequence
     C                     PARM           ULRTCD           Return code
      *
      *  Get Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*FIRST ' P@OPTN
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Check Return Code
     C           P@RTCD    IFNE *BLANKS
     C           BJTYLC    OREQ 'D'                        ON-DELETED
     C           *LOCK     IN   LDA
     C                     MOVELULPGM     DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 01 *
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     Z-ADD1         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Ensure Report Spool File is recorded by RCF
     C                     Z-ADDULSNUM    ULSFNO
     C                     CALL 'ZSFILE'
     C                     PARM           ULRSEQ
     C                     PARM *BLANK    ULENTY
     C                     PARM           ULSPFL
     C                     PARM           ULSFNO
     C                     PARM           ULZERR
      *
      ** If error occured in SFILE, call error handling routine
     C           ULZERR    IFNE *BLANKS
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Switch on 'ACTION' field in display file
     C           ULMODE    IFEQ 'AUDIT  '
     C           ULMODE    OREQ 'INTERIM'
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN41
     C                     ENDIF
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ELSE
     C                     MOVE *OFF      *IN98
     C                     END
      *
      *  Write report headings
     C                     MOVELULMODE    L#MODE
     C                     WRITEBY3060H1
      *
      *
     C                     ENDSR
      /EJECT
      ********************************************************************
**  CPY@
(c) Finastra International Limited 1997
