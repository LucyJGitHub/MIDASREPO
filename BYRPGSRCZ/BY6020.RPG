     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BoE JE & Amt. Accruals PL Extract/Valid.')       *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6020 - Jour. Entry and Amt. Accruals PL Extract/Valid.     *
      *                                                               *
      *  Called By: BYC6020 - Jour. Entry and Amt. Accr. PL Ext.Val.  *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD027             Date 21Jan09               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 LLN022             Date 24Jan05               *
      *                 LLN021/044         Date 05May04               *
      *                 LLN021/040         Date 30Apr04               *
      *                 LLN021/037         Date 23Mar04               *
      *                 LLN021/011         Date 10Feb04               *
      *                 LLN021             Date 09Jan04               *
      *                 LLN021             Date 17Nov03               *
      *                 LLN021             Date 28Oct03               *
      *                 LLN016             Date 28Oct03               *
      *                 LLN012             Date 28Oct03               *
      *                 179320             Date 22May00               *
      *                 171161             Date 13Jan00               *
      *                 167631             Date 07Oct99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment History                                            *
      *  -----------------                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  LLN022     - BOE Upgrade to MidasPlus                        *
      *  LLN021/044 - Account section on APOSTPD not alway            *
      *  LLN021/040 - Associted Customer Number overwritten           *
      *  LLN021/037 - Output Fee and Provision Code                   *
      *  LLN021/011 - Re-instate removed code                         *
      *  LLN021 - Remove GLJNPXL1                                     *
      *  LLN021 - Currency not always output                          *
      *  LLN021 - Convert to local curreny                            *
      *  LLN021 - BoE Profit and Loss return                          *
      *  LLN016 - Upgrade BoE to DBAR3.                               *
      *  LLN012 - Additional BoE Changes (Patch B)                    *
      *  179320 - Details are not being extracted in a single branch  *
      *           environment because the branch code work field      *
      *           ULBRCA is not being set up.                         *
      *  171161 - Duplicate Entries reported when supplementary       *
      *           product code is extracted.                          *
      *  167631 - Set up overflow indicator when writing details to   *
      *           BY6020D1.                                           *
      *                                                               *
      *****************************************************************
      *
     FAPOSTV5 IF  E           K        DISK         KINFSR *PSSR
     F                                              KINFDS INFDS1
      *  Postings by - Assoc. Cust. and Acc. Code
      *
     FSDCURRL0IF  E           K        DISK         KINFSR *PSSR
      *  Currency details
      *
     FSDCUHSL1IF  E           K        DISK         KINFSR *PSSR
      *  Historic SPOT Currency rates
      *
     F*GLJNPXL1IF  E           K        DISK         KINFSR *PSSR          LLN021
     F*           GLJENPX10                         KRENAMEGLJNP1          LLN021
      *  Bank of England Inc/Exp P&L Reporting
      *
     FAMACC   IF  E           K        DISK         KINFSR *PSSR
     F            AMACCAF                           KIGNORE
     F            AMACCZF                           KIGNORE
      *  Bank of England Inc/Exp P&L Reporting
      *
     FSDBRCHL1IF  E           K        DISK         KINFSR *PSSR
      *  Branches
      *
     FBYBRCHY0IF  E           K        DISK         KINFSR *PSSR                            LLN016
      *  Branches to be extracted for BoE Reporting                                         LLN016
      *
     FBYACEXPPIF  E           K        DISK         KINFSR *PSSR
      *  Account Extract Details File
      *
     FBYRPDTPPIF  E                    DISK         KINFSR *PSSR      UC
      *  Extract Date File
      *
     F*YEXDTV5IF  E           K        DISK         KINFSR *PSSR      UC  LLN021
      ***Bank of England Extract File                                     LLN021
      *                                                                   LLN021
     FBYEXIEPPO   E                    DISK         KINFSR *PSSR
      *  Bank of England Extract PL File - (Uses member BYEXJCM)
      *
     FBY6020P1O   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOL1
      *  Bank of England Validation Report - Valid
      *
     FBY6020P6O   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOL6
      *  Bank of England Validation Report - Invalid
      *
      /EJECT
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E                    @CPY    1   1 80
     E                    @CCY      200  3               Currencies
     E                    @CYS      200  3               Currencies - Swift
     E                    @CYD      200 13 8             Currencies - SPOT rate historic
     E                    @CYR      200  1               Currencies - SPOT rate mult/div
     E                    @TAC      200 15 0             Total assets - ccy
     E                    @TAS      200 15 0             Total assets - sterling
     E                    @TLC      200 15 0             Total liabilities - ccy
     E                    @TLS      200 15 0             Total liabilities - STG
      *
      /SPACE 3
     C/EJECT
      ** Local data area for database error details
     ILDA       E DSLDA                         256
      *
      ** Data Structure using Bank file format
     ISDBANK    E DSSDBANKPD
      *
      ** Data Structure using Currency Details
     ISDCURR    E DSSDCURRPD                200
      *
      ** Data Structure for Customer Details
     ISDCUST    E DSSDCUSTPD
      *
      ** Data Structure for Bank of England Customer Details
     ISDCUS7    E DSBYCUSTX0                                                                 LLN016
      *
      ** Data Structure for Account Codes File                                 LLN021
     ISDACOD    E DSSDACODPD                                                   LLN021
      ** Data Structure for extra customer details returned by BY9110
      *       ULLISO  - ISO Code of customer's country of location
      *       ULRISO  - ISO Code of customer's country of citizenship
      *       ULRWCD  - Weighting amount
     ISDCUSX      DS
     I                                        1   2 ULLISO
     I                                        3   4 ULRISO
     I                                        5   80ULRWCD
      *
      ** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM ULPGMN
     I                                      244 253 ULWSID
     I                                      254 263 ULUSER
      *
      * File information data structure (for database files only)
     IINFDS1    E DSY2I1DSP
      *
      * First DS For Access Programs, Short Data Structure
     IDSFDY     E DSDSFDY
      *
      * DS For Access Programs, Long Data Structure
     IDSSDY     E DSDSSDY
      *
      * Data structure for retrieval of data from DTAARA/RUNDAT
     IRUNDAT    E DSRUNDAT
      *
      *  File Information Data Structure for BY6020P1
     ISPOOL1      DS
     I                                    B 123 1240ULNUM1
     I                                    B 188 1890ULOVF1
     I                                    B 367 3680ULCLN1
      *
      *  File Information Data Structure for BY2010P6
     ISPOOL6      DS
     I                                    B 123 1240ULNUM6
     I                                    B 188 1890ULOVF6
     I                                    B 367 3680ULCLN6
      *
      *  Input parameters data structure
      *
     II#DTA       DS                            256
      *                                       From Currency & amount
     I                                        1   3 I#FCCY
     I                                    P   4  110I#FAMT
      *                                       To Currency & amount
     I                                       12  14 I#TCCY
     I                                    P  15  220I#TAMT
      *                                       Rate and multi/div ind.
     I                                       23  23 I#MDIN
     I                                    P  24  368I#RATE
      *                            Output     Rate and multi/div ind.
      *                                       If no in rate
     I                                       37  37 O#MDIN
     I                                    P  38  508O#RATE
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Main Processing                                               *
      *                                                               *
      * Calls: SR/#LMAIN - Main Processing                            *
      *        SR/#LTERM - Termination Processing                     *
      *                                                               *
      *****************************************************************
      *  Main Processing
     C                     EXSR #LMAIN
      *
      *  Termination Processing
     C                     EXSR #LTERM
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LMAIN - Main Processing                                   *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:      SR/#LBRCH - Branch Processing                     *
      *                                                               *
      *****************************************************************
     C           #LMAIN    BEGSR                           ** SR/#LDETL**
      *
     C           *LOVAL    SETLLSDBRCHL1
     C                     READ SDBRCHL1                 88
     C           *IN88     DOWEQ*OFF
      *
      *  Branch processing
     C                     EXSR #LBRCH
      *
      *  Process Deals if branch to be included
     C           ULIBOE    IFEQ 'Y'
     C********** PKEY      SETLLAPOSTV5                                                       LLN021
     C********** PKEY      READEAPOSTV5                  89                                   LLN021
     C           ULBRCA    SETLLAPOSTV5                                                       LLN021
     C           ULBRCA    READEAPOSTV5                  89                                   LLN021
     C           *IN89     DOWEQ*OFF
      *
     C           PSTD      IFGE W0DAYN                                       LLN021
      ** Account section on APOSTPD is not always correct so get from     LLN021
      *  SDACODPD                                                         LLN021
      *                                                                   LLN021
     C**********           MOVELACOD      @@ACOD  4                                   LLN021  LLN022
     C                     MOVELACOD      @@ACOD 10                       LLN021              LLN022
     C                     CALL 'AOACODR0'                                LLN021
     C                     PARM *BLANKS   P@RTCD                          LLN021
     C                     PARM '*KEY   ' P@OPTN                          LLN021
     C**********           PARM @@ACOD    P@ACOD  4                                   LLN021  LLN022
     C                     PARM @@ACOD    P@ACOD 10                       LLN021              LLN022
     C           SDACOD    PARM SDACOD    DSSDY                           LLN021
      *                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVE ACOD      DBKEY            ************   LLN021
     C                     MOVEL'SDCAODPD'DBFILE           * DBERR 06 *   LLN021
     C                     Z-ADD6         DBASE                           LLN021
     C                     EXSR #LDBER                                    LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C*********  ACSC      IFEQ 'IN'                                      LLN021
     C********** ACSC      OREQ 'EX'                                      LLN021
     C           A5ACSC    IFEQ 'IN'                                      LLN021
     C           A5ACSC    OREQ 'EX'                                      LLN021
      *                                                                      LLN021
      *  Use the relevant customer number
     C********** ASOC      IFEQ 0                                                             CSD027
     C           ASOC      IFEQ *BLANKS                                                       CSD027
     C**********           Z-ADDCNUM      ULCNUM                                              CSD027
     C                     MOVE CNUM      ULCNUM                                              CSD027
     C                     ELSE
     C**********           Z-ADDASOC      ULCNUM                                              CSD027
     C                     MOVE ASOC      ULCNUM                                              CSD027
     C                     ENDIF
      *
      * Analytical Account - customer no. can be different
     C           SPOS      IFEQ '  GE-AA'
      *
     C                     MOVELOTRF      FAREF
      *
     C           PKEY4     KLIST
     C                     KFLD           FAREF
      *
     C           PKEY4     CHAINAMACC                90
     C           *IN90     IFEQ *OFF
     C**********           Z-ADDDRCN      ULCNUM                                              CSD027
     C                     MOVE DRCN      ULCNUM                                              CSD027
     C                     ENDIF
      *
     C                     ENDIF
      *
      **Check*if*present on Bank of England Inc/Exp P&L Reporting         LLN021
     C*****************    EXSR #CKBTC                                    LLN021
     C***********W0INCL    IFEQ 'Y'                                       LLN021
      *                                                                   LLN021
      * Process according to mode                                         LLN021
     C           ULMODE    IFEQ 'INTERIM'                                 LLN021
     C           ULMODE    OREQ 'EXTRACT'                                 LLN021
     C           ULMODE    OREQ 'LIST   '                                 LLN021
      *                                                                   LLN021
      * Load SPOT details, if needed                                      LLN021
     C           PSTD      IFNE ULPSTD                                    LLN021
     C                     EXSR #LDCYS                                    LLN021
     C                     MOVE PSTD      ULPSTD  50                      LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
      *  Clear File Format                                                LLN021
     C                     CLEARBYEXIED0                                  LLN021
      *                                                                   LLN021
      * Set Up Extract Information                                        LLN021
     C                     EXSR #GETDL                                    LLN021
      *                                                                   LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C********             ENDIF                                          LLN021
      *
     C                     ENDIF                                                              LLN021
      *                                                                                       LLN021
     C                     ENDIF                                                              LLN021
      *                                                                                       LLN021
     C********** PKEY      READEAPOSTV5                  89                                   LLN021
     C           ULBRCA    READEAPOSTV5                  89                                   LLN021
      *
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     READ SDBRCHL1                 88
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LBRCH - Processing For New Branch                         *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:      SR/#LRPTT - Final Report Processing               *
      *                                                               *
      *****************************************************************
     C           #LBRCH    BEGSR                           ** SR/#LBRCH**
      *
      *  Reset flag
     C                     MOVE 'N'       ULIBOE           Include branch
      *
      *  If single branch all records must be extracted
     C           AGMBIN    IFEQ 'N'
     C                     MOVE 'Y'       ULIBOE
     C                     MOVE A8BRCD    ULBRCA  3                       179320
      *
      *  Multi-branch processing
     C                     ELSE
      *
      *  If files open report currency summaries and close files
     C           ULOPNF    IFEQ 'Y'
     C                     EXSR #LRPTT
     C                     ENDIF
     C                     MOVE A8BRCD    ULBRCA                          LLN012
      *
      *   Check if new branch is to be included in report extract
     C           ULBRCA    CHAINBYBRCHY0             90                                       LLN016
     C           *IN90     IFEQ *OFF
     C                     MOVELA8IBOE    ULIBOE
     C                     ENDIF
      *
      *   Reset open flag and page numbers
     C                     MOVE 'N'       ULOPNF  1
     C                     Z-ADD0         PAGE1
     C                     Z-ADD0         PAGE6
      *
      *   Include branch in Bank of England Extract
     C           ULIBOE    IFEQ 'Y'
      *
      *  Open printer files for new branch
     C                     MOVE 'Y'       ULOPNF  1
     C                     OPEN BY6020P1
     C                     OPEN BY6020P6
      *
      *  RCF processing for printer file BY6020P1
     C                     Z-ADDULNUM1    ULSFNO
     C*****                CALL 'ZSFILE'                                                      LLN021
     C*****                PARM           ULRSEQ  5                                           LLN021
     C*****                PARM           ULBRCA  3                                           LLN021
     C*****                PARM 'BY6020P1'ULFNAM 10                                           LLN021
     C*****                PARM           ULSFNO  60                                          LLN021
     C*****                PARM           ULRTCD  7                                           LLN021
     C*******    ULRTCD    IFNE *BLANK                                                        LLN021
     C********             MOVEL'*NONE'   DBKEY            ************                       LLN021
     C*********            MOVEL'ZSFILE  'DBFILE           * DBERR 01 *                       LLN021
     C*******              Z-ADD1         DBASE            ************                       LLN021
     C*******              EXSR #LDBER                                                        LLN021
     C*********            ENDIF                                                              LLN021
      *  RCF processing for printer file BY6020P6
     C*********            Z-ADDULNUM6    ULSFNO                                              LLN021
     C*****                CALL 'ZSFILE'                                                      LLN021
     C*****                PARM           ULRSEQ                                              LLN021
     C*****                PARM           ULBRCA                                              LLN021
     C*****                PARM 'BY6020P6'ULFNAM                                              LLN021
     C*****                PARM           ULSFNO                                              LLN021
     C*****                PARM           ULRTCD                                              LLN021
     C******     ULRTCD    IFNE *BLANK                                                        LLN021
     C*******              MOVEL'*NONE'   DBKEY            ************                       LLN021
     C*******              MOVEL'ZSFILE  'DBFILE           * DBERR 02 *                       LLN021
     C*******              Z-ADD2         DBASE            ************                       LLN021
     C********             EXSR #LDBER                                                        LLN021
     C*********            ENDIF                                                              LLN021
      *
      *  Write report headings to BY6020P1 and BY6020P6
     C                     MOVE L0RPDA    L#RPDA
     C                     MOVE A8BRCD    L#BRCD
     C                     MOVE A8BRNM    L#BRNM
     C                     WRITEBY6020H1
     C                     WRITEBY6020H6
      *
     C                     ENDIF
     C                     ENDIF
      *
      *  Reset currency summary totals
     C                     Z-ADD0         @TAC
     C                     Z-ADD0         @TLC
     C                     Z-ADD0         @TAS
     C                     Z-ADD0         @TLS
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #GETDL - Get Extract Details                                  *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:                                                        *
      *         SR/#LNAFM - Format NA Deal Details                    *
      *         SR/#LRPXT - Report/Extract Details                    *
      *                                                               *
      *****************************************************************
     C           #GETDL    BEGSR                           ** SR/#GETDL**
      *
      *  Format work fields for account
     C                     EXSR #FORMT
      *
      *  Report/Extract details
     C                     EXSR #LRPXT
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #FORMT - Format Work Fields For Deals                         *
      *                                                               *
      * Called by:  SR/#GETDL - Get Details                           *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #FORMT    BEGSR                           ** SR/#FORMT**
      *
      * Error indicators
     C                     MOVEA'0'       *IN,38
     C                     MOVEA'0'       *IN,41
      *
     C           ULCNUM    IFNE ULPCUS                     Previous customer
      *
     C                     MOVE ULCNUM    ULCUST
     C                     CLEARSDCUST
     C                     CLEARSDCUS7
     C                     CLEARSDCUSX
     C                     CALL 'BY9110'
     C                     PARM           ULCUST           Customer number
     C                     PARM           SDCUST           SDCUSTPD details
     C                     PARM           SDCUS7           BYCUSTX0 details
     C                     PARM           SDCUSX           Extra details
     C                     PARM 0         ULERR1
     C                     PARM *BLANK    ULRTCD           Return code
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 02 *
     C                     MOVEL'BY9110  'DBFILE           ************
     C                     Z-ADD02        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  Set customer valid flag
     C           ULERR1    IFNE 0
     C                     MOVE 'N'       UVALD            Invalid customer
     C                     ELSE
     C                     MOVE 'Y'       UVALD            Valid customer
     C                     ENDIF
      *
      * Save customer
     C**********           Z-ADDULCNUM    ULPCUS           Previous customer                  CSD027
     C                     MOVE ULCNUM    ULPCUS           Previous customer                  CSD027
      * Customer Number
     C**********           MOVELULCNUM    L8CUST                                              CSD027
     C                     MOVE ULCNUM    L8CUST                                              CSD027
      *
     C                     ENDIF
      ******
      **Retail*account number for transaction reference                   LLN021
      ********                                                            LLN021
     C******************** MOVELACNO      L8TRAN                          LLN021
      ********                                                            LLN021
      **Access BYEXDTPP                                                   LLN021
      **Most output fields are named the same as on this file so          LLN021
      **no need to set up for output once obtained from here.             LLN021
     C**********           MOVELULCNUM    KCNUM                           LLN021
     C**********           MOVELCCY       KCCY                            LLN021
     C***********          MOVELBRCA      KBRCA                           LLN021
     C************         MOVE ACOD      W0ACOD  4                       LLN021
     C************         MOVE ACSQ      W0ACSQ  2                       LLN021
     C********** W0ACOD    CAT  W0ACSQ    KTRAN                           LLN021
      **********                                                          LLN021
     C************         MOVEL'GL'      KMMOD                           LLN021
     C***********          MOVEL'GL'      L8MMOD                          LLN021
      **********                                                          LLN021
     C********   KEXDT     KLIST                                          LLN021
     C**********           KFLD           KMMOD   2                       LLN021
     C***********                                                         LLN021
     C*******              KFLD           KCNUM   60                      LLN021
     C*******              KFLD           KCCY    3                       LLN021
     C*******              KFLD           KTRAN  10                       LLN021
      *******                                                             LLN021
     C*******    KEXDT     CHAINBYEXDTV5             76                   LLN021
      ********                                                            LLN021
      **Invalid if no record found on BYEXDTV5                            LLN021
     C********** *IN76     IFEQ *ON                                       LLN021
     C**********           MOVE *ON       *IN41                           LLN021
     C***********          CLEARBYEXDTD0                                  LLN021
     C************         GOTO CONT                                      LLN021
     C*************        ENDIF                                          LLN021
      *                                                                   LLN021
      * Set up fields                                                     LLN021
     C******************** Z-ADDCNUM      L8CUST                          LLN021
     C**********           Z-ADDULCNUM    L8CUST                                       LLN021 CSD027
     C                     MOVE ULCNUM    L8CUST                                              CSD027
     C                     MOVELBRCA      L8BRCD                          LLN021
     C                     MOVEL'GL'      L8MMOD                          LLN021
      *                                                                   LLN021
      * Move account code an sequence number into L8TRAN                  LLN021
     C**********           MOVELACOD      ACODA   4                                    LLN021 LLN022
     C                     MOVELACOD      ACODA  10                                           LLN022
     C                     MOVELACSQ      ACSQA   2                       LLN021
     C           ACODA     CAT  ACSQA     L8TRAN           Transaction    LLN021
      *
      * Set up date fields
     C                     Z-ADDPSTD      L8DDAT
     C                     Z-ADDPSTD      L8VDAT
     C                     Z-ADDPSTD      L8MDAT
      *
      * Product Code
      * First see if record exists for Account code,
      * Institution Code and Industry Code. If not try
      * with Account code and Inst. code and then if still
      * no record, try only with Account Code.
      *
     C                     Z-ADD0         L8PCOD
      *
     C                     MOVELACOD      KACOD
     C***************      MOVELL8LINS    KLINS                           LLN021
     C***************      MOVELL8LIND    KLIND                           LLN021
     C                     MOVELBBLINC    KLINS                           LLN021
     C                     MOVELBBLICD    KLIND                           LLN021
      *
     C           KPCOD     KLIST
     C**********           KFLD           KACOD   40                                          LLN022
     C                     KFLD           KACOD  100                                          LLN022
     C                     KFLD           KLINS   20
     C                     KFLD           KLIND   3
      *
     C           KPCOD     CHAINBYACEXPP             78
     C           *IN78     IFEQ *ON
     C                     MOVE *BLANKS   KLIND
     C           KPCOD     CHAINBYACEXPP             78
     C                     ENDIF
     C           *IN78     IFEQ *ON
     C                     MOVE *BLANKS   KLINS
     C           KPCOD     CHAINBYACEXPP             78
     C                     ENDIF
      *
      * Invalid if no record found.
     C           *IN78     IFEQ *ON
     C                     MOVE *ON       *IN38
     C                     GOTO CONT
     C                     ENDIF
      *
      *  Add Fee and Provision Type                                        LLN021
      *                                                                    LLN021
     C                     MOVELL1PLFT    L8FEET                           LLN021
     C                     MOVELL1PLTP    L8PROV                           LLN021
      *
      * Convert amount in Base Currency equivalent
      *
     C                     MOVE CCY       I#FCCY
     C                     Z-ADDPSTA      I#FAMT
      *
     C                     Z-ADD1         #
     C           CCY       LOKUP@CCY,#                   90
     C                     MOVE @CYD,#    I#RATE
     C                     MOVE @CYR,#    I#MDIN
      *
     C                     MOVELBJCYCD    I#TCCY
     C                     Z-ADD0         I#TAMT
      *
     C                     CALL 'AOCCYCV0'
     C                     PARM *BLANKS   ULRTCD
     C                     PARM '*F*T*RAT'W0ACT   8
     C           I#DTA     PARM I#DTA     P0DTA 256
      *
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'I#DTA'   DBKEY            * DBERR 13 *
     C                     MOVEL'AOCCYCV0'DBFILE           ************
     C                     Z-ADD13        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **Convert*Base*currency equivalent into GBP equivalent                 LLN021
      * Convert Base currency equivalent into local currency                 LLN021
      *
     C                     MOVE BJCYCD    I#FCCY
     C                     Z-ADDI#TAMT    I#FAMT
      *
     C                     Z-ADD1         #
     C***********'GBP'     LOKUP@CCY,#                   90                  LLN021
     C           BJLCCY    LOKUP@CCY,#                   90                  LLN021
     C                     MOVE @CYD,#    I#RATE
     C                     MOVE @CYR,#    I#MDIN
      *
     C           I#MDIN    IFEQ 'D'
     C                     MOVE 'M'       I#MDIN
     C                     ELSE
     C                     MOVE 'D'       I#MDIN
     C                     ENDIF
      *
     C*******************  MOVEL'GBP'     I#TCCY                             LLN021
     C                     MOVELBJLCCY    I#TCCY                             LLN021
     C                     Z-ADD0         I#TAMT
      *
     C                     CALL 'AOCCYCV0'
     C                     PARM *BLANKS   ULRTCD
     C                     PARM '*F*T*RAT'W0ACT   8
     C           I#DTA     PARM I#DTA     P0DTA 256
      *
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'I#DTA'   DBKEY            * DBERR 13 *
     C                     MOVEL'AOCCYCV0'DBFILE           ************
     C                     Z-ADD13        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDI#TAMT    BCEQ   150
      *
      * Move fields from APOSTPD into output fields
      * Scaled ccy amount / Sterling Equiv
     C           DRCR      IFEQ 0
     C                     Z-ADDPSTA      L8CUAS
     C                     Z-ADDBCEQ      L8SGAM
     C                     ELSE
     C                     Z-SUBPSTA      L8CUAS
     C                     Z-SUBBCEQ      L8SGAM
     C                     ENDIF
      *
      * Product code follows Dr/Cr
      *
     C           L8CUAS    IFGE 0
     C                     Z-ADDL1PLDR    L8PCOD
     C                     ELSE
     C                     Z-ADDL1PLCR    L8PCOD
     C                     ENDIF
      *
      * Invalid if product code is 0
     C           L8PCOD    IFEQ 0
     C                     MOVE *ON       *IN38
     C                     GOTO CONT
     C                     ENDIF
      *
     C           CONT      TAG
      *
     C                     Z-ADD1         #
     C           CCY       LOKUP@CCY,#                   90
     C                     MOVE @CYS,#    L8CCYS
      *
     C                     MOVELBBCSSN    L8CSSN
     C                     MOVELBBPCNB    L8PCNB
      *
     C                     MOVELBBLICD    L8LIND
     C                     MOVELBBLINC    L8LINS
      *
     C                     MOVELULLISO    L8ISOC
     C                     MOVELULRISO    L8RISO
      *
     C                     MOVE *BLANKS   L8MCAT
     C                     MOVE *BLANKS   L8NNSI
     C                     MOVE *BLANKS   L8STGI
     C                     MOVE *BLANKS   L8SIO
     C                     MOVE *BLANKS   L8INTB
     C                     MOVE *BLANKS   L8ODIN
     C                     MOVE *BLANKS   L8MBSE
     C                     MOVE *BLANKS   L8CPNR
     C                     MOVE *BLANKS   L8MKTS
     C                     MOVE *BLANKS   L8SMKV
     C                     MOVE *BLANKS   L8MKDC
     C                     MOVE *BLANKS   L8SMXD
     C                     MOVE *BLANKS   L8MXDD
     C                     MOVE *BLANKS   L8MKCC
     C                     MOVE *BLANKS   L8SMXC
     C                     MOVE *BLANKS   L8MXCD
     C                     MOVE *BLANKS   L8OPHW
     C                     MOVE *BLANKS   L8OPCS
     C                     MOVE *BLANKS   L8SOPC
     C                     MOVE *BLANKS   L8OMPS
     C                     MOVE *BLANKS   L8SOMP
     C                     MOVE *BLANKS   L8OTCX
     C                     MOVE *BLANKS   L8ONUD
     C                     MOVE *BLANKS   L8ONUP
     C                     MOVE *BLANKS   L8CTYP
     C                     MOVE *BLANKS   L8DTYP
     C                     MOVE *BLANKS   L8DRVT
     C                     MOVE *BLANKS   L8ELIG
     C                     MOVE *BLANKS   L8NETI
     C                     MOVE *BLANKS   L8RWCD
     C*******************  MOVE *BLANKS   L8HAIN                            LLN021
     C                     MOVELLHASI     L8HAIN                            LLN021
     C                     MOVE *BLANKS   L8GTEE
     C                     MOVE *BLANKS   L8QUAL
     C                     MOVE *BLANKS   L8CNTP
     C                     MOVE *BLANKS   L8BTEX
     C                     MOVE *BLANKS   L8LREX
     C                     MOVE *BLANKS   L8ETYP
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRPXT - Report/Extract Processing                         *
      *                                                               *
      * Called by:                                                    *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * Calls:      SR/#LRPT1 - Report Valid Details to BY6020P1      *
      *             SR/#LRPT6 - Report Valid Details to BY6020P6      *
      *                                                               *
      *****************************************************************
     C           #LRPXT    BEGSR                           ** SR/#LRPXT**
      *
      *  Format report fields
     C                     EXSR #LRPTD
      *
      *  No report for product code = 9998
     C           ULPCOD    IFNE 9998                                       LLN021
      *
      *  Details are valid - report details to BY6020P1
     C           UVALD     IFEQ 'Y'
     C           L8PCOD    ANDNE9999                                       LLN021
     C           *IN38     ANDNE*ON
     C           *IN41     ANDNE*ON
      *
      *    Report details to BY6020P1
     C                     EXSR #LRPT1
      *
      *    Create extract record if extract mode
     C           ULMODE    IFEQ 'EXTRACT'
     C                     EXSR #LXTRC
     C                     ENDIF
      *
      *  Details are invalid - report details to BY6020P6
     C                     ELSE
     C                     EXSR #LRPT6
     C                     ENDIF
      *
     C                     ENDIF                                            LLN021
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LXTRC - Create Extract Record                                *
      *                                                               *
      * Called by: Sr/#LRPXT - Report/Extract Details                 *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LXTRC    BEGSR                           ** SR/#LXTRC**
      *
     C                     MOVE 'B'       L8RTYP           Record type       LLN021
     C                     MOVE CCY       L8CYCD                             LLN021
      *                                                                      LLN021
     C                     WRITEBYEXIED0
      *
      *  Accumulate total assets and liabilities
      *
     C                     Z-ADD1         #
     C           L8CYCD    LOKUP@CCY,#                   90
     C           L8PCOD    IFGE 1100
     C           L8PCOD    ANDLE1960
     C                     ADD  L8CUAS    @TAC,#
     C                     ADD  L8SGAM    @TAS,#
     C                     ENDIF
     C           L8PCOD    IFGE 2100
     C           L8PCOD    ANDLE3990
     C                     ADD  L8CUAS    @TLC,#
     C                     ADD  L8SGAM    @TLS,#
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRPTT - Final Reporting                                   *
      *                                                               *
      * Called by:  SR/#LBRCH - Change of Branch Processing           *
      *             SR/#LTERM - Termination Processing                *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           #LRPTT    BEGSR                           ** SR/#LRPTT**
      *
      *  No details to report
     C           ULRPT1    IFNE 'Y'
     C                     WRITEBY6020E1               90
     C                     ENDIF
     C           ULRPT6    IFNE 'Y'
     C                     WRITEBY6020E6               90
     C                     ENDIF
      *
      *   Report currency summary if EXTRACT mode on BY6020P1
     C           ULMODE    IFEQ 'EXTRACT'
     C           ULRPT1    ANDEQ'Y'
      *
      *    Write heading
     C                     WRITEBY6020R1               90
      *
      *    Write detail record for each currency
     C                     Z-ADD1         #
     C                     Z-ADD0         ULTOTA
     C                     Z-ADD0         ULTOTL
     C           @CCY,#    DOWNE*BLANK
     C           #         ANDLT200
     C           #         OCUR SDCURR
      *
     C           @TAC,#    IFNE 0
     C           @TLC,#    ORNE 0
     C                     MOVE A6CYCD    L#CYCD           Currency
     C                     MOVE A6CYNM    L#CYNM           Currency name
     C                     CALL 'ZSEDIT'
     C                     PARM @TAC,#    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - ccy
     C                     MOVE ULAMTA    L#TLAC
     C                     CALL 'ZSEDIT'
     C                     PARM @TAS,#    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - STG
     C                     MOVE ULAMTA    L#TLAS
     C                     CALL 'ZSEDIT'
     C                     PARM @TLC,#    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - ccy
     C                     MOVE ULAMTA    L#TLLC
     C                     CALL 'ZSEDIT'
     C                     PARM @TLS,#    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - STG
     C                     MOVE ULAMTA    L#TLLS
      *
      *    Report details
     C                     WRITEBY6020S1               90
     C           ULCLN1    IFGT ULOVF1                     Overflow
     C                     WRITEBY6020R1
     C                     ENDIF
      *
      *    Accumulate total assets and liabilities in sterling
     C                     ADD  @TAS,#    ULTOTA
     C                     ADD  @TLS,#    ULTOTL
     C                     ENDIF
      *
     C                     ADD  1         #
     C                     ENDDO
      *
      *
      *   Report overall asset and libilities totals
     C                     CALL 'ZSEDIT'
     C                     PARM ULTOTA    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - STG
     C                     MOVE ULAMTA    L#TOTA
     C                     CALL 'ZSEDIT'
     C                     PARM ULTOTL    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - STG
     C                     MOVE ULAMTA    L#TOTL
     C           ULCLN1    IFGT ULOVF1                     Overflow
     C                     WRITEBY6020R1
     C                     ENDIF
     C                     WRITEBY6020T1               90
      *
     C                     ENDIF
      *
      *
      *
      *  Write end of report
     C                     WRITEBY6020Z1               90
     C                     WRITEBY6020Z6               90
      *
      *  Close reports
     C                     CLOSEBY6020P1
     C                     CLOSEBY6020P6
      *
      *  Reset report indicators and page numbers
     C                     MOVE 'N'       ULOPNF
     C                     MOVE 'N'       ULRPT1
     C                     MOVE 'N'       ULRPT6
     C                     Z-ADD0         PAGE1
     C                     Z-ADD0         PAGE6
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LDBER - Database Error Handling                              *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           #LDBER    BEGSR                           ** #LDBER **
      *
      *  Update LDA with details of error
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM
     C                     OUT  LDA
      *
      *  Report data base error details
     C                     WRITEBY6020F6
      *
      *  Terminate program
     C                     EXSR *PSSR
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
      *  Check if error has previously been reported
     C           ULERRI    IFNE 'Y'
     C                     MOVE 'Y'       ULERRI  1
     C                     MOVE 'Y'       ULRTCD  7
     C                     DUMP
     C                     ENDIF
      *
      *  Set on external indicators
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LTERM - Termination Processing                               *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LTERM    BEGSR                           ** SR/#LTERM**
      *
      *  Final reporting
     C           ULOPNF    IFEQ 'Y'
     C                     EXSR #LRPTT
     C                     ENDIF
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation Routine                       *
      *                                                               *
      * Called by:  At program initialisation                         *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR                           ** *INZSR SR**
      *
     C           *ENTRY    PLIST
     C                     PARM           ULMODE  7        Run mode
     C                     PARM           ULRSEQ  5        Report sequence
     C                     PARM           ULRTCD           Return code
      *
      *  Initialise work fields
     C                     EXSR #LDEFN
      *
      *  Set report indicator according to mode
     C           ULMODE    IFEQ 'EXTRACT'
     C                     MOVE *ON       *IN21
     C                     ELSE
     C                     MOVE *OFF      *IN21
     C                     ENDIF
      *
      *  Get Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    P@RTCD  7
     C                     PARM '*FIRST ' P@OPTN  7
     C                     PARM           SDBANK
     C           P@RTCD    IFNE *BLANKS
     C           BJTYLC    OREQ 'D'                        ON-DELETED
     C                     MOVEL'*NONE '  DBKEY            ************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERR 14 *
     C                     Z-ADD14        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
      *
      *  Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ELSE
     C                     MOVE *OFF      *IN98
     C                     END
      *
      *  Copyright statement
     C                     MOVEA@CPY      ULCPY@ 80
      *
      ** Define external data areas
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           RUNDAT
      *
      ** Define and initialise work fields
     C                     Z-ADD0         #       50
     C                     Z-ADD0         X       20
      *
      *  Get the extract date
     C                     OPEN BYRPDTPP
     C           1         CHAINBYRPDTPP             90
     C           *IN90     IFEQ *ON
     C                     MOVEL'*NONE '  DBKEY            ************
     C                     MOVEL'BYRPDTPP'DBFILE           * DBERR 19 *
     C                     Z-ADD19        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
     C                     CLOSEBYRPDTPP
      *
      * Check if multi-branching
     C                     IN   RUNDAT
     C                     UNLCKRUNDAT
      *  Single branch:
     C           AGMBIN    IFNE 'Y'
     C                     MOVE 'Y'       ULOPNF  1
     C                     MOVE *OFF      *IN20
      *
      *  RCF processing for printer file BY6020P1
     C                     OPEN BY6020P1
     C                     Z-ADDULNUM1    ULSFNO
     C*****                CALL 'ZSFILE'                                                      LLN021
     C*****                PARM           ULRSEQ                                              LLN021
     C*****                PARM           ULBRCA                                              LLN021
     C*****                PARM 'BY6020P1'ULFNAM                                              LLN021
     C*****                PARM           ULSFNO                                              LLN021
     C*****                PARM           ULRTCD                                              LLN021
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 19 *
     C                     MOVEL'ZSFILE  'DBFILE           ************
     C                     Z-ADD19        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  RCF processing for printer file BY6020P6
     C                     OPEN BY6020P6
     C                     Z-ADDULNUM6    ULSFNO
     C*****                CALL 'ZSFILE'                                                      LLN021
     C*****                PARM           ULRSEQ                                              LLN021
     C*****                PARM           ULBRCA                                              LLN021
     C*****                PARM 'BY6020P6'ULFNAM                                              LLN021
     C*****                PARM           ULSFNO                                              LLN021
     C*****                PARM           ULRTCD                                              LLN021
     C           ULRTCD    IFNE *BLANK
     C                     MOVEL'*NONE '  DBKEY            ************
     C                     MOVEL'ZSFILE  'DBFILE           * DBERR 20 *
     C                     Z-ADD20        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
      *
      *  Write report headings to BY6020P1 and BY6020P6
     C                     MOVE L0RPDA    L#RPDA
     C                     WRITEBY6020H1
     C                     WRITEBY6020H6
      *
      *  Multi branching indicator
     C                     ELSE
     C                     MOVE *ON       *IN20
     C                     ENDIF
      *
      *  Load currency details
     C                     Z-ADD0         #
     C           *IN90     DOWEQ*OFF
     C           #         ANDLT200
     C                     ADD  1         #
     C           #         OCUR SDCURR
     C                     READ SDCURRL0                 90
     C           *IN90     IFEQ *OFF
     C                     MOVE A6CYCD    @CCY,#
     C                     MOVE A6SWCY    @CYS,#
     C                     ENDIF
     C                     ENDDO
      *
      *  Get decimal places for sterling currency code
     C                     Z-ADD1         #
     C           BJCYCD    LOKUP@CCY,#                   90
     C           *IN90     IFEQ *ON
     C           #         OCUR SDCURR
     C                     Z-ADDA6NBDP    UL$NDP
     C                     ELSE
     C           200       OCUR SDCURR
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*KEY   ' P@OPTN
     C                     PARM           L8CYCD
     C           SDCURR    PARM           DSSDY
     C           P@RTCD    IFNE *BLANK                     Error on call
     C                     MOVELBJCYCD    DBKEY            ************
     C                     MOVEL'SDCURRPD'DBFILE           * DBERR 21 *
     C                     Z-ADD21        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
     C                     Z-ADDA6NBDP    UL$NDP
     C                     ENDIF
      *
      *  Open extract file if EXTRACT mode
     C           ULMODE    IFEQ 'EXTRACT'
     C************         OPEN BYEXDTV5                                                      LLN021
     C                     ENDIF
      *
      * Set up start of month date
      *
     C                     CALL 'ZDATE2'
     C                     PARM BJRDNB    ULDAYN  50
     C                     PARM           BJDFIN
     C                     PARM           ULDATE  60
     C                     PARM           ULDATA  7
      *
     C                     MOVE ULDATE    W0DATE  6
     C           BJDFIN    IFEQ 'M'
     C                     MOVELW0DATE    W0DAT4  4
     C                     MOVE '01'      W0DAT4
     C                     MOVELW0DAT4    W0DATE
     C                     ELSE
     C                     MOVEL'01'      W0DATE
     C                     ENDIF
      *
     C                     MOVE W0DATE    ZDATEA
     C                     CALL 'ZDATE1'
     C                     PARM *BLANKS   ERROR   7
     C                     PARM           ZDATEA  60
     C                     PARM           BJDFIN
     C                     PARM           ZDAYNO  50
      *
     C                     Z-ADDZDAYNO    W0DAYN  50
      *
     C           PKEY      KLIST
     C                     KFLD           W0DAYN
     C                     KFLD           ULBRCA
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LDCYS - Load Currency SPOT details (historic)             *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LDCYS    BEGSR                           ** SR/#LDCYS**
      *
     C                     Z-ADD0         #
      *
     C           PKEY2     KLIST
     C                     KFLD           PSTD
     C                     KFLD           @CCY,#
      *
     C           1         DO   200       #
      *
     C           PKEY2     CHAINSDCUHSL1             90
      *
     C           *IN90     IFEQ *OFF
     C                     Z-ADDG2SPRT    @CYD,#
     C                     MOVE G2MDIN    @CYR,#
     C                     ELSE
     C                     Z-ADD0         @CYD,#
     C                     MOVE ' '       @CYR,#
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      **SR/#CKBTC - Check batch file                                  *
      ****8                                                           *
      **Called by: Main                                               *
      ***                                                             *
      ***alls:                                                        *
      ***********                                                     *
      *****************************************************************
     C***********#CKBTC    BEGSR                           ** SR/#LDCYS** LLN021
      **********                                                          LLN021
     C**********           MOVE 'Y'       W0INCL  1                       LLN021
      **********                                                          LLN021
     C********** SPOS      IFNE '  GE-AA'                                 LLN021
      **********                                                          LLN021
     C***********          MOVELSPOS      W0BATC  6                       LLN021
     C***********          MOVELW0BATC    W0BTNB  3                       LLN021
     C***********          MOVE W0BATC    W0BINB  30                      LLN021
     C***********          MOVE BRCA      W0IBCA  3                       LLN021
     C***********          MOVE ULCNUM    W0CUST  6                       LLN021
     C***********          MOVE CCY       W0CYCD  3                       LLN021
     C***********          MOVE ACOD      W0ACCD  4                       LLN021
     C************         MOVE ACSQ      W0ACSN  2                       LLN021
     C***********          MOVE PSTD      W0PSTD  50                      LLN021
      ***********                                                         LLN021
     C***********PKEY3     KLIST                                          LLN021
     C***********          KFLD           W0BTNB                          LLN021
     C***********          KFLD           W0BINB                          LLN021
     C***********          KFLD           W0IBCA                          LLN021
     C***********          KFLD           W0CUST                          LLN021
     C***********          KFLD           W0CYCD                          LLN021
     C***********          KFLD           W0ACCD                          LLN021
     C***********          KFLD           W0ACSN                          LLN021
     C*****                KFLD           W0PSTD                          LLN021
      *****                                                               LLN021
     C********   PKEY3     CHAINGLJNPXL1             90                   LLN021
      ********                                                            LLN021
     C********   BOINPL    IFEQ 'N'                                       LLN021
     C*********  *IN90     ANDEQ*OFF                                      LLN021
     C*********            MOVE 'N'       W0INCL                          LLN021
     C*********            ENDIF                                          LLN021
      **********                                                          LLN021
     C*********            ENDIF                                          LLN021
      *********                                                           LLN021
     C**********           ENDSR                                          LLN021
      /EJECT                                                              LLN021
      *****************************************************************
      *                                                               *
      * SR/#LRPTD - Format Report Details                             *
      *                                                               *
      * Called by:  SR/#LRPXT - Report/Extract Details                *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPTD    BEGSR                           ** SR/#LRPTD**
      *
      *  Reset report indicators
     C                     MOVEA'00000000'*IN,30
      *
      *  Set up report fields
     C**********           MOVELULCNUM    L#CUST           Customer                           CSD027
     C**********           MOVELULCNUM    L#CNUM           Customer                           CSD027
     C                     MOVE ULCNUM    L#CUST           Customer                           CSD027
     C                     MOVE ULCNUM    L#CNUM           Customer                           CSD027
     C                     MOVELCCY       L#CYCD           Currency
     C                     MOVELCCY       L#CCYD           Currency
     C                     MOVELACOD      L#ACOD           a/c code
     C                     MOVELACSQ      L#ACSQ           a/c sequence
     C                     MOVE ULLISO    L#LISO           Customer country code
     C                     MOVE ULRISO    L#RISO           Risk country code
     C                     MOVE BBCOLC    L#COLC           Customer country code
     C                     MOVE BBCNCZ    L#CNCZ           Customer country code
     C                     MOVE L8LIND    L#LIND           Industry code
     C                     MOVE L8LINS    L#LINS           Institution code
     C                     MOVE BBCRNM    L#CRNM           Customer report name
     C                     CALL 'ZSEDIT'
     C                     PARM L8CUAS    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA
     C                     MOVE ULAMTA    L#CUAS           Currency amount
     C                     CALL 'ZSEDIT'
     C                     PARM L8SGAM    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Sterling equivalent
     C                     MOVE ULAMTA    L#SGAM
     C                     MOVE L8PCOD    L#PCOD
     C                     MOVE L8BTEX    L#BTEX
     C                     MOVE L8LREX    L#LREX
      *
      *  Set indicator to report extract criteria not found
     C           L8PCOD    IFEQ 9999
     C                     MOVE *ON       *IN41
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRPT1 - Write Details to BY6020P1                         *
      *                                                               *
      * Called by:  SR/#LRPXT - Report/Extract Details                *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPT1    BEGSR                           ** SR/#LRPT1**
      *                                                                   171161
      ** Reset customer report indicator                                  171161
     C                     MOVE *OFF      *IN30                           171161
      *
      *  Check if customer has changed since customer last printed
     C           ULCNUM    IFNE ULCUS1
     C                     MOVE *ON       *IN30
     C                     MOVE ULCNUM    ULCUS1
     C                     ENDIF
     C*
      *  Write details to report
     C           ULCLN1    IFGT ULOVF1
     C           *INOF     OREQ *ON                                       167631
     C                     WRITEBY6020H1
     C                     MOVE *ON       *IN30
     C                     ENDIF
     C                     WRITEBY6020D1               OF                 167631
      *
      *  Set flag to indicate details written
     C           ULRPT1    IFNE 'Y'
     C                     MOVE 'Y'       ULRPT1
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      * SR/#LRPT6 - Write Details to BY6020P6                         *
      *                                                               *
      * Called by:  SR/#LRPXT - Report/Extract Details                *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPT6    BEGSR                                       **
      *
      *  Set indicators to report details of invalid details
     C                     MOVE ULERR1    ULERR2
     C                     MOVEAULERR2    *IN,31
      *
      *  Determine no. of errors to be reported for overflow check
     C                     Z-ADD0         X
     C   31                ADD  1         X
     C   32                ADD  1         X
     C   33                ADD  1         X
     C   34                ADD  1         X
     C   35                ADD  1         X
     C   36                ADD  1         X
     C   37                ADD  1         X
      *
      *  Write details to report
     C           ULOVF6    SUB  ULCLN6    ULLDIF
     C                     SUB  X         ULLDIF
     C           ULLDIF    IFLT 5
     C           ULCNUM    ANDNEULCUS6
     C           ULLDIF    ORLT 0
     C           ULCNUM    ANDEQULCUS6
     C                     WRITEBY6020H6
     C                     WRITEBY6020D6
     C                     ELSE
     C           ULCNUM    IFNE ULCUS6
     C                     WRITEBY6020D6               90
     C                     ENDIF
     C                     ENDIF
     C                     WRITEBY6020D7               90
      *
      * Save customer last printed
     C                     MOVE ULCNUM    ULCUS6
      *
      *  Set flag to indicate details written
     C           ULRPT6    IFNE 'Y'
     C                     MOVE 'Y'       ULRPT6
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LDEFN - Field Definition Routine                             *
      *                                                               *
      * Called by:  None - Contains definitions only                  *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           #LDEFN    BEGSR                           ** SR/#LDEFN**
      *
      *  Copyright statement
     C                     MOVEA@CPY      ULCPY@ 80
      *
      ** Define and initialise work fields
     C                     Z-ADD0         #       50
     C                     Z-ADD0         X       20
     C                     Z-ADD0         ULSFNO  60       Spool file no.
     C                     Z-ADD0         ULDAYN  50       Date - Midas day no.
     C                     Z-ADD0         ULDATE  60       Date - DDMMYY
     C                     Z-ADD0         ULTOTA 150       Total assets
     C                     Z-ADD0         ULTOTL 150       Total liabilities
     C                     Z-ADD0         ULMPER  50       Category period date
     C                     Z-ADD0         ULINAM 150       In amount
     C                     Z-ADD0         ULOTAM 150       Out amount
     C                     Z-ADD0         ULAMNT 150       Amount
     C                     Z-ADD0         ULVDAT  50       Value date
     C                     Z-ADD0         ULMDAT  50       Maturity dat
     C                     Z-ADD0         ULNOTS  50       Notice date
     C**********           Z-ADD0         ULACOD  40       A/C code                           LLN022
     C                     Z-ADD0         ULACOD 100       A/C code                           LLN022
     C                     Z-ADD0         ULLDIF  30       Line difference
     C                     Z-ADD0         ULERR1  70       Error indicators
     C                     MOVE *BLANK    ULERRI  1        Error indicators
     C                     MOVE *BLANK    UVALD   1        Valid deal
     C                     MOVE *BLANK    ULRPT1  1        Details reported
     C                     MOVE *BLANK    ULRPT6  1        Details reported
     C                     MOVE *BLANK    ULOPNF  1        Files opened
     C                     MOVE *BLANK    ULPROC  1        Process account
     C                     MOVE *BLANK    ULBRCA  3        Branch code
     C                     MOVE *BLANK    P@RTCD  7
     C                     MOVE *BLANK    P@OPTN  7
     C                     MOVE *BLANK    ULDATA  7        Date - DDMMMYY
     C                     MOVE *BLANK    ULLOCN  3        Location
     C                     MOVE *BLANK    ULAMTA 21        Edited amount field
     C                     MOVE *BLANK    ULECOD  1        Edit code
     C                     MOVE *BLANK    ULFNAM 10        File name
     C                     MOVE *BLANK    ULERR2  7        Error indicators
     C                     MOVE *BLANK    ULODIN  1        overdue ind tors
     C                     MOVE *BLANK    ULRISO  2        risk countrytors
     C                     MOVE *BLANK    UPLISO  2        Cust countrytors
     C                     MOVE *BLANK    UPRISO  2        Risk countrytors
     C                     MOVE *BLANK    WWRISO  2        work countrytors
     C                     MOVE *BLANK    ULCSNO  6        work cust notors
     C                     MOVE *BLANK    ULINBR  1        Int bearing tors
     C                     MOVE *BLANK    ULACDA  4        Account code - alpha
     C                     MOVE *BLANK    ULASQA  2        Account seq. - alpha
     C           *LIKE     DEFN CNUM      ULCNUM           Customer
     C           *LIKE     DEFN CNUM      ULCUS1           Customer Print 1
     C           *LIKE     DEFN CNUM      ULCUS6           Customer Print 6
     C           *LIKE     DEFN ACSQ      ULACSQ           Account sequence
     C           *LIKE     DEFN CNUM      ULPCUS           Previous customer
     C           *LIKE     DEFN CCY       ULCYCD           Currency
     C           *LIKE     DEFN BBCUST    ULCUST           Customer
     C           *LIKE     DEFN L1PCCR    ULPCCR           DR product code
     C           *LIKE     DEFN L1PCDR    ULPCDR           CR product code
     C           *LIKE     DEFN L1PCSP    ULPCSP           Supplementary code
     C           *LIKE     DEFN L8PCOD    ULPCOD           Product code
     C           *LIKE     DEFN L8CUAS    ULCUAS           Currency amount
     C           *LIKE     DEFN L8SGAM    ULSGAM           Sterling equivalent
     C           *LIKE     DEFN L8MMOD    ULMMOD           Module id.
     C           *LIKE     DEFN L8MCAT    ULMCAT           Maturity category
     C           *LIKE     DEFN L8LIND    ULLIND           Industry code
     C           *LIKE     DEFN A8IBOE    ULIBOE           Include branch
     C           *LIKE     DEFN A6NBDP    ULNBDP           No. of decimal places
     C           *LIKE     DEFN A6NBDP    UL$NDP           Sterling dec. places
     C           *LIKE     DEFN CCY       ULFCCY           From currency
     C           *LIKE     DEFN CCY       ULTCCY           To currency
     C           *LIKE     DEFN L1LINS    ULLINS           Iinstitutione
     C           *LIKE     DEFN L1MIND    ULLICD           Industry code                      LLN021
     C           *LIKE     DEFN L8BTEX    ULBTEX           BT Extract     LLN012
     C           *LIKE     DEFN L8LREX    ULLREX           LR Extract     LLN012
      *
      *  Key List Definitions
      *  ====================
      *
      *  Account extract criteria file
     C           K1ACEX    KLIST
     C                     KFLD           ULACOD           Account code
     C                     KFLD           ULLINS           Institution
     C                     KFLD           ULLICD           Industry code                      LLN021
      *
      *  Account extension file
     C           K1ACCN    KLIST
     C                     KFLD           BRCA             Branch
     C                     KFLD           CNUM             Customer
     C                     KFLD           CCY              Currency
     C                     KFLD           ACOD             Account code
     C                     KFLD           ACSQ             Sequence no.
      *
      *  Retail accounts interest details
     C           K2ACCN    KLIST
     C                     KFLD           CNUM             Customer
     C                     KFLD           CCY              Currency
     C                     KFLD           ACOD             Account code
     C                     KFLD           ACSQ             Sequence no.
     C                     KFLD           BRCA             Branch
      *
     C                     ENDSR
      ********************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/EJECT
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2004
