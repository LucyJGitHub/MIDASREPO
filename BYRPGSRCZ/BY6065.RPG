     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BoE Securities Trades PL Extract/Validation')    *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY6065 - Securities Trades PL Extract/Validation             *
      *                                                               *
      *  Called By: BYC6065 - Securities Trades PL Extract/Validation *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSD027             Date 21Jan09               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 LLN022             Date 24Jan05               *
      *                 LLN021/022         Date 01Mar04               *
      *                 LLN021/023         Date 01Mar04               *
      *                 LLN021/013         Date 11Feb04               *
      *                 179320             Date 03Nov03               *
      *                 LLN012             Date 03Nov03               *
      *                 LLN021  *CREATE    Date 03Nov03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Amendment History                                            *
      *  -----------------                                            *
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Convert Customer Number to Alpha                    *
      *  LLN022 - BOE Upgrade to MidasPlus                            *
      *  LLN021/022 - Dealing Profits not generated                   *
      *  LLN021/023 - Product Code 6113 appears under Payable         *
      *  LLN021/013 - Change sign of event amount                     *
      *  179320 - Details are not being extracted in a single branch  *
      *           environment because the branch code work field      *
      *           ULBRCA is not being set up.                         *
      *  LLN012 - Financial Services Authority - Liquidity Report     *
      *  LLN021 - BoE Profit and Loss return                          *
      *                                                               *
      *****************************************************************
      *
     FTRADSDL0IF  E           K        DISK         KINFSR *PSSR
      *
     FMXMESET1IF  E           K        DISK         KINFSR *PSSR
     F            MXDESETP0                         KRENAMEMXDESET0
     F                                              KINFDS INFDS1
      *
      *  Midas SE Monthly Events - Securities Book Positions
      *
     FSDCURRL0IF  E           K        DISK         KINFSR *PSSR
      *  Currency details
      *
     FSDBRCHL1IF  E           K        DISK         KINFSR *PSSR
      *  Branches
      *
     FSECTI   IF  E           K        DISK         KINFSR *PSSR
      *  Securities details by issuer
      *
     FBYBRCHY0IF  E           K        DISK         KINFSR *PSSR
      *  Branches to be extracted for BoE Reporting
      *
     FINVTP   IF  E           K        DISK         KINFSR *PSSR
      *  Investment type details
      *
     FSDCUHSL2IF  E           K        DISK         KINFSR *PSSR
      *  Historic SPOT Currency rates
      *
     FBYSECTY0IF  E           K        DISK         KINFSR *PSSR
      *  Securities Extension
      *
     FBYSEEXV0IF  E           K        DISK         KINFSR *PSSR
     F            BYSEEXD0                          KRENAMEBYSEEXB0
      *  Securities Extract Criteria
      *
     FBYSEEXV1IF  E           K        DISK         KINFSR *PSSR
     F            BYSEEXD0                          KRENAMEBYSEEXB1
      *  Securities Extract Criteria
      *
     FBYRPDTPPIF  E                    DISK         KINFSR *PSSR      UC
      *  Extract Date File
      *
     FBYEXDTV7IF  E           K        DISK         KINFSR *PSSR
      *  Bank of England Extract File
      *
     FBYMTDTV7IF  E           K        DISK         KINFSR *PSSR                              LLN021
     F            BYEXDTD0                          KRENAMEBYMTDTP0                           LLN021
      *  Bank of England Extract File (Mature)                                                LLN021
     FBYEXIEPPO   E                    DISK         KINFSR *PSSR
      *  Bank of England Extract PL File - (Uses member BYEXTRM)
      *
     FBY6065P1O   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOL1
      *  Bank of England Validation Report - Valid
      *
     FBY6065P2O   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOL2
      *  Bank of England Validation Report - Invalid
      *
      /EJECT
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E                    @CPY    1   1 80
     E                    @CCY      200  3
     E                    @CYD      200 13 8             Currencies - SPOT rate
     E                    @CYR      200  1               Currencies - SPOT rate
     E                    @TAC      200 15 0             Total assets - ccy
     E                    @TAS      200 15 0             Total assets - sterling
     E                    @TLC      200 15 0             Total liabilities - ccy
     E                    @TLS      200 15 0             Total liabilities - STG
      *
      /SPACE 3
     C/EJECT
      ** Local data area for database error details
     ILDA       E DSLDA                         256
      *
      ** Data Structure using Bank file format
     ISDBANK    E DSSDBANKPD
      *
      ** Data Structure using Currency Details
     ISDCURR    E DSSDCURRPD                200
      *
      ** Data Structure for Customer Details
     ISDCUST    E DSSDCUSTPD
      *
      ** Data Structure for Account Codes File                                                LLN021
     ISDACOD    E DSSDACODPD                                                                  LLN021
      ** Data Structure for extra customer details returned by BY9110
      *       ULLISO  - ISO Code of customer's country of location
      *       ULCISO  - ISO Code of customer's country of citizenship
      *       ULRWCD  - Weighting amount
     ISDCUSX      DS
     I                                        1   2 ULLISO
     I                                        3   4 ULCISO
     I                                        5   80ULRWCD
      *
     IBYCUST    E DSBYCUSTX0
      ** Data Structure for Bank of England Customer Details
      *
      ** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM ULPGMN
     I                                      244 253 ULWSID
     I                                      254 263 ULUSER
      *
      * File information data structure (for database files only)
     IINFDS1    E DSY2I1DSP
      *
      * First DS For Access Programs, Short Data Structure
     IDSFDY     E DSDSFDY
      *
      * DS For Access Programs, Long Data Structure
     IDSSDY     E DSDSSDY
      *
      * Data structure for retrieval of data from DTAARA/RUNDAT
     IRUNDAT    E DSRUNDAT
      *
      *  File Information Data Structure for BY6065P1
     ISPOOL1      DS
     I                                    B 123 1240ULNUM1
     I                                    B 188 1890ULOVF1
     I                                    B 367 3680ULCLN1
      *
      *  File Information Data Structure for BY6065P2
     ISPOOL2      DS
     I                                    B 123 1240ULNUM2
     I                                    B 188 1890ULOVF2
     I                                    B 367 3680ULCLN2
      *
      *  Input parameters data structure
      *
     II#DTA       DS                            256
      *                                       From Currency & amount
     I                                        1   3 I#FCCY
     I                                    P   4  110I#FAMT
      *                                       To Currency & amount
     I                                       12  14 I#TCCY
     I                                    P  15  220I#TAMT
      *                                       Rate and multi/div ind.
     I                                       23  23 I#MDIN
     I                                    P  24  368I#RATE
      *                            Output     Rate and multi/div ind.
      *                                       If no in rate
     I                                       37  37 O#MDIN
     I                                    P  38  508O#RATE
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * Main Processing                                               *
      *                                                               *
      * Calls: SR/#LMAIN - Main Processing                            *
      *        SR/#LTERM - Termination Processing                     *
      *                                                               *
      *****************************************************************
      *  Main Processing
     C                     EXSR #LMAIN
      *
      *  Termination Processing
     C                     EXSR #LTERM
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LMAIN - Main Processing                                   *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:      SR/#LBRCH - Branch Processing                     *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      *****************************************************************
     C           #LMAIN    BEGSR                           ** SR/#LDETL**
      *
      * Process according to mode
     C           ULMODE    IFEQ 'INTERIM'
     C           ULMODE    OREQ 'EXTRACT'
     C           ULMODE    OREQ 'LIST   '
      *
     C           *LOVAL    SETLLSDBRCHL1
     C                     READ SDBRCHL1                 88
     C           *IN88     DOWEQ*OFF
      *
      *  Branch processing
     C                     EXSR #LBRCH
      *
      *  Process Deals if branch to be included
     C           ULIBOE    IFEQ 'Y'
      *
      **  Read through securities file by issuer
     C           *LOVAL    SETLLSECTI
     C                     READ SECTI                    89
     C           *IN89     DOWEQ*OFF
      *
      ** Get additional security details
     C                     EXSR #LSECD
      *
      ** Change of issuer
     C           ULISSR    IFNE ISSR
     C**********           MOVE ISSR      ULISSR  60                                          CSD027
     C                     MOVE ISSR      ULISSR  6                                           CSD027
     C                     MOVE ISSR      ULCUST  6
     C                     EXSR #LISSR
     C                     ENDIF
      *
      ** Read all trades FOR SECURITY AND BRANCH
     C           K3TRAD    SETLLTRADSDL0
     C           K3TRAD    READETRADSDL0                 87
     C           *IN87     DOWEQ*OFF
      *
     C                     MOVE TDRF      W0TDRF
      *
     C           K4TRAD    SETLLMXMESET1
     C           K4TRAD    READEMXMESET1                 86
     C           *IN86     DOWEQ*OFF
      *
      **  Only process records with event date < or = EOM date         LLN021
      *                                                                LLN021
     C           EVDT      IFLE CUREO                                                         LLN021
     C           EVDT      ANDGECURSO                                                         LLN021
      *                                                                                       LLN021
      **  Only process records with I/E account codes                  LLN021
      *                                                                LLN021
     C                     EXSR #IEACC                                 LLN021
      *                                                                LLN021
      *
      * Load SPOT details, if needed
     C           EVDT      IFNE ULEVDT
     C                     EXSR #LDCYS
     C                     MOVE EVDT      ULEVDT  50
     C                     ENDIF
      *
      *  Clear File Format
     C                     CLEARBYEXIED0
      *
      * Set Up Extract Information
     C                     EXSR #GETDL
      *
     C                     ENDIF                                          LLN021
     C           K4TRAD    READEMXMESET1                 86
     C                     ENDDO
      *
     C           K3TRAD    READETRADSDL0                 87
     C                     ENDDO
      *
     C                     READ SECTI                    89
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     READ SDBRCHL1                 88
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LISSR - Process Issuer Details                            *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:  *None                                                 *
      *                                                               *
      *****************************************************************
     C           #LISSR    BEGSR
      *
      ** Clear customer data
     C                     CLEARSDCUST
     C                     CLEARBYCUST
     C                     CLEARSDCUSX
     C                     MOVE 'Y'       UVALD
      *
      ** Get customer details
      *
     C                     CALL 'BY9110'
     C                     PARM           ULCUST
     C                     PARM           SDCUST
     C                     PARM           BYCUST
     C                     PARM           SDCUSX
     C                     PARM 0         ULERR1
     C                     PARM *BLANK    ULRTCD
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 04 *
     C                     MOVEL'BY9110  'DBFILE           ************
     C                     Z-ADD04        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Set report indicators (*IN31 to *IN37) with returned settings
     C           ULERR1    IFNE 0
     C                     MOVE 'N'       UVALD   1
     C                     ELSE
     C                     MOVE 'Y'       UVALD
     C                     ENDIF
      *
      **  Initialise work fields
     C**********           MOVE ULCUST    ULCNUM  60                                          CSD027
     C                     MOVE ULCUST    ULCNUM  6                                           CSD027
     C                     MOVE BBCRNM    ULCRNM 20
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LSECD - Get Additional Security Details                   *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:  *None                                                 *
      *                                                               *
      *****************************************************************
     C           #LSECD    BEGSR
      *
      ** Save security shortname
     C                     MOVE SESN      ULSESN 10
      *
     C                     CLEARBYSECTD0
      *
      ** Get security details from LF/SECTYDY0
     C           SESN      CHAINBYSECTD0             90
      *
      **  Get investment type details
     C           K2INVT    CHAININVTP                90
     C           *IN90     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVELSITP      DBKEY            * DBERR 06 *
     C                     MOVEL'INVTP   'DBFILE           ************
     C                     Z-ADD06        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Get the SWIFT currency code
     C                     Z-ADD1         #
     C           NMCY      LOKUP@CCY,#                   90
     C           *IN90     IFEQ *ON
     C           #         OCUR SDCURR
     C                     ELSE
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVELNMCY      DBKEY            * DBERR 08 *
     C                     MOVEL'SDCURRL0'DBFILE           ************
     C                     Z-ADD08        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LBRCH - Processing For New Branch                         *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:      SR/#LRPTT - Final Report Processing               *
      *                                                               *
      *****************************************************************
     C           #LBRCH    BEGSR                           ** SR/#LBRCH**
      *
      *  Reset flag
     C                     MOVE 'N'       ULIBOE           Include branch
      *
      *  If single branch all records must be extracted
     C           AGMBIN    IFEQ 'N'
     C                     MOVE 'Y'       ULIBOE  1
     C                     MOVE A8BRCD    ULBRCA  3                       179320
      *
      *  Multi-branch processing
     C                     ELSE
      *
      *  If files open report currency summaries and close files
     C           ULOPNF    IFEQ 'Y'
     C                     EXSR #LRPTT
     C                     ENDIF
     C                     MOVE A8BRCD    ULBRCA                          LLN012
      *
      *   Check if new branch is to be included in report extract
     C           ULBRCA    CHAINBYBRCHY0             90
     C           *IN90     IFEQ *OFF
     C                     MOVELA8IBOE    ULIBOE
     C                     ENDIF
      *
      *   Reset open flag and page numbers
     C                     MOVE 'N'       ULOPNF  1
     C                     Z-ADD0         PAGE1
     C                     Z-ADD0         PAGE2
      *
      *   Include branch in Bank of England Extract
     C           ULIBOE    IFEQ 'Y'
      *
      *  Open printer files for new branch
     C                     MOVE 'Y'       ULOPNF  1
     C                     OPEN BY6065P1
     C                     OPEN BY6065P2
      *
      *  RCF processing for printer file BY6065P1
     C                     Z-ADDULNUM1    ULSFNO  60
     C**********           CALL 'ZSFILE'                                                      LLN021
     C**********           PARM           ULRSEQ  5                                           LLN021
     C**********           PARM           ULBRCA  3                                           LLN021
     C**********           PARM 'BY6065P1'ULFNAM 10                                           LLN021
     C**********           PARM           ULSFNO  60                                          LLN021
     C**********           PARM           ULRTCD  7                                           LLN021
     C           ULRTCD    IFNE *BLANK
     C                     MOVEL'*NONE'   DBKEY            ************
     C                     MOVEL'ZSFILE  'DBFILE           * DBERR 01 *
     C                     Z-ADD1         DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
      *  RCF processing for printer file BY6065P2
     C                     Z-ADDULNUM2    ULSFNO
     C**********           CALL 'ZSFILE'                                                      LLN021
     C**********           PARM           ULRSEQ                                              LLN021
     C**********           PARM           ULBRCA                                              LLN021
     C**********           PARM 'BY6065P2'ULFNAM                                              LLN021
     C**********           PARM           ULSFNO                                              LLN021
     C**********           PARM           ULRTCD                                              LLN021
     C           ULRTCD    IFNE *BLANK
     C                     MOVEL'*NONE'   DBKEY            ************
     C                     MOVEL'ZSFILE  'DBFILE           * DBERR 02 *
     C                     Z-ADD2         DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
      *
      *  Write report headings to BY6065P1 and BY6065P2
     C                     MOVE L0RPDA    L#RPDA
     C                     MOVE A8BRCD    L#BRCD
     C                     MOVE A8BRNM    L#BRNM
     C                     WRITEBY6065H1
     C                     WRITEBY6065H2
      *
     C                     ENDIF
     C                     ENDIF
      *
      *  Reset currency summary totals
     C                     Z-ADD0         @TAC
     C                     Z-ADD0         @TLC
     C                     Z-ADD0         @TAS
     C                     Z-ADD0         @TLS
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #GETDL - Get Extract Details                                  *
      *                                                               *
      * Called by:  SR/#LMAIN - Main Processing                       *
      *                                                               *
      * Calls:                                                        *
      *         SR/#LNAFM - Format NA Deal Details                    *
      *         SR/#LRPXT - Report/Extract Details                    *
      *                                                               *
      *****************************************************************
     C           #GETDL    BEGSR                           ** SR/#GETDL**
      *
      *  Format work fields for deal
     C                     EXSR #FORMT
      *
      *  Report/Extract deal details
     C                     EXSR #LRPXT
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #FORMT - Format Work Fields For Deals                         *
      *                                                               *
      * Called by:  SR/#GETDL - Get Details                           *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #FORMT    BEGSR                           ** SR/#FORMT**
      *
     C*******************  MOVELTDRF      ULTRAN  6                                           LLN021
     C                     MOVELSESN      ULTRAN 10                                           LLN021
     C                     Z-ADDTDDT      L8DDAT
     C                     Z-ADDTDVD      L8VDAT
     C*******************  MOVELTDTP      ULTDTP  2                                           LLN021
      *
      * Customer Number
     C**********           MOVELULCNUM    L8CUST                                              CSD027
     C                     MOVE ULCNUM    L8CUST                                              CSD027
      *
      * Access BYEXDTPP
      * Most output fields are named the same as on this file so
      * no need to set up for output once obtained from here.
      * Check on maured this mobth file also                              LLN021
      *
     C                     MOVEL'TP'      KMMOD
     C                     MOVELULBRCA    KBRCA
     C                     MOVE ULTRAN    KTRAN
     C                     MOVELEVCY      KCCY
      *
     C           KEXDT     KLIST
     C                     KFLD           KMMOD   2
     C                     KFLD           KBRCA   3
     C**********           KFLD           KTRAN  10                                           LLN022
     C                     KFLD           KTRAN  12                                           LLN022
     C                     KFLD           KCCY    3
      *
     C           KEXDT     CHAINBYEXDTV7             76
      *
     C           *IN76     IFEQ *ON
     C           KEXDT     CHAINBYMTDTV7             76                   LLN021
      *
     C           *IN76     IFEQ *ON                                       LLN021
      * If invalid, try using TS instead
     C                     MOVEL'TS'      KMMOD
     C           KEXDT     CHAINBYEXDTV7             76
      *
     C           *IN76     IFEQ *ON                                       LLN021
      *                                                                   LLN021
     C           KEXDT     CHAINBYMTDTV7             76                   LLN021
     C           *IN76     IFEQ *ON                                       LLN021
      * If invalid, try using CP instead                                  LLN021
     C                     MOVEL'CP'      KMMOD                           LLN021
     C           KEXDT     CHAINBYEXDTV7             76                   LLN021
      *                                                                   LLN021
     C           *IN76     IFEQ *ON                                       LLN021
     C           KEXDT     CHAINBYMTDTV7             76                   LLN021
      *
      * If invalid, drop it
     C           *IN76     IFEQ *ON
      *                                                                   LLN021
     C           KEXDT     CHAINBYMTDTV7             76                   LLN021
     C           *IN76     IFEQ *ON                                       LLN021
      *
     C                     MOVE *ON       *IN41
     C                     MOVE 'N'       UVALD
     C                     CLEARBYEXDTD0
     C                     GOTO CONT
     C                     ENDIF
      *
     C                     ENDIF                                                              LLN021
     C                     ENDIF                                                              LLN021
     C                     ENDIF                                                              LLN021
     C                     ENDIF                                                              LLN021
     C                     ENDIF                                                              LLN021
     C                     ENDIF
      *
      *
      ** Product code
      *
     C                     Z-ADD0         L8PCOD
      *
     C                     MOVELL8MMOD    ULTDTP  2                                           LLN021
     C                     MOVELTRFR      ULTDRF  6                                           LLN021
     C                     MOVE L8LINS    ULLINS
     C                     MOVE L8LIND    ULMIND
     C                     MOVE SITP      ULSITP
      *
      *  Check extract criteria exists using Industry code
     C           K1SEE1    CHAINBYSEEXV1             78
      *
     C           *IN78     IFEQ *ON
     C           K1SEE2    CHAINBYSEEXV1             78
      *
     C           *IN78     IFEQ *ON
     C           K0SEE1    CHAINBYSEEXV0             78
      *
     C           *IN78     IFEQ *ON
     C           K0SEE2    CHAINBYSEEXV0             78
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      * Invalid if no record found
     C           *IN78     IFEQ *ON
     C                     MOVE *ON       *IN38
     C                     MOVE 'N'       UVALD
     C                     GOTO CONT
     C                     ENDIF
      *
      * Product code setup
      * Fee code setup                                                                        LLN021
      *
     C                     SELEC
      *
     C           DECATC    WHEQ '35'                                                          LLN021
     C           DECATC    OREQ '37'                                                          LLN021
     C           DECATC    OREQ '38'                                                          LLN021
     C           DECATC    OREQ '39'                                                          LLN021
     C           DECATC    OREQ '40'                                                          LLN021
     C           DECATC    OREQ '41'                                                          LLN021
     C           DECATC    OREQ '44'                                                          LLN021
     C                     Z-ADDL5PLTX    L8PCOD
      *
     C           DECATC    WHEQ '17'                                                          LLN021
     C           DECATC    OREQ '18'                                                          LLN021
     C           DECATC    OREQ '19'                                                          LLN021
     C           DECATC    OREQ '20'                                                          LLN021
     C********** DECATC    WHEQ '23'                                                          LLN021
     C           DECATC    OREQ '23'                                                          LLN021
     C           DECATC    OREQ '24'
     C           DECATC    OREQ '25'
     C           DECATC    OREQ '26'
     C           DECATC    OREQ '29'                       LLN021
     C           DECATC    OREQ '30'                       LLN021
     C           DECATC    OREQ '31'                       LLN021
     C           DECATC    OREQ '32'                       LLN021
     C           DECATC    OREQ '33'                       LLN021
     C           DECATC    OREQ '34'                       LLN021
     C           DECATC    OREQ '35'                       LLN021
     C           DECATC    OREQ '36'                       LLN021
     C           DECATC    OREQ '42'                       LLN021
     C           DECATC    OREQ '43'                       LLN021
     C*********************Z-ADDL5PLCG    L8PCOD           LLN021
     C* If event amt < 0 then add in the Credit charge     LLN021
     C* else add in the Debit Charge.                      LLN021
     C           EVAM      IFLT 0                          LLN021
     C                     Z-ADDL5CCHG    L8PCOD           LLN021
     C                     MOVELL5CFEE    L8FEET           LLN021
     C                     ELSE                            LLN021
     C                     Z-ADDL5DCHG    L8PCOD           LLN021
     C                     MOVELL5DFEE    L8FEET           LLN021
     C                     ENDIF                           LLN021
      *
     C                     ENDSL
      *
      * Invalid if product code is 0
     C           L8PCOD    IFEQ 0
     C                     MOVE 'N'       UVALD
     C                     MOVE *ON       *IN38
     C                     ENDIF
      *
     C           CONT      TAG
      *
      * Move fields from MXMEFCV1 into output fields
      * Scaled ccy amount
     C                     Z-ADDEVAM      L8CUAS
      *
      * Convert amount in Base Currency equivalent
      *
     C           EVCY      IFNE BJLCCY                                                        LLN021
     C                     EXSR #LOCCY                                                        LLN021
     C                     Z-ADD@LAMT     L8SGAM                                              LLN021
     C                     ELSE                                                               LLN021
     C                     Z-ADDEVAM      L8SGAM
     C                     ENDIF                                                              LLN021
      *                                                                                       LLN021
     C**********           MOVE EVCY      I#FCCY                                              LLN021
     C**********           Z-ADDEVAM      I#FAMT                                              LLN021
      *
     C**********           Z-ADD1         #                                                   LLN021
     C********** EVCY      LOKUP@CCY,#                   90                                   LLN021
     C**********           Z-ADD@CYD,#    I#RATE                                              LLN021
     C**********           MOVE @CYR,#    I#MDIN                                              LLN021
      *
     C**********           MOVELBJCYCD    I#TCCY                                              LLN021
     C**********           Z-ADD0         I#TAMT                                              LLN021
      *
     C**********           CALL 'AOCCYCV0'                                                    LLN021
     C**********           PARM *BLANKS   ULRTCD                                              LLN021
     C**********           PARM '*F*T*RAT'W0ACT   8                                           LLN021
     C********** I#DTA     PARM I#DTA     P0DTA 256                                           LLN021
      *
     C********** ULRTCD    IFNE *BLANK                                                        LLN021
     C********** *LOCK     IN   LDA                                                           LLN021
     C**********           MOVELULPGMN    DBPGM            ************                       LLN021
     C**********           MOVEL'I#DTA'   DBKEY            * DBERR 13 *                       LLN021
     C**********           MOVEL'AOCCYCV0'DBFILE           ************                       LLN021
     C**********           Z-ADD13        DBASE                                               LLN021
     C**********           OUT  LDA                                                           LLN021
     C**********           EXSR *PSSR                                                         LLN021
     C**********           ENDIF                                                              LLN021
      *
      * Convert Base currency equivalent into GBP equivalent
      *
     C**********           MOVE BJCYCD    I#FCCY                                              LLN021
     C**********           Z-ADDI#TAMT    I#FAMT                                              LLN021
      *
     C**********           Z-ADD1         #                                                   LLN021
     C********** 'GBP'     LOKUP@CCY,#                   90                                   LLN021
     C**********           Z-ADD@CYD,#    I#RATE                                              LLN021
     C**********           MOVE @CYR,#    I#MDIN                                              LLN021
      *
     C********** I#MDIN    IFEQ 'D'                                                           LLN021
     C**********           MOVE 'M'       I#MDIN                                              LLN021
     C**********           ELSE                                                               LLN021
     C**********           MOVE 'D'       I#MDIN                                              LLN021
     C**********           ENDIF                                                              LLN021
      *
     C**********           MOVEL'GBP'     I#TCCY                                              LLN021
     C**********           Z-ADD0         I#TAMT                                              LLN021
      *
     C**********           CALL 'AOCCYCV0'                                                    LLN021
     C**********           PARM *BLANKS   ULRTCD                                              LLN021
     C**********           PARM '*F*T*RAT'W0ACT   8                                           LLN021
     C********** I#DTA     PARM I#DTA     P0DTA 256                                           LLN021
      *
     C********** ULRTCD    IFNE *BLANK                                                        LLN021
     C********** *LOCK     IN   LDA                                                           LLN021
     C**********           MOVELULPGMN    DBPGM            ************                       LLN021
     C**********           MOVEL'I#DTA'   DBKEY            * DBERR 13 *                       LLN021
     C**********           MOVEL'AOCCYCV0'DBFILE           ************                       LLN021
     C**********           Z-ADD13        DBASE                                               LLN021
     C**********           OUT  LDA                                                           LLN021
     C**********           EXSR *PSSR                                                         LLN021
     C**********           ENDIF                                                              LLN021
      *
     C**********           Z-ADDI#TAMT    L8SGAM                                              LLN021
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRPXT - Report/Extract Processing                         *
      *                                                               *
      * Called by:                                                    *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * Calls:      SR/#LRPT1 - Report Valid Details to BY6065P1      *
      *             SR/#LRPT2 - Report Valid Details to BY6065P2      *
      *                                                               *
      *****************************************************************
     C           #LRPXT    BEGSR                           ** SR/#LRPXT**
      *
      *  Do not report/extract details for product code 9998
     C           L8PCOD    IFNE 9998
      *
      *  Details are valid - report details to BY6065P1
     C           UVALD     IFEQ 'Y'
     C           L8PCOD    ANDNE9999
      *
      *    Report details to BY6065P1
     C                     EXSR #LRPT1
      *
      *    Create extract record if extract mode
     C           ULMODE    IFEQ 'EXTRACT'
     C                     EXSR #LXTRC
     C                     ENDIF
      *
      *  Details are invalid - report details to BY6065P2
     C                     ELSE
     C                     EXSR #LRPT2
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE *OFF      *IN38
     C                     MOVE *OFF      *IN41
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LXTRC - Create Extract Record                                *
      *                                                               *
      * Called by: Sr/#LRPXT - Report/Extract Details                 *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LXTRC    BEGSR                           ** SR/#LXTRC**
      *
     C                     WRITEBYEXIED0
      *
      *  Accumulate total assets and liabilities
      ** (only if Extract Record is for the BT report)                    LLN012
     C           L8BTEX    IFEQ 'Y'                                       LLN012
      *                                                                   LLN012
     C                     Z-ADD1         #
     C           L8CYCD    LOKUP@CCY,#                   90
     C           L8PCOD    IFGE 1100
     C           L8PCOD    ANDLE1960
     C                     ADD  L8CUAS    @TAC,#
     C                     ADD  L8SGAM    @TAS,#
     C                     ENDIF
     C           L8PCOD    IFGE 2100
     C           L8PCOD    ANDLE3990
     C                     ADD  L8CUAS    @TLC,#
     C                     ADD  L8SGAM    @TLS,#
     C                     ENDIF
      *                                                                   LLN012
     C                     ENDIF                                          LLN012
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * #LRPT1 - Write Details of Valid Deals to BY6065P1             *
      *                                                               *
      * Called by: SR/#LRPXT - Report/Extract Details                 *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPT1    BEGSR                           ** SR/#LRPT1**
      *
      *  Reset report indicator
     C                     MOVE *OFF      *IN30
     C           L8PCOD    IFEQ 9999
     C                     MOVE *ON       *IN41
     C                     ENDIF
      *
      *  Set up report fields
     C**********           MOVELULCUST    L#CNUM           Customer                           CSD027
     C                     MOVE ULCUST    L#CNUM           Customer                           CSD027
     C                     MOVELULCRNM    L#CRNM
     C***************      MOVELULTRAN    L#TRAN                          LLN021
     C                     MOVELULTDRF    L#TDRF                          LLN021
     C***********          MOVELULTDTP    L#TDTP                          LLN021
     C                     MOVEL'CH'      L#TDTP                          LLN021
     C                     MOVELL8FEET    L#FEET                          LLN021
     C                     CALL 'ZDATE2'                   Trade date
     C                     PARM L8DDAT    ULDAYN  50
     C                     PARM           BJDFIN
     C                     PARM           ULDATE  60
     C           L#TDAT    PARM           ULDATA  7
     C                     CALL 'ZDATE2'                   Value date
     C                     PARM L8VDAT    ULDAYN  50
     C                     PARM           BJDFIN
     C                     PARM           ULDATE  60
     C           L#VDAT    PARM           ULDATA  7
     C                     MOVELL8CYCD    L#CYCD           Currency
     C                     MOVE ULLISO    L#LISO           Customer country code
     C                     MOVE ULCISO    L#RISO           Risk country code
     C                     MOVE L8LIND    L#INDC           Industry code
     C                     MOVE L8LINS    L#INSC           Institution code
     C                     MOVE BBCRNM    L#CRNM           Customer report name
     C                     CALL 'ZSEDIT'
     C                     PARM L8CUAS    ULAMNT 150
     C                     PARM A6NBDP    ULNBDP  10
     C                     PARM 'J'       ULECOD  1
     C                     PARM           ULAMTA 21
     C                     MOVE ULAMTA    L#CUAS           Currency amount
     C                     CALL 'ZSEDIT'
     C                     PARM L8SGAM    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA
     C                     MOVE ULAMTA    L#SGAM           Sterling equivalent
     C                     MOVE L8PCOD    L#PCOD           Product code
     C                     MOVELL8BTEX    L#BTEX  1        BT Indicator   LLN012
     C                     MOVELL8LREX    L#LREX  1        LR Indicator   LLN012
      *
      *  Set print customer indicator
     C           L8CUST    IFNE ULCUS1
     C                     MOVE *ON       *IN30
     C**********           Z-ADDL8CUST    ULCUS1  60
     C                     MOVELL8CUST    ULCUS1  6                                           CSD027
     C                     ENDIF
      *
      *  Write details to report
     C           ULOVF1    SUB  ULCLN1    ULLDIF  30
     C   30                SUB  2         ULLDIF
     C           ULLDIF    IFLT 0
     C                     MOVE *ON       *IN30
     C                     WRITEBY6065H1
     C                     ENDIF
     C                     WRITEBY6065D1               90
      *
     C                     CLEARBY6065D1
      *
      *  Set flag to indicate details written
     C           ULRPT1    IFNE 'Y'
     C                     MOVE 'Y'       ULRPT1  1
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LRPT2 - Write Details of Invalid Deals To BY6065P2           *
      *                                                               *
      * Called by: SR/#LRPXT - Report/Extract Details                 *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LRPT2    BEGSR                           ** SR/#LRPT2**
      *
      *  Set indicators to report details of invalid details
     C                     MOVE ULERR1    ULERR2
     C                     MOVEAULERR2    *IN,31
      *
      *  Determine no. of errors to be reported for overflow check
     C                     Z-ADD0         X
     C   31                ADD  1         X
     C   32                ADD  1         X
     C   33                ADD  1         X
     C   34                ADD  1         X
     C   35                ADD  1         X
     C   36                ADD  1         X
     C   37                ADD  1         X
      *
      *  Set up report fields
     C**********           MOVELL8CUST    L#CNUM           Customer                           CSD027
     C                     MOVE L8CUST    L#CNUM           Customer                           CSD027
     C                     MOVELBBCRNM    L#CRNM
     C******************** MOVELULTRAN    L#TRAN                          LLN021
     C                     MOVELULTDRF    L#TDRF                          LLN021
     C                     MOVELULTDTP    L#TDTP
     C                     CALL 'ZDATE2'                   Value date
     C                     PARM L8DDAT    ULDAYN  50
     C                     PARM           BJDFIN
     C                     PARM           ULDATE  60
     C           L#TDAT    PARM           ULDATA  7
     C                     CALL 'ZDATE2'                   Value date
     C                     PARM L8VDAT    ULDAYN  50
     C                     PARM           BJDFIN
     C                     PARM           ULDATE  60
     C           L#VDAT    PARM           ULDATA  7
     C                     MOVE ULCISO    L#CISO           Customer country code
     C                     MOVE ULLISO    L#LISO           Risk country code
     C                     MOVE L8LIND    L#INDC           Industry code
     C                     MOVE L8LINS    L#INSC           Institution code
     C                     MOVELEVCY      L#CYCD           Currency
     C                     MOVE BBCRNM    L#CRNM           Customer report name
     C                     CALL 'ZSEDIT'
     C                     PARM L8CUAS    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA
     C                     MOVE ULAMTA    L#CUAS           Currency amount
     C                     CALL 'ZSEDIT'
     C                     PARM L8SGAM    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA
     C                     MOVE ULAMTA    L#SGAM           Sterling equivalent
     C                     MOVE L8PCOD    L#PCOD           Product code
      *
      *  Set report indicators if product code in error
     C           L8PCOD    IFEQ 9999
     C                     MOVE *ON       *IN41
     C                     ENDIF
      *
      *  Write details to report
     C           ULOVF2    SUB  ULCLN2    ULLDIF
     C                     SUB  X         ULLDIF
     C           ULLDIF    IFLE 5
     C           L8CUST    ANDNEULCUS2
     C           ULLDIF    ORLE 0
     C           L8CUST    ANDEQULCUS2
     C                     MOVE *ON       *IN30
     C                     WRITEBY6065H2
     C                     WRITEBY6065D2               90
     C                     ELSE
     C           ULCNUM    IFNE ULCUS2
     C                     WRITEBY6065D2               90
     C                     ENDIF
     C                     ENDIF
     C                     WRITEBY6065D3               90
      *
      *  Reset report indicators
     C                     MOVE *OFF      *IN30
     C                     MOVEA'0000000' *IN,31
     C                     CLEARBY6065D3
      *
      *  Save customer
     C**********           Z-ADDULCNUM    ULCUS2  60                                          CSD027
     C                     MOVE ULCNUM    ULCUS2  6                                           CSD027
      *
      *  Set flag to indicate details written
     C           ULRPT2    IFNE 'Y'
     C                     MOVE 'Y'       ULRPT2  1
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SR/#LRPTT - Final Reporting                                   *
      *                                                               *
      * Called by:  SR/#LBRCH - Change of Branch Processing           *
      *             SR/#LTERM - Termination Processing                *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           #LRPTT    BEGSR                           ** SR/#LRPTT**
      *
      *  No details to report
     C           ULRPT1    IFNE 'Y'
     C                     WRITEBY6065E1               90
     C                     ENDIF
      *
      *  No details to report
     C           ULRPT2    IFNE 'Y'
     C                     WRITEBY6065E2               90
     C                     ENDIF
      *
      *   Report currency summary if EXTRACT mode
     C           ULMODE    IFEQ 'EXTRACT'
     C           ULRPT1    ANDEQ'Y'
      *
      *   Write heading
     C                     WRITEBY6065R1
      *
      *   Write detail record for each currency
     C                     Z-ADD0         ULTOTA
     C                     Z-ADD0         ULTOTL
     C                     Z-ADD1         #
     C           @CCY,#    DOWNE*BLANK
     C           #         ANDLT200
     C           #         OCUR SDCURR
      *
     C           @TAC,#    IFNE 0
     C           @TLC,#    ORNE 0
     C                     MOVE A6CYCD    L#CYCD           Currency
     C                     MOVE A6CYNM    L#CYNM           Currency name
     C                     CALL 'ZSEDIT'
     C                     PARM @TAC,#    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - ccy
     C                     MOVE ULAMTA    L#TLAC
     C                     CALL 'ZSEDIT'
     C                     PARM @TAS,#    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - STG
     C                     MOVE ULAMTA    L#TLAS
     C                     CALL 'ZSEDIT'
     C                     PARM @TLC,#    ULAMNT
     C                     PARM A6NBDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - ccy
     C                     MOVE ULAMTA    L#TLLC
     C                     CALL 'ZSEDIT'
     C                     PARM @TLS,#    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - STG
     C                     MOVE ULAMTA    L#TLLS
      *
      *   Report details
     C                     WRITEBY6065S1               90
     C           ULCLN1    IFGT ULOVF1                     Overflow
     C                     WRITEBY6065R1
     C                     ENDIF
      *
      ** Write record for total assets in CCY                             LLN012
     C*********************MOVELULBRCA    LTBRCD                          LLN012
     C*********************MOVEL'DL'      LTMMOD                          LLN012
     C*********************MOVEL'A'       LTASLI                          LLN012
     C*********************MOVEL@CCY,#    LTCYCD                          LLN012
     C*********************MOVEL@TAC,#    LTCUAS                          LLN012
     C*********************MOVEL@TAS,#    LTSGAM                          LLN012
     C*********************WRITEBYTOTED0               90                 LLN012
      *                                                                   LLN012
      ** Write record for total liabilities in CCY                        LLN012
     C*********************MOVELULBRCA    LTBRCD                          LLN012
     C*********************MOVEL'DL'      LTMMOD                          LLN012
     C*********************MOVEL'L'       LTASLI                          LLN012
     C*********************MOVEL@CCY,#    LTCYCD                          LLN012
     C*********************MOVEL@TLC,#    LTCUAS                          LLN012
     C*********************MOVEL@TLS,#    LTSGAM                          LLN012
      *                                                                   LLN012
     C*********************WRITEBYTOTED0               90                 LLN012
      *                                                                   LLN012
      *  Accumulate total assets and liabilities in sterling
     C                     ADD  @TAS,#    ULTOTA 150
     C                     ADD  @TLS,#    ULTOTL 150
     C                     ENDIF
      *
     C                     ADD  1         #
     C                     ENDDO
      *
      *   Report overall asset and libilities totals
     C                     CALL 'ZSEDIT'
     C                     PARM ULTOTA    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total assets - STG
     C                     MOVE ULAMTA    L#TOTA
     C                     CALL 'ZSEDIT'
     C                     PARM ULTOTL    ULAMNT
     C                     PARM UL$NDP    ULNBDP
     C                     PARM 'J'       ULECOD
     C                     PARM           ULAMTA           Total liabs. - STG
     C                     MOVE ULAMTA    L#TOTL
     C           ULCLN1    IFGT ULOVF1                     Overflow
     C                     WRITEBY6065R1
     C                     ENDIF
     C                     WRITEBY6065T1               90
      *
     C                     ENDIF
      *
      *  Write end of report FOR BY6065P1
     C                     WRITEBY6065Z1               90
      *
      *  Write end of report FOR BY6065P2
     C                     WRITEBY6065Z2               90
      *
      *  Close files
     C                     CLOSEBY6065P1
     C                     CLOSEBY6065P2
      *
      *  Reset file open flag indicator and totals
     C                     MOVE 'N'       ULOPNF
     C                     MOVE 'N'       ULRPT1
     C                     MOVE 'N'       ULRPT2
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LDBER - Database Error Handling                              *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           #LDBER    BEGSR                           ** #LDBER **
      *
      *  Update LDA with details of error
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM
     C                     OUT  LDA
      *
      *  Report data base error details
     C                     WRITEBY6065F2
      *
      *  Terminate program
     C                     EXSR *PSSR
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
      *  Check if error has previously been reported
     C           ULERRI    IFNE 'Y'
     C                     MOVE 'Y'       ULERRI  1
     C                     MOVE 'Y'       ULRTCD  7
     C                     DUMP
     C                     ENDIF
      *
      *  Set on external indicators
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LTERM - Termination Processing                               *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LTERM    BEGSR                           ** SR/#LTERM**
      *
      *  Final reporting
     C           ULOPNF    IFEQ 'Y'
     C                     EXSR #LRPTT
     C                     ENDIF
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation Routine                       *
      *                                                               *
      * Called by:  At program initialisation                         *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR                           ** *INZSR SR**
      *
     C           *ENTRY    PLIST
     C                     PARM           ULMODE  7        Run mode
     C                     PARM           ULRSEQ  5        Report sequence
     C                     PARM           ULRTCD           Return code
     C                     PARM           CUREOM  5                    LLN021
     C                     PARM           CURSOM  5                    LLN021
      *
      *  Set report indicator according to mode
     C           ULMODE    IFEQ 'EXTRACT'
     C                     MOVE *ON       *IN21
     C                     ELSE
     C                     MOVE *OFF      *IN21
     C                     ENDIF
      *
      *
      *  Get month end details                                                                LLN021
      *                                                                                       LLN021
     C                     MOVELCURSOM    CURSO   50                                          LLN021
     C                     MOVELCUREOM    CUREO   50                                          LLN021
      *                                                                                       LLN021
      *  Get Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    P@RTCD  7
     C                     PARM '*FIRST ' P@OPTN  7
     C                     PARM           SDBANK
     C           P@RTCD    IFNE *BLANKS
     C           BJTYLC    OREQ 'D'                        ON-DELETED
     C                     MOVEL'*NONE '  DBKEY            ************
     C                     MOVEL'SDBANKPD'DBFILE           * DBERR 14 *
     C                     Z-ADD14        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
      *
      *  Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ELSE
     C                     MOVE *OFF      *IN98
     C                     END
      *
      *  Copyright statement
     C                     MOVEA@CPY      ULCPY@ 80
      *
      ** Define external data areas
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           RUNDAT
      *
      ** Define and initialise work fields
     C                     Z-ADD0         #       50
     C                     Z-ADD0         X       20
      *
      *  Get the extract date
     C                     OPEN BYRPDTPP
     C           1         CHAINBYRPDTPP             90
     C           *IN90     IFEQ *ON
     C                     MOVEL'*NONE '  DBKEY            ************
     C                     MOVEL'BYRPDTPP'DBFILE           * DBERR 19 *
     C                     Z-ADD19        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
     C                     CLOSEBYRPDTPP
      *
      * Check if multi-branching
     C                     IN   RUNDAT
     C                     UNLCKRUNDAT
      *  Single branch:
     C           AGMBIN    IFNE 'Y'
     C                     MOVE 'Y'       ULOPNF  1
     C                     MOVE *OFF      *IN20
      *
      *  RCF processing for printer file BY6065P1
     C                     OPEN BY6065P1
     C                     Z-ADDULNUM1    ULSFNO
     C                     CALL 'ZSFILE'
      **********                                                                              LLN021
     C**********           PARM           ULRSEQ                                              LLN021
     C**********           PARM           ULBRCA                                              LLN021
     C**********           PARM 'BY6065P1'ULFNAM                                              LLN021
     C**********           PARM           ULSFNO                                              LLN021
     C**********           PARM           ULRTCD                                              LLN021
     C           ULRTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVELULPGMN    DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 19 *
     C                     MOVEL'ZSFILE  'DBFILE           ************
     C                     Z-ADD19        DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  RCF processing for printer file BY6065P2
     C                     OPEN BY6065P2
     C                     Z-ADDULNUM2    ULSFNO
     C                     CALL 'ZSFILE'
     C**********           PARM           ULRSEQ                                              LLN021
     C**********           PARM           ULBRCA                                              LLN021
     C**********           PARM 'BY6065P2'ULFNAM                                              LLN021
     C**********           PARM           ULSFNO                                              LLN021
     C**********           PARM           ULRTCD                                              LLN021
     C           ULRTCD    IFNE *BLANK
     C                     MOVEL'*NONE '  DBKEY            ************
     C                     MOVEL'ZSFILE  'DBFILE           * DBERR 20 *
     C                     Z-ADD20        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
      *
      *  Write report headings to BY6065P1 and BY6065P2
     C                     MOVE L0RPDA    L#RPDA
     C                     WRITEBY6065H1
     C                     WRITEBY6065H2
      *
      *  Multi branching indicator
     C                     ELSE
     C                     MOVE *ON       *IN20
     C                     ENDIF
      *
      *  Load currency details
     C                     Z-ADD0         #
     C           *IN90     DOWEQ*OFF
     C           #         ANDLT200
     C                     ADD  1         #
     C           #         OCUR SDCURR
     C                     READ SDCURRL0                 90
     C           *IN90     IFEQ *OFF
     C                     MOVE A6CYCD    @CCY,#
     C                     ENDIF
     C                     ENDDO
      *
      *  Get decimal places for sterling currency code
     C                     Z-ADD1         #
     C           BJCYCD    LOKUP@CCY,#                   90
     C           *IN90     IFEQ *ON
     C           #         OCUR SDCURR
     C                     Z-ADDA6NBDP    UL$NDP  10
     C                     ELSE
     C           200       OCUR SDCURR
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*KEY   ' P@OPTN
     C                     PARM           L8CYCD
     C           SDCURR    PARM           DSSDY
     C           P@RTCD    IFNE *BLANK                     Error on call
     C                     MOVELBJCYCD    DBKEY            ************
     C                     MOVEL'SDCURRPD'DBFILE           * DBERR 21 *
     C                     Z-ADD21        DBASE            ************
     C                     EXSR #LDBER
     C                     ENDIF
     C                     Z-ADDA6NBDP    UL$NDP
     C                     ENDIF
      *
     C                     Z-ADD0         ULERR1  70
     C                     MOVE *BLANK    ULERR2  7        Error indicators
      *
      *  Investment type key list
     C           K2INVT    KLIST
     C                     KFLD           NMCY
     C                     KFLD           SITP
      *
      ** Trades key list
     C           K3TRAD    KLIST
     C                     KFLD           ULBRCA
     C                     KFLD           ULSESN
      *
      ** Trades events key list
     C           K4TRAD    KLIST
     C                     KFLD           ULBRCA
     C                     KFLD           W0TDRF  60
      *
      *  Securities extract criteria key list - BYSEEXV1
     C           K1SEE1    KLIST
     C                     KFLD           ULSESN
     C                     KFLD           ULLINS
     C                     KFLD           ULMIND
      *
      *  Securities extract criteria key list
     C           K1SEE2    KLIST
     C                     KFLD           ULSESN
      *
      *  Securities extract criteria key list - BYSEEXV0
     C           K0SEE1    KLIST
     C                     KFLD           W0SESN 10
     C                     KFLD           ULSITP  3
     C                     KFLD           ULLINS  20
     C                     KFLD           ULMIND  3
      *
     C           K0SEE2    KLIST
     C                     KFLD           W0SESN 10
     C                     KFLD           ULSITP
     C                     KFLD           W0LINS  20
     C                     KFLD           W0MIND  3
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      * SR/#LDCYS - Load Currency SPOT details (historic)             *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *****************************************************************
     C           #LDCYS    BEGSR                           ** SR/#LDCYS**
      *
     C                     Z-ADD0         #
      *
     C           PKEY2     KLIST
     C                     KFLD           @CCY,#
     C                     KFLD           EVDT
      *
     C           1         DO   200       #
      *
     C           PKEY2     SETLLSDCUHSL2                 90
     C   90      PKEY2     READESDCUHSL2                 90
     C  N90                READPSDCUHSL2                 90
      *
     C           *IN90     IFEQ *OFF
     C                     Z-ADDG2SPRT    @CYD,#
     C                     MOVE G2MDIN    @CYR,#
     C                     ELSE
     C                     Z-ADD0         @CYD,#
     C                     MOVE *BLANKS   @CYR,#
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      ********************************************************************
      *                                                               *   LLN021
      * #IEACC - Check whether Income/Expense Account                 *   LLN021
      *                                                               *   LLN021
      * Called by:  L#MAIN                                            *   LLN021
      *                                                               *   LLN021
      * Calls: None                                                   *   LLN021
      *                                                               *   LLN021
      *****************************************************************   LLN021
     C           #IEACC    BEGSR                           ** *IEACC SR** LLN021
      *                                                                   LLN021
     C                     MOVE *OFF      *IN25                           LLN021
     C                     MOVE *OFF      *IN26                           LLN021
      *                                                                   LLN021
     C           EADAC1    IFNE 0                                         LLN021
     C**********           MOVELEADAC1    @ADAC1  4                                    LLN021 LLN022
     C                     MOVELEADAC1    @ADAC1 10                                           LLN022
     C                     CALL 'AOACODR0'                                LLN021
     C                     PARM *BLANKS   P@RTCD                          LLN021
     C                     PARM '*KEY   ' P@OPTN                          LLN021
     C**********           PARM @ADAC1    P@ACOD  4                                    LLN021 LLN022
     C                     PARM @ADAC1    P@ACOD 10                                           LLN022
     C           SDACOD    PARM SDACOD    DSSDY                           LLN021
      *                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVELEADAC1    DBKEY            ************   LLN021
     C                     MOVEL'SDCAODPD'DBFILE           * DBERR 05 *   LLN021
     C                     Z-ADD5         DBASE            ************   LLN021
     C                     EXSR #LDBER                                    LLN021
      *                                                                   LLN021
     C                     ELSE                                           LLN021
      *                                                                   LLN021
      * Check whether I/E account                                         LLN021
      *                                                                   LLN021
     C           A5ACSC    IFEQ 'IN'                                      LLN021
     C           A5ACSC    OREQ 'EX'                                      LLN021
     C                     MOVE '1'       *IN25                           LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C           *IN25     IFEQ '0'                                       LLN021
     C           EADAC2    ANDNE0                                         LLN021
     C**********           MOVELEADAC2    @ADAC2  4                                    LLN021 LLN022
     C                     MOVELEADAC2    @ADAC2 10                                           LLN022
     C                     CALL 'AOACODR0'                                LLN021
     C                     PARM *BLANKS   P@RTCD                          LLN021
     C                     PARM '*KEY   ' P@OPTN                          LLN021
     C                     PARM @ADAC2    P@ACOD
     C           SDACOD    PARM SDACOD    DSSDY                           LLN021
      *                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVELEADAC2    DBKEY            ************   LLN021
     C                     MOVEL'SDCAODPD'DBFILE           * DBERR 05 *   LLN021
     C                     Z-ADD5         DBASE            ************   LLN021
     C                     EXSR #LDBER                                    LLN021
      *                                                                   LLN021
     C                     ELSE                                           LLN021
      *                                                                   LLN021
      * Check whether I/E account                                         LLN021
      *                                                                   LLN021
     C           A5ACSC    IFEQ 'IN'                                      LLN021
     C           A5ACSC    OREQ 'EX'                                      LLN021
     C                     MOVE '1'       *IN25                           LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
      *                                                                   LLN021
     C           *IN25     IFEQ '0'                                       LLN021
     C           EADAC3    ANDNE0                                         LLN021
     C**********           MOVELEADAC3    @ADAC3  4                                    LLN021 LLN022
     C                     MOVELEADAC3    @ADAC3 10                                           LLN022
     C                     CALL 'AOACODR0'                                LLN021
     C                     PARM *BLANKS   P@RTCD                          LLN021
     C                     PARM '*KEY   ' P@OPTN                          LLN021
     C                     PARM @ADAC3    P@ACOD                          LLN021
     C           SDACOD    PARM SDACOD    DSSDY                           LLN021
      *                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVELEADAC3    DBKEY            ************   LLN021
     C                     MOVEL'SDCAODPD'DBFILE           * DBERR 05 *   LLN021
     C                     Z-ADD5         DBASE            ************   LLN021
     C                     EXSR #LDBER                                    LLN021
      *                                                                   LLN021
     C                     ELSE                                           LLN021
      *                                                                   LLN021
      * Check whether I/E account                                         LLN021
      *                                                                   LLN021
     C           A5ACSC    IFEQ 'IN'                                      LLN021
     C           A5ACSC    OREQ 'EX'                                      LLN021
     C                     MOVE '1'       *IN25                           LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C           *IN25     IFEQ '0'                                       LLN021
     C           EACAC1    ANDNE0                                         LLN021
     C**********           MOVELEACAC1    @ACAC1  4                                    LLN021 LLN022
     C                     MOVELEACAC1    @ACAC1 10                                           LLN022
     C                     CALL 'AOACODR0'                                LLN021
     C                     PARM *BLANKS   P@RTCD                          LLN021
     C                     PARM '*KEY   ' P@OPTN                          LLN021
     C                     PARM @ACAC1    P@ACOD                          LLN021
     C           SDACOD    PARM SDACOD    DSSDY                           LLN021
      *                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVELEACAC1    DBKEY            ************   LLN021
     C                     MOVEL'SDCAODPD'DBFILE           * DBERR 05 *   LLN021
     C                     Z-ADD5         DBASE            ************   LLN021
     C                     EXSR #LDBER                                    LLN021
      *                                                                   LLN021
     C                     ELSE                                           LLN021
      *                                                                   LLN021
      * Check whether I/E account                                         LLN021
      *                                                                   LLN021
     C           A5ACSC    IFEQ 'IN'                                      LLN021
     C           A5ACSC    OREQ 'EX'                                      LLN021
     C                     MOVE '1'       *IN26                           LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C           *IN25     IFEQ '0'                                       LLN021
     C           *IN26     ANDEQ'0'                                       LLN021
     C           EACAC2    ANDNE0                                         LLN021
     C**********           MOVELEACAC2    @ACAC2  4                                    LLN021 LLN022
     C                     MOVELEACAC2    @ACAC2 10                                           LLN022
     C                     CALL 'AOACODR0'                                LLN021
     C                     PARM *BLANKS   P@RTCD                          LLN021
     C                     PARM '*KEY   ' P@OPTN                          LLN021
     C                     PARM @ACAC2    P@ACOD                          LLN021
     C           SDACOD    PARM SDACOD    DSSDY                           LLN021
      *                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVELEACAC2    DBKEY            ************   LLN021
     C                     MOVEL'SDCAODPD'DBFILE           * DBERR 05 *   LLN021
     C                     Z-ADD5         DBASE            ************   LLN021
     C                     EXSR #LDBER                                    LLN021
      *                                                                   LLN021
     C                     ELSE                                           LLN021
      *                                                                   LLN021
      * Check whether I/E account                                         LLN021
      *                                                                   LLN021
     C           A5ACSC    IFEQ 'IN'                                      LLN021
     C           A5ACSC    OREQ 'EX'                                      LLN021
     C                     MOVE '1'       *IN26                           LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C           *IN25     IFEQ '0'                                       LLN021
     C           *IN26     ANDEQ'0'                                       LLN021
     C           EACAC3    ANDNE0                                         LLN021
     C**********           MOVELEACAC3    @ACAC3  4                                    LLN021 LLN022
     C                     MOVELEACAC3    @ACAC3 10                                           LLN022
     C                     CALL 'AOACODR0'                                LLN021
     C                     PARM *BLANKS   P@RTCD                          LLN021
     C                     PARM '*KEY   ' P@OPTN                          LLN021
     C                     PARM @ACAC3    P@ACOD                          LLN021
     C           SDACOD    PARM SDACOD    DSSDY                           LLN021
      *                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVELEACAC3    DBKEY            ************   LLN021
     C                     MOVEL'SDCAODPD'DBFILE           * DBERR 05 *   LLN021
     C                     Z-ADD5         DBASE            ************   LLN021
     C                     EXSR #LDBER                                    LLN021
      *                                                                   LLN021
     C                     ELSE                                           LLN021
      *                                                                   LLN021
      * Check whether I/E account                                         LLN021
      *                                                                   LLN021
     C           A5ACSC    IFEQ 'IN'                                      LLN021
     C           A5ACSC    OREQ 'EX'                                      LLN021
     C                     MOVE '1'       *IN26                           LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C           *IN25     IFEQ *ON
     C           EVAM      IFLT 0                                         LLN021
     C*******************  Z-ADDEVAM      EVAM                            LLN021
     C                     Z-SUBEVAM      EVAM                            LLN021
     C                     Z-SUBBCEQ      BCEQ                            LLN021
     C                     END                                            LLN021
     C                     END                                            LLN021
      *                                                                   LLN021
      *
     C           *IN26     IFEQ *ON
     C********** EVAM      IFGT 0                                                             LLN021
     C********** EVAM      IFLT 0                                  LLN021 LLN021
     C                     Z-SUBEVAM      EVAM
     C                     Z-SUBBCEQ      BCEQ
     C**********           END                                            LLN021
     C                     END
      *                                                                   LLN021
     C                     ENDSR                                          LLN021
      *
      /EJECT                                                              LLN021
      *****************************************************************   LLN021
      *                                                               *   LLN021
      * #LOCCY - Convert to local currency using value date and       *   LLN021
      *          Historic Spot Rates File                             *   LLN021
      *                                                               *   LLN021
      * Called by:  #FORMT                                            *   LLN021
      *                                                               *   LLN021
      * Calls: None                                                   *   LLN021
      *                                                               *   LLN021
      *****************************************************************   LLN021
     C           #LOCCY    BEGSR                           ** #LOCCY SR** LLN021
      *                                                                   LLN021
     C                     Z-ADD0         @LAMT  150                      LLN021
     C                     CALL 'GL0765'                                  LLN021
     C                     PARM '*MSG   ' P@RTCD                          LLN021
     C                     PARM BCCY      P@CUR1  3                       LLN021
     C                     PARM BJLCCY    P@CUR2  3                       LLN021
     C                     PARM EVDT      P@DATE  50                      LLN021
     C           @LAMT     PARM BCEQ      P@AMT  190                      LLN021
     C*                                                                   LLN021
     C** If error in access program, exit via DBERR subroutine            LLN021
     C*                                                                   LLN021
     C           P@RTCD    IFNE *BLANKS                                   LLN021
     C                     MOVELBCEQ      DBKEY            ************   LLN021
     C                     MOVEL'GL0765  'DBFILE           * DBERR 06 *   LLN021
     C                     Z-ADD6         DBASE            ************   LLN021
     C                     EXSR #LDBER                                    LLN021
      *                                                                   LLN021
     C                     ENDIF                                          LLN021
      *                                                                   LLN021
     C                     ENDSR                                          LLN021
      *
      ********************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/EJECT
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2004
