     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas BoE Convert Amount Between Currencies')          *
      *****************************************************************
      *                                                               *
      *  Midas - Bank of England                                      *
      *                                                               *
      *  BY9070 - Convert Amount Between Currencies                   *
      *                                                               *
      *  Function:  This program converts an amount from one specified*
      *             currency to another.                              *
      *                                                               *
      *  Called By: Bank of England Extract Programs                  *
      *                                                               *
      *  (c) Finastra International Limited 1997                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. LLN022             Date 03Apr05               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN022 - BOE Upgrade to MidasPlus.                           *
      *                                                               *
      *****************************************************************
      *
      * File description for files defined
     FSDCURRL0IF  E           K        DISK         KINFSR *PSSR
     F                                              KINFDS INFDS1
      /EJECT
      *
      ** E-Specs for Currency Conversion Subroutine ZCCYCN
      /COPY ZSRSRC,ZPOWERZ1
      *
      *  Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      ** Array for Currency Codes
     E                    @CCY      200  3
      /SPACE 3
      ** Local data area for database error details
     ILDA       E DSLDA                         256
      *
      ** Data Structure using Bank file format
     ISDBANK    E DSSDBANKPD
      *
      ** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM DSPGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      * File information data structure (for database files only)
     IINFDS1    E DSY2I1DSP
      *
      * FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     IDSFDY     E DSDSFDY
      *
      * SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     IDSSDY     E DSDSSDY
      *
      *
     ISDCURR    E DSSDCURRPD                199
      *
      ** Trading Data ICD
     ISDTRAD    E DSSDTRADPD
      /EJECT
      *****************************************************************
      *                                                               *
      * Main Processing                                               *
      *                                                               *
      * Calls: SR/#LDETL - Detail Processing                          *
      *        SR/#LTERM - Termination Processing                     *
      *                                                               *
      *****************************************************************
      *
      *
      *  Detail Processing
     C                     EXSR #LDETL
      *
      *
      *  Termination Processing
     C                     EXSR #LTERM
      *
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * #LDETL - Detail Processing                                    *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:  SR/ZCCYCN                                             *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           #LDETL    BEGSR                                       **
      *
      ** Get index no. for Input Currency
     C                     Z-ADD1         #
     C           ULICCY    LOKUP@CCY,#                   90
      *
      ** End program if Input Currency not found
     C           *IN90     IFEQ *OFF
     C           *LOCK     IN   LDA
     C                     MOVELDSPGM     DBPGM            ************
     C                     MOVELULICCY    DBKEY            * DBERR 03 *
     C                     MOVEL'SDCURRPD'DBFILE           ************
     C                     Z-ADD3         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Get details for Input Currency
     C           #         OCUR SDCURR
      *
      ** Prepare Input Ccy fields for conversion
     C                     MOVE ULICCY    ZCCYF
     C                     MOVE A6MDIN    ZMDINF
     C                     Z-ADDA6SPRT    ZRATEF
     C                     Z-ADDA6NBDP    ZCDPF
     C                     Z-ADDULIAMT    ZAMTF
      *
      ** Get index no. for Output Currency
     C                     Z-ADD1         #
     C           ULOCCY    LOKUP@CCY,#                   90
      *
      ** End program if Output Currency not found
     C           *IN90     IFEQ *OFF
     C           *LOCK     IN   LDA
     C                     MOVELDSPGM     DBPGM            ************
     C                     MOVELULOCCY    DBKEY            * DBERR 04 *
     C                     MOVEL'SDCURRPD'DBFILE           ************
     C                     Z-ADD4         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Get details for Output Currency
     C           #         OCUR SDCURR
      *
      ** Prepare Output Ccy fields for conversion
     C                     MOVE ULOCCY    ZCCYT
     C                     MOVE A6MDIN    ZMDINT
     C                     Z-ADDA6SPRT    ZRATET
     C                     Z-ADDA6NBDP    ZCDPT
      *
      ** Perform Conversion
     C                     EXSR ZCCYCN
      *
      ** Move result into passed paramter field
     C                     Z-ADDZAMTT     ULOAMT
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LTERM - Termination Processing                               *
      *                                                               *
      * Called by:  Main Processing                                   *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           #LTERM    BEGSR                                       **
      *
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * #LDEFN - Field Definition Routine                             *
      *                                                               *
      * Called by:  None - Contains definitions only                  *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           #LDEFN    BEGSR                                       **
      *
      ** Define external data areas
     C           *NAMVAR   DEFN           LDA
      *
      ** Define and initialise work fields
     C                     MOVE *BLANK    ULRTCD  1
     C                     MOVE *BLANK    ULERRI  1                    dicator
     C                     MOVE *BLANK    P@RTCD  7
     C                     MOVE *BLANK    P@OPTN  7
     C                     Z-ADD0         #       30
     C*                    MOVE *BLANK    ULSTMD  1
     C*                    Z-ADD0         ULSTDP  10
     C*                    Z-ADD0         ULSTSR 138
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                                       **
      *
      *  Check if error has previously been reported
     C           ULERRI    IFEQ *BLANK
     C                     MOVE 'Y'       ULERRI
     C                     MOVE 'Y'       ULRTCD
     C                     DUMP
     C                     ENDIF
      *
      *  Set on external indicators
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      *  Terminate program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation Routine                       *
      *                                                               *
      * Called by:  At program initialisation                         *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR                                       **
      *
     C           *ENTRY    PLIST
     C                     PARM           ULIAMT 150
     C                     PARM           ULICCY  3
     C                     PARM           ULOAMT 150
     C                     PARM           ULOCCY  3
     C                     PARM           ULRTCD
      *
      ** Copyright message
     C                     MOVEACPY@      ULCPY   8
      *
      *  Get Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    P@RTCD  7
     C                     PARM '*FIRST ' P@OPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Check Return Code
     C           P@RTCD    IFNE *BLANKS
     C           BJTYLC    OREQ 'D'                        ON-DELETED
     C           *LOCK     IN   LDA
     C                     MOVELDSPGM     DBPGM            ************
     C                     MOVEL'*NONE'   DBKEY            * DBERR 01 *
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     Z-ADD1         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *  Get Trading Data ICD Record For GBP Currency Code (BLCYCD)
     C                     CALL 'AOTRADR0'
     C                     PARM *BLANK    P@RTCD
     C                     PARM '*FIRST ' P@OPTN
     C                     PARM           SDTRAD
     C           P@RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVELDSPGM     DBPGM            ************
     C                     MOVEL'*FIRST'  DBKEY            * DBERR 02 *
     C                     MOVEL'SDTRADPD'DBFILE           ************
     C                     Z-ADD2         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ELSE
     C                     MOVE *OFF      *IN98
     C                     END
      *
      ** Load Array and Data Structure with Currency Details
     C           *LOVAL    SETLLSDCURRL0
     C                     Z-ADD0         #
     C           *IN90     DOWEQ*OFF
     C           #         ANDLT200
     C                     ADD  1         #
     C           #         OCUR SDCURR
     C                     READ SDCURRL0                 90
     C           *IN90     IFEQ *OFF
     C                     MOVE A6CYCD    @CCY,#
     C                     ENDIF
     C                     ENDDO
      *
      *
     C                     ENDSR
      /EJECT
      ********************************************************************
      *
      ** Currency Conversion Subroutine
      /COPY ZSRSRC,ZCCYCN
      /COPY ZSRSRC,ZPOWERZ2
**  CPY@
(c) Finastra International Limited 1997
