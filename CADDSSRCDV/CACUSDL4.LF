      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM15                                                      *
/*EXI *  TEXT('Midas CA All DRS deals files des date/branch')         *
      *****************************************************************
      *                                                               *
      *  Midas - Common Applications                                  *
      *                                                               *
      *  CACUSDL4 - DRS CATCHALL DEALS FILE KEYED BY                  *
      *             DESCENDING DATE ORDER THEN BRANCH                 *
      *                                                               *
      *  (c) Finastra International Limited 2007                      *
      *                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CDL099             Date 06Oct17               *
      *                 CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CTI006             Date 31Mar14               *
      *                 CSW212             Date 03May12               *
      *                 CRE073             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 249976             Date 14Sep07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER050 - Annualised Percentage Rate (Recompile)              *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance Receive Settlement Instructions (Recompile) *
      *  CTI006 - Trade Finance BF Midas 2.2 Interface (Recompile)    *
      *  CSW212 - SWIFT 2012 Changes (Recompile)                      *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CER059 - German Feature Upgrade to Delhi(Recompile)          *
      *  249976 - Include complete deals in enquiry.                  *
      *****************************************************************
     A          R DEALSDBF                  PFILE(DEALSDB)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2007')
     A          K CNUM
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
     A          S RECI                      VALUES('M')
      *
     A          R DEALSDCF                  PFILE(DEALSDC)
     A          K CNUM
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
     A          S RECI                      VALUES('M')
     A            DTYP                      CMP(NE 'CI')
      *
     A          R DLSDCFCI                  PFILE(DEALSDC)
     A            RECI      R
     A            DLNO      R
     A            RCDT      R
     A            RCDC      R
     A            BRCA      R
     A            DTYP      R
     A            DLST      R
     A            DDAT      R
     A            VDAT      R
     A            MDAT      R
     A            CDAS      R
     A            BRKC      R
     A            BAGE      R
     A            CCY       R
     A            PCPL      R
     A            INTC      R
     A            ZZ006     R
     A            BRTT      R
     A            BRTE      R
     A            RTSP      R
     A            INTR      R
     A            NOTD      R
     A            RNNA      R
     A            CNUM      R               RENAME(CHNA)
     A            FACT      R
     A            FCNO      R
     A            OPAM      R
     A            BCXR      R
     A            LCXR      R
     A            SDST      R
     A            OSDA      R
     A            TSTN      R
     A            MDST      R
     A            OMDA      R
     A            TMAN      R
     A            RODN      R
     A            RODA      R
     A            RBDN      R
     A            RBDA      R
     A            ICBS      R
     A            IPFR      R
     A            TOTI      R
     A            NIDT      R
     A            IBDT      R
     A            SLID      R
     A            AIPD      R
     A            DLRC      R
     A            IACD      R
     A            AITC      R
     A            AIAN      R
     A            AIAP      R
     A            IPRD      R
     A            IPRM      R
     A            ICTD      R
     A            ZZ009     R
     A            DAVD      R
     A            DASN      R
     A            FACO      R
     A            SPI       R
     A            INTF      R
     A            ZZ008     R
     A            TAXI      R
     A            TAXD      R
     A            TAXM      R
     A            DNSI      R
     A            DSTI      R
     A            CNFI      R
     A            ACEI      R
     A            TLXI      R
     A            ZZ009A    R
     A            F57U      R
     A            LSWC      R
     A            LSWS      R
     A            ZZ002     R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            DBLN      R
     A            DRID      R
     A            DDMR      R
     A            BOKC      R
     A            LNKDN     R
     A            RSTM      R
     A            RONS      R
     A            RIBN      R
     A            RIBA      R
     A            ROBN      R
     A            ROCN      R
     A            PSTM      R
     A            PONS      R
     A            PIBN      R
     A            PIBA      R
     A            POBN      R
     A            POCN      R
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            ORBR      R
     A            PRFC      R
     A            ROBR      R
     A            POBR      R
     A            CVMR      R
     A            NACSG     R
     A            JRDFC     R
     A            JFLID     R
     A            FUID      R
     A          K CNUM
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
     A          S RECI                      VALUES('M')
     A            DTYP                      CMP(EQ 'CI')
      *
     A          R DEALSDDF                  PFILE(DEALSDD)
     A          K ISCN
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
     A          S RECI                      VALUES('M')
      *
     A          R DEALSDEF                  PFILE(DEALSDE)
     A          K SCUS
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
     A          S RECI                      VALUES('M')
      *
     A          R DEALSDGF                  PFILE(DEALSDG)
     A          K CNUM
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
     A          O RECI                      CMP(EQ '*')
      *
     A          R DLBHISD0                  PFILE(DLBHISPD)
     A          K CNUM
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
      *
     A          R DLCHISD0                  PFILE(DLCHISPD)
     A          K CNUM
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
      *
     A          R DLDHISD0                  PFILE(DLDHISPD)
     A          K ISCN
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
      *
     A          R DLEHISD0                  PFILE(DLEHISPD)
     A          K SCUS
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
      *
     A          R DLGHISD0                  PFILE(DLGHISPD)
     A          K CNUM
     A          K DDAT                      DESCEND
     A          K BRCA
     A          K DLNO
      *
     A          R FXDEALP0                  PFILE(FXDEALPP)
      *
     A          K FHDCNO
     A          K FHDDDN                    DESCEND
     A          K FHMBCA
     A          K FHDN38
     A          O FHDLTF                    CMP(EQ '*')
     A          O FHDSTS                    CMP(EQ 'M')
      *
     A          R MMDELDP0                  PFILE(MMDELDPP)
     A          K HKCNUM
     A          K HKDDND                    DESCEND
     A          K HKBRCA
     A          K HKDN38
     A          O HKDLTF                    CMP(EQ '*')
     A          O HKDSTS                    CMP(EQ 'M')
      *
     A          R MMDENBP0                  PFILE(MMDENBPP)
      *
     A          K HLISCN
     A          K HLDDND                    DESCEND
     A          K HLBRCA
     A          K HLDN38
     A          O HLDLTF                    CMP(EQ '*')
     A          O HLDSTS                    CMP(EQ 'M')
      *
     A          R MMDENSP0                  PFILE(MMDENSPP)
     A          K HMCNUM
     A          K HMDDND                    DESCEND
     A          K HMBRCA
     A          K HMDN38
     A          O HMDLTF                    CMP(EQ '*')
     A          O HMDSTS                    CMP(EQ 'M')
      *
