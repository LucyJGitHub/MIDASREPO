/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas CB Prepare Non-Working Day COB')                */
/*********************************************************************/
/*                                                                   */
/*       Midas - Close of Business Processing                        */
/*                                                                   */
/*       CBC000001 - Midas CB Prepare Non-Working Day COB            */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2012           */
/*                                                                   */
/*       Last Amend No. CCB021  *CREATE    Date 06Aug12              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CCB021 - COB Restructure - Non Working Day COB              */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RTGDTA)
 
             DCL        VAR(&RTGDTA)  TYPE(*CHAR) LEN(80)
             DCL        VAR(&MPHAS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&REPLY) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&CURUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(5)
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&POPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DSDBNK) TYPE(*CHAR) LEN(200)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems +
                          Ltd. 2012')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Display MIDAS information screen */
 
             RTVMSG     MSGID(SCM0012) MSGF(MIDASMSG) MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
             RTVMSG     MSGID(SCM0204) MSGF(MIDASMSG) MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (301 50)) VALUE(&MESSAGE)
             CALL       PGM(SCC0010) PARM('CBC000001' 'OPTION' +
                          &REPLY)
             IF         COND(&REPLY *EQ 'N') THEN(GOTO CMDLBL(END))
 
/* Retrieve the phase of day */
 
             RTVDTAARA  DTAARA(MPHAS (1 1)) RTNVAR(&MPHAS)
 
/* If the COB is already running */
 
             IF         COND((&MPHAS *NE 'A') *AND (&MPHAS *NE 'N')) +
                          THEN(DO)
                RTVMSG     MSGID(SCM0205) MSGF(MIDASMSG) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(SCM0206) MSGF(MIDASMSG) MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (301 50)) VALUE(&MESSAGE +
                             *BCAT &MPHAS)
                CALL       PGM(SCC0010) PARM('CBC000001' 'ENTER' ' ')
                GOTO       CMDLBL(END)
             ENDDO
 
             CHGVAR     VAR(&PRTCD) VALUE(*BLANKS)
             CHGVAR     VAR(&POPTN) VALUE('*FIRST ')
             CHGVAR     VAR(&DSDBNK) VALUE(*BLANKS)
             CALL       PGM(AOBANKR0) PARM(&PRTCD &POPTN &DSDBNK)
 
/* If the system is in input cycle */
 
             IF         COND(&MPHAS *EQ 'A') THEN(DO)
                RTVJOBA    USER(&CURUSER)
                CALL       PGM(CB000005) PARM(&CURUSER &USER)
                IF         COND(&USER *EQ '*NO  ') THEN(DO)
                   RTVMSG     MSGID(SFM0030) MSGF(MIDASMSG) +
                                MSG(&MESSAGE)
                   CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                   CALL       PGM(SCC0010) PARM('CBC000001' 'ENTER' +
                                ' ')
                   GOTO       CMDLBL(END)
                ENDDO
                ELSE DO
                   TFRJOB     JOBQ(MJOBQ) RTGDTA(NWDC)
                ENDDO
             ENDDO
 
/* If the system is in Non Working Day COB */
 
             IF         COND(&MPHAS *EQ 'N') THEN(DO)
                CALL       PGM(CBC000003)
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
             SNDPGMMSG  MSG('Prepare Non-Working Day COB +
                          Terminated Abnormally') +
                          TOPGMQ(*EXT) TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
             ENDPGM
