/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas CB Display job spooled files control program')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Close of Business Processing                        */
/*                                                                   */
/*       CBC0066 - Display job spooled files Control program         */
/*                                                                   */
/*       (c) Finastra International Limited 2003                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CAAA02 *CREATE     Date 20Nov03              */
/*                      xxxxxx             Date ddmmmyy              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CAAA02 - Created for displaying spooled files for a         */
/*                qualified job.                                     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&JOB &JOBUSER &JOBNBR &RTN)
 
             DCL        VAR(&JOB)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&RTN)     TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&RECURSIVE) TYPE(*CHAR) LEN(1) VALUE(N)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2003')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/* Clear output file. */
             DLTF       FILE(QTEMP/CBSPLFPD)
             MONMSG     MSGID(CPF2105)
             CRTDUPOBJ  OBJ(CBSPLFPD) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
 
/* Call program to retrieve inform about spooled files for the job specified. */
             OVRDBF     FILE(CBSPLFPD) TOFILE(QTEMP/CBSPLFPD) +
                          OVRSCOPE(*JOB)
             CALL       PGM(CB0066) PARM(&JOB &JOBUSER &JOBNBR)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             ELSE       CMD(DO)
/* If no error, call CB0066 to display spooled files for the job. */
 
/* Clear output file. */
                CALL       PGM(CB0067) PARM(&JOB &JOBUSER &JOBNBR)
                IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                   GOTO       CMDLBL(ABNOR)
                ENDDO
             ENDDO
 
             DLTOVR     FILE(CBSPLFPD) LVL(*JOB)
             GOTO       CMDLBL(ENDPGM)
 
ABNOR:
/* Abnormal termination */
             DLTOVR     FILE(CBSPLFPD) LVL(*JOB)
             MONMSG     MSGID(CPF9841)
             CHGVAR     VAR(&RTN) VALUE('ERROR')
             IF         COND(&RECURSIVE *EQ 'N') THEN(DO)
                CHGVAR     VAR(&RECURSIVE) VALUE(Y)
                RTVMSG     MSGID(UPM0001) MSGF(UTMSGF) +
                             MSGDTA('CBC0066') MSG(&MESSAGE)
                MONMSG     MSGID(CPF0000)
                CHGDTAARA  DTAARA(MIDASMSG (101 50)) VALUE(&MESSAGE)
                MONMSG     MSGID(CPF0000)
                CHGDTAARA  DTAARA(MIDASMSG (201 50)) VALUE('Check +
                             joblog and dump for details')
                MONMSG     MSGID(CPF0000)
                CALL       PGM(SCC0010) PARM('CBC0066' 'JOBLOG' 'Y')
                MONMSG     MSGID(CPF0000 MCH0000)
                CHGJOB     SWS(XXXXXX00)
             ENDDO
 
ENDPGM:
             ENDPGM
