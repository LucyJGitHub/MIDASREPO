      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas COB Scheduling maintenance/control screen')
      *****************************************************************
      *                                                               *
      *  Midas - System Control Module                                *
      *                                                               *
      *  DSPF/CB0004DF - COB Scheduling Maintenance/Control Screen    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG10848           Date 15Mar06               *
      *  Prev Amend No. CCB014  *Create    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG10848 - Schedule not functioning for Global Processing    *
      *  CCB014 - Pre-Scheduled Batch Close Of Business               *
      *                                                               *
      *****************************************************************
     A                                      CA03
     A                                      CF09
     A                                      CF10
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Screen 1 Format
      *
     A          R CB0004F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      OVERLAY
     A                                      PUTOVR
     A            SUSER         10      1  2
     A            SPGMN         10      1 13
     A                                  1 67'Date'
     A            SDATE          7      1 72
     A                                      OVRDTA
     A            SWJOB         10      2  2
     A                                  2 22'COB Scheduling Maintenance/Control'
     A                                      DSPATR(HI)
     A                                  2 67'Time'
     A            STIME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  5 02'Automatic Scheduling..:'
     A            #0AUSC         1A  B  5 26DSPATR(HI)
     A                                      OVRDTA
     A    N11                               DSPATR(UL)
     A     11                               DSPATR(RI PC)
      **Start of Change BUG10848
     A            #0ZONE        31   O  8  2
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A**********                        7 02'Scheduled Date .......:'
     A**********  #0SCDT         6A  B  7 26DSPATR(HI)
     A                                  9 02'Scheduled Date .......:'
     A            #0SCDT         6A  B  9 26DSPATR(HI)
      ** End of Change BUG10848
     A                                      OVRDTA
     A    N12                               DSPATR(UL)
     A     12                               DSPATR(RI PC)
      **Start of Change BUG10848
     A**********                        7 35'Time...:'
     A**********  #0SCTH         2A  B  7 44DSPATR(HI)
     A                                  9 37'Time...:'
     A            #0SCTH         2A  B  9 46DSPATR(HI)
      **End of Change BUG10848
     A                                      OVRDTA
     A    N13                               DSPATR(UL)
     A     13                               DSPATR(RI PC)
      **Start of Change BUG10848
     A**********  #0SCTM         2A  B  7 47DSPATR(HI)
     A            #0SCTM         2A  B  9 49DSPATR(HI)
      **End of Change BUG10848
     A                                      OVRDTA
     A    N14                               DSPATR(UL)
     A     14                               DSPATR(RI PC)
      **Start of Change BUG10848
     A**********                        7 50'hh:mm'
     A                                  9 54'hh:mm'
     A**********                        8 02'Warning Period .......:'
     A**********  #0SCWR         2A  B  8 26DSPATR(HI)
     A                                 10 02'Warning Period .......:'
     A            #0SCWR         2A  B 10 26DSPATR(HI)
      **End of Change BUG10848
     A                                      OVRDTA
     A    N15                               DSPATR(UL)
     A     15                               DSPATR(RI PC)
      **Start of Change BUG10848
     A**********                        8 29'mm'
     A                                 10 29'mm'
     A                                 12  2'Actual COB Schedule (Server)'
     A                                       DSPATR(HI UL)
     A                                 13  2'Server'
     A                                 13  9'Date'
     A                                 13 14'..........:'
     A            #0SDAT         7   O 13 26
     A                                      OVRDTA
     A                                 13 37'Time...:'
     A            #0STIM         6  0O 13 46
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                 14  2'Schedule'
     A                                 14 11'Date'
     A                                 14 16'........:'
     A            #0SCDA         7   O 14 26
     A                                      OVRDTA
     A                                 14 37'Time...:'
     A            #0SCTI         5   O 14 46
     A                                      OVRDTA
     A                                 14 54'hh:mm'
     A**********                       10 02'Actual Status :'
     A**********  #0SCS1        60A  O 10 18DSPATR(HI)
     A**********                            OVRDTA
     A**********  #0SCS2        60A  O 11 18DSPATR(HI)
     A**********                            OVRDTA
     A                                 16 02'Actual Status :'
     A            #0SCS1        60A  O 16 18DSPATR(HI)
     A                                      OVRDTA
     A            #0SCS2        60A  O 17 18DSPATR(HI)
     A                                      OVRDTA
      ** End of Change BUG10848
     A                                 22 02'F3=Exit  F9=(Re)Start/Change Sche+
     A                                       dule  F10=Stop Schedule'
      *
      ** Message Subfile Record Format
      *
     A          R CB0004S0                  TEXT('MESSAGE SUBFILE FORMAT')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Message Subfile Control Format
      *
     A          R CB0004C0
     A                                      SFLCTL(CB0004S0)
     A                                      OVERLAY
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  76
     AON76                                  SFLEND
     A            ##PGM                     SFLPGMQ
