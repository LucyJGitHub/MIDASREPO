      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB COB halt menu screen')
/*ADD *  CHGDSPF FILE(CB0020DF) WAITRCD(600)                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  CB0020DF  CLOSE OF BUSINESS HALT MENU SCREEN                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK025             Date 23Nov06               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 108076             DATE 13SEP96               *
     A*                 075277             DATE 23AUG94               *
     A*                 E39162             DATE 30MAY92               *
     A*                 E26775             DATE 31JUL91               *
     A*                 E26709             DATE 31JUL91               *
     A*                 LN0397             DATE 20JUN90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK025 - Revert back fix of CAAA02                           *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           Cause Web page to redisplay after the same number   *
      *           of seconds as the WAITRCD time for this DSPF.       *
     A*  108076 - Add an extra non display option for Halt menu       *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  E26775 - AMEND HELP TAGS TO REFERENCE THE MERGED SCINS.      *
     A*  E26709 - PLACE HELP TAG FOR 'REQUEST AN IMMEDIATE HALT TO    *
     A*           THE CLOSE OF BUSINESS RUN' ON THE SAME INDICATOR    *
     A*           AS THE OPTION.                                      *
     A*  LN0397 - ADDITION OF HELP TO COB SCREENS                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*
     A                                      DSPSIZ(24 80 *DS3)
      ** Start of CPK025
      ** Start of CAAA02
     A******************                    INVITE
     A                                      INVITE                                            CPK025
      ** End of CAAA02
      ** End of CPK025
     A*********              START OF CHANGE               /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                      END OF CHANGE                 /E39162/
     A*                                                        /LN0397/
     A*                                                        /LN0397/
     A                                      CA03(03 'F3=Exit')
     A          R CB0020F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
 
      ** Start of CPK025
      ** Start of CAAA02 (NOTE: The following lines ARE meaningful)
     A***********%%WB 6 FLD <!-- -->\r\n\r\n+
     A***********%%WB +<!-- Script to cause auto refresh -->\r\n\r\n+
     A***********%%WB +<script language="JavaScript">\r\n+
     A***********%%WB +\tsetTimeout("validateAndSubmit('ENTER')",600000);+
     A***********%%WB +\r\n</script>\r\n\r\n
      ** End of CAAA02
      ** End of CPK025
 
     A                                      PUTOVR
     A                                  1 13'CB0020'
     A                                      OVRATR
     A                                  1 30'HALT MENU'
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A            SCCURT         8A     2 71OVRDTA
     A            WSID          10A  O  2  2
     A                                      OVRDTA
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
     A          R CB0020F1
     A*
     A                                      TEXT('Selection Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A  65                                  CA06(06 'F6=Confirm')
     A  65                                  CA12(12 'F12=Previous')
     A*                                                    /LN0397/
      *                                                Begin /*E26709*/
      *                                                  End /*E26709*/
     A*                                                    /LN0397/
     A                                  6 21'Please select one of the following'
     A                                 15 21'Enter Selection'
     A            SELHLT         1   B 15 38
     A  60                                  DSPATR(RI)
     A  65                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 21'1. Request/Remove a Timed Halt'
     A                                 10 21'2. Request a Halt Before a Compone-
     A                                      nt'
     A                                 11 21'3. Request a Halt After a Componen-
     A                                      t '
     A                                 12 21'4. Request an Immediate Halt to th-
     A                                      e Close of Business Run'
     A N34                                  DSPATR(ND)
     A                                      OVRATR
      **                                                        /*Start 108076*/
     A                                 13 21'5. Halt for Modules Maintenance'
     A N35                                  DSPATR(ND)
     A                                      OVRATR
      **                                                          /*End 108076*/
     A                                 23  2'F3=Exit'
     A  60        ERRMSG        78A  O 22  2TEXT('Error Message Array')
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A          R CB0020F2
     A*
     A                                      TEXT('Format to clear message fld')
     A                                      OVERLAY
     A            CLRMSG        78A  O 22  2
