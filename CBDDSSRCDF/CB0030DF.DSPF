      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Request timed halt screen')
/*ADD *  CHGDSPF FILE(CB0030DF) WAITRCD(600)                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  CB0030DF  REQUEST TIMED HALT INPUT SCREEN                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK025             Date 23Nov06               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 075277             DATE 23AUG94               *
     A*                 E39162             DATE 30MAY92               *
     A*                 E26775             DATE 31JUL91               *
     A*                 LN0397             DATE 20JUN90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK025 - Revert back fix of CAAA02                           *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           Cause Web page to redisplay after the same number   *
      *           of seconds as the WAITRCD time for this DSPF.       *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  E26775 - AMEND HELP TAGS TO REFERENCE THE MERGED SCINS.      *
     A*  LN0397 - ADDITION OF HELP TO COB SCREENS                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
      ** Start of CPK025
      ** Start of CAAA02
     A******************                    INVITE
     A                                      INVITE                                            CPK025
      ** End of CAAA02
      ** End of CPK025
     A*                                                        /LN0397/
     A*                                                        /LN0397/
     A                                      CA03(03 'F3=Exit')
     A  60                                  CA06(06 'F6=Confirm')
     A  65                                  CA10(10 'F10=Delete')
     A*
     A          R CB0030F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      PUTOVR
 
      ** Start of CPK025
      ** Start of CAAA02 (NOTE: The following lines ARE meaningful)
     A***********%%WB 6 FLD <!-- -->\r\n\r\n+
     A***********%%WB +<!-- Script to cause auto refresh -->\r\n\r\n+
     A***********%%WB +<script language="JavaScript">\r\n+
     A***********%%WB +\tsetTimeout("validateAndSubmit('ENTER')",600000);+
     A***********%%WB +\r\n</script>\r\n\r\n
      ** End of CAAA02
      ** End of CPK025
 
     A                                  1 13'CB0030'
     A                                      OVRATR
     A                                  1 28'REQUEST TIMED HALT'
     A                                      DSPATR(HI UL)
     A                                      OVRATR
     A            SCCURT         8A     2 71OVRDTA
     A            WSID          10A  O  2  2
     A                                      OVRDTA
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
     A*
     A          R CB0030F1
     A*
     A                                      TEXT('Initial Screen Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  9 21'Enter time of halt'
     A                                      OVRATR
     A            SCTIM          4A  B  9 41TEXT('Halt time input')
     A  40                                  DSPATR(RI)
     A  60                                  DSPATR(PR)
     A                                      OVRDTA
     A  40
     AO 60        MSG           78A  O 22  2TEXT('Message Array')
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                 23  2'F3=Exit'
     A                                      OVRATR
     A  60                             23 11'F6=Confirm'
     A                                      OVRATR
     A*
     A          R CB0030F2
     A*
     A                                      TEXT('Initial Screen Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  7 21'Existing timed halt requested for'
     A                                      OVRATR
     A            SCHLIN         4A  O  7 55TEXT('Halt time already input')
     A                                      OVRATR
     A                                  9 21'Enter new time of halt if required'
     A                                      OVRATR
     A            SCTIM          4A  B  9 56TEXT('Halt time input')
     A  40                                  DSPATR(RI)
     A  60
     AO 65                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 18'or Enter `Y` to remove'
     A                                      OVRATR
     A                                 10 41'existing timed halt'
     A                                      OVRATR
     A            RMVHLT         1A  B 10 61TEXT('Remove halt time')
     A  41                                  DSPATR(RI)
     A  60
     AO 65                                  DSPATR(PR)
     A                                      OVRDTA
     A  40
     AO 41
     AO 60
     AO 65        MSG           78A  O 22  2TEXT('Message Array')
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                 23  2'F3=Exit'
     A                                      OVRATR
     A  60                             23 11'F6=Confirm'
     A                                      OVRATR
     A  65                             23 11'F10=Delete'
     A                                      OVRATR
