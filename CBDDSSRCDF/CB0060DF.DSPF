      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Close of business enquiry screen')
/*ADD *  CHGDSPF FILE(CB0060DF) WAITRCD(600)                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  CB0060DF  CLOSE OF BUSINESS ENQUIRY SCREEN                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPK025             Date 23Nov06               *
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 075277             DATE 23AUG94               *
     A*                 E39162             DATE 30MAY92               *
     A*                 E26775             DATE 31JUL91               *
     A*                 LN0397             DATE 20JUN90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CPK025 - Revert back fix of CAAA02                           *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           Cause Web page to redisplay after the same number   *
      *           of seconds as the WAITRCD time for this DSPF.       *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  E26775 - AMEND HELP TAGS TO REFERENCE THE MERGED SCINS.      *
     A*  LN0397 - ADDITION OF HELP TO COB SCREENS                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
      ** Start of CPK025
      ** Start of CAAA02
     A**31**************                    INVITE
     A  31                                  INVITE                                            CPK025
      ** End of CAAA02
      ** End of CPK025
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A*                                                        /LN0397/
     A*                                                        /LN0397/
     A                                      CA03(03 'F3=Exit')
     A*
     A          R CB0060F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
 
      ** Start of CPK025
      ** Start of CAAA02 (NOTE: The following lines ARE meaningful)
     A***********%%WB 6 FLD <!-- -->\r\n\r\n+
     A***********%%WB +<!-- Script to cause auto refresh -->\r\n\r\n+
     A***********%%WB +<script language="JavaScript">\r\n+
     A***********%%WB +\tsetTimeout("validateAndSubmit('ENTER')",600000);+
     A***********%%WB +\r\n</script>\r\n\r\n
      ** End of CAAA02
      ** End of CPK025
 
     A                                  1 13'CB0060'
     A                                  1 24'CLOSE OF BUSINESS ENQUIRY'
     A                                      DSPATR(HI UL)
     A                                      OVRATR
     A                                  2 71TIME
     A            WSID          10A  O  2  2
     A                                      OVRDTA
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
     A*
     A          R CB0060F1
     A                                      TEXT('Trailer Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
     A                                      OVRATR
     A                                 23 11'Rollup/Rolldown'
     A                                      OVRATR
     A*
     A          R CB0060S0                  SFL
     A                                      TEXT('Subfile Record Format')
     A            ENQMSG        78   O  4  2
     A*
     A          R CB0060C0                  SFLCTL(CB0060S0)
     A                                      TEXT('Subfile Control Format')
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N30N31                               SFLCLR
     A  60                                  SFLEND
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  50                                  SFLMSGID(CPF5203 QCPFMSG)
     A N60                                  ROLLUP(25)
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A          R CB0060F2
     A                                      TEXT('No Details Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 11 13'*** CLOSE OF BUSINESS'
     A                                      OVRATR
     A                                 11 35'PROCESSING HAS'
     A                                      OVRATR
     A                                 11 50'COMMENCED ***'
     A                                      OVRATR
     A                                 12 17'*** NO MESSAGES HAVE'
     A                                      OVRATR
     A                                 12 38'YET BEEN PRODUCED ***'
     A                                      OVRATR
