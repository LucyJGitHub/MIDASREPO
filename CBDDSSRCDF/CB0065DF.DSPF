      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Restart failed components screen')
/*ADD *  CHGDSPF FILE(CB0065DF) WAITRCD(600)                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  CB0065DF  RESTART FAILED COMPONENTS SCREEN                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPK025             Date 23Nov06               *
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 S01517             DATE 28NOV94               *
     A*                 075277             DATE 23AUG94               *
     A*                 E39162             DATE 30MAY92               *
     A*                 E26775             DATE 31JUL91               *
     A*                 LN0989             DATE 10DEC90               *   LN0989
     A*                 LN0397             DATE 20JUN90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CPK025 - Revert back fix of CAAA02                           *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           Cause Web page to redisplay after the same number   *
      *           of seconds as the WAITRCD time for this DSPF.       *
     A*  S01517 - Allow Component Bypass in COB                       *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  E26775 - AMEND HELP TAGS TO REFERENCE THE MERGED SCINS.      *
     A*  LN0989 - ALLOW ENQUIRE INTO JOB DETAILS                      *   LN0989
     A*  LN0397 - ADDITION OF HELP TO COB SCREENS                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
      ** Start of CPK025
      ** Start of CAAA02
     A**21**************                    INVITE
     A  21                                  INVITE                                            CPK025
      ** End of CAAA02
      ** End of CPK025
     A*                                                        /LN0397/
     A*                                                        /LN0397/
     A                                      CA03(03 'F3=Exit')
     A                                      CA05(05 'F5=Refresh')
     A          R CB0065F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      PUTOVR
 
      ** Start of CPK025
      ** Start of CAAA02 (NOTE: The following lines ARE meaningful)
     A***********%%WB 6 FLD <!-- -->\r\n\r\n+
     A***********%%WB +<!-- Script to cause auto refresh -->\r\n\r\n+
     A***********%%WB +<script language="JavaScript">\r\n+
     A***********%%WB +\tsetTimeout("validateAndSubmit('ENTER')",600000);+
     A***********%%WB +\r\n</script>\r\n\r\n
      ** End of CAAA02
      ** End of CPK025
 
     A                                  1 13'CB0065'
     A                                  1 25'RESTART FAILED COMPONENTS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                                  2 71TIME
     A            WSID          10A  O  2  2
     A                                      OVRDTA
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
     A          R CB0065S0                  SFL
     A*
     A                                      TEXT('Subfile Records Format')
     A  24                                  SFLNXTCHG
     A            SCCOT         10A  O  4 16TEXT('Component Name')
     A            RSTMSG        17A  O  4 31TEXT('Restarted msg/Comp Seq No')
     A            RESTRT         1A  B  4 53TEXT('Restarted input field')
     A  60                                  DSPATR(RI PC)
     A  65                                  DSPATR(PR ND)
     A            CHKINP         1  0H      TEXT('Hidden field to check input')
     A          R CB0065C0                  SFLCTL(CB0065S0)
     A*
     A                                      TEXT('Subfile Control Format')
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  70                                  SFLEND
     A*                                                               /*LN0989*/
     A***********                           SFLSIZ(0015)
     A***********                           SFLPAG(0014)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A*                                                               /*LN0989*/
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  3 14'Component Name'
     A                                      OVRATR
     A                                  3 35'Sequence No.'
     A                                      OVRATR
     A*                                                               /*LN0989*/
     A************                      3 50'Restart'
     A                                  3 50'Option'
     A*                                                               /*LN0989*/
     A                                      OVRATR
     A          R CB0065F1
     A*
     A                                      TEXT('Message format')
     A                                      OVERLAY
     A                                      PUTOVR
     A  42                             11 16'*** NO FAILED COMPONENTS'
     A                                      OVRATR
     A  42                             11 41'TO RESTART ***'
     A                                      OVRATR
     A          R CB0065F2
     A*
     A                                      TEXT('Message format')
     A                                      OVERLAY
     A                                      PUTOVR
     A*                                                               /*LN0989*/
     A**********                       20 20'Enter `Y` to restart component'
     A*                                              Begin /S01517/
     A***********                      22  2'R=Restart  E=Enquire'
     A                                 22  2'R=Restart  E=Enquire  B=Bypass'
     A*                                              End   /S01517/
     A*                                                               /*LN0989*/
     A  42                                  DSPATR(ND)
     A                                      OVRATR
     A*                                                               /*LN0989*/
     A**60********MSG           78A  O 22  2TEXT('Message Array')
     A  60        MSG           78A  O 24  2TEXT('Message Array')
     A*                                                               /*LN0989*/
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                 23  2'F3=Exit'
     A                                      OVRATR
     A                                 23 11'F5=Refresh'
     A                                      OVRATR
     A                                 23 23'Rollup/Rolldown'
     A                                      OVRATR
     A          R CB0065F3
     A*
     A                                      TEXT('Format to clear message fld')
     A                                      OVERLAY
     A            CLRMSG        78A  O 22  2
