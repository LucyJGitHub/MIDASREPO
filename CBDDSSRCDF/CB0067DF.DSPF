      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Job spooled files subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  DSPF/CB0067DF - Midas CB Job Spooled files subfile           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  NOTE: The lines beginning A*%%WB are webfacing related code  *
      *        and must NOT be deleted.                               *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA02  *CREATE    Date 11Nov03               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA02 - Created to list job spooled files                   *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *================================================================
     A          R #SFLRCE                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
      *
     A            #1SFSPL      150   H      TEXT('Splooled file')
     A*%%WB 19 FLD
     A            #1SFPDF      150   H      TEXT('Splooled PDF')
     A*%%WB 19 FLD
     A            #1SFPCL      150   H      TEXT('Splooled PCL')
     A*%%WB 19 FLD
     A            #1SPLF        10   O  8  2TEXT('Spooled File')
     A                                      DSPATR(HI)
     A            #1OUTQ        10   O  8 14TEXT('OUTQ name')
     A                                      DSPATR(HI)
     A            #1UDTA        10   O  8 26TEXT('User Data')
     A                                      DSPATR(HI)
     A            #1STAT        10   O  8 38TEXT('Status')
     A                                      DSPATR(HI)
     A            #1TPAG         4  0O  8 50TEXT('Total Pages')
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  8 62'Spool'
     A*%%WB 12 FLD 9 launchiSeriesforWeb(l<%=zOrder%>__ySFLCTE$_y1SFSPL$<%=rrn%+
     A*%%WB +>.value)
     A                                  8 69'PDF'
     A*%%WB 12 FLD 9 launchiSeriesforWeb(l<%=zOrder%>__ySFLCTE$_y1SFPDF$<%=rrn%+
     A*%%WB +>.value)
     A                                  8 74'PCL'
     A*%%WB 12 FLD 9 launchiSeriesforWeb(l<%=zOrder%>__ySFLCTE$_y1SFPCL$<%=rrn%+
     A*%%WB +>.value)
      *=========================================================================
     A          R #SFLCTE                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCE)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80
     AA 81                                  SFLDSP
     A  82                                  SFLEND(*MORE)
      * Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
      * SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
      *.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'CB0067F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  2 33'Job Spooled files'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  4'Job:'
     A            #2JOB         10   O  4 11TEXT('Job')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 26'User:'
     A            #2JUSR        10   O  4 34TEXT('Job User')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 49'Number:'
     A            #2JNBR         6   O  4 59TEXT('Job Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6 16'Device or'
     A                                  6 52'Total'
     A                                  7  4'File'
     A                                  7 16'Queue'
     A                                  7 28'User Data'
     A                                  7 40'Status'
     A                                  7 52'Pages'
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                      OVRATR
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
