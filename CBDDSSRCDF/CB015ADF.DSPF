      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Close of business monitor menu')              *
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  DSPF/CB015ADF - CLOSE OF BUSINESS MENU                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSC054             Date 23Feb12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSF002             Date 11Sep03               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 201864             Date 09Aug02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             DATE 08MAY02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 094273             DATE 31OCT95               *
      *                 090199             Date 07JUL95               *
      *                 S01420             Date 12APR95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC054 - Period End Adjustments                              *
      *  CSF002 - Global Processing                                   *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           INVITE is in this DSPF but is not used.             *
      *  201864 - Change the INVITE keyword from being a file level   *
      *           keyword to a record format level keyword of format  *
      *           CB0015F1.                                           *
      *  CPK014 - R4.01 Packaging:                                    *
      *           - The HOME key processing bypasses the R4 security  *
      *             and must be removed. This change should NOT be    *
      *             reintroduced.                                     *
      *  094273 - Add option to rejournal failed journalled files.    *
      *  090199 - Re-compilation to correct multi-language            *
      *           references.                                         *
      *  S01420 - CoB Enhancements                                    *
      *                                                               *
      *****************************************************************
     A**Start of change 201864
     A*********                             INVITE
     A**End of change 201864
     A                                      PRINT(MPRINT)
     A**********                            HOME(89)                                        /CPK015/
      *
     A          R CB0015F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
     A                                  1 13'CB0015'
     A                                      OVRATR
     A                                  1 29'MONITOR MENU'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            SCCURT         8A     2 71OVRDTA
     A            WSID          10A  O  2  2
     A                                      OVRDTA
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
      *
     A          R CB0015F1
     A**Start of change 201864
      ** Start of CAAA02
     A******************                    INVITE
      ** End of CAAA02
     A**End of change 201864
     A                                      TEXT('Selection Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A  65                                  CA06(06 'F6=Confirm')
     A  65                                  CA12(12 'F12=Previous')
      *                                     Start of change /094273/
      *                                       End of change /094273/
     A                                  4 21'1.  Halt close of business menu'
     A N55
     AON31                                  DSPATR(ND)
     A                                      OVRATR
     A                                  5 21'2.  Restart after halt'
     A N55
     AON33                                  DSPATR(ND)
     A                                      OVRATR
     A                                  6 21'3.  Investigate/restart'
     A N55
     AON32                                  DSPATR(ND)
     A                                      OVRATR
     A                                  6 45'failed components'
     A N55
     AON32                                  DSPATR(ND)
     A                                      OVRATR
     A                                  7 21'4.  Reopen input cycle'
     A N55
     AON36                                  DSPATR(ND)
     A                                      OVRATR
     A**Start of change CSF002
     A**********                        8 21'5.  Close of business reports menu'              CSF002
     A*N46******                            DSPATR(ND)                                        CSF002
     A**********                            OVRATR                                            CSF002
     A**End of change CSF002
      **Start of change CSC054
     A                                  8 21'5.  Period End Adjustments menu'
     A N55
     AON46                                  DSPATR(ND)
     A                                      OVRATR
      **End of change CSC054
     A                                  9 21'6.  Enter rundate'
     A N55
     AON47                                  DSPATR(ND)
     A                                      OVRATR
     A                                 10 21'7.  Save parameters maintenance'
     A N55
     AON48                                  DSPATR(ND)
     A                                      OVRATR
     A                                 11 21'8.  Close of business enquiry scre-
     A                                      en'
     A N34                                  DSPATR(ND)
     A                                      OVRATR
     A                                 12 21'9.  Return to close of'
     A N37                                  DSPATR(ND)
     A                                      OVRATR
     A                                 12 44'business main monitor'
     A N37                                  DSPATR(ND)
     A                                      OVRATR
     A                                 13 21'10. Command entry screen'
     A N55
     AON35                                  DSPATR(ND)
     A                                      OVRATR
     A                                 14 21'11. Work with active users'
     A N55
     AON51 52
     AON52 51
     AON52N51                               DSPATR(ND)
     A                                      OVRATR
     A                                 15 21'12. Work with active jobs'
     A N55
     AON51 52
     AON52 51
     AON52N51                               DSPATR(ND)
     A                                      OVRATR
      *                                     Start of change /094273/
     A                                 16 21'13. Display journal failure screen'
     A N39                                  DSPATR(ND)
     A                                      OVRATR
     A**********                       16 21'13. Sign off and leave'
     A                                 17 21'98. Sign off and leave'
      *                                       End of change /094273/
     A N51                                  DSPATR(ND)
     A                                      OVRATR
      *                                     Start of change /094273/
     A**********                       16 44'close of business running'
     A                                 17 44'close of business running'
      *                                       End of change /094273/
     A N51                                  DSPATR(ND)
     A                                      OVRATR
     A                                 18 21'99. Sign off and stop'
     A N55
     AON38                                  DSPATR(ND)
     A                                      OVRATR
     A                                 18 43'close of business processing'
     A N55
     AON38                                  DSPATR(ND)
     A                                      OVRATR
     A                                 20 21'Enter selection'
     A            SELECT         2   B 20 38
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A            ERRMSG        78A  O 22  2TEXT('Error Message Array')
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A          R CB0015F2
     A                                      TEXT('Format to clear message fld')
     A                                      OVERLAY
     A            CLRMSG        78A  O 22  2
