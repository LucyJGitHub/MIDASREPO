      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Dependency table report input')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*  CB0200DF  DEPENDENCY TABLE REPORT INPUT SCREEN               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 075277             DATE 23AUG94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  075277 - Correct copyright text (not annotated)              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*
     A*       FUNCTION OF INDICATORS
     A*
     A*       IND       USE
     A*
     A*       03      KEY F3
     A*       12      KEY F12
     A*       47      DISPLAY ERROR MESSAGE
     A*       48      NO FIELDS ENTERED ON FORMAT CB0200F1
     A*       49      INVALID DETAILS ENTERED ON FORMAT CB0200F2
     A*       50      INVALID DETAILS ENTERED ON FORMAT CB0200F3
     A*       51      INVALID DETAILS ENTERED ON FORMAT CB0200F3
     A*       52      INVALID DETAILS ENTERED ON FORMAT CB0200F2
     A*       60      INVALID DETAILS ENTERED ON FORMAT CB0200F1
     A*       61      INVALID DETAILS ENTERED ON FORMAT CB0200F1
     A*       62      INVALID DETAILS ENTERED ON FORMAT CB0200F1
     A*       63      INVALID DETAILS ENTERED ON FORMAT CB0200F1
     A*       64      INVALID FIELD ENTRY ON FORMAT CB0200F1
     A*       65      INVALID FIELD ENTRY ON FORMAT CB0200F1
     A*       66      INVALID FIELD ENTRY ON FORMAT CB0200F1
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03 'F3=Exit')
     A          R CB0200F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
     A*
     A                                  1 13'CB0200'
     A                                      OVRATR
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
     A                                  2 71TIME
     A            WSID          10A  O  2  2
     A                                      OVRDTA
     A                                  1 24'DEPENDENCY TABLE REPORT'
     A                                      DSPATR(HI UL)
     A                                      OVRATR
     A*
     A          R CB0200F1
     A*
     A                                      TEXT('Main Selection Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A                               O  5 28'Type of Report'
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                               O  8 14'Enter an `X` to select -
     A                                      required report'
     A                                      OVRATR
     A                               O 10 23'For a single component'
     A                                      OVRATR
     A            SCOMRP         1A  B 10 54
     A  48
     AO 60
     AO 61
     AO 63
     AO 64                                  DSPATR(RI)
     A N47
     AO 62
     AO 65
     AO 66                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                                 11 20'or For a single module'
     A            SMODRP         1A  B 11 54
     A  48
     AO 60
     AO 62
     AO 63
     AO 65                                  DSPATR(RI)
     A N47
     AO 61
     AO 64
     AO 66                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                               O 12 14'or enter `ALL` to -
     A                                      select all components'
     A                                      OVRATR
     A            SALLRP         3A  B 12 54
     A  48
     AO 61
     AO 62
     AO 63
     AO 66                                  DSPATR(RI)
     A N47
     AO 60
     AO 64
     AO 65                                  DSPATR(UL HI)
     A                                      OVRDTA
     A                               O 23  2'F3=Exit'
     A                                      OVRATR
     A  47        MSG           78   O 22  2TEXT('Error Message Array')
     A  47                                  DSPATR(RI)
     A N47                                  DSPATR(ND)
     A                                      OVRDTA
     A*
     A          R CB0200F2
     A*
     A                                      TEXT('Single Component Format')
     A                                      CA12(12 'F12=Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A                               O  5 22'Report for Single Component'
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                               O  8 19'Enter Component Name '
     A                                      OVRATR
     A                               O  9 25'Component Sequence Number '
     A                                      OVRATR
     A            SCCOT         10A  B  8 53
     A  49                                  DSPATR(RI)
     A N49                                  DSPATR(UL)
     A N49                                  DSPATR(HI)
     A                                      OVRDTA
     A                                      TEXT('Component Name entered')
     A            SCSEQ          5A  B  9 53
     A  52                                  DSPATR(RI)
     A N52                                  DSPATR(UL)
     A N52                                  DSPATR(HI)
     A                                      OVRDTA
     A                                      TEXT('Component Sequence Number ent-
     A                                      ered')
     A                               O 23  2'F3=Exit  F12=Previous'
     A                                      OVRATR
     A  47        MSG           78A  O 22  2TEXT('Error Message Array')
     A N47                                  DSPATR(ND)
     A  47                                  DSPATR(RI)
     A                                      OVRDTA
     A          R CB0200F3
     A*
     A                                      TEXT('Single Module Format')
     A                                      CA12(12 'F12=Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A                               O  5 24'Report for single module'
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                               O  8 24'Enter Module ID'
     A                                      OVRATR
     A            SCMOD          2A  B  8 41
     A  47N50
     AO 47 51                               DSPATR(RI)
     A N47N50
     AON47N51                               DSPATR(UL)
     A N47N50
     AON47N51                               DSPATR(HI)
     A                                      OVRDTA
     A                                      TEXT('Module ID entered')
     A                               O 23  2'F3=Exit  F12=Previous'
     A                                      OVRATR
     A  47        MSG           78A  O 22  2TEXT('Error Message Array')
     A N47                                  DSPATR(ND)
     A  47                                  DSPATR(RI)
     A                                      OVRDTA
     A*
     A          R CB0200F4
     A*
     A                                      TEXT('Format to clear message fld')
     A                                      OVERLAY
     A            CLRMSG        78A  O 22  2
