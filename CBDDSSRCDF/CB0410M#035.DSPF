      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Dependency relations maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Module                             *
      *                                                               *
      *  DSPF/CB0410M# - Dependency Relations maintenance.            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD058986           Date 11Oct21               *
      *  Prev Amend No. MD055891           Date 01Sep20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB008             Date 12Oct98               *
      *                 105705             Date 12Jul96               *
      *                 090416             Date 12JUL95               *
      *                 S01459             Date 12JUL95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058986 - Add Logical Delete for components and dependencies*
      *  MD055891 - Deliverable Data Split for COB                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB008 - Synon Closedown Project (CB):                       *
      *           - File removed from CB model.                       *
      *  105705 - Addition of standard hooks.                         *
      *  090416 - 1. Standardise the header/footer.                   *
      *           2. Remove 'components depends-on unique No' from    *
      *              the screen and generate it in the background.    *
      *           3. Addition of help.                                *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      * Start of change MD055891
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPTITLE('COBMENU - Help Text')
     A                                      HLPPNLGRP('COB2' COBMENU)
      * End of change MD055891
     A*=========================================================================
     A*/COPY WNCPYSRC,CB0410M#99
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,CB0410M#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A                                      INDTXT(34 'Invalid: #1DSON')
     A                                      INDTXT(35 'Invalid: #1DSOS')
     A                                      INDTXT(36 'Invalid: #1DDON')
     A                                      INDTXT(37 'Invalid: #1DDOS')
     A  84                                  SFLNXTCHG
      * Start of change MD055891
     A**********  #1DBRC        34   H      TEXT('Existing record fields')
      * Start of change MD058986
     A**********  #1DBRC        35   H      TEXT('Existing record fields')                  MD058986
     A            #1DBRC        36   H      TEXT('Existing record fields')
      * End of change MD058986
      * End of change MD055891
     A            #1DRUN         3   H      TEXT('Comp Depends-on Unique No')
     A            #1CDST         1   H      TEXT('Component Depended-on Sts')
     A            #1SEL          1   B  9  2
     A                                      VALUES('D' +
     A                                                 )
     A                                      CHECK(AB)
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A  78N33                               DSPATR(ND)
     A N78N33                               DSPATR(UL HI)
     A            #1DSON        10   B  9  7TEXT('Component Depends-on Name')
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1DSOS         5   B  9 22TEXT('Compnt Depends-on Seq No')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1DDON        10   B  9 41TEXT('Compnt Depended-on Name')
     A  77                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N77N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #1DDOS         5   B  9 56TEXT('Compnt Depended-on Seq No')
     A  77                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N77N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,CB0410M#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,CB0410M#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2DSON')
     A                                      SETOF(32 'Invalid: #2DSOS')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0410M1'
     A            ##AANA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 25'Dependency Relations Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2DRUN         3   H      TEXT('Comp Depends-on Unique No')
     A                                  5 13'Component                        -
     A                                      Depends on'
     A                                  6  5'    Name        Sequence         -
     A                                           Name        Sequence'
     A N89        #2DSON        10   B  7  7TEXT('Component Depends-on Name')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A N89        #2DSOS         5   B  7 22TEXT('Compnt Depends-on Seq No')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,CB0410M#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,CB0410M#94
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,CB0410M#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,CB0410M#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,CB0410M#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,CB0410M#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,CB0410M#89
