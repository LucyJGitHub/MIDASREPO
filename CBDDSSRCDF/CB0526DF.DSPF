     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB COB messages screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  DSPF/CB0526DF - SEND MESSAGES                                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. 090199             Date 07JUL95               *
     A*  Prev Amend No. S01420             Date 12APR95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  S01420 - CoB Enhancements                                    *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      VLDCMDKEY(60)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A          R CB0526F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'CB0526'
     A                                  1 33'Send a Message'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  7  3'Receiver. . . . . . . . . :'
     A            DDUSER        10A  O  7 33OVRATR
     A                                 12  3'Message . . . . . . . . . :'
     A            DDMESG        40A  B 12 33CHECK(LC)
     A N48N49                               DSPATR(UL)
     A  48                                  DSPATR(PR)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 14  3'Message Severity (H/L). . :'
     A            DDPRTY         1A  B 14 33DSPATR(HI)
     A N50N51                               DSPATR(UL)
     A  50                                  DSPATR(PR)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 16  3'Schedule Message(Y/N) . . :'
     A            DDSCHD         1   B 16 33DSPATR(HI)
     A N52N53                               DSPATR(UL)
     A  52                                  DSPATR(PR)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 16 38'Delay Time (in min) . . . :'
     A            DDTIME         3S 0B 16 67CHECK(FE)
     A N56N57                               DSPATR(UL)
     A  56                                  DSPATR(PR)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                 10  3'Message Type (I/L/S)  . . :'
     A            DDTYPE         1   B 10 33
     A N58N59                               DSPATR(UL)
     A  58                                  DSPATR(PR)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A*=========================================================================
     A          R CB0526F2
     A                                      TEXT('Command keys.')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'F3 = End  F12 = Previous'
     A                                 22 28'F10 = Send Message'
     A*=========================================================================
     A          R CB0526S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R CB0526C0                  TEXT('Program messages')
     A                                      SFLCTL(CB0526S0)
     A                                      SFLPAG(02) SFLSIZ(06)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
