      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Display active users')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  DSPF/CB0532DF - Display Active Users                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 090199             Date 07JUL95               *
      *  Prev Amend No. S01420             Date 12APR95               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
      *           REFERENCES.                                         *
      *                                                               *
      *  S01420 - CoB Enhancements                                    *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA07(07)
     A                                      CA08(08)
      *
     A          R CB0532S0                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(08 'Lock/Unlock')
     A                                      SETOF(98 'Force input format')
     A                                      SETOF(90 'QSNDDTAQ error')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(35 'Invalid DDSELC')
     A  84                                  SFLNXTCHG
     A                                 10  3'>'
     A N36                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            DDSELC         1A  B 10  6
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A            DDUSER        10A  O 10 10TEXT('User ID')
     A            DDJOB         10A  O 10 22TEXT('Job Name')
     A            DDNBR          6A  O 10 34TEXT('Job Number')
     A            DDDTAQ        10A  O 10 42TEXT('Job Data Queue')
     A            DDSTRT         6Y 0O 10 54TEXT('Job Start Time')
     A                                      EDTWRD('0 :  :  ')
     A            DDSSTS         7A  O 10 64TEXT('Job Status')
     A            DDCTRL         7A  O 10 72TEXT('Job Control')
     A
     A*
     A          R CB0532C0                  SFLCTL(CB0532S0)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error ind.')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            SFLRRN         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'CB0532'
     A                                  1 34'Active Users'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  2'Selection : '
     A                                  5  2'S=Signoff  L=Lock  U=Unlock  M=Mes-
     A                                      sage  C=Give Control  R=Remove Cont-
     A                                      rol'
     A                                  8 10'User ID'
     A                                      DSPATR(UL)
     A                                  8 22'Job Name'
     A                                      DSPATR(UL)
     A                                  8 42'Data Queue'
     A                                      DSPATR(UL)
     A                                  8 54'St. time'
     A                                      DSPATR(UL)
     A                                  8 64'Status'
     A                                      DSPATR(UL)
     A                                  8 34'Job Nr'
     A                                      DSPATR(UL)
     A                                  8  5'Sel'
     A                                      DSPATR(UL)
     A                                  8 72'    '
      *
     A          R CB0532T1
     A                                      TEXT('Command keys.')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'F3 = End  F5 = Refresh'
     A  51                             22 26'F7 = Display CoB log'
     A N50                             22 48'F8 = Lock all  '
     A  50                             22 48'F8 = Unlock all'
      *
     A          R CB0532S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R CB0532C1                  TEXT('Program messages')
     A                                      SFLCTL(CB0532S1)
     A                                      SFLPAG(02) SFLSIZ(06)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DDPGM                     SFLPGMQ
