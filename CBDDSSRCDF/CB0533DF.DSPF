      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Check lock and control')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  DSPF/CB0533DF - Check Lock and Control                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 118814             Date 03JUN97               *
      *  Prev Amend No. 090199             Date 07JUL95               *
      *                 S01420             Date 12APR95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  118814 - Helpdoc changed from CBINS to SCINS.                *
      *  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
      *           REFERENCES.                                         *
      *                                                               *
      *  S01420 - CoB Enhancements                                    *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *                                    Start of change /118814/
     A**************************************BAR2')
      *                                      End of change /118814/
      *
     A          R CB0533F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
      *                                    Start of change /118814/
     A**************************************BAR2')
      *                                      End of change /118814/
      *                                    Start of change /118814/
     A**************************************BAR2')
      *                                      End of change /118814/
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'CB0533'
     A                                  1 35'EXIT MENU'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 10  5'Remove Lock Status before leaving'
     A N50                                  DSPATR(ND)
     A                                      OVRATR
     A                                 10 40'(Y/N)'
     A N50                                  DSPATR(ND)
     A                                      OVRATR
     A            DDLOCK         1A  B 10 51TEXT('Lock Indicator')
     A  50                                  DSPATR(UL)
     A  50                                  DSPATR(HI)
     A N50                                  DSPATR(ND)
     A N50                                  DSPATR(PR)
     A                                      VALUES('Y' 'N')
     A                                      OVRATR
     A                                 16  5'Give Control to another user . . '
     A N51                                  DSPATR(ND)
     A                                      OVRATR
     A                                 16 40'(Y/N)'
     A N51                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCTRL         1A  B 16 51TEXT('Lock Indicator')
     A  51                                  DSPATR(UL)
     A  51                                  DSPATR(HI)
     A N51                                  DSPATR(ND)
     A N51                                  DSPATR(PR)
     A                                      VALUES('Y' 'N')
     A                                      OVRATR
     A                                  7  5'The Close of Business is currently-
     A                                       locked by you :'
     A N50                                  DSPATR(ND)
     A                                      OVRATR
     A                                 13  5'Other user(s) are still active. Yo-
     A                                      u are the only one to have the '
     A N51                                  DSPATR(ND)
     A                                      OVRATR
     A                                 14  5'Menu Control :'
     A N51                                  DSPATR(ND)
     A                                      OVRATR
