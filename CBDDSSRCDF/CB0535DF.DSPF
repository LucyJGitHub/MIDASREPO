      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Display COB trace')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  DSPF/CB0535DF - Display CoB Trace                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 23Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 090199             Date 07JUL95               *
      *                 S01420             Date 12APR95               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAAA03 - Webfacing Changes.                                  *
      *  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
      *           REFERENCES.                                         *
      *                                                               *
      *  S01420 - CoB Enhancements                                    *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R CB0535S0                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      SETOF(90 'QSNDDTAQ error')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(35 'Invalid DDSELC')
     A  84                                  SFLNXTCHG
     A            DDSIGN         1A  O  7  2
      ** Start of Change CAAA03
     A**********                            DSPATR(RI HI)                                     CAAA03
     A  83                                  DSPATR(RI)
     A                                      DSPATR(HI)
      ** End of Change CAAA03
     A N83                                  DSPATR(ND)
     A            DDLINE        75A  O  7  4TEXT('SUBFILE LINE')
     A N83                                  DSPATR(HI)
     A
      *
     A          R CB0535C0                  SFLCTL(CB0535S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error ind.')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            SFLRRN         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'CB0535'
     A                                  1 28'Close of business trace'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4 04'User'
     A                                      DSPATR(UL)
     A                                  4 16'Job Name'
     A                                      DSPATR(UL)
     A                                  4 28'Job Nbr'
     A                                      DSPATR(UL)
     A                                  4 37'Sys Date'
     A                                      DSPATR(UL)
     A                                  4 48'Sys Time'
     A                                      DSPATR(UL)
     A                                  4 59'Phase'
     A                                      DSPATR(UL)
     A                                  4 66'Sbs Date'
     A                                      DSPATR(UL)
     A                                  5  6'Action Taken'
     A                                      DSPATR(UL)
 
      *
     A          R CB0535T1
     A                                      TEXT('Command keys.')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'F3 = End  F5 = Refresh'
      *
     A          R CB0535S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(23)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R CB0535C1                  TEXT('Program messages')
     A                                      SFLCTL(CB0535S1)
     A                                      SFLPAG(02) SFLSIZ(06)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            DDPGM                     SFLPGMQ
