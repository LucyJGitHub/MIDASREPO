      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Display auto COB options')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*   DSPF/CB0600D# - Display Auto C.O.B. Opt. Display file       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*                 S01495             DATE 01OCT94               *
     A*                 075277             DATE 23AUG94               *
     A*                 S01375             DATE 22APR92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01495 - COB Enhancements.                                   *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  S01375 - Automatic Close of Business                         ****
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A*********              Start of Change               /S01495/
     A*********   #1NDSO         5   H      TEXT('Next Day Set Up Option')
     A*********   #1ICIN         5   H      TEXT('Input Cycle Initiation OP')
     A*********   #1PDPO        10   H      TEXT('Prompt Display Option')
     A*                      End of Change                 /S01495/
     A            #1SASP         2  0H      TEXT('Savefile ASP Option')
     A            #1SSAV         4   H      TEXT('Submit Save Savefile jobs')
     A            #1MSGS        10   H      TEXT('Input Cycle Msgq status')
     A            #1CSAV         4   H      TEXT('Clear Savefiles Option')
     A            #1MON          5   H      TEXT('DB SAVF name - Monday')
     A            #1TUE          5   H      TEXT('DB SAVF name - Tuesday')
     A            #1WED          5   H      TEXT('DB SAVF name - Wednesday')
     A            #1THR          5   H      TEXT('DB SAVF name - Thursday')
     A            #1FRI          5   H      TEXT('DB SAVF name - Friday')
     A            #1SAT          5   H      TEXT('DB SAVF name - Saturday')
     A            #1SUN          5   H      TEXT('DB SAVF name - Sunday')
     A            #1TSFO         5   H      TEXT('Term save of RCV to ?')
     A            #1NSSO         5   H      TEXT('NDSU save of DB to ?')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B  9  7
     A                                      VALUES('E' +
     A                                             'A')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1AUTO        10      9 10TEXT('Auto C.O.B. key')
     A                                      DSPATR(HI)
     A            #1CBNA        50      9 22TEXT('Auto C.O.B. Narrative')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A**********                            TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      HOME(30 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'CF09.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2AUTO')
     A                                      SETOF(32 'Invalid: #2CBNA')
     A*.........................................................................
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0600D1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 31'Display Auto C.O.B. Options'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  4'Position on C.O.B.'
     A            #2AUTO        10   B  4 25TEXT('Auto C.O.B. key')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  3'Select on Narrative'
     A            #2CBNA        50   B  5 25TEXT('Auto C.O.B. Narrative')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  81                              7  7'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 10'  C.O.B'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 37'C.O.B. Narrative'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 10'Definition'
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A**********                            TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 22  2'F3=Exit             F9=Add        -
     A                                         A=Amend               E=Enquire'
     A                                      DSPATR(HI) COLOR(BLU)
     A                                 23  2' '
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
