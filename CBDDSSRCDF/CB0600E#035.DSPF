      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Display auto COB details')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                     *
     A*                                                               *
     A*  DSPF/CB0600E# - Display Auto C.O.B. Det Display record (1 Sc *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 126248             Date 19NOV97               *
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*                 S01495             DATE 01OCT94               *
     A*                 075277             DATE 23AUG94               *
     A*                 S01375             DATE 22APR92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  126248  -  Screen should read "termination", not "termation" *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01495 - COB Enhancements.                                   *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  S01375 - Automatic Close of Business                         *
     A*                                                               *
     A*****************************************************************
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      HOME(30 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0600E1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 27'Display Auto C.O.B. Det KEY SCREEN-
     A                                      '
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Auto C.O.B. key :'
     A            #1AUTO        10      4 22TEXT('Auto C.O.B. key')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI) COLOR(BLU)
     A                                 23  2' '
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      HOME(30 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A*********              Start of Change               /S01495/
     A*                      End of Change                 /S01495/
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0600E2'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 31'Display Auto C.O.B. Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  9'Auto C.O.B. key'
     A            #1AUTO        10      4 28TEXT('Auto C.O.B. key')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  9'Narrative  . . :'
     A            #1CBNA        50      5 28TEXT('Auto C.O.B. Narrative')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*********              Start of Change               /S01495/
     A*********                         6  9'Phase Control'
     A*********                             DSPATR(UL)
     A*********                         6 28'Stop at start of Next Day Set Up  -
     A*********                             . :'
     A*********   #1NDSO         5      6 68TEXT('Next Day Set Up Option')
     A*N25*****                             OVRDTA
     A*********                             DSPATR(HI)
     A*********                         7 28'Stop at start of Input Cycle Initi-
     A*********                             ation'
     A*********   #1ICIN         5      7 68TEXT('Input Cycle Initiation OP')
     A*N25*****                             OVRDTA
     A*********                             DSPATR(HI)
     A*********                         8 28'Present C.O.B. prompts at . . . . -
     A*********                             . :'
     A*********   #1PDPO        10      8 68TEXT('Prompt Display Option')
     A*N25*****                             OVRDTA
     A*********                             DSPATR(HI)
     A                                  9  9'Phase Control'
     A                                      DSPATR(UL)
     A*                      End of Change                 /S01495/
     A*                      Start of Change               /126248/
     A*********                         9 28'Save of receivers in Termation to -
     A*********                             . :'
     A                                  9 28'Save of receivers in Termination t-
     A                                      o :'
     A*                      End of Change                 /126248/
     A            #1TSFO         5      9 68TEXT('Term save of RCV to ?')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10 28'Save of database in Next Day Set u-
     A                                      p to'
     A            #1NSSO         5     10 68TEXT('NDSU save of DB to ?')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11 28'Input Cycle Message Queue Status  -
     A                                      . :'
     A            #1MSGS        10     11 68TEXT('Input Cycle Msgq status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  9'Savefile Control'
     A                                      DSPATR(UL)
     A                                 12 28'Savefile User ASP . . . . :'
     A            #1SASP         2  0  12 58TEXT('Savefile ASP Option')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13 28'Clear Savefiles Option  . :'
     A            #1CSAV         4     13 58TEXT('Clear Savefiles Option')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14 28'Submit Save Savefile job  :'
     A            #1SSAV         4     14 58TEXT('Submit Save Savefile jobs')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16 28'Database Savefile Names'
     A                                      DSPATR(UL)
     A                                 17  9'Monday . . :'
     A            #1MON          5     17 24TEXT('DB SAVF name - Monday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17 30'Tuesday  . :'
     A            #1TUE          5     17 45TEXT('DB SAVF name - Tuesday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17 51'Wednesday  :'
     A            #1WED          5     17 66TEXT('DB SAVF name - Wednesday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  9'Thursday . :'
     A            #1THR          5     18 24TEXT('DB SAVF name - Thursday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18 30'Friday . . :'
     A            #1FRI          5     18 45TEXT('DB SAVF name - Friday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18 51'Saturday . :'
     A            #1SAT          5     18 66TEXT('DB SAVF name - Saturday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  9'Sunday . . :'
     A            #1SUN          5     19 24TEXT('DB SAVF name - Sunday')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'Last Change Information'
     A                                      DSPATR(UL)
     A                                 21  2'User name :'
     A            #1AUSR        10     21 16TEXT('User name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21 27'Action Date :'
     A            #1ARDT         7     21 43TEXT('Action Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21 51'Time :'
     A            #1ATIM         6  0  21 60TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI) COLOR(BLU)
     A                                 23  2' '
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
