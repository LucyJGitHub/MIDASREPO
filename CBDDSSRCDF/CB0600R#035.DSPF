      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Auto COB detail maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*   DSPF/CB0600R# - Edit Auto C.O.B. details Edit Record (1 Scre*
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 126248             Date 19NOV97               *
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*                 S01495             DATE 01OCT94               *
     A*                 075277             DATE 23AUG94               *
     A*                 S01375             DATE 22APR92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  126248  -  Screen should read "termination", not "termation" *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01495 - COB Enhancements.                                   *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  S01375 - Automatic Close of Business                         ****
     A*                                                               *
     A*****************************************************************
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      HOME(30 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1AUTO')
     A*.........................................................................
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0600R1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 32'Edit Auto C.O.B. details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Auto C.O.B. key .'
     A            #1AUTO        10   B  4 22TEXT('Auto C.O.B. key')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A**********                            TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI) COLOR(BLU)
     A                                 23  2' '
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A**********                            TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      HOME(30 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CBNA')
     A*********              Start of Change               /S01495/
     A*********                             SETOF(32 'Invalid: #1NDSO')
     A*********                             SETOF(33 'Invalid: #1ICIN')
     A*********                             SETOF(34 'Invalid: #1PDPO')
     A*                      End of Change                 /S01495/
     A                                      SETOF(35 'Invalid: #1TSFO')
     A                                      SETOF(36 'Invalid: #1NSSO')
     A                                      SETOF(37 'Invalid: #1MSGS')
     A                                      SETOF(38 'Invalid: #1SASP')
     A                                      SETOF(39 'Invalid: #1CSAV')
     A                                      SETOF(40 'Invalid: #1SSAV')
     A                                      SETOF(41 'Invalid: #1MON')
     A                                      SETOF(42 'Invalid: #1TUE')
     A                                      SETOF(43 'Invalid: #1WED')
     A                                      SETOF(44 'Invalid: #1THR')
     A                                      SETOF(45 'Invalid: #1FRI')
     A                                      SETOF(46 'Invalid: #1SAT')
     A                                      SETOF(47 'Invalid: #1SUN')
     A*.........................................................................
     A*********              Start of Change               /S01495/
     A*                      End of Change                 /S01495/
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0600R2'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 28'Edit Auto C.O.B. Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  9'Auto C.O.B. key'
     A            #1AUTO        10      4 28TEXT('Auto C.O.B. key')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  5  9'Narrative  . . .'
     A            #1CBNA        50   B  5 28TEXT('Auto C.O.B. Narrative')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*********              Start of Change               /S01495/
     A*********                         6  9'Phase Control'
     A                                  9  9'Phase Control'
     A                                      DSPATR(UL)
     A*********                         6 28'Stop at start of Next Day Set Up  -
     A*********                             . .'
     A*********   #1NDSO         5   B  6 68TEXT('Next Day Set Up Option')
     A*********                             CHECK(FE)
     A*N25*****                             OVRDTA
     A**32*****                             DSPATR(RI PC)
     A*N32*****                             DSPATR(UL HI)
     A*N25*****                             OVRATR
     A*********                         7 28'Stop at start of Input Cycle Initi-
     A*********                             ation'
     A*********   #1ICIN         5   B  7 68TEXT('Input Cycle Initiation OP')
     A*********                             CHECK(FE)
     A*N25*****                             OVRDTA
     A**33*****                             DSPATR(RI PC)
     A*N33*****                             DSPATR(UL HI)
     A*N25*****                             OVRATR
     A*********                         8 28'Present C.O.B. prompts at . . . . -
     A*********                             . .'
     A*********   #1PDPO        10   B  8 68TEXT('Prompt Display Option')
     A*********                             CHECK(LC FE)
     A*N25*****                             OVRDTA
     A**34*****                             DSPATR(RI PC)
     A*N34*****                             DSPATR(UL HI)
     A*N25*****                             OVRATR
     A*                      End of Change                 /S01495/
     A*                      Start of Change               /126248/
     A*********                         9 28'Save of receivers in Termation to -
     A*********                             . .'
     A                                  9 28'Save of receivers in Termination t-
     A                                      o :'
     A*                      End of Change                 /126248/
     A            #1TSFO         5   B  9 68TEXT('Term save of RCV to ?')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10 28'Save of database in Next Day Set u-
     A                                      p to'
     A            #1NSSO         5   B 10 68TEXT('NDSU save of DB to ?')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11 28'Input Cycle Message Queue Status  -
     A                                      . .'
     A            #1MSGS        10   B 11 68TEXT('Input Cycle Msgq status')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  9'Savefile Control'
     A                                      DSPATR(UL)
     A                                 12 28'Savefile User ASP . . . . .'
     A            #1SASP         2  0B 12 58TEXT('Savefile ASP Option')
     A                                      CHECK(RZ)
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13 28'Clear Savefiles Option  . .'
     A            #1CSAV         4   B 13 58TEXT('Clear Savefiles Option')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14 28'Submit Save Savefile job  .'
     A            #1SSAV         4   B 14 58TEXT('Submit Save Savefile jobs')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16 28'Database Savefile Names'
     A                                      DSPATR(UL)
     A                                 17  9'Monday . . .'
     A            #1MON          5   B 17 24TEXT('DB SAVF name - Monday')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 17 30'Tuesday  . .'
     A            #1TUE          5   B 17 45TEXT('DB SAVF name - Tuesday')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 17 51'Wednesday  .'
     A            #1WED          5   B 17 66TEXT('DB SAVF name - Wednesday')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18  9'Thursday . .'
     A            #1THR          5   B 18 24TEXT('DB SAVF name - Thursday')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18 30'Friday . . .'
     A            #1FRI          5   B 18 45TEXT('DB SAVF name - Friday')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18 51'Saturday . .'
     A            #1SAT          5   B 18 66TEXT('DB SAVF name - Saturday')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19  9'Sunday . . .'
     A            #1SUN          5   B 19 24TEXT('DB SAVF name - Sunday')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'Last Change Information'
     A                                      DSPATR(UL)
     A                                 21  2'User name :'
     A            #1AUSR        10     21 16TEXT('User name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21 27'Action Date :'
     A            #1ARDT         7     21 43TEXT('Action Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 21 51'Time :'
     A            #1ATIM         6  0  21 60TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A**********                            TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89                             22  2'F3=Exit         F12=Key Screen'
     A                                      DSPATR(HI) COLOR(BLU)
     A N89                             22  2'F3=Exit'
     A                                      DSPATR(HI) COLOR(BLU)
     A                                 23  2' '
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
