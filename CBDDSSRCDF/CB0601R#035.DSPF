      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Auto COB parameters maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*   DSPF/CB0601R# - Edit Auto C.O.B. Parameter  Edit Record (1s)*
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 090199             Date 12JUL95               *
     A*  PREV AMEND NO. S01495             DATE 01OCT94               *
     A*       AMEND NO. 075277             DATE 23AUG94               *
     A*                 S01375             DATE 22APR92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01495 - COB Enhancements.                                   *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  S01375 - Automatic Close of Business                         ****
     A*                                                               *
     A*****************************************************************
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      HOME(30 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1ACOB')
     A*.........................................................................
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0601R1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 29'Edit Auto C.O.B. ICD Key Screen'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Auto C.O.B. Control Key .'
     A            #1ACOB         6   B  4 30TEXT('Auto C.O.B. Control Key')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 22  2' '
     A                                      DSPATR(HI) COLOR(BLU)
     A  89                             23  2'F3=Exit   F9=Go to ''Change'' mode-
     A                                      '
     A                                      DSPATR(HI) COLOR(BLU)
     A N89                             23  2'F3=Exit   F9=Go to ''Add'' mode'
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      HOME(30 'Reset.')
     A                                      CF06(06 'CF06.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*********              Start of Change               /S01495/
     A*********                             SETOF(31 'Invalid: #1COBT')
     A*                      End of Change                 /S01495/
     A                                      SETOF(32 'Invalid: #1DAYF')
     A                                      SETOF(33 'Invalid: #1DYKY')
     A                                      SETOF(34 'Invalid: #1WKF')
     A                                      SETOF(35 'Invalid: #1WKKY')
     A                                      SETOF(36 'Invalid: #1MTHF')
     A                                      SETOF(37 'Invalid: #1MHKY')
     A                                      SETOF(38 'Invalid: #1YRF')
     A                                      SETOF(39 'Invalid: #1YRKY')
     A                                      SETOF(40 'Invalid: #1LDWK')
     A*.........................................................................
     A*********              Start of Change               /S01495/
     A*                      End of Change                 /S01495/
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CB0601R2'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 21'Edit Auto C.O.B. Parameter +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1ACOB         6   H      TEXT('Auto C.O.B. Control Key')
     A            #1CBRT        10   H      TEXT('COB Run type')
     A            #1CBRO         5   H      TEXT('COB Rcv save option')
     A            #1CBDO         5   H      TEXT('COB DB save option')
     A            #1CBCF         4   H      TEXT('COB Clear savefiles opt.')
     A            #1CBMD         7   H      TEXT('COB new run date')
     A            #1DBRN         5  0H      TEXT('COB new run day no.')
     A            #1CBDN         5   H      TEXT('COB DB savefile name')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A*********              Start of Change               /S01495/
     A*********                         5 11'Auto C.O.B. Indicator  . . . . .'
     A*********   #1COBT         4   B  5 46TEXT('Auto C.O.B. flag')
     A************                          CHECK(FE)
     A*N25*****                             OVRDTA
     A**31*****                             DSPATR(RI PC)
     A*N31*****                             DSPATR(UL HI)
     A*N25*****                             OVRATR
     A*                      End of Change                 /S01495/
     A                                  7  2'Processing Options'
     A                                      DSPATR(UL)
     A                                  9  3'  Freq.  '
     A                                      DSPATR(UL)
     A                                  9 14'Auto C.O.B.&Type'
     A                                      DSPATR(UL)
     A                                  9 31'                 Auto C.O.B. Descr-
     A                                      iption'
     A                                 10  3'Daily  .'
     A            #1DAYF         4   B 10 14TEXT('Auto Daily flag')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1DYKY        10   B 10 19TEXT('Auto Daily Type')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1CBNA        50     10 30TEXT('Auto C.O.B. Narrative')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  3'Weekend'
     A            #1WKF          4   B 11 14TEXT('Auto Weekend flag')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1WKKY        10   B 11 19TEXT('Auto Weekend Type Auto C.')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1WKNA        50     11 30TEXT('Auto Weekend Type Narr')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  3'Month End'
     A            #1MTHF         4   B 12 14TEXT('Auto Month End Flag')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1MHKY        10   B 12 19TEXT('Auto Month End Type Auto')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1MHNA        50     12 30TEXT('Auto Month End Type Narr')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  3'Year End'
     A            #1YRF          4   B 13 14TEXT('Auto Year End Flag')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1YRKY        10   B 13 19TEXT('Auto Year End Type Auto C')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #1YRNA        50     13 30TEXT('Auto Year End Type Narr')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15 11'Last Day of Week .'
     A            #1LDWK         3   B 15 32TEXT('Last Day of Week')
     A************                          CHECK(FE)
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 17 11'Last Change Information'
     A                                      DSPATR(UL)
     A                                 18 11'User name . :'
     A            #1AUSR        10     18 27TEXT('User name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19 11'Action Date :'
     A            #1ARDT         7     19 27TEXT('Action Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 11'Action Time :'
     A            #1ATIM         6  0  20 27TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A**********                            TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI) COLOR(BLU)
     A                                 23  2'F6=Edit Auto C.O.B. types'
     A                                      DSPATR(HI) COLOR(BLU)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
