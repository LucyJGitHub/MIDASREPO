      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PEA menu screen')                                *
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  CB0700DF - Close of Business PEA Menu Screen                 *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSC054  *CREATE    Date 23Feb12               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC054 - Period End Adjustments                              *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INVITE
     A                                      PRINT(*LIBL/MPRINT)
     A N65                                  CA03(03 'F3=Exit')
     A          R CB0700F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
      *
     A                                      PUTOVR
     A                                  1 13'CB0700'
     A                                      OVRATR
     A                                  1 27'PERIOD END ADJUSTMENTS MENU'
     A                                      DSPATR(UL HI)
     A                                      OVRATR
     A                                  2 71TIME
     A            WSID          10A  O  2  2
     A                                      OVRDTA
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
      *
     A          R CB0700F1
     A                                      TEXT('Selection Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A  65                                  CA06(06 'F6=Confirm')
     A  65                                  CA12(12 'F12=Previous')
     A                                  6 11'Please select one of the following'
     A                                 15 11'Enter Selection'
     A            SELECT         1A  B 15 28
     A  60                                  DSPATR(RI)
     A  65                                  DSPATR(PR)
     A                                      OVRDTA
     A N65                             23  2'F3=Exit'
     A  60        ERRMSG        78A  O 22  2TEXT('Error Message Array')
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  9 11'1. Re-open input cycle in the peri-
     A                                      od end system'
     A                                 10 11'2. Generate a report of all the tr-
     A                                      ansactions to be transferred'
     A                                 11 11'3. Transfer the transactions to th-
     A                                      e live system and close'
     A                                 12 14'the period end system'
     A                                 13 11'4. Clear the period end system and-
     A                                       prepare for the next period end'
     A          R CB0700F2
      *
     A                                      TEXT('Format to clear message fld')
     A                                      OVERLAY
     A            CLRMSG        78A  O 22  2
