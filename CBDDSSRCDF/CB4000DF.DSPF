      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB User Defined Events')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  DSPF/CB4000DF - Midas CB User Defined Events                 *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. CCB019  *CREATE    Date 21Sep22               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB019 - Close of Business User Defined Events               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      ERRSFL
      *
      ** --------------------------------------------------------------
      ** Header Format
      ** --------------------------------------------------------------
      *
     A          R CB4000F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
      *
     A                                      CF03
     A                                      CF12
     A                                 23  2'F3-Exit F9-Create New Event '
     A                                      DSPATR(HI)
     A          R CB4000F1
     A                                      CF03
     A                                      CF12
     A N91                                  CF10
     A                                      OVERLAY
     A  91                                  PROTECT
     A                                  1 32'Event Details'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A                                  4  2'Program (Library/Pgm Name):'
     A                                      DSPATR(HI)
     A            SLIBR         10A  B  4 31
     A  91                                  DSPATR(PR)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                  4 42'/'
     A            SNAME         10A  B  4 44
     A  91                                  DSPATR(PR)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A                                  6  2'Description . . . . . . . :'
     A                                      DSPATR(HI)
     A            SDESC         30A  B  6 31
     A  91                                  DSPATR(PR)
     A                                  8  2'COB or I/C  . . . . . . . :'
     A                                      DSPATR(HI)
     A            SCOBI          1A  B  8 31
     A  91                                  DSPATR(PR)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A                                  8 34'Anchor Point . . :'
     A                                      DSPATR(HI)
     A            SCLOC          1A  B  8 53
     A  91                                  DSPATR(PR)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A                                 10 34'I/C Run time . . :'
     A                                      DSPATR(HI)
     A            SITIM          6A  B 10 53
     A  91                                  DSPATR(PR)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A                                  8 55'Frequency :'
     A                                      DSPATR(HI)
     A            SFREQ          2A  B  8 68
     A  91                                  DSPATR(PR)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A                                  9 34'Ignore COB Error :'
     A                                      DSPATR(HI)
     A            SALER          1A  B  9 53
     A  91                                  DSPATR(PR)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A                                 11  3'Allow Immediate run. . . :'
     A                                      DSPATR(HI)
     A            SAIMD          1A  B 11 31
     A  91                                  DSPATR(PR)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A                                 13  3'Spool File . . . . . . . :'
     A                                      DSPATR(HI)
     A                                 13 31'Library/Output Queue'
     A                                      DSPATR(HI)
     A            SQULI         10A  B 13 53
     A  91                                  DSPATR(PR)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 13 64'/'
     A            SQUEU         10A  B 13 66
     A  91                                  DSPATR(PR)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 15  3'Copy file. . . . . . . . :'
     A                                      DSPATR(HI)
     A N67                             15 31'From Library/File'
     A N67                                  DSPATR(HI)
     A  67                             15 31'Library/Query Outfile'
     A  67                                  DSPATR(HI)
     A            SCFRL         10A  B 15 53
     A  91                                  DSPATR(PR)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                 15 64'/'
     A            SCFRF         10A  B 15 67
     A  91                                  DSPATR(PR)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 16 31'To Library/File'
     A                                      DSPATR(HI)
     A            SCTOL         10A  B 16 53
     A  91                                  DSPATR(PR)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 16 64'/'
     A            SCTOF         10A  B 16 67
     A  91                                  DSPATR(PR)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 17 31'Add/Replace. . . .'
     A                                      DSPATR(HI)
     A            SADDR          1A  B 17 53
     A  91                                  DSPATR(PR)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N67                             18  3'Parameters . . . . . . . :'
     A N67                                  DSPATR(HI)
     A  67                             18  3'Query Input Files. . . . :'
     A  67                                  DSPATR(HI)
     A            SPAR0         10A  B 18 31
     A  91                                  DSPATR(PR)
     A            SPAR1         10A  B 18 43
     A  91                                  DSPATR(PR)
     A            SPAR2         10A  B 19 31
     A  91                                  DSPATR(PR)
     A            SPAR3         10A  B 19 43
     A  91                                  DSPATR(PR)
     A            SPAR4         10A  B 20 31
     A  91                                  DSPATR(PR)
     A            SPAR5         10A  B 20 43
     A  91                                  DSPATR(PR)
     A            SPAR6         10A  B 21 31
     A  91                                  DSPATR(PR)
     A            SPAR7         10A  B 21 43
     A  91                                  DSPATR(PR)
     A            SPAR8         10A  B 22 31
     A  91                                  DSPATR(PR)
     A            SPAR9         10A  B 22 43
     A  91                                  DSPATR(PR)
     A                                 23  2'F3-Exit F12-Previous'
     A                                      DSPATR(HI)
     A                                  9  2'Invoke Pgm/Query. . . . . :'
     A                                      DSPATR(HI)
     A            SMQ            1A  B  9 31
     A  91                                  DSPATR(PR)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A                                  3  2'Sequence Number . . . . . .'
     A                                      DSPATR(HI)
     A                                 23 24'F10-Add Record'
     A                                      DSPATR(HI)
     A  91
     AO 70                                  DSPATR(ND)
     A            SSEQN          3A  B  3 31
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                 23 40'F10-Amend Record'
     A                                      DSPATR(HI)
     A N70                                  DSPATR(ND)
     A          R CB4000W0
     A                                      WINDOW(5 5 15 60)
     A                                      OVERLAY
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      RMVWDW
     A                                      USRRSTDSP
     A                                  1  1'Options Available'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  1'3'
     A                                      DSPATR(HI)
     A                                  3  5'- Copy Event (Midas Menu must be P-
     A                                      resent)'
     A                                  4  1'I'
     A                                      DSPATR(HI)
     A                                  4  5'- Ignore this Event'
     A                                  5  1'R'
     A                                      DSPATR(HI)
     A                                  5  5'- Reinstate ignored Event'
     A                                  6  1'E'
     A                                      DSPATR(HI)
     A                                  6  5'- Enquire on Event'
     A                                  7  1'A'
     A                                      DSPATR(HI)
     A                                  7  5'- Amend Event (Not available on LO-
     A                                      C options)'
     A                                  4 26'(Not available on LOC Options'
     A                                  8  1'C'
     A                                      DSPATR(HI)
     A                                  8  5'- Immediately Run Event (If Allowe-
     A                                      d)'
     A                                  9  1'V'
     A                                      DSPATR(HI)
     A                                  9  5'- View Associated Spool Files'
     A                                 10  1'Q'
     A                                      DSPATR(HI)
     A                                 10  5'- Review Last Extracted File if A-
     A                                      vailable'
     A                                 11  1'D'
     A                                      DSPATR(HI)
     A                                 11  5'- Delete Event (not Available on L-
     A                                      OC options)'
     A                                 14  2'Press Enter to Continue'
     A                                      DSPATR(HI)
     A          R CB4000W1
     A                                      CF12
     A                                      CF10
     A                                      OVERLAY
     A                                      WINDOW(8 5 5 50)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      RMVWDW
     A                                      USRRSTDSP
     A                                  1 18'Confirm Delete'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2  2'Deletion of Event...'
     A            SSEQN          3   O  3  2DSPATR(HI)
     A            SDESC         30   O  3 17DSPATR(HI)
     A            SNAME         10   O  3  6DSPATR(HI)
     A                                  4  2'F10-Confirm  F12-Cancel'
      *
      ** --------------------------------------------------------------
      ** Subfile record and subfile control
      ** --------------------------------------------------------------
      *
     A          R CB4000S0                  SFL
     A            SIGNO          1   H
     A            S0OPT          1A  B  7  2
     A                                      VALUES('3' +
     A                                             'I' +
     A                                             'R' +
     A                                             'E' +
     A                                             'A' +
     A                                             'C' +
     A                                             'V' +
     A                                             'Q' +
     A                                             'D' +
     A                                             ' ' )
     A            SNAME         10   O  7  9
     A N92                                  DSPATR(HI)
     A            SDESC         30   O  7 20
     A N92                                  DSPATR(HI)
     A            SCOBI          1   O  7 51
     A N92                                  DSPATR(HI)
     A            SWHEN          6   O  7 54
     A N92                                  DSPATR(HI)
     A            SLOCK          1   O  7 62
     A N92                                  DSPATR(HI)
     A            SSEQN          3   O  7  5
     A N92                                  DSPATR(HI)
     A            SSUCC          1   O  7 66
     A N92                                  DSPATR(HI)
     A            SLRTI          6   O  7 68
     A N92                                  DSPATR(HI)
     A            SLRDT          6   O  7 75
     A N92                                  DSPATR(HI)
      *
      ** Subfile Control
      *
     A          R CB4000C0                  SFLCTL(CB4000S0)
     A                                      ALTNAME('y')
     A                                      TEXT('y')
     A                                      CF09
     A                                      CF03
     A                                      CF10
     A                                      CF04
     A                                      OVERLAY
     A  80                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  82                                  SFLDLT
     A  99                                  SFLEND(*MORE)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A            S0RLRN         4S 0H      SFLRCDNBR
     A                                  1 31'User Defined Events'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A                                  6  2'Op Seq Name       Description     -
     A                                                    Cob When   LOC  OK  T-
     A                                      ime Date '
     A                                  5 50'I/C'
     A                                  5 66'Last Run'
     A                                  4  2'Press F4 to list available options'
     A                                      DSPATR(UL)
      *
      ** --------------------------------------------------------------
      ** Message subfile - record and control
      ** --------------------------------------------------------------
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      ** Subfile Message Control
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
