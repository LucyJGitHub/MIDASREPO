      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Midas information screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                     *
     A*                                                               *
     A*  CBC0700DF - MIDAS INFORMATION SCREEN                         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 075277             Date 23Aug94               *
      *                 071613             Date 06Jun94               *
     A*                 E31647             DATE 16SEP92               *
     A*                 E39162             DATE 30MAY92               *
     A*                 E27476             DATE 04SEP91               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  071613 - Remove 'BIS' references.                            *
     A*  E31647 - EOD prompt screen bypassed when enter key is        *
     A*           pressed by accident. - Help facility added for      *
     A*           F12 processing.                                     *
     A*   E39162 - Change 'PRINT' to 'PRINT(MPRINT)'.                 *
     A*   E27476  -  Rewrite CB Midas Information Screen to use       *
     A*              MSGF/(GB)MIDASMSG.                               *
     A*                                                               *
     A*****************************************************************
      *
      *
      ******************************************
      * FORMAT     ¦ USED BY PGM ¦ INDICATORS  *
      ******************************************
      * BLANK      ¦             ¦             *
      *========================================*
      * ENTER      ¦             ¦             *
      *========================================*
      * JOBLOG     ¦ CBC1701     ¦    NONE     *
      *            ¦ CBC1702     ¦    NONE     *
      *========================================*
      * OPTION     ¦             ¦             *
      *========================================*
      * RETRY      ¦ CBC1702     ¦      1      *
      *            ¦ CBC1701     ¦      2      *
      ******************************************
      *
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      KEEP
     A            USER          10   O  1  2
     A                                  1 13'CBC0700'
      *                                    Start of change /071613/
     A**********                        1 26'B I S  ¦  M i d a s  ¦  A B S'
     A**********                            DSPATR(HI)
      *                                      End of change /071613/
     A            DATE           7   O  1 72
     A            WSID          10   O  2  2
      *                                    Start of change /071613/
     A**********                        2 26'M i d a s  Information Screen'
     A                                  2 29'Midas Information Screen'
      *                                      End of change /071613/
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A                                  4  2'From:'
     A            FPGM          10   O  4  8
      *
     A          R MIDASMSG
     A                                      OVERLAY
     A                                      KEEP
     A            LINE1         50      7 16
     A            LINE2         50      8 16
     A            LINE3         50      9 16
     A            LINE4         50     10 16
     A            LINE5         50     11 16
     A            LINE6         50     12 16
     A            LINE7         50     13 16
     A            LINE8         50     14 16
     A            LINE9         50     15 16
     A            LINE10        50     16 16
      *
     A          R TRAILER                   TEXT('COPYRIGHT STATEMENT')
     A                                      OVERLAY
     A                                      KEEP
     A                                 24  2'(c) Finastra International +
     A                                      Limited +
     A                                      2001'
      *
     A          R BLANK
     A                                      OVERLAY
     A                                      KEEP
     A                                 18 03' '
      *
     A          R ENTER
     A                                      OVERLAY
     A                                      KEEP
     A                                 23  2'Press enter to continue.'
      *
     A          R JOBLOG
     A                                      OVERLAY
     A                                      KEEP
     A                                 18 19'Do you wish to display the +
     A                                      joblog? (Y/N)'
     A            REPLY1         1   B 18 61
     A                                      CHECK(ME)
     A                                      VALUES('Y' 'N')
      *
     A          R OPTION
     A                                      OVERLAY
     A                                      KEEP
      *                   START OF CHANGE                  /E31647/
     A*                   END OF CHANGE                    /E31647/
      *                                                    Start S01345
     A                                 18 22'Please enter required option (Y/N)'
     A            REPLY1         1   B 18 58
     A                                      CHECK(ME)
     A                                      VALUES('Y' 'N')
      *
     A          R RETRY
     A                                      OVERLAY
     A                                      KEEP
     A                                 18 25'Do you wish to re-try? (Y/N)'
     A            REPLY1         1   B 18 55
     A                                      CHECK(ME)
     A                                      VALUES('Y' 'N')
