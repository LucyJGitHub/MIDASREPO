      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB COB request halts screen')
/*ADD *  CHGDSPF FILE(CBHALTDF) WAITRCD(600)                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  CBHALTDF  REQUEST HALTS INPUT SCREENS                        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CPK025             Date 23Nov06               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 075277             DATE 23AUG94               *
     A*                 E39162             DATE 30MAY92               *
     A*                 E26775             DATE 31JUL91               *
     A*                 LN0397             DATE 20JUN90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPK025 - Revert back fix of CAAA02                           *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           Cause Web page to redisplay after the same number   *
      *           of seconds as the WAITRCD time for this DSPF.       *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  E26775 - AMEND HELP TAGS TO REFERENCE THE MERGED SCINS.      *
     A*  LN0397 - ADDITION OF HELP TO COB SCREENS                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A                                      DSPSIZ(24 80 *DS3)
     A*********              START OF CHANGE               /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                      END OF CHANGE                 /E39162/
      ** Start of CPK025
      ** Start of CAAA02
     A******************                    INVITE
     A                                      INVITE                                            CPK025
      ** End of CAAA02
      ** End of CPK025
     A*                                                        /LN0397/
     A*                                                        /LN0397/
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03(03 'F3=Exit')
     A  65                                  CA06(06 'F6=Confirm')
     A  65                                  CA12(12 'F12=Previous')
     A*
     A          R CBHALTF0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
 
      ** Start of CPK025
      ** Start of CAAA02 (NOTE: The following lines ARE meaningful)
     A***********%%WB 6 FLD <!-- -->\r\n\r\n+
     A***********%%WB +<!-- Script to cause auto refresh -->\r\n\r\n+
     A***********%%WB +<script language="JavaScript">\r\n+
     A***********%%WB +\tsetTimeout("validateAndSubmit('ENTER')",600000);+
     A***********%%WB +\r\n</script>\r\n\r\n
      ** End of CAAA02
      ** End of CPK025
 
     A                                      PUTOVR
     A            PROGNO         6A  O  1 13TEXT('Program reference')
     A                                      OVRDTA
     A            TITLE         29A  O  1 22TEXT('Screen title')
     A                                      OVRDTA
     A                                      DSPATR(HI UL)
     A            SCCURT         8A     2 71TEXT('Screen time')
     A                                      OVRDTA
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A                                      OVRDTA
     A            WSID          10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
     A*
     A          R CBHALTF1
     A*
     A                                      TEXT('Input Details Format')
     A                                      OVERLAY
     A                                      PUTOVR
     A*                                                        /LN0397/
     A*                                                        /LN0397/
     A                                  9 13'Component Name'
     A                                      OVRATR
     A            SCCOT         10A  B  9 28TEXT('Component name entered')
     A  60                                  DSPATR(RI)
     A  65                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 43'Component Sequence No.'
     A                                      OVRATR
     A            SCSEQ          5A  B  9 66TEXT('Component number entered')
     A  55
     AO 60                                  DSPATR(RI)
     A  65                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 13'Text'
     A  45
     AON50                                  DSPATR(ND)
     A                                      OVRATR
     A N45
     AON55        DHCTXT        60   O 10 18TEXT('Component Text')
     A                                      OVRDTA
     A  55
     AO 60
     AO 65        MSG           78A  O 22  2TEXT('Message Array')
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                 23  2'F3=Exit'
     A                                 23 11'F6=Confirm'
     A N65                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 23'F12=Previous'
     A N65                                  DSPATR(ND)
     A                                      OVRATR
     A          R CBHALTF2
     A*
     A                                      TEXT('Format to clear message fld')
     A                                      OVERLAY
     A            CLRMSG        78A  O 22  2
