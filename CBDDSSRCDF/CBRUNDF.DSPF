      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CB Multi-streamed COB controller')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*  CBRUNDF  MULTI-STREAMED CLOSE OF BUSINESS                    *
     A*           CONTROLLER SCREEN                                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             Date 12Jul95               *
     A*                 075277             DATE 23AUG94               *
     A*                 E26775             DATE 31JUL91               *
     A*                 LN0397             DATE 20JUN90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  075277 - Correct copyright text (not annotated)              *
     A*  E26775 - AMEND HELP TAGS TO REFERENCE THE MERGED SCINS.      *
     A*  LN0397 - ADDITION OF HELP TO COB SCREENS                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*                                                        /LN0397/
     A*                                                        /LN0397/
     A          R CBRUNF0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                                    /LN0397/
     A*                                                    /LN0397/
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                  1 13'CBRUN'
     A                                  1 22'REQUEST TYPE OF AUDIT REPORT FOR A-
     A                                      ND HALTS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A            JOB           10A  O  2  2TEXT('Job name')
     A                                  2 28'TO CLOSE OF BUSINESS PROCESS-
     A                                      ING'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 71TIME
     A                                  7 12'Enter ''Y'' to produce either or b-
     A                                      oth types of Audit'
     A                                  8 12'Report during today''s Close of Bu-
     A                                      siness processing'
     A                                  9 24'For a report by time'
     A            AUDTIM         1A  B  9 47TEXT('Audit Report by time')
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A                                 10 24'For a report by module'
     A            AUDMOD         1A  B 10 47TEXT('Audit Report by module')
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                 14 12'Enter ''Y'' to display the menu to-
     A                                       request halts'
     A                                 15 12'during today''s Close of Business-
     A                                       processing'
     A            AUDHAL         1A  B 15 61TEXT('Halt requested indicator')
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A                                 22  2'Invalid entry - enter ''Y'' for re-
     A                                      quired option                      -
     A                                                '
     A  50
     AO 51
     AO 52                                  DSPATR(RI)
     A N50N51N52                            DSPATR(ND)
     A                                      TEXT('Highlighted Error message')
     A                                 23  6'Press ENTER to continue'
     A          R CBRUNF1
     A                                      TEXT('Restarted screen')
     A                                      FRCDTA
      ** Start of CAAA02
     A******************                    UNLOCK
      ** End   of CAAA02
     A                                      KEEP
     A                                  1 25'Midas Daily Processing    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 71TIME
     A            JOB           10A  O  2  2TEXT('Job name')
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A            USRID         10A  O  1  2TEXT('User ID')
     A                                  1 13'CBRUN'
     A                                 11 20'Close of Business'
     A                                 11 38'processing restarted'
     A                                 24  2'(c) Finastra International +
     A                                      Limited +
     A                                      2001'
     A          R CBRUNF2                   TEXT('Started screen')
     A                                      FRCDTA
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A                                      KEEP
     A*
     A                                  1 25'Midas Daily Processing    '
     A                                      DSPATR(HI UL)
     A                                  2 71TIME
     A            JOB           10A  O  2  2TEXT('Job name')
     A            RUNA           7A  O  1 72TEXT('Run date, DDMMMYY')
     A            USRID         10A  O  1  2TEXT('User ID')
     A                               O  1 13'CBRUN'
     A*
     A                                 11 20'Set up of Close of Business'
     A                                 11 48'processing started'
     A                                 24  2'(c) Finastra International +
     A                                      Limited +
     A                                      2001'
     A                                      DSPATR(HI)
