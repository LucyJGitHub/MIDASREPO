      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas CB Copy of components physical file')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Module                             *
      *                                                               *
      *  PF/CHKCMPPD - COB Components Master Check File               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD058986           Date 11Oct21               *
      *  Prev Amend No. MD055891           Date 01Sep20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB003             Date 02Oct96               *
      *                 S01491             Date 05Oct94               *
      *                 S01459             DATE 05OCT94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058986 - Add Logical Delete for components and dependencies*
      *  MD055891 - Deliverable Data Split for COB                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB003 - Perf.Enh. Two new fields added.                     *
      *  S01491 - Add new fields for Autocall Project.                *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A          R CHKCMPD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            DHCOTT        10          TEXT('Component Name')
     A                                      COLHDG('Component' +
     A                                             'Name')
     A            DHCSEQ         5          TEXT('Component Sequence Number')
     A                                      COLHDG('Component' +
     A                                             'Sequence Number')
     A            DHCSTS         1          TEXT('Component Status')
     A                                      COLHDG('Component' +
     A                                             'Status')
     A                                      DFT(' ')
     A            DHCSHP         1S 0       TEXT('Scheduling Priority')
     A                                      COLHDG('Scheduling' +
     A                                             'Priority')
     A                                      EDTCDE(4)
     A            DHCEPY         2S 0       TEXT('Execution Priority')
     A                                      COLHDG('Execution' +
     A                                             'Priority')
     A                                      EDTCDE(4)
     A            DHCTSL        11  0       TEXT('Timeslice')
     A                                      COLHDG('Timeslice')
     A                                      EDTCDE(4)
     A                                      DFT(10000)
     A            DHCFOB         1          TEXT('FCOOB Error Acceptability')
     A                                      COLHDG('FCOOB Error' +
     A                                             'Acceptability')
     A            DHCTXT        60          TEXT('Component Text')
     A                                      COLHDG('Component Text')
     A            DHCPRM       100          TEXT('Execution Parameters')
     A                                      COLHDG('Execution Parameters')
     A            DHCRQD         1          TEXT('Sel for Today''s COB Flag')
     A                                      COLHDG('Sel for Today''s' +
     A                                             'COB Flag')
     A                                      DFT('N')
     A            DHCMOD         2          TEXT('Module')
     A                                      COLHDG('Module')
     A            DHCEMI         1          TEXT('Complex Module Conds Flag')
     A                                      COLHDG('Complex Module' +
     A                                             'Conds Flag')
     A            DHCRES         1          TEXT('Restart Flag')
     A                                      COLHDG('Restart' +
     A                                             'Flag')
     A                                      DFT('N')
     A            DHCODP         5  0       TEXT('No.Outstndng Dependencies')
     A                                      COLHDG('No.Outstndng' +
     A                                             'Dependencies')
     A                                      EDTCDE(4)
     A            DHCMEF         1          TEXT('Mut.Exclusive Comps.Flag')
     A                                      COLHDG('Mut.Exclusive' +
     A                                             'Comps.Flag')
     A            DHCFRQ         2          TEXT('Run Conditions')
     A                                      COLHDG('Run' +
     A                                             'Conditions')
     A            DHCSDE         5  0       TEXT('Start Date')
     A                                      COLHDG('Start' +
     A                                             'Date')
     A                                      EDTCDE(4)
     A            DHCSTI         6S 0       TEXT('Start Time')
     A                                      COLHDG('Start' +
     A                                             'Time')
     A                                      EDTCDE(4)
     A            DHCEDE         5  0       TEXT('End Date')
     A                                      COLHDG('End' +
     A                                             'Date')
     A                                      EDTCDE(4)
     A            DHCETI         6S 0       TEXT('End Time')
     A                                      COLHDG('End' +
     A                                             'Time')
     A                                      EDTCDE(4)
     A            DHCHTB         1          TEXT('Halt Before')
     A                                      COLHDG('Halt' +
     A                                             'Before')
     A                                      DFT('N')
     A            DHCHTA         1          TEXT('Halt After')
     A                                      COLHDG('Halt' +
     A                                             'After')
     A                                      DFT('N')
     A            DHCFAL         1          TEXT('Component Failed Today Fl')
     A                                      COLHDG('Component' +
     A                                             'Failed Today Fl')
     A                                      DFT('N')
      *                                    Start of change /S01491/
     A            DHEODT         6  0       TEXT('End of Day Run Time')
     A                                      COLHDG('End of Day' +
     A                                             'Run Time')
     A            DHEOMT         6  0       TEXT('End of Month Run Time')
     A                                      COLHDG('End of Month' +
     A                                             'Run Time')
     A            DHBOMT         6  0       TEXT('Begin of Month Run Time')
     A                                      COLHDG('Begin of Month' +
     A                                             'Run Time')
     A            DHEOYT         6  0       TEXT('End of Year Run Time')
     A                                      COLHDG('End of Year' +
     A                                             'Run Time')
     A            DHEODC         6  0       TEXT('End of Day CPU Time')
     A                                      COLHDG('End of Day' +
     A                                             'CPU Time')
     A            DHEOMC         6  0       TEXT('End of Month CPU Time')
     A                                      COLHDG('End of Month' +
     A                                             'CPU Time')
     A            DHBOMC         6  0       TEXT('Begin of Month CPU Time')
     A                                      COLHDG('Begin of Month' +
     A                                             'CPU Time')
     A            DHEOYC         6  0       TEXT('End of Year CPU Time')
     A                                      COLHDG('End of Year' +
     A                                             'CPU Time')
     A            DHABIC         1          TEXT('Allow Bypass in COB')
     A                                      COLHDG('Allow Bypass' +
     A                                             'in COB')
     A            DHCBSN         8          TEXT('COB Stream Name')
     A                                      COLHDG('COB Stream' +
     A                                             'Name')
      *                                      End of change /S01491/
     A*********              Start of Change               /CCB003/
     A            DHTASK         1          TEXT('Task Split Job')
     A                                      COLHDG('Task Split' +
     A                                             'Job')
     A            DHTSNO         3  0       TEXT('No. of Task Split Jobs')
     A                                      COLHDG('No. of Task' +
     A                                             'Split Jobs')
     A*                      End of Change                 /CCB003/
      * Start of Change MD055891
     A            DHMODE         1          TEXT('Mode')
     A                                      COLHDG('Mode')
     A            DHCPCA         1          TEXT('Exec Parm Chg Allw')
     A                                      COLHDG('Exec Parm' +
     A                                             'Chg Allw')
      * End of Change MD055891
      * Start of Change MD058986
     A            DHLOGD         1          TEXT('Logically Deleted')
     A                                      COLHDG('Logically' +
     A                                             'Deleted')
      * End of Change MD058986
     A*=====================================================================
