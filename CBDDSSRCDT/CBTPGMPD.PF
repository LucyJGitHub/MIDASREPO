      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas CB Task Split component relationships')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Module                             *
      *                                                               *
      *  PF/CBTPGMPD - Task Split component linkages                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *   If the attributes of this file are changed then the         *
      *   associated BRIDGE upgrade utilities must also be amended.   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CCB003  *CREATE    Date 07JUL97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB003 - CoB Task Split Enhancement:                         *
      *           File to show what CoB components form a single      *
      *           Task Split logical component                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R CBTPGMD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            CBOLDP    R               REFFLD(CCOT)
     A                                      TEXT('Comp. name before Task Split')
                                            COLHDG('Component' 'name before' +
                                                   'Task Split')
     A            CBOLDS    R               REFFLD(SEQNO)
     A                                      TEXT('Comp. seq before Task Split')
                                            COLHDG('Component' 'seq before' +
                                                   'Task Split')
     A            CBSTRP    R               REFFLD(CCOT)
     A                                      TEXT('Start Split component')
                                            COLHDG('Start' 'Split' 'Component')
     A            CBSTRS    R               REFFLD(SEQNO)
     A                                      TEXT('Start Split component seq')
                                            COLHDG('Start' 'Split' 'Comp. seq')
     A            CBSRVP    R               REFFLD(CCOT)
     A                                      TEXT('Server component')
     A                                      COLHDG('Server' 'component')
     A            CBSRVS    R               REFFLD(SEQNO)
     A                                      TEXT('Server component sequence')
     A                                      COLHDG('Server' 'component' 'seq')
     A            CBSTSK    R               REFFLD(CCOT)
     A                                      TEXT('Sub-task component')
     A                                      COLHDG('Sub-task' 'component')
     A            CBSTKS    R               REFFLD(SEQNO)
     A                                      TEXT('Sub-task component sequence')
     A                                      COLHDG('Sub-task' 'component' 'seq')
     A            CBEPRC    R               REFFLD(CCOT)
     A                                      TEXT('End Process component')
     A                                      COLHDG('End' 'Process' 'component')
     A            CBEPRS    R               REFFLD(SEQNO)
     A                                      TEXT('End Process component seq')
     A                                      COLHDG('End' 'Process' 'comp seq')
     A            CBESTK    R               REFFLD(CCOT)
     A                                      TEXT('End Sub-task component')
     A                                      COLHDG('End' 'Sub-task' 'component')
     A            CBESTS    R               REFFLD(SEQNO)
     A                                      TEXT('End Sub-task component seq')
     A                                      COLHDG('End' 'Sub-task' 'comp seq')
     A            CBESPT    R               REFFLD(CCOT)
     A                                      TEXT('End Split component')
     A                                      COLHDG('End Split' 'component')
     A            CBESPS    R               REFFLD(SEQNO)
     A                                      TEXT('End Split component sequence')
     A                                      COLHDG('End Split' 'component' +
     A                                           'sequence')
