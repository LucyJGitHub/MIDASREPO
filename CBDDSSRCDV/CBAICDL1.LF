      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas CB Automatic COB control')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*  LF/CBAICDL1 - Automatic C.O.B Control  Retrieval Index       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01495             Date 01Oct94               *
      *                 S01375             Date 22Apr92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01495 - COB Enhancements                                    *
     A*  S01375 - Automatic Close of Business                         ****
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A                                      JDFTVAL
     A                                      UNIQUE
     A*=====================================================================
     A          R @AICDF1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      JFILE(CBAICDPD +
     A                                            CBACOBPD +
     A                                            CBACOBPD +
     A                                            CBACOBPD +
     A                                            CBACOBPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Automatic C.O.B. Control to Automatic C.O.B. Data
     A          J                           JOIN(1 2)
     A                                      JFLD(ABDYKY AAAUTO)
     A*---------------------------------------------------------------------
     A* Join from Automatic C.O.B. Control to Automatic C.O.B. Data
     A          J                           JOIN(1 3)
     A                                      JFLD(ABWKKY AAAUTO)
     A*---------------------------------------------------------------------
     A* Join from Automatic C.O.B. Control to Automatic C.O.B. Data
     A          J                           JOIN(1 4)
     A                                      JFLD(ABMHKY AAAUTO)
     A*---------------------------------------------------------------------
     A* Join from Automatic C.O.B. Control to Automatic C.O.B. Data
     A          J                           JOIN(1 5)
     A                                      JFLD(ABYRKY AAAUTO)
     A*---------------------------------------------------------------------
     A            ABACOB                    TEXT('Auto C.O.B. Control Key')
     A                                      JREF(1)
     A*********              Start of Change               /S01495/
     A************ABCOBT                    TEXT('Auto C.O.B. flag')
     A            ABCOBT                    TEXT('This is now a redundant fld')
     A*                      End of Change                 /S01495/
     A                                      JREF(1)
     A            ABDAYF                    TEXT('Auto Daily flag')
     A                                      JREF(1)
     A            ABDYKY                    TEXT('Auto Daily Type')
     A                                      JREF(1)
     A            ABCBNA                    TEXT('Auto C.O.B. Narrative')
     A                                      JREF(2)
     A                                      RENAME(AACBNA)
     A            ABWKF                     TEXT('Auto Weekend flag')
     A                                      JREF(1)
     A            ABWKKY                    TEXT('Auto Weekend Type Auto C.')
     A                                      JREF(1)
     A            ABWKNA                    TEXT('Auto Weekend Type Narr')
     A                                      JREF(3)
     A                                      RENAME(AACBNA)
     A            ABMTHF                    TEXT('Auto Month End Flag')
     A                                      JREF(1)
     A            ABMHKY                    TEXT('Auto Month End Type Auto')
     A                                      JREF(1)
     A            ABMHNA                    TEXT('Auto Month End Type Narr')
     A                                      JREF(4)
     A                                      RENAME(AACBNA)
     A            ABYRF                     TEXT('Auto Year End Flag')
     A                                      JREF(1)
     A            ABYRKY                    TEXT('Auto Year End Type Auto C')
     A                                      JREF(1)
     A            ABYRNA                    TEXT('Auto Year End Type Narr')
     A                                      JREF(5)
     A                                      RENAME(AACBNA)
     A            ABLDWK                    TEXT('Last Day of Week')
     A                                      JREF(1)
     A            ABCBRT                    TEXT('COB Run type')
     A                                      JREF(1)
     A            ABCBRO                    TEXT('COB Rcv save option')
     A                                      JREF(1)
     A            ABCBDO                    TEXT('COB DB save option')
     A                                      JREF(1)
     A            ABCBCF                    TEXT('COB Clear savefiles opt.')
     A                                      JREF(1)
     A            ABCBMD                    TEXT('COB new run date')
     A                                      JREF(1)
     A            ABDBRN                    TEXT('COB new run day no.')
     A                                      JREF(1)
     A            ABCBDN                    TEXT('COB DB savefile name')
     A                                      JREF(1)
     A            ABAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            ABAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            ABATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            ABARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            ABAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            ABRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K ABACOB
     A*=====================================================================
