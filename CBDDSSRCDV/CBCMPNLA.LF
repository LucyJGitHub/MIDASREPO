      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas CB Components ModuleId & Required Today')        *
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Module                             *
      *                                                               *
      *  LF/CBCMPNLA - COB Components by Module ID and                *
      *                Required Today                                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CCB020             Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB008             Date 02Jan98               *
      *                 CCB003             Date 28NOV96               *
      *                 S01517             DATE 06JUN94               *
      *                 S01491             DATE 06JUN94               *
      *                 S01459             DATE 06JUN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB020 - COB Restructure - Secondary COB Infrastructure      *
      *  CCB008 - Synon Closedown Project (CB):                       *
      *           - File removed from CB model.                       *
      *  CCB003 - CoB Task Split Enhancement - 2 Fields added:        *
      *           - Task Split Job Ind. &                             *
      *           - Number of Task Split Jobs.                        *
      *  S01517 - Addition of new fields for Allow COB Component      *
      *           Bypass Project.                                     *
      *  S01491 - Addition of new fields for Autocall Project:        *
      *           - Four run time fields.                             *
      *           - Four CPU time fields.                             *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A                                      FIFO
     A          R @CMPNHQ                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(CBCMPNPD)
     A*---------------------------------------------------------------------
     A            DHCOTT                    TEXT('Component Name')
     A            DHCSEQ                    TEXT('Component Sequence Number')
     A            DHCSTS                    TEXT('Component Status')
     A            DHCSHP                    TEXT('Scheduling Priority')
     A            DHCEPY                    TEXT('Execution Priority')
     A            DHCTSL                    TEXT('Timeslice')
     A            DHCFOB                    TEXT('FCOOB Error Acceptability')
     A            DHCTXT                    TEXT('Component Text')
     A            DHCPRM                    TEXT('Execution Parameters')
     A            DHCRQD                    TEXT('Sel for Today''s COB Flag')
     A            DHCMOD                    TEXT('Module')
     A            DHCEMI                    TEXT('Complex Module Conds Flag')
     A            DHCRES                    TEXT('Restart Flag')
     A            DHCODP                    TEXT('No.Outstndng Dependencies')
     A            DHCMEF                    TEXT('Mut.Exclusive Comps.Flag')
     A            DHCFRQ                    TEXT('Run Conditions')
     A            DHCSDE                    TEXT('Start Date')
     A            DHCSTI                    TEXT('Start Time')
     A            DHCEDE                    TEXT('End Date')
     A            DHCETI                    TEXT('End Time')
     A            DHCHTB                    TEXT('Halt Before')
     A            DHCHTA                    TEXT('Halt After')
     A            DHCFAL                    TEXT('Component Failed Today Fl')
     A            DHEODT                    TEXT('End of Day Run Time')
     A            DHEOMT                    TEXT('End of Month Run Time')
     A            DHBOMT                    TEXT('Begin of Month Run Time')
     A            DHEOYT                    TEXT('End of Year Run Time')
     A            DHEODC                    TEXT('End of Day CPU Time')
     A            DHEOMC                    TEXT('End of Month CPU Time')
     A            DHBOMC                    TEXT('Begin of Month CPU Time')
     A            DHEOYC                    TEXT('End of Year CPU Time')
     A            DHABIC                    TEXT('Allow Bypass in COB')
     A            DHCBSN                    TEXT('COB Stream Name')
     A            DHTASK                    TEXT('Task Split Job Indicator')
     A            DHTSNO                    TEXT('Number of Task Split Jobs')
     A*.....................................................................
     A* Key fields
     A          K DHCMOD
     A          K DHCOTT
     A          K DHCSEQ
     A*.....................................................................
     A* SELECT: Selected for Today's COB
     A* Field    : Sel for Today's COB Flag
     A* Condition: Yes
     A          S DHCRQD                    COMP(EQ 'Y')
     A*.....................................................................
      **Start of Change CCB020
     A****OMIT:*COBBEGIN/COBEND****************************************                       CCB020
      *   OMIT: COBBEGIN/SCOBFINISH
      **End of Change CCB020
     A* Field    : Component Name
     A* Condition: cobbegin
     A          O DHCOTT                    COMP(EQ 'COBBEGIN')
     A* Field    : Component Name
      **Start of Change CCB020
     A**Condition:*Cobend**********************************************                       CCB020
      * Condition: SCOBFINISH
     A**********  DHCOTT                    COMP(EQ 'COBEND')                                 CCB020
     A            DHCOTT                    COMP(EQ 'SCOBFINISH')
      **End of Change CCB020
     A*=====================================================================
