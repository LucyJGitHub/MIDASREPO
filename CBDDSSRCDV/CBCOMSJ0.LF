      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas CB Join COB component to switch feature')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  LF/CBCOMSJ0 - Join COB Component Name to Switchable feature  *
     A*                 related COB Component                         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD056948           Date 12Oct20               *
      *  Prev Amend No. MD055681           Date 31Jul20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB003             Date 28Nov96               *
      *                 S01491             DATE 01OCT94               *
      *                 062860             DATE 05APR94               *
     A*                 043911             DATE 16DEC93               *
     A*                 062453             DATE 28OCT93               *
     A*                 S01407             DATE 20JAN93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD056948 - Missing selection when feature is disabled.       *
      *  MD055681 - Deliverable Data Split for SAR                    *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
      *  CCB003 - CoB Task Split Enhancement - 2 Fields added:        *
      *           Recompile only                                      *
     A*  S01491 - Autocall. Recompiled over new CBCOMSPD.             *
     A*  062860 - Core data items cannot be made switchable           *
     A*  043911 - Change creation parameters to have WAITFILE time    *
     A*           of 60 seconds instead of *IMMED.                    *
     A*  062453 - Recompile file into DTA lib (make redundant in DV)  *
     A*  S01407 - Switchability Enhancements                          *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DYNSLT
     A                                      JDFTVAL
      * Start of change MD055681
     A********* R CBCMPND0                  JFILE(CBCOMSPD SCSARCPD SCSARDPD)
     A          R CBCMPND0                  JFILE(CBCOMSPD SCSARCPD SCSRDXTD)
      * End of change MD055681
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(DHCOTT ACCOTT)
     A                                      JFLD(DHCSEQ ACCSEQ)
     A          J                           JOIN(2 3)
     A                                      JFLD(ACSARN SARN)
     A*
     A            DHCOTT
     A            DHCSEQ
     A            ACSARN
     A            ACAVAL
      * Start of change MD055681
     A**********  SARD
     A            ENABLD
      * End of change MD055681
     A            DHCFRQ
     A            DHCMOD
     A            DHCEMI
     A*
     A          K DHCOTT
     A          K DHCSEQ
     A*
     A          S ACSARN                    COMP(EQ ' ')
     A*
     A          S ACSARN                    COMP(NE ' ')
      * Start of change MD055681
     A**********  SARD                      COMP(NE ' ')
      * End of change MD055681
     A            ACAVAL                    COMP(EQ 'Y')
      * Start of change MD055681
     A            ENABLD                    COMP(EQ 'E')
      * End of change MD055681
     A*                                                      /*062860*/
     A          S ACSARN                    COMP(NE ' ')
      * Start of change MD055681
     A**********  SARD                      COMP(EQ ' ')
     A            ENABLD                    COMP(EQ ' ')
      * End of change MD055681
     A            ACAVAL                    COMP(EQ 'N')
     A*                                                      /*062860*/
      * Start of change MD056948
     A          S ACSARN                    COMP(NE ' ')
     A            ENABLD                    COMP(EQ 'D')
     A            ACAVAL                    COMP(EQ 'N')
      * End of change MD056948
