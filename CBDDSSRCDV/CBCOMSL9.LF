      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMWF                                                      *
/*EXI *  TEXT('Midas CB Select compnts ready for schedule')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                     *
     A*                                                               *
     A*  LF/CBCOMSL9   SELECTION OF COMPONENTS READY TO BE SCHEDULED  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCB003             Date 28Nov96               *
      *                 S01491             Date 20Jun94               *
     A*                 043911             DATE 16DEC93               *
     A*                 062453             DATE 28OCT93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB003 - CoB Task Split Enhancement - 2 Fields added:        *
      *           - Task Split Job Ind. &                             *
      *           - Number of Task Split Jobs.                        *
     A*  S01491 - Autocall.                                           *
     A*  043911 - Change creation parameters to have WAITFILE time    *
     A*           of 60 seconds instead of *IMMED.                    *
     A*  062453 - Recompile file into DTA lib (make redundant in DV)  *
     A*                                                               *
     A*****************************************************************
     A          R @CMPNHO                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(CBCOMSPD)
     A*---------------------------------------------------------------------
     A            DHCOTT                    TEXT('Component Name')
     A            DHCSEQ                    TEXT('Component Sequence Number')
     A            DHCSTS                    TEXT('Component Status')
     A            DHCSHP                    TEXT('Scheduling Priority')
     A            DHCEPY                    TEXT('Execution Priority')
     A            DHCTSL                    TEXT('Timeslice')
     A            DHCFOB                    TEXT('FCOOB Error Acceptability')
     A            DHCTXT                    TEXT('Component Text')
     A            DHCPRM                    TEXT('Execution Parameters')
     A            DHCRQD                    TEXT('Sel for Today''s COB Flag')
     A            DHCMOD                    TEXT('Module')
     A            DHCEMI                    TEXT('Complex Module Conds Flag')
     A            DHCRES                    TEXT('Restart Flag')
     A            DHCODP                    TEXT('No.Outstndng Dependencies')
     A            DHCMEF                    TEXT('Mut.Exclusive Comps.Flag')
     A            DHCFRQ                    TEXT('Run Conditions')
     A            DHCSDE                    TEXT('Start Date')
     A            DHCSTI                    TEXT('Start Time')
     A            DHCEDE                    TEXT('End Date')
     A            DHCETI                    TEXT('End Time')
     A            DHCHTB                    TEXT('Halt Before')
     A            DHCHTA                    TEXT('Halt After')
     A            DHCFAL                    TEXT('Component Failed Today Fl')
     A*********              Start of Change               /S01491/
     A            DHEODT                    TEXT('End of day time')
     A            DHEOMT                    TEXT('End of month time')
     A            DHBOMT                    TEXT('Beginning of month time')
     A            DHEOYT                    TEXT('End of year time')
     A            DHEODC                    TEXT('End of day CPU time')
     A            DHEOMC                    TEXT('End of month CPU time')
     A            DHBOMC                    TEXT('Beginning of month CPU time')
     A            DHEOYC                    TEXT('End of year CPU time')
     A            DHABIC                    TEXT('Allow Bypass in COB')
     A            DHCBSN                    TEXT('COB Stream Name')
     A*                      End of Change                 /S01491/
     A            DHTASK                    TEXT('Task Split Job Indicator')
     A            DHTSNO                    TEXT('Number of Task Split Jobs')
     A*.....................................................................
     A* Key fields
     A          K DHCSHP
     A          K DHCRQD
     A*.....................................................................
     A* Select: Status = R & No. Outstanding Deps = 0
     A*          Fld/Cond: Component Status          Restart
     A          S DHCSTS                    COMP(EQ 'R')
     A*          Fld/Cond: No.Outstndng Dependencies Equal zero
     A            DHCODP                    COMP(EQ 0)
     A*.....................................................................
     A* Select: Status = blank & No. Outstanding Deps = 0
     A*          Fld/Cond: Component Status          Default Value
     A          S DHCSTS                    COMP(EQ ' ')
     A*          Fld/Cond: No.Outstndng Dependencies Equal zero
     A            DHCODP                    COMP(EQ 0)
