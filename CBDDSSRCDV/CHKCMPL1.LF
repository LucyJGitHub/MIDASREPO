      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas CB Copy comp on no of outs deps = 0')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Module                             *
      *                                                               *
      *  LF/CHKCMPL1 - COB Check File (Dependencies = 0)              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Note: Core amendments to this file must be applied via       *
      *        the core synon model.                                  *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCB003             Date 02Oct96               *
      *                 S01491             Date 05Oct94               *
      *                 S01459             DATE 05OCT94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB003 - Perf.Enh. Two new fields added.                     *
      *  S01491 - Autocall Project.                                   *
      *           - Recompile over extended CBCMPNPD.                 *
      *  S01459 - Addition of Midas headerbox.                        *
      *                                                               *
      *****************************************************************
     A          R CHKCMPD1
     A                                      PFILE(CHKCMPPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            DHCOTT                    TEXT('Component Name')
     A            DHCSEQ                    TEXT('Component Sequence Number')
     A            DHCSTS                    TEXT('Component Status')
     A            DHCSHP                    TEXT('Scheduling Priority')
     A            DHCEPY                    TEXT('Execution Priority')
     A            DHCTSL                    TEXT('Timeslice')
     A            DHCFOB                    TEXT('FCOOB Error Acceptability')
     A            DHCTXT                    TEXT('Component Text')
     A            DHCPRM                    TEXT('Execution Parameters')
     A            DHCRQD                    TEXT('Sel for Today''s COB Flag')
     A            DHCMOD                    TEXT('Module')
     A            DHCEMI                    TEXT('Complex Module Conds Flag')
     A            DHCRES                    TEXT('Restart Flag')
     A            DHCODP                    TEXT('No.Outstndng Dependencies')
     A            DHCMEF                    TEXT('Mut.Exclusive Comps.Flag')
     A            DHCFRQ                    TEXT('Run Conditions')
     A            DHCSDE                    TEXT('Start Date')
     A            DHCSTI                    TEXT('Start Time')
     A            DHCEDE                    TEXT('End Date')
     A            DHCETI                    TEXT('End Time')
     A            DHCHTB                    TEXT('Halt Before')
     A            DHCHTA                    TEXT('Halt After')
     A            DHCFAL                    TEXT('Component Failed Today Fl')
     A*********              Start of Change               /CCB003/
     A            DHTASK                    TEXT('Task Split Job')
     A            DHTSNO                    TEXT('No. of Task Split Jobs')
     A*                      End of Change                 /CCB003/
     A*.....................................................................
     A* Key fields
     A          K DHCOTT
     A          K DHCSEQ
     A*.....................................................................
     A* Select field Number of Outstanding Dependencies = 0
     A* and status not equal to complete
     A          S DHCODP                    COMP(EQ 0)
     A            DHCSTS                    COMP(NE 'C')
