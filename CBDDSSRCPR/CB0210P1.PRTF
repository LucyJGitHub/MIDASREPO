      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CB COB dependency table report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing                         *
     A*                                                               *
     A*  PRTF/CB0210P1  DEPENDENCY TABLE REPORT                       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCB003             Date 21Oct96               *
      *                 044853             Date 23Sep92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB003 - COB Performance Enhancements - Task Split           *
      *           Amended to show new fields for Task Split process.  *
     A*  044853 - EXTEND REPORT TYPE HEADING TO 30 LONG TO            *
     A*           ACCOMODATE LONGER TITLES.                           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A          R RPTHDG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                  3  3'REFERENCE CB0210'
     A            BJURPT        53      3 37
     A                                  3107'DATE'
     A            BJMRDT         7      3112
     A                                  3122'PAGE'
     A            PAGE           5S 0   3127 EDTCDE( 4)
     A                                  5 46'CLOSE OF BUSINESS DEPENDENCY '
     A                                  6 46'-----------------------------'
     A                                  5 75'TABLE REPORT  FOR '
     A                                  6 75'------------'
     A*
     A**                                    START OF /044853/
     A************REPTYP        20      5 93
     A            REPTYP        30      5 93
     A**                                    END   OF /044853/
     A*
     A          R COMPDT                    TEXT('COMPONENT DETAILS')
     A                                  7  3'COMPONENT SEQ.NO.'
     A            DHCOTT        10      9  3
     A            DHCSEQ         5      9 14
     A                                  7 25'TEXT'
     A            DHCTXT        60      9 21
     A                                 11 10'PARAMETERS'
     A            DHCPRM       100     11 21
     A                                 13 10'SCHEDULING PRIORITY'
     A            DHCSHP         1  0  13 31
     A                                 13 40'RUN'
     A            RUNTP         76     13 44
     A                                 15 10'EXECUTION  PRIORITY'
     A            DHCEPY         2  0  15 30
     A                                 15 40'TIMESLICE'
     A            DHCTSL        11  0  15 50EDTCDE(Z)
     A                                 15 66'CONTINUE IF FILE CONTROLS'
     A                                 15 92'OUT OF BALANCE'
     A            DHCFOB         1     15108
     A                                 15117'MODULE ID'
     A            DHCMOD         2     15127
      *
      ** Start of /CCB003/
      *
     A                                 16 10'TASK SPLIT JOB:'
     A            DHTASK         1     16 26
     A  30                             16 30'NUMBER OF TASK SPLIT JOBS:'
     A  30        DHTSNO         3  0  16 57
      *
      ** End   of /CCB003/
      *
     A          R OTMDDT                    TEXT('OTHER MODULES DETAILS')
     A  70
     AO 81                                  SPACEB(2)
     A  70
     AO 81                                 3'OTHER MODULES REQUIRED BY '
     A  70
     AO 81        DHCOTT        10        29
     A N81                                  SPACEA(1)
     A  81                                41'(cont.)'
     A  81                                  SPACEA(1)
     A  89                                 7'OR'
     A  89                                  SPACEB(1)
     A            OTMDFL        80        10
     A N89                                  SPACEB(1)
     A*
     A          R DEPCDT                    TEXT('DEPENDENT COMPONENT DETAILS')
     A                                      SPACEB(2)
     A  71
     AO 83                                 3'COMPONENTS WHICH MUST '
     A  71
     AO 83                                25'BE RUN PRIOR TO '
     A  71
     AO 83        COMPNM        10        41TEXT('COMPONENT NAME')
     A N83                                  SPACEA(2)
     A  83                                52'(cont.)'
     A  83                                  SPACEA(2)
     A            DIDDON        10        10TEXT('COMPONENT NAME')
     A            DIDDOS         5        22TEXT('COMPONENT SEQ NO.')
     A            DHCTXT        60        28TEXT('COMPONENT TEXT')
     A*
     A          R DEPONC                    TEXT('DEPENDS ON COMPONENT DETAILS')
     A                                      SPACEB(2)
     A  72
     AO 84                                 3'COMPONENTS WHICH MUST '
     A  72
     AO 84                                25'RUN AFTER'
     A  72
     AO 84        COMPNM        10        35TEXT('COMPONENT NAME')
     A N84                                  SPACEA(2)
     A  84                                46'(cont.)'
     A  84                                  SPACEA(2)
     A            DIDSON        10        10TEXT('COMPONENT NAME')
     A            DIDSOS         5        22TEXT('COMPONENT SEQ NO.')
     A            DHCTXT        60        28TEXT('COMPONENT TEXT')
     A*
     A          R MUEXDT                    TEXT('MUTUALLY EXCLUSIVE DETAILS')
     A                                      SPACEB(2)
     A  73
     AO 85                                 3'MUTUALLY EXCLUSIVE COMPONENTS '
     A N85                                  SPACEA(2)
     A  85                                34'(cont.)'
     A  85                                  SPACEA(2)
     A            DKMECN        10        10
     A            DKMECS         5        22
     A            DHCTXT        60        28
     A*
     A          R CONLIN                    TEXT('CONTINUATION LINE')
     A                                      SPACEB(1)
     A                                   113'(cont.)'
     A*
     A          R NODTRP                    TEXT('NO DETAILS TO REPORT')
     A                                      SPACEB(3)
     A                                    53'*** NO DETAILS '
     A                                    68'TO REPORT ***'
     A*
     A          R DBERDT                    TEXT('DATA BASE ERROR')
     A                                      SPACEB(3)
     A                                    45'DATABASE ERROR '
     A            DBASE          3  0     60
     A                                    63'IN PROGRAM '
     A            DBPGM         10A       74
     A                                      SPACEA(1)
     A                                    45'FILE '
     A            DBFILE         8A       50
     A                                    55', KEY '
     A            DBKEY         29A       61
     A*
     A          R RPTEND                    TEXT('REPORT END')
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
     A*
