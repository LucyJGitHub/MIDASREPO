      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CB COB components text summary rept')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*  PRTF/CB0250P1  CLOSE OF BUSINESS COMPONENTS                  *
     A*                 SUMMARY REPORT                                *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO.00                 DATE  00/00/00              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A          R RPTHDG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                  3  2'REFERENCE CB0250'
     A            BJURPT        53A     3 37TEXT('USER REPORT TITLE')
     A                                  3107'DATE'
     A            BJMRDT         7A     3112TEXT('MIDAS RUN DATE')
     A                                  3122'PAGE'
     A            PAGE           5S 0   3127 EDTCDE( 4)
     A*
     A          R SUBHDG                    TEXT('REPORT SUB-HEADING')
     A                                      SPACEB(1)
     A                                    51'COMPONENT SUMMARY'
     A                                    69'REPORT'
     A                                      SPACEA(1)
     A                                    51'------------------'
     A                                    69'------'
     A                                      SPACEA(1)
     A                                     4'COMPONENT'
     A                                    21'SEQ.NO.'
     A                                    56'TEXT'
     A                                      SPACEA(1)
     A*
     A          R DETLIN                    TEXT('COMPONENT DETAILS')
     A                                      SPACEB(1)
     A            DHCOTT        10A        4TEXT('COMPONENT NAME')
     A            DHCSEQ         5A       21TEXT('COMPONENT SEQUENCE NUMBER')
     A            DHCTXT        60A       32TEXT('COMPONENT TEXT')
     A          R PRMLIN                    TEXT('COMPONENT PARAMETERS')
     A                                      SPACEB(1)
     A                                     8'EXECUTION PARAMETERS'
     A            DHCPRM       100A       32TEXT('EXECUTION PARAMETERS')
     A*
     A          R DBELIN                    TEXT('DATA BASE ERROR')
     A                                      SPACEB(3)
     A                                    45'DATABASE ERROR '
     A            DBASE          3  0     60
     A                                    63'IN PROGRAM '
     A            DBPGM         10A       74
     A                                      SPACEA(1)
     A                                    45'FILE '
     A            DBFILE         8A       50
     A                                    55', KEY '
     A            DBKEY         29A       61
     A*
     A          R RPTEND                    TEXT('REPORT END')
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
     A*
