      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CB COB audit report')                            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*  PRTF/CB0300P1  CLOSE OF BUSINESS AUDIT REPORT BY TIME        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD024294           Date 07May14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CCB015             Date 09Mar06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                00                 DATE  00/00/00              *
     A*                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD024294 - Added information on time taken by Main CoB       *
      *  CCB015 - CoB Audit Report Enhancement.                       *
      *           Report Bypassed Status                              *
      *                                                               *
     A*****************************************************************
     A                                       REF(MFRFL)
     A          R RPTHDG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                  3  3'REFERENCE CB0300'
     A            BJURPT        53A     3 37TEXT('USER TITLE')
     A                                  3107'DATE'
     A            BJMRDT         7A     3112TEXT('MIDAS RUN DATE')
     A                                  3122'PAGE'
     A            PAGE           5S 0   3127 EDTCDE( 4)
     A                                  5 45'CLOSE OF BUSINESS AUDIT '
     A                                  6 45'------------------------'
     A                                  5 69'REPORT BY TIME'
     A                                  6 69'--------------'
     A*
     A          R SUBHDG                    TEXT('REPORT SUB-HEADING')
     A                                      SPACEB(1)
     A
     A                                     3'COMPONENT     SEQ.NO.'
     A
     A                                    30'TEXT'
     A
     A                                    87'START TIME  END TIME'
     A
     A                                   110'RUN TIME'
     A
     A                                   122'STATUS'
     A                                      SPACEA(1)
     A*
     A          R DETLIN                    TEXT('COMPONENT DETAILS')
     A                                      SPACEB(1)
     A            DHCOTT        10A        3TEXT('COMPONENT NAME')
     A            DHCSEQ         5A       17TEXT('COMPONENT SEQUENCE NUMBER')
     A            DHCTXT        60A       25TEXT('COMPONENT TEXT')
     A            SNH            2  0O    88TEXT('START NUMBER OF HOURS')
     A                                    90':'
     A            SNM            2  0O    91TEXT('START NUMBER OF MINUTES')
     A                                    93':'
     A            SNS            2  0O    94TEXT('START NUMBER OF SECONDS')
     A            ENH            2  0O    99TEXT('END NUMBER OF HOURS')
     A                                   101':'
     A            ENM            2  0O   102TEXT('END NUMBER OF MINUTES')
     A                                   104':'
     A            ENS            2  0O   105TEXT('END NUMBER OF SECONDS')
     A            HRS            2  0O   110TEXT('HOURS RUN')
     A                                   112':'
     A            MIN            2  0O   113TEXT('MINUTES RUN')
     A                                   115':'
     A            SEC            2  0O   116TEXT('SECONDS RUN')
     A  71                               122'FAILED'
     A  72                               122'FCOOB'
     A*/COPY WNCPYSRC,CB0300P101
     A  73                               122'BYPASSED'                                        CCB015
     A*
     A          R HLTHDG                    TEXT('HALT DETAILS HEADING')
     A                                      SPACEB(2)
     A                                    25'CLOSE OF BUSINESS HALT DETAILS'
     A N79                                  SPACEA(1)
     A  79                                55' (cont.)'
     A                                      SPACEA(1)
     A                                    25'------------------------------'
     A  79                                55'--------'
     A*
     A          R HLTLIN                    TEXT('HALT DETAILS')
     A                                      SPACEB(1)
     A            HTXT          60A       25TEXT('HALT TEXT')
     A*
     A          R TOTLIN                    TEXT('TOTALS LINE')
     A                                      SPACEB(3)
     A                                    25'CLOSE OF BUSINESS RUN SUMMARY'
     A                                      SPACEA(1)
     A                                    25'-----------------------------'
     A                                      SPACEA(1)
     A                                    25'RUN COMMENCED AT'
     A            SNH            2  0O    42TEXT('START NUMBER OF HOURS')
     A                                    44':'
     A            SNM            2  0O    45TEXT('START NUMBER OF MINUTES')
     A                                    47':'
     A            SNS            2  0O    48TEXT('START NUMBER OF SECONDS')
     A                                    51'ON'
     A            STDT           7A       54TEXT('START DATE')
     A                                      SPACEA(1)
     A                                    25'RUN COMPLETED AT'
     A            ENH            2  0O    42TEXT('END NUMBER OF HOURS')
     A                                    44':'
     A            ENM            2  0O    45TEXT('END NUMBER OF MINUTES')
     A                                    47':'
     A            ENS            2  0O    48TEXT('END NUMBER OF SECONDS')
     A                                    51'ON'
     A            ENDT           7A       54TEXT('END DATE')
     A                                      SPACEA(1)
     A                                    25'TOTAL TIME TAKEN'
     A            HRS            2  0O    42TEXT('HOURS RUN')
     A                                    44':'
     A            MIN            2  0O    45TEXT('MINUTES RUN')
     A                                    47':'
     A            SEC            2  0O    48TEXT('SECONDS RUN')
     A**Start of change MD024294
     A                                      SPACEA(1)
     A                                    25'MAIN COB TIME'
     A            RMHRS          2  0O    42TEXT('MCOB Hours Run')
     A                                    44':'
     A            RMMIN          2  0O    45TEXT('MCOB Minutes Run')
     A                                    47':'
     A            RMSEC          2  0O    48TEXT('MCOB Seconds Run')
     A**End of change MD024294
     A*
     A          R DBELIN                    TEXT('DATA BASE ERROR')
     A                                      SPACEB(3)
     A                                    45'DATABASE ERROR '
     A            DBASE          3  0     60
     A                                    63'IN PROGRAM '
     A            DBPGM         10A       74
     A                                      SPACEA(1)
     A                                    45'FILE '
     A            DBFILE         8A       50
     A                                    55', KEY '
     A            DBKEY         29A       61
     A*
     A          R RPTEND                    TEXT('REPORT END')
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
     A*
