      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CB COB loop checker report')                     *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*  PRTF/CB9010P1  LOOP CHECKER REPORT                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. CCB020            Date  00Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No.00                 DATE  00/00/00              *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *  CCB020 - COB Restructure - Secondary COB Infrastructure      *
      *                                                               *
      *****************************************************************
     A*                                                               *
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***
     A                                  3  3'REFERENCE CB9010'
     A                                  3 55'COB LOOP CHECKER REPORT'
     A*
     A                                  3100DATE EDTCDE(Y)
     A                                  3111TIME
     A                                  3122'PAGE :'
     A                                    +1PAGNBR EDTCDE(4)
     A*
     A                                  4 55'-----------------------'
     A***
     A          R DETL01                    SPACEB(2)
     A                                    11'No components depend on COBBEGIN'
     A***
     A          R DETL02                    SPACEB(2)
     A                                    11'BROKEN CHAIN - No components +
     A                                       depend on '
     A            DIDDON        10        50
     A            DIDSOS         5        61
     A***
     A          R DETL03                    SPACEB(2) SPACEA(1)
     A                                    11'LOOP exists in the following +
     A                                       chain :'
     A***
     A          R DETL04                    SPACEB(1)
     A*
     A            NUMA           3  0     16EDTCDE(4)
     A                                    19'.'
     A            COMPA         15        20
     A  01                                  HIGHLIGHT
     A*
     A            NUMB           3  0     36EDTCDE(4)
     A                                    39'.'
     A            COMPB         15        40
     A  02                                  HIGHLIGHT
     A*
     A            NUMC           3  0     56EDTCDE(4)
     A                                    59'.'
     A            COMPC         15        60
     A  03                                  HIGHLIGHT
     A*
     A            NUMD           3  0     76EDTCDE(4)
     A                                    79'.'
     A            COMPD         15        80
     A  04                                  HIGHLIGHT
     A*
     A            NUME           3  0     96EDTCDE(4)
     A                                    99'.'
     A            COMPE         15       100
     A  05                                  HIGHLIGHT
     A***
     A          R DETL05                    SPACEB(2)
     A                                    11'DATABASE ERROR : record not +
     A                                       found for :'
     A            DIDDON        10        51
     A            DIDDOS         5        66
     A***
     A          R DETL06                    SPACEB(2)
     A                                    11'REDUNDANT DEPENDENCY for :'
     A            DIDDON        10        38
     A            DIDDOS         5        53
      **Start of Change CCB020
     A**********                          60'to COBEND'                                       CCB020
     A                                    60'to SCOBFINISH'
      **End of Change CCB020
     A***
     A          R DETL07                    SPACEB(2)
     A                                    11'DATABASE ERROR : record not +
     A                                       found for :'
     A            DIDDON        10        51
     A            DIDDOS         5        66
     A            DIDSON        10        75
     A            DIDSOS         5        90
     A            DIDRUN         3       100
     A***
     A          R DETL08                    SPACEB(2)
     A                                    11'COMPONENT NOT PROCESSED - not +
     A                                       in chain :'
     A            DIDDON        10        51
     A            DIDDOS         5        66
     A            DIDSON        10        75
     A            DIDSOS         5        90
     A            DIDRUN         3       100
     A***
     A          R DETL09                    SPACEB(2)
     A                                    54'NO DEPENDENCY LOOPS FOUND'
     A*
     A          R RPTEND                    TEXT('REPORT END')
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
     A*
