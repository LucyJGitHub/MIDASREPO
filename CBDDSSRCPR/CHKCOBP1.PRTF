      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CB Validation of COB files report')              *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing
     A*                                                               *
     A*  PRTF/CHKCOBP1 - COB COMPONENTS AND DEPENDENCIES              *
     A*                  VALIDATION REPORT                            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CCB020C            Date 16May13               *
      *                 CCB024             Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 S01417             DATE 06JUL93               *
     A*                 E27972             DATE 20OCT91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB020C - COB Restructure - Secondary COB Infrastructure     *
      *  CCB024 - COB Restructure - COB Tuning                        *
     A*  S01417  -  Fix Overlapping Lines On Report                   *   S01417
     A*             Amend report text lines for :                     *
     A*                'No Dependencies found for Component'          *
     A*             Remove Prev Amendment 'E27972'                    *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  E27972  -  IF THE NUMBER OF OUTSTANDING DEPENDENCIES         *
     A*             DEFINED ON THE COMPONENTS FILE IS ZERO THEN       *
     A*             WRITE THE DETAILS OF THE DISCREPANCY TO THE       *
     A*             REPORT.                                           *
     A*             A SECOND PHASE OF THIS FIX WAS DONE ON 20OCT91    *
     A*             AFTER RECEIVING NEW DETAILS FROM GREG.            *
     A*                                                               *
     A*****************************************************************
     A          R RPTHDG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                  3  3'REFERENCE CHKCOBP1'
     A            BJURPT        53A     3 37TEXT('BANK REPORT TITLE')
     A                                  3107'DATE'
     A            BJMRDT         7A     3112TEXT('MIDAS RUN DATE')
     A                                  3122'PAGE'
     A            PAGE           5S 0   3127 EDTCDE( 4)
     A                                  5 36'CLOSE OF BUSINESS COMPONENT AND DE-
     A                                      PENDENCY FILE ERROR REPORT'
     A                                  6 36'-----------------------------------
     A                                      --------------------------'
     A*
     A          R CMPERR                    TEXT('COMPONENT ERROR')
     A                                      SPACEB(1)
     A  31                                 2'No record exists on the Components-
     A                                       file for component'
     A  31        D2DSON        10A       56
     A  31        D2DSOS         5A       67
     A  31                                73'which is defined as being dependen-
     A                                      t on component'
     A  31                                  SPACEA(1)
     A  31        D2DDON        10A        2
     A  31        D2DDOS         5A       13
     A  31                                19'in the Dependency file.'
     A  31                                  SPACEA(1)
     A  31                                 2'Checking cannot continue until the-
     A                                       component is added to the Componen-
     A                                      ts file.'
     A*
     A  32                                 2'No records exist on the Components-
     A                                       file.'
     A  32                                  SPACEA(1)
     A  32                                 2'Checking cannot continue.'
     A*
     A  33                                 2'Component'
     A  33        D1COTT        10A       12
     A  33        D1CSEQ         5A       23
     A  33                                29'is not defined as being a Depended-
     A                                      -on component in the Dependencies f-
     A                                      ile.'
     A  33                                  SPACEA(1)
     A  33                                 2'Checking cannot continue.'
     A*
     A  34                                 2'Dummy component COBBEGIN has not b-
     A                                      een defined to the Components file.'
     A  34                                  SPACEA(1)
     A  34                                 2'Checking cannot continue.'
      **Start of Change CCB020C
     A  40                                 2'Secondary COB component'
     A  40        P#DDON        10A       26
     A  40        P#DDOS         5A       37
     A  40                                43'has an incorrect dependency +
     A                                      with Main COB component'
     A  40        P#DSON        10A       95
     A  40        P#DSOS         5A      106
      *
     A  41                                  SPACEA(1)
     A  41                                 2'NWD CoB component'
     A  41        P#COTT        10A       20
     A  41        P#CSEQ         5A       31
     A  41                                37'cannot have a sequence number +
     A                                      beginning with 2.'
      **End of Change CCB020C
     A*
     A          R DEPER1                    TEXT('DEPENDENCY ERROR FORMAT 1')
     A** Start Of S01417
     A**************************************SPACEB(1)***************************
     A                                      SPACEB(2)
     A** End Of S01417
     A                                     2'The number of outstanding Dependen-
     A                                      cies defined on the Components file-
     A                                       for Component'
     A            D2COTT        10A       86
     A            D2CSEQ         5A       97
     A** Start Of S01417
     A***********************************103'is'
     A                                   103'was'
     A** START OF E27972
     A************D2CODP         5S      106EDTCDE(4)
     A*N62********D2CODP         5S      106EDTCDE(4)
     A N62        D2CODP         5S      107EDTCDE(4)
     A                                      SPACEA(1)
     A**62********                       106'ZERO.'
     A  62                               107'ZERO'
     A** END OF E27972
     A** End Of S01417
     A                                      SPACEA(1)
     A** Start Of S01417
     A*N62*********************************2'but this component is defined as a-
     A***************************************Depends-on component in the follow-
     A**************************************ing'
     A*N62********NREC***********5S*******75EDTCDE(4)
     A*N62********************************81'dependencies:'
     A                                     2'and this component was +
     A                                      defined as a Depends-on +
     A                                      component in the following'
     A            NREC           5S       75EDTCDE(4)
     A                                    81'dependencies:'
     A                                      SPACEA(1)
     A                                     2'This discrepancy has +
     A                                      been corrected'
     A***START*OF*E27972
     A**62*********************************2'This component must be defined as -
     A**************************************a Depends-on component in at least -
     A**************************************one dependency.'
     A***END*OF*E27972
     A** End Of S01417
     A*
     A          R DEPER2                    TEXT('DEPENDENCY ERROR FORMAT 2')
     A                                      SPACEB(1)
     A            D1DSON        10A        2
     A            D1DSOS         5A       13
     A            D1DRUN         3A       20
     A            D1DDON        10A       25
     A            D1DDOS         5A       36
     A*
     A** Start Of S01417
     A**********R*DEPER3********************TEXT('DEPENDENCY ERROR FORMAT 3')
     A**************************************SPACEB(1)
     A*************************************2'Checking cannot continue until thi-
     A**************************************s discrepancy has been corrected.'
     A** End Of S01417
     A*
     A          R MUTERR                    TEXT('MUTUALLY EXCL ERROR')
     A                                      SPACEB(1)
     A                                     2'Mutually Exclusive Flag set to Y f-
     A                                      or component'
     A            D2DSON        10A       49
     A            D2DSOS         5A       60
     A                                    66'but no record was found on the Mut-
     A                                      ually Exclusive Components File.'
     A*
     A          R MDCERR                    TEXT('MODULE COND ERROR')
     A                                      SPACEB(1)
     A                                     2'Module Condition Flag set to Y for-
     A                                       component'
     A            D2DSON        10A       47
     A            D2DSOS         5A       58
     A                                    64'but no records were found on the M-
     A                                      odule Conditions File.'
     A*
     A** Start Of S01417
     A**********R INCHDR                    TEXT('INCOMPLETE SECTION HEADER')
     A**********                            SPACEB(1)
     A**********                           2'The following records were not val-
     A**********                            idated by the checking process:'
     A**********
     A**********R INCCMP                    TEXT('INCOMPLETE COMPONENTS')
     A**********                            SPACEB(1)
     A**********                           2'The component'
     A**********  D2COTT        10A       16
     A**********  D2CSEQ         5A       26
     A**********                          32'was not validated by the checking -
     A**********                            process.'
     A**********
     A**********R INCDPR                    TEXT('INCOMPLETE DEPENDENCIES')
     A**********                            SPACEB(1)
     A**********                           2'The dependency relation defining c-
     A**********                            omponent'
     A**********  D2DSON        10A       45
     A**********  D2DSOS         5A       56
     A**********                          62'as depending on'
     A**********  D2DDON        10A       78
     A**********  D2DDOS         5A       89
     A**********                          95'was not validated by the'
     A**********                            SPACEA(1)
     A**********                           2'checking process.'
     A**********
     A*
     A          R NODTRP                    TEXT('NO DETAILS TO REPORT')
     A                                      SPACEB(3)
     A                                     2'The validation of the Close of Bus-
     A                                      iness Component and Dependency file-
     A                                      s in your system has completed norm-
     A                                      ally. The files are correct.'
     A** Start of CCB024
     A  37                                 2'The number of predecessor dependen+
     A                                       cies defined on the Components +
     A                                       file for the following +
     A                                       Component(s) exceeds 40.'
     A  37                                  SPACEB(3)
     A  37                                 2'This will not prevent the Close +
     A                                       of Business to function normally +
     A                                       but may impact the COB Modelling +
     A                                       Utility if used by the Bank.'
     A  37                                  SPACEB(1)
     A          R MODEL                     TEXT('Components Above 40 +
     A                                            Predecessors')
     A                                      SPACEB(2)
     A            D3COTT        10A        2
     A            D3CSEQ         5A       13
     A            D3CDSC        50A       28
     A                                    83'Dependency Count: '
     A            D3DPCT         3A      101SPACEA(1)
     A
     A**End of CCB024
     A*
     A** Start Of S01417
     A          R LOOPFND                   TEXT('REPORT END')
     A                                      SPACEB(2)
     A                                     2'In validating the +
     A                                      Dependencies file, a +
     A                                      Dependency loop has been +
     A                                      discovered. See Report +
     A                                      CB9010 for details'
     A** End Of S01417
     A*
     A          R RPTEND                    TEXT('REPORT END')
     A                                      SPACEB(3)
     A                                    56'*** End of report ***'
     A*
