      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CB COB component level file report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Close of Business Processing Utilities
     A*                                                               *
     A*   PRTF/CHKLVLP1  CLOSE OF BUSINESS COMPONENT LEVEL REPORT     *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO.  XXXXXX           DATE  00XXX00               *
     A*                                                               *
     A*****************************************************************
     A          R RPTHDG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                  3  3'REFERENCE CHKLVLP1'
     A            BJURPT        53A     3 37TEXT('BANK REPORT TITLE')
     A                                  3107'DATE'
     A            BJMRDT         7A     3112TEXT('MIDAS RUN DATE')
     A                                  3122'PAGE'
     A            PAGE           5S 0   3127 EDTCDE( 4)
     A*
     A          R SUBHDG                    TEXT('REPORT SUB-HEADING')
     A                                      SPACEB(1)
     A                                    51'CLOSE OF BUSINESS COMPONENT LEVEL -
     A                                      CHART'
     A                                      SPACEA(1)
     A                                    51'-----------------------------------
     A                                      -----'
     A                                      SPACEA(1)
     A                                    17'Depended-on Component'
     A                                    50'Level no'
     A                                    58'Component'
     A                                    69'Seq.no.'
     A                                   100'Depends-on component'
     A                                      SPACEA(1)
     A*
     A          R DEPDON                    TEXT('DEPENDED ON COMPONENT')
     A                                      SPACEB(1)
     A            ARR1W         48A        1TEXT('SIX COMPONENTS')
     A*
     A          R COMDET                    TEXT('COMPONENT DETAILS')
     A            D0FLVL         3  0     50TEXT('First Level Order')
     A                                      EDTCDE(4)
     A                                    53'.'
     A            D0SLVL         3  0     54TEXT('Second Level Order')
     A            D0COTT        10        58TEXT('Component Name')
     A            D0CSEQ         5        69TEXT('Component Sequence Number')
     A*
     A          R DEPSON                    TEXT('DEPENDS ON COMPONENT')
     A            ARR2W         48A       83TEXT('SIX COMPONENTS')
     A*
     A          R DBELIN                    TEXT('DATABASE ERROR')
     A                                      SPACEB(3)
     A                                    45'Database error'
     A            DBASE          3  0     60
     A                                    64'in program'
     A            DBPGM         10A       74
     A                                      SPACEA(1)
     A                                    45'File'
     A            DBFILE         8A       50
     A                                    58', Key'
     A            DBKEY         29A       64
     A*
     A          R RPTEND                    TEXT('REPORT END')
     A                                      SPACEB(3)
     A                                    56'*** End of report ***'
     A*
