     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Close of business halt modules')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business processing                         *
      *                                                               *
      *  CB0047 - Close of Business halt modules maintenance          *
      *                                                               *
      *  Function:  This program allows a user to request a specific  *
      *             halt.                                             *
      *                                                               *
      *  Called By: CB0020  - Close of Business halt menu             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 108076 *CREATE     Date 130996                *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  108076 - Create new program for processing of new option     *
      *                                                               *
      *****************************************************************
     FCBCOMSL0UF  E           K        DISK         KINFSR *PSSR      UC
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
     C*
     C* Open the file in turn making it user control
     C*
     C                     OPEN CBCOMSL0
     C*
     C*
     C* Called from program CB0020, passing parameters
     C*
     C           *ENTRY    PLIST
     C                     PARM           COMNAM 10
     C                     PARM           COMNUM  5
     C                     PARM           MODE    1
     C                     PARM           RTNCDE  5
     C*
     C* Make a keylist for search argument
     C*
     C           COMBKY    KLIST
     C                     KFLD           COMNAM
     C                     KFLD           COMNUM
     C*
     C           COMBKY    CHAINCBCOMSL0             90
     C*
     C* If no records found
     C*
     C           *IN90     IFEQ *ON
     C                     MOVEL'*NRF'    RTNCDE
     C                     ENDIF
     C*
     C* If record found
     C*
     C           *IN90     IFEQ *OFF
     C*
     C           DHCSTS    IFNE *BLANKS
     C                     MOVEL'*COMP'   RTNCDE
     C*
     C                     ELSE
     C*
     C                     SELEC
     C*
     C           MODE      WHEQ 'A'
     C                     MOVE 'Y'       DHCHTA
     C*
     C           MODE      WHEQ 'B'
     C                     MOVE 'Y'       DHCHTB
     C*
     C                     ENDSL
     C*
     C* Update the file using the record format, if it fails then
     C* show an error message
     C*
     C                     UPDAT@DHREGG                91
     C*
     C           *IN91     IFEQ *OFF
     C                     MOVE *BLANKS   RTNCDE
     C                     ELSE
     C                     MOVEL'*FAIL'   RTNCDE
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C*
     C                     CLOSECBCOMSL0
     C*
     C                     SETON                         LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     MOVEL'*FAIL'   RTNCDE
     C                     END
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
