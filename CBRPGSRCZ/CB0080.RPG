     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Restart of COB after sched. fails')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing
     F*                                                               *
     F*  CB0080 - RESTART OF COB AFTER SCHEDULER FAILS                *
     F*                                                               *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  PREV AMEND NO.                    DATE 00/00/00              *
     F*                                                               *
     F*****************************************************************
     FCBCOMSLBUF  E           K        DISK
     F*
      /SPACE 2
     F********************************************************************
     F*
     F*     C  L   FUNCTION OF INDICATORS
     F*
     F*    01      NO RECORDS IN COMPONENTS FILE - CBCOMSLB
     F*
     F*       LR   LAST RECORD INDICATOR
     F*
     F*     U7&U8  ERROR IN COMPONENT CALLED
     F*
     F*****************************************************************
     F/EJECT
     E                    CPY@    1   1 80
     E** COPYRIGHT ARRAY
     E*
     E*****************************************************************
     E/EJECT
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     I*****************************************************************
     I/EJECT
     C*****************************************************************
     C*
     C**   ACCEPT PARAMETER FOR CALL TO CBC0085
     C*
     C           *ENTRY    PLIST
     C                     PARM           STREAM  20
     C*
     C**   READ FIRST RECORD FROM LF/CBCOMSLB
     C*
     C                     READ CBCOMSLB                 01
     C*
     C**   REPEAT WHILE RECORDS FOUND
     C*
     C           *IN01     DOWEQ'0'
     C*
     C**   CHANGE STATUS TO RESTART AND SCHEDULING PRIORITY TO 1
     C*
     C                     MOVE 'R'       DHCSTS
     C                     Z-ADD1         DHCSHP
     C*
     C**   UPDATE RECORD
     C*
     C                     EXCPTCOMOUT
     C*
     C**   READ NEXT RECORD FROM LF/CBCOMSLB
     C*
     C                     READ CBCOMSLB                 01
     C*
     C                     END
     C*
     C**   CALL CBC0085 TO RESET DTAQ'S CBDTQA & CBDTQC
     C*
     C                     MOVE 'N'       NFLD    1
     C                     MOVE 'N'       ERR     1
     C                     CALL 'CBC0085'
     C                     PARM           NFLD
     C                     PARM           STREAM
     C                     PARM           ERR
     C*
     C**   IF ERR = 'Y' THEN ERROR OCCURED IN CALL - SET ON U7 & U8
     C*
     C           ERR       IFEQ 'Y'
     C                     SETON                       U7U8
     C                     END
     C*
     C                     SETON                         LR
     C*
     C*****************************************************************
      /EJECT
     O*****************************************************************
     O*
     O**   UPDATE STATUS & SCHEDULING PRIORITY FIELDS ON LF/CBCOMSLB
     O*
     O@CMPNHR E                COMOUT
     O*
     O                         DHCSTS
     O                         DHCSHP
     O*
     O********************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
