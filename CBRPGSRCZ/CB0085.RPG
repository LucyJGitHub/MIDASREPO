     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Ensure file integr after COB halt')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing
     F*                                                               *
     F*  CB0085 - ENSURE FILE INTEGRITY AFTER COB HALT                *
     F*                                                               *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  PREV AMEND NO.                    DATE 00/00/00              *
     F*                                                               *
     F*****************************************************************
     F*
     FCBCOMSPDIF  E           K        DISK
     FCBCOMSL0UF  E           K        DISK
     F                                              KCOMIT
     FCBDEPSL3UF  E           K        DISK
     F                                              KCOMIT
     F*
      /SPACE 2
     F********************************************************************
     F*
     F*     C  L   FUNCTION OF INDICATORS
     F*
     F*    01      RECORD NOT FOUND ON PF/CBCOMSPD
     F*    02      RECORD NOT FOUND ON LF/CBDEPSL3
     F*    03      RECORD NOT FOUND ON LF/CBCOMSL0
     F*
     F*       LR   LAST RECORD INDICATOR
     F*
     F*      U7&U8 DATABASE ERROR
     F*
     F*****************************************************************
      /EJECT
     E*****************************************************************
     E*
     E                    CPY@    1   1 80
     E** COPYRIGHT ARRAY
     E*
     E*****************************************************************
      /EJECT
     I*****************************************************************
     I*
     ICOMKDS      DS
     I                                        1  10 DHCOTT
     I                                       11  15 DHCSEQ
     I** DATA STRUCTURE FOR KEY FIELD COMKEY TO BE MOVED INTO DBKEY FIELD
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**     LOCAL DATA AREA
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     I*****************************************************************
      /EJECT
     C*****************************************************************
     C*
     C**   KEY TO CHAIN TO CBCOMSL0
     C*
     C           COMKEY    KLIST
     C                     KFLD           COMNAM 10
     C                     KFLD           COMSEQ  5
     C*
     C**   KEY TO CHAIN TO CBDEPSL3
     C*
     C           DEPKEY    KLIST
     C                     KFLD           COMPNM 10
     C                     KFLD           COMPSQ  5
     C*
     C** SET UP PROGRAM NAME FOR DATABASE ERRORS
     C*
     C                     MOVEL'CB0085  'DBPGM
     C                     MOVE *BLANKS   DBKEY
     C*
     C**   READ FIRST RECORD FROM PF/CBCOMSPD
     C*
     C                     READ CBCOMSPD                 01
     C*
     C**   DOW RECORDS EXIST ON PF/CBCOMSPD WITH DHABST = 'C'
     C*
     C           *IN01     DOWEQ'0'
     C*
     C           DHCSTS    IFEQ 'C'
     C*
     C**   FIND CORRESPONDING RECORD ON LF/CBDEPSL3
     C*
     C                     MOVE DHCOTT    COMPNM
     C                     MOVE DHCSEQ    COMPSQ
     C           DEPKEY    SETLLCBDEPSL3
     C           DEPKEY    READECBDEPSL3                 02
     C*
     C**   DOW RECORDS FOUND ON LF/CBDEPSL3
     C*
     C           *IN02     DOWEQ'0'
     C*
     C**   AMEND DEPENDENCY STATUS TO 'COMPLETE' AND UPDATE FILE
     C*
     C                     MOVE 'C'       DICDST
     C                     EXCPTDEPOUT
     C*
     C**   REFLECT CHANGE ON LF/CBCOMSL0
     C*
     C                     MOVE DIDSON    COMNAM
     C                     MOVE DIDSOS    COMSEQ
     C           COMKEY    SETLLCBCOMSL0
     C           COMKEY    READECBCOMSL0                 03
     C*
     C**   IF RECORD NOT FOUND THEN DATABASE ERROR
     C*
     C           *IN03     IFEQ '1'
     C                     MOVE *BLANKS   DBKEY
     C                     MOVELCOMKDS    DBKEY
     C                     MOVEL'CMPL0'   DBFILE           ***************
     C                     Z-ADD1         DBASE            * DB ERROR 01 *
     C                     EXSR DBERR                      ***************
     C                     END
     C*
     C**   SUBTRACT 1 FROM THE NUMBER OF OUTSTANDING DEPENDENCIES AND
     C**   UPDATE LF/CBCOMSL0
     C*
     C                     SUB  1         DHCODP
     C                     EXCPTCOMOUT
     C*
     C**   READ NEXT RECORD FROM LF/CBDEPSL3 WITH SAME KEY
     C*
     C           DEPKEY    READECBDEPSL3                 02
     C*
     C                     END
     C*
     C                     COMIT
     C*
     C                     END
     C                     READ CBCOMSPD                 01
     C*
     C                     END
     C                     SETON                         LR
     C*************************************************************
      /EJECT
     C*************************************************************
     C*
      ** S/R DBERR TO PERFORM DATABASE ERROR EXIT FROM PROGRAM
      *
      ** CALLED FROM MAIN CYCLE
      *
      ** CALLS NO OTHER SUBROUTINES
     C*
     C*************************************************************
     C*
     C           DBERR     BEGSR
     C*
     C** SETON INDICATORS U7 U8 LR
     C** AND RETURN TO CALLING PROGRAM
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     O********************************************************************
     O*
     O**   UPDATE NO.OF OUTSTANDING DEPENDENCIES FIELD ON LF/CBCOMSL0
     O*
     O@DHREGG E                COMOUT
     O*
     O                         DHCODP
     O*
     O**   UPDATE DEPENDENCY STATUS FIELD ON LF/CBDEPSL3
     O*
     O@DPRLHS E                DEPOUT
     O*
     O                         DICDST
     O********************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
