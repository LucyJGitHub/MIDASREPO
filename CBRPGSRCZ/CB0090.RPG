     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Restart of COB after m/c crash')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing
     F*                                                               *
     F*  CB0090 - RESTART OF COB AFTER MACHINE CRASH                  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. LN0545             DATE 20JUL90               *
     F*                                                               *
     F*****************************************************************
     F*                                                               **
     F*  LN0545 - RE-TRY THE LOCK OF CBSTAT TO AVOID POTENTIAL        *
     F*           COB CRASH AS A NUMBER OF PROGRAMS PERFORM THE       *
     F*           *LOCK TO UPDATE THE DATA AREA                       *
     F*****************************************************************
     FCBCOMSL7UF  E           K        DISK
     F*
      /SPACE 2
     F********************************************************************
     F*
     F*     C  L   FUNCTION OF INDICATORS
     F*
     F*    01      END OF FILE IN CBCOMSL7
     F*    70      RE-TRY LOCK OF DATA AREA                               LN0545
     F*
     F*       LR   LAST RECORD INDICATOR
     F*
     F*     U7&U8  ERROR IN COMPONENT CALLED  (CBC0085)
     F*
     F*****************************************************************
     F/EJECT
     E                    CPY@    1   1 80
     E** COPYRIGHT ARRAY
     E*
     E*****************************************************************
     E/EJECT
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     ICBSTAT      DS                             50
     I                                       18  19 NOAS
     I** DATA AREA CBSTAT
     I*
     I*****************************************************************
     I/EJECT
     C*****************************************************************
     C*
     C**   ACCEPT PARAMETER FOR CALL TO CBC0085
     C*
     C           *ENTRY    PLIST
     C                     PARM           STREAM  20
     C*
     C** DEFINE DATA AREA CBSTAT
     C*
     C           *NAMVAR   DEFN           CBSTAT
     C*
     C**   READ FIRST RECORD FROM LF/CBCOMSL7
     C*
     C                     READ CBCOMSL7                 01
     C*
     C**   REPEAT WHILE RECORDS FOUND
     C*
     C           *IN01     DOWEQ'0'
     C*
     C**   CHANGE STATUS TO RESTART AND SCHEDULING PRIORITY TO 1
     C*
     C                     MOVE 'R'       DHCSTS
     C                     Z-ADD1         DHCSHP
     C*
     C**   UPDATE RECORD
     C*
     C                     EXCPTCOMOUT
     C*
     C**   READ NEXT RECORD FROM LF/CBCOMSL7
     C*
     C                     READ CBCOMSL7                 01
     C*
     C                     END
     C*
     C**   UPDATE NO. OF ACTIVE STREAMS ON CBSTAT TO '00'
     C*
     C           *IN70     DOUEQ'0'                                       LN0545
     C************LOCK     IN   CBSTAT                                    LN0545
     C           *LOCK     IN   CBSTAT                 70                 LN0545
     C                     END                                            LN0545
     C                     MOVE '00'      NOAS
     C                     OUT  CBSTAT
     C*
     C**   CALL CBC0085 TO RESET DTAQ'S CBDTQA, CBDTQB & CBDTQC
     C*
     C                     MOVE 'Y'       NFLD    1
     C                     MOVE 'N'       ERR     1
     C                     CALL 'CBC0085'
     C                     PARM           NFLD
     C                     PARM           STREAM
     C                     PARM           ERR
     C*
     C**   IF ERR = 'Y' THEN ERROR OCCURED IN CALL - SET ON U7 & U8
     C*
     C           ERR       IFEQ 'Y'
     C                     SETON                       U7U8
     C                     END
     C*
     C                     SETON                         LR
     C*
     C*****************************************************************
      /EJECT
     O*****************************************************************
     O*
     O**   UPDATE STATUS & SCHEDULING PRIORITY FIELDS ON LF/CBCOMSL7
     O*
     O@CMPNHN E                COMOUT
     O*
     O                         DHCSTS
     O                         DHCSHP
     O*
     O********************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
