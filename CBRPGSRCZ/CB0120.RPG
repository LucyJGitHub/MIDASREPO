     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Amend component not to run today')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing
     F*                                                               *
     F*  CB0120 - AMEND COMPONENT NOT TO RUN TODAY                    *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO.                    DATE 00/00/00              *
     F*                                                               *
     F*****************************************************************
     FCBCOMSL0UF  E           K        DISK
     F*
      /SPACE 2
     F********************************************************************
     F*
     F*     C  L   FUNCTION OF INDICATORS
     F*
     F*
     F*    50      FAIL ON CHAIN TO CBCOMSL0
     F*       LR   END OF FILE
     F*
     F*    U7&U8   DATABASE ERROR
     F*
     F*****************************************************************
     F/EJECT
     E*
     E                    CPY@    1   1 80
     E** COPYRIGHT ARRAY
     E*
     E*****************************************************************
     E/EJECT
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**     LOCAL DATA AREA
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I** DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     ICKEYDS      DS
     I                                        1  10 CMPNMX
     I                                       11  15 CMPSQX
     I** DATA STRUCTURE FOR KEY FIELD CKEY TO BE MOVED INTO DBKEY FIELD
     I*
     I*****************************************************************
     I/EJECT
     C*
     C** LIST FOR PARAMETER PASSED TO PROGRAM
     C*
     C           *ENTRY    PLIST
     C                     PARM           COMPNM 10
     C                     PARM           COMPSQ  50
     C                     PARM           ERROR   1
     C*
     C** KEY LIST FOR CHAIN TO CBCOMSL0
     C*
     C           CKEY      KLIST
     C                     KFLD           COMPNM
     C                     KFLD           COMPSA  5
     C*
     C                     MOVE COMPSQ    COMPSA
     C*
     C** SET UP PROGRAM NAME FOR DATABASE ERRORS
     C*
     C                     MOVEL'CB0120  'DBPGM
     C                     MOVE *BLANKS   DBKEY
     C*
     C** CHAIN TO CBCOMSL0 FOR COMPONENT DETAILS
     C*
     C           CKEY      CHAIN@DHREGG              50
     C*
     C** IF COMPONENT NOT FOUND SET ERROR PARAMETER TO 'Y'
     C** AND EXIT PROGRAM VIA DATABASE ERROR SUBROUTINE
     C*
     C           *IN50     IFEQ '1'
     C                     MOVE 'Y'       ERROR   1
     C                     MOVE COMPNM    CMPNMX
     C                     MOVE COMPSQ    CMPSQX
     C                     MOVE 'CMPL0'   DBFILE
     C                     MOVELCKEYDS    DBKEY            ***************
     C                     Z-ADD1         DBASE            * DB ERROR 01 *
     C                     EXSR DBERR                      ***************
     C                     END
     C*
     C** UPDATE REQUIRED TODAY FIELD TO 'N'
     C*
     C                     MOVE 'N'       DHCRQD
     C                     EXCPTRCOMU
     C*
     C** RETURN TO CALLING PROGRAM
     C*
     C                     SETON                     LR
     C*
      /EJECT
     C*************************************************************
     C*
     C** S/R DBERR TO PERFORM DATABASE ERROR EXIT FROM PROGRAM
     C*
     C** CALLED FROM MAIN CYCLE
     C*
     C** CALLS NO OTHER SUBROUTINES
     C*
     C*****************************************************************
     C*
     C           DBERR     BEGSR
     C*
     C** SETON INDICATORS U7 U8 LR
     C** AND RETURN TO CALLING PROGRAM
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     O********************************************************************
     O*
     O** UPDATE PARAMETERS AND REQUIRED TODAY FIELDS ON COMPONENTS FILE
     O*
     O@DHREGG E                RCOMU
     O*
     O                         DHCRQD
     O********************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
