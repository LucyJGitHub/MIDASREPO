     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Set on halt after component flag')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing
     F*                                                               *
     F*   CB0175 - SET ON HALT AFTER COMPONENT FLAG                   *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. LN0465             Date 02Jul90               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*   LN0465 - PROGRAM TO SET ON HALT AFTER COMPONENT FLAG        *
     F*            FOR MISSING ACCOUNT KEY PROCESSING                 *
     F*                                                               *
     F*****************************************************************
      /EJECT
     FCBCOMSL0UF  E           K        DISK
     F                                              KINFSR COMSSR
      *  COB components by name and sequence number.
      *  -------------------------------------------
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *           FUNCTION OF INDICATORS                              *
      *           ----------------------                              *
      *  10       Record not found on LF/CBCOMSL0.                    *
      *  20       Loop prevention indicator for LF/CBCOMSL0.          *
      *  U7       DB error.                                           *
      *  U8       DB error.                                           *
      *  LR       Last record indicator.                              *
      *                                                               *
      *---------------------------------------------------------------*
     E                    CPY@    1   1 80
      /EJECT
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      ** Local data area.
      *
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  Main process -  Sets on halt after component flag on the     *
      *                  relevant COB component record with status.   *
      *                                                               *
      *---------------------------------------------------------------*
     C                     MOVEACPY@      BIS@   80
      *
      **  Entry parameter list and key list to access components file.
      *
     C           *ENTRY    PLIST
     C                     PARM           P@CNAM 10
     C                     PARM           P@CSEQ  50
      *
     C           CMSKEY    KLIST
     C                     KFLD           P@CNAM
     C                     KFLD           PACSEQ
      *
     C                     MOVE P@CSEQ    PACSEQ  5
      *
      ** Read record.
      *
     C           CMSKEY    CHAINCBCOMSL0             10
      *
     C           *IN10     IFEQ '0'
      *
      ** Update.
      *
     C                     MOVE 'Y'       DHCHTA
     C                     EXCPTUPDATE
      *
     C                     ELSE
      *
      ** DB error.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE 'CBCOMSL0'DBFILE
     C                     MOVELP@CNAM    KEY    15
     C                     MOVE PACSEQ    KEY
     C                     MOVELKEY       DBKEY            ***************
     C                     MOVE 'CB0175'  DBPGM            * DB ERROR 01 *
     C                     Z-ADD01        DBASE            ***************
     C                     OUT  LDA
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
      *
     C                     END
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      /EJECT
      *---------------------------------------------------------------*
      *  Index to subroutines.                                        *
      *                                                               *
      *  COMSSR - file exception/error subroutine.                    *
      *  *PSSR  - program exception/error subroutine.                 *
      *                                                               *
      *---------------------------------------------------------------*
      /SPACE 3
      *---------------------------------------------------------------*
      *                                                               *
      *  COMSSR subroutine - in the event of a file exception error , *
      *                      for LF/CBCOMSL0 , this subroutine will   *
      *                      produce a dump.                          *
      *                                                               *
      *  Calls subroutine : *PSSR                                     *
      *  Called from : main process                                   *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           COMSSR    BEGSR
      *
     C           *LOCK     IN   LDA
     C                     MOVE 'CBCOMSL0'DBFILE
     C                     MOVELP@CNAM    KEY
     C                     MOVE PACSEQ    KEY
     C                     MOVE KEY       DBKEY            ***************
     C                     MOVE 'CB0175'  DBPGM            * DB ERROR 02 *
     C                     Z-ADD02        DBASE            ***************
     C                     OUT  LDA
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
      *
     C                     EXSR *PSSR
      *
     C                     ENDSR
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  *PSSR subroutine - in the event of a programme exception/    *
      *                     error , this subroutine will produce a    *
      *                     dump and return to caller.                *
      *                                                               *
      *  Called from : COMSSR                                         *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           *PSSR     BEGSR
      *
     C           *IN20     IFEQ '0'
     C                     MOVE '1'       *IN20
     C                     DUMP
     C                     END
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
     O@DHREGG E                UPDATE
     O                         DHCHTA
** CPY@
(c) Finastra International Limited 2001
