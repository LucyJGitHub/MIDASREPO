     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Retreive previous job run attributes')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Module                             *
      *                                                               *
      *  CB0180 - Retrieve Job Attributes of Previous Run             *
      *                                                               *
      *  Function:  This program retrieves the job name, job user,    *
      *             and job number of the previous run of this job.   *
      *             It is used when a component is re-started to      *
      *             determine entries to be removed from database     *
      *             files.                                            *
      *                                                               *
      *  Called By: CBC0180 - Place job attributes in data area       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. 240724             Date 06Jun06               *
      *                 211714             Date 26May06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CCB009  *CREATE    Date 01Jun00               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  211714 - COB component SDC4100 00015 produced a dump for     *
      *           CB0180. Place additional conditions to prevent      *
      *           the dumping of CB0180.                              *
      *  CCB009 - Journal files throughout the close of business.     *
      *                                                               *
      *****************************************************************
     FCBAUDTL1IF  E           K        DISK
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Define data area LDA.
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Program parameter list.
      *
     C           *ENTRY    PLIST
     C                     PARM           COMP   10
     C                     PARM           COMS    5
     C                     PARM           #JOB   10
     C                     PARM           #USR   10
     C                     PARM           #NBR    6
      *
      ** Set up variables.
      *
     C                     MOVE *BLANKS   BLNK10 10
     C                     MOVE *BLANKS   BLNK6   6
     C                     MOVE BLNK10    #JOB   10
     C                     MOVE BLNK10    #USR   10
     C                     MOVE BLNK6     #NBR    6
      *
      ** Set up key to close of business error audit file.
      *
     C           CBAUKY    KLIST
     C                     KFLD           COMN   10
     C                     KFLD           COSQ    50
     C                     KFLD           SDAT    50
     C                     KFLD           STIM    60
      *
     C                     MOVE COMP      COMN
     C                     MOVE COMS      COSQ
     C                     MOVE COMS      COMSN   50
     C                     Z-ADD*HIVAL    STIM
     C                     Z-ADD*HIVAL    SDAT
      *                                                                                       240724
     C           SETLL     TAG                                                                240724
      *                                                                                       240724
     C           CBAUKY    SETLLCBAUDTL1
      *
      ** First READP will return current job.
      *
     C                     READPCBAUDTL1                 88
      *                                                                                       240724
      ** If the record read is greater than the SETLL key values,                             240724
      ** perform the SETLL again as it means another record has been                          240724
      ** written to the file in the meantime.                                                 240724
      *                                                                                       240724
     C           *IN88     IFEQ '0'                                                           240724
     C           CCOT      IFGT COMN                                                          240724
     C           CCOT      OREQ COMN                                                          240724
     C           CSEQ      ANDGTCOMSN                                                         240724
     C                     GOTO SETLL                                                         240724
     C                     END                                                                240724
     C                     END                                                                240724
     C           COMN      IFEQ 'SDC4100 '                                                    211714
     C           COMN      ANDEQCCOT                                                          211714
     C           COMSN     ANDLTCSEQ                                                          211714
      *                                                                                       211714
     C                     MOVE '1'       *IN10                                               211714
     C                     ELSE                                                               211714
     C                     MOVE '0'       *IN10                                               211714
      *
      ** If record not found, database error.
      *
     C           COMN      IFNE CCOT
     C           COMSN     ORNE CSEQ
      *
      ** Data base error in file CBAUDTL1
      *
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'CBAUDTL1'DBFILE           *DB ERROR 001 *
     C                     MOVELCOMN      DBKEY            ***************
     C                     MOVEL'CB0180'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
     C                     ENDIF                                                              211714
      *
      ** Clear job fields.
      *
     C                     MOVE BLNK10    JOJOB
     C                     MOVE BLNK10    JOUSER
     C                     MOVE BLNK6     JONBR
      *
      ** Second READP will return previous job.
      *
     C                     READPCBAUDTL1                 88
      *
      ** If previous run of this component is found,
      *
     C           COMN      IFEQ CCOT
     C           COMSN     ANDEQCSEQ
      *
      ** move job name, user, and number to parameter fields to
      ** return them to calling program.
      *
     C                     MOVE JOJOB     #JOB   10
     C                     MOVE JOUSER    #USR   10
     C                     MOVE JONBR     #NBR    6
      *
     C                     ENDIF
      *
      ** Return to calling program.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: Mainline processing                                *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
