     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Dummy pgm to bypass failed comps')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing
     F*                                                               *
     F*  CB0500 - DUMMY PROGRAM TO BYPASS FAILED COMPONENTS           *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CCB015             Date 09Mar06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01517             Date 10Aug94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB015 - CoB Audit Report Enhancement.                       *
     F*  S01517 - Allow Component Bypass in COB                       *
     F*           N.B. This program will be called by CB0065 and the  *
     F*           error processing was included to prevent the program*
     F*           from looping and database error to be reported      *
     F*****************************************************************
     FCBCOMSL8IF  E           K        DISK                               S01117
     FCBCOMSL0UF  E           K        DISK                               S01117
     E*
     E** COPYRIGHT ARRAY
     E                    CPY@    1   1 80
     E*
     E*****************************************************************
     E/EJECT
     I*
     I** CBSTAT DATA AREA
     I*
     ICBSTAT     UDS                             50
     I                                       14  160CBFAIL
     I*
     I** LOCAL DATA AREA STRUCTURE
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     IMSGDS       DS
     I                                        1  10 NAM
     I                                       11  15 SEQ
     I                                       16  16 STATUS
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *                                                                                       CCB015
      ** Data Structure for AOSVALR0 string                                                   CCB015
      *                                                                                       CCB015
     ISVAL1       DS                            200                                           CCB015
     I                                        1   1 SVAL11                                    CCB015
      *                                                                                       CCB015
     I              'COBFlagBypass'       C         COBFBP                                    CCB015
      *                                                                                       CCB015
     C*
     C           *ENTRY    PLIST
     C                     PARM           CMTNAM 10
     C***********          PARM           CMTSEQ  5
     C           WSEQR     PARM WSEQI     CMTSEQ  5                       S01517
     C*
     C** DEFINE LDA TO PROGRAM
     C*
     C           *NAMVAR   DEFN           LDA
     C*
     C** DEFINE CBSTAT TO PROGRAM
     C*
     C           *NAMVAR   DEFN           CBSTAT
     C*
     C** KEY LIST FOR CBCOMSL8 AND CBCOMSL0
     C*
     C           COMKEY    KLIST
     C                     KFLD           CMTNAM
     C                     KFLD           CMTSEQ
     C*                                                                   S01517
     C**   Define work fields                                             S01517
     C*                                                                   S01517
     C           *LIKE     DEFN CMTSEQ    WSEQR                           S01517
     C           *LIKE     DEFN CMTSEQ    WSEQI                           S01517
      *                                                                                       CCB015
      ** Ascertain if feature CCB015 is On, call AOSARDR0                                     CCB015
      *                                                                                       CCB015
     C                     CALL 'AOSARDR0'                                                    CCB015
     C                     PARM '       ' @RTCD   7                                           CCB015
     C                     PARM '*VERIFY' @OPTN   7                                           CCB015
     C                     PARM 'CCB015'  @SARD   6                                           CCB015
      *                                                                                       CCB015
     C           @RTCD     IFEQ *BLANKS                                                       CCB015
     C                     MOVEL'Y'       CCB015  1                                           CCB015
     C                     ELSE                                                               CCB015
     C                     MOVEL'N'       CCB015                                              CCB015
     C                     ENDIF                                                              CCB015
      *                                                                                       CCB015
      ** Ascertain if system value 'COBFlagBypass' is On, call AOSVALR0                       CCB015
      *                                                                                       CCB015
     C                     CALL 'AOSVALR0'                                                    CCB015
     C                     PARM           @RTCD                                               CCB015
     C                     PARM COBFBP    SVALK1 20                                           CCB015
     C                     PARM           SVAL1 200                                           CCB015
     C                     PARM           SVALK2 20                                           CCB015
     C                     PARM           SVAL2 200                                           CCB015
     C                     PARM           SVALK3 20                                           CCB015
     C                     PARM           SVAL3 200                                           CCB015
     C                     PARM           SVALK4 20                                           CCB015
     C                     PARM           SVAL4 200                                           CCB015
     C                     PARM           SVALK5 20                                           CCB015
     C                     PARM           SVAL5 200                                           CCB015
     C                     PARM           SVALK6 20                                           CCB015
     C                     PARM           SVAL6 200                                           CCB015
     C                     PARM           SVALK7 20                                           CCB015
     C                     PARM           SVAL7 200                                           CCB015
     C                     PARM           SVALK8 20                                           CCB015
     C                     PARM           SVAL8 200                                           CCB015
     C                     PARM           SVALK9 20                                           CCB015
     C                     PARM           SVAL9 200                                           CCB015
     C                     PARM           SVALK0 20                                           CCB015
     C                     PARM           SVAL10200                                           CCB015
      *                                                                                       CCB015
     C           @RTCD     IFNE *BLANKS                                                       CCB015
      *                                                                                       CCB015
     C           SVAL10    IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK0    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL9     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK9    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL8     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK8    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL7     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK7    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL6     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK6    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL5     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK5    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL4     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK4    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL3     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK3    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL2     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK2    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
     C           SVAL1     IFEQ '*NRF'                                                        CCB015
     C                     MOVE SVALK1    DBKEY                                               CCB015
     C                     ENDIF                                                              CCB015
      *                                                                                       CCB015
     C           *LOCK     IN   LDA                                                           CCB015
     C                     Z-ADD4         DBASE                                               CCB015
     C                     MOVE 'SDSVALPD'DBFILE                                              CCB015
     C                     MOVE 'CB0500'  DBPGM                                               CCB015
     C                     OUT  LDA                                                           CCB015
     C                     EXSR DBERR                                                         CCB015
     C                     ENDIF                                                              CCB015
     C*
     C** CHECK THAT PROGRAM PASSED AS PARAMETER HAS ACTULLY FAILED
     C*
     C           COMKEY    CHAIN@CMPNHP              30
     C*
     C** IF HAS NOT FAILED,DATABASE ERROR
     C*
     C           *IN30     IFEQ '1'
     C                     MOVEL'CB0500'  DBPGM
     C                     MOVEL'CBCOMSPD'DBFILE
     C                     MOVELCMTNAM    DBKEY
     C                     MOVE CMTSEQ    DBKEY
     C                     Z-ADD01        DBASE
     C                     SETON                     U7U8LR
     C                     EXSR DBERR                                     S01517
     C                     GOTO END
     C                     END
     C*
     C** ACCESS RECORD FROM CBCOMSPD AND CHANGE COMPONENT STATUS FIELD
     C** TO COMPLETE, AND THEN UPDATE THE RECORD
     C*
     C           COMKEY    CHAIN@DHREGG              30
     C                     MOVE 'C'       DHCSTS
     C*
     C** SET COMPONENT END TIME (DHCETI) TO SYSTEM TIME
     C*
     C                     CALL 'CBTIME'                                  LN0278
     C                     PARM           DAYNOA  5                       LN0278
     C                     PARM           STIME   6                       LN0278
     C                     PARM           DFMT    1                       LN0278
     C                     MOVE STIME     DHCETI                          LN0278
     C*
     C** SET COMPONENT END DATE (DHCEDE) TO SYSTEM DATE USING
     C** VALUE FROM PROGRAM CBTIME                                        LN0278
     C*
     C                     MOVE DAYNOA    DHCEDE                          LN0278
     C/COPY WNCPYSRC,CB0500C001
     C           CCB015    IFEQ 'Y'                                                           CCB015
     C           SVAL11    ANDEQ'Y'                                                           CCB015
     C                     MOVE 'B'       DHCFAL                                              CCB015
     C                     ENDIF                                                              CCB015
     C*
     C                     EXCPTCOMS1
     C*
     C** REDUCE NUMBER OF FAILED COMPONENTS ON CBSTAT BY ONE
     C*
     C                     SUB  1         CBFAIL
     C                     OUT  CBSTAT
     C*
     C** SEND MESSAGE TO SAY THAT THIS COMPONENT HAS COMPLETED
      ** THIS DOES THE DEPENDENCIES FILE UPDATE AND THE REDUCTION OF
      ** THE NUMBER OF OUTSTANDING DEPENDENCIES ON THE COMPONENTS FILE.
     C*
     C                     Z-ADD16        LEN
     C                     MOVE 'C'       STATUS
     C                     MOVELCMTNAM    NAM
     C                     MOVELCMTSEQ    SEQ
     C                     CALL 'QSNDDTAQ'
     C                     PARM 'CBDTQA'  DTAQ   10
     C                     PARM '*LIBL'   LIB    10
     C                     PARM           LEN     50
     C                     PARM MSGDS     MSG    16
     C*                                                                   S01517
     C                     OUT  LDA                                       S01517
     C                     SETON                         LR               S01517
     C                     RETRN                                          S01517
     C*
     C           END       TAG
     C*
     C/EJECT
     C********************************************************************
     C*
     C** DATABASE ERROR SUBROUTINE TO PERFORM DATABASE ERROR
     C** EXIT FROM PROGRAM
     C*
     C** CALLED FROM MAIN CYCLE
     C*
     C** CALLS NO OTHER SUBROUTINES
     C*
     C********************************************************************
     C*
     C           DBERR     BEGSR
     C*
     C** SETON INDICATORS U7 U8 LR AND RETURN TO CALLING PROGRAM
     C*
     C                     SETON                     U7U8LR
     C                     OUT  LDA
     C                     MOVE '99999'   WSEQI                           S01517
     C                     DUMP                                           S01517
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C/EJECT
     O*
     O** OUTPUT FOR COMPONENT STATUS FIELD ON CBCOMSPD
     O*
     O@DHREGG E                COMS1
     O*
     O                         DHCSTS
     O                         DHCEDE
     O                         DHCETI
     O/COPY WNCPYSRC,CB0500O001
     O                         DHCFAL                                                         CCB015
     O*
**  CPY@
(c) Finastra International Limited 2001
