     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Number of users in system')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  CB0516 - User Counter                                        *
      *                                                               *
      *  Function:  This program is in charge to count the active     *
      *             users logged in CoB.                              *
      *                                                               *
      *  Called By: CBCMONB - Batch Monitoring                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CCB012             Date 26Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPK008             Date 01JUL97               *
      *                 090415             Date 12JUL95               *
      *                 S01420             Date 12APR95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CCB012 - Use 70 job streams for close of business.           *
      *           Recompile over changed CBTRAN.                      *
      *  CPK008 - DBA R2 Packaging                                    *
      *         - Recompile over DS/CBTRAN                            *
      *  090415 - The DSPW autosignoff should only apply to CBCMOND.  *
      *  S01420 - CoB Enhancements                                    *
      *                                                               *
      *****************************************************************
      /EJECT
     FCBUSERPDIF  E                    DISK
      *
     FCBUSERL1UF  E           K        DISK                           UC
     F            CBUSERD0                          KRENAMECBUSERDX
     F                                              KINFDS INFDS
     F                                              KINFSR *PSSR
     FCBDSCJPDUF  E           K        DISK                      A
     F                                              KINFSR *PSSR
      *
     E                    CPY@    1   1 80
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IWWDATA    E DSCBTRAN
      ** Transmitter data area
     IINFDS       DS
     I                                     *STATUS  STATUS
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      **
     I            DS
     I                                    B   1   40WWLENG
     I** RETURN DATA STRUCTURE
     I            DS
     I                                        1 111 WWRETR
     I                                       51  57 WWACTY
     I                                       98 107 WWPGMA
     I                                      108 111 WWSTAT
     I** TIME DATA STRUCTURE
     I            DS
     I                                        1   60WWTIMR
     I                                        1   20WWHOUR
     I                                        3   40WWMINT
     I                                        5   60WWSECO
     I            DS
     I                                        1  26 WWQUAL
     I                                        1  10 USJOB
     I                                       11  20 USUSER
     I                                       21  26 USNBR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -                                                       *
      * #MAIN    - MAIN SUBROUTINE                                       *
      * #INIT    - INITIAL PROCESSING SUBROUTINE                         *
      * #END     - END PROCESSING SUBROUTINE                             *
      * *PSSR    - ERROR SUBROUTINE                                      *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * QUSRJOBI - API COMMAND TO RETIEVE JOB INFORMATION                *
      *                                                                  *
      ********************************************************************
      *
     C                     EXSR #INIT
      *
     C                     EXSR #MAIN
      *
     C                     EXSR #END
      *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #MAIN    - MAIN SUBROUTINE                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * QUSRJOBI - API COMMAND TO RETIEVE JOB INFORMATION                *
      * *PSSR    - ERROR SUBROUTINE                                      *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRTRN   - RETURN PARAMETER                                      *
      * WWLENG   - LENGHT OF OUTPUT PARAMETER                            *
      * WWINPT   - INPUT PARAMETER FOR API COMMAND                       *
      * WWQUAL   - CONCATENATION OF JOB NAME, USER NAME & JOB NUMBER     *
      * WWINTR   - OUTPUT PARAMETER FOR API COMMAND                      *
      * WWRETR   - WORK FIELD FOR RETURN PARAMETER                       *
      * WWSTAT   - WORK FIELD FOR JOB STATUS                             *
      *                                                                  *
      ********************************************************************
     C           #MAIN     BEGSR
      *
     C                     READ CBUSERPD                 80
     C           *IN80     DOWEQ'0'
     C                     MOVELUSJOB     WWJOB
     C                     MOVELUSUSER    WWUSER
     C                     MOVE USNBR     WWNBR
     C                     CALL 'QUSRJOBI'             99
     C                     PARM           WWRTRN111
     C                     PARM           WWLENG
     C                     PARM           WWINPT
     C                     PARM           WWQUAL
     C                     PARM           WWINTR
     C                     MOVELWWRTRN    WWRETR
     C           *IN99     IFEQ '0'
     C           WWACTY    ANDEQ'*ACTIVE'
     C           COUNTR    ADD  1         COUNTR
     C                     EXSR #STUS
     C                     ELSE
     C                     EXSR #DELT
     C                     END
     C                     READ CBUSERPD                 80
     C                     END
      *
     C                     ENDSR
      ********************************************************************
     C           #DELT     BEGSR
     C**
     C                     OPEN CBUSERL1
     C           USDTAQ    CHAINCBUSERDX             81
     C           *IN81     IFEQ '0'
     C                     DELETCBUSERDX               98
     C           *IN98     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'CBUSERL1'DBFILE
     C                     Z-ADD1         DBASE
     C                     MOVEL'CB0516'  DBPGM
     C                     MOVELUSDTAQ    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C                     END
     C                     CLOSECBUSERL1
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
     C           #STUS     BEGSR
     C**
     C********** WWPGMA    IFNE 'CBC0015A'                                090415
     C           WWPGMA    IFEQ 'CBCMOND'                                 090415
     C           WWPGMA    OREQ 'CBMOND'                                  090415
     C           WWKEY     CHAINCBDSCJPD             80
     C**
     C           WWSTAT    IFNE 'DSPW'
     C           *IN80     ANDEQ'0'
     C                     DELETCBDSCJD0
     C                     END
     C**
     C           WWSTAT    IFEQ 'DSPW'
     C**
     C           *IN80     IFEQ '0'
     C           WWTIME    IFLT DJCTNB
     C                     ADD  86400     WWTIME
     C                     END
     C           WWTIME    SUB  DJCTNB    WWOLDT  50
     C           WWOLDT    IFGT 300
     C                     EXSR #ENDJ
     C                     ELSE
     C                     UPDATCBDSCJD0
     C                     END
     C                     ELSE
     C                     MOVELUSUSER    DJUSER
     C                     MOVELUSJOB     DJJOB
     C                     MOVE USNBR     DJNBR
     C                     Z-ADDWWTIME    DJCTNB
     C                     WRITECBDSCJD0
     C                     END
     C                     END
     C                     END
     C**
     C                     ENDSR
      /EJECT
      ********************************************************************
      *                                                                  *
      * #ENDJ    - END JOB                                               *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #STUS    -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #ENDJ     BEGSR
     C                     EXSR #DELT
     C**
     C                     CALL 'CBC0523'
     C                     PARM           WWJOB
     C                     PARM           WWUSER
     C                     PARM           WWNBR
     C**
     C                     ENDSR
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIAL PROCESSING SUBROUTINE                         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDATA   - CBTRAN LAYOUT                                         *
      * WWLENG   - LENGHT OF OUTPUT PARAMETER                            *
      * WWINTR   - OUTPUT PARAMETER FOR API COMMAND                      *
      * WWINPT   - INPUT PARAMETER FOR API COMMAND                       *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR
      *
      ** Set up copyright parameter
      *
     C           WWKEY     KLIST
     C                     KFLD           WWJOB  10
     C                     KFLD           WWUSER 10
     C                     KFLD           WWNBR   6
     C**
     C                     MOVEACPY@      ACT@   80
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN CBTRAN    WWDATA
      *
     C                     Z-ADD111       WWLENG
     C                     MOVEL*BLANKS   WWINTR 16
     C                     MOVEL'JOBI0200'WWINPT  8
     C                     Z-ADD0         COUNTR  30
     C                     Z-ADD0         WWTIME  50
     C                     EXSR #TIME
      *
     C                     ENDSR
      ****************************************************************
      /EJECT
     C           #TIME     BEGSR
     C                     TIME           WWTIMR
     C           WWHOUR    MULT 60        WWTIME
     C           WWTIME    MULT 60        WWTIME
     C           WWMINT    MULT 60        WWTEMP  50
     C           WWTIME    ADD  WWTEMP    WWTIME
     C           WWTIME    ADD  WWSECO    WWTIME
     C                     ENDSR
      *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #END     - END PROCESSING SUBROUTINE                             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDATA   - CBTRAN LAYOUT                                         *
      *                                                                  *
      ********************************************************************
     C           #END      BEGSR
      *
     C           *IN99     DOUEQ'0'
     C           *LOCK     IN   WWDATA                 99
     C                     END
     C                     MOVE COUNTR    TNCONT
     C                     OUT  WWDATA
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - ERROR SUBROUTINE                                      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - MAIN SUBROUTINE                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
      *
      ** Roll back changes, print DUMP and Cancel program
      *
     C                     ROLBK
     C                     DUMP
     C                     END
     C                     RETRN
      *
     C                     ENDSR
**  CPY@
(c) Finastra International Limited 2001
