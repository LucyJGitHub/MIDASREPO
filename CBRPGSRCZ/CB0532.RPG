     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Display active users')
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  CB0532  - ACTIVE USERS                                       *
      *                                                               *
      *  Function:  This program is in charge to display the          *
      *             users logged in CoB.                              *
      *                                                               *
      *  Called By: CB0015A - CoB menu                                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 TDA035             Date 02Apr04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSC011             Date 27May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01420             Date 12APR95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  TDA035 - RTS Signon changes for MidasPlus. (Recompile)       *
      *  CSC011 - Add COB User processing from MUSERDD.               *
      *  S01420 - CoB Enhancements                                    *
      *                                                               *
      *****************************************************************
      /EJECT
     FCB0532DFCF  E                    WORKSTN
     F                                        RRN   KSFILE CB0532S0
     F                                              KINFDS INFDS#
      * DSP: COB Active Users Display File
      *
     FCBUSERPDIF  E                    DISK
     F                                              KINFDS INFDS1
     FCBUSERL2IF  E           K        DISK                           UC
     F            CBUSERD0                          KRENAMECBUSERD2
     FCBUSERL0IF  E           K        DISK                           UC
     F            CBUSERD0                          KRENAMECBUSERDX
     FCBTRACPDIF  E                    DISK                           UC
     F                                              KINFDS INFDS2
      * COB: Active Users
     FMUSER   IF  E           K        DISK                                                   CSC011
      ************************************************************
      *   I N D I C A T O R S   U S E D
      *-----------------------------------------------------------
      *   03      Exit requested
      *   05      Refresh requested
      *   25
      *   27      Next page requested
      *   35      Selection error
      *   36      Protect input field
      *   80      SFCLR
      *   81      SFLDSP
      *   82      SFLEND
      *   84      SFLNXTCHG
      *   86      PUTOVR
      *   88      READ #SFLRCD
      *   90      Call ended in error
      *   98      Force input
      *   99      Global error
      ************************************************************
      *
      *              *************************
      *              ** INDICATORS NOT USED **
      *              ** ------------------- **
      *              *************************
      *
      *       ***************************************************
      *       * 01   02   ..   04   ..   06   07   08   09   10 *
      *       * 11   12   13   14   15   16   17   18   19   20 *
      *       * 21   22   23   24   ..   26   ..   28   29   30 *
      *       * 31   32   33   34   ..   ..   37   38   39   40 *
      *       * 41   42   43   44   45   46   47   48   49   50 *
      *       * 51   52   53   54   55   56   57   58   59   60 *
      *       * 61   62   63   64   65   66   67   68   69   70 *
      *       * 71   72   73   74   75   76   77   78   79   .. *
      *       * ..   ..   83   ..   85   ..   87   ..   89   .. *
      *       * 91   92   93   94   95   96   97   ..   ..      *
      *       ***************************************************
      ***********************************************************
      /EJECT
     E                    CPY@    1   1 80
      * Data structures:-
     IPGMDS     ESDSY2PGDSP
      * Program data structure.
     IJBDTTM      DS
      * Job date/time.
     I                                        1   60WWJDT
     I                                        1   20WWJYY
     I                                        3   40WWJMM
     I                                        5   60WWJDD
     I                                        7  120WWJTM
     I                                        7   80WWJHH
     I                                        9  100WWJNN
     I                                       11  120WWJSS
     I            DS
     I                                        1  76 WWRETR
     I                                       51  57 WWSTAT
     I            DS
     I                                        1  26 WWQUAL
     I                                        1  10 USJOB
     I                                       11  20 USUSER
     I                                       21  26 USNBR
     I            DS
     I                                    B   1   40WWLENG
      *
     IINFDS#    E DSY2I#DSP
      * Display file information data structure.
      *
     IINFDS1    E DSY2I1DSP
      * File information data structure.
      *
     IINFDS2      DS
     I                                    B 156 1590WWNBRR
      * File information data structure.
     IRUNDAT      DS
     I                                        1   7 MRDT
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
      /EJECT
      *****************************************************************
      * Initialise
     C                     EXSR ZZINIT
      *
     C                     DO   *HIVAL
      * Initialise & load subfile page.
     C                     EXSR BAIZSF
     C                     MOVEL'N'       W0RSF   1
      * Display screen until reload requested:
     C           W0RSF     DOWEQ'N'
      * Display screen.
     C                     EXSR CAEXFM
      *   Process response:
      * Cancel & exit program.
     C                     SELEC
     C           *IN03     WHEQ *ON
     C                     EXSR ZYEXPG
      * HOME: Request subfile reload.
     C           *IN05     WHEQ *ON
     C                     MOVE 'Y'       W0RSF
     C           *IN07     WHEQ *ON
     C           *IN51     IFEQ '1'
     C                     CALL 'CB0535'
     C                     MOVE 'Y'       W0RSF
     C                     END
     C           *IN08     WHEQ *ON
     C                     EXSR ZYLOCK
      *   Display next SFL page.
     C           *IN27     WHEQ *ON
     C                     EXSR BBLDSF
      *   Process screen input.
     C                     OTHER
     C                     EXSR DAPR##
     C                     ENDSL
      *
     C                     ENDDO                           OD W0RSF
     C                     ENDDO                           OD *HIVAL
      *****************************************************************
      /EJECT
      *================================================================
      * BAIZSF subroutine.
      * Initialise & Load subfile page.
      *================================================================
     C           BAIZSF    BEGSR
      * Clear subfile.
     C                     SETON                     80
     C                     WRITECB0532C0
     C                     SETOF                     80
      * Reset no of records in subfile.
     C                     Z-ADD*ZERO     WWRRMX  50 81     SETOF 81
     C           1         SETLLCBUSERD0             82
     C                     READ CBUSERD0                 82*82=EOF
      * Load subfile page.
     C                     Z-ADD0         WWRROK  50
     C                     EXSR BBLDSF
     C           BAEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * BBLDSF
      * Load subfile page.
      *================================================================
     C           BBLDSF    BEGSR
      * Re-establish fields in read-ahead record.
     C   27                DO
     C  N82                READPCBUSERD0                 90*
     C  N82                READ CBUSERD0                 90*
     C                     ENDDO
      *
      * Setof record error indicator.
     C                     SETOF                         35
      * Start at previous highest record in SFL.
     C                     Z-ADDWWRRMX    RRN     50
      * Reset count of DBF records read.
     C                     Z-ADD0         WWRRRD  50
      *................................................................
      * Load next SFL page until SFL page full
     C           *IN82     DOWEQ'0'                        DO
     C           WWRROK    ANDLTWWSFPG
     C           USDTAQ    IFEQ WWDTQF
     C                     MOVE '1'       *IN36
     C                     END
      * Load SFL fields.
     C                     EXSR MBFL#1
      * Output to subfile.
     C                     ADD  1         RRN        81    *
     C                     ADD  1         WWRROK
      *
     C                     MOVEL'       ' DDCTRL
     C           USCTRL    IFEQ 'C'
     C                     MOVEL'VERIFY'  WWVRFY  6
     C                     CALL 'CBC0515'              90
     C                     PARM           USUSER
     C                     PARM           USJOB
     C                     PARM           USNBR
     C                     PARM           @DQNAM
     C                     PARM           WWVRFY
     C                     PARM           WWRTCD
     C           WWRTCD    IFEQ 'Y'
     C                     MOVEL'CONTROL' DDCTRL
     C                     END
     C                     END
      *
     C           USLOCK    IFNE 'LOCK'
     C                     CALL 'QUSRJOBI'             99
     C                     PARM           WWRTRN 76
     C                     PARM           WWLENG
     C                     PARM           WWINPT
     C                     PARM           WWQUAL
     C                     PARM           WWINTR
     C           *IN99     IFEQ '0'
     C                     MOVELWWRTRN    WWRETR
     C                     MOVELWWSTAT    DDSSTS
     C                     ELSE
     C                     MOVELWWERRO    DDSSTS
     C                     END
     C                     ELSE
     C                     MOVEL'*LOCKED' DDSSTS
     C                     END
     C                     WRITECB0532S0
     C                     MOVE '0'       *IN36
      * Increment records read count.
     C                     ADD  1         WWRRRD
     C           BYPASS    TAG
     C                     READ CBUSERD0                 82*82=EOF
     C                     ENDDO                           OD 1 - WWSFPG
      *................................................................
      * If no DBF records found, display error message.
     C           RRN       IFEQ *ZERO
     C           *IN82     ANDEQ*ON
      * Send message '*No data to display'
     C                     MOVEL'Y2U0008' ZAMSID           Message id.
     C                     MOVEL'Y2USRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS                     Send message
     C                     ENDIF                           FI RRN  = *ZERO
      *
      *................................................................
      * Save highest SFL record load can continue at end point.
     C           RRN       IFGT WWRRMX
     C           WWRRMX    ADD  1         SFLRRN
     C                     Z-ADDRRN       WWRRMX
     C                     ENDIF
     C                     Z-ADD0         WWRROK
     C           BBEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * CAEXFM
      * Display screen.
      *================================================================
     C           CAEXFM    BEGSR
      * Update screen time.
     C                     TIME           WWTME            Screen time.
     C                     WRITECB0532C1
     C                     WRITECB0532T1
     C                     EXFMTCB0532C0
     C                     MOVE '0'       *IN89
     C                     OPEN CBUSERL0
     C           WWKEY     CHAINCBUSERDX             80
     C                     CLOSECBUSERL0
     C           *IN80     IFEQ '0'
     C           USLOCK    ANDEQ'LOCK'
     C                     MOVEL'RETURN'  WWCODE
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
      * Update job time.
     C                     TIME           WWJTM            Job time.
      * Clear messages from program message queue.
     C                     CALL 'Y2CLMSC'
     C                     PARM DDPGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      * Reset first message only flag.
     C                     MOVEL'Y'       ZAFSMS  1      99*
     C                     SETOF                         88*
     C           CAEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * DAPR##
      * Process screen input.
      *================================================================
     C           DAPR##    BEGSR
      * Maintain subfile position where possible.
     C           @#SFRC    IFGT *ZERO
     C                     Z-ADD@#SFRC    SFLRRN
     C                     ENDIF
      * Reload subfile requested:
     C           W0RSF     CABEQ'Y'       DAEXIT
     C           *IN81     IFEQ '1'
      * Process subfile records.
     C                     EXSR DBPRSF
     C                     END
      * If error, Quit processing:
     C           *IN99     CABEQ'1'       DAEXIT
     C           DAEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * DBPRSF
      * Process modified subfile records.
      *================================================================
     C           DBPRSF    BEGSR
     C                     READCCB0532S0                 88*
     C           *IN88     DOWEQ*OFF
      * Process SFL record.
     C                     EXSR DCPRSR
     C           USDTAQ    IFEQ WWDTQF
     C                     MOVE '1'       *IN36
     C                     END
     C                     UPDATCB0532S0
     C                     MOVE '0'       *IN36
     C                     READCCB0532S0                 88*
     C                     ENDDO
     C           DBEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * DBPRSF
      * Process modified subfile records.
      *================================================================
     C           ZYLOCK    BEGSR
     C                     MOVEL'*ALL   ' WWALL  10
     C                     MOVEL*BLANK    WWERR   1
     C           *IN50     IFEQ '0'
     C                     MOVEL##USR     WWINFO
     C                     MOVEL'MASTER'  WWMAST
     C                     MOVEL'L'       LOCK
     C                     CALL 'CBC0520'
     C                     PARM           WWALL
     C                     PARM           WWERR
     C                     MOVE '1'       *IN50
     C                     ELSE
     C                     MOVEL*BLANKS   WWMAST 10
     C                     MOVEL*BLANKS   WWINFO 10
     C                     MOVEL*BLANK    LOCK    1
     C                     CALL 'CBC0521'
     C                     PARM           WWALL
     C                     PARM           WWERR
     C                     MOVE '0'       *IN50
     C                     END
     C                     MOVE 'Y'       W0RSF
     C                     ENDSR
      *================================================================
      /EJECT
      *================================================================
      * DCPRSR
      * Process subfile record.
      *================================================================
     C           DCPRSR    BEGSR
      * Setof error indicators and SFLNXTCHG.
     C                     SETOF                     35
     C                     SETOF                     98    *
     C                     SETOF                     89    *
      *
      ** SELECTION SHOULD BE L,M,S, OR U
      *
     C           DDSELC    IFNE 'L'
     C           DDSELC    ANDNE'U'
     C           DDSELC    ANDNE'S'
     C           DDSELC    ANDNE'M'
     C           DDSELC    ANDNE'C'
     C           DDSELC    ANDNE'R'
     C                     MOVE '1'       *IN35
     C                     MOVE '1'       *IN39
     C                     MOVEL'CBM0145' ZAMSID           Message id.
     C                     MOVEL'CBUSRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS
     C                     END
     C           DDSELC    IFEQ 'C'
     C           WWAUTH    ANDNE'Y'
     C           DDSELC    OREQ 'R'
     C           WWAUTH    ANDNE'Y'
     C                     MOVE '1'       *IN35
     C                     MOVE '1'       *IN39
     C                     MOVEL'CBM0148' ZAMSID           Message id.
     C                     MOVEL'CBUSRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS
     C                     END
      *                                                                                       CSC011
      * Check that user is defined as authorised to COB                                       CSC011
      *                                                                                       CSC011
     C           DDSELC    IFEQ 'C'                                                           CSC011
     C           *LOVAL    SETLLMUSER                                                         CSC011
     C           DDUSER    CHAINMUSER                81                                       CSC011
     C           COBAUT    IFNE 'Y'                                                           CSC011
     C                     MOVE '1'       *IN35                                               CSC011
     C                     MOVE '1'       *IN39                                               CSC011
     C                     MOVEL'CBM0250' ZAMSID           Message id.                        CSC011
     C                     MOVEL'CBUSRMSG'ZAMSGF           Message file.                      CSC011
     C                     EXSR ZASNMS                                                        CSC011
     C                     END                                                                CSC011
     C                     END                                                                CSC011
      *
      *  ONLY SELECTION M  ALLOWED FOR REQUESTER
      *
     C           *IN35     IFEQ '0'
     C           DDSELC    ANDNE'M'
     C           DDDTAQ    ANDEQWWDTQF
     C                     MOVE '1'       *IN35
     C                     MOVE '1'       *IN39
     C                     MOVEL'CBM0144' ZAMSID           Message id.
     C                     MOVEL'CBUSRMSG'ZAMSGF           Message file.
     C                     EXSR ZASNMS
     C                     END
      *  NO ERRORS
     C           *IN35     IFEQ '0'
      * USER: Process subfile record (Pre-confirm)
      *CASE:     RCD.*SFLSEL is Give Control
     C           DDSELC    IFEQ 'C'                        *IF
     C                     MOVELDDDTAQ    @DQNAM 10
     C                     MOVELDDUSER    @DQUSR 10
     C                     MOVELDDJOB     @DQJOB 10
     C                     MOVELDDNBR     @DQNBR  6
     C                     MOVEL'CONTRL'  @DQMOD  6
     C                     CALL 'CBC0515'              90
     C                     PARM           @DQUSR
     C                     PARM           @DQJOB
     C                     PARM           @DQNBR
     C                     PARM           @DQNAM
     C                     PARM           @DQMOD
     C                     PARM           WWRTCD  1
      *
     C           *IN90     IFEQ '1'
     C                     SETON                     98    *
     C                     END
     C                     MOVE 'Y'       W0RSF            *Reload subfile
      *CASE:     CTL.*CMD key is *Exit
     C           *IN03     IFEQ '1'                        *IF
     C                     EXSR ZYEXPG
     C                     END                             *FI
     C                     ENDIF
      *CASE:     RCD.*SFLSEL is Give Control
     C           DDSELC    IFEQ 'R'                        *IF
     C                     MOVELDDDTAQ    @DQNAM 10
     C                     MOVELDDUSER    @DQUSR 10
     C                     MOVELDDJOB     @DQJOB 10
     C                     MOVELDDNBR     @DQNBR  6
     C                     MOVEL'RMVCTL'  @DQMOD  6
     C                     CALL 'CBC0515'              90
     C                     PARM           @DQUSR
     C                     PARM           @DQJOB
     C                     PARM           @DQNBR
     C                     PARM           @DQNAM
     C                     PARM           @DQMOD
     C                     PARM           WWRTCD  1
      *
     C           *IN90     IFEQ '1'
     C                     SETON                     98    *
     C                     END
     C                     MOVE 'Y'       W0RSF            *Reload subfile
      *CASE:     CTL.*CMD key is *Exit
     C           *IN03     IFEQ '1'                        *IF
     C                     EXSR ZYEXPG
     C                     END                             *FI
     C                     ENDIF
      *CASE:     RCD.*SFLSEL is Signoff
     C           DDSELC    IFEQ 'S'                        *IF
      * Send signoff request for this user
     C                     MOVELDDDTAQ    @DQNAM 10
     C                     MOVELDDUSER    @DQUSR 10
     C                     MOVELDDJOB     @DQJOB 10
     C                     MOVELDDNBR     @DQNBR  6
      *
     C                     CALL 'CBC0522'              90
     C                     PARM           @DQUSR
     C                     PARM           @DQJOB
     C                     PARM           @DQNBR
     C                     PARM           @DQNAM
      *
     C           *IN90     IFEQ '1'
     C                     SETON                     98    *
     C                     END
     C                     MOVE 'Y'       W0RSF            *Reload subfile
      *CASE:     CTL.*CMD key is *Exit
     C           *IN03     IFEQ '1'                        *IF
     C                     EXSR ZYEXPG
     C                     END                             *FI
     C                     ENDIF
      *CASE:     RCD.*SFLSEL is LOCK
     C           DDSELC    IFEQ 'L'                        *IF
     C           DDSSTS    ANDNE'*LOCKED'
     C                     MOVELDDDTAQ    @DQNAM 10
      *
     C                     CALL 'CBC0520'              90
     C                     PARM           @DQNAM
     C                     PARM           @ERROR  1
      *
     C                     MOVE 'Y'       W0RSF            *Reload subfile
      *CASE:     CTL.*CMD key is *Exit
     C           *IN03     IFEQ '1'                        *IF
     C                     EXSR ZYEXPG
     C                     END                             *FI
     C                     ENDIF
      *CASE:     RCD.*SFLSEL is UNLOCK
     C           DDSELC    IFEQ 'U'                        *IF
     C           DDSSTS    ANDEQ'*LOCKED'
     C                     MOVELDDDTAQ    @DQNAM 10
      *
      *
     C                     CALL 'CBC0521'              90
     C                     PARM           @DQNAM
     C                     PARM           @ERROR  1
      *
     C                     MOVE 'Y'       W0RSF            *Reload subfile
      *CASE:     CTL.*CMD key is *Exit
     C           *IN03     IFEQ '1'                        *IF
     C                     EXSR ZYEXPG
     C                     END                             *FI
     C                     ENDIF
     C           DDSELC    IFEQ 'M'                        *IF
      * Send signoff request for this user
     C                     MOVELDDUSER    @DUSER 10
     C                     MOVELDDJOB     @DJOB  10
     C                     MOVELDDNBR     @DNBR   6
     C                     MOVELDDDTAQ    @DQNAM 10
     C                     MOVEL*BLANKS   @DQRTN  8
      *
     C                     CALL 'CBC0526'              90
     C                     PARM           @DUSER
     C                     PARM           @DJOB
     C                     PARM           @DNBR
     C                     PARM           @DQNAM
     C                     PARM           @DQRTN
     C           @DQRTN    IFEQ 'EXIT    '
     C                     EXSR ZYEXPG
     C                     END
      *
     C           *IN90     IFEQ '1'
     C                     SETON                     98    *
     C                     END
     C                     MOVE 'Y'       W0RSF            *Reload subfile
      *CASE:     CTL.*CMD key is *Exit
     C           *IN03     IFEQ '1'                        *IF
     C                     EXSR ZYEXPG
     C                     END                             *FI
     C                     END                             *FI
     C                     END                             *FI
     C           *IN98     IFEQ *ON
      * SFLRCD invalid.
     C  N99                Z-ADDRRN       SFLRRN     99    Invalid SFLRCD
     C                     SETON                     84    SFLNXTCHG
     C                     ELSE
      * SFLRCD valid.
     C                     SETOF                     84    SFLNXTCHG
     C                     MOVEL*BLANK    DDSELC
     C                     ENDIF                           FI *IN98
     C           *IN98     IFEQ *ON
      * SFLRCD invalid.
     C  N99                Z-ADDRRN       SFLRRN     99    Invalid SFLRCD
     C                     SETON                     84    SFLNXTCHG
     C                     ENDIF                           FI *IN98
     C           DCEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * MBFL#1
      * Move CBUSERD0   fields to subfile.
      *================================================================
     C           MBFL#1    BEGSR
     C                     MOVELUSUSER    DDUSER           User ID
     C                     MOVELUSJOB     DDJOB            Job Name
     C                     MOVELUSNBR     DDNBR            Job Number
     C                     MOVELUSDTAQ    DDDTAQ           Job Data Queue
     C                     MOVELUSSTRT    DDSTRT           Job Start Time
     C                     MOVEL*BLANK    DDSELC           *SFLSEL
     C           MBEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * ZASNMS
      * Send message to program's message queue.
      *================================================================
     C           ZASNMS    BEGSR
     C           ZAPGM     IFEQ *BLANK
     C                     MOVELDDPGM     ZAPGM
     C                     ENDIF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      * Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSID           Message Id.
     C                     MOVEL*BLANK    ZAMSGF           Message file.
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C           ZAEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * ZYEXPG
      * Exit program: Direct.
      *================================================================
     C           ZYEXPG    BEGSR
      * Terminate program.
     C                     SETON                     LR
      *
      * Exit program.
     C                     RETRN
      *
     C           ZYEXIT    ENDSR
      *================================================================
      /EJECT
      *================================================================
      * ZZINIT
      * Initialisation.
      *================================================================
     C           ZZINIT    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           WWCODE 10
      *
     C           WWKEY     KLIST
     C                     KFLD           ##JOB
     C                     KFLD           ##USR
     C                     KFLD           WWJNO   6
      *
     C                     MOVE '>'       ZZM002  1
      *
     C                     MOVE ##JNO     WWJNO
     C                     MOVEACPY@      ACT@   80
     C                     MOVEL##PGM     DDPGM
      *
     C                     MOVE *BLANKS   @DQDTA256
      * Setup job date/time.
     C                     Z-ADDUDATE     WWJDT            Job date.
     C                     TIME           WWJTM            Job time.
     C                     TIME           WWTME   60       Screen time.
      * Define work field midas rundate
     C                     MOVEL*BLANK    WUMRDT  7
      * Define work field run day number
     C                     Z-ADD*ZERO     WURDNB  50
      * Define work field Set Up Complete
     C                     MOVEL*BLANK    WUSUC   1
      * Define work field date format flag
     C                     MOVEL*BLANK    WUDFF   1
      * LOOK FOR CONTROL AUTHORITY
     C                     MOVEL'VERIFY'  WWVRFY  6
     C                     CALL 'CBC0515'
     C                     PARM           ##USR
     C                     PARM           ##JOB
     C                     PARM           ##JNO
     C                     PARM           WWDTAQ 10
     C                     PARM           WWVRFY
     C                     PARM           WWAUTH  1
      *
     C                     Z-ADD15        WWSFPG  30       SFLPAG
     C                     Z-ADD1         SFLRRN
     C                     Z-ADD*ZERO     WWRRMX           MAX RECNO
     C                     Z-ADD76        WWLENG
     C                     MOVEL*BLANKS   WWINTR 16
     C                     MOVEL'JOBI0100'WWINPT  8
     C                     MOVEL'DTQ'     WWDTQ9  9
     C                     MOVE ##JNO     WWDTQ9
     C                     MOVELWWDTQ9    WWDTQF 10
     C                     MOVEL'MASTER  'WWMAST 10
     C                     MOVEL'*ERROR  'WWERRO  7
      *
     C                     OPEN CBUSERL2
     C           WWMAST    CHAINCBUSERD2             80
     C           *IN80     IFEQ '0'
     C                     MOVE '1'       *IN50
     C                     END
     C                     CLOSECBUSERL2
     C**
     C                     MOVE *OFF      *IN51
     C                     OPEN CBTRACPD
     C                     READ CBTRACPD                 85
     C                     CLOSECBTRACPD
     C           WWNBRR    IFNE 0
     C                     MOVE *ON       *IN51
     C                     END
      *................................................................
      * USER: Initialise program
      * Get Rundate - Rundate  *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     MOVE MRDT      SRUNA
     C                     MOVE MRDT      WUMRDT
     C                     MOVE RDNB      WURDNB
     C                     MOVE SUC       WUSUC
     C                     MOVE DFF       WUDFF
     C                     MOVE ##USR     SUSER
     C                     MOVE ##JOB     SWSID
     C           ZZEXIT    ENDSR
      *================================================================
**  CPY@
(c) Finastra International Limited 2001
