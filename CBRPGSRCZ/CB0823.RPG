     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Check for BoE conditions')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing                     *
     F*                                                               *
     F*  CB0823 - Check for Bank of England conditions                *
     F*           (EOM, accrual date followed by NWD, etc.)           *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01488             Date 19Apr94               *
      *                                    Date DDMmmYY               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  S01488 - Created for CoB run conditions enhancements         *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E*
     E** Array containing Copyright statement
     E*
     ISDBANK    E DSSDBANKPD
      *
      *  EXTERNAL DATA STRUCTURE FOR ACCESS PROGRAM FORMAT
      *
     ISDGELR    E DSSDGELRPD
      *
      *  EXTERNAL DATA STRUCTURE FOR ACCESS PROGRAM FORMAT
      *
     IDSFDY     E DSDSFDY
      *
     I** FIRST DATA STRUCTURE FOR ACCESS PROGRAM, SHORT DATA STRUCTURE
     I*
     ILDA       E DSLDA                         256
      *
     ISDSTAT     UDS                            256
      *
      *    Type of end of day
      *
     I                                      116 1160ACCR
     I            DS
      *
      *    Month of run date
      *
     I                                        1   7 RUNDAT
     I                                        3   5 MON1
     I            DS
      *
      *    Month of next working day
      *
     I                                        1   7 ZADATE
     I                                        3   5 MON2
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
     C           *ENTRY    PLIST
     C                     PARM           RUN     1
     C                     PARM           RTN     1
     C                     MOVEACPY@      BIS@   80
     C           *NAMVAR   DEFN           LDA
      *
      *    Use access program to obtain record from SDBANKPD
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' P@RTCD  7
     C                     PARM '*FIRST ' P@OPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      *    Database error if blank return code not received from access pgm
      *
     C           P@RTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 01  *
     C                     MOVEL'CB0823'  DBPGM            ***************
     C                     OUT  LDA
     C                     RETRN
     C                     END
     C                     MOVE BJMRDT    RUNDAT
      *
      *    Check if next working day is in this month
      *
     C                     Z-ADDBJDNWD    ZDAYNO
     C                     CALL 'ZDATE2'
     C                     PARM           ZDAYNO  50       Value date
     C                     PARM BJDFIN    ZDATFM  1        Date format ind
     C                     PARM           ZDATE   60       Value date
     C                     PARM           ZADATE  7        Run-date in DDM
     C                     FREE 'ZDATE2'
      *
     C           MON1      IFNE MON2
     C                     MOVE 'Y'       EOM     1
     C                     ENDIF
      *
      *    Use access program to obtain record from SDGELRPD
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' P@RTCD  7
     C                     PARM '*FIRST ' P@OPTN  7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
      *    Database error if blank return code not received from access pgm
      *
     C           P@RTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDGELRPD'DBFILE           *DB ERROR 02  *
     C                     MOVEL'CB0823'  DBPGM            ***************
     C                     OUT  LDA
     C                     RETRN
     C                     END
      *
      *    Set run value if conditions met
      *
     C           EOM       IFEQ 'Y'
     C           ACCR      ANDEQ3
     C           BKAPFQ    ANDEQ'D'
     C           BKANWD    ANDEQ'0'
     C                     MOVE 'Y'       RUN
     C                     ELSE
     C                     MOVE 'N'       RUN
     C                     ENDIF
      *
      *    Set blank return code and end program
      *
     C                     MOVE ' '       RTN
     C                     SETON                     LR
     C                     RETRN
** CPY@   OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
