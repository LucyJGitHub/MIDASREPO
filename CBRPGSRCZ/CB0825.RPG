     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Check for last Monday, etc of month')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing                     *
     F*                                                               *
     F*  CB0825 - Check for last Monday, etc of month                 *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01488             Date 18Apr94               *
      *                                    Date DDMmmYY               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  S01488 - Created for CoB run conditions enhancements         *
     F*                                                               *
     F*****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E*
     E** Array containing Copyright statement
     E*
     E                    TABNO   7   7  1 0 TABRET  1   DAY NOS./RTNCDES
     ISDBANK    E DSSDBANKPD
      *
      *  EXTERNAL DATA STRUCTURE FOR ACCESS PROGRAM FORMAT
      *
     IDSFDY     E DSDSFDY
      *
     I** FIRST DATA STRUCTURE FOR ACCESS PROGRAM, SHORT DATA STRUCTURE
     I*
     ILDA       E DSLDA                         256
      *
     I            DS
      *
      *    Month of run date
      *
     I                                        1   7 RUNDAT
     I                                        3   5 MON1
     I            DS
      *
      *    Month of one week hence
      *
     I                                        1   7 ZADATE
     I                                        3   5 MON2
     C           *ENTRY    PLIST
     C                     PARM           RUN     1
     C                     PARM           RTN     1
     C                     MOVEACPY@      BIS@   80
     C           *NAMVAR   DEFN           LDA
      *
      *    Use access program to obtain record from SDBANKPD
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' P@RTCD  7
     C                     PARM '*FIRST ' P@OPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      *    Database error if blank return code not received from access pgm
      *
     C           P@RTCD    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 01  *
     C                     MOVEL'CB0825'  DBPGM            ***************
     C                     OUT  LDA
     C                     RETRN
     C                     END
     C                     MOVE BJMRDT    RUNDAT
      *
      *    Determine weekday number and corresponding return code
      *
     C           BJRDNB    DIV  7         W5D0    50
     C                     MVR            DAYNO   10
     C           DAYNO     LOKUPTABNO     TABRET         40
     C      40             MOVE TABRET    DAYRTN  1
      *
      *    Check if this day is last in month
      *
     C           BJRDNB    ADD  7         ZDAYNO
     C                     CALL 'ZDATE2'
     C                     PARM           ZDAYNO  50       Value date
     C                     PARM BJDFIN    ZDATFM  1        Date format ind
     C                     PARM           ZDATE   60       Value date
     C                     PARM           ZADATE  7        Run-date in DDM
     C                     FREE 'ZDATE2'
      *
     C           MON1      IFNE MON2
     C                     MOVE DAYRTN    RUN
     C                     ELSE
     C                     MOVE 'N'       RUN
     C                     ENDIF
      *
      *    Set blank return code and end program
      *
     C                     MOVE ' '       RTN
     C                     SETON                     LR
     C                     RETRN
** CPY@   OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**   TABNO/TABRET - DAY NUMBERS & RETURN CODES
05162731425364
