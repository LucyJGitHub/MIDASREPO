     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Check for day of week')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Close of Business Processing                         *
     F*                                                               *
     F*  CB0832 - CHECK FOR DAY OF WEEK                               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. 133648 *CREATE     DATE 15APR98               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  133648 - Add Run Condition for Every Monday, Tuesday etc.    *
     F*                                                               *
     F*****************************************************************
     E*
     E                    CPY@    1   1 80
     E                    TABNO   7   7  1 0 TABDAY  3   DAY NOS. & NAMES
     E*
     IRUNDAT    E DSRUNDAT
      *
      *    Run date data area
      *
     C           *ENTRY    PLIST
     C                     PARM           DAYNAM  3
     C                     MOVE '   '     DAYNAM
      *
      *    Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      *    Retrieve run date from data area
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     UNLCK*NAMVAR
      *
      *    Check for day of week
      *
     C           AGRDNB    DIV  7         DUMP    50
     C                     MVR            DAYNO   10
     C           DAYNO     LOKUPTABNO     TABDAY         40
     C      40             MOVE TABDAY    DAYNAM  3
      *
     C                     SETON                     LR
      *
      *******************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR'*CANCL'
      *
      *******************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   TABNO/TABDAY - DAY NUMBERS & SHORTNAMES
0FRI1SAT2SUN3MON4TUE5WED6THU
