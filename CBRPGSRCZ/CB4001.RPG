     H            Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CB Run User Defined Event')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Close of Business Processing                         *
      *                                                               *
      *  CB4000 - Midas CB User Defined Events                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CCB019  *CREATE    Date 20Mar12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB019 - Close of business Scheduler Enhancement             *
      *           Upgrade IPL089                                      *
      *                                                               *
      *****************************************************************
     FCBUDCFL0UF  E           K        DISK
      *
     C           *ENTRY    PLIST
     C                     PARM           SEQ     3
     C                     PARM           RETURN  1
      *
     C           SEQ       CHAINCBUDCFL0             99
     C                     UPDATCBUDCFPF
      *
      ** Get OUTQ/LIBL value and call change OUTQ program
     C                     MOVE CMQUEU    AQUEU  10
     C                     MOVE CMQULI    AQULI  10
     C                     MOVE CMNAME    ANAME  10
     C           AQUEU     IFNE *BLANKS
     C                     CALL 'CBC4002'
     C                     PARM           AQUEU
     C                     PARM           AQULI
     C                     PARM           ANAME
     C                     ENDIF
      *
     C           CMMQ      IFEQ 'P'
     C                     CALL 'CBC4003'
     C                     PARM           CMNAME
     C                     PARM           CMLIBR
     C                     PARM           CMPAR0
     C                     PARM           CMPAR1
     C                     PARM           CMPAR2
     C                     PARM           CMPAR3
     C                     PARM           CMPAR4
     C                     PARM           CMPAR5
     C                     PARM           CMPAR6
     C                     PARM           CMPAR7
     C                     PARM           CMPAR8
     C                     PARM           CMPAR9
     C                     PARM           RETURN  1
     C                     ENDIF
     C           CMMQ      IFEQ 'Q'
     C                     CALL 'CBC4004'
     C                     PARM           CMNAME
     C                     PARM           CMLIBR
     C                     PARM           CMCFRF
     C                     PARM           CMCFRL
     C                     PARM           CMPAR0
     C                     PARM           CMPAR1
     C                     PARM           CMPAR2
     C                     PARM           CMPAR3
     C                     PARM           CMPAR4
     C                     PARM           CMPAR5
     C                     PARM           CMPAR6
     C                     PARM           CMPAR7
     C                     PARM           CMPAR8
     C                     PARM           CMPAR9
     C                     PARM           RETURN  1
     C                     ENDIF
      *
     C           RETURN    IFEQ *BLANKS
      *
     C           CMCFRL    IFNE *BLANKS
     C           CMCFRF    ORNE *BLANKS
     C           CMCTOL    ORNE *BLANKS
     C           CMCTOF    ORNE *BLANKS
     C                     CALL 'CBC4005'
     C                     PARM           CMCFRL
     C                     PARM           CMCFRF
     C                     PARM           CMCTOL
     C                     PARM           CMCTOF
     C                     PARM           CMADDR
     C                     PARM           RETURN
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           SEQ       CHAINCBUDCFL0             99
      *
     C                     TIME           SIX     60
     C                     MOVE SIX       CMLRTI
     C                     Z-ADDUDATE     SIX
     C                     MOVE SIX       CMLRDT
     C           RETURN    IFEQ ' '
     C                     MOVE 'Y'       CMSUCC
     C                     ELSE
     C                     MOVE 'N'       CMSUCC
     C                     ENDIF
      *
     C                     UPDATCBUDCFPF
      *
     C                     MOVE *ON       *INLR
      *
