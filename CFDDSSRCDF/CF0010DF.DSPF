      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CF Link branches to TRAM systems')
     A*****************************************************************
     A*                                                               *
     A*  Midas - CONFIRMATION MATCHING                   *
     A*                                                               *
     A*       CF0010DF - LINK BRANCH TO TRAM SYSTEM                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 104633             DATE 17JUN96               *
     A*  PREV AMEND NO. E39162             DATE 30MAY92               *
     A*                 S01184             DATE 15DEC89               *
     A*                                    DATE DDMMMYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  S01184 - CREATED FOR CONFIRMATION MATCHING                   *
     A*                                                               *
     A*****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A          R CF0010F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      CHGINPDFT
     A            WSID          10A  O  1  2
     A                                  1 27'GROUP BRANCHES FOR TRAM'
     A                                      DSPATR(UL HI)
     A                                  1 63TIME
     A            BJHZNB         7A  O  1 73
     A                                  4  7'TRAM System ID'
     A            SFTI           3A  B  4 24
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(PR)
     A                                  4 32'Action Code'
     A            SFAC           1A  B  4 46
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(PR)
     A                                  5  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
     A          R CF0010F2                  SFL
     A                                      CHGINPDFT
     A  22                                  SFLNXTCHG
     A            SFBR           3A  B  9 38
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(PR)
     A            SHBR           3A  H
     A          R CF0010F3                  SFLCTL(CF0010F2)
     A                                      CA10
     A                                      CA12
     A  20                                  SFLDSP
     A  20                                  SFLDSPCTL
     A N20                                  SFLCLR
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A  21                                  SFLEND
     A                                      OVERLAY
     A            DSPREC         4  0H      SFLRCDNBR
     A                                  7  7'Branches Linked to this System'
     A          R CF0010F4
     A            SFMG          80A  O 22  1
     A  40                                  DSPATR(RI)
      ******                                                   Start  /104633/
     A******                           23  2'F3-Exit'
     A**20**                           23 11'F12-Previous  Rollup/Rolldown'
     A**25**                           23 42'F10-Delete Links'
     A                                 23  2'F3=Exit'
     A  25                             23 12'F10=Delete Links'
     A  20                             23 31'F12=Previous   Rollup/Rolldown'
      ******                                                   End    /104633/
