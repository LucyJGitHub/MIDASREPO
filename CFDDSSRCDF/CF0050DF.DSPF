      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CF Dealing confirmations status rpt request')
     A*****************************************************************
     A*                                                               *
     A*  Midas - CONFIRMATION MATCHING                   *
     A*                                                               *
     A*       CF0050DF - DEALING CONFIRMATIONS STATUS REPORT          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 104633             Date 02Jul96               *
      *                 E39162             Date 30May92               *
     A*                 S01310             DATE 06DEC90               *
     A*                 S01184             DATE 15DEC89               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  S01310 - SWIFT RATIONALISATION                               *
     A*  S01184 - CREATED FOR CONFIRMATION MATCHING                   *
     A*                                                               *
     A*****************************************************************
     A*********        START OF CHANGE                     /39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /39162/
     A                                      CA03
      *                                                                 /S01310/
     A                                      CA12
      *                                                                 /S01310/
     A          R CF0050F1                  OVERLAY
      *                                                                 /S01310/
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *                                                                 /S01310/
     A                                      CHGINPDFT
     A            WSID          10A  O  1  2
     A                                  1 22'DEALING CONFIRMATIONS STATUS +
     A                                      REPORT'
     A                                      DSPATR(UL HI)
     A                                  1 63TIME
     A            BJHZNB         7A  O  1 73
     A                                  3 13'Start From Deal Number:'
     A            SFDS           6A  B  3 37
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(PR)
     A  41                                  DSPATR(HI)
     A N41                                  DSPATR(RI PC)
     A                                  3 46'End of Range:'
     A            SFDE           6A  B  3 60
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(PR)
     A  42                                  DSPATR(HI)
     A N42                                  DSPATR(RI PC)
     A                                  4 13'Select Counterparty Status?'
     A            SFCP           1A  B  4 41
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(PR)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A                                  4 45'Select Broker Status?'
     A            SFBK           1A  B  4 67
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(PR)
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A          R CF0050F2                  OVERLAY
      *                                                                 /S01310/
     A*********************                 CA12
      *                                                                 /S01310/
     A                                  6 25'Enter "Y" to include Status type:'
     A            SF01           1A  B  8 22
     A N01                                  DSPATR(HI)
     A  01                                  DSPATR(RI PC)
     A                                  8 25'Waiting'
     A            SF02           1A  B  9 22
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(RI PC)
     A                                  9 25'Timed Out'
     A            SF50           1A  B 11 22
     A N03                                  DSPATR(HI)
     A  03                                  DSPATR(RI PC)
     A                                 11 25'Proposed Pair'
     A            SF51           1A  B 12 22
     A N04                                  DSPATR(HI)
     A  04                                  DSPATR(RI PC)
     A                                 12 25'Paired (Error in Our Confirmation)'
     A            SF52           1A  B 13 22
     A N05                                  DSPATR(HI)
     A  05                                  DSPATR(RI PC)
     A                                 13 25'Paired (Error in Their +
     A                                      Confirmation)'
     A            SF53           1A  B 14 22
     A N06                                  DSPATR(HI)
     A  06                                  DSPATR(RI PC)
     A                                 14 25'Paired (Error in Both +
     A                                      Confirmations)'
     A            SF54           1A  B 15 22
     A N07                                  DSPATR(HI)
     A  07                                  DSPATR(RI PC)
     A                                 15 25'Proposed Pair (Investigate)'
     A            SF97           1A  B 17 22
     A N08                                  DSPATR(HI)
     A  08                                  DSPATR(RI PC)
     A                                 17 25'Confirmation Not Expected'
     A            SF99           1A  B 18 22
     A N09                                  DSPATR(HI)
     A  09                                  DSPATR(RI PC)
     A                                 18 25'Paired (No Error)'
     A          R CF0050F3
      *                                                                 /S01310/
     A  45                             20  2'Branch:'
     A  45        RENT           3   O 20 10
     A  45        A8BRNM        30   O 20 14
      *                                                                 /S01310/
     A            SFMG          78A  O 22  2
     A  40                                  DSPATR(RI)
      *                                                                 /S01310/
      *******                                     Start  /104633/
     A*******                          23  2'F3-Exit  F12-Previous'
     A                                 23  2'F3=Exit   F12=Previous'
      *******                                       End  /104633/
     A*********************            23  2'F3-Exit'
     A**24*****************            23 11'F12-Previous'
      *                                                                 /S01310/
