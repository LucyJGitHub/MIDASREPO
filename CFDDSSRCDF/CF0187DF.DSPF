      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CF Resend dealing confirmations to TRAM')
     A*****************************************************************
     A*                                                               *
     A*  Midas - CONFIRMATION MATCHING                   *
     A*                                                               *
     A*       CF0187DF - RESEND DEALING CONFIRMATIONS TO TRAM         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 104633             DATE 02JUL96               *
     A*  PREV AMEND NO. E39162             DATE 30MAY92           *
     A*                 S01310             DATE 06JUN91           *
     A*                  00000             DATE 00XXX00           *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.             *
     A*  S01310 - SWIFT RATIONALISATION                          *
     A*                                                               *
     A*****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A*
     A*
     A          R CF0187F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            WSID          10A  O  1  2
     A                                  1 27'RESEND DEAL DETAILS TO TRAM'
     A                                      DSPATR(HI UL)
     A                                  1 63TIME
     A            BJHZNB         7A  O  1 73
     A                                      TEXT('MIDAS Run Date')
     A          R CF0187F2                  OVERLAY
     A*
     A*
     A                                  6 17'Enter X to Select'
     A            SFOD           1A  B  8 14
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A                                  8 17'Our Deal'
     A            SFIM           1A  B  9 14
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A                                  9 17'Incoming Message'
     A            SFFT           1A  B 10 14
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A                                 10 17'From Time'
      *                                                                 /S01310/
     A  49                             20  2'Branch:'
     A  49        SBCH           3   B 20 10
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
      *                                                                 /S01310/
     A            SFMG          70A  O 22  2
     A  40                                  DSPATR(RI)
      ******                                       Start  /104633/
     A******                           23  2'F3-Exit'
     A                                 23  2'F3=Exit'
      ******                                        End   /104633/
     A          R CF0187F3                  OVERLAY
     A                                      CA12
     A*
     A*
     A                                 10 30'Deal Number:'
     A            SFDN           6A  B 10 44
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
      *                                                                 /S01310/
     A  49                             20  2'Branch:'
     A  49        SBCH           3     20 10
      *                                                                 /S01310/
     A          R CF0187F4                  OVERLAY
     A                                      CA12
     A*
     A*
     A                                 10  6'SWIFT Message Input +
     A                                       Reference (MIR):'
     A            SFIR          28A  B 10 44
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
      *                                                                 /S01310/
     A  49                             20  2'Branch:'
     A  49        SBCH           3     20 10
      *                                                                 /S01310/
     A          R CF0187F5                  OVERLAY
     A                                      CA12
     A*
     A*
     A                                 10 28'Time (HH ; MM):'
     A            SFHR           2A  B 10 45
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A            SFMN           2A  B 10 48
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
      *                                                                 /S01310/
     A  49                             20  2'Branch:'
     A  49        SBCH           3     20 10
      *                                                                 /S01310/
     A          R CF0187F6                  OVERLAY
     A            SFMG          70A  O 22  2
     A  40                                  DSPATR(RI)
      ******                                        Start  /104633/
     A******                           23  2'F3-Exit F12-Previous'
     A                                 23  2'F3=Exit   F12=Previous'
      ******                                          End  /104633/
