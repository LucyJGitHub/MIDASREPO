      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas CF Extract outgoing dealing messages')
      *****************************************************************
      *                                                               *
      *  Midas - CONFIRMATION MATCHING                                *
      *                                                               *
      *   CF0120AU - EXTRACT OUTGOING DEALING MESSAGES                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   LAST AMEND NO. 081700            DATE 15MAR95               *
      *   PREV AMEND NO. S01310            DATE 06DEC90               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   081700 - Output spool file to correct OUTQ                  *
      *   S01310 - SWIFT RATIONALISATION                              *
      *                                                               *
      *****************************************************************
      *                                                               *
/****A*:*SPOOL(*YES)*OUTQ(*JOB)*FORMTYPE(*STD)*COPIES(1)************:**   081700
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE CF0120'
     A            BJAGNA        53A       37
     A                                   101'DATE'
     A            BJHZNB         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A          R MSGERROR
     A                                      SKIPB(6)
     A                                    37'TRAM OUTGOING MESSAGE COMPRESSION '
     A                                    71'ERROR REPORT'
     A                                    37'__________________________________'
     A                                    71'____________'
     A                                      SKIPA(17)
     A                                    10'MESSAGE WITH TRANSACTION '
     A                                    35'REFERENCE'
     A            TRNO      R             45
     A                                    62'CANNOT BE SENT AS COMPRESSED '
     A                                    91'MESSAGE IS GREATER THAN 2048 BYTES'
     A                                      SKIPA(43)
     A                                    53'*** END OF REPORT ***'
     A          R DBERROR
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SKIPA(43)
     A                                    53'*** END OF REPORT ***'
