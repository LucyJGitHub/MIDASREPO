      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas CF Extract outgoing dealing msgs Concord')
      *****************************************************************
      *                                                               *
      *  Midas - CONFIRMATION MATCHING                            *
      *                                                               *
      *  CF0121AU - Extract Outgoing Dealing Messages for Concord     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCF001             Date 11Apr95               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCF001 - Created for Concord Interface                       *
      *                                                               *
      *****************************************************************
     A*
     A                                      REF(MFRFL)
     A          R HEADER
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE CF0121'
     A            BJAGNA        53A       37
     A                                   101'DATE'
     A            BJHZNB         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A          R MSGERROR
     A                                      SKIPB(6)
     A                                    37'CONCORD OUTGOING MESSAGE COMPRESSI'
     A                                    71'ON ERROR REPORT'
     A                                    37'__________________________________'
     A                                    71'_______________'
     A                                      SKIPA(17)
     A                                    10'MESSAGE WITH TRANSACTION '
     A                                    35'REFERENCE'
     A            TRNO      R             45
     A                                    62'CANNOT BE SENT AS COMPRESSED '
     A                                    91'MESSAGE IS GREATER THAN 2048 BYTES'
     A                                      SKIPA(43)
     A                                    53'*** END OF REPORT ***'
     A          R DBERROR
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SKIPA(43)
     A                                    53'*** END OF REPORT ***'
