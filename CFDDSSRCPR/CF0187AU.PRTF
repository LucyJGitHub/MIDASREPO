      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas CF Resend Dealing confirmations to TRAM')
     A*****************************************************************
     A*                                                               *
     A*  Midas - CONFIRMATION MATCHING
     A*                                                               *
     A*   CF0187AU - RESEND DEALING CONFIRMATIONS TO TRAM             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 081700             Date 15Mar95               *
     A*                 S01310             Date 21May91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*   081700 - Output spool file to correct OUTQ                  *
     A*   S01310 - SWIFT RATIONALISATION                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE CF0187'
     A            BJAGNA        53A       37
     A            BJHZNB         7A      106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SKIPA(6)
     A                                    37'CONFIRMATION MATCHING RESEND DEAL '
     A                                    71'DETAILS AUDIT REPORT'
     A                                    37'__________________________________'
     A                                    71'____________________'
     A                                      SKIPA(9)
     A                                     7'THE FOLLOWING MESSAGES WILL BE '
     A                                    38'RESENT TO TRAM'
     A                                      SKIPA(12)
     A                                     7'IN/OUT  REFERENCE'
     A                                    45'DATE     TIME    TYPE  CUSTOMER'
     A                                      SKIPA(14)
     A          R INLINE
     A                                     9'I'
     A            MIR       R             15
     A            MODE      R             45
     A            MOTM      R             54
     A            MTPY      R             62
      * Begin /CSD027/
     A**********  CNUM      R             68                                                  CSD027
     A            CNUM           6A       68TEXT('Customer number')
      * End /CSD027/
     A                                      SPACEA(1)
     A          R OUTLINE
     A                                     9'O'
      *                                                                   S01310
     A            TRNO      R             15
     A            MGDE      R             45
     A************TNRF******R             15
     A************MGDT******R             45
      *                                                                   S01310
     A            MGTM      R             54
     A            MTPY      R             62
      * Begin /CSD027/
     A**********  DECN      R             68                                                  CSD027
     A            DECN           6A       68TEXT('Customer no of destination')
      * End /CSD027/
     A                                      SPACEA(1)
     A          R BLANK
     A                                     9' '
     A                                      SPACEA(1)
     A          R EOREPORT
     A                                    53'*** END OF REPORT ***'
     A                                      SPACEB(2)
     A          R DBERROR
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
