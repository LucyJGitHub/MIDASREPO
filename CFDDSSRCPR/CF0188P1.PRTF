      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CF Dealing confirmations status aud.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - CONFIRMATION MATCHING
     A*                                                               *
     A*   CF0188P1 - DEALING CONFIRMATIONS STATUS AUDIT PRINT         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*   LAST AMEND NO. 122374            DATE 26MAY06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. CCF003            DATE 01AUG96               *
     A*   PREV AMEND NO. E39374            DATE 30APR92               *
     A*                  S01310            DATE 21MAY91               *
     A*---------------------------------------------------------------*
     A*   122374 - Move Branch field to prevent overlapping of report *
     A*            header.                                            *
     A*   CCF003  - Include MT360 and MT362 Confirmation Matching     *
     A*   E39374 - UNDERLINE WRONG + DATE + BANK TITLE MISSING        *
     A*   S01310 - SWIFT RATIONALISATION                              *
     A                                      REF(MFRFL)
     A          R HEADER1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A* START FIX E39374                                               __'
     A*********                            7'REFERENCE CF0188'
     A                                     7'REFERENCE CF0188P1'
     A******      BJAGNA        53A       37
     A******      BJHZNB         7A      106
     A            BJURPT        53A       37
     A            BJMRDT         7A      106
     A* END FIX E39374                                                 __'
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SKIPA(5)
      *                                      /* Start of SC#122374 */
     A**49********                         7'BRANCH'
     A**49********BRCA      R             14
     A**49********A8BRNM        30        18
      *                                     /* End of SC#122374 */
     A                                    43'DEALING CONFIRMATIONS STATUS'
     A* START FIX E39374
     A*****                               43'____________________________'
     A* END FIX E39374
     A                                    71' AUDIT REPORT'
     A* START FIX E39374
     A                                      SKIPA(6)
     A                                    43'____________________________'
     A* END FIX E39374
     A                                    71'_____________'
      *                                      /* Start of SC#122374 */
     A                                      SKIPA(7)
     A  49                                 7'BRANCH'
     A  49        BRCA      R             14
     A  49        A8BRNM        30        18
      *                                     /* End of SC#122374 */
      *
     A          R HEADER2
     A                                      SKIPB(10)
     A                                     2'DEAL  MSG MSG  DEAL SUB  DEAL'
     A                                    41'AMOUNT'
     A                                    55'CCY  VALUE      STATUS'
     A                                    87'COUNTERPARTY/BROKER'
     A                                   112'COUNTERPARTY/BROKER'
     A                                      SPACEA(1)
     A                                     1'NUMBER SEQ TYPE TYPE TYPE DATE'
     A                                    60'DATE'
     A                                    92'REFERENCE'
     A                                      SPACEA(3)
      *
     A          R DETAIL1                   SPACEA(1)
     A            @DLE           6S 0      1
     A            @SEQ           3A        8
     A            MTPY      R             12
     A       N32  TNTP      R             18
     A       N32  SBTP      R             23
     A       N32  @DAT           7A       26
     A  40   N32  AM10          13  0     37EDTCDE(1)
     A  41   N32  AM11          13  1     37EDTCDE(1)
     A  42   N32  AM12          13  2     37EDTCDE(1)
     A  43   N32  AM13          13  3     37EDTCDE(1)
     A       N32  CCY1           3A       55
     A       N32  @VAT           7A       59
     A            @STSC         15A       67
     A            MIR       R             83
     A       N30
     AO      N32  RPNM      R            112
     A*
     A          R DETAIL2                   SPACEA(1)
     A            TEXT          20A        2
     A     48N50                          29'SELL'
     A*                                              Begin /CCF003/
     A**44*48N32* AM20          13  0     37EDTCDE(1)
     A**45*48N32* AM21          13  1     37EDTCDE(1)
     A**46*48N32* AM22          13  2     37EDTCDE(1)
     A**47*48N32* AM23          13  3     37EDTCDE(1)
     A*****48N32* CCY2           3A       55
     A  44 50N32
     AO 44 48N32  AM20          13  0     37EDTCDE(1)
     A  45 50N32
     AO 45 48N32  AM21          13  1     37EDTCDE(1)
     A  46 50N32
     AO 46 48N32  AM22          13  2     37EDTCDE(1)
     A  47 50N32
     AO 47 48N32  AM23          13  3     37EDTCDE(1)
     A     50N32
     AO    48N32  CCY2           3A       55
     A*                                              End   /CCF003/
     A                                    83'(COUNTERPARTY)'
     A       N30
     AO      N32  RPTN      R            103
     A*
     A          R DETAIL3                   SPACEA(1)
     A            @STSB         15A       67
     A            BKRF          16A       83
     A       N32  BNAM          30A      103
     A*
     A          R DETAIL4                   SPACEA(2)
     A                                    83'(BROKER)'
     A       N32  BTWN          10A      103
      *
     A          R NODETAIL
     A                                    53'***** NO DETAILS *****'
     A                                      SPACEB(2)
      *
     A          R EOREPORT
     A                                    53'*** END OF REPORT ***'
     A                                      SPACEB(2)
