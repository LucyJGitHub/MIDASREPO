      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CF Dealing Ccnfirmations status report')
     F*****************************************************************
     F*                                                               *
     F*  Midas - CONFIRMATION MATCHING
     F*                                                               *
     F*       CF0190P1 - DEALING CONFIRMATIONS STATUS REPORT          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 152085             Date 15May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                  S01310            Date 21May91               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *   CDL038 - Extended Deal Sub Type (Recompile)                 *
      *   152085 - Report title and date missing                      *
      *   S01310 - SWIFT RATIONALISATION                              *
     F*                                                               *
     F*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE CF0190'
      ** Start of fix 152085
     A**********  BJAGNA        53A       37
     A**********  BJHZNB         7A      106
     A            BJURPT        53A       37
     A            BJMRDT         7A      106
      ** End of fix 152085
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SKIPA(5)
      *                                                                 /S01310/
     A  49                                 7'BRANCH'
     A  49        BRCA      R             14
     A  49        A8BRNM        30        18
      *                                                                 /S01310/
     A                                    48'DEALING CONFIRMATIONS STATUS'
     A                                    48'____________________________'
     A                                    76' REPORT'
     A                                    76'_______'
     A                                      SKIPA(10)
     A          R HEADER2
     A                                    47'SELECTION CRITERIA USED IN THIS'
     A                                    47'_______________________________'
     A                                    78' REPORT'
     A                                    78'_______'
     A                                      SPACEA(5)
     A                                    47'START OF DEAL RANGE:'
     A            SFDS           6A       70SPACEA(2)
     A                                    47'END OF DEAL RANGE:'
     A            SFDE           6A       70SPACEA(2)
     A            TXT1          31A       47
     A                                      SPACEA(2)
     A          R HEADER3
     A                                    70'PENDING'
     A                                      SPACEA(1)
     A                                    70'TIMED OUT'
     A                                      SPACEA(1)
     A                                    70'PROPOSED PAIR'
     A                                      SPACEA(1)
     A                                    70'PAIRED ERROR-O'
     A                                      SPACEA(1)
     A                                    70'PAIRED ERROR-T'
     A                                      SPACEA(1)
     A                                    70'PAIRED ERROR-B'
     A                                      SPACEA(1)
     A                                    70'PROPOSED INVEST'
     A                                      SPACEA(1)
     A                                    70'NONE EXPECTED'
     A                                      SPACEA(1)
     A                                    70'PAIRED NO ERROR'
     A          R HEADER4
     A            TXT2          15A       70
     A                                      SPACEA(1)
     A          R HEADER5
     A                                     2'DEAL  MSG MSG  DEAL SUB  DEAL'
     A                                    41'AMOUNT'
     A                                    55'CCY  VALUE      STATUS'
     A                                    87'COUNTERPARTY/BROKER'
     A                                   112'COUNTERPARTY/BROKER'
     A                                      SPACEA(1)
     A                                     1'NUMBER SEQ TYPE TYPE TYPE DATE'
     A                                    60'DATE'
     A                                    92'REFERENCE'
     A                                      SPACEA(3)
     A          R DETAIL1                   SPACEA(1)
     A            DLKY           6S 0      1
     A            @SEQ           3A        8
     A            MTPY      R             12
     A       N32  DTYP      R             18
     A       N32  DLST      R             23
     A       N32  @DAT           7A       26
     A  40   N32  AM10          13  0     37EDTCDE(1)
     A  41   N32  AM11          13  1     37EDTCDE(1)
     A  42   N32  AM12          13  2     37EDTCDE(1)
     A  43   N32  AM13          13  3     37EDTCDE(1)
     A       N32  CCY1           3A       55
     A       N32  @VAT           7A       59
     A            @ST1          15A       67
     A            MIR       R             83
     A            RPNM      R            112
     A*
     A          R DETAIL2                   SPACEA(1)
     A            TXT3          20A        2
     A     48                             29'SELL'
     A  44 48N32  AM20          13  0     37EDTCDE(1)
     A  45 48N32  AM21          13  1     37EDTCDE(1)
     A  46 48N32  AM22          13  2     37EDTCDE(1)
     A  47 48N32  AM23          13  3     37EDTCDE(1)
     A     48N32  CCY2           3A       55
     A                                    83'(COUNTERPARTY)'
     A            RPTN      R            103
     A*
     A          R DETAIL3                   SPACEA(1)
     A       N32  @ST2          15A       67
     A            BKRF          16A       83
     A       N32  BNAM          30A      103
     A*
     A          R DETAIL4                   SPACEA(2)
     A                                    83'(BROKER)'
     A       N32  BTWN          10A      103
     A*
     A          R NODETAIL
     A                                    53'***** NO DETAILS *****'
     A                                      SPACEB(2)
     A          R EOREPORT
     A                                    53'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *                                                                   S01310
     A**********R*DBERROR**
     A*********************                 SKIPB(17)
     A*********************               34'**** DATABASE ERROR ****'
     A*********************                 SPACEA(1)
     A*********************               20'******************************'
     A*********************               50'************************'
     A*********************                 SPACEA(1)
     A*********************               20'*    FILE NAME'
     A************DBFILE***      8        39
     A*********************               73'*'
     A*********************                 SPACEA(1)
     A*********************               20'*    KEY'
     A************DBKEY****     29        39
     A*********************               73'*'
     A*********************                 SPACEA(1)
     A*********************               20'*    PROGRAM NAME'
     A************DBPGM****     10        39
     A*********************               73'*'
     A*********************                 SPACEA(1)
     A*********************               20'*    AT'
     A************DBASE****      3  0     39
     A*********************               73'*'
     A*********************                 SPACEA(1)
     A*********************               20'******************************'
     A*********************               50'************************'
      *                                                                   S01310
