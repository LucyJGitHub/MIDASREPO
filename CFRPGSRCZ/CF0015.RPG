     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CF TRAM/branch groupings audit.')
     F*****************************************************************
     F*                                                               *
     F*  Midas - CONFIRMATION MATCHING
     F*                                                               *
     F*       CF0015 - TRAM/BRANCH GROUPINGS AUDIT REPORT             *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E39376             Date 01May92               *
      *                 S01184             Date 15Dec89               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*       E39376 - RECOMPILED FOR CHANGE TO PRINTER FILE          *
     F*       S01184 - CREATED FOR CONFIRMATION MATCHING              *
     F*                                                               *
     F*****************************************************************
     FCFBREXPDIF  E                    DISK
     FSDBANKPDIF  E                    DISK
     FCF0015AUO   E             20     PRINTER
     F*****************************************************************
     F* FUNCTION OF INDICATORS
     F* 20               OVERFLOW
     F* 30               ERROR SDBANKPD
     F* 31               EOF CFBREXPD
     F* 32               DETAIL LINE OUTPUT TO REPORT
     F* U7U8LR           DATABASE ERROR
     E*****************************************************************
     E                    MSGA    1   5 17
     E**  MESSAGES TABLE
     I*****************************************************************
     I*
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**  LOCAL DATA AREA - DATABASE ERRORS
     I*****************************************************************
      /EJECT
     C*****************************************************************
     C*               INDEX TO SUBROUTINES                            *
     C*        MAIN  - MAIN PROCESS                                   *
     C*        INIT  - INITIAL PROCESS                                *
     C*        *PSSR - ERROR HANDLING                                 *
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*        MAIN - MAIN PROCESS                                    *
     C*                                                               *
     C*        CALLS  INIT - INITIAL PROCESS                          *
     C*                                                               *
     C*        CALLED BY                                              *
     C*****************************************************************
     C*
     C                     EXSR INIT
     C*
     C**  READ CHANGES TO BRANCH CODES FILE
     C                     READ CFBREXPD                 31
     C           *IN31     DOWEQ'0'
     C           *IN20     IFEQ '1'
     C                     WRITEHEADER
     C                     SETOF                     20
     C                     END
     C                     MOVE *BLANKS   TXT1
     C                     MOVE *BLANKS   TXT2
     C*
     C**  "OVERWRITTEN WITH..."
     C           LCTP      IFEQ 'A'
     C           IMBR      IFEQ *BLANKS
     C                     MOVEL'BLANKS'  TXT2
     C                     ELSE
     C**  ORIGINAL BRANCH
     C                     MOVELIMBR      TXT2
     C                     END
     C                     MOVE MSGA,1    TXT1
     C                     MOVE SHBR      IMBR
     C                     END
     C*
     C**  "DELETED"
     C           LCTP      IFEQ 'D'
     C                     MOVE MSGA,2    TXT1
     C                     END
     C*
     C**  "INSERTED"
     C           LCTP      IFEQ 'I'
     C                     MOVE MSGA,3    TXT1
     C                     END
     C*
     C**  "DEFAULT REMOVED"
     C           LCTP      IFEQ 'R'
     C                     MOVE MSGA,4    TXT1
     C                     END
     C*
     C**  "DEFAULT INSTALLED"
     C           LCTP      IFEQ 'S'
     C                     MOVE MSGA,5    TXT1
     C                     END
     C*
     C**  OUTPUT TO PRINTER FILE
     C                     WRITEDETAIL
     C                     MOVE '1'       *IN32
     C                     READ CFBREXPD                 31
     C                     END
     C*
     C                     WRITEEOREPORT
     C*
     C                     SETON                     LR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*        INIT - INITIAL PROCESS                                 *
     C*                                                               *
     C*        CALLS       *PSSR - ERROR HANDLING                     *
     C*                                                               *
     C*        CALLED BY   MAIN - MAIN PROCESS                        *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'CF0015  'DBPGM
     C                     OUT  LDA
     C*
     C**  OBTAIN BANK'S STANDING DATA FROM PF/SDBANKPD
     C                     READ SDBANKPD                 30
     C           *IN30     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD01        DBASE            * DB ERROR 01 *
     C                     MOVEL'FIRST'   DBKEY            ***************
     C                     MOVEL'SDBANKPD'DBFILE
     C                     OUT  LDA
     C                     WRITEHEADER
     C                     WRITEDBERROR
     C                     EXSR *PSSR
     C                     END
     C*
     C                     WRITEHEADER
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*       *PSSR - ERROR HANDLING                                  *
     C*                                                               *
     C*        CALLS       NONE                                       *
     C*                                                               *
     C*        CALLED BY   INIT - INITIAL PROCESS                     *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
**  MESSAGES (MSGA)                                            *****
OVERWRITTEN WITH
DELETED                                                                ON MANUAL
INSERTED
DEFAULT REMOVED
DEFAULT INSTALLED
