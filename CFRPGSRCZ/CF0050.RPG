     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CF DL confs. status report request')
     F*****************************************************************
     F*                                                               *
     F*  Midas - CONFIRMATION MATCHING
     F*                                                               *
     F*       CF0050 - DEALING CONFIRMATIONS STATUS REPORT (CONTROL)  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01310             Date 31MAY91               *
     F*                 S01184             Date 15DEC89               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  S01310 - SWIFT RATIONALISATION                               *
     F*  S01184 - CREATED FOR CONFIRMATION MATCHING                   *
     F*                                                               *
     F*****************************************************************
     FCF0050DFCF  E                    WORKSTN
     FSDBANKPDIF  E                    DISK
     FSDBRCHL0IF  E           K        DISK                               S01310
     F*****************************************************************
     F* ID C  F  H  L    FUNCTION OF INDICATORS
     F* 01-09            STATUS SELECTION ERROR
     F* 24               PROTECT SELECTION SCREEN
     F* 30               ERROR SDBANKPD
     F* 40               SCREEN ERROR
     F* 41 OFF           START DEAL NO ERROR
     F* 42 OFF           END DEAL NO ERROR
     F* 43               COUNTERPARTY SELECTION ERROR
     F* 44               BROKER SELECTION ERROR
     F* 45               Multibranching on                                S01310
     F* 60               QCAEXEC ERROR
     F* U7U8             DB ERROR
     F* KC               F3
     F* KL               F12
     F*****************************************************************
     E*
     E                    ARY         9  1               STATUS
     E                    MSGA    1   6 78               MSGS
     E                    CPY@    1   1 80
     E********************CMDA    1   2 80               QCAEXEC CMD      S01310
     E*
     E*****************************************************************
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**  LOCAL DATA AREA - DATABASE ERRORS
     I*
     I           SDS
     I                                      244 253 WSID
     I**  WORKSTATION ID
     I*
     IPARAM       DS                                                      S01310
     I                                        1   6 SFDS                  S01310
     I                                        7  12 SFDE                  S01310
      *                                                                   S01310
     I                                       13  14 @SLT                  S01310
     I                                       13  13 SFCP                  S01310
     I                                       14  14 SFBK                  S01310
      *                                                                   S01310
     I                                       15  23 ARY                   S01310
     I                                       15  15 SF01                  S01310
     I                                       16  16 SF02                  S01310
     I                                       17  17 SF50                  S01310
     I                                       18  18 SF51                  S01310
     I                                       19  19 SF52                  S01310
     I                                       20  20 SF53                  S01310
     I                                       21  21 SF54                  S01310
     I                                       22  22 SF97                  S01310
     I                                       23  23 SF99                  S01310
     I                                       15  23 @ARY                  S01310
      **  Parameters data structure                                       S01310
      *                                                                   S01310
     I************DS*******                                               S01310
     I*********************                   1   2 @SLT                  S01310
     I*********************                   1   1 SFCP                  S01310
     I*********************                   2   2 SFBK                  S01310
     I****COUNTERPARTY,*BROKER SELECTIONS                                 S01310
     I*********************                                               S01310
     I************DS*******                                               S01310
     I*********************                   1   9 ARY                   S01310
     I*********************                   1   1 SF01                  S01310
     I*********************                   2   2 SF02                  S01310
     I*********************                   3   3 SF50                  S01310
     I*********************                   4   4 SF51                  S01310
     I*********************                   5   5 SF52                  S01310
     I*********************                   6   6 SF53                  S01310
     I*********************                   7   7 SF54                  S01310
     I*********************                   8   8 SF97                  S01310
     I*********************                   9   9 SF99                  S01310
     I*********************                   1   9 @ARY                  S01310
     I****FEEDBACK*STATUS*SELECTIONS                                      S01310
     I*********************                                               S01310
     I*CMD********DS*******                                               S01310
     I*********************                   1  80 @CMD1                 S01310
     I*********************                  81 146 @CMD2                 S01310
     I*********************                  91  96 @PRM1                 S01310
     I*********************                 102 107 @PRM2                 S01310
     I*********************                 113 114 @PRM3                 S01310
     I*********************                 120 128 @PRM4                 S01310
     I****QCMDEXC*Command*Data Structure                                  S01310
      *                                                                   S01310
     IDSRUN       DS                             13                       S01310
     I                                        1   7 RUNA                  S01310
     I                                       13  13 MBIN                  S01310
      **  RUNDAT data area                                                S01310
      *                                                                   S01310
     C           *ENTRY    PLIST                                          S01310
     C                     PARM           RSEQ    5                       S01310
     C                     PARM           RLEV    1                       S01310
     C                     PARM           RENT    3                       S01310
     C                     PARM           PARAM                           S01310
     C                     PARM           RACT    1                       S01310
     C                     PARM           RCMD    1                       S01310
      *
     I*****************************************************************
      /EJECT
     C*****************************************************************
     C*               INDEX TO SUBROUTINES                            *
     C*        MAIN - MAIN PROCESS                                    *
     C*        INIT - INITIAL PROCESS                                 *
     C*        VSEL - VALIDATE COUNTERPARTY/BROKER                   *
     C*        VSTS - VALIDATE STATUS                                *
     C*********SBMT*-*SUBMIT REPORT                                  *    S01310
     C*        *PSSR - ERROR HANDLING                                *
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*        MAIN - MAIN PROCESS                                    *
     C*                                                               *
     C*        CALLS   INIT - INITIAL PROCESS                         *
     C*                VSEL - VALIDATE COUNTERPARTY/BROKER            *
     C*                VSTS - VALIDATE STATUS                         *
     C*****************SBMT - SUBMIT REPORT                           *   S01310
     C*****************************************************************
     C*
     C                     EXSR INIT
     C*
     C**  DISPLAY SELECTION SCREEN UNTIL F3 PRESSED
     C           *INKC     DOUEQ'1'                                       S01310
     C           *INKL     OREQ '1'                                       S01310
     C           @VLD      OREQ 'Y'                                       S01310
     C************INKC*****DOWEQ'0'                                       S01310
     C*
     C**  IF NO ERRORS CLEAR INPUT FIELDS
     C           *IN40     IFEQ '0'
     C                     MOVE *BLANKS   SFDS
     C                     MOVE *BLANKS   SFDE
     C                     MOVE *BLANKS   @SLT
     C                     END
     C*
     C                     SETOF                     24
     C                     WRITECF0050F3
     C                     EXFMTCF0050F1
     C                     MOVE *BLANKS   SFMG
     C                     SETOF                     404344
     C                     SETON                     4142
     C*********************MOVE 'Y'       @VLD    1                       S01310
     C*
     C**  IF F3 NOT PRESSED VALIDATE SELECTION
     C           *INKC     IFEQ '0'
     C           *INKL     ANDEQ'0'                                       S01310
     C                     EXSR VSEL
     C*
     C**  IF SELECTION VALID
     C           *IN40     IFEQ '0'                                       S01310
     C***********@VLD******IFEQ 'Y'                                       S01310
     C*
     C**  IF COUNTERPARTY AND BROKER SUBMIT REPORT IMMEDIATELY
     C           @SLT      IFEQ 'YY'
     C                     MOVE 'Y'       @VLD    1                       S01310
     C*********************EXSR SBMT                                      S01310
     C*
     C**  DISPLAY STATUS SCREEN UNTIL F3 OR F12 PRESSED OR VALID
     C                     ELSE
     C           *INKC     DOUEQ'1'
     C           *INKL     OREQ '1'
     C           @VLD      OREQ 'Y'
     C*
     C**  IF NO ERRORS CLEAR OPTION INPUT FIELDS
     C           *IN40     IFEQ '0'
     C                     MOVE *BLANKS   ARY
     C                     END
     C*
     C**  DISPLAY SCREEN WITH STATUS OPTIONS
     C                     SETON                     24
     C                     WRITECF0050F3
     C                     WRITECF0050F1
     C                     EXFMTCF0050F2
     C                     MOVE *BLANKS   SFMG
     C                     SETOF                     40
     C                     SETOF                     010203
     C                     SETOF                     040506
     C                     SETOF                     070809
     C*********************MOVE 'Y'       @VLD    1                       S01310
     C*
     C**  IF F3 AND F12 NOT PRESSED VALIDATE STATUS
     C           *INKC     IFEQ '0'
     C           *INKL     ANDEQ'0'
     C                     EXSR VSTS
     C*
     C**  IF SELECTION VALID SUBMIT REPORT
     C           *IN40     IFEQ '0'                                       S01310
     C                     MOVE 'Y'       @VLD                            S01310
     C***********@VLD******IFEQ 'Y'                                       S01310
     C*********************EXSR SBMT                                      S01310
     C                     END
     C                     END
     C                     END
      *                                                                   S01310
     C                     MOVE '0'       *INKL                           S01310
     C                     END
     C                     END
     C                     END
     C                     END
     C*
      **  If F3 pressed terminate program                                 S01310
     C           *INKC     IFEQ '1'                                       S01310
     C                     MOVE 'E'       RCMD                            S01310
     C                     RETRN                                          S01310
     C                     END                                            S01310
      *                                                                   S01310
      **  If F12 pressed terminate program                                S01310
     C           *INKL     IFEQ '1'                                       S01310
     C                     MOVE 'F'       RCMD                            S01310
     C                     RETRN                                          S01310
     C                     END                                            S01310
      *
     C                     SETON                     LR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*        INIT - INITIAL PROCESS                                 *
     C*                                                               *
     C*        CALLED BY   MAIN  - MAIN PROCESS                       *
     C*                                                               *
     C*        CALLS       *PSSR - ERROR HANDLING                     *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
      *
     C**  DEFINE LOCAL DATA AREA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE                          S01310
     C                     MOVE *BLANKS   DBKEY                           S01310
     C                     MOVE *BLANKS   DBASE                           S01310
     C                     MOVEL'CF0050  'DBPGM
     C                     OUT  LDA
     C*
     C**  READ BANK DETAILS
     C           1         CHAINSDBANKPD             30                   S01310
     C*********************READ SDBANKPD                 30               S01310
     C*
     C           *IN30     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD01        DBASE            * DB ERROR 01 *
     C                     MOVEL'FIRST'   DBKEY
     C                     MOVEL'SDBANKPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C**  SWITCH OFF NON NUMERIC DEAL NUMBER ERROR
     C                     SETON                     4142
     C*
      **  Access RUNDAT to find run date & multibranching indicator       S01310
     C           *NAMVAR   DEFN RUNDAT    DSRUN                           S01310
     C                     IN   DSRUN                                     S01310
      *                                                                   S01310
      **  If multibranching on                                            S01310
     C           MBIN      IFEQ 'Y'                                       S01310
      *                                                                   S01310
      **  If not system level                                             S01310
     C           RLEV      IFNE 'S'                                       S01310
      *                                                                   S01310
      **  Access SDBRCHL0 for branch name                                 S01310
     C           RENT      IFNE 'ALL'                                     S01310
     C           RENT      CHAINSDBRCHL0             88                   S01310
      *                                                                   S01310
     C           *IN88     IFEQ '1'                                       S01310
     C           *LOCK     IN   LDA                                       S01310
     C                     MOVE '01'      DBASE                           S01310
     C                     MOVE RENT      DBKEY            * DB ERROR 01 *S01310
     C                     MOVE 'SDBRCHL0'DBFILE                          S01310
     C                     OUT  LDA                                       S01310
     C                     EXSR *PSSR                                     S01310
     C                     END                                            S01310
     C                     END                                            S01310
      *                                                                   S01310
      **  Display branch code                                             S01310
     C                     MOVE '1'       *IN45                           S01310
     C                     END                                            S01310
     C                     END                                            S01310
      *
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*        VSEL - COUNTERPARTY/BROKER                             *
     C*                                                               *
     C*        CALLED BY   MAIN - MAIN PROCESS                        *
     C*                                                               *
     C*        CALLS       NONE                                       *
     C*****************************************************************
     C*
     C           VSEL      BEGSR
     C*
     C**  DEAL NUMBERS MUST BE NUMERIC
     C                     TESTN          SFDS       41
     C                     TESTN          SFDE       42
     C           *IN41     IFEQ '0'
     C           *IN42     OREQ '0'
     C                     MOVELMSGA,1    SFMG
     C                     SETON                     40
     C*********************MOVE 'N'       @VLD                            S01310
     C*
     C**  END DEAL MUST BE GREATER THAN OR EQUAL TO START DEAL
     C                     ELSE
     C           SFDE      IFLT SFDS
     C                     MOVELMSGA,6    SFMG
     C                     SETON                     40
     C                     SETOF                     4142
     C*********************MOVE 'N'       @VLD                            S01310
     C*
     C**  COUNTERPARTY OR BROKER MUST BE SELECTED
     C                     ELSE
     C           @SLT      IFEQ *BLANKS
     C                     MOVELMSGA,2    SFMG
     C                     SETON                     404344
     C*********************MOVE 'N'       @VLD                            S01310
     C                     ELSE
     C*
     C**  COUNTERPARTY SELECTION MUST BE Y, N OR BLANK
     C           SFCP      IFNE 'Y'
     C           SFCP      ANDNE'N'
     C           SFCP      ANDNE' '
     C                     MOVELMSGA,2    SFMG
     C                     SETON                     4043
     C*********************MOVE 'N'       @VLD                            S01310
     C                     END
     C*
     C**  BROKER SELECTION MUST BE Y, N OR BLANK
     C           SFBK      IFNE 'Y'
     C           SFBK      ANDNE'N'
     C           SFBK      ANDNE' '
     C                     MOVELMSGA,2    SFMG
     C                     SETON                     4044
     C*********************MOVE 'N'       @VLD                            S01310
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*        VSTS - VALIDATE STATUS                                 *
     C*                                                               *
     C*        CALLED BY   MAIN - MAIN PROCESS                        *
     C*                                                               *
     C*        CALLS       NONE                                       *
     C*****************************************************************
     C*
     C           VSTS      BEGSR
     C*
     C           @ARY      IFEQ *BLANKS
     C                     MOVELMSGA,5    SFMG
     C                     SETON                     400102
     C                     SETON                     030405
     C                     SETON                     060708
     C                     SETON                     09
     C*********************MOVE 'N'       @VLD                            S01310
     C                     ELSE
     C*
     C**  STATUS SELECTION MUST BE Y, N OR BLANK
     C                     Z-ADD1         X       30
     C           X         DOWLE9
     C           ARY,X     IFNE 'Y'
     C           ARY,X     ANDNE'N'
     C           ARY,X     ANDNE' '
     C                     MOVELMSGA,3    SFMG
     C                     MOVE '1'       *IN,X
     C                     SETON                     40
     C*********************MOVE 'N'       @VLD                            S01310
     C                     END
     C                     ADD  1         X
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*********SBMT*-*SUBMIT REPORT                                   *   S01310
     C*********************                                           *   S01310
     C*********CALLED*BY***MAIN - MAIN PROCESS                        *   S01310
     C*********************                                           *   S01310
     C*********CALLS*******NONE                                       *   S01310
     C*****************************************************************
     C*********************                                               S01310
     C***********SBMT******BEGSR                                          S01310
     C*********************                                               S01310
     C****CALL*CFC0055*WITH PARAMATERS OF START DEAL NUMBER, END DEAL     S01310
     C****NUMBER,*COUNTERPARTY?, BROKER? AND STATUS OPTIONS               S01310
     C*********************MOVELCMDA,1    @CMD1                           S01310
     C*********************MOVELCMDA,2    @CMD2                           S01310
     C*********************MOVE SFDS      @PRM1                           S01310
     C*********************MOVE SFDE      @PRM2                           S01310
     C*********************MOVE @SLT      @PRM3                           S01310
     C*********************MOVE @ARY      @PRM4                           S01310
     C*********************CALL 'QCMDEXC'              60                 S01200
     C*********************PARM           @CMD                            S01310
     C*********************PARM 146       @LEN   155                      S01310
     C*********************MOVE MSGA,4    SFMG                            S01310
     C*********************                                               S01310
     C*********************ENDSR                                          S01310
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*        *PSSR - ERROR HANDLING                                 *
     C*                                                               *
     C*        CALLED BY   INIT - INITIAL PROCESS                     *
     C*                                                               *
     C*        CALLS       NONE                                       *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
**  MESSAGES
001  START AND END DEAL NUMBER MUST BE ENTERED AND MUST BE NUMERIC
002  MUST ENTER 'Y' TO SELECT BY BROKER AND/OR COUNTERPARTY STATUS
003  ENTER 'Y' TO SELECT OPTION, 'N' OR BLANK TO OMIT
REPORT SUBMITTED
004  AT LEAST ONE OPTION MUST BE SELECTED
005  END OF RANGE MUST NOT BE LESS THAN START
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
