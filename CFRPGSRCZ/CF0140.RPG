     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CF Collect latest feedback from TRAM')
     F*****************************************************************
     F*                                                               *
     F*  Midas - CONFIRMATION MATCHING
     F*                                                               *
     F*       CF0140 - COLLECT LATEST FEEDBACK FROM TRAM              *
     F*                                                               *
     F*       N.B.  any changes to the format of CFFEEDPD or          *
     F*             CFWFEDPD will require changes in this program     *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 113352             Date 13FEB97               *
     F*                 S01408             Date 10MAY96               *
     F*                      S01408            DATE 12JUN95           *
     F*                      S01310            DATE 07AUG91           *
     F*                      S01184            DATE 15DEC89           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  113352 - RE-COMPILE OVER CHANGES IN MGOREFPD                 *
     F*  S01408 - Addition of core hook  CF0140CCP1                   *
     F*  S01408 - Addition of core hooks CF0140FFPG                   *
     F*                                  CF0140IIPG                   *
     F*                                  CF0140CCPG                   *
     F*  S01310 - SWIFT RATIONALISATION                               *
     F*  S01184 - CREATED FOR CONFIRMATION MATCHING                   *
     F*                                                               *
     F*****************************************************************
     FCFWFEDL0IF  E           K        DISK
     F            CFFEEDD0                          KRENAMECFWFEDD0
     FCFFEEDL0UF  F     256 16AI    13 DISK                      A
     FMGOREFL0IF  E           K        DISK                               S01310
     F*                                                                   S01408
     F/COPY WNCPYSRC,CF0140FFPG                                           S01408
     F*                                                                   S01408
     F*****************************************************************
     F* FUNCTION OF INDICATORS
     F*
     F* 10               EOF MGOREFL0
     F* 80               EOF CFWFEDL0
     F* 81               EOF CFFEEDL0
     F* U7U8LR           DATABASE ERROR
     F*
     F*****************************************************************
     I*
     ICFFEEDL0NS
     I                                       13  28 IMRF
     I*
     I** INTERNAL DESCRIPTION ONLY READS IN 1 FIELD, SO
     I** DATA IS NOT OVERWRITTEN.
     I*
     I/COPY WNCPYSRC,CF0140I001
     I            DS                              6                       S01184
     I**  CFWFEDPD CHANGE DATE IN DDMMYY FORMAT (received from TRAM)      S01184
     I                                        1   2 @D                    S01184
     I                                        3   4 @M                    S01184
     I                                        5   6 @Y                    S01184
     I                                        1   60@DTSC                 S01184
     I*
     I            DS                              6                       S01184
     I**  CFFEEDPD CHANGE DATE IN SWIFT (YYMMDD) FORMAT                   S01184
     I                                        1   2 YY                    S01184
     I                                        3   4 MM                    S01184
     I                                        5   6 DD                    S01184
     I                                        1   60DTSC                  S01184
     I*                                                                   S01408
     I/COPY WNCPYSRC,CF0140IIPG                                           S01408
     I*                                                                   S01408
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*               INDEX TO SUBROUTINES                            *
     C*        MAIN - MAIN PROCESS                                    *
     C*       *PSSR - ERROR HANDLING                                  *
     C*****************************************************************
      /EJECT
     C*                                                                   S01408
     C/COPY WNCPYSRC,CF0140CCP1                                           S01408
     C*                                                                   S01408
     C*****************************************************************
     C*                                                               *
     C*        MAIN   - MAIN PROCESS                                  *
     C*                                                               *
     C*        CALLS  - NONE                                          *
     C*                                                               *
     C*****************************************************************
     C*
     C**  OBTAIN LATEST FEEDBACK RECORD
     C**  (CFWFEDL0 is key on descending DATE/TIME within IMRF)
     C                     READ CFWFEDL0                 80
     C*
     C           *IN80     DOWEQ'0'
     C*
     C**  Convert DDMMYY from CFWFEDPD (DTSC) to YYMMDD for CFFEEDPD
     C**  (also DTSC)
     C                     MOVELDTSC      @DTSC
     C                     MOVEL@D        DD
     C                     MOVEL@Y        YY
     C*
     C**  WRITE NEW RECORD, UPDATE EXISTING RECORD ON FEEDBACK FILE
     C           IMRF      CHAINCFFEEDL0             81
     C*
     C           *IN81     IFEQ '1'
      *
      **  Obtain branch for new record                                    S01310
     C           IMRF      CHAINMGOREFL0             10                   S01310
     C           *IN10     IFEQ '0'                                       S01310
     C                     EXCPTADD
     C                     END                                            S01310
     C                     ELSE
     C/COPY WNCPYSRC,CF0140C001
     C                     EXCPTUPD
     C                     END
     C*                                                                   S01408
     C/COPY WNCPYSRC,CF0140CCPG                                           S01408
     C*                                                                   S01408
     C*
     C**  OBTAIN LATEST FEEDBACK WITH NEXT TRANSACTION REFERENCE
     C**  (CFWFEDL0 is key on descending change DATE/TIME within IMRF)
     C*
     C           IMRF      SETGTCFWFEDL0
     C                     READ CFWFEDL0                 80
     C                     END
     C*
     C                     SETON                     LR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*       *PSSR - ERROR HANDLING                                  *
     C*                                                               *
     C*        CALLS  - NONE                                          *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     OCFFEEDL0EADD             ADD
     O                         DTSC       6
     O                         TMSC      12
     O                         IMRF      28
     O                         FRF1      63
     O                         FRF2      98
     O                         CPRF     114
     O                         CPSX     116
     O                         BKRF     132
     O                         BKSX     134
     O                         CHSN     135
     O                         CNRQ     136
     O                         BRCA     139
     OCFFEEDL0E                UPD
     O                         DTSC       6
     O                         TMSC      12
     O                         IMRF      28
     O                         FRF1      63
     O                         FRF2      98
     O                         CPRF     114
     O                         CPSX     116
     O                         BKRF     132
     O                         BKSX     134
     O*********************    CHSN     135                               S01310
     O*********************    CNRQ     136                               S01310
     O/COPY WNCPYSRC,CF0140O001
