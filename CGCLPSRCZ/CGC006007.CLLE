/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas CG LE Fee Rate Change Advices')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas - User Defined Correspondence                         */
/*                                                                   */
/*       CGC006007  - Midas CG LE Fee Rate Change Advices            */
/*                                                                   */
/*       (c) Finastra International Limited 2018                     */
/*                                                                   */
/*       Last Amend No. CLE071  *CREATE    Date 18Jul18              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CLE071 - Value Dating of Rate Changes to Fees               */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&COMP_NAME &COMP_SEQ &EXTRACT &MEMBER +
                          &COMMIT_OPT)

/** Declare variables */

             DCL        VAR(&COMP_NAME)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMP_SEQ)   TYPE(*DEC)  LEN(5)
             DCL        VAR(&COMP_SEQA)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&EXTRACT)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMMIT_OPT) TYPE(*CHAR) LEN(3)
             DCL        VAR(&RTN_CODE)   TYPE(*CHAR) LEN(7)
             DCL        VAR(&STATUS)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&DATA)       TYPE(*CHAR) LEN(50)
             DCL        VAR(&MSG_DATA)   TYPE(*CHAR) LEN(256)
             DCL        VAR(&ACTION) TYPE(*CHAR) LEN(8)

             COPYRIGHT TEXT('(c) Finastra International Limited 2018')

/** Global Monitor Message */

             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) +
                           EXEC(GOTO ABNOR)

/** Send Message to MRUNQ and standard processing parameters */

             SNDPGMMSG  MSG('CGC006007 - Fee Rate Change Notices') +
                          TOMSGQ(MRUNQ)

/** Create data LDA in QTEMP */

             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)

/** Call Extract File Instance Management */
             CHGVAR     VAR(&ACTION) VALUE('*PREPARE')
             CALL       PGM(CGC3605) PARM(&ACTION)

/** Run Extract Component */

             CHGVAR     VAR(&COMP_SEQA) VALUE(&COMP_SEQ)
             CHGVAR     VAR(&MSG_DATA) VALUE(&COMP_NAME *CAT +
                          &COMP_SEQA *CAT &EXTRACT *CAT &MEMBER +
                          *CAT &COMMIT_OPT)
             SNDPGMMSG  MSGID(CGD1271) MSGF(CGUSRMSG) +
                           MSGDTA(&MSG_DATA) TOPGMQ(*PRV)

             OVRDBF     FILE(FCLTY) SHARE(*NO)
             CALL       PGM(CG6077) PARM(&RTN_CODE &COMMIT_OPT)
             DLTOVR     FILE(FCLTY)
             IF         COND(&RTN_CODE *NE '       ') THEN(DO)
                ROLLBACK
                MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             ENDDO

/** Check for Database errors trapped by program */

             IF    COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) +
                              MSGDTA(&DATA) TOPGMQ(*EXT) +
                              TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO

/** Call Custom Defined Data Extract pgm */

             CALL  PGM(CGC3604)
             IF    COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) +
                              MSGDTA(&DATA) TOPGMQ(*EXT) +
                              TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO

             GOTO       CMDLBL(END)

/** Report Database errors trapped by program */
DBERR:
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) +
                           MSGDTA(&DATA) TOPGMQ(*EXT) TOMSGQ(MOPERQ)

/** Abnormal termination processing */
ABNOR:
             CHGVAR     VAR(&ACTION) VALUE('*CLEAR  ')
             CALL       PGM(CGC3605) PARM(&ACTION)
             ROLLBACK
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)

             SNDPGMMSG  MSG('Program CGC006007 ended abnormally') +
                           TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          CGC006007 ended abnormally') MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)

 END:
             RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             ENDPGM
