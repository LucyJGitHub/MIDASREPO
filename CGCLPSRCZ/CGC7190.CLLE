/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas Load Retail Interest Scale Qualif Acct codes')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       CGC7190 - Load Retail Interest Scale Qualifying Acct Codes  */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2011           */
/*                                                                   */
/*       Last Amend No. CER042  *CREATE    Date 11May11              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CER042 - Interest Scale Report Correspondence               */
/*                (Upgrade of FGE178/179)                            */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&PGMQ) TYPE(*CHAR) LEN(10) VALUE('+
                          CGC7190')
             DCL        VAR(&RELQ) TYPE(*CHAR) LEN(5) VALUE('*SAME')
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7) VALUE('  ')
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10) +
                          VALUE('CGUSRMSG')
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(132) VALUE(' ')
             DCL        VAR(&MSGTYP) TYPE(*CHAR) LEN(7) VALUE(' ')
             DCL        VAR(&WSTERM) TYPE(*CHAR) LEN(4) VALUE(' ')
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2011')
 
/* Declare the screen                                               */
             DCLF       FILE(CGC7190DF)
 
/* Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Override Message file */
             OVRMSGF    MSGF(CGUSRMSG) TOMSGF(GBCGUSRMSG)
 
/* Create LDA if it does not exist */
 
             CHKOBJ     OBJ(LDA) OBJTYPE(*DTAARA)
             MONMSG MSGID(CPF9801) EXEC(DO)
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          TEXT('Midas SD Local data area')
 
             CALL       PGM(Y2CLMSC) PARM(&PGMQ &RELQ)
             ENDDO
 
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             RTVJOBA    JOB(&D0WSID) USER(&D0USER)
             RTVDTAARA  (RUNDAT (1 7)) RTNVAR(&D0RUNA)
             CHGJOB     SWS(XXXXXX00)
             CHGVAR     VAR(&##PGM) VALUE(&PGMQ)
 
/* Write/read the screen */
DSPLY:
             SNDF      RCDFMT(#MSGCTL)
             SNDRCVF    RCDFMT(CGC7190F0)
 
/* Clear the program message queue */
             CALL Y2CLMSC PARM(&PGMQ &RELQ)
 
/* Function termination */
             IF         COND(&IN03) THEN(GOTO CMDLBL(END))
 
/* Confirm refresh file */
             IF &IN06 THEN(DO)
 
/* Allocate the Retail Interest Scale Qualifying Account Codes file */
             ALCOBJ     OBJ((CGISRQPD *FILE *EXCLRD)) WAIT(1)
 
/* File is currently in use */
                MONMSG CPF1002 EXEC(DO)
 
/* Clear the program message queue                                  */
                   CALL Y2CLMSC PARM(&PGMQ &RELQ)
 
/* Display file in use message                                      */
                   CHGVAR &MSGID VALUE('ISR0029')
 
/* Write message/s to the program message queue                     */
                   CALL Y2SNMGC PARM(&PGMQ &RELQ &MSGID &MSGF &MSGDTA +
                                     &MSGTYP)
 
                   CHGVAR     VAR(&MSGID) VALUE(' ')
                   CHGVAR     VAR(&MSGDTA) VALUE(' ')
                   CHGVAR     VAR(&MSGTYP) VALUE(' ')
 
                   GOTO       CMDLBL(DSPLY)
             ENDDO
 
/* Clear the Retail Interest Scale qualifying account codes file of data */
             CLRPFM     FILE(CGISRQPD)
 
/* File is currently in use                                         */
                MONMSG (CPF3130) EXEC(DO)
 
/* Clear the program message queue                                  */
                   CALL Y2CLMSC PARM(&PGMQ &RELQ)
 
/* Display file in use message                                      */
                   CHGVAR &MSGID VALUE('ISR0029')
 
/* Write message/s to the program message queue                     */
                   CALL Y2SNMGC PARM(&PGMQ &RELQ &MSGID &MSGF &MSGDTA +
                                     &MSGTYP)
 
                   CHGVAR     VAR(&MSGID) VALUE(' ')
                   CHGVAR     VAR(&MSGDTA) VALUE(' ')
                   CHGVAR     VAR(&MSGTYP) VALUE(' ')
 
                   GOTO       CMDLBL(DSPLY)
             ENDDO
 
             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Re-build the Retail Interest Scale qualifying account codes file */
             CALL       PGM(CG7190)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* Function termination                                             */
             GOTO       CMDLBL(END)
 
             ENDDO
 
             GOTO DSPLY
 
/* Abnormal termination */
 ABNOR:
             DMPCLPGM
             MONMSG (CPF0000 MCH0000 RPG0000)
 
             CHGJOB    SWS(XXXXXX11)
             MONMSG (CPF0000 MCH0000 RPG0000)
 
             SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                           CGC7190 ended abnormally - see job log') +
                           TOPGMQ(*EXT) TOMSGQ(MOPERQ)
             MONMSG (CPF0000 MCH0000 RPG0000)
 
 
/* Normal termination */
 
 END:
 
/* De-allocate the Retail Interest Scale Qualifying Account Codes file */
 
             DLCOBJ OBJ((CGISRQPD *FILE *EXCLRD))
             MONMSG (CPF0000 MCH0000 RPG0000)
 
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Misys International +
                          Banking Systems Ltd.')
             ENDPGM
 
