/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas COB Extract Interest Scale component')          */
/*********************************************************************/
/*                                                                   */
/*       Midas - User Defined Correspondence                         */
/*                                                                   */
/*       CGC7290 - COB Extract Interest Scale component              */
/*                                                                   */
/*       (c) Finastra International Limited 2011                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CER042  *CREATE    Date 11May11              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CER042 - Interest Scale Report Correspondence               */
/*                (Upgrade of FGE178/179)                            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&COMP_NAME &COMP_SEQ &EXTRACT &MEMBER +
                          &COMMIT_OPT)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2011')
 
/*  Declare the variables                                       */
             DCL        VAR(&RTN_CODE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&COMP_NAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMP_SEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&COMP_SEQA) TYPE(*CHAR) LEN(5)
             DCL        VAR(&COMMIT_OPT) TYPE(*CHAR) LEN(3)
             DCL        VAR(&EXTRACT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DATA) TYPE(*CHAR) LEN(50)
             DCL        VAR(&MSG_DATA) TYPE(*CHAR) LEN(256)
 
             DCL        VAR(&ACTION) TYPE(*CHAR) LEN(8)
 
             DCL        VAR(&SRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SOPTN) TYPE(*CHAR) LEN(7) VALUE('*VERIFY')
             DCL        VAR(&SSARD) TYPE(*CHAR) LEN(6) VALUE('CER042')
 
/* Global Monitor Message */
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Send Message to MRUNQ and standard processing parameters */
             SNDPGMMSG  MSG('CGC7290 - COB Interest Scale +
                          Component') TOMSGQ(MRUNQ)
 
/* Set off the job switches */
             CHGJOB     SWS(XXXXX000)
 
/* Reset the return code */
             CHGVAR     VAR(&RTN_CODE) VALUE(' ')
 
/* Verify for switch CER042 (Interest Scale Report Correspondence) */
             CALL       PGM(AOSARDR0) PARM(&SRTCD &SOPTN &SSARD)
             IF         COND(&SRTCD *EQ '       ') THEN(DO)
 
/* Create data LDA in QTEMP  */
                CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
                MONMSG     MSGID(CPF1023)
 
 
/** Call Extract File Instance Management */
                CHGVAR     VAR(&ACTION) VALUE('*PREPARE')
                CALL       PGM(CGC3605) PARM(&ACTION)
 
/* Run Extract Component */
                CHGVAR     VAR(&COMP_SEQA) VALUE(&COMP_SEQ)
                CHGVAR     VAR(&MSG_DATA) VALUE(&COMP_NAME *CAT +
                          &COMP_SEQA *CAT &EXTRACT *CAT &MEMBER +
                          *CAT &COMMIT_OPT)
                SNDPGMMSG  MSGID(CGD1271) MSGF(CGUSRMSG) +
                          MSGDTA(&MSG_DATA) TOPGMQ(*PRV)
                CALL       PGM(CG7290) PARM(&RTN_CODE &COMMIT_OPT)
 
/* Check for Database errors trapped by program */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&DATA) +
                          TOPGMQ(*EXT) TOMSGQ(MOPERQ)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
/** Call Custom-Defined Data Extract pgm */
             CALL       PGM(CGC3604)
 
/* Report Database errors trapped by program */
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&DATA) +
                          TOPGMQ(*EXT) TOMSGQ(MOPERQ)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
    ENDDO
 
             GOTO       CMDLBL(ENDCLPGM)
 
/* Abnormal termination processing */
ABNOR:
             CHGVAR     VAR(&ACTION) VALUE('*CLEAR  ')
             CALL       PGM(CGC3605) PARM(&ACTION)
             ROLLBACK
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
 
             SNDPGMMSG  MSG('Program CGC7290 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          CGC7290 ended abnormally') MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
 
 ENDCLPGM:   RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Finastra International +
                          Limited')
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             ENDPGM
