/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas Interest Scale Lending Historic Extract')       */
/*********************************************************************/
/*                                                                   */
/*       Midas - User Defined Correspondence                         */
/*                                                                   */
/*       CGC7360 - Interest Scale Lending Historic Extract           */
/*                                                                   */
/*       (c) Finastra International Limited 2011                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CER042  *CREATE    Date 11May11              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CER042 - Interest Scale Report Correspondence               */
/*                (Upgrade of FGE178/179)                            */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&STDT &ENDT &BRCA &CNUM &CCY &DEAL &SUB)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2011')
 
/*  Declare the variables */
             DCL        VAR(&STDT) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&ENDT) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&START) TYPE(*CHAR) LEN(5)
             DCL        VAR(&END) TYPE(*CHAR) LEN(5)
             DCL        VAR(&BRCA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&CNUM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CUST) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CCY) TYPE(*CHAR) LEN(3)
             DCL        VAR(&DEAL) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SUB) TYPE(*CHAR) LEN(2)
             DCL        VAR(&QRYSEL) TYPE(*CHAR) LEN(200)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
 
/* Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Create LDA if it does not already exist */
 
             CHKOBJ     OBJ(LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ') TEXT('Midas SD AS400 Local +
                          Date Area'))
 
/* Set-off Database Error Switches */
 
             CHGJOB     SWS(XXXXXX00)
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
 
             OVRDBF     FILE(HISTSHP) SHARE(*YES)
 
/*  Set the date as alpha and the select fields */
             CHGVAR     VAR(&START) VALUE(&STDT)
             CHGVAR     VAR(&END) VALUE(&ENDT)
             CHGVAR     VAR(&CUST) VALUE(&CNUM)
 
/*  Select on : Event date is between run date                */
/*                            and date next working day       */
/*              Record type is interest payment               */
/*              Deal type is selected deal type of historic   */
/*              Deal sub-type is sub-type of historic         */
/*              Branch code is branch code of historic        */
/*              Customer number is of historic                */
/*              Currency is of historic                       */
 
             CHGVAR     VAR(&QRYSEL) VALUE('(VDAT *GE ' *CAT &START +
                          *CAT ') *AND (VDAT *LE ' *CAT &END *CAT +
                          ') *AND (AMTP = "IN") *AND (LTYP *EQ "' +
                          *CAT &DEAL *CAT '") *AND (SUTP *EQ "' +
                          *CAT &SUB *CAT '") *AND (BRCA *EQ "' *CAT +
                          &BRCA *CAT '") *AND (CNUM *EQ "' *CAT +
                          &CUST *CAT '") *AND (CCY *EQ "' *CAT &CCY +
                          *CAT '")')
 
/*  Open the query file with a selection                        */
 
             OPNQRYF    FILE((HISTSHP)) QRYSLT(&QRYSEL)
 
/*  Call the extract program */
 
             CALL       PGM(CG7230)
 
/*  Close the query file */
 
             CLOF       OPNID(HISTSHP)
 
/*  Database error handling */
 
             IF   COND(%SWITCH(XXXXXX11))   THEN(DO)
                RTVDTAARA   DTAARA(LDA (134 50))   RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                    TOPGMQ(*EXT) TOMSGQ(MOPERQ)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             DMPCLPGM
             SNDPGMMSG  MSG('Program CGC7360 ended abnormally') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
 
/* Normal termination */
 
 END:        ENDPGM
