      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  SCGSNMSC   - Midas CG Send message                           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  CGSNMS   : Send message to program's message queue           *
      *                                                               *
      *  CALLED BY: When reporting errors                             *
      *                                                               *
      *  CALLS    : *NONE                                             *
      *                                                               *
      *****************************************************************
     CSR         CGSNMS    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'CGSNMS'  @STK,Q
      *
      * If no program send to ##PGM
      *
     C           CGPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     CGPGMQ
     C                     END
      *
      * If level to send is blanks default to *SAME
      *
     C           CGPGRL    IFEQ *BLANK
     C                     MOVEL'*SAME'   CGPGRL
     C                     END
      *
      * If no message file specified, use default
      *
     C           CGMSGF    IFEQ *BLANK
     C                     MOVEL'CGUSRMSG'CGMSGF
     C                     END
      *
     C                     CALL 'AOCREPT'
     C                     PARM           CGMSID  7
     C                     PARM           CGMSGF 10
     C                     PARM           CGMSFL 10
     C                     PARM           CGMSDT256
     C                     PARM           CGPGRL  5
     C                     PARM           CGPGMQ 10
     C                     PARM           CGMSGQ 10
     C                     PARM           CGMSGT  7
      *
     C                     MOVEL'Y'       CGMSGS  1
      *
      * Clear all fields for default mechanism next time
      *
     C                     MOVEL*BLANK    CGMSID
     C                     MOVEL*BLANK    CGMSGF
     C                     MOVEL*BLANK    CGMSFL
     C                     MOVEL*BLANK    CGMSDT
     C                     MOVEL*BLANK    CGPGRL
     C                     MOVEL*BLANK    CGPGMQ
     C                     MOVEL*BLANK    CGMSGQ
     C                     MOVEL*BLANK    CGMSGT
      *
      *  Unwind subroutine stack name
      *
     C           CGEXIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
      *****************************************************************
      * End of /COPY SCGSNMSC                                         *
      *****************************************************************
