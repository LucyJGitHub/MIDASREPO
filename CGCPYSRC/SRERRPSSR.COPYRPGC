      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  SRERRPSSR  - Midas CG Standard *PSSR error subroutine.       *
      *                                                               *
      *   Subroutine *PSSR handles program errors.                    *
      *  An ILE version of this /Copy is created as /Copy SRERRPSSRL. *   CLE011
      *  These two sources should be made concurrent.                 *   CLE011
      *                                                               *
      *   Note: this routine requires copy member SRERRI or           *
      *         its equivalent in the input specifications.           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CLE011             Date 07Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 086243             Date 06Apr95               *
      *                 nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *  086243 - Substring literal on its length not translated      *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR  **
     C*
     C                     DUMP
     C                     MOVE CSC022    CSC022  1                                           CSC022
     C                     MOVE WCMTSK    WCMTSK  1                                           CSC022
     C           CSC022    IFEQ 'N'                                                           CSC022
     C           CSC022    OREQ 'Y'                                                           CSC022
     C           WCMTSK    ANDEQ'N'                                                           CSC022
     C                     ROLBK                       90
     C                     ENDIF                                                              CSC022
     C*
     C* If second time through, halt:
     C*
     C           @@PSSR    IFNE *BLANK
     C                     SETON                     H1  LR
     C                     RETRN
     C                     ENDIF
     C*
     C* Flag routine as started; define action code:
     C*
     C                     MOVE 'Y'       @@PSSR  1
     C                     MOVE W0ACT     W0ACT   8
     C* Select:
     C                     SELEC
     C*
     C* Test field W0ACT (set externally but defined here):
     C*___________________________________________________________________
     C********** W0ACT     WHEQ '*DIAGNOS'                                086243
     C           W0ACT     WHNE '*PRINT  '                                086243
     C           ##STCD    ANDEQ121                          array index
     C********** W0ACT     OREQ '*DIAGNOS'                                086243
     C           W0ACT     ORNE '*PRINT  '                                086243
     C           ##STCD    ANDEQ122                          occurrence
     C*
     C* In diagnostic mode, with an array index error --
     C*  issue a specific message and return:
     C*
     C                     CALL 'AOCREPT'              9090
     C                     PARM 'CGD1262' #MSGID  7
     C                     PARM 'CGUSRMSG'#MSGF  10
     C                     PARM *BLANKS   #MSGFL 10
     C                     PARM *BLANKS   #MSGDT256
     C                     PARM '*PRV '   #MSGR   5
     C                     PARM '*'       #PRGM  10
     C                     PARM 'MOPERQ ' #MSGQ  10
     C                     PARM '*INFO  ' #MSGT   7
     C*
     C* Reset subroutine flag:
     C*
     C                     MOVE 'N'       @@PSSR
     C*___________________________________________________________________
     C           W0ACT     WHEQ '*PRINT  '
     C*
     C* In print mode --
     C*  issue a specific message and return:
     C*
     C                     MOVE '*ERROR ' W0RTN   7
     C*
     C                     CALL 'AOCREPT'              9090
     C                     PARM 'CGD1262' #MSGID
     C                     PARM 'CGUSRMSG'#MSGF
     C                     PARM *BLANKS   #MSGFL
     C                     PARM *BLANKS   #MSGDT
     C                     PARM '*PRV '   #MSGR
     C                     PARM '*'       #PRGM
     C                     PARM 'MOPERQ ' #MSGQ
     C                     PARM '*INFO  ' #MSGT
     C*
     C* Reset subroutine flag:
     C*
     C                     MOVE 'N'       @@PSSR
     C*___________________________________________________________________
     C                     OTHER
     C*
     C* Any other mode or type of error.
     C*
     C* Set LDA values and format message for MOPERQ:
     C*
     C                     MOVE ##PGM     ERPGM
     C                     MOVE ##RTVN    ERFILE
     C           ##MSID    CAT  ##SQNO    ERKEY     P
     C                     Z-ADD998       ERERNB
     C                     MOVEL##MSWK    ERNARR
     C*
     C* Move data to the LDA data area:
     C*
     C           *LOCK     IN   LDA
     C                     MOVELW0KEY     DBKEY
     C                     Z-ADDW0ERNB    DBASE
     C                     MOVELW0FILE    DBFILE
     C                     MOVEL##PGM     DBPGM
     C                     OUT  LDA
     C*
     C* Provide a fuller report:
     C*
     C                     MOVEA@STK      #@STK 100
     C*
     C                     CALL 'CGZERROR'             9090
     C                     PARM           ##ERTN  7
     C                     PARM           DSPGM
     C                     PARM           ERDTA
     C                     PARM           #@STK
     C* End the program:
     C                     SETON                     U7U8LR
     C                     MOVE '*ERROR*' W0RTN   7
     C                     RETRN
     C*
     C                     ENDSL
     C*
     C                     ENDSR
     C*****************************************************************
      *****************************************************************
      * End of /COPY SRERRPSSR                                        *
      *****************************************************************
