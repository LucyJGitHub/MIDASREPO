      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Display correspondent - select')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG0021DF - Display Correspondent     Select         *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD053             Date 01Jun06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*       Amend No. S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(34 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1CPNC         8   H      TEXT('Counterparty Nostro Code')
     A            #1BICC         8   H      TEXT('BIC (Bank, Country, & Loc')
     A            #1BICB         3   H      TEXT('BIC (Branch Code)')
     A            #1CTYP        10   H      TEXT('Correspondent type')
     A            #1RTYP         1   H      TEXT('Relationship type')
     A            #1FATT        30   H      TEXT('For Attention of')
      **Start of change CSD053
     A**********  #1ADD1        50   H      TEXT('Address Line 1')                            CSD053
     A            #1ADD1        50O  H      TEXT('Address Line 1')
     A**********  #1ADD2        50   H      TEXT('Address Line 2')                            CSD053
     A            #1ADD2        50O  H      TEXT('Address Line 2')
     A**********  #1ADD3        50   H      TEXT('Address Line 3')                            CSD053
     A            #1ADD3        50O  H      TEXT('Address Line 3')
     A**********  #1ADD4        50   H      TEXT('Address Line 4')                            CSD053
     A            #1ADD4        50O  H      TEXT('Address Line 4')
     A**********  #1ADD5        50   H      TEXT('Address Line 5')                            CSD053
     A            #1ADD5        50O  H      TEXT('Address Line 5')
     A**********  #1ADD6        50   H      TEXT('Address Line 6')                            CSD053
     A            #1ADD6        50O  H      TEXT('Address Line 6')
     A**********  #1ADD7        50   H      TEXT('address line 7')                            CSD053
     A            #1ADD7        50O  H      TEXT('address line 7')
     A**********  #1ADD8        50   H      TEXT('address line 8')                            CSD053
     A            #1ADD8        50O  H      TEXT('address line 8')
      **End of change CSD053
     A            #1ZIP         10   H      TEXT('ZIP code')
     A            #1DPRT         6   H      TEXT('Default Print Schedule')
     A            #1DPDT         5  0H      TEXT('Default Print Date')
     A            #1CDTE         5  0H      TEXT('Creation date')
     A            #1OBRC         3   H      TEXT('Override Branch')
     A            #1DRET         5  0H      TEXT('Default Retention Period')
     A            #1PLAB         1   H      TEXT('Print Address Label')
     A            #1ATDT         5  0H      TEXT('Activation Date         m')
     A            #1DADT         5  0H      TEXT('De-Activation Date      m')
     A            #1CSTR        10   H      TEXT('Structure Type')
     A            #1LCCD         3   H      TEXT('Location Code')
     A            #1FAX         20   H      TEXT('Fax Number')
     A            #1TEL         20   H      TEXT('Telephone Number')
     A            #1CREF        20   H      TEXT('Clearing Reference')
     A            #1DYNB         3  0H      TEXT('Default print day number')
     A            #1BRCA         3   H      TEXT('Branch Code')
     A            #1ERT         10   H      TEXT('Electronic route code')
     A            #1DMSH        10   H      TEXT('Default Mail Shot code')
     A            #1CCCY         3   H      TEXT('Charge Currency')
     A            #1CITM        15  0H      TEXT('Charge per item')
     A            #1BSNB        15  0H      TEXT('Charge Not Posted')
     A            #1CACC        15  0H      TEXT('Charge Accumlated to-date')
     A            #1CFRQ         1   H      TEXT('Post charge frequency')
     A            #1NTDT         5  0H      TEXT('Next Post Date')
     A            #1ACHG         1   H      TEXT('Apply Charge')
      **Start of change CGL029
     A**********  #1CACT        18   H      TEXT('Charge Account')                            CGL029
     A            #1CACT        24   H      TEXT('Charge Account')
      **End of change CGL029
     A            #1PDAY         3  0H      TEXT('Post Charges day number')
     A            #1DESC        30   H      TEXT('Description')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B 11  6
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1CORR        10     11 10TEXT('Correspondent')
     A                                      DSPATR(HI)
      **Start of change CSD053
     A**********  #1CORN        30     11 24TEXT('Correspondent Name')                        CSD053
     A            #1CORN        30O    11 24TEXT('Correspondent Name')
      **End of change CSD053
     A                                      DSPATR(HI)
     A            #1CUST         6     11 57TEXT('Customer Number        SD')
     A                                      DSPATR(HI)
     A            #1LGID         2     11 67TEXT('Language identifier')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CORR')
     A                                      SETOF(32 'Invalid: #2CORN')
     A                                      SETOF(33 'Invalid: #2LGID')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0021F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A            #2RECI         1   H      TEXT('Record Identifier')
     A            #2CUST         6   H      TEXT('Customer Number        SD')
     A            #2CPNC         8   H      TEXT('Counterparty Nostro Code')
     A            #2BICC         8   H      TEXT('BIC (Bank, Country, & Loc')
     A            #2BICB         3   H      TEXT('BIC (Branch Code)')
     A            #2CTYP        10   H      TEXT('Correspondent type')
     A            #2RTYP         1   H      TEXT('Relationship type')
     A            #2FATT        30   H      TEXT('For Attention of')
      **Start of change CSD053
     A**********  #2ADD1        50   H      TEXT('Address Line 1')                            CSD053
     A            #2ADD1        50O  H      TEXT('Address Line 1')
     A**********  #2ADD2        50   H      TEXT('Address Line 2')                            CSD053
     A            #2ADD2        50O  H      TEXT('Address Line 2')
     A**********  #2ADD3        50   H      TEXT('Address Line 3')                            CSD053
     A            #2ADD3        50O  H      TEXT('Address Line 3')
     A**********  #2ADD4        50   H      TEXT('Address Line 4')                            CSD053
     A            #2ADD4        50O  H      TEXT('Address Line 4')
     A**********  #2ADD5        50   H      TEXT('Address Line 5')                            CSD053
     A            #2ADD5        50O  H      TEXT('Address Line 5')
     A**********  #2ADD6        50   H      TEXT('Address Line 6')                            CSD053
     A            #2ADD6        50O  H      TEXT('Address Line 6')
     A**********  #2ADD7        50   H      TEXT('address line 7')                            CSD053
     A            #2ADD7        50O  H      TEXT('address line 7')
     A**********  #2ADD8        50   H      TEXT('address line 8')                            CSD053
     A            #2ADD8        50O  H      TEXT('address line 8')
      **End of change CSD053
     A            #2ZIP         10   H      TEXT('ZIP code')
     A            #2DPRT         6   H      TEXT('Default Print Schedule')
     A            #2DPDT         5  0H      TEXT('Default Print Date')
     A            #2CDTE         5  0H      TEXT('Creation date')
     A            #2OBRC         3   H      TEXT('Override Branch')
     A            #2DRET         5  0H      TEXT('Default Retention Period')
     A            #2PLAB         1   H      TEXT('Print Address Label')
     A            #2ATDT         5  0H      TEXT('Activation Date         m')
     A            #2DADT         5  0H      TEXT('De-Activation Date      m')
     A            #2CSTR        10   H      TEXT('Structure Type')
     A            #2BRCA         3   H      TEXT('Branch Code')
     A            #2ERT         10   H      TEXT('Electronic route code')
     A            #2DMSH        10   H      TEXT('Default Mail Shot code')
     A            #2CCCY         3   H      TEXT('Charge Currency')
     A            #2CITM        15  0H      TEXT('Charge per item')
     A            #2BSNB        15  0H      TEXT('Charge Not Posted')
     A            #2CACC        15  0H      TEXT('Charge Accumlated to-date')
     A            #2CFRQ         1   H      TEXT('Post charge frequency')
     A            #2NTDT         5  0H      TEXT('Next Post Date')
     A            #2ACHG         1   H      TEXT('Apply Charge')
      **Start of change CGL029
     A**********  #2CACT        18   H      TEXT('Charge Account')                            CGL029
     A            #2CACT        24   H      TEXT('Charge Account')
      **End of change CGL029
     A            #2DESC        30   H      TEXT('Description')
     A            #2AJOB        10   H      TEXT('Job name')
     A            #2AUSR        10   H      TEXT('User name')
     A            #2ATIM         6  0H      TEXT('Action Time')
     A            #2ARDT         7   H      TEXT('Action Date')
     A            #2AACT         1   H      TEXT('Action Type')
     A            #2RDNB         5  0H      TEXT('Run day number')
     A                                  4  3'Position on.: Correspondent'
     A            #2CORR        10   B  4 31TEXT('Correspondent')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6  3'Select on...: Name'
      **Start of change CSD053
     A**********  #2CORN        30   B  6 22TEXT('Correspondent Name')                        CSD053
     A            #2CORN        30O  B  6 22TEXT('Correspondent Name')
      **End of change CSD053
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 54'Lang'
     A            #2LGID         2   B  6 59TEXT('Language identifier')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A  81                              8  6'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8  9'Correspondent'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 24'Correspondent Name'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 56' Midas'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 66'Lang'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 56'Customer'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 56' Number'
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
