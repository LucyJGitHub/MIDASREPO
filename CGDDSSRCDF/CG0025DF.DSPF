      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Correspondent types maintenance')
     A*%%TS  SD  19941018  131023  UDC_PGMR    REL-V2R2M0  5738-PW1
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG0025DF - Retail/General ledger UDC Interface      *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. S01522       DATE 23NOV94                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*=========================================================================
     A          R #RCDFM1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A                                      OVERLAY
     A* Command keys............................................................
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(31 'Invalid: #1OPTN')
     A*.........................................................................
     A            ##USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'CG0025F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                 12 10'Do You wish to see User Defined Co-
     A                                      rrespondence'
     A            ##OPTN         1A  B 12 64
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PC)
     A                                 12 57'(Y/N)'
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78A  O 22  2TEXT('Command Line 1')
     A                                      DSPATR(HI)
     A            #KCMD2        78A  O 23  2TEXT('Command Line 2')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
