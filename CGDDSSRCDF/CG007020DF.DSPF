      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Interest Scale Enqry & Reprint Selection')    *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/CG007020DF - Interest Scale Enquiry & Reprint Selection *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. AR797795*CREATE    Date 19Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR797795 - Interest Scale Report Enquiry is missing in       *
      *              BFMidas                                          *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A  25                                  CA05(05 'Reset.')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Subfile Record Format
      *
     A          R CG007020S0                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2011')
     A  52                                  SFLNXTCHG
     A            T7ACT          1A  B  9  2TEXT('Action Code')
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            T7BRCH         3A  O  9  4TEXT('Branch')
     A            T7CNUM         6A  O  9 10TEXT('Customer Number')
     A            T7ACOD        10A  O  9 19TEXT('Account Code')
     A            T7DLTP         2A  O  9 32TEXT('Type')
     A            T7ITEM         8A  O  9 53TEXT('Int Scale Rep Reference')
     A            T7PDAT         7A  O  9 66TEXT('Production Date')
     A            T7IBAN        47   O 10 19TEXT('IBAN')
     A N36                                  DSPATR(ND)
     A            T7DLST         2A  O  9 36TEXT('Sub Type')
     A            T7DLNO         6A  O  9 42TEXT('Deal/Loan No')
     A            T7STAT         1   O  9 76
     A N37                                  DSPATR(ND)
      *
      ** Subfile Control
      *
     A          R CG007020C0                SFLCTL(CG007020S0)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      TEXT('Subfile control.')
     A N51                                  ROLLUP(22 'Next page.')
     A N71                                  CF03(03 'Exit')
     A  71                                  CF12(12 'Previous')
     A                                      OVERLAY
     A N50 53                               SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A N50 51 53                            SFLEND(*MORE)
     A            #0RLRN         4S 0H      SFLRCDNBR
     A            T9BRCA         3A  B  6  4TEXT('Branch')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A            T9CNUM         6A  B  6 10TEXT('Customer Number')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A            T9ACOD        10A  B  6 19TEXT('Account Code')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
     A            T9DLTP         2A  B  6 32TEXT('Deal Type')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A            T9ITEM         8A  B  6 53TEXT('Int Scale Item ID')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
     A            T9PDAT         7A  B  6 66TEXT('Production Date')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A  36        T9IBAN        47A  B  7 19TEXT('IBAN')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      CHECK(LC)
      *
     A            T9DLST         2A  B  6 36TEXT('Sub Type')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
     A            T9DLNO         6A  B  6 42TEXT('Deal/Loan No')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
      ** Header
      *
     A          R CG007020H0
     A                                      TEXT('Command keys')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous.')
     A                                      OVERLAY
     A            #0USER        10   O  1  2
     A            #0RUNA         7   O  1 73
     A            #0WSID        10   O  2  2
     A            #0TIME         6  0O  2 73
     A                                      EDTWRD('  :  :  ')
     A                                  5 10'No'
     A                                  4  2'Branch'
     A                                  4 10'Cust'
     A                                  4 19'Retail A/c'
     A                                  4 32'Type/'
     A                                  5 32'Subtype'
     A  36                              5 19'(IBAN)'
     A                                  4 42'Deal/'
     A                                  5 42'Loan No'
     A                                  4 52'Int. Scale'
     A                                  5 53'Item ID'
     A                                  4 65'Production'
     A                                  5 67'Date'
     A            #0PGMN        10   O  1 13
     A                                      TEXT('Program Name')
     A                                  2 18'Interest Scale Report Enquiry and -
     A                                      Reprint'
     A                                      DSPATR(HI)
      *
     A          R CG007020F0
     A                                      TEXT('Command keys')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous.')
     A                                      OVERLAY
     A                                 23  3'F3=Exit'
     A                                 23 14'F5=Refresh'
     A                                 23 28'E=Enquire Header'
     A                                 23 48'D=View Item Details'
     A                                 23 71'R=Reprint'
      *
      ** No data record
      *
     A          R CG007020D1
     A                                      TEXT('Command keys')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous.')
     A            #0USER        10   O  1  2
     A            #0RUNA         7   O  1 73
     A            #0WSID        10   O  2  2
     A                                  2 18'Interest Scale Report Enquiry and -
     A                                      Reprint'
     A                                      DSPATR(HI)
     A            #0TIME         6  0O  2 73
     A                                      EDTWRD('  :  :  ')
      *
     A N89        #0POS1         3A  B  6  4TEXT('Branch')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A N89        #0POS2         6A  B  6 10TEXT('Customer Number')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A N89        #0POS3        10A  B  6 19TEXT('Account Code')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
     A N89        #0POS4         2A  B  6 32TEXT('Type')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
     A N89        #0POS5         8A  B  6 53TEXT('Int Scale Item ID')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
     A N89        #0POS7         7A  B  6 66TEXT('Production Date')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      CHECK(LC)
     A N89 36     #0POS6        47A  B  7 19TEXT('IBAN')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                 24  1'No Data to Display'
     A                                      DSPATR(HI)
     A                                  4  2'Branch'
     A                                  4 10'Cust'
     A                                  5 10'No'
     A                                  4 19'Retail A/c'
     A  36                              5 19'(IBAN)'
     A                                  4 32'Type/'
     A                                  5 32'Subtype'
     A                                  4 42'Deal/'
     A                                  5 42'Loan No'
     A                                  4 52'Int. Scale'
     A                                  5 53'Item ID'
     A                                  4 65'Production'
     A                                  5 67'Date'
      *
     A N89        #0POS42        2A  B  6 36TEXT('Sub Type')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
      *
     A N89        #0POS8         6A  B  6 42TEXT('Deal/Loan No')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A            #0PGMN        10   O  1 13
     A                                 23  3'F3=Exit'
     A                                 23 14'F5=Refresh'
      *
      ** Message Record Format
      *
     A          R CG007020S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      ** Message Control
      *
     A          R CG007020C1                SFLCTL(CG007020S1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
