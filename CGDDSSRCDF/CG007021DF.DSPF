      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Int Scale Enqry & Reprint - Details Enqry')   *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/CG007021DF - Interest Scale Enquiry & Reprint Details   *
      *                       Enquiry                                 *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR797795 *CREATE   Date 19Jul11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR797795 - Interest Scale Report Enquiry is missing in       *
      *              BFMidas                                          *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A  25                                  CA05(05 'Reset.')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Subfile Record Format
      *
     A          R CG007021S0                SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2011')
     A  52                                  SFLNXTCHG
     A            D1TAGN        35   O  6  2TEXT('Tag Name')
     A  72                                  DSPATR(HI)
     A            D1TAGV        42A  O  6 39TEXT('Tag Value')
      *
      ** Subfile Control
      *
     A          R CG007021C0                SFLCTL(CG007021S0)
     A                                      TEXT('Subfile control.')
     A N51                                  ROLLUP(22 'Next page.')
     A N71                                  CF12(12 'Previous')
     A                                      OVERLAY
     A N50 53                               SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A N50 51 53                            SFLEND(*MORE)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A            #0RLRN         4S 0H      SFLRCDNBR
      *
      ** Header
      *
     A          R CG007021H0
     A                                      TEXT('Command keys')
     A                                      CF12(12 'Previous.')
     A                                      OVERLAY
     A            #0USER        10   O  1  2
     A            #0RUNA         7   O  1 73
     A            #0WSID        10   O  2  2
     A            #0TIME         6  0O  2 73
     A                                      EDTWRD('  :  :  ')
     A            #0PGMN        10   O  1 13
     A                                      TEXT('Program Name')
     A                                  4  2'Item Identifier :'
     A            H1ITEM         8A  O  4 20TEXT('Item Identifier')
     A                                  2 18'Interest Scale Report Enquiry and -
     A                                      Reprint'
     A                                      DSPATR(HI)
      *
     A          R CG007021F0
     A                                      TEXT('Command keys')
     A                                      CF12(12 'Previous.')
     A                                      OVERLAY
     A                                 23  3'F12=Previous'
     A          R CG007021D1
     A                                      TEXT('Command keys')
     A                                      CF12(12 'Previous.')
     A            #0USER        10   O  1  2
     A            #0RUNA         7   O  1 73
     A            #0WSID        10   O  2  2
     A                                  2 18'Interest Scale Report Enquiry and -
     A                                      Reprint'
     A                                      DSPATR(HI)
     A            #0TIME         6  0O  2 73
     A                                      EDTWRD('  :  :  ')
     A            #0PGMN        10   O  1 13
     A                                      TEXT('Program Name')
     A                                  4  2'Item Identifier :'
     A            H1ITEM         8A  O  4 20TEXT('Item Identifier')
     A                                 24  1'No Item Details to Display'
     A                                      DSPATR(HI)
     A                                 23  3'F12=Previous'
      *
      ** Message Record Format
      *
     A          R CG007021S1                SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
      ** Message Control
      *
     A          R CG007021C1                SFLCTL(CG007021S1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
