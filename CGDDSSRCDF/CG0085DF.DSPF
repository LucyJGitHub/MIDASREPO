      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Print Unauthorised Correspondence')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  CG0085DF - Print Unauthorised Correspondence                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CSD012  *CREATE    Date 15Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD012 - Standing Data Authorisation                         *
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
      *
      ** Subfile Record Format
      *
     A          R CG0085S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  94                                  SFLNXTCHG
     A            DDOPTN         1A  B  8  3TEXT('Option')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI UL)
      **Start of change CGL029
     A**********  DDKEY         20A  O  8  7DSPATR(HI)                                        CGL029
     A**********  DDFUNC        21A  O  8 29DSPATR(HI)                                        CGL029
     A**********  DDACTN         1A  O  8 53DSPATR(HI)                                        CGL029
     A**********  DDUSER        10A  O  8 60DSPATR(HI)                                        CGL029
     A            DDKEY         26A  O  8  7DSPATR(HI)
     A            DDFUNC        21A  O  8 35DSPATR(HI)
     A            DDACTN         1A  O  8 59DSPATR(HI)
     A            DDUSER        10A  O  8 65DSPATR(HI)
      **End of change CGL029
      *
      ** Subfile Control
      *
     A          R CG0085C0                  TEXT('Subfile control.')
     A                                      SFLCTL(CG0085S0)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A            @@RRN          4  0H      SFLRCDNBR
     A            DDUSR         10A  O  1  2OVRDTA
     A                                  1 13'CG0085'
     A                                  1 25'Print Unauthorised Correspondence'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            AGMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  7'Key'
      **Start of change CGL029
     A**********                        4 29'Function'                                        CGL029
     A**********                        4 51'Action'                                          CGL029
     A**********                        4 59'Last Action'                                     CGL029
      **********                                                                              CGL029
     A**********                        5 62'User'                                            CGL029
      **********                                                                              CGL029
     A**********  DDKYSL        20   B  6  7TEXT('Key')                                       CGL029
     A                                  4 35'Function'
     A                                  4 57'Action'
     A                                  4 65'Last Action'
     A                                  5 68'User'
      *
     A            DDKYSL        26   B  6  7TEXT('Key')
      **End of change CGL029
     A N25                                  OVRDTA
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      **Start of change CGL029
     A**********  DDFNSL        21   B  6 29TEXT('Function')                                  CGL029
     A            DDFNSL        21   B  6 35TEXT('Function')
      **End of change CGL029
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      ** Command Line
      *
     A          R CG0085F0                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY PUTOVR
     A                                 23  2'P=Print'
     A                                 23 60'F3=Exit  F5=Refresh'
      *
      ** No Record found
      *
     A          R CG0085NR                  TEXT('No Record found')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSR         10A  O  1  2OVRDTA
     A                                  1 13'CG0085'
     A                                  1 24'Print Unauthorised Correspondence'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            AGMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 23'NO UNAUTHORISED TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      *
      ** Message Record Format
      *
     A          R CG0085S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Control
      *
     A          R CG0085C1                  TEXT('Program messages')
     A                                      SFLCTL(CG0085S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
