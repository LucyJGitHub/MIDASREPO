      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Correspondent schedules maintenance')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*  DSPF/CG0280DF - Edit Corr. Schedule Edit record(2 screens)*
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 117207             Date 21May01               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 082884             Date 30Jan95               *
     A*                   S01522       DATE 23NOV94                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    117207 - Retention Period of Max. 99 Days is too short. *
     A*    082884 - Change text to Number of Pages                 *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1PTYP')
     A                                      SETOF(32 'Invalid: #1PSTP')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0280F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1SEQN         3  0H      TEXT('Sequence Number')
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Correspondent . . . :'
     A            #1CORR        10      6 26TEXT('Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Print Item Type . . .'
     A            #1PTYP        10   B  7 26TEXT('Print Item Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 37'Sub-type .'
     A            #1PSTP        10   B  7 50TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A                                      CF12(12 'Previous')
     A                                      ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(28 'ROLLDOWN')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1MACT')
     A                                      SETOF(32 'Invalid: #1ACCD')
     A                                      SETOF(33 'Invalid: #1APDF')
     A                                      SETOF(34 'Invalid: #1PCOR')
     A                                      SETOF(35 'Invalid: #1LGID')
     A                                      SETOF(36 'Invalid: #1MSHC')
     A                                      SETOF(37 'Invalid: #1PSCH')
     A                                      SETOF(38 'Invalid: #1SFDY')
     A                                      SETOF(39 'Invalid: #1NPDT')
     A                                      SETOF(40 'Invalid: #1RETP')
     A                                      SETOF(41 'Invalid: #1PLAB')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0280F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1SEQN         3  0H      TEXT('Sequence Number')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1OSQN         5  0H      TEXT('Output Sequence Number')
     A            #1NPAG         5  0H      TEXT('Number of Pages')
     A            #1TPAG         5  0H      TEXT('Total Number of Pages')
     A            #1RDTE         5  0H      TEXT('Reset Date')
     A            #1RFRQ         1   H      TEXT('Reset Frequency')
     A            #1RSDN         3  0H      TEXT('Reset Day Number')
     A            #1CCCY         3   H      TEXT('Charge Currency')
     A            #1CITM        15  0H      TEXT('Charge per item')
     A            #1BSNB        15  0H      TEXT('Charge Not Posted')
     A            #1CACC        15  0H      TEXT('Charge Accumlated to-date')
     A            #1CFRQ         1   H      TEXT('Post charge frequency')
     A            #1NTDT         5  0H      TEXT('Next Post Date')
     A            #1ACHG         1   H      TEXT('Apply Charge')
      **Start of change CGL029
     A**********  #1CACT        18   H      TEXT('Charge Account')                            CGL029
     A            #1CACT        24   H      TEXT('Charge Account')
      **End of change CGL029
     A            #1PDAY         3  0H      TEXT('Post Charges day number')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Correspondent . . . :'
     A            #1CORR        10      6 26TEXT('Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Print Item Type . . :'
     A            #1PTYP        10      7 26TEXT('Print Item Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7 37'Sub-type :'
     A            #1PSTP        10      7 50TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Midas Information'
     A                                      DSPATR(UL HI)
     A       N79                        9 24'Midas Account Identifier  .'
     A        79                        9 24'Midas Account Identifier  :'
      **Start of change CGL029
     A**********  #1MACT        18   B  9 54TEXT('Midas Account Identifier')                  CGL029
     A            #1MACT        24   B  9 54TEXT('Midas Account Identifier')
      **End of change CGL029
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       10 24'Account Code  . . . . . . .'
     A        79                       10 24'Account Code  . . . . . . :'
      **Start of change CGL029
     A**********  #1ACCD         4   B 10 54TEXT('Account Code')                              CGL029
     A            #1ACCD        10   B 10 54TEXT('Account Code')
      **End of change CGL029
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       11 24'Defaults must be applied  .'
     A        79                       11 24'Defaults must be applied  :'
     A            #1APDF         1   B 11 54TEXT('Defaults must be applied')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'Schedule Characteristics'
     A                                      DSPATR(UL HI)
     A       N79                       13 29'Primary Correspondent . . .'
     A        79                       13 29'Primary Correspondent . . :'
     A            #1PCOR        10   B 13 59TEXT('Primary Correspondent')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       14 29'Language identifier . . . .'
     A        79                       14 29'Language identifier . . . :'
     A            #1LGID         2   B 14 59TEXT('Language identifier')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       15 29'Mail Shot code  . . . . . .'
     A        79                       15 29'Mail Shot code  . . . . . :'
     A            #1MSHC        10   B 15 59TEXT('Mail Shot code')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 17  2'Print Information'
     A                                      DSPATR(UL HI)
     A       N79                       17 29'Print Schedule  . . . . . .'
     A        79                       17 29'Print Schedule  . . . . . :'
     A            #1PSCH         6   B 17 59TEXT('Print Schedule')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N79
     AO 37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       18 29'Print Day Number  . . . . .'
     A        79                       18 29'Print Day Number  . . . . :'
     A            #1SFDY         3  0H      TEXT('Frequency Day Number')
     A            #SSFDY         2   B 18 59TEXT('Frequency Day Number')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N79
     AO 38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       19 29'Next Print Date . . . . . .'
     A        79                       19 29'Next Print Date . . . . . :'
     A            #1NPDT         5  0H      TEXT('Next Print Date')
     A            #SNPDT         6   B 19 59TEXT('Next Print Date')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N79
     AO 39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       20 29'Retention Period  . . . . .'
     A        79                       20 29'Retention Period  . . . . :'
     A            #1RETP         5  0H      TEXT('Retention Period')
     A*/COPY WNCPYSRC,CG0280DF01
     A* Start of Change 117207
     A******      #SRETP         2   B 20 59TEXT('Retention Period')                          117207
     A            #SRETP         4   B 20 59TEXT('Retention Period')
     A* End of Change 117207
     A*/COPY WNCPYSRC,CG0280DF02
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N79
     AO 40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       21 29'Print Address Label . . . .'
     A        79                       21 29'Print Address Label . . . :'
     A            #1PLAB         1   B 21 59TEXT('Print Address Label')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N79
     AO 41                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #ACMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #ACMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL2                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A                                      CF12(12 'Previous')
     A                                      ROLLDOWN(28 'Previous page.')
     A* SETOFFS.................................................................
     A                                      SETOF(27 'ROLLUP')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1OSQN')
     A                                      SETOF(32 'Invalid: #1NPAG')
     A                                      SETOF(33 'Invalid: #1RDTE')
     A                                      SETOF(34 'Invalid: #1RFRQ')
     A                                      SETOF(35 'Invalid: #1RSDN')
     A                                      SETOF(36 'Invalid: #1CCCY')
     A                                      SETOF(37 'Invalid: #1CITM')
     A                                      SETOF(38 'Invalid: #1BSNB')
     A                                      SETOF(39 'Invalid: #1CACC')
     A                                      SETOF(40 'Invalid: #1CFRQ')
     A                                      SETOF(41 'Invalid: #1PDAY')
     A                                      SETOF(42 'Invalid: #1NTDT')
     A                                      SETOF(43 'Invalid: #1ACHG')
     A                                      SETOF(44 'Invalid: #1CACT')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0280F3'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1SEQN         3  0H      TEXT('Sequence Number')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1PSCH         6   H      TEXT('Print Schedule')
     A            #1PCOR        10   H      TEXT('Primary Correspondent')
     A            #1LGID         2   H      TEXT('Language identifier')
      **Start of change CGL029
     A**********  #1ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #1ACCD        10   H      TEXT('Account Code')
      **End of change CGL029
     A            #1NPDT         5  0H      TEXT('Next Print Date')
      **Start of change CGL029
     A**********  #1MACT        18   H      TEXT('Midas Account Identifier')                  CGL029
     A            #1MACT        24   H      TEXT('Midas Account Identifier')
      **End of change CGL029
     A            #1PLAB         1   H      TEXT('Print Address Label')
     A            #1SFDY         3  0H      TEXT('Frequency Day Number')
     A            #1RETP         5  0H      TEXT('Retention Period')
     A            #1MSHC        10   H      TEXT('Mail Shot code')
     A            #1APDF         1   H      TEXT('Defaults must be applied')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Correspondent . . . :'
     A            #1CORR        10      6 26TEXT('Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Print Item Type . . :'
     A            #1PTYP        10      7 26TEXT('Print Item Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7 37'Sub-type :'
     A            #1PSTP        10      7 50TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Output Information'
     A                                      DSPATR(UL HI)
     A       N79                        9 25'Output Sequence Number  . .'
     A        79                        9 25'Output Sequence Number  . :'
     A            #1OSQN         5  0H      TEXT('Output Sequence Number')
     A            #SOSQN         5   B  9 55TEXT('Output Sequence Number')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A* Start 082884
     A*******N79                       10 25'Total Number of Pages . . .'
     A******* 79                       10 25'Total Number of Pages . . :'
     A       N79                       10 25'Number of Pages . . . . . .'
     A        79                       10 25'Number of Pages . . . . . :'
     A* End 082884
     A            #1NPAG         5  0H      TEXT('Total Number of Pages')
     A            #SNPAG         5   B 10 55TEXT('Total Number of Pages')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10 61'Total . . :'
     A            #1TPAG         5  0  10 75TEXT('Total Number of Pages')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                       11 25'Reset Date  . . . . . . . .'
     A        79                       11 25'Reset Date  . . . . . . . :'
     A            #1RDTE         5  0H      TEXT('Reset Date')
     A            #SRDTE         6   B 11 55TEXT('Reset Date')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       11 63'Frequency .'
     A        79                       11 63'Frequency :'
     A            #1RFRQ         1   B 11 77TEXT('Reset Frequency')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       12 25'Reset Day Number  . . . . .'
     A        79                       12 25'Reset Day Number  . . . . :'
     A            #1RSDN         3  0H      TEXT('Reset Day Number')
     A            #SRSDN         2   B 12 55TEXT('Reset Day Number')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'Charge Information'
     A                                      DSPATR(UL HI)
     A       N79                       13 25'Charge Currency . . . . . .'
     A        79                       13 25'Charge Currency . . . . . :'
     A            #1CCCY         3   B 13 55TEXT('Charge Currency')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N26                                  OVRATR
     A       N79                       14 25'Charge per item . . . . . .'
     A        79                       14 25'Charge per item . . . . . :'
     A            #1CITM        15  0H      TEXT('Charge per item')
     A            #SCITM        14   B 14 55TEXT('Charge per item')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N79
     AO 37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       15 25'Charge Not Posted . . . . .'
     A        79                       15 25'Charge Not Posted . . . . :'
     A            #1BSNB        15  0H      TEXT('Charge Not Posted')
     A            #SBSNB        14   B 15 55TEXT('Charge Not Posted')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N79
     AO 38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       16 25'Charge Accumlated to-date .'
     A        79                       16 25'Charge Accumlated to-date :'
     A            #1CACC        15  0H      TEXT('Charge Accumlated to-date')
     A            #SCACC        16     16 55TEXT('Charge Accumlated to-date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                       17 25'Post charge frequency . . .'
     A        79                       17 25'Post charge frequency . . :'
     A            #1CFRQ         1   B 17 55TEXT('Post charge frequency')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N79
     AO 40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       17 57'Day number .'
     A        79                       17 57'Day number :'
     A            #1PDAY         3  0H      TEXT('Post Charges day number')
     A            #SPDAY         2   B 17 72TEXT('Post Charges day number')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N79
     AO 41                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       18 25'Next Post Date  . . . . . .'
     A        79                       18 25'Next Post Date  . . . . . :'
     A            #1NTDT         5  0H      TEXT('Next Post Date')
     A            #SNTDT         6   B 18 55TEXT('Next Post Date')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A N79
     AO 42                                  DSPATR(UL)
     A N42                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       18 62'Apply Charge .'
     A        79                       18 62'Apply Charge :'
     A            #1ACHG         1   B 18 79TEXT('Apply Charge')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A N79
     AO 43                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       19 25'Charge Account  . . . . . .'
     A        79                       19 25'Charge Account  . . . . . :'
      **Start of change CGL029
     A**********  #1CACT        18   B 19 55TEXT('Charge Account')                            CGL029
     A            #1CACT        24   B 19 55TEXT('Charge Account')
      **End of change CGL029
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N79
     AO 44                                  DSPATR(UL)
     A N44                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 21  2'User name.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     21 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 21 25'Action Time.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  21 39TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 21 48'Action Date.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     21 62TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT3                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #BCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #BCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
