      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Display correspondent schedules')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG0290DF - Display Corr. Schedules   Display file   *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(33 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1CORR        10   H      TEXT('Correspondent')
     A            #1SEQN         3  0H      TEXT('Sequence Number')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1PSCH         6   H      TEXT('Print Schedule')
      **Start of change CGL029
     A**********  #1ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #1ACCD        10   H      TEXT('Account Code')
      **End of change CGL029
     A            #1NPDT         5  0H      TEXT('Next Print Date')
     A            #1SFDY         3  0H      TEXT('Frequency Day Number')
     A            #1RETP         5  0H      TEXT('Retention Period')
     A            #1MSHC        10   H      TEXT('Mail Shot code')
     A            #1APDF         1   H      TEXT('Defaults must be applied')
     A            #1OSQN         5  0H      TEXT('Output Sequence Number')
     A            #1NPAG         5  0H      TEXT('Number of Pages')
     A            #1TPAG         5  0H      TEXT('Total Number of Pages')
     A            #1RDTE         5  0H      TEXT('Reset Date')
     A            #1RFRQ         1   H      TEXT('Reset Frequency')
     A            #1CCCY         3   H      TEXT('Charge Currency')
     A            #1CITM        15  0H      TEXT('Charge per item')
     A            #1BSNB        15  0H      TEXT('Charge Not Posted')
     A            #1CACC        15  0H      TEXT('Charge Accumlated to-date')
     A            #1CFRQ         1   H      TEXT('Post charge frequency')
     A            #1NTDT         5  0H      TEXT('Next Post Date')
     A            #1ACHG         1   H      TEXT('Apply Charge')
      **Start of change CGL029
     A**********  #1CACT        18   H      TEXT('Charge Account')                            CGL029
     A            #1CACT        24   H      TEXT('Charge Account')
      **End of change CGL029
     A            #1PDAY         3  0H      TEXT('Post Charges day number')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B 11  3
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1PTYP        10     11  6TEXT('Print Item Type')
     A                                      DSPATR(HI)
     A            #1PSTP        10     11 18TEXT('Print Item Sub-type')
     A                                      DSPATR(HI)
      **Start of change CGL029
     A**********  #1PCOR        10     11 31TEXT('Primary Correspondent')                     CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********  #1LGID         2     11 46TEXT('Language identifier')                       CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********  #1MACT        18     11 52TEXT('Midas Account Identifier')                  CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********  #1PLAB         1     11 74TEXT('Print Address Label')                       CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A            #1PCOR        10     11 30TEXT('Primary Correspondent')
     A                                      DSPATR(HI)
     A            #1LGID         2     11 43TEXT('Language identifier')
     A                                      DSPATR(HI)
     A            #1MACT        24     11 48TEXT('Midas Account Identifier')
     A                                      DSPATR(HI)
     A            #1PLAB         1     11 76TEXT('Print Address Label')
     A                                      DSPATR(HI)
      **End of change CGL029
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2PTYP')
     A                                      SETOF(32 'Invalid: #2PSTP')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0290F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A            #2SEQN         3  0H      TEXT('Sequence Number')
     A            #2RECI         1   H      TEXT('Record Identifier')
     A            #2PSCH         6   H      TEXT('Print Schedule')
     A            #2PCOR        10   H      TEXT('Primary Correspondent')
     A            #2LGID         2   H      TEXT('Language identifier')
      **Start of change CGL029
     A**********  #2ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #2ACCD        10   H      TEXT('Account Code')
      **End of change CGL029
     A            #2NPDT         5  0H      TEXT('Next Print Date')
      **Start of change CGL029
     A**********  #2MACT        18   H      TEXT('Midas Account Identifier')                  CGL029
     A            #2MACT        24   H      TEXT('Midas Account Identifier')
      **End of change CGL029
     A            #2PLAB         1   H      TEXT('Print Address Label')
     A            #2SFDY         3  0H      TEXT('Frequency Day Number')
     A            #2RETP         5  0H      TEXT('Retention Period')
     A            #2MSHC        10   H      TEXT('Mail Shot code')
     A            #2OSQN         5  0H      TEXT('Output Sequence Number')
     A            #2NPAG         5  0H      TEXT('Number of Pages')
     A            #2TPAG         5  0H      TEXT('Total Number of Pages')
     A            #2RDTE         5  0H      TEXT('Reset Date')
     A            #2RFRQ         1   H      TEXT('Reset Frequency')
     A            #2CCCY         3   H      TEXT('Charge Currency')
     A            #2CITM        15  0H      TEXT('Charge per item')
     A            #2BSNB        15  0H      TEXT('Charge Not Posted')
     A            #2CACC        15  0H      TEXT('Charge Accumlated to-date')
     A            #2CFRQ         1   H      TEXT('Post charge frequency')
     A            #2NTDT         5  0H      TEXT('Next Post Date')
     A            #2ACHG         1   H      TEXT('Apply Charge')
      **Start of change CGL029
     A**********  #2CACT        18   H      TEXT('Charge Account')                            CGL029
     A            #2CACT        24   H      TEXT('Charge Account')
      **End of change CGL029
     A            #2PDAY         3  0H      TEXT('Post Charges day number')
     A            #2AJOB        10   H      TEXT('Job name')
     A            #2AUSR        10   H      TEXT('User name')
     A            #2ATIM         6  0H      TEXT('Action Time')
     A            #2ARDT         7   H      TEXT('Action Date')
     A            #2AACT         1   H      TEXT('Action Type')
     A            #2RDNB         5  0H      TEXT('Run day number')
     A                                  4  3'Positioned on.: Correspondent'
     A            #2CORR        10      4 33TEXT('Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  3'Position on...: Print Item Type'
     A            #2PTYP        10   B  6 35TEXT('Print Item Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A            #2PSTP        10   B  6 47TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A  81                              8  3'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8  6' Print'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 18'Print Item'
     A                                      DSPATR(HI) COLOR(WHT)
      **Start of change CGL029
     A**81******                        8 30'   Primary'                                      CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A**81******                        8 45'Lang.'                                           CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A**81******                        8 52'Midas Account'                                   CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A**81******                        8 72'Print'                                           CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A  81                              8 32'Primary'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 42'Lang'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 48'Midas Account'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 74'Print'
     A                                      DSPATR(HI) COLOR(WHT)
      **End of change CGL029
     A  81                              9  6'Item Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 18' Sub-type'
     A                                      DSPATR(HI) COLOR(WHT)
      **Start of change CGL029
     A**81******                        9 30'Correspondent'                                   CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A**81******                        9 45' id.'                                            CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A**81******                        9 52'Identifier'                                      CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A**81******                        9 72'Addr.'                                           CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A**81******                       10 72'Label'                                           CGL029
     A**********                            DSPATR(HI) COLOR(WHT)                             CGL029
     A  81                              9 32'Corresp'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 43'ID'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 48'Identifier'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 74'Addr.'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 74'Label'
     A                                      DSPATR(HI) COLOR(WHT)
      **End of change CGL029
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
