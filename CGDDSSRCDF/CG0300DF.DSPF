      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Printer profile maintenance')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG0300DF - Edit Printer Profile      Edit rc(1 scrn)*
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. 190022             Date 15Jan99               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  190022 - Increased the size of OVRPRTF string to accomodate  *
      *           more options for the OVRPRTF command.               *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1PRTF')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0300F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . :'
     A            #PACTC         1      4 22TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Printer Profile .'
     A            #1PRTF        10   B  6 22TEXT('Printer Profile')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A                                      CF12(12 'Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1DESC')
     A                                      SETOF(32 'Invalid: #1PRTN')
     A                                      SETOF(33 'Invalid: #1SPLF')
     A                                      SETOF(34 'Invalid: #1APF')
     A                                      SETOF(35 'Invalid: #SOVR')
     A                                      SETOF(36 'Invalid: #SAPF')
     A                                      SETOF(37 'Invalid: #1ORCF')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0300F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1APFS       256   H      TEXT('APF/AFP Command String')
     A            #1OVRP       256   H      TEXT('OVRPRTF String')
      *                                              Begin /190022/
     A            #1OVR2       256   H      TEXT('OVRPRTF String 2')
     A            #1OVR3       256   H      TEXT('OVRPRTF String 3')
     A            #1OVR4       256   H      TEXT('OVRPRTF String 4')
      *                                              End   /190022/
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . :'
     A            #PACTC         1      4 22TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Printer Profile :'
     A            #1PRTF        10      6 22TEXT('Printer Profile')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                        9  2'Description  . . . . . .'
     A        79                        9  2'Description  . . . . . :'
     A            #1DESC        30   B  9 29TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       11  2'Printer File Name  . . .'
     A        79                       11  2'Printer File Name  . . :'
     A            #1PRTN        10   B 11 29TEXT('Printer File Name')
     A                                      CHECK(VNE)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       11 40'Spool File Name  . . . .'
     A        79                       11 40'Spool File Name  . . . :'
     A            #1SPLF        10   B 11 67TEXT('Spool File Name')
     A                                      CHECK(VNE)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       13  2'Edit Override String . .'
     A        79                       13  2'Override String Setup. :'
     A        79  #OOVR          1     13 29TEXT('Override Setup')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79  #SOVR          1   B 13 29TEXT('Edit Override')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OOST        40     13 31TEXT('Override String')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                       15  2'Use APF/AFP Set up . . .'
     A        79                       15  2'Use APF/AFP Set up . . :'
     A            #1APF          1   B 15 29TEXT('Use APF/AFP Set up')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       16  2'Edit APF String  . . . .'
     A        79                       16  2'APF String Setup . . . :'
     A        79  #OAPF          1     16 29TEXT('APF Setup')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79  #SAPF          1   B 16 29TEXT('Edit APF')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1OAPS        40     16 31TEXT('APF String')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                       17  2'Output Instructions  . .'
     A        79                       17  2'Output Instructions  . :'
     A            #1ORCF        10   B 17 29TEXT('RCF Output Instructions')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N79
     AO 37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
