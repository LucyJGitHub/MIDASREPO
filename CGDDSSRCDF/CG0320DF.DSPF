      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Layout association maintenance')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG0320DF - Edit Layout Association   Edit rc(1 scrn)*
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1PTYP')
     A                                      SETOF(32 'Invalid: #1PSTP')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0320F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1ASSC         8  0H      TEXT('Association Identifier')
     A                                  4  2'Action Code  . . . . . :'
     A            #PACTC         1      4 29TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Print Item Type  . . . .'
     A            #1PTYP        10   B  6 29TEXT('Print Item Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 40'Sub-type  . .'
     A            #1PSTP        10   B  6 56TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  77                                  CF10(10 'Delete.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1LGID')
     A                                      SETOF(32 'Invalid: #1BRCA')
     A                                      SETOF(33 'Invalid: #1CTYP')
     A                                      SETOF(34 'Invalid: #1CORR')
     A                                      SETOF(35 'Invalid: #1MACT')
     A                                      SETOF(36 'Invalid: #1ACCD')
     A                                      SETOF(37 'Invalid: #1LAYO')
     A                                      SETOF(38 'Invalid: #1OPRT')
     A                                      SETOF(39 'Invalid: #1DESC')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0320F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1ASSC         8  0H      TEXT('Association Identifier')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code  . . . . . :'
     A            #PACTC         1      4 29TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Print Item Type  . . . :'
     A            #1PTYP        10      6 29TEXT('Print Item Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6 40'Item Sub-type  . :'
     A            #1PSTP        10      6 61TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                        8  2'Language identifier  . . .'
     A        79                        8  2'Language identifier  . . :'
     A            #1LGID         2   B  8 31TEXT('Language identifier')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                        8 34'Branch Code  .'
     A        79                        8 34'Branch Code  :'
     A            #1BRCA         3   B  8 51TEXT('Branch Code')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       10  2'Correspondent type . . . .'
     A        79                       10  2'Correspondent type . . . :'
     A            #1CTYP        10   B 10 31TEXT('Correspondent type')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       10 42'Correspondent  .'
     A        79                       10 42'Correspondent  :'
     A            #1CORR        10   B 10 61TEXT('Correspondent')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       12  2'Midas Account Identifier .'
     A        79                       12  2'Midas Account Identifier :'
      **Start of change CGL029
     A**********  #1MACT        18   B 12 31TEXT('Midas Account Identifier')                  CGL029
     A            #1MACT        24   B 12 31TEXT('Midas Account Identifier')
      **End of change CGL029
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
      **Start of change CGL029
     A*******N79                       12 50'Account Code  .'                                 CGL029
     A********79                       12 50'Account Code  :'                                 CGL029
     A**********  #1ACCD         4   B 12 68TEXT('Account Code')                              CGL029
     A N79                             13  2'Account Code . . . . . . .'
     A  79                             13  2'Account Code . . . . . . :'
     A            #1ACCD        10   B 13 31TEXT('Account Code')
      **End of change CGL029
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       14  2'Layout Identifier  . . . .'
     A        79                       14  2'Layout Identifier  . . . :'
     A            #1LAYO        10   B 14 31TEXT('Layout Identifier')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N79
     AO 37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       15  2'Overriding Prt Profile . .'
     A        79                       15  2'Overriding Prt Profile . :'
     A            #1OPRT        10   B 15 31TEXT('Overriding Prt Profile')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N79
     AO 38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       17  2'Description  . . . . . . .'
     A        79                       17  2'Description  . . . . . . :'
     A            #1DESC        30   B 17 31TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N79
     AO 39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'User name.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
