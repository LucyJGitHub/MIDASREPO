      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Display layout association')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG0330DF - Display Layout Association Display file  *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 084126             Date 02Mar95               *
     A*                 S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    084126 - Change literals to variables in the subfile    *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(42 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1ASSC         8  0H      TEXT('Association Identifier')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B 13  6
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1PTYP        10     13  9TEXT('Print Item Type')
     A                                      DSPATR(HI)
     A            #1PSTP        10     13 21TEXT('Print Item Sub-type')
     A                                      DSPATR(HI)
     A            #1LGID         2     13 34TEXT('Language identifier')
     A                                      DSPATR(HI)
     A            #1BRCA         3     13 40TEXT('Branch Code')
     A                                      DSPATR(HI)
     A            #1CTYP        10     13 48TEXT('Correspondent type')
     A                                      DSPATR(HI)
     A            #1CORR        10     13 63TEXT('Correspondent')
     A                                      DSPATR(HI)
     A*_________________________________________________________________________
     A* 084126                                                            084126
     A************                     14  9'Account Id.'                 084126
     A            #1@TG1        11     14  9
     A* 084126                                                            084126
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      **Start of change CGL029
     A**********  #1MACT        18     14 21TEXT('Midas Account Identifier')                  CGL029
     A            #1MACT        24     14 21TEXT('Midas Account Identifier')
      **End of change CGL029
     A                                      DSPATR(HI)
     A*_________________________________________________________________________
     A* 084126                                                            084126
     A************                     14 41'Account Code'                084126
      **Start of change CGL029
     A**********  #1@TG2        13     14 41                                                  CGL029
     A            #1@TG2        13     14 47
      **End of change CGL029
     A* 084126                                                            084126
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      **Start of change CGL029
     A**********  #1ACCD         4     14 55TEXT('Account Code')                              CGL029
     A            #1ACCD        10     14 61TEXT('Account Code')
      **End of change CGL029
     A                                      DSPATR(HI)
     A*_________________________________________________________________________
     A* 084126                                                            084126
     A************                     15  9'Layout Identifier'           084126
     A            #1@TG3        17     15  9
     A* 084126                                                            084126
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     A            #1LAYO        10     15 27TEXT('Layout Identifier')
     A                                      DSPATR(HI)
     A*_________________________________________________________________________
     A* 084126                                                            084126
     A************                     15 39'Overriding Prt Profile'      084126
     A            #1@TG4        22     15 39
     A* 084126                                                            084126
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     A            #1OPRT        10     15 62TEXT('Overriding Prt Profile')
     A                                      DSPATR(HI)
     A*_________________________________________________________________________
     A* 084126                                                            084126
     A************                     16  9'Description'                 084126
     A            #1@TG5        11     16  9
     A* 084126                                                            084126
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     A            #1DESC        30     16 21TEXT('Description')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(02) SFLSIZ(03)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2PTYP')
     A                                      SETOF(32 'Invalid: #2PSTP')
     A                                      SETOF(33 'Invalid: #2LGID')
     A                                      SETOF(34 'Invalid: #2BRCA')
     A                                      SETOF(35 'Invalid: #2CORR')
     A                                      SETOF(36 'Invalid: #2CTYP')
     A                                      SETOF(37 'Invalid: #2MACT')
     A                                      SETOF(38 'Invalid: #2ACCD')
     A                                      SETOF(39 'Invalid: #2LAYO')
     A                                      SETOF(40 'Invalid: #2OPRT')
     A                                      SETOF(41 'Invalid: #2DESC')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0330F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A            #2ASSC         8  0H      TEXT('Association Identifier')
     A            #2RECI         1   H      TEXT('Record Identifier')
     A            #2AJOB        10   H      TEXT('Job name')
     A            #2AUSR        10   H      TEXT('User name')
     A            #2ATIM         6  0H      TEXT('Action Time')
     A            #2ARDT         7   H      TEXT('Action Date')
     A            #2AACT         1   H      TEXT('Action Type')
     A            #2RDNB         5  0H      TEXT('Run day number')
     A                                  4  3'Position on.: Print Item Type'
     A            #2PTYP        10   B  4 33TEXT('Print Item Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  4 45'Sub-type'
     A            #2PSTP        10   B  4 54TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  5  3'Select on...: Language'
     A            #2LGID         2   B  5 26TEXT('Language identifier')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  5 30'Branch'
     A            #2BRCA         3   B  5 37TEXT('Branch Code')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 17'Correspondent'
     A            #2CORR        10   B  6 31TEXT('Correspondent')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 43'Type'
     A            #2CTYP        10   B  6 48TEXT('Correspondent type')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 17'Account Id.'
      **Start of change CGL029
     A**********  #2MACT        18   B  7 29TEXT('Midas Account Identifier')                  CGL029
     A            #2MACT        24   B  7 29TEXT('Midas Account Identifier')
      **End of change CGL029
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      **Start of change CGL029
     A**********                        7 49'Account Code'                                    CGL029
     A**********  #2ACCD         4   B  7 62TEXT('Account Code')                              CGL029
     A                                  7 55'Account Code'
     A            #2ACCD        10   B  7 68TEXT('Account Code')
      **End of change CGL029
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 17'Layout Identifier'
     A            #2LAYO        10   B  8 35TEXT('Layout Identifier')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 47'Overriding Prt Profile'
     A            #2OPRT        10   B  8 70TEXT('Overriding Prt Profile')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  9 17'Description'
     A            #2DESC        30   B  9 29TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A  81                             11  6'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11  9'Print'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 21'Print Item'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 33'Lang'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 39'Branch'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 47'Correspondent'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 62'Correspondent'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             12  9'Item Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             12 21'Sub-type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             12 33' Id'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             12 47'    Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
