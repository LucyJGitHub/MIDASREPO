      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG User narrative maintenance')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG0340DF - Edit Data El Form by Lg   Edit rc(1 scrn)*
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. 080867       DATE 29DEC94                *
     A*    PREV AMEND NO. S01522       DATE 23NOV94                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*    080867 - Use Narrative fields for free text             *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1FOPT')
     A                                      SETOF(32 'Invalid: #1FRMT')
     A                                      SETOF(33 'Invalid: #1LGID')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0340F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Format  . . . . . . .'
     A            #1FOPT        10   B  6 26TEXT('Formatting Options')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7  2'Option  . . . . . . .'
     A            #1FRMT        15   B  7 26TEXT('Format Type')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8  2'Language identifier .'
     A            #1LGID         2   B  8 26TEXT('Language identifier')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A                                      CF12(12 'Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
      * 080867 Start
     A***********                           SETOF(31 'Invalid: #1DESC')
     A                                      SETOF(31 'Invalid: #1NAR1')
     A                                      SETOF(32 'Invalid: #1NAR2')
     A                                      SETOF(33 'Invalid: #1NAR3')
     A                                      SETOF(34 'Invalid: #1NAR4')
     A                                      SETOF(35 'Invalid: #1DESC')
      * 080867 End
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG0340F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1FPGM        10   H      TEXT('Formatting Program')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Formatting Options  :'
     A            #1FOPT        10      6 26TEXT('Formatting Options')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Format Type . . . . :'
     A            #1FRMT        15      7 26TEXT('Format Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Language identifier :'
     A            #1LGID         2      8 26TEXT('Language identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      * 080867 Start
      ************
     A*******N79**                     10  2'Narrative . . . . . .'
     A********79**                     10  2'Narrative . . . . . :'
     A************#1DESC        30   B 10 26TEXT('Description')
     A************                          CHECK(LC)
     A*N25********                          OVRDTA
     A**79********                          DSPATR(PR)
     A**31********                          DSPATR(RI PC)
     A*N79********
     A*O*31********                          DSPATR(UL)
     A*N31********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     16  2'Last Change Information'
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(UL HI)
     A*N25********                          OVRATR
     A************                     17  2'User name.:'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1AUSR        10     17 14TEXT('User name')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     17 25'Action Date.:'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1ARDT         7     17 39TEXT('Action Date')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     17 47'Action Time.:'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1ATIM         6  0  17 61TEXT('Action Time')
     A************                          EDTWRD('  :  :  ')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A       N79                       10  2'Narrative .'
     A        79                       10  2'Narrative :'
     A            #1NAR1        64   B 10 16TEXT('Narrative 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1NAR2        64   B 11 16TEXT('Narrative 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1NAR3        64   B 12 16TEXT('Narrative 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1NAR4        64   B 13 16TEXT('Narrative 4')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       15  2'Description . . . . .'
     A        79                       15  2'Description . . . . :'
     A            #1DESC        30   B 15 26TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19  2'User name.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     19 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19 25'Action Date.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     19 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19 47'Action Time.:'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  19 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      * 080867 End
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
