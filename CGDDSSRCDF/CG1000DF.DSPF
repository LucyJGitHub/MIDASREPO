      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG RDE maintenance')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG1000DF   Edit RDE         Edit record(1 screen)   *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1RDE')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1000F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Report Data Element .'
     A            #1RDE         10   B  6 26TEXT('Report Data Element')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1ELYP')
     A                                      SETOF(32 'Invalid: #1MAXL')
     A                                      SETOF(33 'Invalid: #1DESC')
     A                                      SETOF(34 'Invalid: #1FOPT')
     A                                      SETOF(35 'Invalid: #1ETXT')
     A                                      SETOF(36 'Invalid: #1DRVD')
     A                                      SETOF(37 'Invalid: #1DSRC')
     A                                      SETOF(38 'Invalid: #1SPOS')
     A                                      SETOF(39 'Invalid: #1DINT')
     A                                      SETOF(40 'Invalid: #1EPOS')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1000F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Report Data Element :'
     A            #1RDE         10      6 26TEXT('Report Data Element')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Basic Attributes'
     A                                      DSPATR(UL HI)
     A       N79                        8 21'Element Type  . .'
     A        79                        8 21'Element Type  . :'
     A            #1ELYP         6   B  8 41TEXT('Element Type')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                        9 21'Maximum Length  .'
     A        79                        9 21'Maximum Length  :'
     A            #1MAXL         5  0B  9 41TEXT('Maximum Length')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       10 21'Description . . .'
     A        79                       10 21'Description . . :'
     A            #1DESC        30   B 10 41TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 12  2'Formatting'
     A                                      DSPATR(UL HI)
     A       N79                       12 21'Options . . . . .'
     A        79                       12 21'Options . . . . :'
     A            #1FOPT        10   B 12 41TEXT('Formatting Options')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       13 21'Example Data  . .'
     A        79                       13 21'Example Data  . :'
     A            #1ETXT        25   B 13 41TEXT('Example Data')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15  2'Derived Info.'
     A                                      DSPATR(UL HI)
     A       N79                       15 21'Allowed ? .'
     A        79                       15 21'Allowed ? :'
     A            #1DRVD         1   B 15 35TEXT('Allow Derived Information')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 15 51'Mail Merge Data'
     A                                      DSPATR(UL HI)
     A       N79                       16 21'Source  . .'
     A        79                       16 21'Source  . :'
     A            #1DSRC        10   B 16 35TEXT('Derived Information Src')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N79
     AO 37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       16 51'Start Position  .'
     A        79                       16 51'Start Position  :'
     A            #1SPOS         5  0B 16 71TEXT('Start Position')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N79
     AO 38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 17 21'Internal ref'
     A            #1DINT        10   B 17 35TEXT('Derived Data internal ref')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N79
     AO 39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       17 51'End Position  . .'
     A        79                       17 51'End Position  . :'
     A            #1EPOS         5  0B 17 71TEXT('End Position')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N79
     AO 40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  20 39TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 48'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     20 62TEXT('Action Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
