      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Display RDEs')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG1010DF   Display RDEs              Display file   *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(37 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1FOPT        10   H      TEXT('Formatting Options')
     A            #1MAXL         5  0H      TEXT('Maximum Length')
     A            #1ETXT        25   H      TEXT('Example Data')
     A            #1DINT        10   H      TEXT('Derived Data internal ref')
     A            #1SPOS         5  0H      TEXT('Start Position')
     A            #1EPOS         5  0H      TEXT('End Position')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B 12  2
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1RDE         10     12  5TEXT('Report Data Element')
     A                                      DSPATR(HI)
     A            #1ELYP         6     12 18TEXT('Element Type')
     A                                      DSPATR(HI)
     A            #1DRVD         1     12 30TEXT('Allow Derived Information')
     A                                      DSPATR(HI)
     A            #1DSRC        10     12 36TEXT('Derived Information Src')
     A                                      DSPATR(HI)
     A            #1DESC        30     12 49TEXT('Description')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(09) SFLSIZ(10)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2RDE')
     A                                      SETOF(32 'Invalid: #2ELYP')
     A                                      SETOF(33 'Invalid: #2DRVD')
     A                                      SETOF(34 'Invalid: #2DSRC')
     A                                      SETOF(35 'Invalid: #2DESC')
     A                                      SETOF(36 'Invalid: #2FOPT')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1010F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A            #2RECI         1   H      TEXT('Record Identifier')
     A            #2MAXL         5  0H      TEXT('Maximum Length')
     A            #2ETXT        25   H      TEXT('Example Data')
     A            #2DINT        10   H      TEXT('Derived Data internal ref')
     A            #2SPOS         5  0H      TEXT('Start Position')
     A            #2EPOS         5  0H      TEXT('End Position')
     A            #2AJOB        10   H      TEXT('Job name')
     A            #2AUSR        10   H      TEXT('User name')
     A            #2ATIM         6  0H      TEXT('Action Time')
     A            #2ARDT         7   H      TEXT('Action Date')
     A            #2AACT         1   H      TEXT('Action Type')
     A            #2RDNB         5  0H      TEXT('Run day number')
     A                                  5  2'Position on.: Report Data Element'
     A            #2RDE         10   B  5 36TEXT('Report Data Element')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7  2'Select on..: Element Type'
     A            #2ELYP         6   B  7 28TEXT('Element Type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 36'Derived ?'
     A            #2DRVD         1   B  7 46TEXT('Allow Derived Information')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 49'Information Src'
     A            #2DSRC        10   B  7 65TEXT('Derived Information Src')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 15'Description'
     A            #2DESC        30   B  8 28TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 60'Edit Opt'
     A            #2FOPT        10   B  8 69TEXT('Formatting Options')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A  81                             10  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10  5'Report Data'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 18'Element'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 27'Derived'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 36'Information'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 49'Description'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11  5'Element'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 18'Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 27'   ?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 36'  Source'
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
