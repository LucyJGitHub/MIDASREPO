      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Display group sets select')
     A**************************************************************
     A*                                                            *
     A*  Midas - User Defined Correspondence                   *
     A*                                                            *
     A*   DSPF/CG1041DF   Display Group Sets        Select         *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(37 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B 12  3
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            #1GRPS         6     12  6TEXT('Group Set Name')
     A                                      DSPATR(HI)
     A            #1CSET         1     12 19TEXT('Complex Set Indicator')
     A                                      DSPATR(HI)
     A            #1MREC         1     12 28TEXT('Multiple Indicator')
     A                                      DSPATR(HI)
     A            #1RSET         1     12 37TEXT('Restricted Set Indicator')
     A                                      DSPATR(HI)
     A            #1MMGR         1     12 45TEXT('Mail Merge Group')
     A                                      DSPATR(HI)
     A            #1DESC        30     12 50TEXT('Description')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(09) SFLSIZ(10)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2GRPS')
     A                                      SETOF(32 'Invalid: #2CSET')
     A                                      SETOF(33 'Invalid: #2MREC')
     A                                      SETOF(34 'Invalid: #2RSET')
     A                                      SETOF(35 'Invalid: #2MMGR')
     A                                      SETOF(36 'Invalid: #2DESC')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1041F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A                                  4  2'Position on: Group Set Name'
     A            #2GRPS         6   B  4 30TEXT('Group Set Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6  2'Select on..: Complex Set'
     A            #2CSET         1   B  6 27TEXT('Complex Set Indicator')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 30'Multiple'
     A            #2MREC         1   B  6 39TEXT('Multiple Indicator')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 42'Restricted'
     A            #2RSET         1   B  6 53TEXT('Restricted Set Indicator')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 56'Mail Merge'
     A            #2MMGR         1   B  6 67TEXT('Mail Merge Group')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  7 15'Description'
     A            #2DESC        30   B  7 27TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A  81                              9  3'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9  6'Group'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 16'Complex'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 25'Multiple'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 35'Restr'''
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 43'Mail'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 50'Description'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10  6'Set Name'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 16'  Set'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 25'Records'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 35' Set'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             10 43'Merge'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                             11 43'  ?'
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
