      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Format option maintenance')
     A**************************************************************
     A*                                                            *
     A*  Midas - User Defined Correspondence                   *
     A*                                                            *
     A*   DSPF/CG1120DF   Edit Format Option    Edit record(1 scrn)*
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 083684             Date 22Feb95               *
      *                 080867             Date 29Dec94               *
     A*                   S01522       DATE 23NOV94                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
     A*    083684 - Formatting program should be either '*NONE'    *
     A*             or existing program.                           *
     A*    080867 - Use Narrative fields for free text             *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1FRMT')
     A*.HELP TEXT...............................................................
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1120F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . :'
     A            #PACTC         1      4 25TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Formatting Options :'
     A            #1FOPT        10      6 25TEXT('Formatting Options')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Format Type  . . . .'
     A            #1FRMT        15   B  7 25TEXT('Format Type')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1FPGM')
      * 080867 Start
     A***********                           SETOF(32 'Invalid: #1DESC')
     A                                      SETOF(32 'Invalid: #1NAR1')
     A                                      SETOF(33 'Invalid: #1NAR2')
     A                                      SETOF(34 'Invalid: #1NAR3')
     A                                      SETOF(35 'Invalid: #1NAR4')
     A                                      SETOF(36 'Invalid: #1DESC')
      * 080867 End
     A*.HELP TEXT...............................................................
      * 080867 Start
      * 080867 End
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1120F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code  . . . :'
     A            #PACTC         1      4 25TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Formatting Options :'
     A            #1FOPT        10      6 25TEXT('Formatting Options')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Format Type  . . . :'
     A            #1FRMT        15      7 25TEXT('Format Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                        9  2'Formatting Program .'
     A        79                        9  2'Formatting Program :'
     A            #1FPGM        10   B  9 25TEXT('Formatting Program')
      * start 083684
     A************************************* CHECK(VNE)
      * end-- 083684
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
      * 080867 Start
      ************
     A*******N79**                     11  2'Description  . . . .'
     A********79**                     11  2'Description  . . . :'
     A************#1DESC        30   B 11 25TEXT('Description')
     A************                          CHECK(LC)
     A*N25********                          OVRDTA
     A**79********                          DSPATR(PR)
     A**32********                          DSPATR(RI PC)
     A*N79********
     A*O*32********                          DSPATR(UL)
     A*N32********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     16  2'Last Change Information'
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(UL HI)
     A*N25********                          OVRATR
     A************                     17  2'User name :'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1AUSR        10     17 14TEXT('User name')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     17 25'Action Time :'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1ATIM         6  0  17 39TEXT('Action Time')
     A************                          EDTWRD('  :  :  ')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     17 48'Action Date :'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1ARDT         7     17 62TEXT('Action Date')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A       N79                       11  2'Narrative .'
     A        79                       11  2'Narrative :'
     A            #1NAR1        64   B 11 16TEXT('Narrative 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1NAR2        64   B 12 16TEXT('Narrative 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1NAR3        64   B 13 16TEXT('Narrative 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1NAR4        64   B 14 16TEXT('Narrative 4')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       16  2'Description  . . . .'
     A        79                       16  2'Description  . . . :'
     A            #1DESC        30   B 16 25TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  20 39TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 48'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     20 62TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      * 080867 End
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
