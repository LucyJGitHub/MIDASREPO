      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Print item type maintenance')
     A**************************************************************
     A*                                                            *
     A*  Midas - User Defined Correspondence                   *
     A*                                                            *
     A*   DSPF/CG1170DF - Edit Print Item Type  Edit record(1 scrn)*
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. CCG009       DATE 01JUN95                *
     A*    PREV AMEND NO. S01522       DATE 23NOV94                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*    CCG009 - Private Banking UDC                            *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1PTYP')
     A                                      SETOF(32 'Invalid: #1PSTP')
     A*.HELP TEXT...............................................................
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1170F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Print Item Type . . .'
     A            #1PTYP        10   B  6 26TEXT('Print Item Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6 37'Sub-type'
     A            #1PSTP        10   B  6 46TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1GRPS')
     A                                      SETOF(32 'Invalid: #1AACD')
     A                                      SETOF(33 'Invalid: #1ALAC')
     A                                      SETOF(34 'Invalid: #1DESC')
     A*                                                      /*CCG009*/
     A                                      SETOF(35 'Invalid: #1GPHD')
     A                                      SETOF(36 'Invalid: #1GPSQ')
     A*                                                      /*CCG009*/
     A*.HELP TEXT...............................................................
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG1170F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . . . :'
     A            #PACTC         1      4 26TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Print Item Type . . :'
     A            #1PTYP        10      6 26TEXT('Print Item Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6 37'Sub-type'
     A            #1PSTP        10      6 46TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A       N79                        9  2'Group Set Name  . . .'
     A        79                        9  2'Group Set Name  . . :'
     A            #1GRPS         6   B  9 26TEXT('Group Set Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       11  2'Allow Account Code on schedule  .-
     A                                       . .'
     A        79                       11  2'Allow Account Code on schedule  .-
     A                                       . :'
     A            #1AACD         1   B 11 42TEXT('Allow Account Code')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       12  2'Allow Account Identifier on sched-
     A                                      ule .'
     A        79                       12  2'Allow Account Identifier on sched-
     A                                      ule :'
     A            #1ALAC         1   B 12 42TEXT('Allow Account Identifier')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N79
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       14  2'Description . . . . .'
     A        79                       14  2'Description . . . . :'
     A            #1DESC        30   B 14 26TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N79
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A*                                                      /*CCG009*/
     A       N79                       16  2'Group Header/Detail . . . . . . .-
     A                                       . .'
     A        79                       16  2'Group Header/Detail . . . . . . .-
     A                                       . :'
     A            #1GPHD         1   B 16 42TEXT('Group Header/Detail')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N79
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A       N79                       17  2'Group Sequence. . . . . . . . . .-
     A                                       . .'
     A        79                       17  2'Group Sequence. . . . . . . . . .-
     A                                       . :'
     A            #1GPSQ         2   B 17 42TEXT('Group Sequence')
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N79
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A************                     17  2'Last Change Information'
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(UL HI)
     A*N25********                          OVRATR
     A************                     18  2'User name :'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1AUSR        10     18 14TEXT('User name')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     18 25'Action Date :'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1ARDT         7     18 39TEXT('Action Date')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A************                     18 47'Action Time :'
     A**78********                          DSPATR(ND)
     A*N25********                          OVRATR
     A************#1ATIM         6  0  18 61TEXT('Action Time')
     A************                          EDTWRD('  :  :  ')
     A*N25********                          OVRDTA
     A**78********                          DSPATR(ND)
     A*N78********                          DSPATR(HI)
     A*N25********                          OVRATR
     A*                                                      /*CCG009*/
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
