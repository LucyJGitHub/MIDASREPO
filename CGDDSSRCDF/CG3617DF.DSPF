      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Schema and elmnt names group set defn')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3617DF - Schema and Element names maintenance         *
      *                  Group Set Definition                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CA03
     A                                      PRINT(MPRINT)
      *
      **  Screen 1 Subfile Record Format.
      *
     A          R CG3617S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  31                                  SFLNXTCHG
     A            #0SELE         1A  B 10 13DSPATR(HI)
     A N23N39                               DSPATR(UL)
     A  23N39                               DSPATR(RI PC)
     A  39                                  DSPATR(PR)
     A            #0GSET         6A  O 10 17
     A            #0GSTD        30A  O 10 35
      *
      **  Screen 1 Control Record Format
      *
     A          R CG3617C0                  SFLCTL(CG3617S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A  29                                  SFLCLR
     A N29                                  SFLDSPCTL
     A N29 28                               SFLDSP
     A N29 28 37                            SFLEND(*MORE)
     A N37                                  ROLLUP(27)
     A                                      CF05
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3617F1'
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
      *
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 24'Schema and Element names maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3 33'Group Set Definition'
     A                                      DSPATR(HI)
      *
     A                                  5  2'Position on: Group Set Definition'
     A            #0CGSE         6A  B  5 37
     A                                      DSPATR(HI)
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
      *
     A                                  7 17'Group Set'
     A                                  7 35'Description'
     A                                  8 17'Definition'
      *
      **  Screen 1 Footings Format.
      *
     A          R CG3617F0
     A                                 22  2'1=Select'
     A                                 23  2'F3=Exit   F5=Refresh'
      *
      **  Error message Subfile Format
      *
     A          R CG3617S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      **  Error message Subfile Controller Format
      *
     A          R CG3617C1                  TEXT('Program messages')
     A                                      SFLCTL(CG3617S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
