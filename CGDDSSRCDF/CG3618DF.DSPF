      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Schema and elmnt names paths conversion')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3618DF - Schema and Element names maintenance         *
      *                  Conversion Types                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 24Mar04               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA03 - Webfacing Changes.                                  *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R CG3618F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
      *
     A                                      PUTOVR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3618F1'
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
      *
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 24'Schema and Element names maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3 35'Conversion Types'
     A                                      DSPATR(HI)
      *
     A                                  5  2'Group Set Definition:'
     A            #0GSET         6A  O  5 24DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6  2'Description:'
     A            #0GSTD        30A  O  6 15DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  8  8'Type'
     A                                  8 25'Description'
      *
     A            #0SPAT         1A  B 10  5DSPATR(UL)
     A N01                                  DSPATR(HI)
     A                                      OVRDTA
     A  01                                  DSPATR(RI PC)
     A                                 10  8'Paths'
     A                                 10 25'Generated paths'
      *
     A            #0SREF         1A  B 11  5DSPATR(UL)
     A N02                                  DSPATR(HI)
     A                                      OVRDTA
     A  02                                  DSPATR(RI PC)
     A                                 11  8'References'
     A                                 11 25'Print item type / sub type'
      *
     A                                 22  2'1=Select'
      *
     A                                 23  2'F3=End   F5=Refresh   +
     A                                          F12=Previous'
      *
     A          R CG3618S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MKEY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
     A          R CG3618C0                  TEXT('Program messages')
     A                                      SFLCTL(CG3618S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
