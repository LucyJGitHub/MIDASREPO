      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Schema & elmnt names paths conversion lst')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3619DF - Schema and Element names maintenance         *
      *                  Paths Conversion List                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A N10                                  CF05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header
      *
     A          R CG3619F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3619F1'
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 24'Schema and Element names maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
     A                                  3 34'Paths Conversion'
     A                                      DSPATR(HI)
      *
      ** Details Subfile
      *
     A          R CG3619S0                  SFL
     A  03                                  SFLNXTCHG
     A            #0SELE         1A  B 11  2
     A  05                                  DSPATR(RI PC)
     A N05                                  DSPATR(HI)
     A            #0PATH        70A  O 11  4
     A            #0NAME        30A  O 12 40
      *
      ** Details Subfile Control
      *
     A          R CG3619C0                  SFLCTL(CG3619S0)
     A  01                                  SFLDSP
     A                                      SFLDSPCTL
     A  02                                  SFLCLR
     A                                      SFLSIZ(6)
     A                                      SFLPAG(5)
     A  07                                  SFLEND(*MORE)
     A N08                                  ROLLUP(09)
     A                                      OVERLAY
     A            #0RRN          4S 0H      SFLRCDNBR
     A                                  5  2'Group Set Definition:'
     A            #0DEFN         6A  O  5 24DSPATR(HI)
     A                                  6  2'Description:'
     A            #0DESC        30A  O  6 15DSPATR(HI)
     A                                  8  2'Position on: Path'
     A            #0POSI        60A  B  8 20CHECK(LC)
     A  04                                  DSPATR(PC)
     A N04                                  DSPATR(HI)
     A  10                                  DSPATR(PR)
     A                                 10  4'Path'
     A                                 10 40'Element Name'
      *
      ** Footer
      *
     A          R CG3619F1                  OVERLAY
     A                                 22  2'A=Amend   E=Enquire'
     A N10                             23  2'F3=Exit   F5=Refresh   F12=Cancel'
     A  10                             23 12'F3=Exit   F12=Cancel'
      *
      ** Message Subfile
      *
     A          R CG3619S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGM                     SFLPGMQ
      *
      ** Message Subfile  Control
      *
     A          R CG3619C1                  TEXT('Program messages')
     A                                      SFLCTL(CG3619S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGM                     SFLPGMQ
      *
