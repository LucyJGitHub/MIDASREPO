      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Schema and elmnt names paths conversion')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3620DF - Schema and Element names maintenance         *
      *                  Paths Conversion                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R CG3620F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      PUTOVR
     A N09                                  CA05
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3620F1'
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
      *
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 24'Schema and Element names maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3 34'Paths Conversion'
     A                                      DSPATR(HI)
      *
     A                                  5  2'Group Set Definition:'
     A            #0GSET         6A  O  5 24DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6  2'Description:'
     A            #0GSTD        30A  O  6 15DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  8  2'Path:'
     A            #0PATH        70A  O  8  8DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 10  2'Element Name:'
     A            #0ELEM        30A  B 10 16DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A  09                                  DSPATR(PR)
     A N23N09                               DSPATR(UL)
     A                                      CHECK(LC)
     A                                      OVRDTA
      *
     A                                 23  2'F3=End'
     A N09                             23 11'F5=Refresh   F12=Previous'
     A  09                             23 11'F12=Previous'
      *                                                                       ==
     A          R CG3620S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGKY                   SFLMSGKEY
     A            #1PGM                     SFLPGMQ
      *                                                                       ==
     A          R CG3620C1                  TEXT('Program messages')
     A                                      SFLCTL(CG3620S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGM                     SFLPGMQ
