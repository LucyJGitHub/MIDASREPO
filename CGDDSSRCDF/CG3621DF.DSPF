      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Schema & elmnt names ref conversion list')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3621DF - Schema and Element names maintenance         *
      *                  Reference Conversion List                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A N12                                  CA05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Header Record Format
      *
     A          R CG3621F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3621F1'
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0MRDT    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 24'Schema and Element names maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
     A                                  3 33'References Conversion'
     A                                      DSPATR(HI)
      *
      ** Subfile Record Format
      *
     A          R CG3621S0                  SFL
     A  01                                  SFLNXTCHG
     A            #0SELE         1A  B 12  2
     A                                      DSPATR(HI)
     A N02                                  DSPATR(UL)
     A  02                                  DSPATR(RI PC)
     A            #0ARGT        23A  O 12  4
     A            #0DSCR        30A  O 12 30
     A            #0NWFD        30A  O 13 50
      *
      ** Subfile Control Record Format.
      *
     A          R CG3621C0                  TEXT('Correspondence +
     A                                      Manager Data Extract Name +
     A                                      MNT')
      *
     A                                      SFLCTL(CG3621S0)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A                                      OVERLAY
     A  03                                  SFLCLR
     A N03                                  SFLDSPCTL
     A N03 04                               SFLDSP
     A N03 05 04                            SFLEND(*MORE)
     A N05                                  ROLLUP(06 'Next page')
     A            #0SFLR         4S 0H      SFLRCDNBR
      *
     A                                  5  2'Group set definition:'
     A            #0GRPS         6A  O  5 24
     A                                      DSPATR(HI)
     A                                  6  2'Description:'
     A            #0DSCG        30A  O  6 15
     A                                      DSPATR(HI)
     A                                  8  2'Position on: Reference'
     A            #0RFFE        21A  B  8 25
     A  12                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                 10  4'Reference'
     A                                 10 30'Description'
     A                                 10 50'Schema Name'
      *
      ** Screen Footings Format
      *
     A          R CG3621F1
     A                                 22 02'A=Amend   E=Enquire'
     A                                 23 02'F3=Exit   F5=Refresh'
     A N12                             23 25'F12=Previous'
      *
      ** Message Subfile Record Format
      *
     A          R CG3621S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
      *
     A            #1MSGK                    SFLMSGKEY
     A            #1PGM                     SFLPGMQ
      *
      ** Message Subfile Control Format
      *
     A          R CG3621C1                  TEXT('Program messages')
      *
     A                                      SFLCTL(CG3621S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  07
     AON07                                  SFLEND
     A            #1PGM                     SFLPGMQ
