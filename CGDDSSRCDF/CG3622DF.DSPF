      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Schema and elmnt names refer conversion')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3622DF - Schema and Element names maintenance         *
      *                  Reference Conversion                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A  04                                  CA05
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R CG3622F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      PUTOVR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3622F1'
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
      *
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 24'Schema and Element names maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3 33'Reference Conversion'
     A                                      DSPATR(HI)
      *
     A                                  5  2'Group Set Definition:'
     A            #0GSET         6A  O  5 24
     A                                      DSPATR(HI)
      *
     A                                  6  2'Description:'
     A            #0GSTD        30A  O  6 15
     A                                      DSPATR(HI)
      *
     A                                  9  2'Reference:'
     A            #0REFR        21A  O  9 13
      *
     A                                  9 36'Description:'
     A            #0REFD        30A  O  9 49
      *
     A                                 12  2'Schema Name:'
     A            #0SHNM        30A  B 12 15CHECK(LC)
     A N03N02                               DSPATR(HI)
     A  02                                  DSPATR(RI PC)
     A N03N02                               DSPATR(UL)
     A  03                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A  03                             23  2'F3=End   F12=Previous'
     A  04                             23  2'F3=End   F5=Refresh   +
     A                                       F12=Previous'
      *
     A          R CG3622S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R CG3622C0                  TEXT('Program messages')
     A                                      SFLCTL(CG3622S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
