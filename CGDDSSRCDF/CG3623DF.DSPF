      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG System fields maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3623DF - System fields Maintenance                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A N50                                  CA05
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R CG3623F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3623F1'
     A                                  1 33'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 32'System fields maintenance'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A          R CG3623S0
     A                                      SFL
     A  43                                  SFLNXTCHG
      *
     A            #0SELE         1   B  7  3TEXT('Select Option')
     A N45                                  DSPATR(UL HI)
     A  45                                  DSPATR(RI PC)
      *
     A            #0SRDE        10   O  7  7TEXT('System Field')
     A            #0DESC        30   O  7 22TEXT('Description')
     A            #0ELEM        30   O  8 51TEXT('Element Name')
      *
     A          R CG3623C0                  TEXT('SELECT SCREEN CONTROL')
      *
     A                                      SFLCTL(CG3623S0)
     A  41                                  SFLDSP
     A                                      SFLDSPCTL
     A  47                                  SFLEND(*MORE)
     A  42                                  SFLCLR
     A N48                                  ROLLUP(49)
     A                                      SFLPAG(7) SFLSIZ(8)
     A                                      OVERLAY
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4  2'Position on: System field'
     A            #0POSI        10A  B  4 28TEXT('System Field')
     A  44                                  DSPATR(PC)
     A N44                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
      *
     A                                  6  7'System Field'
     A                                  6 22'Description'
     A                                  6 51'Element Name'
      *
     A          R CG3623F1                  TEXT('Select Screen Footer')
     A                                      OVERLAY
      *
     A                                 22  2'A=Amend   E=Enquire'
     A N50                             23  2'F3=Exit   F5=Refresh'
     A  50                             23  2'F3=Exit'
      *
     A          R CG3623S1                  TEXT('Program messages subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R CG3623C1                  TEXT('Program messages control')
     A                                      SFLCTL(CG3623S1)
     A                                      OVERLAY PUTOVR
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
