      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Data extract name system field conversion')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3624DF - Data Extract Name System Field Conversion    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CCG015  *CREATE    Date 09Aug01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA12
     A N04                                  CA05
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R CG3624F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      PUTOVR
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'CG3624F1'
     A                                  1 33'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
      *
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 32'System fields maintenance'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  5  2'System Field:'
     A            #0SYST        10A  O  5 16DSPATR(HI)
      *
     A                                  6  2'Description:'
     A            #0SYSD        30A  O  6 16DSPATR(HI)
      *
     A                                  8  2'Element Name:'
     A            #0ELEM        30A  B  8 16CHECK(LC)
     A N02N04                               DSPATR(HI)
     A  02                                  DSPATR(RI PC)
     A N02N04                               DSPATR(UL)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 11  2'Group Name:'
     A            #0GRPN        30A  B 11 16CHECK(LC)
     A N03N05                               DSPATR(HI)
     A  03                                  DSPATR(RI PC)
     A N03N05                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 23  2'F3=End'
     A N04                             23 11'F5=Refresh   F12=Previous'
     A  04                             23 11'F12=Previous'
      *
     A          R CG3624S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R CG3624C0                  TEXT('Program messages')
     A                                      SFLCTL(CG3624S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
