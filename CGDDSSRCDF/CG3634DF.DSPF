      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Custom-defined data extract')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3634DF - Custom-defined Data Extract                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CCG015  *CREATE    Date 21Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG015 - Correspondence Manager                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      ** ==============================================================
      ** SUBFILE DEFINITION
      ** ==============================================================
     A          R CG3634S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            SOPTN          1   B  9  3
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(UL)
     A            SRQNM         15   O  9  6
     A                                      DSPATR(HI)
     A            SSHNM         30   O  9 22
     A                                      DSPATR(HI)
     A            SACTV          1   O  9 55
     A                                      DSPATR(HI)
     A            SRQDS         20   O  9 60
     A                                      DSPATR(HI)
      ** ==============================================================
      ** SUBFILE CONTROL RECORD FORMAT
      ** ==============================================================
     A          R CG3634C0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      ** Function keys definitions
     A                                      CF03
     A                                      CF09
     A                                      CF05
     A                                      SFLCTL(CG3634S0)
     A                                      OVERLAY
     A  82                                  SFLDSP
     A  83                                  SFLDSPCTL
     A  87                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  86                                  SFLEND
     A N86                                  ROLLUP(88)
      ** Page of sub file to be displayed
     A            RECNS0         4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'CG3634'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  8  3'?'
     A                                  2 29'Custom-defined Data Extract'
     A                                      DSPATR(HI)
     A                                  4  3'Position on:'
     A                                  4 16'Request Name'
     A            SPRQNM        15   B  4 29CHECK(LC)
     A                                      DSPATR(HI)
     A                                  5  3'Select on:'
     A                                  5 14'Schema Name'
     A            SSSHNM        30   B  5 26CHECK(LC)
     A                                      DSPATR(HI)
     A                                  6  3'Select on:'
     A                                  6 14'Active'
     A            SSACTV         1   B  6 21
     A                                      DSPATR(HI)
     A                                  8  6'Request Name'
     A                                  8 22'Schema Name'
     A                                  8 52'Active'
     A                                  8 60'Description'
      ** ==============================================================
      ** FOOTER DEFINITION
      ** ==============================================================
     A          R CG3634F0                  TEXT('Select Screen Footer')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F9=Add'
     A                                 23 17'F5=Refresh'
     A                                 22  2'A=Amend'
     A                                 22 10'E=Enquire'
     A                                 22 20'D=Delete'
     A                                 22 29'X=Activate'
      ** ==============================================================
      ** BOTTOM TEXT
      ** ==============================================================
     A          R CG3634F1                  TEXT('Bottom Text')
     A                                      OVERLAY
     A            SBOTX         79   O 19  2
      *
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
