      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG SQL request editor')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3635DF - SQL requests editor                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CCG015  *CREATE    Date 22Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG015 - Correspondence Manager                              *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A*
     A          R CG3635S0                  SFL
     A  53                                  SFLNXTCHG
     A            SOPTN          1   B  8  6
     A N61                                  DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI)
     A  61
     AO 66                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A  68                                  DSPATR(ND)
     A            SSTMT         60   B  8 10
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A  68                                  DSPATR(PR)
     A N68                                  DSPATR(UL)
     A*
     A*=========================================================================
     A          R CG3635C0                  SFLCTL(CG3635S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF03
     A    N64                               CF10
     A                                      CF12
     A N65N64                               CF13
     A N65N64                               CF14
     A                                      OVERLAY
     A  52                                  SFLDSP
     A                                      SFLDSPCTL
     A  54                                  SFLINZ
     A  51                                  SFLCLR
     A  50                                  SFLEND
     A            RECNS0         4S 0H      SFLRCDNBR
     A            SCRLS0         5S 0H      SFLSCROLL
     A                                  1 13'CG3635'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  2 29'Custom-defined data extract'
     A                                      DSPATR(HI)
     A                                  4  2'Request Name:'
     A            SRQNM         15A  B  4 16
     A N62                                  DSPATR(HI)
     A N62N67                               DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  67                                  DSPATR(PR)
     A                                  4 33'Schema Name:'
     A            SSHNM         30A  B  4 46
     A                                      CHECK(LC)
     A N60                                  DSPATR(HI)
     A N60N67                               DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A  67                                  DSPATR(PR)
     A                                  5  2'Description :'
     A            SRQDS         35A  B  5 16
     A                                      CHECK(LC)
     A N63                                  DSPATR(HI)
     A N63N67                               DSPATR(UL)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A  67                                  DSPATR(PR)
     A                                  7  5'Cmd'
     A                                  7 33'SQL statements'
     A*
     A*=========================================================================
     A          R CG3635F0
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Previous'
     A N65N64                          23 23'F13=Syntax Check'
     A N65N64                          23 40'F14=Activate'
     A N65N64                          23 53'F10=Save/Exit'
     A  65N64                          23 67'F10=Confirm'
     A                                 22  2'I=Insert'
     A                                 22 11'D=Delete'
      ** ==============================================================
      ** BOTTOM TEXT
      ** ==============================================================
     A          R CG3635F1                  TEXT('Bottom Text')
     A                                      OVERLAY
     A            SBOTX         79   O 19  2
