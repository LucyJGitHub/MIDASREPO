      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG List of schema names')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG3636DF - List of Schema Names                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CCG015  *CREATE    Date 08Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCG015 - Correspondence Manager                              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      ** ==============================================================
      ** SUBFILE DEFINITION
      ** ==============================================================
     A          R CG3636S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            SOPTN          1   B  9  3
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(UL)
     A            SSHNM         30   O  9  6
     A                                      DSPATR(HI)
     A            SDESC         30   O  9 38
     A                                      DSPATR(HI)
      ** ==============================================================
      ** SUBFILE CONTROL RECORD FORMAT
      ** ==============================================================
     A          R CG3636C0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      ** Function keys definitions
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(CG3636S0)
     A                                      OVERLAY
     A  82                                  SFLDSP
     A  83                                  SFLDSPCTL
     A  87                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  86                                  SFLEND
     A N86                                  ROLLUP(88)
      ** Page of sub file to be displayed
     A            RECNS0         4  0H      SFLRCDNBR
     A            SUSER         10A  O  1  2TEXT('User Name')
     A                                  1 13'CG3636'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  1 31'Correspondence Manager'
     A                                      DSPATR(HI)
     A                                  2 35'Schema Names'
     A                                      DSPATR(HI)
     A                                  5  3'Position on:'
     A                                  5 16'Schema Name'
     A            SPSHNM        30   B  5 29DSPATR(HI)
     A                                      CHECK(LC)
     A                                  7  3'?'
     A                                  7  6'Schema Name'
     A                                  7 38'Description'
      ** ==============================================================
      ** FOOTER DEFINITION
      ** ==============================================================
     A          R CG3636F0                  TEXT('Select Screen Footer')
     A                                      OVERLAY
     A                                 22  2'1=Select'
     A                                 23  2'F3=Exit'
     A                                 23 10'F12=Previous'
      ** ==============================================================
      ** BOTTOM TEXT
      ** ==============================================================
     A          R CG3636F1                  TEXT('Bottom Text')
     A                                      OVERLAY
     A            SBOTX         79   O 19  2
      *
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
