      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Display reference records')
     A**************************************************************
     A*                                                            *
     A*  Midas - User defined correspondence                       *
     A*                                                            *
     A*  DSPF/CG5000DF - Correspondence Management: display        *
     A*                  reference records                         *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CCG017             Date 23Jun06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CCG015             Date 21Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCG009             Date 01Jun95               *
     A*                   090199       Date 12JUL95                *
     A*                   S01522       Date 10FEB95                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG017 - Reprint using Item ID                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CCG015  -  Correspondence Manager - Phase 1               *
     A*  CCG009  -  Private Banking UDC                            *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01522  -  User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1PCOR        10   H      TEXT('Primary Correspondent')
     A            #1ORBR         3   H      TEXT('Originating Branch Code')
     A            #1OVBR         3   H      TEXT('Overriding Branch Code')
     A            #1MODI         2   H      TEXT('Module id')
     A            #1MTRN        15   H      TEXT('Midas Transaction Ref.')
     A**Start of change CGL029
     A**********  #1MACT        18   H      TEXT('Midas Account Identifier')                  CGL029
     A            #1MACT        24   H      TEXT('Midas Account Identifier')
     A**End of change CGL029
     A            #1ATRM         1   H      TEXT('Auto-Transmission Ind.')
     A            #1PRTD         5  0H      TEXT('Scheduled Print Date')
     A            #1DDAT         5  0H      TEXT('Scheduled Drop Date')
     A            #1LGID         2   H      TEXT('Language identifier')
     A            #1MSHT         6   H      TEXT('Mail Shot Identifier')
     A            #1CHQR        10   H      TEXT('Cheque Reference')
     A            #1FREF        25   H      TEXT('Further Reference')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A*                                                      /*CCG009*/
     A            #1GPHD         1   H      TEXT('Group Header/Detail')
     A*                                                      /*CCG009*/
      **Start of change CCG017
     A            ##ITEM         8   H      TEXT('Item Id')
      **End of change CCG017
     A            #1SEL          1   B  8  2
     A  32                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            #1BRCA         3      8  5TEXT('Branch Code')
     A                                      DSPATR(HI)
     A            #1ITEM         8  0   8 15TEXT('Item Identifier')
     A                                      DSPATR(HI)
     A            #1DCOR        10      8 25TEXT('Destination Correspondent')
     A                                      DSPATR(HI)
     A            #1PTYP        10      8 42TEXT('Print Item Type')
     A                                      DSPATR(HI)
     A            #1PSTP        10      8 54TEXT('Print Item Sub-type')
     A                                      DSPATR(HI)
     A            #1ISTS         4      8 67TEXT('Item Status')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2DCOR')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG5000F1'
     A                                      DSPATR(HI)
     A                                  1 31'  Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 32'Display Reference Records'
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  3'Position on..: Destination Corres-
     A                                      pondent'
     A            #2DCOR        10   B  4 44TEXT('Destination Correspondent')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  81                              6  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6  5'Branch'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 15'Item'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 15'Id'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 25'Destination'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 25'Correspondent'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 42'Print item -'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 42'Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 54'Subtype'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 54'Item'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 67'Status'
     A                                      DSPATR(HI) COLOR(WHT)
      *                                          Begin /CCG015/
     A*=========================================================================
     A          R #SFLRCD2                  TEXT('Subfile record.')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1PCOR        10   H      TEXT('Primary Correspondent')
     A            #1ORBR         3   H      TEXT('Originating Branch Code')
     A            #1OVBR         3   H      TEXT('Overriding Branch Code')
     A            #1MODI         2   H      TEXT('Module id')
     A            #1MTRN        15   H      TEXT('Midas Transaction Ref.')
     A**Start of change CGL029
     A**********  #1MACT        18   H      TEXT('Midas Account Identifier')                  CGL029
     A            #1MACT        24   H      TEXT('Midas Account Identifier')
     A**End of change CGL029
     A            #1ATRM         1   H      TEXT('Auto-Transmission Ind.')
     A            #1PRTD         5  0H      TEXT('Scheduled Print Date')
     A            #1DDAT         5  0H      TEXT('Scheduled Drop Date')
     A            #1LGID         2   H      TEXT('Language identifier')
     A            #1MSHT         6   H      TEXT('Mail Shot Identifier')
     A            #1CHQR        10   H      TEXT('Cheque Reference')
     A            #1FREF        25   H      TEXT('Further Reference')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1GPHD         1   H      TEXT('Group Header/Detail')
      *
     A            #1SEL          1   B  8  2
     A  32                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A N32                                  DSPATR(HI)
     A            #1BRCA         3      8  5TEXT('Branch Code')
     A                                      DSPATR(HI)
     A            #1ITEM         8  0   8 13TEXT('Item Identifier')
     A                                      DSPATR(HI)
     A            #1DCOR        10      8 23TEXT('Destination Correspondent')
     A                                      DSPATR(HI)
     A            #1PDFN        35      8 35TEXT('PDF File Name')
     A                                      DSPATR(HI)
     A            #1ISTS         4      8 72TEXT('Item Status')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL2                  TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD2)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2DCOR')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG5000F1'
     A                                      DSPATR(HI)
     A                                  1 31'  Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 32'Display Reference Records'
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  3'Position on..: Destination Corres-
     A                                      pondent'
     A            #2DCOR        10   B  4 44TEXT('Destination Correspondent')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  81                              6  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6  5'Branch'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7  5'Code'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 13'Item'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 13'ID'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 23'Dest'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 23'Corresp'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 35'PDF'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 35'File Name'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 72'Item'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 72'Status'
     A                                      DSPATR(HI) COLOR(WHT)
      *                                          End   /CCG015/
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
