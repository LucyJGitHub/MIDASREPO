      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Rreference records maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User defined correspondence                          *
     A*                                                               *
     A*  DSPF/CG5010DF - Correspondence Management: select            *
     A*                  reference records                            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 116275             Date 15Apr97               *
      *                 090199             Date 12Jul95               *
     A*                 083775             DATE 17FEB95               *
     A*                 S01522             DATE 10FEB95               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  116275  -  The word Unknown is being truncated to Unknow     *
     A*             Extend the field to 7 characters from 6.          *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  083775  -  Define Retention Period as an alpha               *
     A*  S01522  -  User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*
     A*
     A          R #RCDDTL1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      CF03(03 'Exit.')
     A                                      CF12(12 'Previous.')
     A  89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG5010F1'
     A                                      DSPATR(HI)
     A                                  1 30'  Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1PGRP         6   H      TEXT('Print Item Group Set Name')
     A            #1MSHT         6   H      TEXT('Mail Shot Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . . . . . . :'
     A            #PACTC         1      4 32TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4 50'Item Identifier :'
     A            #1ITEM         8  0   4 70TEXT('Item Identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Destination Correspondent :'
     A            #1DCOR        10      6 32TEXT('Destination Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6 50'Branch Code  . . . . . :'
     A            #1BRCA         3      6 77TEXT('Branch Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Primary Correspondent . . :'
     A            #1PCOR        10      7 32TEXT('Primary Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7 50'Originating Branch . . :'
     A            #1ORBR         3      7 77TEXT('Originating Branch Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Module id . . . . . . . . :'
     A            #1MODI         2      8 32TEXT('Module id')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8 50'Overriding Branch  . . :'
     A            #1OVBR         3      8 77TEXT('Overriding Branch Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,CG5010DF01
     A                                 10  2'Print Item Type . . . . . :'
     A            #1PTYP        10     10 32TEXT('Print Item Type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10 50'Sub-type  . . . :'
     A            #1PSTP        10     10 70TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'Midas Transaction Ref.  . :'
     A            #1MTRN        15     12 32TEXT('Midas Transaction Ref.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                       12 50'Account Id'                                      CGL029
     A**********  #1MACT        18     12 62TEXT('Midas Account Identifier')                  CGL029
     A                                 12 50'A/C Id'
     A            #1MACT        24     12 57TEXT('Midas Account Identifier')
     A**End of change CGL029
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13  2'Item status - code. . . . :'
     A            #1ISTS         4     13 32TEXT('Item Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13 50'Scheduled Print Date :'
      * Start 116275
     A*********** #1PRDA ******* 6 *** 13 75TEXT('Scheduled Print Date') *******
     A            #1PRDA         7     13 74TEXT('Scheduled Print Date')
      * End 116275
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'            - description :'
     A            #1STDS        30     14 32TEXT('Item Status desc.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'Cheque Reference  . . . . :'
     A            #1CHQR        10     15 32TEXT('Cheque Reference')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15 50'Auto-Transmission Ind. . :'
     A            #1ATRM         1     15 79TEXT('Auto-Transmission Ind.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'Retention period  . . . . :'
      * Start 083775
     A************#1DDAT         5  0  16 32TEXT('Retention period')
     A************                          EDTCDE(4)
     A            #1DDAT         5  0H      TEXT('Retention period')
      * Start 116275
     A*********** #1DDTA ******* 6 *** 16 32TEXT('Retention period') **** 116275
     A            #1DDTA         7     16 32TEXT('Retention period')
      * End 116275
      * End 083775
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16 50'Language identifier  . . :'
     A            #1LGID         2     16 78TEXT('Language identifier')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'Further Reference . . . . :'
     A            #1FREF        25     17 32TEXT('Further Reference')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'Last Change Information'
     A                                      DSPATR(UL HI)
     A                                 20  2'User name'
     A            #1AUSR        10     20 12TEXT('User name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 23'Action Time'
     A            #1ATIM         6  0  20 35TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 44'Action Date'
     A            #1ARDT         7     20 56TEXT('Action Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
