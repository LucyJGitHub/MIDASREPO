      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Corr. Management Prompt')
     A**************************************************************
     A*                                                            *
     A*  Midas - User defined correspondence                   *
     A*                                                            *
     A*  DSPF/CG5020DF - Correspondence Management: get selection  *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CCG017             Date 23Jun06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 095461             Date 24Oct95               *
      *                 090199             Date 12Jul95               *
     A*                 S01522             DATE 10FEB95               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG017 - Reprint using Item ID                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  095461  -  Add Correspondent Type Selection               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01522  -  User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* COMMAND KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'CF12 Previous.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1DCOR')
     A                                      SETOF(32 'Invalid: #1PCOR')
     A                                      SETOF(33 'Invalid: #1BRCA')
     A                                      SETOF(34 'Invalid: #1MODI')
     A                                      SETOF(35 'Invalid: #1ORBR')
     A                                      SETOF(36 'Invalid: #1MTRN')
     A                                      SETOF(37 'Invalid: #1OVBR')
     A                                      SETOF(38 'Invalid: #1MACT')
     A                                      SETOF(39 'Invalid: #1ISTS')
     A                                      SETOF(40 'Invalid: #1PRTD')
     A                                      SETOF(41 'Invalid: #1PTYP')
     A                                      SETOF(42 'Invalid: #1PSTP')
     A                                      SETOF(43 'Invalid: #1LGID')
     A                                      SETOF(44 'Invalid: #1ATRM')
     A* Start 095461
     A                                      SETOF(45 'Invalid: #1CTYP')
     A* End 095461
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG5020F1'
     A                                      DSPATR(HI)
     A                                  1 31'  Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A            #PPGRP         6   H      TEXT('Print Item Group Set Name')
     A            #1ITEM         8  0H      TEXT('Item Identifier')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1DDAT         5  0H      TEXT('Scheduled Drop Date')
     A            #1MSHT         6   H      TEXT('Mail Shot Identifier')
     A            #1CHQR        10   H      TEXT('Cheque Reference')
     A            #1FREF        25   H      TEXT('Further Reference')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1PRTD         5  0H      TEXT('Scheduled Print Date')
     A                                  4  2'Position on..: Destination Corres-
     A                                      pondent .'
     A            #1DCOR        10   B  4 47TEXT('Destination Correspondent')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  6  2'Select on....:'
     A                                  8  2'Primary Correspondent . . .'
     A            #1PCOR        10   B  8 32TEXT('Primary Correspondent')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  8 52'Branch Code  . . . .'
     A            #1BRCA         3   B  8 75TEXT('Branch Code')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  9  2'Module id . . . . . . . . .'
     A            #1MODI         2   B  9 32TEXT('Module id')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                  9 52'Originating Branch .'
     A            #1ORBR         3   B  9 75TEXT('Originating Branch Code')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 10  2'Midas Transaction Ref.  . .'
     A            #1MTRN        15   B 10 32TEXT('Midas Transaction Ref.')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 10 52'Overriding Branch  .'
     A            #1OVBR         3   B 10 75TEXT('Overriding Branch Code')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 11  2'Midas Account Identifier  .'
     A**Start of change CGL029
     A**********  #1MACT        18   B 11 32TEXT('Midas Account Identifier')                  CGL029
     A            #1MACT        24   B 11 32TEXT('Midas Account Identifier')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A* Start 095461
     A                                 12  2'Correspondent Type. . . . .'
     A            #1CTYP        10   B 12 32TEXT('Correspondent Type')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A* End 095461
     A                                 13  2'Item Status . . . . . . . .'
     A            #1ISTS         4   B 13 32TEXT('Item Status')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 14  2'Scheduled Print Date  . . .'
     A            ##PDAT         6   B 14 32TEXT('Scheduled Print Date')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 16  2'Print Item Type . . . . . .'
     A            #1PTYP        10   B 16 32TEXT('Print Item Type')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 17  2'Print Item Sub-type . . . .'
     A            #1PSTP        10   B 17 32TEXT('Print Item Sub-type')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 19  2'Language identifier . . . .'
     A            #1LGID         2   B 19 32TEXT('Language identifier')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'Auto-Transmission Ind.  . .'
     A            #1ATRM         1   B 20 32TEXT('Auto-Transmission Ind.')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
      **Start of change CCG017
     A                                 20 52'Item ID . .'
     A            ##ITEM         8   B 20 64TEXT('Item ID')
     A N55                                  OVRDTA
     A  52                                  DSPATR(RI PC)
     A N52                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N55                                  OVRATR
      **End of change CCG017
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
