      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG C/M Display Log Record')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG5030DF - Display Log Record Display record(1 scrn)*
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. S01522       DATE 31JAN95                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG5030F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1ITEM         8  0H      TEXT('Item Identifier')
     A                                  4  2'Action Code . . . . . . . :'
     A            #PACTC         1      4 32TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Destination Correspondent :'
     A            #1DCOR        10      6 32TEXT('Destination Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Date of Action  . . . . . :'
     A            #1DTAC         5  0H      TEXT('Date of Action')
     A            #KDTAC         6      7 32TEXT('Date of Action')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  76                                  CF08(08 'Spool File')
     A                                      CF12(12 'Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG5030F2'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1ITEM         8  0H      TEXT('Item Identifier')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1MSDT       132   H      TEXT('Action data')
     A            #1CPGM        10   H      TEXT('Calling Process')
     A            #1RPGM        10   H      TEXT('Called Process')
     A            #1SJOB        10   H      TEXT('Spool Job Name')
     A            #1SUSR        10   H      TEXT('Spool Job User')
     A            #1SJNO         6  0H      TEXT('Spool Job Number')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code . . . . . . . :'
     A            #PACTC         1      4 32TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Destination Correspondent :'
     A            #1DCOR        10      6 32TEXT('Destination Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Date of Action  . . . . . :'
     A            #1DTAC         5  0H      TEXT('Date of Action')
     A            #KDTAC         6      7 32TEXT('Date of Action')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Log Information . . . . . :'
     A            #1MSID         7   H      TEXT('Action (Msg id.)')
     A            #1LIN1        78   O 10  2TEXT('Log Info Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1LIN2        78   O 11  2TEXT('Log Info Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1LIN3        78   O 12  2TEXT('Log Info Line 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1LIN4        78   O 13  2TEXT('Log Info Line 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'Spool File Name . . . . . :'
     A N76                                  DSPATR(ND)
     A            #1SPLF        10     15 32TEXT('Spool File Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N76                                  DSPATR(ND)
     A                                 15 43'Number . :'
     A N76                                  DSPATR(ND)
     A            #1SPLN         5  0  15 56TEXT('Spool File Number')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N76                                  DSPATR(ND)
     A                                 17  2'Start Page Number . . . . :'
     A N76                                  DSPATR(ND)
     A            #1SPGE         5  0  17 32TEXT('Start Page Number')
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N76                                  DSPATR(ND)
     A                                 17 38'End Page Number . :'
     A N76                                  DSPATR(ND)
     A            #1EPGE         5  0  17 60TEXT('End Page Number')
     A N76                                  DSPATR(ND)
     A                                      EDTCDE(3)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'Last Change Information'
     A                                      DSPATR(UL HI)
     A                                 20  2'User name.:'
     A            #1AUSR        10     20 14TEXT('User name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 25'Action Date.:'
     A            #1ARDT         7     20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20 47'Action Time.:'
     A            #1ATIM         6  0  20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
