      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG C/M display log records screen')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module                     *
     A*                                                            *
     A*   DSPF/CG5040DF - Display Log records       Display file   *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 176583             Date 08Mar00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 23Nov94               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
     A*    176583 - Add Action time to Subfile report              *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1ITEM         8  0H      TEXT('Item Identifier')
     A            #1DCOR        10   H      TEXT('Destination Correspondent')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1MSID         7   H      TEXT('Action (Msg id.)')
     A            #1MSDT       132   H      TEXT('Action data')
     A            #1CPGM        10   H      TEXT('Calling Process')
     A            #1RPGM        10   H      TEXT('Called Process')
     A            #1SPLF        10   H      TEXT('Spool File Name')
     A            #1SPLN         5  0H      TEXT('Spool File Number')
     A            #1SJOB        10   H      TEXT('Spool Job Name')
     A            #1SUSR        10   H      TEXT('Spool Job User')
     A            #1SJNO         6  0H      TEXT('Spool Job Number')
     A            #1SPGE         5  0H      TEXT('Start Page Number')
     A            #1EPGE         5  0H      TEXT('End Page Number')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1DTAC         5  0H      TEXT('Date of Action')
     A            #1SEL          1   B 10  2
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
      *                                                  Begin /176583/
     A************#SDTAC*********6     10  5TEXT('Date of Action')
     A***************************           DSPATR(HI)
     A************#STEXT********65     10 12TEXT('Text')
     A***************************           DSPATR(HI)
     A            #SDTAC         6     10  4TEXT('Date of Action')
     A                                      DSPATR(HI)
     A            #STIMA         6  0  10 11TEXT('Action Time')
     A                                      DSPATR(HI)
     A            #STEXT        62     10 18TEXT('Text')
     A                                      DSPATR(HI)
      *                                                  End   /176583/
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(11) SFLSIZ(12)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #CDTAC')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG5040F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A            #2ITEM         8  0H      TEXT('Item Identifier')
     A            #2RECI         1   H      TEXT('Record Identifier')
     A            #2MSID         7   H      TEXT('Action (Msg id.)')
     A            #2MSDT       132   H      TEXT('Action data')
     A            #2CPGM        10   H      TEXT('Calling Process')
     A            #2RPGM        10   H      TEXT('Called Process')
     A            #2SPLF        10   H      TEXT('Spool File Name')
     A            #2SPLN         5  0H      TEXT('Spool File Number')
     A            #2SJOB        10   H      TEXT('Spool Job Name')
     A            #2SUSR        10   H      TEXT('Spool Job User')
     A            #2SJNO         6  0H      TEXT('Spool Job Number')
     A            #2SPGE         5  0H      TEXT('Start Page Number')
     A            #2EPGE         5  0H      TEXT('End Page Number')
     A            #2AJOB        10   H      TEXT('Job name')
     A            #2AUSR        10   H      TEXT('User name')
     A            #2ATIM         6  0H      TEXT('Action Time')
     A            #2ARDT         7   H      TEXT('Action Date')
     A            #2AACT         1   H      TEXT('Action Type')
     A            #2RDNB         5  0H      TEXT('Run day number')
     A            #2DTAC         5  0H      TEXT('Date of Action')
     A                                  4  3'Selected on.: Destination Corresp-
     A                                      ondent'
     A            #2DCOR        10      4 43TEXT('Destination Correspondent')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  3'Position on.: Date'
     A            #CDTAC         6   B  6 22TEXT('Date of Action')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A  81                              8  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
      *                                                  Begin /176583/
     A**81***********************       8  5'Action'
     A***************************           DSPATR(HI) COLOR(WHT)
     A**81***********************       9  5' Date '
     A***************************           DSPATR(HI) COLOR(WHT)
     A**81***********************       9 12'Description'
     A***************************           DSPATR(HI) COLOR(WHT)
     A  81                              8  4'Action'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9  4' Date '
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 18'Description'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 11'Action'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 11' Time '
     A                                      DSPATR(HI) COLOR(WHT)
      *                                                  End   /176583/
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
