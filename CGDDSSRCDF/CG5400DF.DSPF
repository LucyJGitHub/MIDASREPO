      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG UDC Print Summary of Corresponden. Items')
      **************************************************************
      *                                                            *
      *  Midas - User Defined Correspondence                       *
      *                                                            *
      *  DSPF/CG5400DF - Print Summary of Correspondence Items     *
      *                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. 089141             DATE 15JUN95            *
      *  PREV AMEND NO. 088008             DATE 18MAY95            *
      *                 086662             DATE 26APR95            *
      *                 S01522             DATE 23NOV94            *
      *                                                            *
      **************************************************************
      *                                                            *
      *  089141 - Resequence help text                             *
      *  088008 - Print last in sequence for date                  *
      *  086662 - Add Correspondent type                           *
      *  S01522 - User Defined Correspondence                      *
      *                                                            *
      **************************************************************
      *
      *---------------------------------------------------------------*
      *  File Level Keywords                                          *
      *---------------------------------------------------------------*
     A                                       INDARA
     A                                       PRINT(MPRINT)
     A                                       CHGINPDFT
     A                                       CA03
     A                                       CA05
     A  79                                   CA10
     A                                       CA12
      *
      *---------------------------------------------------------------*
      *  Main Screen                                                  *
      *---------------------------------------------------------------*
     A          R @5400S1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A*.........................................................................
      *
      **  Screen Header Fields
     A            S#USER        10      1  2TEXT('User ID')
     A                                      DSPATR(HI)
     A            S#PGM         10      1 13TEXT('Program')
     A                                      DSPATR(HI)
     A                                  1 38'Midas'
     A            S#MRDT         7      1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
      *
     A            S#JOB         10      2  2TEXT('JOB Name')
     A                                      DSPATR(HI)
     A            S#TITL        40      2 21TEXT('Title')
     A                                      DSPATR(HI)
     A            S#TIME         6  0   2 72TEXT('Time')
     A                                      DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
      *
      **  Prompt Fields - Selection
     A                                  5  7'Selection Fields:'
     A                                      DSPATR(UL)
      *
     A                                  7  7'Correspondent  . . . . . .'
     A            S#DCOR        10   B  7 37
     A  78                                  DSPATR(PR)
     A  30                                  DSPATR(RI PC)
     A N78
     AO 30                                  DSPATR(UL)
     A N30                                  DSPATR(HI)
      *
      * 086662 Start
     A                                  7 48'or type'
     A            S#CTYP        10   B  7 56
     A  78                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N78
     AO 47                                  DSPATR(UL)
     A N47                                  DSPATR(HI)
      *
      * 086662 End
     A                                  8  7'Primary Only . . . . . . .'
     A            S#PRIM         1   B  8 37
     A  78                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N78
     AO 31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A                                  8 40'(Y/N)'
      *
     A                                  9  7'Print Type/Sub-Type  . . .'
     A            S#PTYP        10   B  9 37
     A  78                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N78
     AO 32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
      *
     A            S#PSTP        10   B  9 49
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N78
     AO 33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
      *
     A                                 10  7'From Scheduled Date  . . .'
     A            S#FRDT         6   B 10 37
     A  78                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N78
     AO 34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
      *
     A                                 11  7'To Scheduled Date  . . . .'
     A            S#TODT         6   B 11 37
     A  78                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N78
     AO 35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
      *
     A                                 12  7'Language ID  . . . . . . .'
     A            S#LGID         2   B 12 37
     A  78                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N78
     AO 36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
      *
      **  5 Status fields are allowed incase of future requirements:
      **  currently only 2 are allowed.
     A                                 13  7'Statuses . . . . . . . . .'
     A            S#STS1         4   B 13 37
     A  78                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N78
     AO 37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
      *
     A            S#STS2         4   B 13 42
     A  78                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N78
     AO 38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
      *
     A            S#STS3         4   B 13 47
     A  78                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N78
     AO 39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
      *
     A            S#STS4         4   B 13 52
     A  78                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N78
     AO 40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
      *
     A            S#STS5         4   B 13 57
     A  78                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N78
     AO 41                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
      *
     A                                 14  7'Auto-Transmission  . . . .'
     A            S#ATRM         1   B 14 37
     A  78                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A N78
     AO 43                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
      *
     A                                 16  7'Summary Only . . . . . . .'
     A            S#SUMM         1   B 16 37
     A  78                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N78
     AO 46                                  DSPATR(UL)
     A N46                                  DSPATR(HI)
     A                                 16 40'(Y/N)'
      *
     A                                 18  7'Sequence Field:'
     A                                      DSPATR(UL)
      *
     A*                                19  7'Order to Print . . . . . .'
     A*           S#ACCP         1   B 19 37
     A* 78                                  DSPATR(PR)
     A* 42                                  DSPATR(RI PC)
     A*N78
     A*O 42                                 DSPATR(UL)
     A*N42                                  DSPATR(HI)
     A*                                19 40 '(L - Layout/C - Correspondent)'
      *
     A                                 20  7'Current or Archive . . . .'
     A            S#ARCH         1   B 20 37
     A  78                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N78
     AO 45                                  DSPATR(UL)
     A N45                                  DSPATR(HI)
      *  Start 088008
     A                                 21  7'Print last change confrm .'
     A            S#PLST         1   B 21 37
     A  78                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N78
     AO 48                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
      *  End 088008
      *
      *
      *---------------------------------------------------------------*
      *  Footer Format                                                *
      *---------------------------------------------------------------*
     A          R @5400S9                   OVERLAY
     A            S#CMDT        79     23  2
      *
      *---------------------------------------------------------------*
      *  Message Subfile Record Format                                *
      *---------------------------------------------------------------*
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      *---------------------------------------------------------------*
      *  Message Subfile Control Record Format                        *
      *---------------------------------------------------------------*
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
