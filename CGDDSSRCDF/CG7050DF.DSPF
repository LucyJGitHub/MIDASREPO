      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Audit Letters Selection Maintenance Display')          *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  DSPF/CG7050DF - Audit Letters Selection Maintenance Display  *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23250A          Date 27Mar09               *
      *                 BUG23250           Date 19Mar09               *
      *                 CER041  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23250A - Show Zero Confirmation on Display in Edit Mode   *
      *  BUG23250 - Show Zero Confirmation on Display                 *
      *  CER041 - Audit Letters Correspondence                        *
      *           (Upgrade of FGE132 and FGE171)                      *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      ** Start of Change BUG23250
     A**********                            DSPSIZ(24 80 *DS3)                              BUG23250
     A                                      DSPSIZ(27 132)
      ** End of Change BUG23250
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'Add a new record')
     A                                      CA10(10 'Delete all Selection')
     A N70                                  ROLLUP(25)
     A                                      VLDCMDKEY(26 'on, if +
     A                                      command key pressed not +
     A                                      ENTER-key')
     A                                      PRINT
      *
     A          R SELSFL                    SFL
     A  74                                  SFLNXTCHG
     A            #CNFR          6A  O  9 13TEXT('Customer no. FROM')
     A            #CNTO          6A  O  9 22TEXT('Customer no. TO')
     A            #BRCH          3A  O  9 35TEXT('Branch')
     A            #MODLE         1A  O  9 46TEXT('Module LE')
     A            #MODRE         1A  O  9 49TEXT('Module RE')
     A            #MODMM         1A  O  9 52TEXT('Module MM')
     A            #MODFX         1A  O  9 55TEXT('Module FX')
     A            SMODFR         1A  O  9 58TEXT('Module FR')
     A            SMODIR         1A  O  9 61TEXT('Module IR')
     A            SMODSE         1A  O  9 64TEXT('Module SE')
     A            SMODFF         1A  O  9 67TEXT('Module FF')
     A            #MODAL         1A  O  9 70TEXT('Module ALL')
      ** Start of Change BUG23250
      ** Start of Change BUG23250A
     A**********  SZEROC         1A  O  9 79TEXT('Zero Conf')                              BUG23250A
     A            SZEROC         1A  B  9 79TEXT('Zero Conf')
     A  23                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
      ** End of Change BUG23250A
     A**********  FDACT          1A  I  9  5TEXT('Select Action')
     A            FDACT          1A  B  9  5TEXT('Select Action')
      ** End of Change BUG23250
     A  22                                  DSPATR(PC)
     A  22                                  DSPATR(RI)
      *
     A          R SELCTL                    SFLCTL(SELSFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  73                                  SFLMSG('No records found.')
     A                                      OVERLAY
     A N69                                  ERASE(SELSFL)
     A  69                                  SFLDSP
     A  71                                  SFLDSPCTL
     A N71                                  SFLCLR
     A  70                                  SFLEND(*MORE)
     A            RRN            4S 0H      SFLRCDNBR
     A                                  1  3'CG7050DF'
     A                                      COLOR(WHT)
      ** Start of Change BUG23250
     A**********                        1 33'Audit Letters'
     A                                  1 55'Audit Letters'
      ** End of Change BUG23250
     A                                      COLOR(WHT)
      ** Start of Change BUG23250
     A**********  #RUNDT         7A     1 72COLOR(WHT)
     A            #RUNDT         7A     1124DSPATR(HI)
      ** End of Change BUG23250
      ** Start of Change BUG23250
     A**********                        2 27'Selection to be processed'
     A                                  2 48'Selection to be processed'
      ** End of Change BUG23250
     A                                      COLOR(WHT)
     A                                  6 13'Customer number'
     A                                      COLOR(WHT)
     A                                  6 34'Branch '
     A                                      COLOR(WHT)
     A                                  6 46'Modules'
     A                                      COLOR(WHT)
     A                                  7 13' From      To '
     A                                      COLOR(WHT)
     A                                  7 46'LE RE MM FX +
     A                                       FR IR SE FF +
     A                                       ALL'
     A                                      COLOR(WHT)
      ** Start of Change BUG23250
     A                                  6 79'Zero'
     A                                      DSPATR(HI)
     A                                  7 79'Confirmation'
     A                                      DSPATR(HI)
      ** End of Change BUG23250
      *
     A          R USRENT
     A                                      OVERLAY
      ** Start of Change BUG23250
     A**********                       20  4'Next processing date:'                         BUG23250
     A                                 21  4'Next processing date:'
      ** End of Change BUG23250
     A                                      COLOR(WHT)
      ** Start of Change BUG23250
     A**********  #PDATE         6Y 0B 20 29                                                BUG23250
     A            #PDATE         6Y 0B 21 29
      ** End of Change BUG23250
     A  20                                  DSPATR(RI PC)
      *
     A          R FNKEYS
     A                                      OVERLAY
      ** Start of Change BUG23250
     A**********                       23  4'F3=Exit'                                       BUG23250
     A                                 26  4'F3=Exit'
      ** End of Change BUG23250
     A                                      COLOR(BLU)
      ** Start of Change BUG23250
     A**********                       23 13'F9=Add        '                                BUG23250
     A                                 26 13'F9=Add        '
      ** End of Change BUG23250
     A                                      COLOR(BLU)
      ** Start of Change BUG23250
     A**********                       23 30'F10=Delete all Selections'                     BUG23250
     A                                 26 30'F10=Delete all Selections'
      ** End of Change BUG23250
     A                                      COLOR(BLU)
      *****************************************************************
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *****************************************************************
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  30
     AON30                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A
     A          R FOOTER
      *
     A                                      OVERLAY
      ** Start of Change BUG23250
     A**********                       21  4'A=Amend  '                                     BUG23250
     A                                 23  4'A=Amend  '
      ** End of Change BUG23250
     A                                      COLOR(BLU)
      ** Start of Change BUG23250
     A**********                       21 18'D=Delete  '                                    BUG23250
     A                                 23 18'D=Delete  '
      ** End of Change BUG23250
     A                                      COLOR(BLU)
