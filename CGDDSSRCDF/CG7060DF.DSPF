      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Audit Letters Detail Selection Maintenance Display')   *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence
      *                                                               *
      *  DSPF/CG7060DF - Audit Letters Detail Selection Maintenance   *
      *                  Display                                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER041  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER041 - Audit Letters Correspondence                        *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Prev.')
     A                                      VLDCMDKEY(26 'on, if +
     A                                      command key pressed not +
     A                                      ENTER-key')
     A                                      PRINT
     A                                      HELP
      *
     A          R CGAUDLHF
     A                                  1  3'CG7060DF'
     A                                      COLOR(WHT)
     A                                  1 33'Audit Letters'
     A                                      COLOR(WHT)
     A            SRUNDT         7A     1 72COLOR(WHT)
      *
     A          R CGAUDLDF
     A  90                                  CA10(10 'Confirm Deletion')
     A                                      OVERLAY
     A                                  2 31'Delete Selection'
     A                                      COLOR(WHT)
     A                                  6  6'Customer number:'
     A                                      COLOR(WHT)
     A                                  6 41'Range to:'
     A                                      COLOR(WHT)
     A                                  8  6'Branch code:'
     A                                      COLOR(WHT)
     A                                 10  6'Module(s):'
     A                                      COLOR(WHT)
     A                                 10 20'LE :'
     A                                      COLOR(WHT)
     A                                 11 20'RE :'
     A                                      COLOR(WHT)
     A                                 12 20'MM :'
     A                                      COLOR(WHT)
     A                                 13 20'FX :'
     A                                      COLOR(WHT)
     A                                 14 20'FR :'
     A                                      COLOR(WHT)
     A                                 15 20'IR :'
     A                                      COLOR(WHT)
     A                                 16 20'SE :'
     A                                      COLOR(WHT)
     A                                 17 20'FF :'
     A                                      COLOR(WHT)
     A                                 18 20'ALL:'
     A                                      COLOR(WHT)
     A            #CNFR          6A  O  6 24TEXT('Customer number +
     A                                            FROM')
     A            #CNTO          6A  O  6 52TEXT('Customer number TO')
     A            #BRCH          3A  O  8 20TEXT('Branch code')
     A            #MODLE         1A  O 10 27TEXT('Module LE')
     A            #MODRE         1A  O 11 27TEXT('Module RE')
     A            #MODMM         1A  O 12 27TEXT('Module MM')
     A            #MODFX         1A  O 13 27TEXT('Module FX')
     A            SMODFR         1A  O 14 27TEXT('Module FR')
     A            SMODIR         1A  O 15 27TEXT('Module IR')
     A            SMODSE         1A  O 16 27TEXT('Module SE')
     A            SMODFF         1A  O 17 27TEXT('Module FF')
     A            #MODAL         1A  O 18 27TEXT('Module ALL')
      *
     A          R CGAUDLAF
     A                                      OVERLAY
     A                                  2 32'Amend Selection'
     A                                      COLOR(WHT)
     A                                  6  6'Customer number:'
     A                                      COLOR(WHT)
     A                                  6 41'Range to:'
     A                                      COLOR(WHT)
     A                                  8  6'Branch code (or ALL):'
     A                                      COLOR(WHT)
     A                                 10  6'Module(s):'
     A                                      COLOR(WHT)
     A                                 10 20'LE :'
     A                                      COLOR(WHT)
     A                                 11 20'RE :'
     A                                      COLOR(WHT)
     A                                 12 20'MM :'
     A                                      COLOR(WHT)
     A                                 13 20'FX :'
     A                                      COLOR(WHT)
     A                                 14 20'FR :'
     A                                      COLOR(WHT)
     A                                 15 20'IR :'
     A                                      COLOR(WHT)
     A                                 16 20'SE :'
     A                                      COLOR(WHT)
     A                                 17 20'FF :'
     A                                      COLOR(WHT)
     A                                 18 20'ALL:'
     A                                      COLOR(WHT)
     A            #CNFR          6A  O  6 24TEXT('Customer number FROM')
     A  21
     AO 25                                  DSPATR(PC)
     A  21
     AO 25                                  DSPATR(RI)
     A            #CNTO          6A  B  6 52TEXT('Customer number TO')
     A  21
     AO 22                                  DSPATR(PC)
     A  21
     AO 22                                  DSPATR(RI)
     A            #BRCH          3A  B  8 30TEXT('Branch code')
     A  23                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A            #MODLE         1A  B 10 27TEXT('Module LE')
     A  24
     AO 30                                  DSPATR(PC)
     A  24
     AO 30                                  DSPATR(RI)
     A            #MODRE         1A  B 11 27TEXT('Module RE')
     A  24
     AO 31                                  DSPATR(PC)
     A  24
     AO 31                                  DSPATR(RI)
     A            #MODMM         1A  B 12 27TEXT('Module MM')
     A  24
     AO 32                                  DSPATR(PC)
     A  24
     AO 32                                  DSPATR(RI)
     A            #MODFX         1A  B 13 27TEXT('Module FX')
     A  24
     AO 33                                  DSPATR(PC)
     A  24
     AO 33                                  DSPATR(RI)
     A            SMODFR         1A  B 14 27TEXT('Module FR')
     A  24
     AO 35                                  DSPATR(PC)
     A  24
     AO 35                                  DSPATR(RI)
     A            SMODIR         1A  B 15 27TEXT('Module IR')
     A  24
     AO 36                                  DSPATR(PC)
     A  24
     AO 36                                  DSPATR(RI)
     A            SMODSE         1A  B 16 27TEXT('Module SE')
     A  24
     AO 37                                  DSPATR(PC)
     A  24
     AO 37                                  DSPATR(RI)
     A            SMODFF         1A  B 17 27TEXT('Module FF')
     A  24
     AO 38                                  DSPATR(PC)
     A  24
     AO 38                                  DSPATR(RI)
     A            #MODAL         1A  B 18 27TEXT('Module ALL')
     A  24
     AO 34                                  DSPATR(PC)
     A  24
     AO 34                                  DSPATR(RI)
      *
     A          R CGAUDLEF
     A                                      OVERLAY
     A                                  2 30'Enter New Selection'
     A                                      COLOR(WHT)
     A                                  6  6'Customer number:'
     A                                      COLOR(WHT)
     A                                  6 41'Range to:'
     A                                      COLOR(WHT)
     A                                  8  6'Branch code (or ALL):'
     A                                      COLOR(WHT)
     A                                 10  6'Module(s):'
     A                                      COLOR(WHT)
     A                                 10 20'LE :'
     A                                      COLOR(WHT)
     A                                 11 20'RE :'
     A                                      COLOR(WHT)
     A                                 12 20'MM :'
     A                                      COLOR(WHT)
     A                                 13 20'FX :'
     A                                      COLOR(WHT)
     A                                 14 20'FR :'
     A                                      COLOR(WHT)
     A                                 15 20'IR :'
     A                                      COLOR(WHT)
     A                                 16 20'SE :'
     A                                      COLOR(WHT)
     A                                 17 20'FF :'
     A                                      COLOR(WHT)
     A                                 18 20'ALL:'
     A                                      COLOR(WHT)
     A            #CNFR          6A  B  6 24TEXT('Customer number FROM')
     A  21
     AO 25                                  DSPATR(PC)
     A  21
     AO 25                                  DSPATR(RI)
     A            #CNTO          6A  B  6 52TEXT('Customer number TO')
     A  21
     AO 22                                  DSPATR(PC)
     A  21
     AO 22                                  DSPATR(RI)
     A            #BRCH          3A  B  8 30TEXT('Branch code')
     A  23                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A            #MODLE         1A  B 10 27TEXT('Module LE')
     A  24
     AO 30                                  DSPATR(PC)
     A  24
     AO 30                                  DSPATR(RI)
     A            #MODRE         1A  B 11 27TEXT('Module RE')
     A  24
     AO 31                                  DSPATR(PC)
     A  24
     AO 31                                  DSPATR(RI)
     A            #MODMM         1A  B 12 27TEXT('Module MM')
     A  24
     AO 32                                  DSPATR(PC)
     A  24
     AO 32                                  DSPATR(RI)
     A            #MODFX         1A  B 13 27TEXT('Module FX')
     A  24
     AO 33                                  DSPATR(PC)
     A  24
     AO 33                                  DSPATR(RI)
     A            SMODFR         1A  B 14 27TEXT('Module FR')
     A  24
     AO 35                                  DSPATR(PC)
     A  24
     AO 35                                  DSPATR(RI)
     A            SMODIR         1A  B 15 27TEXT('Module IR')
     A  24
     AO 36                                  DSPATR(PC)
     A  24
     AO 36                                  DSPATR(RI)
     A            SMODSE         1A  B 16 27TEXT('Module SE')
     A  24
     AO 37                                  DSPATR(PC)
     A  24
     AO 37                                  DSPATR(RI)
     A            SMODFF         1A  B 17 27TEXT('Module FF')
     A  24
     AO 38                                  DSPATR(PC)
     A  24
     AO 38                                  DSPATR(RI)
     A            #MODAL         1A  B 18 27TEXT('Module ALL')
     A  24
     AO 34                                  DSPATR(PC)
     A  24
     AO 34                                  DSPATR(RI)
      *
     A          R FNKEYS
     A                                      OVERLAY
     A                                 22  6'F3=Exit'
     A                                      COLOR(BLU)
     A  90                             22 19'F10=Confirm Delete'
     A                                      COLOR(BLU)
     A                                 22 44'F12=Previous Screen'
     A                                      COLOR(BLU)
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  30
     AON30                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A
      *
