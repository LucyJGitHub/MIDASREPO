      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Historic Interest Scale Report Maintenance')     *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing/ Lending Modules                             *
      *                                                               *
      *  CG7150DF - Historic Interest Scale Report Print Request      *
      *                                                               *
      *  (C) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER042  *CREATE    Date 11May11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER042 - Interest Scale Report Correspondence                *
      *           (Upgrade of FGE178/179)                             *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      HELP
     A                                      CA03(03 'Exit')
     A          R CGC7150H1                 TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2011')
     A            D0USER        10   O  1  2
     A            D0PGMN        10A     1 13
     A                                  1 26'Historic Interest Scale Report Req-
     A                                      uest'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            D0RUNA         7   O  1 73
     A            D0WSID        10   O  2  2
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R CG7150S1                  SFL
     A  68
     AO 69                                  SFLNXTCHG
     A            DSOPTN         1A  B  6  2DSPATR(UL)
     A  67
     AO 68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            DSACCT        24A  O  6  5DSPATR(HI)
     A            DSFMTH         6A  O  6 33DSPATR(HI)
     A            DSLMTH         6A  O  6 47DSPATR(HI)
      *
     A          R CG7150S0                  SFLCTL(CG7150S1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A                                      CA09(09)
     A N89                                  PAGEDOWN(25)
     A  40                                  SFLCLR
     A N41                                  ERASE(CG7150S1)
     A                                      SFLDSPCTL
     A  41                                  SFLDSP
     A  89                                  SFLEND
     A            DFRNBR         4S 0H      SFLRCDNBR(CURSOR)
     A                                  4 12'Account'
     A                                  4 31'First Month'
     A                                  4 46'Last Month'
     A            DFACTS        24A  I  5  5DSPATR(HI)
     A            DFFMTS         6A  I  5 33DSPATR(HI)
     A            DFLMTS         6A  I  5 47DSPATR(HI)
      *
     A          R CG7150F0
     A                                      OVERLAY
     A  73                                  CF06(06)
     A  73                                  CA12(12)
     A N73                                  CA09(09)
     A                                  5  4'Account ........:'
     A            D1ACCT        24A  B  5 22
     A  66
     AO 70                                  DSPATR(PC)
     A N73                                  DSPATR(UL)
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A  73                                  DSPATR(PR)
     A            D1ACTN        20   O  5 48DSPATR(HI)
     A                                  7  4'First Month ....:'
     A            D1FDTE         6A  B  7 22
     A N73                                  DSPATR(UL)
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  73                                  DSPATR(PR)
     A            D1FDTN        13   O  7 48DSPATR(HI)
     A                                  9  4'Last Month .....:'
     A            D1LDTE         6A  B  9 22
     A N73                                  DSPATR(UL)
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A  73                                  DSPATR(PR)
     A            D1LDTN        13A  O  9 48DSPATR(HI)
     A                                 23  2'F3=Exit'
     A  73                             23 11'F6=Confirm Delete  F12=Previous'
     A N73                             23 11'F9=Go to "DISPLAY" Mode'
      *
     A          R CG7150F1
     A                                      OVERLAY
     A                                 23  2'D=Delete  F3=Exit  F9=Go to "ADD" -
     A                                      Mode  Rollup  Rolldown'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(25)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
