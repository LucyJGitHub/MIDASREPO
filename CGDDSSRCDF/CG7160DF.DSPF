      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL/LE Interest Scale Qualifying Act code Mnt')   *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing/ Lending Modules                             *
      *                                                               *
      *  CG7160DF - DL/LE Interest Scale Qualifying Account Codes     *
      *                  Maintenance                                  *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CER042  *CREATE    Date 11May11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER042 - Interest Scale Report Correspondence ; added        *
      *           new field                                           *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      HELP
     A                                      CA03(03 'Exit')
     A          R CG7160H1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2011')
     A            D0USER        10A  O  1  2
     A            D0PGMN        10A  O  1 13
     A                                  1 25'Dealing/Lending Interest Scale Rep-
     A                                      ort'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            D0RUNA         7A  O  1 73
     A            D0WSID        10A  O  2  2
     A                                  2 29'Qualifying Account Codes'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R CG7160S1                  SFL
     A  68
     AO 69                                  SFLNXTCHG
     A            DSOPTN         1A  B  7  2DSPATR(UL)
     A  67
     AO 68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            DSDLTP         2A  O  7  8DSPATR(HI)
     A            DSDLST         2A  O  7 18DSPATR(HI)
     A            DSACOD        10S 0O  7 40DSPATR(HI)
     A            DSDESC        25A  O  7 54DSPATR(HI)
     A            DSTRCL         4A  O  7 29DSPATR(HI)
     A N80                                  DSPATR(ND)
      *
     A          R CG7160S0                  SFLCTL(CG7160S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      CA09(09)
     A N89                                  PAGEDOWN(25)
     A  41                                  SFLDSP
     A                                      SFLDSPCTL
     A  40                                  SFLCLR
     A  89                                  SFLEND(*MORE)
     A            DFRNBR         4S 0H      SFLRCDNBR(CURSOR)
     A                                  4  5'Deal/Loan'
     A                                  4 16'Deal/Loan'
     A                                  4 40'Account'
     A                                  5  7'Type'
     A                                  5 16'Sub Type'
     A                                  5 40'Code'
     A                                  5 57'Description'
     A            DFDLTS         2A  I  6  8DSPATR(HI)
     A            DFDLSS         2A  I  6 18DSPATR(HI)
     A            DFACOS        10A  I  6 40DSPATR(HI)
     A  80        DFTRCL         4A  I  6 29DSPATR(HI)
     A N80                                  DSPATR(ND)
     A  80                              4 27'Transaction'
     A  80                              5 29'Class'
      ** Delete Screen
     A          R CG7160F1
     A                                      OVERLAY
     A                                      CF06(06)
     A                                      CA12(12)
     A                                  5  2'Deal/Loan Type/Sub-Type ..:'
     A            D1DLTP         2A  O  5 30DSPATR(HI)
     A            D1DLST         2A  O  5 33DSPATR(HI)
     A            D1DES1        25A  O  5 41DSPATR(HI)
     A                                  9  2'Account Code .............:'
     A            D1ACOD        10S 0O  9 30DSPATR(HI)
     A            D1DES2        25A  O  9 41DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F6=Confirm Delete  F12=Previous'
     A  80                              7  2'Transaction Class ........:'
     A            D1TRCL         4A  O  7 30DSPATR(HI)
     A N80                                  DSPATR(ND)
      **Add Screen
     A          R CG7160F2
     A                                      OVERLAY
     A                                      CF06(06)
     A                                      CA09(09)
     A                                  5  2'Deal/Loan Type/Sub-Type ..:'
     A            D2DLTP         2A  B  5 30DSPATR(UL)
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            D2DLST         2A  B  5 34DSPATR(UL)
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A            D2DES1        25A  O  5 42DSPATR(HI)
     A                                  9  2'Account Code .............:'
     A            D2ACOD        10A  B  9 30DSPATR(UL)
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A            D2DES2        25A  O  9 42DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F9=Go to "DISPLAY" Mode'
     A  80                              7  2'Transaction Class ........:'
     A            D2TRCL         4A  B  7 30DSPATR(UL)
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A N80                                  DSPATR(ND)
     A N80                                  DSPATR(PR)
      ** Enquiry Screen
     A          R CG7160F3
     A                                      OVERLAY
     A                                      CA12(12)
     A                                  5  2'Deal/Loan Type/Sub-Type ..:'
     A            D3DLTP         2A  O  5 30DSPATR(HI)
     A            D3DLST         2A  O  5 33DSPATR(HI)
     A            D3DES1        25A  O  5 42DSPATR(HI)
     A  80                              7  2'Transaction Class ........:'
     A            D3ACOD        10S 0O  9 30DSPATR(HI)
     A            D3DES2        25A  O  9 42DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A                                  9  2'Account Code .............:'
     A            D3TRCL         4A  O  7 30DSPATR(HI)
     A N80                                  DSPATR(ND)
      ** Amend Screen
     A          R CG7160F4
     A                                      OVERLAY
     A                                      CF06(06)
     A                                      CA12(12)
     A                                  5  2'Deal/Loan Type/Sub-Type ..:'
     A            D4DLTP         2A  B  5 30DSPATR(UL)
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            D4DLST         2A  B  5 33DSPATR(UL)
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A            D4DES1        25A  O  5 42DSPATR(HI)
     A                                  9  2'Account Code .............:'
     A            D4ACOD        10A  B  9 30DSPATR(UL)
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A            D4DES2        25A  O  9 42DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F6=Confirm Amend  F12=Previous'
     A  80                              7  2'Transaction Class ........:'
     A            D4TRCL         4A  B  7 30DSPATR(UL)
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A N80                                  DSPATR(ND)
     A N80                                  DSPATR(PR)
      *
     A          R CG7160F5
     A                                      OVERLAY
     A                                 23  2'D=Delete  E=Enquiry  A=Amend  F3=E-
     A                                      xit  F9=Go to "ADD" Mode  Rollup  R-
     A                                      olldown'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(25)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
