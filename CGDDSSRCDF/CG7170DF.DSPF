      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Retail Interest Scale Qualifyng Acct Cod Mnt')   *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing/ Lending Modules                             *
      *                                                               *
      *  CG7170DF - Retail Interest Scale Qualifying Account Codes    *
      *                  Maintenance                                  *
      *                                                               *
      *  (C) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER042  *CREATE    Date 11May11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER042 - Interest Scale Report Correspondence ; added        *
      *           new field                                           *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      HELP
     A                                      CA03(03 'Exit')
     A          R CGC7170H1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2011')
     A            D0USER        10A  O  1  2
     A            D0PGMN        10A  O  1 13
     A                                  1 27'Retail Interest Scale Report'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            D0RUNA         7A  O  1 73
     A            D0WSID        10A  O  2  2
     A                                  2 29'Qualifying Account Codes'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R CG7170S1                  SFL
     A  68
     AO 69                                  SFLNXTCHG
     A            DSOPTN         1A  B  7  2DSPATR(UL)
     A  67
     AO 68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            DSACOD        10  0O  7  6DSPATR(HI)
     A            DSDESC        25A  O  7 20DSPATR(HI)
      *
     A          R CG7170S0                  SFLCTL(CG7170S1)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      CA09(09)
     A N89                                  PAGEDOWN(25)
     A  40                                  SFLCLR
     A N41                                  ERASE(CG7170S1)
     A                                      SFLDSPCTL
     A  41                                  SFLDSP
     A  89                                  SFLEND(*MORE)
     A            DFRNBR         4S 0H      SFLRCDNBR(CURSOR)
     A                                  4  7'Account'
     A                                  5  8'Code'
     A                                  5 20'Description'
     A            DFACOS        10A  I  6  6DSPATR(HI)
      * Delete screen
     A          R CG7170F1
     A                                      OVERLAY
     A                                      CF06(06)
     A                                      CA12(12)
     A                                  5  2'Account Code ...:'
     A            D1ACOD        10S 0O  5 20DSPATR(HI)
     A            D1DESC        25A  O  5 33DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F6=Confirm Delete  F12=Previous'
      * Add screen
     A          R CG7170F2
     A                                      OVERLAY
     A                                      CF06(06)
     A                                      CA09(09)
     A                                  5  2'Account Code ...:'
     A            D2ACOD        10A  B  5 20DSPATR(PC)
     A                                      DSPATR(UL)
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A            D2DESC        25A  O  5 34DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F9=Go to "DISPLAY" Mode'
      * Enquiry screen
     A          R CG7170F3
     A                                      OVERLAY
     A                                      CA12(12)
     A                                  5  2'Account Code ...:'
     A            D3ACOD        10S 0O  5 20DSPATR(HI)
     A            D3DESC        25A  O  5 34DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
      * Amend screen
     A          R CG7170F4
     A                                      OVERLAY
     A                                      CF06(06)
     A                                      CA12(12)
     A                                  5  2'Account Code ...:'
     A            D4ACOD        10A  B  5 20DSPATR(PC)
     A                                      DSPATR(UL)
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(RI)
     A            D4DESC        25A  O  5 34DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F6=Confirm Amend  F12=Previous'
      *
     A          R CG7170F5
     A                                      OVERLAY
     A                                 23  2'D=Delete  E=Enquiry  A=Amend  F3=E-
     A                                      xit  F9=Go to "ADD" Mode  Rollup  R-
     A                                      olldown'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(25)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
