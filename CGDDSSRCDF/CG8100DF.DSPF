      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Plane element selection display.')
     A*****************************************************************
     A*                                                               *
     A*   Midas - - - Message Management Module                   *
     A*                                                               *
     A*   DSPF/CG8100DF - Plane element selection display             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 095463             Date 06Nov95               *
      *                 S01522             Date 10Feb95               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*    095463 - Allow 132 characters screens                      *
     A*    S01522 - User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
      * Start 095463
     A**************                        DSPSIZ(24 80 *DS3)
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
      * End 095463
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=====================================================================
     A          R #SFLRCD                   SFL
     A*
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            #1SEQ          3A  B  9  2TEXT('Sequence')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A            #1PLAN        10A  O  9  7TEXT('Plane name.')
     A            #1DESC        30A  O  9 18TEXT('Description.')
     A            #1DCOL         3S 0H      TEXT('Plane Column')
     A            #1PPLA        10A  H      TEXT('Previous Plane')
     A            #1SEQA         3A  H      TEXT('Association Seq')
     A*.....................................................................
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A*
     A                                      CHGINPDFT(UL)
     A                                      SFLPAG(4) SFLSIZ(5)
     A                                      WINDOW(#WINDOW)
     A                                      TEXT('Subfile control.')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A                                      CF07(07 'Delete Plane.')
     A                                      CF08(08 'Conditions.')
     A                                      CF09(09 'Details.')
     A                                      CF18(18 'Review.')
     A                                      VLDCMDKEY(29 'Valid command key.')
     A                                      INDTXT(80 'Clear subfile.')
     A                                      INDTXT(81 'Display control.')
     A                                      INDTXT(82 'Display subfile.')
     A                                      INDTXT(83 'End of subfile.')
     A  80                                  SFLCLR
     A  81                                  SFLDSPCTL
     A  82                                  SFLDSP
     A  82                                  SFLEND
     A*.........................................................................
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            #TEXT         70A  O  1  2TEXT('Instruction text line.')
     A                                      DSPATR(HI)
     A            #PNAME        10A  O  2  2
     A            #PDESC        30A  B  2 13CHECK(LC)
     A            #TYPE          4A  O  2 44
     A            #TDESC        26A  O  2 49
     A            #TSTRR        15A  O  3  2
     A            #STRRO         3Y 0B  3 18TEXT('Start Row')
     A                                      EDTCDE(4)
     A            #TFROW        15A  O  3 23
     A N46                                  DSPATR(ND)
     A            #FAVRO         3Y 0B  3 39TEXT('First avail Row')
     A                                      EDTCDE(4)
     A N46                                  DSPATR(ND)
     A N46                                  DSPATR(PR)
     A            #NFPAB        17A  O  3 44TEXT('Page at EOD')
     A N42                                  DSPATR(ND)
     A            #PFPAB         1A  B  3 62
     A N42                                  DSPATR(ND)
     A N42                                  DSPATR(PR)
     A            #TSTRC        15A  O  4  2
     A            #STRCO         3Y 0B  4 18TEXT('Start Col')
     A                                      EDTCDE(4)
     A            #TFCOL        15A  O  4 23
     A N46                                  DSPATR(ND)
     A            #FAVCO         3Y 0B  4 39TEXT('First avail Col')
     A                                      EDTCDE(4)
     A N46                                  DSPATR(ND)
     A N46                                  DSPATR(PR)
     A            #NBOXD        17A  O  4 44TEXT('Relative Fields')
     A N43                                  DSPATR(ND)
     A            #PBOXD         1A  B  4 62
     A N43                                  DSPATR(ND)
     A N43                                  DSPATR(PR)
     A            #TENDR        15A  O  5  2
     A            #ENDRO         3Y 0B  5 18TEXT('End Row')
     A                                      EDTCDE(4)
     A            #TOVRR        15A  O  5 23
     A N46                                  DSPATR(ND)
     A            #OVRRO         3Y 0B  5 39TEXT('Overflow Row')
     A                                      EDTCDE(4)
     A N46                                  DSPATR(ND)
     A N46                                  DSPATR(PR)
     A            #NOFOP        17A  O  5 44TEXT('Overflow labels')
     A N44                                  DSPATR(ND)
     A            #POFOP         1A  B  5 62
     A N44                                  DSPATR(ND)
     A N44                                  DSPATR(PR)
     A            #TENDC        15A  O  6  2
     A            #ENDCO         3Y 0B  6 18TEXT('End Col')
     A                                      EDTCDE(4)
     A            #TOVRC        15A  O  6 23
     A N46                                  DSPATR(ND)
     A            #OVRCO         3Y 0B  6 39TEXT('Overflow Col')
     A                                      EDTCDE(4)
     A N46                                  DSPATR(ND)
     A N46                                  DSPATR(PR)
     A            #NFILS        17A  O  6 44TEXT('Fills Strategy')
     A N45                                  DSPATR(ND)
     A            #PFILS         3A  B  6 62
     A N45                                  DSPATR(ND)
     A N45                                  DSPATR(PR)
     A            #TRELR        15A  O  7  2
     A            #RELRO         3Y 0B  7 18TEXT('Relative Row')
     A                                      EDTCDE(4)
     A            #TATTN        17A  O  7 44
     A N41                                  DSPATR(ND)
     A            #PLANE        10A  B  7 62TEXT('Attach Plane')
     A                                      CHECK(LC)
     A N41                                  DSPATR(ND)
     A N41                                  DSPATR(PR)
     A            #TRELC        15A  O  8  2
     A            #RELCO         3Y 0B  8 18TEXT('Relative Col')
     A                                      EDTCDE(4)
     A            #TATTT         4A  O  8 44
     A N47                                  DSPATR(ND)
     A            #PTYPE         1A  B  8 49TEXT('Attach Type')
     A N47                                  DSPATR(ND)
     A N47                                  DSPATR(PR)
     A            #NTYPE        25A  O  8 51
     A N47                                  DSPATR(ND)
     A*---------------------------------------------------------------------
     A          R #WINDOW                    TEXT('Window definition.')
     A*
     A                                       WINDOW(&SLNO  2 16 75)
     A**** These lines may be                WDWBORDER((*COLOR BLU)   +
     A****  reinstated later.                          (DSPATR HI RI) +
     A****                                             (*CHAR '        '))
     A            SLNO           3S 0P       TEXT('Starting line number.')
     A*---------------------------------------------------------------------
     A          R #CMDTXT
     A*
     A                                      WINDOW(#WINDOW)
     A                                      TEXT('Command keys.')
     A                                      OVERLAY
     A                                      PUTOVR
     A            ##CMD1        70A  O 13  2TEXT('Command text line 1.')
     A                                      DSPATR(HI)
     A            ##CMD2        70A  O 14  2TEXT('Command text line 2.')
     A                                      DSPATR(HI)
     A*=====================================================================
     A          R DUMMY                      TEXT('Dummy format.')
     A*
     A                                       OVERLAY PUTOVR ASSUME
     A            DUMMYF         1A  O  1 80 DSPATR(ND)
     A*=====================================================================
     A          R #MSGRCD                    TEXT('Program messages.')
     A*
     A                                       SFL SFLMSGRCD(15)
     A            MSGKEY                     SFLMSGKEY
     A            ##PGM                      SFLPGMQ
     A*.....................................................................
     A          R #MSGCTL                    TEXT('Program messages.')
     A*
     A                                       WINDOW(#WINDOW)
     A*
     A                                       OVERLAY PUTOVR
     A                                       SFLCTL(#MSGRCD)
     A                                       SFLPAG(1) SFLSIZ(2)
     A                                       SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                   SFLEND
     A            ##PGM                      SFLPGMQ
     A*=====================================================================
