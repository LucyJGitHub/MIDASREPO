      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Layouts for the current group set.')
     A*****************************************************************
     A*                                                               *
     A*   Midas - User Defined Correspondence                       *
     A*                                                               *
     A*   DSPF/CG8110DF - Display layouts for a group set.            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 095463             DATE 06NOV95               *
     A*               . 090199             Date 12JUL95               *
     A*                 S01522             DATE 10FEB95               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  095463  -  Allow 132 characters screens                      *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01522  -  User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
      * Start 095463
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
      * End 095463
     A                                       INDARA
     A                                       PRINT(MPRINT)
     A                                       CHGINPDFT
     A*=====================================================================
     A          R #SFLRCD                    SFL
     A*
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            #1SEL          1   B  7  3 TEXT('Selection box.')
     A                                       DSPATR(PC)
     A N31                                   DSPATR(UL)
     A  31                                   DSPATR(PR)
     A            #1LAYO        10   B  7  8 TEXT('Layout identifier.')
     A                                       DSPATR(PR)
     A            #1TPLN        10   B  7 21 TEXT('Plane identifier.')
     A                                       DSPATR(PR)
     A            #1DESC        30   O  7 34 TEXT('Description.')
     A*
     A            #1PLAN        10   H       TEXT('Higher plane.')
     A            #1LTYP         1   H       TEXT('Line type: +
     A                                              L = Layout; +
     A                                              P = Plane.')
     A*.....................................................................
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*
     A                                      BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous.')
     A                                      VLDCMDKEY(29 'Valid command key.')
     A*
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display control')
     A                                      INDTXT(82 'Display subfile')
     A  80                                  SFLCLR
     A  81                                  SFLDSPCTL
     A  82                                  SFLDSP SFLEND(*MORE)
     A* SETOFFS.................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A*
     A            ##USR         10      1  2TEXT('User name')
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG8110F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A                                      DSPATR(HI)
     A*---
     A            ##JOB         10      2  2TEXT('Job name')
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option name')
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('Time')
     A                                      EDTWRD('0 :  :  ')
     A                                      DSPATR(HI)
     A*---
     A            ##INST        60      4  2TEXT('Instructions')
     A                                      DSPATR(HI)
     A            #2PLAN        10   B  4 63TEXT('"Position to" plane.')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PC)
     A*---
     A            ##COLS        70      6  2TEXT('Column headings')
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R #CMDTXT                   TEXT('Command keys')
     A*
     A                                      OVERLAY PUTOVR
     A            ##CMD         70     23  2TEXT('Command text')
     A                                      DSPATR(HI)
     A*=====================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A*
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*.....................................................................
     A          R #MSGCTL                   TEXT('Program messages')
     A*
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(1) SFLSIZ(2)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25
     AO 25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=====================================================================
