      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Plane element actions display.')
     A*****************************************************************
     A*                                                               *
     A*   Midas - - - Message Management Module                       *
     A*                                                               *
     A*   DSPF/CG8200DF - Plane element selection display             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. 100811       DATE 29AUG96                    *
     A*   PREV AMEND NO. 095463       DATE 06NOV95                    *
     A*                  088814       DATE 08JUN95                    *
     A*                  084604       DATE 13MAR95                    *
     A*                  084478       DATE 09MAR95                    *
     A*                  084255       DATE 03MAR95                    *
     A*                  083845       DATE 24FEB95                    *
     A*                  S01522       DATE 10FEB95                    *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*    100811 - RDE Path identification                           *
     A*    095463 - Allow 132 characters screens                      *
     A*    088814 - Change format of conditions and formatting.       *
     A*    084604 - Replace numeric fields by character fields        *
     A*    084478 - Provide centralised commitment control            *
     A*    084255 - Allow sequence by field name or row and column;   *
     A*              allow direct entry of field positions            *
     A*    083845 - indicate presence of conditions/formatting on     *
     A*             list view.                                        *
     A*    S01522 - User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
      * Start 095463
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
      * End 095463
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=====================================================================
     A*  Formats #WINDOW1, #SFLRCD, #SFLCTL, #CMDTXT, #MSGRCD1 and         =
     A*   #MSGCTL1 together provide the main Work with plane display.      =
     A*=====================================================================
     A          R #WINDOW1                  TEXT('Window definition 1.')
     A*
     A                                      WINDOW(3  2 19 75)
     A**** These lines may be               WDWBORDER((*COLOR BLU)   +
     A****  reinstated later.                          (DSPATR HI RI) +
     A****                                             (*CHAR '        '))
     A*---------------------------------------------------------------------
     A          R #SFLRCD                   SFL
     A*
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A  61                                  SFLNXTCHG
     A*
     A            #1SEL          1   B  3  3TEXT('Selection box.')
     A                                                      DSPATR(UL)
     A  31                                                  DSPATR(PC RI)
     A*___________________________________________________________________084255
     A* 084255                                                            084255
     A                                      SFLCSRPRG
     A* 084255                                                            084255
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084255
     A            #1FLD         10   B  3  6TEXT('Field name.')
     A                                                      DSPATR(PR)
     A*___________________________________________________________________084604
     A* 084604 -- start                                                   084604
     A*                                                                   084604
     A*********** #1SROW         3  0B  3 18TEXT('Start row number.')    084604
     A            #1SROW         3  0H      TEXT('Start row number.')
     A            #1SRWA         3   B  3 18TEXT('Start row number +
     A                                             -- character.')
     A*___________________________________________________________________084255
     A* 084255                                                            084255
     A************                            EDTWRD(' 0 ') DSPATR(PR)    084255
     A************                            EDTWRD(' 0 ') DSPATR(UL)    084604
     A                                                      DSPATR(UL)
     A  36                                                  DSPATR(PC RI)
     A* 084255                                                            084255
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084255
     A*********** #1SCOL         3  0B  3 23 TEXT('Start column number.') 084604
     A            #1SCOL         3  0H       TEXT('Start column number.')
     A            #1SCLA         3   B  3 23 TEXT('Start column number +
     A                                             -- character.')
     A*___________________________________________________________________084255
     A* 084255                                                            084255
     A************                            EDTWRD(' 0 ') DSPATR(PR)    084255
     A************                            EDTWRD(' 0 ') DSPATR(UL)    084604
     A                                                      DSPATR(UL)
     A  36                                                  DSPATR(PC RI)
     A* 084255                                                            084255
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084255
     A*                                                                   084604
     A* 084604 -- end                                                     084604
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084604
     A            #1ELEM        10   B  3 28TEXT('Element name.')
     A                                                      DSPATR(PR)
     A            #1PREV         1   B  3 39TEXT('Previous value.')
     A    N35                                               DSPATR(UL)
     A     35                                               DSPATR(PR)
     A  34N35                                               DSPATR(PC RI)
     A*___________________________________________________________________084604
     A* 084604 -- start                                                   084604
     A*                                                                   084604
     A*********** #1LEN          3  0B  3 41 TEXT('Field length.')        084604
     A            #1LEN          3  0H      TEXT('Field length.')
     A            #1LENA         3   O  3 41TEXT('Field length +
     A                                             -- character.')
     A***********                             EDTWRD(' 0 ') DSPATR(PR)    084604
     A*                                                                   084604
     A* 084604 -- end                                                     084604
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084604
     A            #1DESC        30   O  3 46TEXT('Description.')
     A*083845 start --- new fields
     A*088814 start --- change layout
     A************#1CNAR        18   O  4  6 DSPATR(HI)
     A************#1COND         1   B  4 25 TEXT('Conditions ?')
     A************                                          DSPATR(PR)
     A************#1FNAR        22   O  4 28 DSPATR(HI)
     A************#1FORM         1   B  4 51 TEXT('Formatting ?')
     A************                                          DSPATR(PR)
     A            #1COND         4   O  4 68TEXT('Conditions ?')
     A                                                      DSPATR(HI)
     A*******     #1FORM         3   O  4 73 TEXT('Formatting ?')
     A            #1FORM         3   O  4 60TEXT('Formatting ?')
     A                                                      DSPATR(HI)
     A* 100811 start
     A            #1LIN1        30   O  4  6TEXT('PATH 1.')
     A            #1LIN2        30   O  5  6TEXT('PATH 2.')
     A* 100811 end
     A*088814 end
     A*083845 end
     A*
     A            #1ECOL         3  0H      TEXT('Ending column number.')
     A            #1NUM          5  0H      TEXT('Element number.')
     A            #1RRN          9  0H      TEXT('Database file RRN.')
     A            #1LCVL         1   H      TEXT('Saved previous value.')
     A            #1LITD         1   H      TEXT('Literal / data flag.')
     A*___________________________________________________________________084255
     A* 084255                                                            084255
     A            #1SSRW         3  0H      TEXT('Saved start row.')
     A            #1SSCL         3  0H      TEXT('Saved start column.')
     A* 084255                                                            084255
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084255
     A*.....................................................................
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*
     A                                      WINDOW(#WINDOW1)
     A                                      BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A N41                                  CA03(03 'Exit.')
     A N41                                  CF05(05 'Confirm changes.')
     A N41                                  CF06(06 'Add an element.')
     A  41                                  CF10(10 'Confirm deletion.')
     A                                      CA12(12 'Previous.')
     A N41                                  CF14(14 'Add a literal.')
     A N41                                  CF18(18 'Review results.')
     A*___________________________________________________________________084255
     A* 084255                                                            084255
     A N41                                  CF22(22 'Resequence fields.')
     A N41                                  CF24(24 'Show other key list.')
     A* 084255                                                            084255
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084255
     A                                      VLDCMDKEY(29 'Valid command key.')
     A*
     A                                      SFLCTL(#SFLRCD)
     A* 083845 start
     A************************************** SFLPAG(13) SFLSIZ(14)
     A* 100811 start
     A************************************** SFLPAG(6) SFLSIZ(7)
     A                                      SFLPAG(4) SFLSIZ(5)
     A* 100811 end
     A* 083845 end
     A                                      INDTXT(80 'Clear subfile.')
     A                                      INDTXT(81 'Display control.')
     A                                      INDTXT(82 'Display subfile.')
     A
     A* 083845 start
     A                                      SFLDROP(CF04)
     A* 083845 end
     A  80                                  SFLCLR
     A  81                                  SFLDSPCTL
     A  82                                  SFLDSP SFLEND
     A* SETOFFS.................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A*
     A            #RQTXT        40      1  2TEXT('Request for field name.')
     A                                      DSPATR(HI)
     A            #1NFLD        10   B  1 44TEXT('New field name.')
     A                                                      DSPATR(UL)
     A  32                                                  DSPATR(PC RI)
     A*---
     A            #INTXT        70      2  2TEXT('Instruction text line.')
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R #CMDTXT                   TEXT('Command keys.')
     A*
     A                                      WINDOW(#WINDOW1)
     A                                      OVERLAY PUTOVR
     A*
     A            ##CMD1        70     16  2TEXT('Command text line 1.')
     A                                      DSPATR(HI)
     A*---
     A            ##CMD2        70     17  2TEXT('Command text line 2.')
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R #MSGRCD1                  TEXT('Program messages.')
     A*
     A                                      SFL SFLMSGRCD(18)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*.....................................................................
     A          R #MSGCTL1                  TEXT('Program messages.')
     A*
     A                                      WINDOW(#WINDOW1)
     A                                      OVERLAY PUTOVR
     A*
     A                                      SFLCTL(#MSGRCD1)
     A                                      SFLPAG(1) SFLSIZ(2)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25
     AO 25                                  SFLEND
     A            ##PGM                      SFLPGMQ
     A*=====================================================================
     A*  Formats #WINDOW2, #LITERAL, #MSGRCD2 and #MSGCTL2 together        =
     A*   provide the Literal entry display.                               =
     A*=====================================================================
     A          R #WINDOW2                   TEXT('Window definition 2.')
     A*
     A                                       WINDOW(7  2 10 75)
     A**** These lines may be                WDWBORDER((*COLOR BLU)   +
     A****  reinstated later.                          (DSPATR HI RI) +
     A****                                             (*CHAR '        '))
     A*---------------------------------------------------------------------
     A          R #LITERAL                   TEXT('Literal value entry.')
     A*
     A                                       WINDOW(#WINDOW2)
     A                                       OVERLAY PUTOVR
     A* Command keys............................................................
     A                                       CA03(03 'Exit.')
     A                                       CA12(12 'Previous.')
     A                                       VLDCMDKEY(29 'Valid command key.')
     A* SETOFFS.................................................................
     A            #2FLBL        40      1  2 TEXT('Request for literal name.')
     A                                       DSPATR(HI)
     A            #1NFLD        10   B  1 43 TEXT('New field name.')
     A    N33                                               DSPATR(UL)
     A  32N33                                               DSPATR(PC RI)
     A     33                                               DSPATR(PR)
     A*---
     A            #LHEAD        70      2  2 TEXT('Literal headings.')
     A                                       DSPATR(HI)
     A*---
     A            #2TXT1        64   B  3  2 CHECK(LC)
     A                                       DSPATR(PC UL)
     A*---
     A            #2TXT2        64   B  4  2 CHECK(LC)
     A                                       DSPATR(UL)
     A*---
     A            #2TXT3        64   B  5  2 CHECK(LC)
     A                                       DSPATR(UL)
     A*---
     A            #2TXT4        64   B  6  2 CHECK(LC)
     A                                       DSPATR(UL)
     A*---
     A            ##CMD3        70      7  2 TEXT('Command text line 1.')
     A                                       DSPATR(HI)
     A*---
     A            ##CMD4        70      8  2 TEXT('Command text line 2.')
     A                                       DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R #MSGRCD2                   TEXT('Program messages.')
     A*
     A                                       SFL SFLMSGRCD(9)
     A            MSGKEY                     SFLMSGKEY
     A            ##PGM                      SFLPGMQ
     A*.....................................................................
     A          R #MSGCTL2                   TEXT('Program messages.')
     A*
     A                                       WINDOW(#WINDOW2)
     A                                       OVERLAY PUTOVR
     A*
     A                                       SFLCTL(#MSGRCD2)
     A                                       SFLPAG(1) SFLSIZ(2)
     A                                       SFLINZ SFLDSP SFLDSPCTL
     A N25
     AO 25                                   SFLEND
     A            ##PGM                      SFLPGMQ
     A*===================================================================084478
     A********** Formats #WINDOW4, #REMIND, #MSGRCD4 and #MSGCTL4 together084478
     A**********  provide the Changes reminder display.                   084478
     A*===================================================================084478
     A********* R #WINDOW4                   TEXT('Window definition 4.') 084478
     A***********                                                         084478
     A***********                            WINDOW(11 2  3 75)           084478
     A*********** These lines may be         WDWBORDER((*COLOR BLU)   +   084478
     A***********  reinstated later.                   (DSPATR HI RI) +   084478
     A***********                                      (*CHAR '        '))084478
     A***********                                                         084478
     A**Command*keys......................................................084478
     A***********                            CF05(05 'Confirm changes.')  084478
     A***********                            CA12(12 'Previous.')         084478
     A***********                            VLDCMDKEY(29 'Valid command key.')
     A**SETOFFS...........................................................084478
     A*.HELP*TEXT.........................................................084478
     A*...................................................................084478
     A*********** #4WARN        70      2  2 TEXT('Warning message.')     084478
     A***********                            DSPATR(HI)                   084478
     A*=====================================================================
     A*  Format DUMMY is not used. It contains the ASSUME keyword          =
     A*   which must be in at least one format when the WINDOW             =
     A*   keyword is used.                                                 =
     A*=====================================================================
     A          R DUMMY                      TEXT('Dummy format.')
     A*
     A                                       OVERLAY PUTOVR ASSUME
     A            DUMMYF         1A  O  1 80 DSPATR(ND)
     A*=====================================================================
