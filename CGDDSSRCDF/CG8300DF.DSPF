      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Conditions application display.')
     A*****************************************************************
     A*                                                               *
     A*   Midas - User-defined correspondence                         *
     A*                                                               *
     A*   DSPF/CG8300DF - Work with conditions display                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. 095463       DATE 06NOV95                    *
     A*   PREV AMEND NO. 090219       DATE 10JUL95                    *
     A*                  090199       DATE 12JUL95                    *
     A*                  S01522       DATE 10FEB95                    *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*    095463 - Allow 132 characters screens                      *
     A*    090219 - Remove F6 processing.                             *
     A*    090199 - Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*    S01522 - User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
      * Start 095463
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
      * End 095463
     A                                       INDARA
     A                                       PRINT(MPRINT)
     A                                       CHGINPDFT
     A*=====================================================================
     A          R #SFLRCD                    SFL
     A*
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A            #1SEL          1   B  5  3 TEXT('Selection box.')
     A                                        DSPATR(PC UL)
     A            #1ACT          1   B  5  8 TEXT('Active condition flag.')
     A                                        DSPATR(PR)
     A  31                                    DSPATR(HI)
     A            #1LEOD        15   O  5 12 TEXT('Left element or data.')
     A  31                                    DSPATR(HI)
     A            #1LVAL         1   O  5 28 TEXT('Left extender.')
     A  31                                    DSPATR(HI)
     A            #1OPER        10   O  5 39 TEXT('Operator.')
     A  31                                    DSPATR(HI)
     A            #1REOD        15   O  5 51 TEXT('Right element or data.')
     A  31                                    DSPATR(HI)
     A            #1RVAL         1   O  5 67 TEXT('Right extender.')
     A  31                                    DSPATR(HI)
     A*
     A            #1LDSC        25   O  6 12 TEXT('Left element description.')
     A            #1RDSC        25   O  6 51 TEXT('Right element description.')
     A*
     A            #1CSEQ         4  0H       TEXT('Sequence number.')
     A            #1LRPT         6   H       TEXT('Left non-repeating path.')
     A            #1RRPT         6   H       TEXT('Right non-repeating path.')
     A*.....................................................................
     A          R #SFLCTL                    TEXT('Subfile control.')
     A*
     A                                       BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A                                       CA03(03 'Exit.')
     A                                       CA12(12 'Previous.')
     A* 090219 -- start
     A*N40********                           CF06(06 'Maintain conditions.')
     A* 090219 -- End
     A                                       VLDCMDKEY(29 'Valid command key.')
     A*
     A                                       SFLCTL(#SFLRCD)
     A                                       SFLPAG(8) SFLSIZ(9)
     A                                       INDTXT(40 'In review mode.')
     A                                       INDTXT(80 'Clear subfile.')
     A                                       INDTXT(81 'Display control.')
     A                                       INDTXT(82 'Display subfile.')
     A  80                                   SFLCLR
     A  81                                   SFLDSPCTL
     A  82                                   SFLDSP SFLEND
     A                                       SFLDROP(CF04)
     A* SETOFFS.................................................................
     A*.........................................................................
     A            ##SFRC         4  0H       SFLRCDNBR
     A*
     A            ##USR         10      1  2 TEXT('*USER')
     A                                       DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG8300FD1'
     A                                       DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72 TEXT('Midas run date')
     A                                       DSPATR(HI)
     A*---
     A            ##JOB         10      2  2 TEXT('*JOB')
     A                                       DSPATR(HI)
     A            #HEAD1        40      2 21 TEXT('Headings text')
     A                                       DSPATR(HI)
     A            ##TME          6  0   2 72 TEXT('*TIME')
     A                                       EDTWRD('0 :  :  ')
     A                                       DSPATR(HI)
     A*---
     A            #HEAD2        75      4  2 TEXT('Column headings')
     A*---------------------------------------------------------------------
     A          R #CMDTXT                    TEXT('Command keys.')
     A*
     A                                       OVERLAY PUTOVR
     A            ##CMD1        70     22  2 TEXT('Command text line 1.')
     A                                       DSPATR(HI)
     A*---
     A            ##CMD2        70     23  2 TEXT('Command text line 2.')
     A                                       DSPATR(HI)
     A*=====================================================================
     A          R #MSGRCD                    TEXT('Program messages.')
     A*
     A                                       SFL SFLMSGRCD(24)
     A            MSGKEY                     SFLMSGKEY
     A            ##PGM                      SFLPGMQ
     A*.....................................................................
     A          R #MSGCTL                    TEXT('Program messages.')
     A*
     A                                       OVERLAY PUTOVR
     A                                       SFLCTL(#MSGRCD)
     A                                       SFLPAG(1) SFLSIZ(2)
     A                                       SFLINZ SFLDSP SFLDSPCTL
     A N25
     AO 25                                   SFLEND
     A            ##PGM                      SFLPGMQ
     A*=====================================================================
