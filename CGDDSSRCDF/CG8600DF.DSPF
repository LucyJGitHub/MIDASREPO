      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Format selection display.')
     A*****************************************************************
     A*                                                               *
     A*   Midas - - - Message Management Module                   *
     A*                                                               *
     A*   DSPF/CG8600DF - Format selection display                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. 093613       DATE 08JUL96                    *
     A*   PREV AMEND NO. 095463       DATE 06NOV95                    *
     A*                  084604       DATE 13MAR95                    *
     A*                  084073       DATE 01MAR95                    *
     A*                  084005       DATE 27FEB95                    *
     A*                  S01522       DATE 10FEB95                    *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  093613 - Allow reset/delete of formating options for a field *
     A*    095463 - Allow 132 characters screens                      *
     A*    084604 - Replace numeric fields by character fields        *
     A*    084073 - Allow entry of justification spacing value        *
     A*    084005 - Show full text of existing format options         *
     A*    S01522 - User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
      * Start 095463
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
      * End 095463
     A                                       INDARA
     A                                       PRINT(MPRINT)
     A                                       CHGINPDFT
     A* Command keys............................................................
     A                                       CA03(03 'Exit.')
     A                                       CA12(12 'Previous.')
     A                                       VLDCMDKEY(29 'Valid command key.')
     A*=====================================================================
     A*  Format #WINDOW controls the window characteristics.               =
     A*=====================================================================
     A          R #WINDOW                    TEXT('Window definition.')
     A*
     A                                       WINDOW(2  2 21 75)
     A**** These lines may be                WDWBORDER((*COLOR BLU)   +
     A****  reinstated later.                          (DSPATR HI RI) +
     A****                                             (*CHAR '        '))
     A*=====================================================================
     A*  Format #SELECT1 allows the selection of Number, Code and / or     =
     A*   Character, together with Length and Alignment values.            =
     A*=====================================================================
     A          R #SELECT1                   TEXT('Selection options.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       BLINK OVERLAY PUTOVR
     A* SETOFFS.................................................................
     A*                                                         /*Start 093613*/
     A* Command keys............................................................
     A                                       CA10(10 'Delete.')
     A*                                                         /*End 093613*/
     A*.........................................................................
     A            #FMTX1        40      1  2 TEXT('Label: "Format +
     A                                             options for field:"')
     A                                       DSPATR(HI)
     A            #FMFLD        10   O  1 44 TEXT('Format field name.')
     A*---
     A            #FMTX2        15      2  2 TEXT('Label: "Length:"')
     A                                       DSPATR(HI)
     A*___________________________________________________________________084604
     A* 084604 -- start                                                   084604
     A*                                                                   084604
     A*********** #FMLEN         5  0B  2 18 TEXT('Format length.')
     A            #FMLEN         5  0H       TEXT('Format length.')
     A            #FMLNA         5   B  2 18 TEXT('Format length +
     A                                             -- character.')
     A*                                                                   084604
     A* 084604 -- end                                                     084604
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084604
     A                                                      DSPATR(UL)
     A            #FMTX3        15      2 27 TEXT('Label: "Alignment:"')
     A                                       DSPATR(HI)
     A*___________________________________________________________________084073
     A*********** #FMALN         3   B  2 44 TEXT('Alignment.')           084073
     A            #FMALN         3   B  2 44 TEXT('Alignment.')
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084073
     A                                       OVRATR         DSPATR(UL)
     A  31                                                  DSPATR(PC RI)
     A*---
     A            #1OPT1         1   B  5  3 TEXT('Selection box 1.')
     A N41                                                  DSPATR(PR ND)
     A  41                                                  DSPATR(PC UL)
     A*___________________________________________________________________084005
     A*********** #1TXT1        40   O  5  7 TEXT('Selection text 1.')    084005
     A            #1TXT1        30   O  5  7 TEXT('Selection text 1.')
     A            #1FOP1        30   O  5 40 TEXT('Current option 1.')
     A            #1TYP1        30   O  6 43 TEXT('Current type 1.')
     A*---
     A*********** #1OPT2         1   B  7  3 TEXT('Selection box 2.')     084005
     A*N42*******                                           DSPATR(PR ND) 084005
     A* 42*******                                           DSPATR(UL)    084005
     A*********** #1TXT2        40   O  7  7 TEXT('Selection text 2.')    084005
     A*---                                                                084005
     A*********** #1OPT3         1   B  9  3 TEXT('Selection box 3.')     084005
     A*N43*******                                           DSPATR(PR ND) 084005
     A* 43*******                                           DSPATR(UL)    084005
     A*********** #1TXT3        40   O  9  7 TEXT('Selection text 3.')    084005
     A*---
     A            #1OPT2         1   B  8  3 TEXT('Selection box 2.')
     A N42                                                  DSPATR(PR ND)
     A  42                                                  DSPATR(UL)
     A            #1TXT2        30   O  8  7 TEXT('Selection text 2.')
     A            #1FOP2        30   O  8 40 TEXT('Current option 2.')
     A            #1TYP2        30   O  9 43 TEXT('Current type 2.')
     A*---
     A            #1OPT3         1   B 11  3 TEXT('Selection box 3.')
     A N43                                                  DSPATR(PR ND)
     A  43                                                  DSPATR(UL)
     A            #1TXT3        30   O 11  7 TEXT('Selection text 3.')
     A            #1FOP3        30   O 11 40 TEXT('Current option 3.')
     A            #1TYP3        30   O 12 43 TEXT('Current type 3.')
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084005
     A*---
     A            ##CMD1        70     18  2 TEXT('Command text line 1.')
     A                                       DSPATR(HI)
     A*---
     A            ##CMD2        70     19  2 TEXT('Command text line 2.')
     A                                       DSPATR(HI)
     A*=====================================================================
     A*  Formats #SFLRCD2, #SFLCTL2 and #CMDTXT2                           =
     A*   together provide the Formatting options display.                 =
     A*=====================================================================
     A          R #SFLRCD2                   SFL
     A*
     A                                       TEXT('Format options subfile.')
     A*
     A*********** #2SEL          1   B  5  3 TEXT('Selection box.')       084005
     A            #2SEL          1   B  6  3 TEXT('Selection box.')
     A                                                      DSPATR(UL)
     A  32                                                  DSPATR(PC RI)
     A*********** #2FOPT        10   B  5  8 TEXT('Format option.')       084005
     A            #2FOPT        10   B  6 15 TEXT('Format option.')
     A                                                      DSPATR(PR)
     A*********** #2TEXT        30   O  5 21 TEXT('Line text.')           084005
     A            #2TEXT        30   O  6 27 TEXT('Line text.')
     A*.....................................................................
     A          R #SFLCTL2                   TEXT('Subfile control 2.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A                                       CA05(05 'Refresh display.')
     A                                       CA09(09 'Add format options.')
     A*
     A                                       SFLCTL(#SFLRCD2)
     A***********                            SFLPAG(12) SFLSIZ(13)        084005
     A                                       SFLPAG(11) SFLSIZ(12)
     A                                       INDTXT(80 'Clear subfile.')
     A                                       INDTXT(81 'Display control.')
     A                                       INDTXT(82 'Display subfile.')
     A  80                                   SFLCLR
     A  81                                   SFLDSPCTL
     A  82                                   SFLDSP SFLEND(*MORE)
     A* SETOFFS.................................................................
     A*.........................................................................
     A            #FMTX1        40   O  1  2 TEXT('Label: "Format +
     A                                             options for field:"')
     A                                       DSPATR(HI)
     A            #FMFLD        10   O  1 44 TEXT('Format field name.')
     A*---
     A            #PRTYP        40   O  3 24 TEXT('Processing type.')
     A*---
     A            #FMTX4        70   O  4  2 TEXT('Column headings.')
     A                                       DSPATR(HI)
     A*---
     A*___________________________________________________________________084005
     A            #FMTX6        12      5  4 TEXT('Label: "Currently:"')
     A                                       DSPATR(HI)
     A            #2CURO        10   O  5 15 TEXT('Current option.')
     A            #2CURD        30   O  5 27 TEXT('Current description.')
     A                                       DSPATR(HI)
     A*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~084005
     A*---------------------------------------------------------------------
     A          R #CMDTXT2                   TEXT('Command keys.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       OVERLAY PUTOVR
     A*
     A            ##CMD3        70     18  2 TEXT('Command text line 3.')
     A                                       DSPATR(HI)
     A*---
     A            ##CMD4        70     19  2 TEXT('Command text line 4.')
     A                                       DSPATR(HI)
     A*=====================================================================
     A*  Format #NOOPTS shows a message to the effect that no              =
     A*   formatting options have been defined for an element.             =
     A*=====================================================================
     A          R #NOOPTS                    TEXT('No options found.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A                                       CA09(09 'Add format options.')
     A* SETOFFS.................................................................
     A            #FMTX1        40      1  2 TEXT('Label: "Format +
     A                                             options for field:"')
     A                                       DSPATR(HI)
     A            #FMFLD        10   O  1 44 TEXT('Format field name.')
     A*---
     A            #NOTX1        40      7  2
     A            #NRDE         10   O  7 43 TEXT('Element name.')
     A*---
     A            #NOTX2        70      9  2
     A*=====================================================================
     A*  Formats #SFLRCD3, #SFLCTL3 and #CMDTXT3                           =
     A*   together provide the Formatting options display.                 =
     A*=====================================================================
     A*                                                    /*Start 093613*/
     A*=====================================================================
     A*  Format #FMTDEL shows a message to confirm deleteion               =
     A*=====================================================================
     A          R #FMTDEL                    TEXT('Del Confirm.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A                                       CA10(10 'Delete.')
     A* SETOFFS.................................................................
     A            #FMTX1        40      1  2 TEXT('Label: "Format +
     A                                             options for field:"')
     A                                       DSPATR(HI)
     A            #FMFLD        10   O  1 44 TEXT('Format field name.')
     A*---
     A            #DELX1        70      7  2
     A*---
     A*                                                    /*End 093613*/
     A          R #SFLRCD3                   SFL
     A*
     A                                       TEXT('Format options subfile.')
     A*
     A            #3SEL          1   B  6  3 TEXT('Selection box.')
     A  33N45                                               DSPATR(PC RI)
     A    N45                                               DSPATR(UL)
     A     45
     AO    46                                               DSPATR(ND PR)
     A            #3FRMT        15   B  6 17 TEXT('Format type.')
     A                                                      DSPATR(PR)
     A     46                                               DSPATR(ND)
     A            #3DESC        30   O  6 34 TEXT('Format description.')
     A     46                                               DSPATR(ND)
     A            #3LGID         2   B  6 67 TEXT('Language ID.')
     A                                                      DSPATR(PR)
     A     46                                               DSPATR(ND)
     A*
     A            #3LTYP         1   H       TEXT('Line type.')
     A*                                        O - Option; L - Language;
     A*                                        B - Blank;  R - Remove
     A            #3FOPT        10   H       TEXT('Formatting option.')
     A            #3RMV          1   H       TEXT('Mark this line to +
     A                                             remove formatting.')
     A            #3DATA       256   H       TEXT('Data string.')
     A*.....................................................................
     A          R #SFLCTL3                   TEXT('Subfile control 3.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A                                       CA09(09 'Add format options.')
     A*
     A                                       SFLCTL(#SFLRCD3)
     A                                       SFLPAG(11) SFLSIZ(12)
     A                                       INDTXT(85 'Clear subfile.')
     A                                       INDTXT(86 'Display control.')
     A                                       INDTXT(87 'Display subfile.')
     A  85                                   SFLCLR
     A  86                                   SFLDSPCTL
     A  87                                   SFLDSP SFLEND(*MORE)
     A* SETOFFS.................................................................
     A*.........................................................................
     A            #FMTX1        40      1  2 TEXT('Label: "Format +
     A                                             options for field:"')
     A                                       DSPATR(HI)
     A            #FMFLD        10   O  1 44 TEXT('Format field name.')
     A*---
     A            #PRTYP        40   O  3 17 TEXT('Processing type.')
     A*---
     A            #FMTX5        70      4  2 TEXT('Column headings.')
     A                                       DSPATR(HI)
     A*---
     A            #FMTX6        12      5  4 TEXT('Label: "Currently:"')
     A                                       DSPATR(HI)
     A            #3CURT        15   O  5 17 TEXT('Current type.')
     A            #3CURD        30   O  5 34 TEXT('Current description.')
     A                                       DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R #CMDTXT3                   TEXT('Command keys.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       OVERLAY PUTOVR
     A*
     A            ##CMD5        70     18  2 TEXT('Command text line 5.')
     A                                       DSPATR(HI)
     A*---
     A            ##CMD6        70     19  2 TEXT('Command text line 6.')
     A                                       DSPATR(HI)
     A*=====================================================================
     A*  Formats #MSGRCD and #MSGCTL display program messages.             =
     A*=====================================================================
     A          R #MSGRCD                    TEXT('Program messages.')
     A*
     A                                       SFL SFLMSGRCD(20)
     A            MSGKEY                     SFLMSGKEY
     A            ##PGM                      SFLPGMQ
     A*.....................................................................
     A          R #MSGCTL                    TEXT('Program messages.')
     A*
     A                                       WINDOW(#WINDOW)
     A                                       OVERLAY PUTOVR
     A*
     A                                       SFLCTL(#MSGRCD)
     A                                       SFLPAG(1) SFLSIZ(2)
     A                                       SFLINZ SFLDSP SFLDSPCTL
     A N25
     AO 25                                   SFLEND
     A            ##PGM                      SFLPGMQ
     A*=====================================================================
     A*  Format DUMMY is not used. It contains the ASSUME keyword          =
     A*   which must be in at least one format when the WINDOW             =
     A*   keyword is used.                                                 =
     A*=====================================================================
     A          R DUMMY                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                       OVERLAY PUTOVR ASSUME
     A            DUMMYF         1A  O  1 80 DSPATR(ND)
     A*=====================================================================
