      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Non-repeating data elements screen.')
     A*****************************************************************
     A*                                                               *
     A*   Midas - User Defined Correspondence Module                  *
     A*                                                               *
     A*   DSPF/CG8800DF - Display non-repeating data elements.        *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 095463             DATE 06NOV95               *
     A*                 090199             Date 12JUL95               *
     A*                 S01522             DATE 10FEB95               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  095463  -  Allow 132 characters screens                      *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01522  -  User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
      * Start 095463
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
      * End 095463
     A                                       INDARA
     A                                       PRINT(MPRINT)
     A                                       CHGINPDFT
     A*=====================================================================
     A          R #SFLRCD                    SFL
     A*
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            #1SEL          1   B  5  2 TEXT('Selection box.')
     A N31                                   DSPATR(PC UL)
     A  31                                   DSPATR(ND PR)
     A            #1LINE        75   O  5  5 TEXT('Descriptions.')
     A*
     A            #1TYPE         1   H       TEXT('Line type flag.')
     A*
     A* The following values identify the selected data element:
     A*
     A            #RGRPS         6   H       TEXT('Upper group set name.')
     A            #AGRPS         6   H       TEXT('Lower group set name.')
     A            #ARDE         10   H       TEXT('Report data element.')
     A            #RRPTH         6   H       TEXT('Repeating path name.')
     A            #RNPTH         6   H       TEXT('Non-repeating path name.')
     A            #DELYP         6   H       TEXT('Non-repeating path name.')
     A            #RFOPT        10   H       TEXT('Formatting options group.')
     A            #DETXT        25   H       TEXT('Example data.')
     A            #DMAXL         5  0H       TEXT('Maximum length.')
     A*.....................................................................
     A          R #SFLCTL                    TEXT('Subfile control.')
     A*
     A                                       BLINK OVERLAY PUTOVR
     A* Command keys............................................................
     A                                       CA03(03 'Exit.')
     A                                       CF05(05 'Position to home.')
     A                                       CF10(10 'Position to cursor.')
     A                                       CA12(12 'Previous.')
     A  82N83                                ROLLUP(27 'Next page.')
     A                                       VLDCMDKEY(29 'Valid command key.')
     A*
     A                                       SFLCTL(#SFLRCD)
     A                                       SFLPAG(17) SFLSIZ(18)
     A                                       INDTXT(80 'Clear subfile')
     A                                       INDTXT(81 'Display control')
     A                                       INDTXT(82 'Display subfile')
     A                                       INDTXT(83 'End of subfile')
     A  80                                   SFLCLR
     A  81                                   SFLDSPCTL
     A  82                                   SFLDSP SFLEND(*MORE)
     A* SETOFFS.................................................................
     A*.........................................................................
     A            ##SFRC         4  0H       SFLRCDNBR
     A*
     A            ##USR         10      1  2 TEXT('User name')
     A                                       DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG8800F1'
     A                                       DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72 TEXT('Midas run date')
     A                                       DSPATR(HI)
     A*---
     A            ##JOB         10      2  2 TEXT('Job name')
     A                                       DSPATR(HI)
     A            ##ONAM        40      2 21 TEXT('Option name')
     A                                       DSPATR(HI)
     A            ##TME          6  0   2 72 TEXT('Time')
     A                                       EDTWRD('0 :  :  ')
     A                                       DSPATR(HI)
     A*---
     A            ##ITYP        30      3 26 TEXT('Item type description')
     A                                       DSPATR(HI)
     A*---------------------------------------------------------------------
     A          R #CMDTXT                    TEXT('Command keys')
     A*
     A                                       OVERLAY PUTOVR
     A            ##CMD         70     23  2 TEXT('Command Line')
     A                                       DSPATR(HI)
     A*=====================================================================
     A          R #MSGRCD                    TEXT('Program messages')
     A*
     A                                       SFL SFLMSGRCD(24)
     A            MSGKEY                     SFLMSGKEY
     A            ##PGM                      SFLPGMQ
     A*.....................................................................
     A          R #MSGCTL                    TEXT('Program messages')
     A*
     A                                       SFLCTL(#MSGRCD)
     A                                       SFLPAG(1) SFLSIZ(2)
     A                                       SFLINZ SFLDSP SFLDSPCTL
     A N25
     AO 25                                   SFLEND
     A            ##PGM                      SFLPGMQ
     A*=====================================================================
