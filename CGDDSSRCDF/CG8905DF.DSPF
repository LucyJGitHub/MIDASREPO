      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG Plane or layout display screen')
      *****************************************************************
      *                                                               *
      *  Midas - UDC Module                                           *
      *                                                               *
      *  DSPF/CG8905DF - Display Layout Window                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 095463             Date 06NOV95               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   095463 - 132 Character Window on the world                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                       INDARA
     A                                       PRINT(MPRINT)
     A                                       CHGINPDFT
      *=====================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                       SFL
     A            #2LINE       127   O  2  2 DSPATR(PC)
      *.....................................................................
     A          R #SFLCTL                    TEXT('Subfile control.')
      *
     A                                       BLINK OVERLAY PUTOVR
      * Command keys............................................................
     A                                       CA03(03 'Exit.')
     A                                       CF05(05 'Confirm changes.')
     A                                       CF06(06 'Action prompt.')
     A                                       CA12(12 'Previous / cancel.')
     A                                       CF15(15 'Move to Home.')
     A                                       CF16(16 'Move to cursor.')
     A                                       CF19(19 'Window to left.')
     A                                       CF20(20 'Window to right.')
     A                                       VLDCMDKEY(29 'Valid command key.')
      *
     A                                       SFLCTL(#SFLRCD)
     A                                       SFLPAG(20) SFLSIZ(21)
     A                                       INDTXT(80 'Clear subfile')
     A                                       INDTXT(81 'Display control')
     A                                       INDTXT(82 'Display subfile')
     A                                       INDTXT(83 'End of subfile')
     A  80                                   SFLCLR
     A  81                                   SFLDSPCTL
     A  82                                   SFLDSP
      * SETOFFS.................................................................
     A            ##SFRC         4  0H       SFLRCDNBR
      *
     A            ##USR         10   H       TEXT('*USER')
     A            ##PGM         10   H       TEXT('*PROGRAM')
     A            ##MRDT         7   H       TEXT('Midas run date')
      *
     A            ##JOB         10   H       TEXT('*JOB')
     A            ##ONAM        40   H       TEXT('Option Name')
     A            ##TME          6  0H       TEXT('*TIME')
      *---------------------------------------------------------------------
     A          R #CMDTXT                    TEXT('Command keys')
     A*
     A                                       OVERLAY PUTOVR
     A            ##CMD1        70     22  2 TEXT('Command Line')
     A                                       DSPATR(HI)
      *
     A            ##CMD2        70     23  2 TEXT('Command text line 2.')
     A                                       DSPATR(HI)
      *=====================================================================
     A          R #MSGRCD                    TEXT('Program messages')
      *
     A                                       SFL SFLMSGRCD(24)
     A            MSGKEY                     SFLMSGKEY
     A            ##PGM                      SFLPGMQ
      *.....................................................................
     A          R #MSGCTL                    TEXT('Program messages')
      *
     A                                       SFLCTL(#MSGRCD)
     A                                       SFLPAG(1) SFLSIZ(2)
     A                                       SFLINZ SFLDSP SFLDSPCTL
     A N25
     AO 25                                   SFLEND
     A            ##PGM                      SFLPGMQ
      *=====================================================================
