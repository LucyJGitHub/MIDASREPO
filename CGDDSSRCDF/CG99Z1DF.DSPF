      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG View utility 1')
     A**************************************************************
     A*                                                            *
     A*  Midas - User Defined Correspondence                       *
     A*                                                            *
     A*   DSPF/CG99Z1DF - View Utility 1                           *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 22Jun95               *
      *                 Sxxxxx             Date ddmmmyy               *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A**Start of change CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            ACTN           1A  B  5  2DSPATR(PC HI UL)
     A            SITEM          9A  O  5  4DSPATR(HI)
     A            DRDCOR        10A  O  5 15
     A            DRPCOR        10A  O  5 26
     A            DRMODI         2A  O  5 37
     A**Start of change CGL029
     A**********  STRAN         18A  O  5 40                                                  CGL029
     A**********  DRPTYP        10A  O  5 59                                                  CGL029
     A**********  DRPSTP        10A  O  5 70                                                  CGL029
     A            STRAN         24A  O  5 40
     A            DRPTYP        10A  O  5 65
     A            DRPSTP        10A  O  5 76
     A**End of change CGL029
     A            DRITMA         5A  H
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A**Start of change CGL029
     A**********                            SFLPAG(16) SFLSIZ(16)                             CGL029
     A                                      SFLPAG(19) SFLSIZ(19)
     A**End of change CGL029
     A                                      BLINK OVERLAY
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND(*MORE)
     A* Command keys............................................................
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'CF12 Previous')
     A                                      ROLLDOWN(05 'Previous page.')
     A                                      ROLLUP(06 'Next page.')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG99Z1F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 29'Correspondence Reference'
     A                                      DSPATR(HI UL)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                  3  2'X'
     A                                  3  4'Item No'
     A                                  3 15'Prim Corr'
     A                                  3 26'Secn Corr'
     A                                  3 40'Transaction'
     A**Start of change CGL029
     A**********                        3 59'Print Type'                                      CGL029
     A**********                        3 70'Print STyp'                                      CGL029
     A                                  3 65'Print Type'
     A                                  3 76'Print STyp'
     A**End of change CGL029
     A            SSITEM         9A  B  4  4DSPATR(HI UL)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A**Start of change CGL029
     A**********                       22  2'E=Enquire  P=Print'                              CGL029
     A                                 25  2'E=Enquire  P=Print'
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                       23  2'F3=Exit    F12=Previous   Rollup-                CGL029
     A                                 26  2'F3=Exit    F12=Previous   Rollup-
     A**End of change CGL029
     A                                       Rolldown'
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A**Start of change CGL029
     A**********                            SFL SFLMSGRCD(24)                                 CGL029
     A                                      SFL SFLMSGRCD(27)
     A**End of change CGL029
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
