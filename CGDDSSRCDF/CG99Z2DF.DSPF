      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas CG View utility 2')
     A**************************************************************
     A*                                                            *
     A*  Midas - User Defined Correspondence                       *
     A*                                                            *
     A*   DSPF/CG99Z2DF - View Utility 2                           *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. 100811       DATE 29AUG96                *
     A*    PREV AMEND NO. S01522       DATE 22JUN95                *
     A*                                                            *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*    100811 - RDE Path identification                        *
     A*    S01522 - User Defined Correspondence                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DATAT         60A  O  4  2
     A            DERPTH         6   O  4 63
     A            DENPTH         6   O  4 70
     A            SSEQ           3S 0O  4 77
     A* 100811 start
     A            #1LIN1        30   O  5  6 TEXT('PATH 1.')
     A            #1LIN2        30   O  6  6 TEXT('PATH 2.')
     A* 100811 end
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A* 100811 start
     A*************                         SFLPAG(17) SFLSIZ(17)
     A                                      SFLPAG(6) SFLSIZ(17)
     A* 100811 end
     A                                      BLINK OVERLAY
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND(*MORE)
     A* 100811 start
     A                                      SFLDROP(CF04)
     A* 100811 end
     A* Command keys............................................................
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'CF12 Previous')
     A                                      ROLLDOWN(05 'Previous page.')
     A                                      ROLLUP(06 'Next page.')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'CG99Z2F1'
     A                                      DSPATR(HI)
     A                                  1 26MSGCON(29 CGA0000 GBCGUSRMSG)
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 26'Correspondence Reference Data'
     A                                      DSPATR(HI UL)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A* 100811 start
     A**********                       23  2'F3=Exit    F12=Previous   Rollup-
     A**********                             Rolldown'
     A                                 23  2'F3=Exit    F4=Path   F12=Previous -
     A                                       Rollup   Rolldown'
     A* 100811 end
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
