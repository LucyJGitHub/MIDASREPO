      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Load Retail Interest Scale Qualifyng Act Cod')   *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  CGC7190DF - Load Retail Interest Scale Qualifying Account    *
      *              Codes                                            *
      *                                                               *
      *  (C) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CER042  *CREATE    Date 11May11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER042 - Interest Scale Report Correspondence                *
      *           (Upgrade of FGE178/179)                             *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      HELP
     A                                      CA03(03 'Exit')
     A                                      CA06(06 'Confirm')
     A          R CGC7190F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2011')
     A                                      OVERLAY
     A            D0USER        10A  O  1  2
     A            D0PGMN        10A  O  1 13
     A                                  1 27'Retail Interest Scale Report'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            D0RUNA         7A  O  1 73
     A            D0WSID        10A  O  2  2
     A                                  2 29'Qualifying Account Codes'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  5  2'Refresh the Interest Scale Report -
     A                                      Retail Qualifying Account Codes dat-
     A                                      a.'
     A                                  6  2'This process replaces the existing-
     A                                       data.'
     A                                 23  2'F3=Exit'
     A                                 23 11'F6=Confirm'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(5)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A* 25
     A*ON25                                  SFLEND
     A N99                                  SFLEND
     A            ##PGM                     SFLPGMQ
