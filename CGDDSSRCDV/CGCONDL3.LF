      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIPM2                                                    *
/*EXI *  MBR(*NONE)                                                   *
/*EXI *  TEXT('Midas CG Conditions               By Right RDE')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                          *
     A*                                                               *
     A*  LF/CGCONDL3 - Conditions                BY RIGHT RDE         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01522             DATE 23NOV94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A*=====================================================================
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @CONDL3                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(CGCONDPD +
     A                                            CGRDTAPD +
     A                                            CGRDTAPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Conditions to Report Data Elements
     A          J                           JOIN(1 2)
     A                                      JFLD(CNLRDE RDRDE)
     A*---------------------------------------------------------------------
     A* Join from Conditions to Report Data Elements
     A          J                           JOIN(1 3)
     A                                      JFLD(CNRRDE RDRDE)
     A*---------------------------------------------------------------------
     A            CNLAYO                    TEXT('Layout Identifier')
     A                                      JREF(1)
     A            CNCID                     TEXT('Condition identifier')
     A                                      JREF(1)
     A            CNCSEQ                    TEXT('Condition sequence')
     A                                      JREF(1)
     A            CNRECI                    TEXT('Record Identifier')
     A                                      JREF(1)
     A            CNSEQA                    TEXT('Order/Sequence')
     A                                      JREF(1)
     A            CNCONG                    TEXT('Grouping')
     A                                      JREF(1)
     A            CNLRP                     TEXT('Left Repeating Path')
     A                                      JREF(1)
     A            CNLNRP                    TEXT('Left Non-Repeating Path')
     A                                      JREF(1)
     A            CNLRDE                    TEXT('Left Report Data Element')
     A                                      JREF(1)
     A            CNLELM                    TEXT('Left Element Type')
     A                                      JREF(2)
     A                                      RENAME(RDELYP)
     A            CNLDTA                    TEXT('Left Data')
     A                                      JREF(1)
     A            CNLVAL                    TEXT('Left Hand Value to use')
     A                                      JREF(1)
     A            CNOPER                    TEXT('Operator')
     A                                      JREF(1)
     A            CNRRP                     TEXT('Right Repeating Path Path')
     A                                      JREF(1)
     A            CNRNRP                    TEXT('Right Non-Repeating Path')
     A                                      JREF(1)
     A            CNRRDE                    TEXT('Right Report Data Element')
     A                                      JREF(1)
     A            CNRELM                    TEXT('Right Element Type')
     A                                      JREF(3)
     A                                      RENAME(RDELYP)
     A            CNRDTA                    TEXT('Right Data')
     A                                      JREF(1)
     A            CNRVAL                    TEXT('Right hand Value to use')
     A                                      JREF(1)
     A            CNDESC                    TEXT('Description')
     A                                      JREF(1)
     A            CNAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            CNAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            CNATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            CNARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            CNAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            CNRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K CNRRDE
     A          K CNCID
     A          K CNCSEQ
     A*.....................................................................
     A* SELECT: Select only Live Records
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S CNRECI                    COMP(EQ 'D')
     A*=====================================================================
