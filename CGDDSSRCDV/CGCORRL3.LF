      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG Correspondent details by account code')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                          *
     A*                                                               *
     A*  LF/CGCORRL3 - Correspondent Details     By Account Code      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD053             Date 01Jun06               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CCG015             Date 13Jun01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 23Nov94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSD053 - Correspondence Manager MultiLanguage Upgrade        *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CCG015 - Correspondence Manager Phase 1                      *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A          R @CORRL3                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(CGCORRPD)
     A*---------------------------------------------------------------------
     A            CDCORR                    TEXT('Correspondent')
     A            CDRECI                    TEXT('Record Identifier')
     A            CDCUST                    TEXT('Customer Number        SD')
     A            CDCPNC                    TEXT('Counterparty Nostro Code')
     A            CDBICC                    TEXT('BIC (Bank, Country, & Loc')
     A            CDBICB                    TEXT('BIC (Branch Code)')
     A            CDBRCA                    TEXT('Branch Code')
     A            CDCTYP                    TEXT('Correspondent type')
     A            CDRTYP                    TEXT('Relationship type')
     A            CDLGID                    TEXT('Language identifier')
     A            CDCORN                    TEXT('Correspondent Name')
     A            CDFATT                    TEXT('For Attention of')
     A            CDADD1                    TEXT('Address Line 1')
     A            CDADD2                    TEXT('Address Line 2')
     A            CDADD3                    TEXT('Address Line 3')
     A            CDADD4                    TEXT('Address Line 4')
     A            CDADD5                    TEXT('Address Line 5')
     A            CDADD6                    TEXT('Address Line 6')
     A            CDADD7                    TEXT('address line 7')
     A            CDADD8                    TEXT('address line 8')
     A            CDZIP                     TEXT('ZIP code')
     A            CDDPRT                    TEXT('Default Print Schedule')
     A            CDDPDT                    TEXT('Default Print Date')
     A            CDCDTE                    TEXT('Creation date')
     A            CDOBRC                    TEXT('Override Branch')
     A            CDDRET                    TEXT('Default Retention Period')
     A            CDPLAB                    TEXT('Print Address Label')
     A            CDATDT                    TEXT('Activation Date')
     A            CDDADT                    TEXT('De-Activation Date')
     A            CDCSTR                    TEXT('Structure Type')
     A            CDLCCD                    TEXT('Location Code')
     A            CDFAX                     TEXT('Fax Number')
     A            CDTEL                     TEXT('Telephone Number')
     A            CDCREF                    TEXT('Clearing Reference')
     A            CDDYNB                    TEXT('Default print day number')
     A            CDERT                     TEXT('Electronic route code')
     A            CDDMSH                    TEXT('Default Mail Shot code')
     A            CDCCCY                    TEXT('Charge Currency')
     A            CDCITM                    TEXT('Charge per item')
     A            CDBSNB                    TEXT('Charge Not Posted')
     A            CDCACC                    TEXT('Charge Accumlated to-date')
     A            CDCFRQ                    TEXT('Post charge frequency')
     A            CDNTDT                    TEXT('Next Post Date')
     A            CDACHG                    TEXT('Apply Charge')
     A            CDCACT                    TEXT('Charge Account')
     A            CDPDAY                    TEXT('Post Charges day number')
     A            CDDESC                    TEXT('Description')
     A            CDAJOB                    TEXT('Job name')
     A            CDAUSR                    TEXT('User name')
     A            CDATIM                    TEXT('Action Time')
     A            CDARDT                    TEXT('Action Date')
     A            CDAACT                    TEXT('Action Type')
     A            CDRDNB                    TEXT('Run day number')
      **Start of change CCG015
     A            CDEML                     TEXT('Email Address')
      **End of change CCG015
     A*.....................................................................
     A* Key fields
     A          K CDBRCA
     A*.....................................................................
     A* SELECT: Select Live Records for Branch
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S CDRECI                    COMP(EQ 'D')
     A* Field    : Relationship type
     A* Condition: Branch
     A            CDRTYP                    COMP(EQ 'B')
     A*.....................................................................
     A* SELECT: Select Live Records for System
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S CDRECI                    COMP(EQ 'D')
     A* Field    : Relationship type
     A* Condition: System Default
     A            CDRTYP                    COMP(EQ 'D')
     A*=====================================================================
