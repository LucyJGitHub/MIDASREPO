      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG Correspondent Details    By Account Id')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  LF/CGCSCHL3 - Correspondent Schedule    By Account Identifier*
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 23Nov94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A          R @CSCHL3                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(CGCSCHPD)
     A*---------------------------------------------------------------------
     A            CPCORR                    TEXT('Correspondent')
     A            CPPTYP                    TEXT('Print Item Type')
     A            CPPSTP                    TEXT('Print Item Sub-type')
     A            CPSEQN                    TEXT('Sequence Number')
     A            CPRECI                    TEXT('Record Identifier')
     A            CPPSCH                    TEXT('Print Schedule')
     A            CPPCOR                    TEXT('Primary Correspondent')
     A            CPLGID                    TEXT('Language identifier')
     A            CPACCD                    TEXT('Account Code')
     A            CPNPDT                    TEXT('Next Print Date')
     A            CPMACT                    TEXT('Midas Account Identifier')
     A            CPPLAB                    TEXT('Print Address Label')
     A            CPSFDY                    TEXT('Frequency Day Number')
     A            CPRETP                    TEXT('Retention Period')
     A            CPMSHC                    TEXT('Mail Shot code')
     A            CPAPDF                    TEXT('Defaults must be applied')
     A            CPOSQN                    TEXT('Output Sequence Number')
     A            CPNPAG                    TEXT('Number of Pages')
     A            CPTPAG                    TEXT('Total Number of Pages')
     A            CPRDTE                    TEXT('Reset Date')
     A            CPRFRQ                    TEXT('Reset Frequency')
     A            CPRSDN                    TEXT('Reset Day Number')
     A            CPCCCY                    TEXT('Charge Currency')
     A            CPCITM                    TEXT('Charge per item')
     A            CPBSNB                    TEXT('Charge Not Posted')
     A            CPCACC                    TEXT('Charge Accumlated to-date')
     A            CPCFRQ                    TEXT('Post charge frequency')
     A            CPNTDT                    TEXT('Next Post Date')
     A            CPACHG                    TEXT('Apply Charge')
     A            CPCACT                    TEXT('Charge Account')
     A            CPPDAY                    TEXT('Post Charges day number')
     A            CPAJOB                    TEXT('Job name')
     A            CPAUSR                    TEXT('User name')
     A            CPATIM                    TEXT('Action Time')
     A            CPARDT                    TEXT('Action Date')
     A            CPAACT                    TEXT('Action Type')
     A            CPRDNB                    TEXT('Run day number')
     A*.....................................................................
     A* Key fields
     A          K CPMACT
     A          K CPCORR
     A          K CPPTYP
     A          K CPPSTP
     A          K CPSEQN
     A*.....................................................................
     A* SELECT: Select Live Records and Accounts
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S CPRECI                    COMP(EQ 'D')
     A* Field    : Midas Account Identifier
     A* Condition: Entered
     A            CPMACT                    COMP(NE ' ')
     A*=====================================================================
