      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG Correspondent schedule by print item type')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  LF/CGCSCHLA - Correspondent Schedule    By Print Item Type.  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD054955 *REDUNDANTDate 16Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 095467             Date 24Oct95               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD054955 - Deliverable Data Split for Correspondence Mgr     *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  095467 - Create sub order for schedules.                     *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
      *=====================================================================
     A          R @CSCHLA                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(CGCSCHPD)
      *---------------------------------------------------------------------
     A            CPCORR                    TEXT('Correspondent')
     A            CPPTYP                    TEXT('Print Item Type')
     A            CPPSTP                    TEXT('Print Item Sub-type')
     A            CPSEQN                    TEXT('Sequence Number')
     A            CPRECI                    TEXT('Record Identifier')
     A            CPPSCH                    TEXT('Print Schedule')
     A            CPPCOR                    TEXT('Primary Correspondent')
     A            CPLGID                    TEXT('Language identifier')
     A            CPACCD                    TEXT('Account Code')
     A            CPNPDT                    TEXT('Next Print Date')
     A            CPMACT                    TEXT('Midas Account Identifier')
     A            CPPLAB                    TEXT('Print Address Label')
     A            CPSFDY                    TEXT('Frequency Day Number')
     A            CPRETP                    TEXT('Retention Period')
     A            CPMSHC                    TEXT('Mail Shot code')
     A            CPAPDF                    TEXT('Defaults must be applied')
     A            CPOSQN                    TEXT('Output Sequence Number')
     A            CPNPAG                    TEXT('Number of Pages')
     A            CPTPAG                    TEXT('Total Number of Pages')
     A            CPRDTE                    TEXT('Reset Date')
     A            CPRFRQ                    TEXT('Reset Frequency')
     A            CPRSDN                    TEXT('Reset Day Number')
     A            CPCCCY                    TEXT('Charge Currency')
     A            CPCITM                    TEXT('Charge per item')
     A            CPBSNB                    TEXT('Charge Not Posted')
     A            CPCACC                    TEXT('Charge Accumlated to-date')
     A            CPCFRQ                    TEXT('Post charge frequency')
     A            CPNTDT                    TEXT('Next Post Date')
     A            CPACHG                    TEXT('Apply Charge')
     A            CPCACT                    TEXT('Charge Account')
     A            CPPDAY                    TEXT('Post Charges day number')
     A            CPAJOB                    TEXT('Job name')
     A            CPAUSR                    TEXT('User name')
     A            CPATIM                    TEXT('Action Time')
     A            CPARDT                    TEXT('Action Date')
     A            CPAACT                    TEXT('Action Type')
     A            CPRDNB                    TEXT('Run day number')
      *.....................................................................
      * Key fields
     A          K CPCORR
     A          K CPPTYP
     A          K CPPSTP
     A          K CPPCOR
     A          K CPACCD
     A          K CPMACT
     A          K CPLGID
     A          K CPSEQN
      *.....................................................................
      * SELECT: Select only Live Records
      * Field    : Record Identifier
      * Condition: Live Record
     A          S CPRECI                    COMP(EQ 'D')
      *=====================================================================
