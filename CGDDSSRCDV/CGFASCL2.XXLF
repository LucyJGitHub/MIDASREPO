      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG RDE/Format Option Assoc.By Format Option')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  LF/CGFASCL1 - RDE/Format Option Assoc.  By Format Option     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD054955 *REDUNDANTDate 16Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 23Nov94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD054955 - Deliverable Data Split for Correspondence Mgr     *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @FASCL2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      JFILE(CGFASCPD +
     A                                            CGRDTAPD +
     A                                            CGFGRPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from RDE/Format Option Assoc. to Report Data Elements
     A          J                           JOIN(1 2)
     A                                      JFLD(FARDE RDRDE)
     A*---------------------------------------------------------------------
     A* Join from RDE/Format Option Assoc. to Format Group
     A          J                           JOIN(1 3)
     A                                      JFLD(FAFOPT FGFOPT)
     A*---------------------------------------------------------------------
     A            FARDE                     TEXT('Report Data Element')
     A                                      JREF(1)
     A            FARELT                    TEXT('RDE Element type')
     A                                      JREF(2)
     A                                      RENAME(RDELYP)
     A            FAFOPT                    TEXT('Formatting Options')
     A                                      JREF(1)
     A            FAFELT                    TEXT('Format Element Type')
     A                                      JREF(3)
     A                                      RENAME(FGELYP)
     A            FARECI                    TEXT('Record Identifier')
     A                                      JREF(1)
     A            FADESC                    TEXT('Description')
     A                                      JREF(1)
     A            FAAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            FAAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            FAATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            FAARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            FAAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            FARDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K FAFOPT
     A          K FARDE
     A*.....................................................................
     A* SELECT: Select Live records only
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S FARECI                    COMP(EQ 'D')
     A*=====================================================================
