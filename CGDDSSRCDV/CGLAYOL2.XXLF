      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG Layout Reference         By Prnt Itm Typ')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  LF/CGLAYOL2 - Layout Reference          By Print Item Type   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD054955 *REDUNDANTDate 16Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCG009             Date 01Jun95               *
      *                 S01522             Date 24Nov94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD054955 - Deliverable Data Split for Correspondence Mgr     *
      *  MD046248 - Finastra Rebranding                               *
     A*  CCG009 - Private Banking UDC                                 *
     A*           (RECOMPILED)                                        *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @LAYOL2                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      JFILE(CGLAYOPD +
     A                                            CGPTYPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Layout Reference to Print Item Type/Sub-Type
     A          J                           JOIN(1 2)
     A                                      JFLD(LYPTYP ITPTYP)
     A                                      JFLD(LYPSTP ITPSTP)
     A*---------------------------------------------------------------------
     A            LYLAYO                    TEXT('Layout Identifier')
     A                                      JREF(1)
     A            LYRECI                    TEXT('Record Identifier')
     A                                      JREF(1)
     A            LYPTYP                    TEXT('Print Item Type')
     A                                      JREF(1)
     A            LYPSTP                    TEXT('Print Item Sub-type')
     A                                      JREF(1)
     A            LYGRPS                    TEXT('Group Set Name')
     A                                      JREF(2)
     A                                      RENAME(ITGRPS)
     A            LYPRTF                    TEXT('Printer Profile')
     A                                      JREF(1)
     A            LYDESC                    TEXT('Description')
     A                                      JREF(1)
     A            LYAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            LYAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            LYATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            LYARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            LYAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            LYRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K LYPTYP
     A          K LYPSTP
     A          K LYLAYO
     A*.....................................................................
     A* SELECT: Select only Live Records
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S LYRECI                    COMP(EQ 'D')
     A*=====================================================================
