      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG UDC Path Information     Retrieval index')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  LF/CGPATHL1 - UDC Path Information      Retrieval index      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD054955 *REDUNDANTDate 16Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 24Nov94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD054955 - Deliverable Data Split for Correspondence Mgr     *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      UNIQUE
     A                                      DYNSLT
     A*=====================================================================
     A          R @PATHL1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      JFILE(CGPATHPD +
     A                                            CGGSETPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from UDC Path Information to Group Set Definition
     A          J                           JOIN(1 2)
     A                                      JFLD(EPLEV1 GSGRPS)
     A*---------------------------------------------------------------------
     A            EPPATH                    TEXT('Path Name')
     A                                      JREF(1)
     A            EPRECI                    TEXT('Record Identifier')
     A                                      JREF(1)
     A            EPLEV1                    TEXT('Level 1 Group Set Name')
     A                                      JREF(1)
     A            EPMREC                    TEXT('Multiple Indicator')
     A                                      JREF(2)
     A                                      RENAME(GSMREC)
     A            EPLEV2                    TEXT('Level 2 Group Set Name')
     A                                      JREF(1)
     A            EPLEV3                    TEXT('Level 3 Group Set Name')
     A                                      JREF(1)
     A            EPLEV4                    TEXT('Level 4 Group Set Name')
     A                                      JREF(1)
     A            EPLEV5                    TEXT('Level 5 Group Set Name')
     A                                      JREF(1)
     A            EPLEV6                    TEXT('Level 6 Group Set Name')
     A                                      JREF(1)
     A            EPLEV7                    TEXT('Level 7 Group Set Name')
     A                                      JREF(1)
     A            EPLEV8                    TEXT('Level 8 Group Set Name')
     A                                      JREF(1)
     A            EPLEV9                    TEXT('Level 9 Group Set Name')
     A                                      JREF(1)
     A            EPLEV0                    TEXT('Level 10 Group Set Name')
     A                                      JREF(1)
     A            EPDESC                    TEXT('Description')
     A                                      JREF(1)
     A            EPAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            EPAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            EPATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            EPARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            EPAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            EPRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K EPPATH
     A*.....................................................................
     A* SELECT: Live records only
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S EPRECI                    COMP(EQ 'D')
     A*=====================================================================
