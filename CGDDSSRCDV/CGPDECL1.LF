      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMNM                                                      *                       CPK014
/*EXI *  DTAMBRS((CGPDECPD (CGPDECPD)) (CGPATHPD (CGPATHPD))          *                       CPK014
/*EXI *  (CGGSETPD (CGGSETPD)))                                       *                       CPK014
/*EXI *  TEXT('Midas CG Plane declaration retrieval')
/*E*S ***LF_IMNMSN*****************************************************                       CPK014
/*E*I ***MBR(*NONE)****************************************************                       CPK014
/*X*I****CHGLF*CGPDECL1*SHARE(*YES)************************************                       CPK014
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                          *
     A*                                                               *
     A*  LF/CGPDECL1 - Plane Declaration         Retrieval index      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CPK014             Date 10May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 24NOV94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CPK014 - Correction to creation parameters to allow the file *
     A*           to be created with a member.                        *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A*=====================================================================
     A                                      JDFTVAL
     A                                      UNIQUE
     A                                      DYNSLT
     A*=====================================================================
     A          R @PDECL1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(CGPDECPD +
     A                                            CGPATHPD +
     A                                            CGGSETPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Plane Declaration to UDC Path Information
     A          J                           JOIN(1 2)
     A                                      JFLD(LPRPTH EPPATH)
     A*---------------------------------------------------------------------
     A* Join from UDC Path Information to Group Set Definition
     A          J                           JOIN(2 3)
     A                                      JFLD(EPLEV1 GSGRPS)
     A*---------------------------------------------------------------------
     A            LPLAYO                    TEXT('Layout Identifier')
     A                                      JREF(1)
     A            LPPLAN                    TEXT('Plane Name')
     A                                      JREF(1)
     A            LPRECI                    TEXT('Record Identifier')
     A                                      JREF(1)
     A            LPSEQA                    TEXT('Order/Sequence')
     A                                      JREF(1)
     A            LPTOPP                    TEXT('Top Level Plane Indicator')
     A                                      JREF(1)
     A            LPRPTH                    TEXT('Repeating Path Path Name')
     A                                      JREF(1)
     A            LPMREC                    TEXT('Multiple Indicator')
     A                                      JREF(3)
     A                                      RENAME(GSMREC)
     A            LPRROW                    TEXT('Relative Row')
     A                                      JREF(1)
     A            LPRCOL                    TEXT('Relative Column')
     A                                      JREF(1)
     A            LPOFOP                    TEXT('Overflow Option')
     A                                      JREF(1)
     A            LPSROW                    TEXT('Start Row')
     A                                      JREF(1)
     A            LPSCOL                    TEXT('Start Column')
     A                                      JREF(1)
     A            LPEROW                    TEXT('End Row')
     A                                      JREF(1)
     A            LPECOL                    TEXT('End Column')
     A                                      JREF(1)
     A            LPOROW                    TEXT('Overflow Row')
     A                                      JREF(1)
     A            LPOCOL                    TEXT('Overflow Column')
     A                                      JREF(1)
     A            LPOINR                    TEXT('Overflow Indent Row')
     A                                      JREF(1)
     A            LPOINC                    TEXT('Overflow Indent Column')
     A                                      JREF(1)
     A            LPOVTR                    TEXT('Overflow Vertex Row')
     A                                      JREF(1)
     A            LPOVTC                    TEXT('Overflow Vertex Column')
     A                                      JREF(1)
     A            LPOFAB                    TEXT('Overflow Before or After')
     A                                      JREF(1)
     A            LPFROW                    TEXT('First Row')
     A                                      JREF(1)
     A            LPFCOL                    TEXT('First Column')
     A                                      JREF(1)
     A            LPFINR                    TEXT('First Indent Row')
     A                                      JREF(1)
     A            LPFINC                    TEXT('First Indent Column')
     A                                      JREF(1)
     A            LPFVTR                    TEXT('First Vertex Row')
     A                                      JREF(1)
     A            LPFVTC                    TEXT('First Vertex Column')
     A                                      JREF(1)
     A            LPBOXD                    TEXT('Box Dimensions')
     A                                      JREF(1)
     A            LPFILS                    TEXT('Fill Strategy')
     A                                      JREF(1)
     A            LPPCON                    TEXT('Plane Condition')
     A                                      JREF(1)
     A            LPTCGP                    TEXT('To Condition Group')
     A                                      JREF(1)
     A            LPDESC                    TEXT('Description')
     A                                      JREF(1)
     A            LPFPAB                    TEXT('Force Page After/Before')
     A                                      JREF(1)
     A            LPAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            LPAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            LPATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            LPARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            LPAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            LPRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K LPLAYO
     A          K LPPLAN
     A*.....................................................................
     A* SELECT: Live records only
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S LPRECI                    COMP(EQ 'D')
     A*=====================================================================
