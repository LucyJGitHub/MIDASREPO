      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMNMSN                                                    *
/*EXI *  MBR(*NONE)                                                   *
/*EXI *  TEXT('Midas CG Plane definition by numer format')
/*ADD *  CHGLF CGPDEFL4 SHARE(*YES)                                   *
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                          *
     A*                                                               *
     A*  LF/CGPDEFL4 - Plane Definition          By Numeric Format    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01522             DATE 24NOV94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A*=====================================================================
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @PDEFL4                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(CGPDEFPD +
     A                                            CGRDTAPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Plane Definition to Report Data Elements
     A          J                           JOIN(1 2)
     A                                      JFLD(LRRDE RDRDE)
     A*---------------------------------------------------------------------
     A            LRLAYO                    TEXT('Layout Identifier')
     A                                      JREF(1)
     A            LRPLAN                    TEXT('Plane Name')
     A                                      JREF(1)
     A            LRFLDN                    TEXT('Field Name')
     A                                      JREF(1)
     A            LRRECI                    TEXT('Record Identifier')
     A                                      JREF(1)
     A            LRSEQA                    TEXT('Order/Sequence')
     A                                      JREF(1)
     A            LRRPTH                    TEXT('Repeating Path Path Name')
     A                                      JREF(1)
     A            LRNPTH                    TEXT('Non-Repeating Path Path')
     A                                      JREF(1)
     A            LRRDE                     TEXT('Report Data Element')
     A                                      JREF(1)
     A            LRELYP                    TEXT('Element Type')
     A                                      JREF(2)
     A                                      RENAME(RDELYP)
     A            LRLITD                    TEXT('Literal/Data')
     A                                      JREF(1)
     A            LRD256                    TEXT('Data (256)')
     A                                      JREF(1)
     A            LRRROW                    TEXT('Relative Row')
     A                                      JREF(1)
     A            LRRCOL                    TEXT('Relative Column')
     A                                      JREF(1)
     A            LROFOP                    TEXT('Overflow Option')
     A                                      JREF(1)
     A            LRSROW                    TEXT('Start Row')
     A                                      JREF(1)
     A            LRSCOL                    TEXT('Start Column')
     A                                      JREF(1)
     A            LREROW                    TEXT('End Row')
     A                                      JREF(1)
     A            LRECOL                    TEXT('End Column')
     A                                      JREF(1)
     A            LROROW                    TEXT('Overflow Row')
     A                                      JREF(1)
     A            LROCOL                    TEXT('Overflow Column')
     A                                      JREF(1)
     A            LROINR                    TEXT('Overflow Indent Row')
     A                                      JREF(1)
     A            LROINC                    TEXT('Overflow Indent Column')
     A                                      JREF(1)
     A            LROVTR                    TEXT('Overflow Vertex Row')
     A                                      JREF(1)
     A            LROVTC                    TEXT('Overflow Vertex Column')
     A                                      JREF(1)
     A            LROFAB                    TEXT('Overflow Before or After')
     A                                      JREF(1)
     A            LRFROW                    TEXT('First Row')
     A                                      JREF(1)
     A            LRFCOL                    TEXT('First Column')
     A                                      JREF(1)
     A            LRFINR                    TEXT('First Indent Row')
     A                                      JREF(1)
     A            LRFINC                    TEXT('First Indent Column')
     A                                      JREF(1)
     A            LRFVTR                    TEXT('First Vertex Row')
     A                                      JREF(1)
     A            LRFVTC                    TEXT('First Vertex Column')
     A                                      JREF(1)
     A            LRBOXD                    TEXT('Box Dimensions')
     A                                      JREF(1)
     A            LRFILS                    TEXT('Fill Strategy')
     A                                      JREF(1)
     A            LRCFMO                    TEXT('Character Format Group')
     A                                      JREF(1)
     A            LRCFOP                    TEXT('Character Format Type')
     A                                      JREF(1)
     A            LRCOPT                    TEXT('Code Format Group')
     A                                      JREF(1)
     A            LRCFTP                    TEXT('Code Format Type')
     A                                      JREF(1)
     A            LRNFMT                    TEXT('Numeric Format Group')
     A                                      JREF(1)
     A            LRNFOP                    TEXT('Numeric Format Type')
     A                                      JREF(1)
     A            LRALGN                    TEXT('Alignment')
     A                                      JREF(1)
     A            LROLEN                    TEXT('Output Length')
     A                                      JREF(1)
     A            LRFCON                    TEXT('Field Condition')
     A                                      JREF(1)
     A            LRTCGP                    TEXT('To Condition Group')
     A                                      JREF(1)
     A            LRDESC                    TEXT('Description')
     A                                      JREF(1)
     A            LRLCVL                    TEXT('Last/Current value')
     A                                      JREF(1)
     A            LRAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            LRAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            LRATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            LRARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            LRAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            LRRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K LRNFMT
     A          K LRNFOP
     A          K LRLAYO
     A          K LRPLAN
     A          K LRFLDN
     A*.....................................................................
     A* SELECT: Select only Live Records
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S LRRECI                    COMP(EQ 'D')
     A*=====================================================================
