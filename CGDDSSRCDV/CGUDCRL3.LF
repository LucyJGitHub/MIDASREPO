      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG UDC Data Reference       By Primary Corr')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  LF/CGUDCRL3 - UDC Data Reference     By Primary Correspondent*
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 24Nov94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A          R @UDCRL3                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(CGUDCRPD)
     A*---------------------------------------------------------------------
     A            DRITEM                    TEXT('Item Identifier')
     A            DRDCOR                    TEXT('Destination Correspondent')
     A            DRRECI                    TEXT('Record Identifier')
     A            DRPCOR                    TEXT('Primary Correspondent')
     A            DRBRCA                    TEXT('Branch Code')
     A            DRORBR                    TEXT('Originating Branch Code')
     A            DROVBR                    TEXT('Overriding Branch Code')
     A            DRMODI                    TEXT('Module id')
     A            DRMTRN                    TEXT('Midas Transaction Ref.')
     A            DRMACT                    TEXT('Midas Account Identifier')
     A            DRISTS                    TEXT('Item Status')
     A            DRPTYP                    TEXT('Print Item Type')
     A            DRPSTP                    TEXT('Print Item Sub-type')
     A            DRATRM                    TEXT('Auto-Transmission Ind.')
     A            DRPRTD                    TEXT('Scheduled Print Date')
     A            DRDDAT                    TEXT('Scheduled Drop Date')
     A            DRLGID                    TEXT('Language identifier')
     A            DRMSHT                    TEXT('Mail Shot Identifier')
     A            DRCHQR                    TEXT('Cheque Reference')
     A            DRFREF                    TEXT('Further Reference')
     A            DRAJOB                    TEXT('Job name')
     A            DRAUSR                    TEXT('User name')
     A            DRATIM                    TEXT('Action Time')
     A            DRARDT                    TEXT('Action Date')
     A            DRAACT                    TEXT('Action Type')
     A            DRRDNB                    TEXT('Run day number')
     A*.....................................................................
     A* Key fields
     A          K DRPCOR
     A          K DRITEM
     A*.....................................................................
     A* SELECT: Select only Live Records
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S DRRECI                    COMP(EQ 'D')
     A*=====================================================================
