      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG UDC Data Reference       By Prnt Itm Sts')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  LF/CGUDCRL7 - UDC Data Reference        By Print Item Status *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCG009             Date 01Jun95               *
      *                 S01522             Date 24Nov94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  CCG009 - Private Banking UDC                                 *
     A*           (RECOMPILED)                                        *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @UDCRL7                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      JFILE(CGUDCRPD +
     A                                            CGPTYPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from UDC Data Reference to Print Item Type/Sub-Type
     A          J                           JOIN(1 2)
     A                                      JFLD(DRPTYP ITPTYP)
     A                                      JFLD(DRPSTP ITPSTP)
     A*---------------------------------------------------------------------
     A            DRITEM                    TEXT('Item Identifier')
     A                                      JREF(1)
     A            DRDCOR                    TEXT('Destination Correspondent')
     A                                      JREF(1)
     A            DRRECI                    TEXT('Record Identifier')
     A                                      JREF(1)
     A            DRPCOR                    TEXT('Primary Correspondent')
     A                                      JREF(1)
     A            DRBRCA                    TEXT('Branch Code')
     A                                      JREF(1)
     A            DRORBR                    TEXT('Originating Branch Code')
     A                                      JREF(1)
     A            DROVBR                    TEXT('Overriding Branch Code')
     A                                      JREF(1)
     A            DRMODI                    TEXT('Module id')
     A                                      JREF(1)
     A            DRMTRN                    TEXT('Midas Transaction Ref.')
     A                                      JREF(1)
     A            DRMACT                    TEXT('Midas Account Identifier')
     A                                      JREF(1)
     A            DRISTS                    TEXT('Item Status')
     A                                      JREF(1)
     A            DRPTYP                    TEXT('Print Item Type')
     A                                      JREF(1)
     A            DRPSTP                    TEXT('Print Item Sub-type')
     A                                      JREF(1)
     A            DRPGRP                    TEXT('Print Item Group Set Name')
     A                                      JREF(2)
     A                                      RENAME(ITGRPS)
     A            DRATRM                    TEXT('Auto-Transmission Ind.')
     A                                      JREF(1)
     A            DRPRTD                    TEXT('Scheduled Print Date')
     A                                      JREF(1)
     A            DRDDAT                    TEXT('Scheduled Drop Date')
     A                                      JREF(1)
     A            DRLGID                    TEXT('Language identifier')
     A                                      JREF(1)
     A            DRMSHT                    TEXT('Mail Shot Identifier')
     A                                      JREF(1)
     A            DRCHQR                    TEXT('Cheque Reference')
     A                                      JREF(1)
     A            DRFREF                    TEXT('Further Reference')
     A                                      JREF(1)
     A            DRAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            DRAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            DRATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            DRARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            DRAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            DRRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K DRISTS
     A          K DRITEM
     A          K DRDCOR
     A*.....................................................................
     A* SELECT: Select only Live Records
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S DRRECI                    COMP(EQ 'D')
     A*=====================================================================
