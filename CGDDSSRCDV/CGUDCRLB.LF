      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG UDC Data Reference       Update index')       *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  LF/CGUDCRLB - UDC Data Reference        Update index         *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD031764           Date 09Jan15               *
      *                 AR797795 *CREATE   Date 19Jul11               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD031764 - Correct KEY as DRDCOR must be added.              *
      *  AR797795 - Interest Scale Report Enquiry is missing in       *
      *              BFMidas                                          *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE
     A          R @UDCRLB                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2011')
     A                                      PFILE(CGUDCRPD)
     A            DRITEM                    TEXT('Item Identifier')
     A            DRRECI                    TEXT('Record Identifier')
     A            DRDCOR                    TEXT('Destination Correspondent')
     A            DRPCOR                    TEXT('Primary Correspondent')
     A            DRBRCA                    TEXT('Branch Code')
     A            DRORBR                    TEXT('Originating Branch Code')
     A            DROVBR                    TEXT('Overriding Branch Code')
     A            DRMODI                    TEXT('Module id')
     A            DRMTRN                    TEXT('Midas Transaction Ref.')
     A            DRMACT                    TEXT('Midas Account Identifier')
     A            DRISTS                    TEXT('Item Status')
     A            DRPTYP                    TEXT('Print Item Type')
     A            DRPSTP                    TEXT('Print Item Sub-type')
     A            DRATRM                    TEXT('Auto-Transmission Ind.')
     A            DRPRTD                    TEXT('Scheduled Print Date')
     A            DRDDAT                    TEXT('Scheduled Drop Date')
     A            DRLGID                    TEXT('Language identifier')
     A            DRMSHT                    TEXT('Mail Shot Identifier')
     A            DRCHQR                    TEXT('Cheque Reference')
     A            DRFREF                    TEXT('Further Reference')
     A            DRAJOB                    TEXT('Job name')
     A            DRAUSR                    TEXT('User name')
     A            DRATIM                    TEXT('Action Time')
     A            DRARDT                    TEXT('Action Date')
     A            DRAACT                    TEXT('Action Type')
     A            DRRDNB                    TEXT('Run day number')
      * Key fields
     A          K DRITEM
      ** Start of change MD031764
     A          K DRDCOR
      ** End of change MD031764
     A          S DRRECI                    COMP(EQ 'D')
     A            DRPTYP                    COMP(EQ 'INTSCL')
