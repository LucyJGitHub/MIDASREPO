      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas CG Arc data reference by correspondant')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  LF/CGXDCRL8 - Arc Data Reference        By Correspondent     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 096452             Date 27Nov95               *
      *                 CCG009             Date 01Jun95               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  096452 - Correct based on file CGUDCRPD to CGXDCRPD          *
     A*  CCG009 - Private Banking UDC  (NEW FILE)                     *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @XDCRL8                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      * Start 096452
     A**********                            JFILE(CGUDCRPD +
     A                                      JFILE(CGXDCRPD +
      * End 096452
     A                                            CGPTYPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Data Reference to Print Item Sub Type
     A          J                           JOIN(1 2)
     A                                      JFLD(DRPTYP ITPTYP)
     A                                      JFLD(DRPSTP ITPSTP)
     A*---------------------------------------------------------------------
     A            DRITEM                    TEXT('Item Identifier')
     A            DRDCOR                    TEXT('Destination Correspondent')
     A            DRRECI                    TEXT('Record Identifier')
     A            DRPCOR                    TEXT('Primary Correspondent')
     A            DRBRCA                    TEXT('Branch Code')
     A            DRORBR                    TEXT('Originating Branch Code')
     A            DROVBR                    TEXT('Overriding Branch Code')
     A            DRMODI                    TEXT('Module id')
     A            DRMTRN                    TEXT('Midas Transaction Ref.')
     A            DRMACT                    TEXT('Midas Account Identifier')
     A            DRISTS                    TEXT('Item Status')
     A            DRPTYP                    TEXT('Print Item Type')
     A            DRPSTP                    TEXT('Print Item Sub-type')
     A            DRATRM                    TEXT('Auto-Transmission Ind.')
     A            DRPRTD                    TEXT('Scheduled Print Date')
     A            DRDDAT                    TEXT('Scheduled Drop Date')
     A            DRLGID                    TEXT('Language identifier')
     A            DRMSHT                    TEXT('Mail Shot Identifier')
     A            DRCHQR                    TEXT('Cheque Reference')
     A            DRFREF                    TEXT('Further Reference')
     A            DRAJOB                    TEXT('Job name')
     A            DRAUSR                    TEXT('User name')
     A            DRATIM                    TEXT('Action Time')
     A            DRARDT                    TEXT('Action Date')
     A            DRAACT                    TEXT('Action Type')
     A            DRRDNB                    TEXT('Run day number')
     A            ITGPHD                    TEXT('Group Header/Detail')
     A*.....................................................................
     A* Key fields
     A          K DRDCOR
     A          K DRITEM
     A*.....................................................................
     A* SELECT: Select only Live Records
     A* Field    : Record Identifier
     A* Condition: Live Record
     A          S DRRECI                    COMP(EQ 'D')
     A* SELECT: Omit if group detail
     A* Field    : Omit if group detail
     A* Condition: 'Y'
     A            ITGPHD                    COMP(NE 'D')
     A*=====================================================================
