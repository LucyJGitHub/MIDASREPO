      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CG Print Group Set Structure')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                          *
     A*                                                               *
     A*  PRTF/CG1160P1 - CG1160 Print Group Set Structure             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 126690             Date 26Nov97               *
      *                 S01522             Date 24Nov94               *
     A*                 Xnnnnn             DATE ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  126690 - Expand RDE Detail Line                              *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R F0HDR1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  6'REFERENCE'
     A            H1REFN        10      3 16
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3100'DATE'
     A            BJMRDT    R           3105REFFLD(RUNA)
     A                                  3119'PAGE'
     A            PAGE1          4  0   3124EDTCDE(Z)
      *
      * Print Relevant Report Heading
      *
     A          R F0HDR2                    TEXT('Header Titles')
     A            H2TITL        60      5 37
     A            H2TUL         60      6 37
      *
      * Print Top Level Group Set Information
      *
     A          R F1HDR1                    TEXT('Top Group Header')
     A                                  8  1'Top Group Set :'
     A            H3GRPS         6      8 16
     A            H3DESC        30      8 28
     A                                 10  1'Complex Set Indicator....:'
     A            H3CSET         1     10 28TEXT('Complex Set Indicator')
     A                                 11  1'Multiple Indicator.......:'
     A            H3MREC         1     11 28TEXT('Multiple Indicator')
     A                                 12  1'Restricted Set Indicator.:'
     A            H3RSET         1     12 28TEXT('Restricted Set Indicator')
     A                                 13  1'Mail Merge Group.........:'
     A            H3MMGR         1     13 28TEXT('Mail Merge Group')
      *
      * Print Simple Group set information
      *
     A          R F1DTL1                    TEXT('Simple Group Set')
     A                                      SPACEB(2)
     A                                     1'********************-
     A                                      ********************-
     A                                      ********************-
     A                                      ********************-
     A                                      ********************-
     A                                      ********************-
     A                                      ************'
     A                                      SPACEA(2)
     A                                     1'    Group Set :'
     A            D1GRPS         6        16
     A            D1DESC        30        28
     A                                      SPACEA(2)
     A                                     1'Complex Set Indicator....:'
     A            D1CSET         1        28TEXT('Complex Set Indicator')
     A                                      SPACEA(1)
     A                                     1'Multiple Indicator.......:'
     A            D1MREC         1        28TEXT('Multiple Indicator')
     A                                      SPACEA(1)
     A                                     1'Restricted Set Indicator.:'
     A            D1RSET         1        28TEXT('Restricted Set Indicator')
     A                                      SPACEA(1)
     A                                     1'Mail Merge Group.........:'
     A            D1MMGR         1        28TEXT('Mail Merge Group')
     A                                      SPACEA(1)
      *
      * Print Path Information
      *
     A          R F1DTL2                    TEXT('Path Information')
     A                                      SPACEB(1)
     A                                     1'Path :'
     A            D2PTH1       120         8
     A                                      SPACEA(1)
     A            D2PTH2       120         8
     A                                      SPACEA(2)
     A                                     1'Top/Repeating Path Path Name.:'
     A            D2RPTH         6        36TEXT('Repeating Path Path Name')
     A                                      SPACEA(1)
     A                                     1'Non-Repeating Path Path......:'
     A            D2NPTH         6        36TEXT('Non-Repeating Path Path')
     A                                      SPACEA(1)
      *
      * Print Report Data Elements for Group Set Headings
      *
     A          R F3DTL1                    TEXT('RDE Headings')
     A                                      SPACEB(2)
     A                                    10'Report Data Element'
     A                                    35'Description'
      *                                          /126690 Begin/
     A                                    67'Element Type'
     A                                    82'Maximum Length'
     A                                   100'Example Data'
      *                                          /126690 End/
     A                                      SPACEA(1)
      *
      * Print Report Data Elements for Group Set
      *
     A          R F4DTL1                    TEXT('RDE Detail')
     A            D4RDE         10        10TEXT('Report Data Element')
     A                                      SPACEB(1)
     A            D4DESC        30        35TEXT('Description')
      *                                          /126690 Begin/
     A            D4ELYP         6        67TEXT('Element Type')
     A            D4MAXL         5  0     82TEXT('Maximum Length')
     A            D4ETXT        25       100TEXT('Example Data')
      *                                          /126690 End/
      *
      * Print No Report Elements
      *
     A          R F5DTL1                    TEXT('No Details to Report')
     A                                      SPACEB(2)
     A                                    56'*** No Details to Report ***'
      *
      *  End of Report
      *
     A          R F0TRL1                    TEXT('End of Report')
     A                                      SPACEB(2)
     A                                    56'*** End of Report ***'
      *
      *  Database Error Detail
      *
     A          R F0DBERR                   TEXT('Database Error')
     A                                      SPACEB(1)
     A                                    30'Database Error'
     A                                      SPACEB(2)
     A                                    30'Error in Program..:'
     A                                      SPACEB(2)
     A            ERPGM     R             50REFFLD(ERPGM CGERDTA)
     A                                    30'File..............:'
     A                                      SPACEB(2)
     A            ERFILE    R             50REFFLD(ERFILE CGERDTA)
     A                                    30'Key...............:'
     A                                      SPACEB(2)
     A            ERKEY     R             50REFFLD(ERKEY CGERDTA)
     A                                    30'Subroutine........:'
     A                                      SPACEB(2)
     A            ERSTK     R             50REFFLD(ERSTK CGERDTA)
     A                                    30'Error number......:'
     A                                      SPACEB(2)
     A            ERERNB    R             50EDTCDE(J)
     A                                      REFFLD(ERERNB CGERDTA)
     A                                    30'Narrative.........:'
     A                                      SPACEB(2)
     A            ERNARR    R             50REFFLD(ERNARR CGERDTA)
     A                                    30'Primary reference.:'
     A                                      SPACEB(2)
     A            ERPREF    R             50REFFLD(ERPREF CGERDTA)
