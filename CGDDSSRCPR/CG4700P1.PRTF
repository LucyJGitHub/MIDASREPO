      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CG UDC Statement Driver Audit')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  PRTF/CG4700P1 - Statement Driver Audit                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 24Nov94               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  S01522 - User Defined Correspondence                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *---------------------------------------------------------------*
      * Report Heading                                                *
      *---------------------------------------------------------------*
     A          R F0HDR1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                       SKIPB(3)
      *
     A                                     6 'REFERENCE'
     A            R#PGM         10        16
     A            BJURPT    R             37 REFFLD(URPT)
     A                                   100 'DATE'
     A            BJMRDT    R            105 REFFLD(RUNA)
     A                                   119 'PAGE'
     A                                   124 PAGNBR
     A                                       EDTCDE(Z)
     A                                       SPACEA(2)
      *
     A            R#TITL        53        37
     A                                       SPACEA(1)
     A            R#TIUL        53        37
     A                                       SPACEA(2)
      *
     A            R#SECT        53        37
     A                                       SPACEA(2)
      *
      *---------------------------------------------------------------*
      * Column Headings                                               *
      *---------------------------------------------------------------*
     A          R F0HDR2                     TEXT('Column Headings')
      *
     A                                     4 'Branch'
     A**Start of change CGL029
     A**********                          12 'Account ID'                                     CGL029
     A**********                          33 'Selected'                                       CGL029
     A**********                          45 'Dropped'                                        CGL029
     A**********                          55 'Reason Dropped'                                 CGL029
     A**********                          78 'Reason Dropped'                                 CGL029
     A                                    17 'Account ID'
     A                                    39 'Selected'
     A                                    51 'Dropped'
     A                                    61 'Reason Dropped'
     A                                    83 'Reason Dropped'
     A**End of change CGL029
     A                                       SPACEA(1)
      *
     A**Start of change CGL029
     A**********                          33 'Midas'                                          CGL029
     A**********                          39 'UDC'                                            CGL029
     A**********                          44 'Midas'                                          CGL029
     A**********                          50 'UDC'                                            CGL029
     A**********                          55 'by Midas'                                       CGL029
     A**********                          78 'by UDC'                                         CGL029
     A                                    39 'Midas'
     A                                    45 'UDC'
     A                                    50 'Midas'
     A                                    56 'UDC'
     A                                    61 'by Midas'
     A                                    83 'by UDC'
     A**End of change CGL029
     A                                       SPACEA(3)
      *
      *---------------------------------------------------------------*
      * Detail Lines                                                  *
      *---------------------------------------------------------------*
     A          R F1DTL1                     TEXT('Detail Lines')
     A                                       SPACEA(1)
      *
     A            R#BRCA         3         5 TEXT('Branch')
     A            R#CNUM         6        12 TEXT('Customer')
     A                                    18 '-'
     A            R#CCY          3        19 TEXT('Currency')
     A                                    22 '-'
     A**Start of change CGL029
     A**********  R#ACOD         4        23 TEXT('Account Code')                             CGL029
     A**********                          27 '-'                                              CGL029
     A**********  R#ACSQ         2        28 TEXT('Account Seq.')                             CGL029
     A**********  R#OMID         1        35 TEXT('Output to Midas')                          CGL029
     A**********  R#OUDC         1        40 TEXT('Output to UDC')                            CGL029
     A**********  R#DMID         1        46 TEXT('Dropped from Midas')                       CGL029
     A**********  R#DUDC         1        51 TEXT('Dropped from UDC')                         CGL029
     A**********  R#RDMI        20        55 TEXT('Reason Drop. from Midas')                  CGL029
     A**********  R#RDUD        20        77 TEXT('Reason Drop. from UDC')                    CGL029
     A            R#ACOD        10        23 TEXT('Account Code')
     A                                    33 '-'
     A            R#ACSQ         2        34 TEXT('Account Seq.')
     A            R#OMID         1        41 TEXT('Output to Midas')
     A            R#OUDC         1        46 TEXT('Output to UDC')
     A            R#DMID         1        52 TEXT('Dropped from Midas')
     A            R#DUDC         1        57 TEXT('Dropped from UDC')
     A            R#RDMI        20        61 TEXT('Reason Drop. from Midas')
     A            R#RDUD        20        83 TEXT('Reason Drop. from UDC')
     A**End of change CGL029
      *
      *---------------------------------------------------------------*
      * Normal Statement Trailer                                      *
      *---------------------------------------------------------------*
     A          R F0TRL1                     TEXT('Normal Statement Trailer')
     A                                       SPACEB(2)
      *
     A                                    30 'No. of STMTAH records read:'
     A            R#NREC         9  0     62 TEXT('STMTAH recs. read')
     A                                       EDTWRD('       0 ')
     A                                       SPACEA(1)
     A                                    30 'No. of STMTAH records output:'
     A            R#SROP         9  0     62 TEXT('STMTAH recs. Output')
     A                                       EDTWRD('       0 ')
     A                                       SPACEA(1)
     A                                    30 'No. of UDC records output:'
     A            R#UROP         9  0     62 TEXT('UDC recs. Output')
     A                                       EDTWRD('       0 ')
      *
      *---------------------------------------------------------------*
      * Turnover Statement Trailer                                    *
      *---------------------------------------------------------------*
     A          R F0TRL2                     TEXT('Turnover Statement Trailer')
     A                                       SPACEB(2)
      *
     A                                    30 'No. of Turnover Accts read:'
     A            R#NREC         9  0     62 TEXT('STMTAH recs. read')
     A                                       EDTWRD('       0 ')
     A                                       SPACEA(1)
     A                                    30 'No. of UDC records output:'
     A            R#UROP         9  0     62 TEXT('UDC recs. Output')
     A                                       EDTWRD('       0 ')
      *
      *---------------------------------------------------------------*
      * Totals Format                                                 *
      *---------------------------------------------------------------*
     A          R F0TRL3                     TEXT('Report Totals')
     A                                       SPACEB(4)
      *
     A                                    30 'Total Available Statements:'
     A            R#TREC         9  0     62 TEXT('Tot.Avail. Stmts.')
     A                                       EDTWRD('       0 ')
     A                                       SPACEA(1)
     A                                    30 'Total STMTAH records output:'
     A            R#MTOT         9  0     62 TEXT('Tot. Mid recs. Output')
     A                                       EDTWRD('       0 ')
     A                                       SPACEA(1)
     A                                    30 'Total UDC records output:'
     A            R#UTOT         9  0     62 TEXT('Tot. UDC recs. Output')
     A                                       EDTWRD('       0 ')
      *
      *---------------------------------------------------------------*
      * End of Report                                                 *
      *---------------------------------------------------------------*
     A          R F0TRL4                     TEXT('End of Report')
     A                                       SPACEB(3)
      *
     A                                    56 '*** End of Report ***'
