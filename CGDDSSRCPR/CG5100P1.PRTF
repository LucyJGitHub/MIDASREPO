      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas CG UDC Print Process Extract')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                      *
     A*                                                               *
     A*  CG5100P1  -  Printer file for RPG program                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 12Dec94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01522  -  User Defined Correspondence                       *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*     SCHEDULE(*FILEEND)                                        *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  6'REFERENCE'
     A            H1REFN        10      3 16
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3100'DATE'
     A            BJMRDT    R           3105REFFLD(RUNA)
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A            H2TITL        60      5 39
     A            H2TUL         60      6 39
     A**
     A          R DETAIL                    SPACEB(2)
     A                                     1'......................-
     A                                      ......................-
     A                                      ......................-
     A                                      ......................-
     A                                      ......................-
     A                                      ......................'
                                            SPACEA(1)
     A                                     5'Item Identifier.....:'
     A            D1ITEM         8  0     26TEXT('Item Identifier')
     A                                    46'Destination Correspondent....:'
     A            D1DCOR        10        76TEXT('Destination Correspondent')
     A                                    96'Branch Code............:'
     A            D1BRCA         3       120TEXT('Branch Code')
     A                                      SPACEA(1)
     A                                     5'Module id...........:'
     A            D1MODI         2        26TEXT('Module id')
     A                                    46'Primary Correspondent........:'
     A            D1PCOR        10        76TEXT('Primary Correspondent')
     A                                    96'Overriding Branch Code.:'
     A            D1OVBR         3       120TEXT('Overriding Branch Code')
     A                                      SPACEA(1)
     A                                     5'Orig Branch Code....:'
     A            D1ORBR         3        26TEXT('Originating Branch Code')
     A                                    46'Midas Transaction Ref........:'
     A            D1MTRN        15        76TEXT('Midas Transaction Ref.')
     A                                    96'Item Status............:'
     A            D1ISTS         4       120TEXT('Item Status')
     A                                      SPACEA(1)
     A**Start of change CGL029
     A**********                           5'Midas Account Id....:'                           CGL029
     A**********  D1MACT        18        26TEXT('Midas Account Identifier')                  CGL029
     A                                     5'Midas A/C Id..:'
     A            D1MACT        24        20TEXT('Midas Account Identifier')
     A**End of change CGL029
     A                                    46'Print Item Type..............:'
     A            D1PTYP        10        76TEXT('Print Item Type')
     A                                    96'Print Item Sub-type....:'
     A            D1PSTP        10       120TEXT('Print Item Sub-type')
     A                                      SPACEA(1)
     A                                     5'Auto-Trans Ind......:'
     A            D1ATRM         1        26TEXT('Auto-Transmission Ind.')
     A                                    46'Scheduled Print Date.........:'
     A            D1PRTD         7        76TEXT('Scheduled Print Date')
     A                                    96'Language identifier....:'
     A            D1LGID         2       120TEXT('Language identifier')
     A                                      SPACEA(1)
     A                                     5'Correspondent type..:'
     A            D1CTYP        10        26TEXT('Correspondent type')
     A                                    46'Layout Identifier............:'
     A            D1LAYO        10        76TEXT('Layout Identifier')
     A                                    96'Printer Profile........:'
     A            D1PRTF        10       120TEXT('Printer Profile')
     A                                      SPACEA(1)
     A                                     5'Printer File Name...:'
     A            D1PRTN        10        26TEXT('Printer File Name')
     A                                    46'Use APF/AFP Set up...........:'
     A            D1APF          1        76TEXT('Use APF/AFP Set up')
     A                                      SPACEA(1)
     A                                     5'APF/AFP Comm String.:'
     A            D1APFS       100        26TEXT('APF/AFP Command String')
     A                                      SPACEA(1)
     A                                     5'OVRPRTF String......:'
     A            D1OVRP       100        26TEXT('OVRPRTF String')
      *
      * Print No Report Elements
      *
     A          R F5DTL1                    TEXT('No Details to Report')
     A                                      SPACEB(2)
     A                                    56'*** No Details to Report ***'
      *
      *  End of Report
      *
     A          R F0TRL1                    TEXT('End of Report')
     A                                      SPACEB(2)
     A                                    56'*** End of Report ***'
