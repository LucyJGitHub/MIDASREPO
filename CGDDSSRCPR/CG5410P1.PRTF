      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CG UDC Summary of Correspond. Items Report')
      *****************************************************************
      *                                                               *
      *  Midas      USER DEFINED CORRESPONDENCE                       *
      *                                                               *
      *  CG5410P1 - Summary of Correspondence Items Report            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD053             Date 01Jun06               *
      *  Prev Amend No. 226537             Date 07Jul05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 089984             Date 03Jul95               *
      *                 088008             DATE 18MAY95               *
      *                 086662             DATE 24APR95               *
      *                 086136             DATE 20MAR95               *
      *                 086136             DATE 13MAR95               *
      *                 S01552             DATE 26JAN95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *         - (Recompile)                                         *
      *  226537 - To & From Scheduled Date printed incorrectly.       *
      *           Changed fields to have 7A instead of 6A definition. *
     A*  089984 - Errors in report                                    *
     A*            o 'Correspondence' should read 'Correspondent'     *
     A*            o Print item reference                             *
     A*            o Get rid of correspondnet descriptions and status *
     A*              description because there's no room.             *
     A*  088008 - Print only last confirmation                        *
     A*  086662 - Add Correspondent type                              *
     A*  086136 - Amend 086136 as if the data comes from CGXDCRPD then*
     A*           'Scheduled drop date' should be shown.              *
     A*  086136 - 'Scheduled drop date' should be 'Retention Period'  *
     A*           and be a number of days as opposed to a date'       *
     A*  S01552 - Created for User Defined Correspondence             *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
      *
      *---------------------------------------------------------------*
      *  Report Selections Format                                     *
      *---------------------------------------------------------------*
     A*
     A          R P1SELEC                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                     2'Reference '
     A                                    12'CG5410P1'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            RDAT           7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                     2'Branch'
     A            PPBRCD         3         9
     A            PPBRNM        30        14
     A                                    48'SUMMARY OF CORRESPONDEN+
     A                                       CE ITEMS REPORT'
     A                                      SPACEA(1)
     A                                    48'~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~'
     A                                      SPACEA(8)
     A*
     A                                     7'System/Branch Level  . . .'
     A            P#RLVL         1        37
     A                                      SPACEA(1)
     A*
     A                                     7'Entity . . . . . . . . . .'
     A            P#BRCH         3        37
     A                                      SPACEA(3)
     A
     A*** Selection fields
     A                                     7'Selection Fields:'
     A                                      SPACEA(2)
     A*
     A                                     7'Correspondent  . . . . . .'
     A            PSDCOR        10        37
      * 086662 Start
     A                                    48 'or type'
     A            PSCTYP        10        56
      * 086662 End
     A                                      SPACEA(1)
      *
     A                                     7'Primary Only . . . . . . .'
     A            PSPRIM         1        37
     A                                      SPACEA(1)
      *
     A                                     7'Print Type/Sub-Type  . . .'
     A            PSPTYP        10        37
     A            PSPSTP        10        49
     A                                      SPACEA(1)
      *
     A                                     7'From Scheduled Date  . . .'
     A**Start of change 226537
     A**********  PSFRDT         6        37                                                  226537
     A            PSFRDT         7        37
     A**End of change 226537
     A                                      SPACEA(1)
      *
     A                                     7'To Scheduled Date  . . . .'
     A**Start of change 226537
     A**********  PSTODT         6        37                                                  226537
     A            PSTODT         7        37
     A**End of change 226537
     A                                      SPACEA(1)
      *
     A                                     7'Language ID  . . . . . . .'
     A            PSLGID         2        37
     A                                      SPACEA(1)
     A*
     A                                     7'Statuses . . . . . . . . .'
     A            PSSTS1         4        37
     A            PSSTS2         4        42
     A            PSSTS3         4        47
     A            PSSTS4         4        52
     A            PSSTS5         4        57
     A                                      SPACEA(1)
     A*
     A                                     7'Auto-Transmission  . . . .'
     A            PSATRM         1        37
     A                                      SPACEA(2)
     A*
     A                                     7'Summary Only . . . . . . .'
     A            PSSUMM         1        37
     A                                      SPACEA(1)
      *
     A                                     7 'Sequence Field:'
     A                                      SPACEA(2)
      *
     A*                                    7'Order to Print . . . . . .'
     A*           PSACCP         1        37
     A*                                   40 '(L - Layout/C - Correspondent)'
     A*                                     SPACEA(2)
      *
     A                                     7'Current or Archive . . . .'
     A            PSARCH         1        37
     A                                      SPACEA(1)
      *  Start 088008
     A                                     7'Print last change confrm .'
     A            PSPLST         1        37
     A                                      SPACEA(1)
      *  End 088008
     A*
      *---------------------------------------------------------------*
      *  No Records Format                                            *
      *---------------------------------------------------------------*
     A          R P1NOREC                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SPACEB(5)
     A                                    48'*** No Items to Report +
     A                                      ***'
     A*
      *---------------------------------------------------------------*
      *  Report Header                                                *
      *---------------------------------------------------------------*
     A          R P1HEADA                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                     2'Reference '
     A                                    12'CG5410P1'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            RDAT           7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                     2'Branch'
     A            PPBRCD         3         9
     A            PPBRNM        30        14
     A                                    48'SUMMARY OF CORRESPONDEN+
     A                                       CE ITEMS REPORT'
     A                                      SPACEA(1)
     A                                    48'~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~'
     A                                      SPACEA(2)
     A*
      *---------------------------------------------------------------*
      *  Summary Report Sub-heading                                   *
      *---------------------------------------------------------------*
     A          R P1HEADB                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A***************                      2'Destination'    /* START   086136*/
     A***************                    +36'Print Item'
     A***************                    +13'Item'
     A***************                     +5'Generation'
     A***************                     +3'Scheduled'
     A***************                     +3'Scheduled'
     A                                  8  2'Destination'
     A                                  8 49'Print Item'
     A                                  8 72'Item'
     A                                  8 81'Generation'
     A                                  8 94'Scheduled'
     A  86                              8106'Scheduled'
     A N86                              8106'Retention'
     A***************                       SPACEA(1)
     A*
     A***************                      2'Correspondent'
     A***************                    +34'Type/Subtype'
     A***************                    +10'Status'
     A***************                     +7'Date'
     A***************                     +6'Print Date'
     A***************                     +2'Drop Date'
     A                                  9  2'Correspondent'
     A                                  9 49'Type/Subtype'
     A                                  9 71'Status'
     A                                  9 84'Date'
     A                                  9 94'Print Date'
     A  86                              9106'Drop Date'
     A N86                              9106'Period'
     A***************                       SPACEA(2)
     A                                 10  2' '
     A*                                                      /* END     086136*/
      *
      *---------------------------------------------------------------*
      *  Summary Detail Line                                          *
      *---------------------------------------------------------------*
     A          R P1DETS
     A*
     A            PPDCOR    R              2REFFLD(DRDCOR CGUDCRPD)
     A            PPDCRN    R             +1REFFLD(CDCORN CGCORRPD)
     A            PPPTYP    R             +6REFFLD(DRPTYP CGUDCRPD)
     A            PPPSTP    R             +1REFFLD(DRPSTP CGUDCRPD)
     A            PPISTS    R             +2REFFLD(DRISTS CGUDCRPD)
     A            PPARDT         7        +6
     A            PPPRTD         7        +6
     A************PPDDAT         7        +5                 /* START   086136*/
     A  86        P1DDAT         7       106
     A N86        PPDDAT         7  0    106EDTCDE(Z)
     A*                                                      /* END     086136*/
     A                                      SPACEA(1)
      *
      *---------------------------------------------------------------*
      *  Detail Line                                                  *
      *---------------------------------------------------------------*
     A          R P1DETL
     A*
     A                                     2'Destination'
     A                                    60'Primary'
     A                                      SPACEA(1)
      *
      * Start 089984
     A************************************ 2'Correspondent  . .'
     A                                     2'Correspondent  . .'
      * End 089984
     A            PPDCOR    R             +2REFFLD(DRDCOR CGUDCRPD)
      * Start 089984
     A*********** PPDCRN    R             +1REFFLD(CDCORN CGCORRPD)
     A*********************************** 60'Correspondence . .'
     A                                    60'Correspondent  . .'
      * End 089984
     A            PPPCOR    R             +6REFFLD(DRPCOR CGUDCRPD)
      * Start 089984
     A*********** PPPCRN    R             +1REFFLD(CDCORN CGCORRPD)
     A                                   116'Ref.'
     A            PPITEM    R             +1REFFLD(DRITEM CGUDCRPD)
      * End 089984
     A                                      SPACEA(2)
     A*
     A                                     2'Print Item'
     A                                    52'Midas'
     A                                      SPACEA(1)
      *
     A                                     2'Type/Subtype . . .'
     A            PPPTYP    R             +2REFFLD(DRPTYP CGUDCRPD)
     A            PPPSTP    R             +1REFFLD(DRPSTP CGUDCRPD)
     A                                    52'Transaction Reference'
     A            PPMTRN    R             +7REFFLD(DRMTRN CGUDCRPD)
     A                                    96'Account'
     A            PPMACT    R             +2REFFLD(DRMACT CGUDCRPD)
     A                                      SPACEA(2)
     A*
     A                                     2'Item'
     A                                    52'Generation'
     A                                    74'Scheduled'
     A************                        96'Scheduled'      /* START   086136*/
     A*                                                      /* START   086136*/
     A  86                                96'Scheduled'
     A N86                                96'Retention'
     A************                                           /* END     086136*/
     A************                                           /* END     086136*/
     A                                   120'Lang.'
     A                                      SPACEA(1)
     A*
     A                                     2'Status . . . . . .'
     A            PPISTS    R             +2REFFLD(DRISTS CGUDCRPD)
      * Start 089984
     A*********** PPIDES    R             +1REFFLD(ISDESC CGISTSPD)
      * End 089984
     A                                    52'Date . . .'
     A            PPARDT         7        +2
     A                                    74'Print Date'
     A            PPPRTD         7        +2
     A************                        96'Drop Date'      /* START   086136*/
     A*                                                      /* START   086136*/
     A  86                                96'Drop Date'
     A N86                                96'Period'
     A*                                                      /* END     086136*/
     A************PPDDAT         7        +2
     A            PPDDAT         7  0     +2EDTCDE(Z)
     A************                                           /* END     086136*/
     A                                   121'Id. '
     A            PPLGID    R             +2REFFLD(DRLGID CGUDCRPD)
     A                                      SPACEA(3)
      *
      *---------------------------------------------------------------*
      *  Rule Line                                                    *
      *---------------------------------------------------------------*
     A          R P1RULE
     A                                     2'~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~'
     A                                      SPACEA(2)
      *
      *---------------------------------------------------------------*
      *  End of Branch Format                                         *
      *---------------------------------------------------------------*
     A          R P1ENDA
     A                                      SPACEB(3)
     A                                    50'*** END OF REPORT FOR BRANCH ***'
      *
      *---------------------------------------------------------------*
      *  End of Report Format                                         *
      *---------------------------------------------------------------*
     A          R P1ENDB
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
