      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60J                                                    *
/*EXI *  TEXT('Midas Interest Scale Exception Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/CG7330P1 - Midas Interest Scale Reporting               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CER042  *CREATE    Date 11May11               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER042 - Interest Scale Report Correspondence                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R CG7330H1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2011')
      *
     A                                      SKIPB(003)
     A                                     1
     A                                      'REFERENCE CG7330P1'
     A            BJURPT    R             37
     A                                      REFFLD(TITL)
     A                                   101
     A                                      'DATE'
     A            AGMRDT         7A      106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                   101
     A                                      'Time'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(002)
     A                                    49
     A                                      'Interest Scale Exception Report'
     A                                   132
     A                                      ' '
     A                                      SPACEA(001)
     A                                    49
     A                                      '-------------------------------'
     A                                      SPACEA(002)
      *
      ** Heading
      *
     A                                     2
     A                                      'Branch'
     A                                    12
     A                                      'Customer'
     A                                    22
     A                                      'Currency'
     A                                    38
     A                                      'Account'
     A                                    48
     A                                      'Account'
     A                                    58
     A                                      'Reference'
     A                                    73
     A                                      'Type'
     A                                    83
     A                                      'Interest'
     A                                    93
     A                                      'Interest'
     A                                   103
     A                                      'Reason'
     A                                      SPACEA(001)
     A                                    40
     A                                      'Code'
     A                                    48
     A                                      'Sequence'
     A                                    60
     A                                      'Number'
     A                                    83
     A                                      'From date'
     A                                    93
     A                                      'To date'
     A                                      SPACEA(002)
      *
      ** Detail Record Format
      *
     A          R CG7330D1
     A                                      SPACEB(001)
     A            Z5BRCA         3         2
     A            Z5CNUM         6        13
     A            Z5CCY          3        25
     A            Z5ACOD        10S 0     36
     A            Z5ACSQ         2S 0     51
     A            F2REFN        10        57
     A            F2TYPE         6        73
     A            F2INFD         7        84
     A            F2INTD         7        94
     A            F2REAS        30       103
      *
      ** No Details
      *
     A          R CG7330D2
     A                                      SPACEB(001)
     A                                    49
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      ** Report Trailer
      *
     A          R CG7330F1
     A                                      SPACEB(001)
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
