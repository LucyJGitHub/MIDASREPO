      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas CG Standard audit report printer file.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - User Defined Correspondence                          *
     A*                                                               *
     A*  PRTF/CGZAUDP1 - Standard Audit Report                        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 092761             DATE 06SEP95               *
     A*  PREV AMEND NO. S01522             DATE 24NOV94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  092761 - Audit reports not going to MPRINTAU                 *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A                                       REF(MFRFL)
     A*=====================================================================
     A* Format F1HED1 prints the standard header:
     A*=====================================================================
     A          R F1HED1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                       SKIPB(3)
     A                                     6'REFERENCE'
     A            P1REFN        10        16 TEXT('Report reference')
     A            BJURPT    R             37 REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105 REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE1          4  0    124 EDTCDE(Z)
     A                                       SPACEA(2)
     A*
     A*=====================================================================
     A* Format F2HED2 prints the report's header:
     A*=====================================================================
     A          R F2HED2                     TEXT('Report header')
     A*
     A            P2TITL        60        37 TEXT('Report title')
     A                                       SPACEA(1)
     A*
     A            P2TUL         60        37 TEXT('Underline')
     A                                       SPACEA(2)
     A*
     A*=====================================================================
     A* Format F3DETL prints details:
     A*=====================================================================
     A          R F3DETL
     A*
     A            P3TEXT        60         6 TEXT('Line text')
     A            P3QTY         21        68 TEXT('Quantity')
     A  62                                88'.'
     A  62        P3EXT          3        89 TEXT('Extension value')
     A                                    96' '
     A                                       SPACEA(1)
     A*
     A*=====================================================================
     A* Format F4SKIP skips a line:
     A*=====================================================================
     A          R F4SKIP
     A*
     A                                     6' '
     A                                       SPACEA(1)
     A*
     A*=====================================================================
     A* Format F5NDTA prints "No details to report":
     A*=====================================================================
     A          R F5NDTA
     A*
     A                                       SPACEB(2)
     A            P5TEXT        60        37 TEXT('"No data to report"')
     A*
     A*=====================================================================
     A* Format F6END prints "End of report":
     A*=====================================================================
     A          R F6END
     A*
     A                                       SPACEB(2)
     A            P6TEXT        60        37 TEXT('"End of report"')
     A*
     A**********************************************************************
