      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CG Standard error report printer file.')
     A*****************************************************************
     A*                                                               *
     A*  Midas     - User Defined Correspondence                  *
     A*                                                               *
     A*  PRTF/CGZERRP1 - Standard Error Report                        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01522             DATE 23NOV94               *
     A*  PREV AMEND NO. Xnnnnn             DATE ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01522 - User Defined Correspondence                         *
     A*                                                               *
     A*****************************************************************
     A                                       REF(MFRFL)
     A*=====================================================================
     A* Format F1HED1 prints the standard header:
     A*=====================================================================
     A          R F1HED1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                       SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'CGZERROR'
     A            BJURPT    R             37 REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105 REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE1          4  0    124 EDTCDE(Z)
     A                                       SPACEA(2)
     A*
     A*=====================================================================
     A* Format F2HED2 prints the report's header:
     A*=====================================================================
     A          R F2HED2                     TEXT('Report header')
     A*
     A                                    56'Standard Error Report'
     A                                       SPACEA(1)
     A*
     A                                    56'---------------------'
     A                                       SPACEA(2)
     A*
     A*=====================================================================
     A* Format F3DETL prints program status data structure details:
     A*=====================================================================
     A          R F3DETL
     A*
     A                                     4'Program status data structure'
     A                                       SPACEA(1)
     A*
     A                                     4'-----------------------------'
     A                                       SPACEA(2)
     A*
     A                                     4'Program name:'
     A            P3PGM         10        34 SPACEA(1)
     A*
     A                                     4'Status:'
     A            P3STCD         5  0     34 SPACEA(1)
     A*
     A                                     4'Previous status:'
     A            P3STCP         5  0     34 SPACEA(1)
     A*
     A                                     4'Source statement seq no:'
     A            P3SQNO         8        34 SPACEA(1)
     A*
     A                                     4'Routine name:'
     A            P3RTVN         8        34 SPACEA(1)
     A*
     A                                     4'Parameters count:'
     A            P3PMCT         3  0     34 SPACEA(1)
     A*
     A                                     4'Exception MSGID:'
     A            P3MSID         7        34 SPACEA(1)
     A*
     A                                     4'MI/ODT number:'
     A            P3MINB         4        34 SPACEA(1)
     A*
     A                                     4'Messages work area:'
     A            P3MSWK        30        34 SPACEA(1)
     A*
     A                                     4'Program library:'
     A            P3PGLB        10        34 SPACEA(1)
     A*
     A                                     4'Retrieved exception data:'
     A            P3MSDA        80        34 SPACEA(1)
     A*
     A                                     4'Called program exception ID:'
     A            P3EXID         4        34 SPACEA(1)
     A*
     A                                     4'Name of file in error:'
     A            P3ERFL         8        34 SPACEA(1)
     A*
     A                                     4'Status info of last file:'
     A            P3ERST        35        34 SPACEA(1)
     A*
     A                                     4'Job name:'
     A            P3JOB         10        34 SPACEA(1)
     A*
     A                                     4'User name:'
     A            P3USR         10        34 SPACEA(1)
     A*
     A                                     4'Job number:'
     A            P3JNO          6  0     34 SPACEA(1)
     A*
     A                                     4'Job entry date:'
     A            P3EDT          6  0     34 SPACEA(1)
     A*
     A                                     4'Job execution date:'
     A            P3SDT          6  0     34 SPACEA(1)
     A*
     A                                     4'Job execution time (hhmmss):'
     A            P3STM          6  0     34 SPACEA(1)
     A*
     A                                     4'Program compiled date:'
     A            P3CPDT         6        34 SPACEA(1)
     A*
     A                                     4'Program compiled time:'
     A            P3CPTM         6        34 SPACEA(1)
     A*
     A                                     4'Compiler level:'
     A            P3CPLV         4        34 SPACEA(1)
     A*
     A                                     4'Source file name:'
     A            P3SRFL        10        34 SPACEA(1)
     A*
     A                                     4'Source file library:'
     A            P3SRLB        10        34 SPACEA(1)
     A*
     A                                     4'Source file member:'
     A            P3SRMB        10        34 SPACEA(2)
     A*
     A*=====================================================================
     A* Format F4DETL prints send message data structure details:
     A*=====================================================================
     A          R F4DETL
     A*
     A                                     4'Send message data structure'
     A                                       SPACEA(1)
     A*
     A                                     4'---------------------------'
     A                                       SPACEA(2)
     A*
     A                                     4'Program name:'
     A            P4PGM         10        34 SPACEA(1)
     A*
     A                                     4'File name:'
     A            P4FILE        10        34 SPACEA(1)
     A*
     A                                     4'Key value:'
     A            P4KEY         30        34 SPACEA(1)
     A*
     A                                     4'Error number:'
     A            P4ERNB         5  0     34 EDTWRD('   0 ')
     A                                       SPACEA(1)
     A*
     A                                     4'Narrative:'
     A            P4NARR        80        34 SPACEA(1)
     A*
     A                                     4'Subroutine stack:'
     A            P4STK1        50        34 SPACEA(1)
     A*
     A            P4STK2        50        34 SPACEA(1)
     A*
     A                                     4'Primary reference:'
     A            P4PREF        30        34 SPACEA(2)
     A*
     A*=====================================================================
     A* Format F5END prints "End of report":
     A*=====================================================================
     A          R F5END
     A*
     A                                       SPACEB(1)
     A                                    56'*** End of report ***'
     A*
     A**********************************************************************
