     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CSD053
/*STD *  RPGBNOCVT                                                    *                       CSD053
/*EXS *  RPGCVTOPT1                                                   *                       CSD053
/*EXI *  TEXT('Midas CG Correspondent initial set up')
/*OVRF*: OVRDBF                                                     : *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG0001 - Initial file setup                                  *
      *                                                               *
      *  Function:  This program creates correspondent records        *
      *             with default initial values for all customers,    *
      *             alternative names and addresses, and counterparty *
      *             nostros. It will also create Correspondent        *
      *             Schedule entries to replicate the alternative     *
      *             address references on Account Details.            *
      *                                                               *
      *             IT IS INTENDED THAT THIS PROGRAM BE MODIFIED ON   *
      *             SITE TO SUIT SPECIFIC CLIENT REQUIREMENTS. SEE    *
      *             THE UDC MoPro Section 'IMPLEMENTATION NOTES' FOR  *
      *             MORE DETAILS.                                     *
      *                                                               *
      *  Called By:  - CMD/CGMIGRATE                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSD053             Date 01Jun06               *
      *                 CSW037A            Date 02May05               *
      *                 BUG6198            Date 04Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 13Jly95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *         - (Recompile)                                         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  BUG6198- Cannot save new Details for CUSD (Recompile)        *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  S01522 - User Defined Correspondence                         *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicator usage                                              *
      *  ~~~~~~~~~~~~~~~                                              *
      *                                                               *
      *  50   -    EoF migrated details                               *
      *  51   -    NrF Customer details                               *
      *                                                               *
      *****************************************************************
      *
     FCGCORRPDO   E                    DISK
     F                                              KINFSR SRFILE
     FCGCASCPDO   E                    DISK
     F                                              KINFSR SRFILE
     FCGCSCHPDO   E                    DISK
     F                                              KINFSR SRFILE
     FSDCUSTL5IF  E           K        DISK
     F                                              KINFSR SRFILE
     FSDALTNL3IF  E           K        DISK
     F                                              KINFSR SRFILE
     FSDCNSTL1IF  E           K        DISK
     F                                              KINFSR SRFILE
     FACCNT   IF  E           K        DISK
     F                                              KINFSR SRFILE
      /EJECT
     E                    CPY@    1   1 80
      ** Copyright statement
      *
      /COPY CGCPYSRC,SRERRE
      /EJECT
      *
     I##DFNR      DS
     I I            'Set up automatically'    1  30 ##S
      *
     I##STMT      DS
     I I            'STATEMENT '              1  10 ##T
      *
     I##ADRF      DS
     I                                        1   8 ADREF1
     I                                        9  10 ADREF2
      ** Address refernce data structure
      *
     I##ACNT      DS
     I**********                              1   60CNUM                                      CSD027
     I                                        1   6 CNUM                                      CSD027
     I                                        7   9 CCY
     I                                       10  190ACOD                                      CGL029
     I                                       20  210ACSQ                                      CGL029
     I                                       22  24 BRCA                                      CGL029
     I**********                             10  130ACOD                                      CGL029
     I**********                             14  150ACSQ                                      CGL029
     I**********                             16  18 BRCA                                      CGL029
      ** Full account i/d
      *
     I            DS
     I                                        1   6 BCCUST
     I**********                              1   60BCCUSN                                    CSD027
     I                                        1   6 BCCUSN                                    CSD027
      ** Redefine alternative name and address customer as numeric
      *
     ICPYR@#      DS
     I                                        1  80 CPY@
      ** Copyright statement
      *
     IRUNDTA    E DSRUNDAT
      ** Data Area for run-date
      *
      /COPY CGCPYSRC,SRERRI
      /EJECT
      *
      ** Parameters: 1. Migrate customers (Y,N)
      **             2. Migrate Alternative names and addresses (Y,N)
      **             3. Migrate Counterparty nostros (Y,N)
      **             4. Create schedule entries for accounts (Y,N)
      **             5. Customer/correspondent prefix (optional)
      **             6. Customer/counterparty nostro prefix (optional)
     C           *ENTRY    PLIST
     C                     PARM           P@CUST  1
     C                     PARM           P@ALTR  1
     C                     PARM           P@NOST  1
     C                     PARM           P@SCHD  1
     C                     PARM           P@CP    2
     C                     PARM           P@NP    2
      *
      ** Set up copyright statement
     C                     MOVE CPYR@#    CPY2@  80
      *
      ** Access run-date
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
      /EJECT
      *
      ** Part 1 - CUSTOMER DETAILS
      ** --------------------------------------------------------------
     C           P@CUST    IFEQ 'Y'
     C                     READ SDCUSTL5                 50
      *
     C           *IN50     DOWEQ'0'
      *
      ** Initialise record format
     C                     CLEAR@CORRPD
      *
      ** Use CAT to build correspondent name from prefix and customer
      ** number - if prefix is blank, CAT should suppress the blanks.
     C           P@CP      CAT  BBCUST:0  CDCORR
      *
      ** Set up remaining fields which may be defaulted
     C                     MOVEL'D'       CDRECI
     C                     MOVELBBCUST    CDCUST
     C                     MOVEL'C'       CDRTYP
     C           BBCRNM    CAT  BBCRTN    CDCORN    P
     C                     Z-ADDAGRDNB    CDCDTE
     C                     MOVEL'N'       CDPLAB
     C                     Z-ADDAGRDNB    CDATDT
     C                     MOVEL'DEFAULT' CDCTYP
     C                     MOVEL'Default' CDCSTR
     C                     MOVEL##DFNR    CDDESC
     C                     MOVEL##JOB     CDAJOB
     C                     MOVEL##USR     CDAUSR
     C                     TIME           CDATIM
     C                     MOVELAGMRDT    CDARDT
     C                     MOVEL'I'       CDAACT
     C                     Z-ADDAGRDNB    CDRDNB
      *
     C/COPY WNCPYSRC,CG0001C001
      ** Write correspondent record
     C                     WRITE@CORRPD
      *
      ** Get next customer
     C                     READ SDCUSTL5                 50
      *
     C                     ENDDO
     C                     ENDIF
      ** --------------------------------------------------------------
      /EJECT
      *
      ** Part 2 - ALTERNATIVE NAMES & ADDRESSES
      ** --------------------------------------------------------------
     C           P@ALTR    IFEQ 'Y'
     C                     READ SDALTNL3                 50
      *
     C           *IN50     DOWEQ'0'
      *
      ** Initialise record format
     C                     CLEAR@CORRPD
      *
      ** Get customer details
     C           BCCUST    CHAINSDCUSTL5             51
     C           *IN51     IFEQ '1'
     C                     MOVEL'SDCUSTL5'W0FILE
     C                     MOVELBCCUST    W0KEY            ***************
     C                     Z-ADD1         W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *
      ** Use CAT to build correspondent name from prefix, customer
      ** number and address code
     C           P@CP      CAT  BCCUST:0  W@CUST 10
     C           W@CUST    CAT  BCADCD:1  CDCORR
      *
      ** Set up remaining fields which may be defaulted
     C                     MOVEL'D'       CDRECI
     C                     MOVEL'L'       CDRTYP
     C                     MOVELBCCNA1    CDADD1
     C                     MOVELBCCNA2    CDADD2
     C                     MOVELBCCNA3    CDADD3
     C                     MOVELBCCNA4    CDADD4
     C                     MOVELBCCRTN    CDADD5
     C                     Z-ADDAGRDNB    CDCDTE
     C           BBCRNM    CAT  BBCRTN    CDCORN    P
     C                     MOVEL'N'       CDPLAB
     C                     Z-ADDAGRDNB    CDATDT
     C                     MOVEL'DEFAULT' CDCTYP
     C                     MOVEL'Default' CDCSTR
     C                     MOVELBCCTNB    CDTEL
     C                     MOVEL##DFNR    CDDESC
     C                     MOVEL##JOB     CDAJOB
     C                     MOVEL##USR     CDAUSR
     C                     TIME           CDATIM
     C                     MOVELAGMRDT    CDARDT
     C                     MOVEL'I'       CDAACT
     C                     Z-ADDAGRDNB    CDRDNB
      *
     C/COPY WNCPYSRC,CG0001C002
      ** Write correspondent record
     C                     WRITE@CORRPD
      *
      ** Create Correspondent Association (Link) record
     C                     CLEAR@CASCPD
     C                     MOVELW@CUST    CAFCOR
     C                     MOVELCDCORR    CATCOR
     C                     MOVEL'D'       CARECI
     C                     MOVEL##DFNR    CADESC
     C                     MOVEL##JOB     CAAJOB
     C                     MOVEL##USR     CAAUSR
     C                     TIME           CAATIM
     C                     MOVELAGMRDT    CAARDT
     C                     MOVEL'I'       CAAACT
     C                     Z-ADDAGRDNB    CARDNB
     C                     WRITE@CASCPD
      *
      ** If requested, create additional schedule details
     C           P@SCHD    IFEQ 'Y'
     C                     EXSR SRPRT4
     C                     ENDIF
      *
      ** Get next record
     C                     READ SDALTNL3                 50
      *
     C                     ENDDO
     C                     ENDIF
      ** --------------------------------------------------------------
      /EJECT
      *
      ** Part 3 - COUNTERPARTY NOSTROS
      ** --------------------------------------------------------------
     C           P@NOST    IFEQ 'Y'
     C                     READ SDCNSTL1                 50
      *
     C           *IN50     DOWEQ'0'
      *
      ** Initialise record format
     C                     CLEAR@CORRPD
      *
      ** Use CAT to build correspondent name from prefix and customer
      ** number - if prefix is blank, CAT should suppress the blanks.
     C           P@NP      CAT  AWCPNC:0  CDCORR
      *
      ** Set up remaining fields which may be defaulted
     C                     MOVEL'D'       CDRECI
     C                     MOVELAWCPNC    CDCPNC
     C                     MOVEL'N'       CDRTYP
     C                     MOVELAWCPNM    CDADD1
     C                     MOVELAWCNTN    CDADD2
     C                     MOVEL'...'     CDADD3
     C                     MOVEL'...'     CDADD4
     C                     MOVELAWCPNM    CDCORN
     C                     Z-ADDAGRDNB    CDCDTE
     C                     MOVEL'N'       CDPLAB
     C                     Z-ADDAGRDNB    CDATDT
     C                     MOVEL'DEFAULT' CDCTYP
     C                     MOVEL'Default' CDCSTR
     C                     MOVELAWBACD    CDCREF
     C                     MOVEL##DFNR    CDDESC
     C                     MOVEL##JOB     CDAJOB
     C                     MOVEL##USR     CDAUSR
     C                     TIME           CDATIM
     C                     MOVELAGMRDT    CDARDT
     C                     MOVEL'I'       CDAACT
     C                     Z-ADDAGRDNB    CDRDNB
      *
     C/COPY WNCPYSRC,CG0001C003
      ** Write correspondent record
     C                     WRITE@CORRPD
      *
      ** Get next counterparty nostro
     C                     READ SDCNSTL1                 50
      *
     C                     ENDDO
     C                     ENDIF
      ** --------------------------------------------------------------
      /EJECT
      *
      ** End program
     C                     SETON                     LR
      ** --------------------------------------------------------------
      /EJECT
      *
      ** Part 4 - CREATE CORRESPONDENT SCHEDULE DETAILS FOR ALTERNATIVE
      **          NAMES AND ADDRESSES.
      ** --------------------------------------------------------------
     C           SRPRT4    BEGSR
      *
      ** Access all accounts for current customer
     C           BCCUSN    CHAINACCNT                50
      *
     C                     Z-ADD0         W@SEQN  30
     C           *IN50     DOWEQ'0'
      *
      ** If live account includes a reference to the current
      ** alternative name & address, generate Correspondent Schedule
     C           RECI      IFEQ 'D'
     C           BCADCD    SCAN ##ADRF                   52
     C           *IN52     IFEQ '1'
     C                     ADD  1         W@SEQN
     C                     EXSR SRSCHD
     C                     ENDIF
     C                     ENDIF
      *
     C           BCCUSN    READEACCNT                    50
     C                     ENDDO
      *
     C                     ENDSR
      ** --------------------------------------------------------------
      /EJECT
      *
      ** SRSCHD - CREATE CORRESPONDENT SCHEDULE RECORD
      ** --------------------------------------------------------------
      *
     C           SRSCHD    BEGSR
      *
      ** Initialise record format
     C                     CLEAR@CSCHPD
      *
      ** Create record details:
      **  o Correspondent is alternative name and address correspondent
     C                     MOVELCDCORR    CPCORR
      **  o Print item type is 'STATEMENT'; subtype 'NORMAL'
     C                     MOVEL##STMT    CPPTYP
     C                     MOVEL'NORMAL'  CPPSTP
      **  o Sequence no. makes record unique for
      **    Correspondent/type/subtype
     C                     Z-ADDW@SEQN    CPSEQN
      **  o Primary corresponent is actual customer
     C                     MOVELW@CUST    CPPCOR
      **  o Account is full account id
     C                     MOVEL##ACNT    CPMACT
      **  o Default values for remaining fields
     C                     MOVEL'D'       CPRECI
     C                     MOVEL'N'       CPPLAB
     C                     MOVEL'N'       CPAPDF
     C                     MOVEL##JOB     CPAJOB
     C                     MOVEL##USR     CPAUSR
     C                     TIME           CPATIM
     C                     MOVELAGMRDT    CPARDT
     C                     MOVEL'I'       CPAACT
     C                     Z-ADDAGRDNB    CPRDNB
      *
     C                     WRITE@CSCHPD
      *
     C                     ENDSR
      ** --------------------------------------------------------------
      /EJECT
      /COPY CGCPYSRC,SRERRC
      /COPY CGCPYSRC,SRERRPSSR
** CPY@ Copyright statement
(c) Misys International Banking Systems Ltd. 2001
