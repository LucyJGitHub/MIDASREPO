     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Referential Integrity Check: Acct Code')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                      *
     F*                                                               *
     F*  CG0110 - Referential Integrity Check                         *
     F*     CGCSCHL2 - Correspondent details by account code.         *
     F*     CGLASCL3 - Layout association table by account code.      *
     F*                                                               *
     F*                                                               *
     F*  Function:  This program tests the first 10 records and if    *
     F*             any exist the return code will be set to 'CGD1180'*
     F*                                                               *
     F*  Called By:                                                   *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSC022             Date 24Feb04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 21Nov94               *
      *                                    Date ddmmmyy               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  90   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,CG0110DFPG
      *
      *  /Copy point for File Definition
      *
     FCGCSCHL2IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Correspondent Details By Account code.
      *
     FCGLASCL3IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Layout association table by Account code.
      *
     F/EJECT
     E/COPY CGCPYSRC,SRERRE
     E/COPY WNCPYSRC,CG0110DEPG
      *
      *  /Copy point for Arrays
      *
     E                    CPY@    1   1 80
     I/COPY WNCPYSRC,CG0110DIPG
      *
      *  /Copy point for Input Specs
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I/COPY CGCPYSRC,SRERRI
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7        Return Code
     C**********           PARM           I#ACOD  4        Account code                       CGL029
     C                     PARM           I#ACOD 10                                           CGL029
     C                     PARM           I#CPGM 10        Calling Pgm tr.
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
     I/COPY WNCPYSRC,CG0190C
      *
      * Check if Account code exists on CGCSCHL2.
      *
     C                     EXSR SRCHL2
      *
      * Check if Account code exists on CGLASCL3.
      *
     C                     EXSR SRSCL3
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      * Return to calling program
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRRRL9   : Process CGCSCHL2                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRCHL2    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRCHL2'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#ACOD    SETLLCGCSCHL2
     C           I#ACOD    READECGCSCHL2                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1180, and send message.
      *
     C                     MOVEL'CGD1180' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1240' CGMSID
     C                     MOVELCPCORR    CGMSDT
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#ACOD    READECGCSCHL2                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXCHL2    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSCL3   : Process CGLASCL3                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRSCL3    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSCL3'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#ACOD    SETLLCGLASCL3
     C           I#ACOD    READECGLASCL3                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1180, and send message.
      *
     C                     MOVEL'CGD1180' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1241' CGMSID
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#ACOD    READECGLASCL3                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXSCL3    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialisation                                    *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Define the number of records to process.
      *
     C                     Z-ADD10        I#NUMR  20
      *
      *  Get Rundate - Rundate
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    ##MRDT  7        Midas Run date
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     C/COPY CGCPYSRC,SRERRPSSR
     C/COPY CGCPYSRC,SCGSNMSC
     C/COPY CGCPYSRC,SRERRC
**  CPY@ table
(c) Finastra International Limited 2001
