     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Ref Integrity Check: Printer Profiles')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                      *
     F*                                                               *
     F*  CG0220 - Referential Integrity Chck                          *
     F*  CGLAYOL3  - Printer profile.                                 *
     F*  CGLASCL7  - Overriding printer file.                         *
     F*                                                               *
     F*                                                               *
     F*  Function:  This program tests the first 10 records and if    *
     F*             they exist they cannot then be deleted and        *
     F*             the return code will be set to CGD1202.           *
     F*                                                               *
     F*  Called By:                                                   *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 21Nov94               *
      *                                    Date ddmmmyy               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  90   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,CG0220DFPG
      *
      *  /Copy point for File Definition
      *
     FCGLAYOL3IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Layout reference by printer profile.
      *
     FCGLASCL7IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Layout association by printer profile.
      *
     F/EJECT
     E/COPY CGCPYSRC,SRERRE
     E/COPY WNCPYSRC,CG0220DEPG
      *
      *  /Copy point for Arrays
      *
     E                    CPY@    1   1 80
     I/COPY WNCPYSRC,CG0220DIPG
      *
      *  /Copy point for Input Specs
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I/COPY CGCPYSRC,SRERRI
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7        Return Code
     C                     PARM           I#PRTF 10        Print schedu
     C                     PARM           I#CPGM 10        Calling Pgm tr.
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
     I/COPY WNCPYSRC,CG0220C
      *
      * Check if Print profile exists on CGLAYOL3.
      *
     C                     EXSR SRYOL3
      *
      * Check if Print profile exists on CGLASCL7.
      *
     C                     EXSR SRSCL7
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      * Return to calling program
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRRRLA   : Process CGLAYOL3                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRYOL3    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRYOL3'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#PRTF    SETLLCGLAYOL3
     C           I#PRTF    READECGLAYOL3                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1218' CGMSID
     C                     MOVELLYLAYO    CGMSDT
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#PRTF    READECGLAYOL3                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXYOL3    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRCHL7   : Process CGLASCL7                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRSCL7    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSCL7'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#PRTF    SETLLCGLASCL7
     C           I#PRTF    READECGLASCL7                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1219' CGMSID
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#PRTF    READECGLASCL7                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXSCLY    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialisation                                    *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Define the number of records to process.
      *
     C                     Z-ADD10        I#NUMR  20
      *
      *  Get Rundate - Rundate
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    ##MRDT  7        Midas Run date
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     C/COPY CGCPYSRC,SRERRPSSR
     C/COPY CGCPYSRC,SCGSNMSC
     C/COPY CGCPYSRC,SRERRC
**  CPY@ table
(c) Finastra International Limited 2001
