     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Ref Integrity Check: Group Sets')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                      *
     F*                                                               *
     F*  CG0230 - Referential Integrity Check                         *
     F*      CGPTYPL2 Print item type/sub type by group set           *
     F*      CGGSASL1 Group set association retrieval index           *
     F*      CGGSASL3 Group set association by to group set           *
     F*      CGDRVDL3 Derived data elements by Group set              *
     F*      CGRASCL2 RDE association table by group/RDE/Order        *
     F*                                                               *
     F*  Function:  This program tests the first 10 records and if    *
     F*             they exist they cannot then be deleted and        *
     F*             the return code will be set to CGD1202.           *
     F*                                                               *
     F*  Called By:                                                   *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 21Nov94               *
      *                                    Date ddmmmyy               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  90   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,CG0230DFPG
      *
      *  /Copy point for File Definition
      *
     FCGPTYPL2IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Print item type/subtype by group set.
      *
     FCGGSASL1IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Group set association retrieval index.
      *
     FCGGSASL3IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Group set association by to group.
      *
     FCGDRVDL3IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Derived data elements by group set.
      *
     FCGRASCL2IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * RDE association table by group/RDE/order.
      *
     F/EJECT
     E/COPY CGCPYSRC,SRERRE
     E/COPY WNCPYSRC,CG0230DEPG
      *
      *  /Copy point for Arrays
      *
     E                    CPY@    1   1 80
      **                                                                                      CSC022
      ** Array to hold commitment jobs name                                                   CSC022
      **                                                                                      CSC022
     E                    WCMT       10 10                                                    CSC022
      **                                                                                      CSC022
     I/COPY WNCPYSRC,CG0230DIPG
      *
      *  /Copy point for Input Specs
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     II#MSG#      DS
     I                                        1  10 I#PTYP
     I                                       11  20 I#PSTP
     I/COPY CGCPYSRC,SRERRI
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
      **                                                                                      CSC022
     ISCCMT       DS                            256                                           CSC022
     I                                        1   30WCMTNO                                    CSC022
     I                                        4 103 WCJOBS                                    CSC022
      ** Commitment Control dataarea                                                          CSC022
      **                                                                                      CSC022
     ISCSARD    E DSSCSARDPD                                                                  CSC022
      ** DS for Switchable Features details                                                   CSC022
      **                                                                                      CSC022
     IDSFDY     E DSDSFDY                                                                     CSC022
      **                                                                                      CSC022
      ** Data Structures used by Access Programs                                              CSC022
      **                                                                                      CSC022
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7        Return Code
     C                     PARM           I#GRPS  6        Group set
     C                     PARM           I#CPGM 10        Calling Pgm tr.
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
     I/COPY WNCPYSRC,CG0230C
      *
      * Check if Group set exists on CGPTYPL2.
      *
     C                     EXSR SRYPL2
      *
      * Check if Group set exists on CGGSASL1.
      *
     C                     EXSR SRASL1
      *
      * Check if Group set exists on CGGSASL3.
      *
     C                     EXSR SRASL3
      *
      * Check if Group set exists on CGDRVDL3.
      *
     C                     EXSR SRVDL3
      *
      * Check if Group set exists on CGRASCL2.
      *
     C                     EXSR SRSCL2
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      * Return to calling program
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRYPL2   : Process CGPTYPL2                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRYPL2    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRYPL2'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#GRPS    SETLLCGPTYPL2
     C           I#GRPS    READECGPTYPL2                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1220' CGMSID
     C                     MOVELITPTYP    I#PTYP
     C                     MOVELITPSTP    I#PSTP
     C                     MOVELI#MSG#    CGMSDT
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#GRPS    READECGPTYPL2                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXYPL2    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRASL1   : Process CGGSASL1                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRASL1    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRASL1'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#GRPS    SETLLCGGSASL1
     C           I#GRPS    READECGGSASL1                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1221' CGMSID
     C                     EXSR CGSNMS
      *
     C           I#GRPS    READECGGSASL1                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXASL1    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRASL3   : Process CGGSASL3                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRASL3    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRASL3'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#GRPS    SETLLCGGSASL3
     C           I#GRPS    READECGGSASL3                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1222' CGMSID
     C                     MOVELGALGST    CGMSDT
     C                     EXSR CGSNMS
      *
     C           I#GRPS    READECGGSASL3                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXASL3    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRVDL3   : Process CGDRVDL3                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRVDL3    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRVDL3'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#GRPS    SETLLCGDRVDL3
     C           I#GRPS    READECGDRVDL3                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1223' CGMSID
     C                     MOVELDDRDE     CGMSDT
     C                     EXSR CGSNMS
      *
     C           I#GRPS    READECGDRVDL3                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXVDL3    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSCL2   : Process CGRASCL2                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRSCL2    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSCL2'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#GRPS    SETLLCGRASCL2
     C           I#GRPS    READECGRASCL2                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1224' CGMSID
     C                     EXSR CGSNMS
      *
     C           I#GRPS    READECGRASCL2                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXSCL2    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialisation                                    *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Define the number of records to process.
      *
     C                     Z-ADD10        I#NUMR  20
      *
      *  Get Rundate - Rundate
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    ##MRDT  7        Midas Run date
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      **                                                                                      CSC022
      ** Initialize CSC022 and Skip Commit/Rollback flags                                     CSC022
      **                                                                                      CSC022
     C                     MOVE 'N'       CSC022  1                                           CSC022
     C                     MOVE 'N'       WCMTSK  1                                           CSC022
      **                                                                                      CSC022
      ** Access SAR details file to determine if CSC022 switchable feature                    CSC022
      ** is switched on                                                                       CSC022
      **                                                                                      CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   P@RTCD  7                                           CSC022
     C                     PARM '*VERIFY 'P@OPTN  7                                           CSC022
     C                     PARM 'CSC022'  P@SARD  6                                           CSC022
     C           SCSARD    PARM SCSARD    DSFDY                                               CSC022
      **                                                                                      CSC022
     C           P@RTCD    IFEQ *BLANKS                                                       CSC022
     C                     MOVE 'Y'       CSC022                                              CSC022
      **                                                                                      CSC022
      ** Get Jobs currently running i batch mode using SCCMRJOB dataarea                      CSC022
      **                                                                                      CSC022
     C           *NAMVAR   DEFN SCCMTJOB  SCCMT                                               CSC022
     C                     IN   SCCMT                                                         CSC022
      **                                                                                      CSC022
     C           WCMTNO    IFGT 0                                                             CSC022
      ** Move committed jobs to arrary for checking                                           CSC022
     C                     MOVEAWCJOBS    WCMT                                                CSC022
      ** Verify if job running exists in array                                                CSC022
     C           ##JOB     LOKUPWCMT                     50                                   CSC022
     C           *IN50     IFEQ *ON                                                           CSC022
     C                     MOVE 'Y'       WCMTSK                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ELSE                                                               CSC022
      ** Execute *PSSR if CSC022 is not found or Database error                               CSC022
     C           P@RTCD    IFNE '*NRF'                                                        CSC022
     C                     MOVEL'CSC022'  W0KEY                                               CSC022
     C                     MOVEL'SCSARDPD'W0FILE                                              CSC022
     C                     Z-ADD1         W0ERNB                                              CSC022
     C                     EXSR SRERR                                                         CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     C/COPY CGCPYSRC,SRERRPSSR
     C/COPY CGCPYSRC,SCGSNMSC
     C/COPY CGCPYSRC,SRERRC
**  CPY@ table
(c) Misys International Banking Systems Ltd. 2001
