     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Ref Integrity Check: RDEs')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                      *
     F*                                                               *
     F*  CG0190 - Referential Integrity Chck                          *
     F*       CGCONDL2 Conditions by left RDE                         *
     F*       CGCONDL3 Conditions by right RDE                        *
     F*       CGPDEFL3 Plane definitions by RDE                       *
     F*       CGDRVDL1 Derived data elements retrieval index          *
     F*       CGRASCL3 RDE association table by RDE                   *
     F*                                                               *
     F*  Function:  This program tests the first 10 records and if    *
     F*             they exist they cannot then be deleted and        *
     F*             the return code will be set to CGD1202.           *
     F*                                                               *
     F*  Called By:                                                   *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01522             Date 21Nov94               *
      *                                    Date ddmmmyy               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  90   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,CG0240DFPG
      *
      *  /Copy point for File Definition
      *
     FCGCONDL2IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Conditions by left RDE
      *
     FCGCONDL3IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Conditions by right RDE
      *
     FCGPDEFL3IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Plane definition by RDE
      *
     FCGDRVDL1IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * Derived data elements retrieval index
      *
     FCGRASCL3IF  E           K        DISK
     F                                              KINFSR SRFILE
      *
      * RDE association table by RDE
      *
     F/EJECT
     E/COPY CGCPYSRC,SRERRE
     E/COPY WNCPYSRC,CG0240DEPG
      *
      *  /Copy point for Arrays
      *
     E                    CPY@    1   1 80
      **                                                                                      CSC022
      ** Array to hold commitment jobs name                                                   CSC022
      **                                                                                      CSC022
     E                    WCMT       10 10                                                    CSC022
      **                                                                                      CSC022
     I/COPY WNCPYSRC,CG0240DIPG
      *
      *  /Copy point for Input Specs
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I/COPY CGCPYSRC,SRERRI
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
      **                                                                                      CSC022
     ISCCMT       DS                            256                                           CSC022
     I                                        1   30WCMTNO                                    CSC022
     I                                        4 103 WCJOBS                                    CSC022
      ** Commitment Control dataarea                                                          CSC022
      **                                                                                      CSC022
     ISCSARD    E DSSCSARDPD                                                                  CSC022
      ** DS for Switchable Features details                                                   CSC022
      **                                                                                      CSC022
     IDSFDY     E DSDSFDY                                                                     CSC022
      **                                                                                      CSC022
      ** Data Structures used by Access Programs                                              CSC022
      **                                                                                      CSC022
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7        Return Code
     C                     PARM           I#RDE  10        RDE
     C                     PARM           I#CPGM 10        Calling Pgm tr.
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
     I/COPY WNCPYSRC,CG0240C
      *
      * Check if left RDE exists on CGCONDL2.
      *
     C                     EXSR SRNDL2
      *
      * Check if right RDE exists on CGCONDL3.
      *
     C                     EXSR SRNDL3
      *
      * Check if RDE exists on CGPDEFL3.
      *
     C                     EXSR SREFL3
      *
      * Check if RDE exists on CGDRVDL1.
      *
     C                     EXSR SRVDL1
      *
      * Check if RDE exists on CGRASCL3.
      *
     C                     EXSR SRSCL3
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      * Return to calling program
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRNDL2   : Process CGCONDL2                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRNDL2    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRNDL2'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#RDE     SETLLCGCONDL2
     C           I#RDE     READECGCONDL2                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1226' CGMSID
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#RDE     READECGCONDL2                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXNDL2    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRNDL3   : Process CGCONDL3                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRNDL3    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRNDL3'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#RDE     SETLLCGCONDL3
     C           I#RDE     READECGCONDL3                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1225' CGMSID
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#RDE     READECGCONDL3                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXNDL3    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SREFL3   : Process CGPDEFL3                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SREFL3    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SREFL3'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#RDE     SETLLCGPDEFL3
     C           I#RDE     READECGPDEFL3                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1227' CGMSID
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#RDE     READECGPDEFL3                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXEFL3    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRVDL1   : Process CGDRVDL1                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRVDL1    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRVLD1'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#RDE     SETLLCGDRVDL1
     C           I#RDE     READECGDRVDL1                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1228' CGMSID
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#RDE     READECGDRVDL1                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXVLD1    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRSCL3   : Process CGRASCL3                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRSCL3    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRSCL3'  @STK,Q
      *
     C                     Z-ADD1         I#NUMR
      *
     C           I#RDE     SETLLCGRASCL3
     C           I#RDE     READECGRASCL3                 90
      *
     C           *IN90     DOWEQ*OFF                       B1
     C           I#NUMR    ANDLT11
      *
      * If record found set return code to CGD1202, and send message.
      *
     C                     MOVEL'CGD1202' W0RTN
      *
      *  Set up message details.
      *
     C                     MOVEL'CGD1229' CGMSID
     C                     MOVELRAGRPS    CGMSDT
     C                     MOVELI#CPGM    CGPGMQ
     C                     EXSR CGSNMS
      *
     C           I#RDE     READECGRASCL3                 90
     C                     ADD  1         I#NUMR
     C                     ENDDO                           E
      *
     C           EXSCL3    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialisation                                    *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Define the number of records to process.
      *
     C                     Z-ADD10        I#NUMR  20
      *
      *  Get Rundate - Rundate
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    ##MRDT  7        Midas Run date
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      **                                                                                      CSC022
      ** Initialize CSC022 and Skip Commit/Rollback flags                                     CSC022
      **                                                                                      CSC022
     C                     MOVE 'N'       CSC022  1                                           CSC022
     C                     MOVE 'N'       WCMTSK  1                                           CSC022
      **                                                                                      CSC022
      ** Access SAR details file to determine if CSC022 switchable feature                    CSC022
      ** is switched on                                                                       CSC022
      **                                                                                      CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   P@RTCD  7                                           CSC022
     C                     PARM '*VERIFY 'P@OPTN  7                                           CSC022
     C                     PARM 'CSC022'  P@SARD  6                                           CSC022
     C           SCSARD    PARM SCSARD    DSFDY                                               CSC022
      **                                                                                      CSC022
     C           P@RTCD    IFEQ *BLANKS                                                       CSC022
     C                     MOVE 'Y'       CSC022                                              CSC022
      **                                                                                      CSC022
      ** Get Jobs currently running i batch mode using SCCMRJOB dataarea                      CSC022
      **                                                                                      CSC022
     C           *NAMVAR   DEFN SCCMTJOB  SCCMT                                               CSC022
     C                     IN   SCCMT                                                         CSC022
      **                                                                                      CSC022
     C           WCMTNO    IFGT 0                                                             CSC022
      ** Move committed jobs to arrary for checking                                           CSC022
     C                     MOVEAWCJOBS    WCMT                                                CSC022
      ** Verify if job running exists in array                                                CSC022
     C           ##JOB     LOKUPWCMT                     50                                   CSC022
     C           *IN50     IFEQ *ON                                                           CSC022
     C                     MOVE 'Y'       WCMTSK                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ELSE                                                               CSC022
      ** Execute *PSSR if CSC022 is not found or Database error                               CSC022
     C           P@RTCD    IFNE '*NRF'                                                        CSC022
     C                     MOVEL'CSC022'  W0KEY                                               CSC022
     C                     MOVEL'SCSARDPD'W0FILE                                              CSC022
     C                     Z-ADD1         W0ERNB                                              CSC022
     C                     EXSR SRERR                                                         CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     C/COPY CGCPYSRC,SRERRPSSR
     C/COPY CGCPYSRC,SCGSNMSC
     C/COPY CGCPYSRC,SRERRC
**  CPY@ table
(c) Misys International Banking Systems Ltd. 2001
