     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Conditions Manipulation')
/*OVRF*: OVRDBF FILE(CGCONDL8) TOFILE(CGCONDL0)                     : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                  *
     F*                                                               *
     F*  CG0450 - Conditions Manipulation                             *
     F*                                                               *
     F*  Function:                                                    *
     F*                                                               *
     F*                                                               *
     F*  Called By:                                                   *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD054955 *REDUNDANTDate 16Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 084416             Date 08Mar95               *
      *                 S01522             Date 09Dec94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD054955 - Deliverable Data Split for Correspondence Mgr     *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  084416 - Override CGCONDL0 to SHARE(*NO)                     *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,CG0450DFPG
     FCGCONDL0IF  E           K        DISK                           UC
     F                                              KINFSR SRFILE
     FCGCONDPDO   E                    DISK                           UC
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     FCGCONDL8UF  E           K        DISK                           UC
     F                                              KINFSR SRFILE
     F            @CONDL0                           KRENAME@CONDL8
     F                                              KCOMIT
     F/EJECT
     E/COPY WNCPYSRC,CG0390DEPG
      *
      *  /Copy point for Arrays
      *
     E*  Array containing Copyright statement
     E                    CPY@    1   1 80
     E                    CMD@    1   3 80
     E                    EDT        80  1
      *
      *  Array of QCMDEXC command to edit
      *
     E/COPY CGCPYSRC,SRERRE
     E*
     E*  Copysource for error processing
     E*
     E/COPY WNCPYSRC,CG0450DEPG
     E/EJECT
     IP1PARM      DS
     I                                        1  10 P1LAYO
     I                                       11  20 P1PLAN
     I                                       21  30 P1FLDN
     I                                       31  380P1TCGP
     I                                       39  460P1CID
     IP2PARM      DS
     I                                        1  10 P2NLAY
     I                                       11  20 P2FMBR
     I                                       21  30 P2TMBR
     IO1PARM      DS
     I                                        1  10 O1LAYO
     I                                       11  20 O1PLAN
     I                                       21  30 O1FLDN
     I                                       31  380O1TCGP
     I                                       39  460O1CID
     IO2PARM      DS
     I                                        1  10 O2NLAY
     I                                       11  20 O2FMBR
     I                                       21  30 O2TMBR
     ISAVPRM      DS
     I                                        1  10 S1NLAY
     I                                       11  20 S1FMBR
     I                                       21  30 S1TMBR
     I/COPY CGCPYSRC,SRERRI
      *
      *  End of Program Error Processing copysource member
      *
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0ACT   8
     C                     PARM           P1PARM
     C                     PARM           P2PARM
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C           P2PARM    ORNE SAVPRM
     C           W0ACT     ORNE SAVACT
     C                     EXSR SRINIT
     C                     END
      *
      ** Main processing
      *
     C                     EXSR SRMAIN
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      * Return to calling program
      *
     C                     RETRN
      *****************************************************************
      *                                                               *
      *  SRMAIN   : Main processing                                   *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRMAIN    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
     C                     MOVEA'000'     *IN,51
     C           W0ACT     CASEQ'*COPY'   SRCOPY
     C           W0ACT     CASEQ'*LDELETE'SRLDEL
     C           W0ACT     CASEQ'*PDELETE'SRPDEL
     C                     ENDCS
      *
      *  Unwind subroutine stack name
      *
     C           EXMAIN    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
      *****************************************************************
      *                                                               *
      *  SRCOPY   : Copy layout                                       *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    :                                                   *
      *                                                               *
      *****************************************************************
     C           SRCOPY    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRCOPY'  @STK,Q
      *
     C                     SELEC
      *
      ** Position file is to number of parameters passed.
      *
     C           P1CID     WHEQ *ZEROS
      *
     C                     SETON                     51
     C           P1LAYO    SETLLCGCONDL0
      *
      ** Position file is to number of parameters passed.
      *
     C           P1CID     WHNE *ZEROS
      *
     C           KKEY01    KLIST
     C                     KFLD           P1LAYO
     C                     KFLD           P1CID
      *
     C                     SETON                     52
     C           KKEY01    SETLLCGCONDL0
      *
     C                     ENDSL
      *
      ** Process records
      *
     C   51      P1LAYO    READECGCONDL0                 98
     C   52      KKEY01    READECGCONDL0                 98
      *
     C           *IN98     DOWEQ*OFF
      *
     C                     MOVELP2NLAY    CNLAYO
      *
     C                     WRITE@CONDPD
      *
     C   51      P1LAYO    READECGCONDL0                 98
     C   52      KKEY01    READECGCONDL0                 98
      *
     C                     ENDDO
      *
      *  Unwind subroutine stack name
      *
     C           EXCOPY    TAG
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRLDEL   : Logical delete                                    *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    :                                                   *
      *                                                               *
      *****************************************************************
     C           SRLDEL    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRLDEL'  @STK,Q
      *
      ** Position file is to number of parameters passed.
      *
     C                     SELEC
     C           P1CID     WHEQ *ZEROS
      *
     C                     SETON                     51
     C           P1LAYO    SETLLCGCONDL8
      *
      ** Position file is to number of parameters passed.
      *
     C           P1CID     WHNE *ZEROS
      *
     C                     SETON                     52
     C           KKEY01    SETLLCGCONDL8
      *
     C                     ENDSL
      *
     C                     MOVE *OFF      *IN97
     C           *IN97     DOWEQ*OFF
     C   51      P1LAYO    READECGCONDL8                 97
     C   52      KKEY01    READECGCONDL8                 97
     C                     MOVEL'*'       CNRECI
     C           *IN97     IFEQ *OFF
     C                     UPDAT@CONDL8
     C                     ENDIF
     C                     ENDDO
      *
     C           EXLDEL    TAG
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPDEL   : Permanant Delete                                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    :                                                   *
      *                                                               *
      *****************************************************************
     C           SRPDEL    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRPDEL'  @STK,Q
      *
      ** Position file is to number of parameters passed.
      *
     C                     SELEC
     C           P1CID     WHEQ *ZEROS
      *
     C                     SETON                     51
     C           P1LAYO    SETLLCGCONDL8
      *
      ** Position file is to number of parameters passed.
      *
     C           P1CID     WHNE *ZEROS
      *
     C                     SETON                     52
     C           KKEY01    SETLLCGCONDL8
      *
     C                     ENDSL
      *
     C                     MOVE *OFF      *IN97
     C           *IN97     DOWEQ*OFF
     C   51      P1LAYO    READECGCONDL8                 97
     C   52      KKEY01    READECGCONDL8                 97
     C           *IN97     IFEQ *OFF
     C                     DELET@CONDL8
     C                     ENDIF
     C                     ENDDO
      *
     C           EXPDEL    TAG
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialisation                                    *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : None                                              *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      ** Store layout and too and from members.
      *
     C                     MOVE P2PARM    SAVPRM
      *
      ** Store action code.
      *
     C           *LIKE     DEFN W0ACT     SAVACT
     C                     MOVELW0ACT     SAVACT
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      ** Close files if open.
      *
     C           FIL001    IFEQ 'Y'
     C                     MOVEL'N'       FIL001
     C                     CLOSECGCONDL0
     C                     ENDIF
      *
     C           FIL002    IFEQ 'Y'
     C                     MOVEL'N'       FIL002
     C                     CLOSECGCONDPD
     C                     ENDIF
      *
     C           FIL003    IFEQ 'Y'
     C                     MOVEL'N'       FIL003
     C                     CLOSECGCONDL8
     C                     ENDIF
      *
      ** Copy
      *
     C           W0ACT     IFEQ '*COPY'
      *
     C                     MOVEACMD@,1    EDT
      *
      ** Override input file
      *
     C                     MOVEL'CGCONDL0'U#FILE 10
      *
     C                     MOVEAU#FILE    EDT,13
     C                     MOVELP2FMBR    U#MBR
     C                     MOVEAU#MBR     EDT,29
     C                     MOVEAEDT       OVRDBF 80
     C                     Z-ADD80        CMDLEN 155
     C                     CALL 'QCMDEXC'                9090
     C                     PARM           OVRDBF
     C                     PARM           CMDLEN
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'*CALL  ' W0FILE
     C                     MOVEL'QCMDEXC' W0KEY
     C                     Z-ADD2         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ELSE
     C                     OPEN CGCONDL0
     C           *LIKE     DEFN *INLR     FIL001
     C                     MOVEL'Y'       FIL001
     C                     END
      *
      ** Override output file
      *
     C                     MOVEACMD@,2    EDT
      *
     C                     MOVEL'CGCONDPD'U#FILE
      *
     C                     MOVEAU#FILE    EDT,13
     C                     MOVELP2TMBR    U#MBR  10
     C                     MOVEAU#MBR     EDT,29
     C                     MOVEAEDT       OVRDBF 80
     C                     Z-ADD80        CMDLEN 155
     C                     CALL 'QCMDEXC'                9090
     C                     PARM           OVRDBF
     C                     PARM           CMDLEN
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'*CALL  ' W0FILE
     C                     MOVEL'QCMDEXC' W0KEY
     C                     Z-ADD3         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ELSE
     C                     OPEN CGCONDPD
     C           *LIKE     DEFN *INLR     FIL002
     C                     MOVEL'Y'       FIL002
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Logical or Permanent delete
      *
     C           W0ACT     IFEQ '*LDELETE'
     C           W0ACT     OREQ '*PDELETE'
      *                                                                   084416
     C                     MOVEACMD@,1    EDT                             084416
      *                                                                   084416
      ** Override input file                                              084416
      *                                                                   084416
     C                     MOVEL'CGCONDL0'U#FILE                          084416
      *                                                                   084416
     C                     MOVEAU#FILE    EDT,13                          084416
     C                     MOVELP2FMBR    U#MBR                           084416
     C                     MOVEAU#MBR     EDT,29                          084416
     C                     MOVEAEDT       OVRDBF                          084416
     C                     Z-ADD80        CMDLEN                          084416
      *                                                                   084416
     C                     CALL 'QCMDEXC'                9090             084416
     C                     PARM           OVRDBF                          084416
     C                     PARM           CMDLEN                          084416
      *                                                                   084416
     C           *IN90     IFEQ '1'                                       084416
     C                     MOVEL'*CALL  ' W0FILE                          084416
     C                     MOVEL'QCMDEXC' W0KEY                           084416
     C                     Z-ADD4         W0ERNB                          084416
     C                     MOVEL'MEM5003' W0MSGD                          084416
     C                     MOVEL'MIDAS  ' W0MSGF                          084416
     C                     EXSR SRERR                                     084416
     C                     ELSE                                           084416
     C                     OPEN CGCONDL0                                  084416
     C                     MOVEL'Y'       FIL001                          084416
     C                     END                                            084416
      *
      ** Override output file
      *
     C                     MOVEACMD@,3    EDT
      *
     C                     MOVEL'CGCONDL8'U#FILE
      *
     C                     MOVEAU#FILE    EDT,13
     C                     MOVELP2FMBR    U#MBR  10
     C                     MOVEAU#MBR     EDT,29
     C                     MOVEAEDT       OVRDBF 80
     C                     Z-ADD80        CMDLEN 155
     C                     CALL 'QCMDEXC'                9090
     C                     PARM           OVRDBF
     C                     PARM           CMDLEN
      *
     C           *IN90     IFEQ '1'
     C                     MOVEL'*CALL  ' W0FILE
     C                     MOVEL'QCMDEXC' W0KEY
     C                     Z-ADD5         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ELSE
     C                     OPEN CGCONDL8
     C           *LIKE     DEFN *INLR     FIL003
     C                     MOVEL'Y'       FIL003
     C                     ENDIF
      *
     C                     ENDIF
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
      *****************************************************************
     C/COPY CGCPYSRC,SRERRC
**  CPY@
(c) Finastra International Limited 2001
** Command Array
OVRDBF FILE(XXXXXXXXXX) MBR(XXXXXXXXXX) SHARE(*NO)
OVRDBF FILE(XXXXXXXXXX) MBR(XXXXXXXXXX) TOFILE(CGCONDPD) SHARE(*NO)
OVRDBF FILE(XXXXXXXXXX) MBR(XXXXXXXXXX) TOFILE(CGCONDL0) SHARE(*NO)
