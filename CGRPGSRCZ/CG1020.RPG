     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG UDC Test action code is valid')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG1020 - UDC Test action code is valid                       *
     F*                                                               *
     F*  Function:  This program checks the SPF set up for            *
     F*             specified action                                  *
     F*                                                               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSC022             Date 24Feb04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 086494             Date 19Apr95               *
      *                 S01522             Date 22Nov94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  086494 - Check Bank level authority if needed                *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  50    First cycle                                            *
     F*  90    General work indicator                                 *
     F*                                                               *
     F*  U7/U8 Error Ocurred                                          *
     F*  LR    End program                                            *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     E                    CPY@    1   1 80
      *
      *  Copyright table
      *
      **                                                                                      CSC022
      ** Array to hold commitment jobs name                                                   CSC022
      **                                                                                      CSC022
     E                    WCMT       10 10                                                    CSC022
      **                                                                                      CSC022
     E/COPY CGCPYSRC,SRERRE
      *
      *  end of Copysource for error processing
      *
     E/EJECT
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     I/COPY CGCPYSRC,SRERRI
      *
      *  End of Program Error Processing copysource member
      *
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
     IZMUSER    E DSZMUSER
      *
      *  Define ZMUSER for default data
      *
     ISDBRCH    E DSSDBRCHPD
      *
      *  Define data for Branch file
      *
     ISDGELR    E DSSDGELRPD
      *
      *  Define data for Branch file
      *
     IW0DATA      DS                            256
      *
      *  Define data structure used to pass parameters
      *
     I                                        1   3 B#BRCA
     I                                        4   4 I#ACTD
     I                                        5  14 I#CPGM
     I                                       15  15 I#BANK                086494
     IDSFDY     E DSDSFDY
     I*
     I* Data Structures used by Access Programs
     I*
     IDSSDY     E DSDSSDY
     I*
     I* Data Structures used by Access Programs
     I*
      **                                                                                      CSC022
     ISCCMT       DS                            256                                           CSC022
     I                                        1   30WCMTNO                                    CSC022
     I                                        4 103 WCJOBS                                    CSC022
      ** Commitment Control dataarea                                                          CSC022
      **                                                                                      CSC022
     ISCSARD    E DSSCSARDPD                                                                  CSC022
      ** DS for Switchable Features details                                                   CSC022
      **                                                                                      CSC022
     I/EJECT
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
     C                     PARM           W0ACT   8
     C                     PARM           W0DATA
      *
      *  Set up primary reference
      *
     C                     MOVELW0DATA    W0PREF
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
      *  Validate depending on action code
      *
     C           W0ACT     CASEQ'*BOOK   'SRBOOK
     C           W0ACT     CASEQ'*ORGN   'SRORGN
     C           W0ACT     CASEQ'*ACTCODE'SRACTD
     C                     CAS            SRACT
     C                     END
      *                                                                   086494
      * Check Bank Level Authority                                        086494
      *                                                                   086494
     C           I#BANK    IFEQ 'Y'                                       086494
     C                     EXSR SRBANK                                    086494
     C                     END                                            086494
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *  Return to calling program
      *
     C                     RETRN
     C/EJECT                                                              086494
     C*****************************************************************   086494
     C*                                                               *   086494
     C*  SRBANK   : Check that user has bank level authority          *   086494
     C*                                                               *   086494
     C*  CALLED BY: Main Processing                                   *   086494
     C*                                                               *   086494
     C*  CALLS    : SRERR   - report error and close down program     *   086494
     C*                                                               *   086494
     C*****************************************************************   086494
     CSR         SRBANK    BEGSR                                          086494
     C*                                                                   086494
     C*  Set up subroutine stack name                                     086494
     C*                                                                   086494
     C                     ADD  1         Q                               086494
     C                     MOVEL'SRBANK'  @STK,Q                          086494
     C*                                                                   086494
     C* Check bank level off ZMUSER                                       086494
     C*                                                                   086494
     C           BANK      IFNE 'Y'                                       086494
     C                     MOVEL'CGD1848' W0RTN                           086494
     C                     MOVEL'CGD1848' ZAMSID                          086494
     C                     MOVELI#CPGM    ZAPGMQ                          086494
     C                     EXSR ZASNMS                                    086494
     C                     GOTO EXBANK                                    086494
     C                     END                                            086494
     C*                                                                   086494
     C                     MOVEL'CGD1849' ZAMSID                          086494
     C                     EXSR ZASNMS                                    086494
     C*                                                                   086494
     C*  Unwind subroutine stack name                                     086494
     C*                                                                   086494
     C           EXBANK    TAG                                            086494
     C                     MOVEA*BLANKS   @STK,Q                          086494
     C                     SUB  1         Q                               086494
     C*                                                                   086494
     CSR                   ENDSR                                          086494
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRBOOK   : Validate that branch is allowed for booking       *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRBOOK    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRBOOK'  @STK,Q
      *
      *  Default branch is not passed
      *
     C           B#BRCA    IFEQ *BLANKS
     C                     MOVELDBRN      B#BRCA
     C                     END
      *
      *  If not multi-branched then no check as only one branch
      *  return default branch.
      *
     C           WUMBIN    IFNE 'Y'
     C                     GOTO EXBOOK
     C                     END
      *
      *  Check branch exists
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   P@RTCD  7        B:Return code
     C                     PARM '*KEY   ' P@OPTN  7        I:Option
     C                     PARM B#BRCA    P@BRCD  3        I:Key field
     C********** SDBRCH    PARM           DSFDY            O:Format                           CGL029
     C           SDBRCH    PARM           DSSDY                                               CGL029
      *
      * Branch not found
      *
     C           P@RTCD    IFNE *BLANKS
     C           P@RTCD    ANDNE'*NOSEL '
     C                     MOVEL'CGD1006' W0RTN
     C                     MOVEL'CGD1006' ZAMSID
     C                     MOVELI#CPGM    ZAPGMQ
     C                     EXSR ZASNMS
     C                     GOTO EXBOOK
     C                     END
      *
      *  If no selection exit with blanks in B#BRCA
      *
     C           P@RTCD    IFEQ '*NOSEL '
     C                     MOVEL'CGD1016' W0RTN
     C                     MOVEL'CGD1016' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVEL*BLANKS   B#BRCA
     C                     GOTO EXBOOK
     C                     ELSE
      *
      *  Move selected branch to output branch
      *
     C           A8BRCD    IFNE B#BRCA
     C                     MOVEL'CGD1017' W0RTN
     C                     MOVEL'CGD1017' ZAMSID
     C                     MOVELA8BRCD    ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVELA8BRCD    B#BRCA
     C                     END
     C                     END
      *
      *  Check user is authorised to booking branch
      *
     C                     CALL 'ZVBBU'
     C                     PARM B#BRCA    ZBRCDX  3        BRANCH CODE 3A
     C                     PARM           ERR     10       ERROR CODE 1S
      *
      * User has no authority to any booking branches
      *
     C           ERR       IFEQ 1
     C                     MOVEL'CGD1012' W0RTN
     C                     MOVEL'CGD1012' ZAMSID
     C                     MOVELB#BRCA    ZAMSDA
     C                     MOVELI#CPGM    ZAPGMQ
     C                     EXSR ZASNMS
     C                     GOTO EXBOOK
     C                     END
      *
      * User has no authority to this booking branch
      *
     C           ERR       IFEQ 2
     C                     MOVEL'CGD1013' W0RTN
     C                     MOVEL'CGD1013' ZAMSID
     C                     MOVELI#CPGM    ZAPGMQ
     C                     MOVELB#BRCA    ZAMSDA
     C                     EXSR ZASNMS
     C                     GOTO EXBOOK
     C                     END
      *
     C                     MOVEL'CGD1014' ZAMSID
     C                     MOVELB#BRCA    ZAMSDA
     C                     EXSR ZASNMS
      *
      *  Unwind subroutine stack name
      *
     C           EXBOOK    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRORGN   : Validate that branch is allowed for originating   *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRORGN    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRORGN'  @STK,Q
      *
      *  Default branch is not passed
      *
     C           B#BRCA    IFEQ *BLANKS
     C                     MOVELDBRN      B#BRCA
     C                     END
      *
      *  If not multi-branched then no check as only one branch
      *  return default branch.
      *
      *  OR Originating branches not used
      *
     C           WUMBIN    IFNE 'Y'
     C           BKOBUV    ORNE 'Y'
      *
     C                     MOVEL'CGD1015' ZAMSID
     C                     EXSR ZASNMS
     C                     GOTO EXORGN
      *
     C                     END
      *
      *  Check branch exists
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   P@RTCD  7        B:Return code
     C                     PARM '*KEY   ' P@OPTN  7        I:Option
     C                     PARM B#BRCA    P@BRCD  3        I:Key field
     C********** SDBRCH    PARM           DSFDY            O:Format                           CGL029
     C           SDBRCH    PARM           DSSDY                                               CGL029
      *
      * Branch not found
      *
     C           P@RTCD    IFNE *BLANKS
     C           P@RTCD    ANDNE'*NOSEL '
     C                     MOVEL'CGD1006' W0RTN
     C                     MOVEL'CGD1006' ZAMSID
     C                     MOVELI#CPGM    ZAPGMQ
     C                     EXSR ZASNMS
     C                     GOTO EXORGN
     C                     END
      *
      *  If no selection exit with blanks in B#BRCA
      *
     C           P@RTCD    IFEQ '*NOSEL '
     C                     MOVEL'CGD1016' W0RTN
     C                     MOVEL'CGD1016' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVEL*BLANKS   B#BRCA
     C                     GOTO EXORGN
     C                     ELSE
      *
      *  Move selected branch to output branch
      *
     C           A8BRCD    IFNE B#BRCA
     C                     MOVEL'CGD1017' W0RTN
     C                     MOVEL'CGD1017' ZAMSID
     C                     MOVELA8BRCD    ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVELA8BRCD    B#BRCA
     C                     END
     C                     END
      *
     C                     CALL 'ZVOBU'
     C                     PARM B#BRCA    ZOBRX   3
     C                     PARM 0         ERR     10
      *
      * User has no authority to any originating branches
      *
     C           ERR       IFEQ 1
     C                     MOVEL'CGD1009' W0RTN
     C                     MOVEL'CGD1009' ZAMSID
     C                     MOVELI#CPGM    ZAPGMQ
     C                     MOVELB#BRCA    ZAMSDA
     C                     EXSR ZASNMS
     C                     GOTO EXORGN
     C                     END
      *
      * User has no authority to this originating branch
      *
     C           ERR       IFEQ 2
     C                     MOVEL'CGD1010' W0RTN
     C                     MOVEL'CGD1010' ZAMSID
     C                     MOVELI#CPGM    ZAPGMQ
     C                     MOVELB#BRCA    ZAMSDA
     C                     EXSR ZASNMS
     C                     GOTO EXORGN
     C                     END
      *
     C                     MOVEL'CGD1011' ZAMSID
     C                     MOVELB#BRCA    ZAMSDA
     C                     EXSR ZASNMS
      *
      *  Unwind subroutine stack name
      *
     C           EXORGN    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRACTD   : Validate that user has access to action code      *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRACTD    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRACTD'  @STK,Q
      *
      *  If not multi-branched then no check as only one branch
      *  return default branch.
      *
     C                     SELEC
     C           WUMBIN    WHNE 'Y'
      *
     C                     CALL 'ZVACTU'
     C                     PARM I#ACTD    ZACTN   1
     C                     PARM 0         ERR     10
      *
      * User has no authority to action
      *
     C           ERR       IFEQ 1
     C                     MOVEL'CGD1004' W0RTN
     C                     MOVEL'CGD1004' ZAMSID
     C                     MOVELI#CPGM    ZAPGMQ
     C                     EXSR ZASNMS
     C                     GOTO EXACTD
     C                     END
      *
     C                     MOVEL'CGD1005' ZAMSID
     C                     MOVELI#ACTD    ZAMSDA
     C                     EXSR ZASNMS
      *
     C           B#BRCA    IFEQ *BLANKS
     C                     MOVELDBRN      B#BRCA
     C                     END
      *
      *  Multi-branched environment
      *
     C                     OTHER
      *
      *  Default branch is not passed
      *
     C           B#BRCA    IFEQ *BLANKS
     C                     MOVELDBRN      B#BRCA
     C                     END
      *
      *  Check branch exists
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   P@RTCD  7        B:Return code
     C                     PARM '*KEY   ' P@OPTN  7        I:Option
     C                     PARM B#BRCA    P@BRCD  3        I:Key field
     C********** SDBRCH    PARM           DSFDY            O:Format                           CGL029
     C           SDBRCH    PARM           DSSDY                                               CGL029
      *
      * Branch not found
      *
     C           P@RTCD    IFNE *BLANKS
     C           P@RTCD    ANDNE'*NOSEL '
     C                     MOVEL'CGD1006' W0RTN
     C                     MOVEL'CGD1006' ZAMSID
     C                     MOVELI#CPGM    ZAPGMQ
     C                     EXSR ZASNMS
     C                     GOTO EXACTD
     C                     END
      *
      *  If no selection exit with blanks in B#BRCA
      *
     C           P@RTCD    IFEQ '*NOSEL '
     C                     MOVEL'CGD1016' W0RTN
     C                     MOVEL'CGD1016' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVEL*BLANKS   B#BRCA
     C                     GOTO EXACTD
     C                     ELSE
      *
      *  Move selected branch to output branch
      *
     C           A8BRCD    IFNE B#BRCA
     C                     MOVEL'CGD1017' W0RTN
     C                     MOVEL'CGD1017' ZAMSID
     C                     MOVELA8BRCD    ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVELA8BRCD    B#BRCA
     C                     END
     C                     END
      *
     C                     CALL 'ZVACTBU'
     C                     PARM I#ACTD    ZACTN   1
     C                     PARM B#BRCA    ZBR     3
     C                     PARM 0         ERR     10
      *
      * User has no authority to any action for branch
      *
     C           ERR       IFEQ 1
     C                     MOVEL'CGD1007' W0RTN
     C                     MOVEL'CGD1007' ZAMSID
     C                     MOVELB#BRCA    ZAMSDA
     C                     MOVELI#CPGM    ZAPGMQ
     C                     EXSR ZASNMS
     C                     GOTO EXACTD
     C                     END
      *
      * User has no authority to action code for this branch
      *
     C           ERR       IFEQ 2
     C                     MOVEL'CGD1008' W0RTN
     C                     MOVEL'CGD1008' ZAMSID
     C                     MOVELB#BRCA    ZAMSDA
     C                     MOVELI#CPGM    ZAPGMQ
     C                     EXSR ZASNMS
     C                     GOTO EXACTD
     C                     END
      *
     C                     MOVEL'CGD1005' ZAMSID
     C                     MOVELI#ACTD    ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ENDSL
      *
      *  Unwind subroutine stack name
      *
     C           EXACTD    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRACT    : Undefined action - error                          *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRACT     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRACT '  @STK,Q
      *
      *  Database error undefined action
      *
     C                     MOVEL'*ACTION 'W0FILE           ***************
     C                     MOVELW0ACT     W0KEY            * DB ERROR 03 *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5006' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
      *
      *  Unwind subroutine stack name
      *
     C           EXACT     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     CSR         ZASNMS    BEGSR
      *================================================================
      * Send message to program's message queue
      *================================================================
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     END
      * If no message file specified, use default
     C           ZAMSGF    IFEQ *BLANK
     C                     MOVEL'CGUSRMSG'ZAMSGF
     C                     END
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message ID
     C                     PARM           ZAMSGF 10        Message file
     C                     PARM           ZAMSDA132        Message data
     C                     PARM           ZAMSTP  7        Message type
      *
      * Clear all fields for default mechanism next time
      *
     C                     MOVEL*BLANK    ZAPGMQ
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
      *================================================================
     CSR         ZAEXIT    ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialise and define fields                      *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Get Rundate information
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      *  Get ZMUSER information
      *
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
      *
      *  Find if originating branches used
      *
     C**********           CALL 'AOGELRR0'             9090                                   CGL029
     C                     CALL 'AOGELRR1'             9090                                   CGL029
     C                     PARM *BLANKS   P@RTCD  7        B:Return code
     C                     PARM '*FIRST'  P@OPTN  7        I:Option
     C********** SDGELR    PARM           DSFDY            O:Format                           CGL029
     C           SDGELR    PARM           DSSDY                                               CGL029
      *
      *  If return with an error (LR seton in called program) then
      *  process for database error.
      *
     C           *IN90     IFEQ '1'
     C           P@RTCD    OREQ '*ERROR*'
     C                     MOVEL'AOGELRR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD2         W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      **                                                                                      CSC022
      ** Initialize CSC022 and Skip Commit/Rollback flags                                     CSC022
      **                                                                                      CSC022
     C                     MOVE 'N'       CSC022  1                                           CSC022
     C                     MOVE 'N'       WCMTSK  1                                           CSC022
      **                                                                                      CSC022
      ** Access SAR details file to determine if CSC022 switchable feature                    CSC022
      ** is switched on                                                                       CSC022
      **                                                                                      CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   P@RTCD                                              CSC022
     C                     PARM '*VERIFY 'P@OPTN                                              CSC022
     C                     PARM 'CSC022'  P@SARD  6                                           CSC022
     C           SCSARD    PARM SCSARD    DSFDY                                               CSC022
      **                                                                                      CSC022
     C           P@RTCD    IFEQ *BLANKS                                                       CSC022
     C                     MOVE 'Y'       CSC022                                              CSC022
      **                                                                                      CSC022
      ** Get Jobs currently running i batch mode using SCCMRJOB dataarea                      CSC022
      **                                                                                      CSC022
     C           *NAMVAR   DEFN SCCMTJOB  SCCMT                                               CSC022
     C                     IN   SCCMT                                                         CSC022
      **                                                                                      CSC022
     C           WCMTNO    IFGT 0                                                             CSC022
      ** Move committed jobs to arrary for checking                                           CSC022
     C                     MOVEAWCJOBS    WCMT                                                CSC022
      ** Verify if job running exists in array                                                CSC022
     C           ##JOB     LOKUPWCMT                     50                                   CSC022
     C           *IN50     IFEQ *ON                                                           CSC022
     C                     MOVE 'Y'       WCMTSK                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ELSE                                                               CSC022
      ** Execute *PSSR if CSC022 is not found or Database error                               CSC022
     C           P@RTCD    IFNE '*NRF'                                                        CSC022
     C                     MOVEL'CSC022'  W0KEY                                               CSC022
     C                     MOVEL'SCSARDPD'W0FILE                                              CSC022
     C                     Z-ADD3         W0ERNB                                              CSC022
     C                     EXSR SRERR                                                         CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
     C                     ENDIF                                                              CSC022
      **                                                                                      CSC022
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *
      *  File and Program Error Processing
      *
     C/COPY CGCPYSRC,SRERRC
     C/EJECT
**  CPY@ table
(c) Finastra International Limited 2001
