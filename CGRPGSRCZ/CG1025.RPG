     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Validate group set parameters.')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                  *
     F*                                                               *
     F*  CG1025  - - - - Validate group set parameters.               *
     F*                                                               *
     F*  Function:   - - This program accepts the entries made on     *
     F*                   the group set amendment screen (CG1030),    *
     F*                   and validates them. If any errors are       *
     F*                   found, an appropriate message ID is         *
     F*                   returned in parameter P0RTN.                *
     F*                                                               *
     F*  Called By: CG1030 (Amend group set)                          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2330            Date 29Apr04               *
      *  Prev Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 084569             Date 10Mar95               *
      *                 S01522             Date 01Jan95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  BUG2330- CG programs looking at wrong dataarea for CSC022    *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
     F*  084569 - Provide additional validation                       *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*  90 -- Error detected.                                        *
     F*  91 -- No records for this group set -- CGRASCL1.             *
     F*  92 -- No records for this group set -- CGGSASL0.             *
     F*  93 -- End of file reached for this group set -- CGGSASL3.    *
     F*  94 -- No records for this group set -- CGGSETL0.             *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Subroutine usage                                             *
     F*  ~~~~~~~~~~~~~~~~                                             *
     F*  SRCSI  -- Validate the complex set indicator.                *
     F*  SRMI   -- Validate the multiple indicator.                   *
     F*  SRGSD  -- Validate the group set definition.                 *
     F*  SRINIT -- Provide initialisation and definitions.            *
     F*                                                               *
     F*  Copied-in routines:                                          *
     F*                                                               *
     F*  *PSSR  -- Program error routine.                             *
     F*  SRFILE -- File error routine.                                *
     F*  SRERR  -- Error reporting routine.                           *
     F*                                                               *
     F*****************************************************************
     F/SPACE
     F*-------------------------------------------------------------------
     F* Copied-in file definitions:
     F*
     F/COPY WNCPYSRC,CG1025FPG
     F*===================================================================
     FCGRASCL1IF  E           K        DISK         KINFSR SRFILE
     F* RDE association table
     F*-------------------------------------------------------------------
     FCGGSASL1IF  E           K        DISK         KINFSR SRFILE
     F* Group set association table -- keyed by From group set
     F*-------------------------------------------------------------------
     FCGGSASL3IF  E           K        DISK         KINFSR SRFILE
     F* Group set association table -- keyed by Linked group set
     F*-------------------------------------------------------------------
     FCGGSETL1IF  E           K        DISK         KINFSR SRFILE
     F* Group set definitions
     F*-------------------------------------------------------------------
     E/EJECT
     E*-------------------------------------------------------------------
     E* Error processing array:
     E*
     E/COPY CGCPYSRC,SRERRE
     E*
     E                    CPY@    1   1 80               Copyright
     E*
     E* Copied-in array definitions:
     E*
     E/COPY WNCPYSRC,CG1025EPG
     E*
     E*-------------------------------------------------------------------
     I/SPACE
     I*-------------------------------------------------------------------
     I/COPY WNCPYSRC,CG1025IPG
     I*
     I* Copied-in data structures
     I*
     I*...................................................................
      *                                                                                       CSC022
      ** Array to hold commitment jobs name                                                   CSC022
      *                                                                                       CSC022
     E                    WCMT       10 10                                                    CSC022
     I/COPY CGCPYSRC,SRERRI
     I*
     I* Error processing data structures
     I*
     I*...................................................................
     IDSFDY     E DSDSFDY                                                                     CSC022
     IWDSJOB    E DSSCCMTJOB                                                                  CSC022
     I              COMITNUM                        WCMTNO                                    CSC022
     I              COMITJOB1                       WJOB01                                    CSC022
     I              COMITJOB2                       WJOB02                                    CSC022
     I              COMITJOB3                       WJOB03                                    CSC022
     I              COMITJOB4                       WJOB04                                    CSC022
     I              COMITJOB5                       WJOB05                                    CSC022
     I              COMITJOB6                       WJOB06                                    CSC022
     I              COMITJOB7                       WJOB07                                    CSC022
     I              COMITJOB8                       WJOB08                                    CSC022
     I              COMITJOB9                       WJOB09                                    CSC022
     I              COMITJOB10                      WJOB10                                    CSC022
     I                                        4 103 WCJOBS                                    CSC022
      ** Commitment Control dataarea                                                          CSC022
      *                                                                                       CSC022
     ISCSARD    E DSSCSARDPD                                                                  CSC022
     IP0PARM      DS
     I*
     I* The P0PARM data structure passes group set details
     I*  to this program, as follows:
     I*
     I*  > P0GSET -- the group set name;
     I*  > P0CSI  -- the complex set indicator;
     I*  > P0MI   -- the multiple indicator;
     I*  > P0RSI  -- the restricted set indicator; and
     I*  > P0MMG  -- the mail merge group flag.
     I*
     I                                        1   6 P0GSET
     I                                        7   7 P0CSI
     I                                        8   8 P0MI
     I                                        9   9 P0RSI
     I                                       10  10 P0MMG
     I*...................................................................
     I* Named constants:
     I*
     I* #MSG01 -- Message identifier for error 1 --
     I*           Records were found for this group set
     I*            on the RDE association table
     I*            (complex set indicator is T or Y)
     I*
     I* #MSG02 -- Message identifier for error 2 --
     I*           Records were found for this group set
     I*            on the group set association table
     I*            (complex set indicator is N)
     I*
     I* #MSG03 -- Message identifier for error 3 --
     I*           Records were found for this group set
     I*            on the RDE association table
     I*            (multiple indicator is T or Y)
     I*
     I* #MSG04 -- Message identifier for error 4 --
     I*           A record was found for a parent group set
     I*            in the group sets definitions file
     I*            with a multiple indicator of "N" (No)
     I*            (multiple indicator is Y)
     I*
     I              'CGD1831'             C         #MSG01
     I              'CGD1832'             C         #MSG02
     I              'CGD1833'             C         #MSG03
     I              'CGD1834'             C         #MSG04
     I*
     I* Note: this list must be kept matched with the similar list in
     I*       program CG1030, since it is used to select which item
     I*       is highlighted as being in error.
     I********************************************************************
     C/EJECT
     C********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           P0PARM 10
     C                     PARM           P0PGM  10
     C*
     C* Add subroutine name to stack:
     C*
     C                     Z-ADD1         Q
     C                     MOVEL'@Main@'  @STK,Q
     C*
     C* Initialise:
     C                     EXSR SRINIT
     C*
     C* Validate the complex set indicator (P0CSI):
     C*
     C                     EXSR SRCSI
     C*
     C* Validate the multiple indicator (P0MI):
     C*
     C           P0RTN     IFEQ *BLANKS
     C                     EXSR SRMI
     C                     ENDIF
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     RETRN
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRCSI validates the complex set indicator.         **
     C********************************************************************
     C           SRCSI     BEGSR                           * S R C S I   *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRCSI '  @STK,Q
     C*
     C* If the complex set indicator is "T" (Top-level)
     C*                              or "Y" (Yes),
     C*  there must be no records for this group set
     C*  in the RDE association table:
     C*
     C           P0CSI     IFEQ 'T'
     C           P0CSI     OREQ 'Y'
     C           P0GSET    CHAINCGRASCL1             91
     C           *IN91     IFEQ *OFF
     C                     MOVE #MSG01    P0RTN
     C                     ENDIF
     C                     ENDIF
     C*
     C* If the complex set indicator is "N" (No),
     C*  there must be no records for this group set
     C*  in the group set association table:
     C*
     C           P0CSI     IFEQ 'N'
     C           P0GSET    CHAINCGGSASL1             92
     C           *IN92     IFEQ *OFF
     C                     MOVE #MSG02    P0RTN
     C                     ENDIF
     C                     ENDIF
     C*
     C           EXCSI     TAG                             <<<=== EXCSI
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRMI validates the multiple indicator.             **
     C********************************************************************
     C           SRMI      BEGSR                           * S R M I     *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRMI  '  @STK,Q
     C*
     C* If the multiple indicator is "T" (Top-level)
     C*                           or "Y" (Yes),
     C*  there must be no records for this group set
     C*  in the RDE association table:
     C*
     C           P0MI      IFEQ 'T'
     C           P0MI      OREQ 'Y'
     C           P0GSET    CHAINCGRASCL1             91
     C           *IN91     IFEQ *OFF
     C                     MOVE #MSG03    P0RTN
     C                     ENDIF
     C                     ENDIF
     C*
     C* If the multiple indicator is "Y" (Yes)
     C*   and there is no error up to this point,
     C*  validate the group sets to which this group
     C*   set is linked (the parent group sets):
     C*
     C           P0MI      IFEQ 'Y'
     C           P0RTN     ANDEQ*BLANKS
     C                     EXSR SRGSD
     C                     ENDIF
     C*
     C           EXMI      TAG                             <<<=== EXMI
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRGSD validates the group set definition.          **
     C********************************************************************
     C           SRGSD     BEGSR                           * S R G S D   *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRGSD '  @STK,Q
     C*
     C* Find the parent group sets for the current group set:
     C*
     C           P0GSET    SETLLCGGSASL3
     C*
     C           *IN93     DOUEQ*ON
     C           P0RTN     ORNE *BLANKS
     C           P0GSET    READECGGSASL3                 93
     C           *IN93     IFEQ *OFF
     C*
     C* If a parent group set has a multiple indicator of "N" (No),
     C*  note this as an error:
     C*
     C           GAFGST    CHAINCGGSETL1             94
     C           *IN94     IFEQ *OFF
     C           GSMREC    ANDEQ'N'
     C                     MOVE #MSG04    P0RTN
     C                     ENDIF
     C*
     C                     ENDIF
     C                     ENDDO
     C*
     C           EXGSD     TAG                             <<<=== EXGSD
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRINIT provides initialisation.                    **
     C********************************************************************
     C           SRINIT    BEGSR                           * S R I N I T *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C* Define and clear error return code:
     C*
     C                     MOVE *BLANKS   P0RTN
     C*
     C* Move the copyright parameter:
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C           EXINIT    TAG                             <<<=== EXINIT
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
      *                                                                                       CSC022
      ** Access SAR details file to determine if CSC022 switchable feature                    CSC022
      ** is switched on                                                                       CSC022
      *                                                                                       CSC022
     C                     MOVE 'N'       CSC022  1                                           CSC022
     C                     MOVE 'N'       WCMTSK  1                                           CSC022
      *                                                                                       CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   PRTCD   7                                           CSC022
     C                     PARM '*VERIFY' POPTN   7                                           CSC022
     C                     PARM 'CSC022'  PSARD   6                                           CSC022
     C           SCSARD    PARM SCSARD    DSFDY                                               CSC022
      *                                                                                       CSC022
     C           PRTCD     IFEQ *BLANKS                                                       CSC022
     C                     MOVE 'Y'       CSC022                                              CSC022
     C                     MOVE 'N'       WCMTSK                                              CSC022
      *                                                                                       CSC022
     C                     IN   WDSJOB                                                        CSC022
      *                                                                                       CSC022
     C           WCMTNO    IFGT 0                                                             CSC022
     C                     MOVEAWCJOBS    WCMT                                                CSC022
     C                     Z-ADD1         I       20                                          CSC022
      *                                                                                       CSC022
     C           ##JOB     LOKUPWCMT,I                   51                                   CSC022
     C           *IN51     IFEQ *ON                                                           CSC022
     C                     MOVE 'Y'       WCMTSK                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ENDIF                                                              CSC022
      *                                                                                       CSC022
     C                     ELSE                                                               CSC022
     C           PRTCD     IFNE '*NRF'                                                        CSC022
     C                     MOVEL'CSC022'  W0KEY                                               CSC022
     C                     MOVEL'SCSARDPD'W0FILE                                              CSC022
     C                     Z-ADD21        W0ERNB                                              CSC022
     C                     EXSR SRERR                                                         CSC022
     C                     ENDIF                                                              CSC022
      *                                                                                       CSC022
     C************NAMVAR   DEFN           WDSJOB                                       CSC022BUG2330
     C           *NAMVAR   DEFN SCCMTJOB  WDSJOB                                             BUG2330
      *                                                                                       CSC022
     C                     ENDIF                                                              CSC022
      *                                                                                       CSC022
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C** Subroutine *PSSR handles program errors.                       **
     C********************************************************************
     C*
     C/COPY CGCPYSRC,SRERRPSSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* File and Program Error Processing
     C*
     C/COPY CGCPYSRC,SRERRC
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* /Copy point for calculation specifications:
     C*
     C/COPY WNCPYSRC,CG1025CPG
     C*
     C********************************************************************
     O/EJECT
     O********************************************************************
     C* /Copy point for output:
     C*
     O/COPY WNCPYSRC,CG1025DOPG
     C*
     O********************************************************************
** CPY@ -- Copyright statement.
(c) Misys International Banking Systems Ltd. 2001
