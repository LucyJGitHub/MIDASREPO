     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CSD053
/*STD *  RPGBNOCVT                                                    *                       CSD053
/*EXS *  RPGCVTOPT1                                                   *                       CSD053
/*EXI *  TEXT('Midas CG COB Print Date Update Processing')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG2001 - Update of Print date on Correspondence              *
      *                                                               *
      *  Function:  This program runs daily in COB. Depending on      *
      *             the frequency the processing includes searching   *
      *             through the correspondents for those with a       *
      *             print date in the past and updating the print     *
      *             date for the next scheduled date. Also works out  *
      *             the next posting date.                            *
      *                                                               *
      *  Called By: mmCnnnn - (program name)                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CSD053             Date 01Jun06               *
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 093197             Date 01Dec95               *
      *                 091826             Date 10Aug95               *
      *                 083780             DATE 16FEB95               *
      *                 S01522             DATE 24NOV94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *           (Recompile)                                         *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  093197 - Update print scheduled date if lower than next      *
      *           working day rather than run date. Otherwise update  *
      *           done one day after week end if date is on Sunday.   *
      *  091826 - Various print generator errors                      *
      *  083780 - Correct DB error and set up Primary reference       *
      *  S01522 - User Defined Correspondence                         *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicator usage                                              *
      *  ~~~~~~~~~~~~~~~                                              *
      *                                                               *
      *  90   -    ROLBK indicator                                    *
      *                                                               *
      *  99   -    General work indicator                             *
      *                                                               *
      *****************************************************************
     FCGCORRL1UP  E           K        DISK         KINFSR SRFILE
     FCGCSCHL1UF  E           K        DISK         KINFSR SRFILE
     FCGPSCHL0IF  E           K        DISK         KINFSR SRFILE
      *
      **  Copyright array.
      *
     E                    ##CPY   1   1 80
      /COPY CGCPYSRC,SRERRE
     I/SPACE 5
      *
     I@CSCHL1
     I              CPPSCH                          CDDPRT                S01117
     I              CPNPDT                          CDDPDT                S01117
     I              CPSFDY                          CDDYNB                S01117
     I              CPNTDT                          CDNTDT                S01117
     I              CPCFRQ                          CDCFRQ                S01117
     I              CPPDAY                          CDPDAY                S01117
     I              CPCITM                          CDCITM                S01117
     I              CPBSNB                          CDBSNB                S01117
     I              CPCACC                          CDCACC                S01117
     I              CPACHG                          CDACHG                S01117
      *
     ISDBANK    E DSSDBANKPD
      *
      **  External DS for bank details
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, short data structure
      *
     ICPYR        DS
      **  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     IJBDTTM      DS
      * Job date/time
     I                                        7  120##JTM
     I                                        7   80##JHH
     I                                        9  100##JNN
     I                                       11  120##JSS
      *
      *  Defined Constants
      *
      *
     I              'HOLDMAIL'            C         ##HOLD
     I              'SUPPRESS'            C         ##SPRS
     I              'IMMEDIATE'           C         ##IMED
     I/COPY CGCPYSRC,SRERRI
      *
     C/SPACE 5
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   SRINIT - Initial processing - On First Call                 *
      *   SRINZ  - Initialise Fields on Each Invocation               *
      *   *PSSR  - Exception/Error Handling                           *
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRINZ  - Initialise Fields on Each Invocation  *
      *                SRFREQ - FREQUENCY PROCESSING                  *
      *****************************************************************
      *
      **  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
      *
      **  Initial processing - Once Only
      *
     C           *IN20     IFEQ *OFF                       B1
     C                     EXSR SRINIT                      1
     C                     MOVE *ON       *IN20             1
     C                     ENDIF                           E1
      *                                                                   083780
      **  Set up Primary Reference to be Correspondent Key                083780
      *                                                                   083780
     C                     MOVELCDCORR    W0PREF                          083780
      *
      *  Must be Within Activation Period
      *
     C           CDATDT    IFLE BJRDNB                     B1
     C           CDDADT    ANDGEBJRDNB                      1
     C           CDATDT    ORLE BJRDNB                     O1
     C           CDDADT    ANDEQ0                           1
     C                     EXSR SRFREQ                      1
     C                     ENDIF                           E1
      *................................................................
      *
      **  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRFREQ                                         *
      * Function    :  Calculate Next PRINT DATE, POSTING DATE        *
      *                                                               *
      * Called by   :                                                 *
      * Calls       :  SRDATE - DATE CALCULATION                      *
      *****************************************************************
     C           SRFREQ    BEGSR
      *
      **  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRFREQ'  @STK,Q
      *
      *................................................................
      *
      *          Reset Schedule flag field
      *
     C                     MOVEL'N'       ##CSCH  1        Schedule read
      *
      *          Calculate dates for primary correspondent
      *
     C                     EXSR SRDATE
     C           ##UPDT    IFEQ 'Y'                        B1
     C                     MOVEL##JOB     CDAJOB           Job name
     C                     MOVEL##USR     CDAUSR           User name
     C                     TIME           ##JTM
     C                     Z-ADD##JTM     CDATIM           Action Time
     C                     MOVELBJMRDT    CDARDT           Action Date
     C                     MOVEL'A'       CDAACT           Action Type
     C                     Z-ADDBJRDNB    CDRDNB           Run day number
     C                     UPDAT@CORRL1                     1
     C                     ENDIF                           E1
      *
     C           CDCORR    SETLL@CSCHL1                  79
     C           *IN79     IFEQ *ON                        B1
     C           CDCORR    READE@CSCHL1                  79 1
     C           *IN79     DOWEQ*OFF                       B2
      *
      *          Set Schedule flag field
      *
     C                     MOVEL'Y'       ##CSCH           Schedule read
      *
      *          Calculate dates for all related schedules
      *
     C                     EXSR SRDATE                      2
     C           ##UPDT    IFEQ 'Y'                        B3
     C                     MOVEL##JOB     CPAJOB           Job name
     C                     MOVEL##USR     CPAUSR           User name
     C                     TIME           ##JTM
     C                     Z-ADD##JTM     CPATIM           Action Time
     C                     MOVELBJMRDT    CPARDT           Action Date
     C                     MOVEL'A'       CPAACT           Action Type
     C                     Z-ADDBJRDNB    CPRDNB           Run day number
     C                     UPDAT@CSCHL1                     3
     C                     ENDIF                           E3
     C           CDCORR    READE@CSCHL1                  79 2
     C                     ENDDO                           E2
     C                     ENDIF                           E1
      *
      *................................................................
      *
     C           EXFREQ    TAG
      *
      **  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRDATE                                         *
      * Function    :  Calculate Next PRINT DATE, POSTING DATE        *
      *                                                               *
      * Called by   :  SRFREQ                                         *
      * Calls       :  SRERR  - Database Error                        *
      *                CG99FREQ - Frequency Processing                *
      *****************************************************************
     C           SRDATE    BEGSR
      *
      **  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRDATE'  @STK,Q
      *
      *................................................................
      *
     C                     MOVE 'N'       ##UPDT  1
     C***********CDDPDT    IFLE BJRDNB                     B1             093197
     C           CDDPDT    IFLT BJDNWD                     B1             093197
     C           CDDPDT    ANDNE0                           1
      *
      *          Fetch frequency
      *
     C           CDDPRT    CHAINCGPSCHL0             79     1
     C           *IN79     IFEQ *ON                        B2
     C                     MOVEL'CGPSCHL0'W0PREF                          083780
     C**********           MOVEL'CG99FREQ'W0FILE           ***************083780
     C                     MOVELCDDPRT    W0KEY            * DB ERROR 2  *
     C                     Z-ADD2         W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD            2
     C                     MOVEL'MIDAS  ' W0MSGF            2
     C                     EXSR SRERR                       2
     C                     ENDIF                           E2
      *
     C           STFREQ    IFNE ##HOLD                     B2
     C           STFREQ    ANDNE##SPRS                      2
     C           STFREQ    ANDNE##IMED                      2
      *
      *  Loop till valid date
      *
     C                     Z-ADD1         ##005N  50
     C           CDDPDT    DOUGTBJRDNB                     D1
     C           ##005N    ORGT 1000
     C                     ADD  1         ##005N
      *
     C                     MOVEL*BLANKS   W0FREQ
      *
      *          Calculate Print Date
      *
     C                     CALL 'CG99FREQ'                  2
     C                     PARM *BLANKS   ##RTCD            2
     C                     PARM '*NEWDATE'W0ACT   8         2
     C                     PARM STFREQ    W0FREQ 10         2
     C                     PARM CDDPDT    W0RDNB  50        2
     C                     PARM CDDYNB    W0MDAY  20        2
     C                     PARM BJLCCY    W0CCY   3         2
     C                     PARM BJSLCD    W0LOC   3         2
     C                     PARM           W0EXT 256         2
     C                     MOVE 'Y'       ##UPDT            2
      *
      **  Error in called program.
      *
     C           ##RTCD    IFNE *BLANK                     B3
      *
     C                     MOVEL'CG99FREQ'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 1  *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD            3
     C                     MOVEL'MIDAS  ' W0MSGF            3
     C                     EXSR SRERR                       3
     C                     ENDIF                           E3
      *
     C                     Z-ADDW0RDNB    CDDPDT            2
     C                     ENDDO                           D1
      *
     C                     ENDIF                           E2
     C                     ENDIF                           E1
      *
     C           CDNTDT    IFLE BJRDNB                     B1
     C           CDNTDT    ANDNE0                           1
      *
      *  Loop till valid date
      *
     C                     Z-ADD1         ##005N
     C           CDNTDT    DOUGTBJRDNB                     D1
     C           ##005N    ORGT 1000
     C                     ADD  1         ##005N
      *
     C                     MOVEL*BLANKS   W0FREQ
      *
      *          Calculate Next Posting date
      *
     C                     CALL 'CG99FREQ'                  1
     C                     PARM *BLANKS   ##RTCD            1
     C                     PARM '*NEWDATE'W0ACT   8         1
     C                     PARM CDCFRQ    W0FREQ 10         1
     C                     PARM CDNTDT    W0RDNB  50        1
     C                     PARM CDPDAY    W0MDAY  20        1
     C                     PARM BJLCCY    W0CCY   3         1
     C                     PARM BJSLCD    W0LOC   3         1
     C                     PARM           W0EXT 256         1
     C                     MOVE 'Y'       ##UPDT            1
      *
      **  Error in called program.
      *
     C           ##RTCD    IFNE *BLANK                     B2
      *
     C                     MOVEL'CG99FREQ'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 1  *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD            2
     C                     MOVEL'MIDAS  ' W0MSGF            2
     C                     EXSR SRERR                       2
     C                     ENDIF                           E2
      *
     C                     Z-ADDW0RDNB    CDNTDT
      *
     C                     ENDDO                           D1
      *
      * If Apply charge yes then do not zero or accumulate
      *
     C           CDACHG    IFNE 'Y'                        B2
     C                     ADD  CDBSNB    CDCACC            2
     C                     Z-ADD0         CDBSNB            2
     C                     ENDIF                           E2
      *
      * If Apply charge on schedule turnover then add in Item Charge
      *
     C***********CDACHG    IFNE 'S'                        B2             091826
     C           CDACHG    IFEQ 'S'                        B2             091826
     C                     Z-ADDCDCITM    CDBSNB            2
     C                     ENDIF                           E2
     C                     ENDIF                           E1
      *
     C           ##CSCH    IFEQ 'Y'                        B1
     C           CPRDTE    ANDLEBJRDNB                      1
     C           CPRDTE    ANDNE0                           1
      *
      *  Loop till valid date
      *
     C                     Z-ADD1         ##005N
     C           CPRDTE    DOUGTBJRDNB                     D1
     C           ##005N    ORGT 1000
     C                     ADD  1         ##005N
      *
     C                     MOVEL*BLANKS   W0FREQ
      *
      *          Calculate Next Reset Date
      *
     C                     CALL 'CG99FREQ'                  1
     C                     PARM *BLANKS   ##RTCD            1
     C                     PARM '*NEWDATE'W0ACT   8         1
     C                     PARM CPRFRQ    W0FREQ 10         1
     C                     PARM CPRDTE    W0RDNB  50        1
     C                     PARM CPRSDN    W0MDAY  20        1
     C                     PARM BJLCCY    W0CCY   3         1
     C                     PARM BJSLCD    W0LOC   3         1
     C                     PARM           W0EXT 256         1
     C                     MOVE 'Y'       ##UPDT            1
      *
      **  Error in called program.
      *
     C           ##RTCD    IFNE *BLANK                     B2
      *
     C                     MOVEL'CG99FREQ'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 1  *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD            2
     C                     MOVEL'MIDAS  ' W0MSGF            2
     C                     EXSR SRERR                       2
     C                     ENDIF                           E2
      *
     C                     Z-ADDW0RDNB    CPRDTE
     C                     ENDDO                           D1
      *
     C                     Z-ADD0         CPOSQN
     C                     Z-ADD0         CPNPAG
     C                     Z-ADD0         CPTPAG
     C                     ENDIF                           E1
      *
      *................................................................
      *
     C           EXDATE    TAG
      *
      **  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  AOBANKR0 - Obtain Bank details                 *
      *                AOGELRR0 - Obtain General Ledger Details       *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
      *
      **  Call access program for Bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM '*FIRST  '##OPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      **  Database error
     C           ##RTCD    IFNE *BLANK
      *
     C                     MOVEL'AOBANKR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD01        W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *
      *................................................................
      *
      **  Define Fields
      *
      **  Copyright
     C                     MOVEA##CPY     ##ACT  80
      *
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRPSSR
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRC
** ##CPY  **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
