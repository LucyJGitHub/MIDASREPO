     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Purge processing.')
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG2010        - Purge processing                             *
     F*                                                               *
     F*  Function:  This program purges archived data.                *
     F*                                                               *
     F*  Called By: To be defined.                                    *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CCG015             Date 09Aug01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 082402             Date 30Jan95               *
      *                 079873             Date 07Dec94               *
     F*                 079872             DATE 07DEC94               *
     F*                 079844             DATE 07DEC94               *
     F*                 079829             DATE 07DEC94               *
     F*                 S01522             DATE 22NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  CCG015 - Correspondence Manager Phase 1                      *
     F*  082402 - Purge listing                                       *
     F*  079873 - Add entry parameter of return code                  *
     F*  079872 - Reset format indicators for data file               *
     F*  079844 - Correct processing to look at item status           *
     F*  079829 - Correct File usage to CGX series                    *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*  80 -- Format @UDTAL0 in file CGXDTAL0.                       *
     F*  81 -- Format @UDT1L0 in file CGXDTAL0.                       *
     F*  82 -- Format @UDT2L0 in file CGXDTAL0.                       *
     F*  83 -- Format @UDT3L0 in file CGXDTAL0.                       *
     F*                                                               *
     F*  90 -- Error detected.                                        *
     F*  91 -- EOF detected.                                          *
     F*  92 -- Item found in archive data reference file.             *
     F*  93 -- EOF detected.                                          *
      *  94 -- End of File (PF/CGXXMLPD)                              *                       CCG015
      *  95 -- Error detected in delete of record in CGXXMLL0         *                       CCG015
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Subroutine usage                                             *
     F*  ~~~~~~~~~~~~~~~~                                             *
     F*  SRUDCR -- Read data from the UDC data reference file.        *
     F*  SRDEL  -- Handle a reference of "Deleted".                   *
     F*  SRITEM -- Handle a change in item identifier.                *
     F*  SRDDTA -- Delete extracted data records.                     *
     F*  SRAUD  -- Produce an audit report.                           *
     F*  SRINIT -- Provide initialisation and definitions.            *
      *  SRDXML -- Delete archived XML data                           *                       CCG015
     F*                                                               *
     F*  Copied in routines:                                          *
     F*                                                               *
     F*  *PSSR  -- Program error routine.                             *
     F*  SRFILE -- File error routine.                                *
     F*  SRERR  -- Error reporting routine.                           *
     F*                                                               *
     F*****************************************************************
     F/SPACE
     F*-------------------------------------------------------------------
     F* Copied-in file definitions:
     F*
     F/COPY WNCPYSRC,CG2010FPG
     F*===================================================================
     FCGXDTAL0UF  E           K        DISK         KINFSR SRFILE
     F                                              KCOMIT
     F* UDC extracted data
     F*-------------------------------------------------------------------
     F*CGUDCLL0UF  E           K        DISK         KINFSR SRFILE        079829
     FCGXDCLL0UF  E           K        DISK         KINFSR SRFILE         079829
     F                                              KCOMIT
     F* UDC data reference log
     F*-------------------------------------------------------------------
     F*CGUDCRL0UF  E           K        DISK         KINFSR SRFILE        079829
     FCGXDCRL0UF  E           K        DISK         KINFSR SRFILE         079829
     F                                              KCOMIT
     F* UDC data reference
      *                                                                                       CCG015
     FCGXXMLL0UF  E           K        DISK         KINFSR SRFILE                             CCG015
     F                                              KCOMIT                                    CCG015
      ** XML Archived Data                                                                    CCG015
      *                                                                                       CCG015
     F*
     F*-------------------------------------------------------------------082402
     FCG2010P1O   E             65     PRINTER      KINFDS SPOOLP     UC  082402
     F*
     F*
     F*-------------------------------------------------------------------
     E/EJECT
     E*-------------------------------------------------------------------
     E* Error processing array:
     E*
     E/COPY CGCPYSRC,SRERRE
     E*
     E                    CPY@    1   1 80               Copyright
     E                    CNT        10  9 0             Count values.
     E                    TXT        10  7               Count texts.
     E                    FIL        10 10               Count files.
     E*
     E* Copied-in array definitions:
     E*
     E/COPY WNCPYSRC,CG2010EPG
     E*
     E*-------------------------------------------------------------------
     I/EJECT
     I*-------------------------------------------------------------------
     I@XDTAL0     80
     I*
     I@XDT1L0     81
     I*
     I@XDT2L0     82
     I*
     I@XDT3L0     83
     I*
     I* Record format identifying indicators used in subroutine SRDDTA.
     I*
     I*...................................................................
     I/COPY WNCPYSRC,CG2010IPG
     I*
     I* Copied-in data structures
     I*
     I*...................................................................
     IW0FMT     E DSCGUDTAPD
     I*
     I* Record format of CGUDTAPD as an input parameter
     I*
     I*...................................................................
     I* Error processing data structures:
     I*
     I/COPY CGCPYSRC,SRERRI
     I*
     I*...................................................................
     I* RUNDAT data area:
     I*
     IRUNDAT    E DSRUNDAT
     I*                                                                   082402
     I*                                                                   082402
     ISDBANK    E DSSDBANKPD                                              082402
     I**  External DS for bank details                                    082402
     I*                                                                   082402
     IDSFDY     E DSDSFDY                                                 082402
     I*** First data structure for access program, Short data structure   082402
     I*
      *                                                                                       CCG015
     ISCSARD    E DSSCSARDPD                                                                  CCG015
      ** Switchable Feature details                                                           CCG015
      *                                                                                       CCG015
     I*...................................................................
     IW1DTA       DS                            256
     I* ______________________________________________
     I* Data structure for data being sent to CGZAUDIT
     I* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     I* Spool file name (if blank, name unchanged)
     I* N.B. To change name, I#SPLR must be Y
     I*       unless first call of program:
     I                                        1  10 I#SPLN
     I* Repeat open and close of spool file:
     I                                       11  11 I#SPLR
     I* Report reference:
     I                                       12  21 I#REF
     I* Report title (uses MSGID on CGUSRMSG):
     I                                       26  32 I#TITL
     I* Title underline (uses MSGID on CGUSRMSG):
     I                                       33  39 I#TUL
     I* Flag to control Report Control Facility:
     I                                       40  40 I#RCF
     I* Define fields for line printing:
     I                                       41  47 I#TEXT
     I                                       48  57 I#FILE
     I                                       58  720I#QTY
     I                                       73  730I#DECS
     I                                       74  74 I#EDIT
     I                                       75  770I#EXT
     I                                       78  78 I#MORE
     I                                       81 130 I#SUB
     I*...................................................................
     I* Data structure for compilation - Copyright insertion:
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I*...................................................................
     I* Message translation fields:
     I*
     IDSMTR       DS
     I                                        1 132 #MSDTA
     I                                      133 264 #MSTX1
     I#MSTX2      DS
     I                                        1 256 #MSTXA
     I                                      257 512 #MSTXB
     I*...................................................................
     I* Count fields counts:
     I*              ~~~~~~
     I***#COUNT      DS                                                   082402
     I#COWNT      DS                                                      082402
     I                                        1  90 CNT
     I**CGUDCRPD*inputs*and*deletions:                                    079829
     I* CGXDCRPD inputs and deletions:                                    079829
     I                                        1   90#CRIN
     I                                       10  180#CRDEL
     I**CGUDCLPD*deletions:*deletions:                                    079829
     I* CGXDCLPD inputs and deletions:                                    079829
     I                                       19  270#CLDEL
     I* Deletions of CGXDTxPD records:
     I                                       28  360#C1DEL
     I                                       37  450#C2DEL
     I                                       46  540#C3DEL
     I                                       55  630#CADEL
      *                                                                                       CCG015
      ** Deletion of CGXXMLPD records:                                                        CCG015
      *                                                                                       CCG015
     I                                       64  720#CMDEL                                    CCG015
      *                                                                                       CCG015
     I* Spares:
     I**********                             64  720#CXXX1                                    CCG015
     I**********                             73  810#CXXX2                                    CCG015
     I**********                             82  900#CXXX3                                    CCG015
      *                                                                                       CCG015
     I                                       73  810#CXXX1                                    CCG015
     I                                       82  900#CXXX2                                    CCG015
      *                                                                                       CCG015
     I* Count fields texts:
     I*              ~~~~~
     I#TEXTS      DS
     I                                        1  70 TXT
     I**CGUDCRPD*inputs*and*deletions:                                    079829
     I* CGXDCRPD inputs and deletions:                                    079829
     I I            'CAD1021'                 1   7 #TRIN
     I I            'CAD1023'                 8  14 #TRDEL
     I**CGUDCLPD*deletions**                                              079829
     I* CGXDCLPD deletions                                                079829
     I I            'CAD1023'                15  21 #TLDEL
     I* Deletions of CGXDTxPD records:
     I I            'CAD1023'                22  28 #T1DEL
     I I            'CAD1023'                29  35 #T2DEL
     I I            'CAD1023'                36  42 #T3DEL
     I I            'CAD1023'                43  49 #TADEL
      *                                                                                       CCG015
      ** Deletion of CGXXMLPD records:                                                        CCG015
      *                                                                                       CCG015
     I I            'CAD1023'                50  56 #TMDEL                                    CCG015
      *                                                                                       CCG015
     I* Spares:
     I*I********    '       '                50  56 #TEXT1                                    CCG015
     I*I********    '       '                57  63 #TEXT2                                    CCG015
     I*I********    '       '                64  70 #TEXT3                                    CCG015
      *                                                                                       CCG015
     I I            '       '                57  63 #TEXT1                                    CCG015
     I I            '       '                64  70 #TEXT2                                    CCG015
      *                                                                                       CCG015
     I*
     I* Count fields files:
     I*              ~~~~~
     I#FILES      DS
     I                                        1 100 FIL
     I**CGUDCRPD*inputs*and*deletions:                                    079829
     I*I*********** 'CGUDCRPD  '              1  10 #FRIN                 079829
     I*I*********** 'CGUDCRPD  '             11  20 #FRDEL                079829
     I* CGXDCRPD inputs and deletions:                                    079829
     I I            'CGXDCRPD  '              1  10 #FRIN                 079829
     I I            'CGXDCRPD  '             11  20 #FRDEL                079829
     I**CGUDCLPD*deletions:*********                                      079829
     I*I*********** 'CGUDCLPD  '             21  30 #FLDEL                079829
     I* CGXDCLPD deletions:                                               079829
     I I            'CGXDCLPD  '             21  30 #FLDEL                079829
     I* Deletions of CGXDTxPD records:
     I I            'CGXDT1PD  '             31  40 #F1DEL
     I I            'CGXDT2PD  '             41  50 #F2DEL
     I I            'CGXDT3PD  '             51  60 #F3DEL
     I I            'CGXDTAPD  '             61  70 #FADEL
      *                                                                                       CCG015
      ** Deletion of CGXXMLPD records:                                                        CCG015
      *                                                                                       CCG015
     I I            'CGXXMLPD  '             71  80 #FMDEL                                    CCG015
      *                                                                                       CCG015
     I* Spares:
     I*I********    '          '             71  80 #FILE1                                    CCG015
     I*I********    '          '             81  90 #FILE2                                    CCG015
     I*I********    '          '             91 100 #FILE3                                    CCG015
      *                                                                                       CCG015
     I I            '          '             81  90 #FILE1                                    CCG015
     I I            '          '             91 100 #FILE2                                    CCG015
      *                                                                                       CCG015
     I*                                                                   082402
     I*** Spool File Data Structure                                       082402
     ISPOOLP      DS                                                      082402
     I                                       83  92 PFILE                 082402
     I                                    B 123 1240PFNUM                 082402
     I                                    B 367 3680LINE                  082402
     I********************************************************************
     C/EJECT
     C********************************************************************
     C*                 M A I N L I N E                                  *
     C********************************************************************
     C           *ENTRY    PLIST                                          079873
     C                     PARM           W0RTN   7                       079873
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
     C*
     C* Initialise program:
     C*
     C           W0INIT    IFNE 'Y'
     C                     EXSR SRINIT
     C                     ENDIF
     C*
     C* Read from the UDC data reference file:
     C*
     C                     EXSR SRUDCR
     C*
     C* Copy in optional additional processing:
     C*
     C/COPY WNCPYSRC,CG2010PRC
     C*
     C* Produce an audit report:
     C*
     C                     EXSR SRAUD
      *                                                                   082402
     C                     EXSR SREND                                     082402
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C* Return to calling program:
     C*
     C                     RETRN
      *****************************************************************
      *
      /EJECT                                                              082402
      *****************************************************************   082402
      * Subroutine  :  SREND                                          *   082402
      * Function    :  Sets on last record and prints no details      *   082402
      *                to report or new header as necessary           *   082402
      * Called by   :  Mainline Processing                            *   082402
      * Calls       :  None                                           *   082402
      *****************************************************************   082402
     C           SREND     BEGSR                                          082402
      *                                                                   082402
      * If new page print header                                          082402
      *                                                                   082402
     C           *IN65     IFEQ *ON                                       082402
     C           #FIRST    ORNE 'Y'                        No records     082402
     C                     WRITEHEADER                                    082402
     C                     SETOF                         65               082402
     C                     ENDIF                                          082402
      *                                                                   082402
      *  Write 'No details to report'                                     082402
      *                                                                   082402
     C           #FIRST    IFNE 'Y'                                       082402
     C                     WRITEF5DTL1                                    082402
     C                     ENDIF                                          082402
      *                                                                   082402
      * To terminate program                                              082402
      *                                                                   082402
     C                     WRITEF0TRL1                                    082402
     C                     CLOSECG2010P1                                  082402
      *                                                                   082402
     C                     ENDSR                                          082402
      *                                                                   082402
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRUDCR reads data from file CGUDCRPD.              **
     C********************************************************************
     C           SRUDCR    BEGSR                           * S R U D C R *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRUDCR'  @STK,Q
     C* Clear hold value:
     C                     Z-ADD*ZERO     W#ITEM
     C*
     C           *IN91     DOUEQ*ON
     C***********          READ CGUDCRL0                 91               079829
     C                     READ CGXDCRL0                 91               079829
     C           *IN91     IFEQ *OFF
     C*
     C* Count the record:
     C                     ADD  1         #CRIN
     C*
     C* Change of item identifier:
     C*
     C           DRITEM    IFNE W#ITEM
     C*
     C* If not first break, test Purge flag for data item processing:
     C*
     C           W#ITEM    IFNE *ZERO
     C                     EXSR SRITEM
     C*
     C* Re-establish record lock after the COMIT:
     C*
     C***********DRITEM    CHAINCGUDCRL0             91                   079829
     C           DRITEM    CHAINCGXDCRL0             91                   079829
     C                     ENDIF
     C*
     C* Reset the Purge flag and save the identifier:
     C*
     C                     MOVE #TRUE     #PURGE  1
     C                     MOVE #FALSE    #ARC    1
     C                     Z-ADDDRITEM    W#ITEM
     C                     ENDIF
     C*...................................................................
     C/EJECT
     C*...................................................................
     C* Test the reference value for "deleted":
     C*
     C                     SELEC
     C* "High hist":
     C           DRDDAT    WHLE AGRDNB
     C                     EXSR SRDEL
     C                     EXSR SRPCUS                                    082402
     C* "Deleted":
     C***********DRFREF    WHEQ W#DEL                                     079844
     C           DRISTS    WHEQ 'DLTD'                                    079844
     C                     EXSR SRDEL
     C                     EXSR SRPCUS
     C* Other:
     C                     OTHER
     C                     MOVE #FALSE    #PURGE
     C                     ENDSL
     C*
     C                     ENDIF
     C                     ENDDO
     C*
     C* For final item, test flags for data item processing:
     C*
     C           W#ITEM    IFNE *ZERO
     C                     EXSR SRITEM
     C                     ENDIF
     C*
     C           EXUDCR    TAG                             <<<=== EXUDCR
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRDEL handles records with a reference of          **
     C**   "deleted" or a history date less than the run date.          **
     C********************************************************************
     C           SRDEL     BEGSR                           * S R D E L   *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRDEL '  @STK,Q
     C*
     C* Delete the reference record:
     C*
     C***********          DELET@UDCRL0                90                 079829
     C                     DELET@XDCRL0                90
     C           *IN90     IFEQ *ON
     C***********          MOVEL'CGUDCRPD'W0FILE           ***************079829
     C                     MOVEL'CGXDCRPD'W0FILE           ***************079829
     C                     MOVELDRITEM    W0KEY            ** Database  **
     C                     Z-ADD1         W0ERNB           ** error 1.  **
     C                     MOVE 'MEM5002' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
     C* Count the deletion:
     C                     ADD  1         #CRDEL
     C*...................................................................
     C/EJECT
     C*...................................................................
     C* Delete reference log records:
     C*
     C***********DRITEM    SETLLCGUDCLL0                                  079829
     C           DRITEM    SETLLCGXDCLL0                                  079829
     C           *IN93     DOUEQ*ON
     C***********DRITEM    READECGUDCLL0                 93               079829
     C           DRITEM    READECGXDCLL0                 93               079829
     C           *IN93     IFEQ *OFF
     C***********          DELET@UDCLL0                90                 079829
     C                     DELET@XDCLL0                90                 079829
     C           *IN90     IFEQ *ON
     C***********          MOVEL'CGUDCLPD'W0FILE           ***************079829
     C                     MOVEL'CGXDCLPD'W0FILE           ***************079829
     C                     MOVELDRITEM    W0KEY            ** Database  **
     C                     Z-ADD2         W0ERNB           ** error 2.  **
     C                     MOVE 'MEM5002' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
     C* Count the deletion:
     C                     ADD  1         #CLDEL
     C                     ENDIF
     C                     ENDDO
     C*
     C           EXDEL     TAG                             <<<=== EXDEL
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRITEM handles a change of item identifier.        **
     C********************************************************************
     C           SRITEM    BEGSR                           * S R I T E M *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRITEM'  @STK,Q
     C*
     C* If the Purge flag is True for the last item set,
     C*  delete item data records:
     C*
     C           #PURGE    IFEQ #TRUE
      *                                                                                       CCG015
      ** If CCG015 is not installed, delete archived records from                             CCG015
      ** CGUDTxPD, else delete from CGUXMLPD.                                                 CCG015
      *                                                                                       CCG015
     C           CCG015    IFEQ 'N'                                                           CCG015
      *                                                                                       CCG015
     C                     EXSR SRDDTA
     C                     ELSE                                                               CCG015
     C                     EXSR SRDXML                                                        CCG015
     C                     ENDIF                                                              CCG015
      *                                                                                       CCG015
     C                     ENDIF
     C* Commit changes:
     C                     COMIT
     C*
     C           EXITEM    TAG                             <<<=== EXITEM
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRDDTA deletes item data records (Purge is True).  **
     C********************************************************************
     C           SRDDTA    BEGSR                           * S R D D T A *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRDDTA'  @STK,Q
     C*
     C* UDC extracted data (64-byte data section):
     C*
     C           W#ITEM    SETLLCGXDTAL0
     C           *IN93     DOUEQ*ON
     C                     MOVEA'0000'    *IN,80                          079872
     C           W#ITEM    READECGXDTAL0                 93
     C           *IN93     IFEQ *OFF
     C*
     C* Select archive and counter according to the format read:
     C*
     C                     SELEC
     C           *IN80     WHEQ *ON
     C                     DELET@XDTAL0                90
     C           *IN90     IFEQ *OFF
     C                     ADD  1         #CADEL
     C                     ELSE
     C                     MOVEL'CGXDTAPD'W0FILE
     C                     ENDIF
     C*
     C           *IN81     WHEQ *ON
     C                     DELET@XDT1L0                90
     C           *IN90     IFEQ *OFF
     C                     ADD  1         #C1DEL
     C                     ELSE
     C                     MOVEL'CGXDT1PD'W0FILE
     C                     ENDIF
     C*...................................................................
     C/EJECT
     C*...................................................................
     C           *IN82     WHEQ *ON
     C                     DELET@XDT2L0                90
     C           *IN90     IFEQ *OFF
     C                     ADD  1         #C2DEL
     C                     ELSE
     C                     MOVEL'CGXDT2PD'W0FILE
     C                     ENDIF
     C*
     C           *IN83     WHEQ *ON
     C                     DELET@XDT3L0                90
     C           *IN90     IFEQ *OFF
     C                     ADD  1         #C3DEL
     C                     ELSE
     C                     MOVEL'CGXDT3PD'W0FILE
     C                     ENDIF
     C*
     C                     ENDSL
     C*
     C           *IN90     IFEQ *ON                        ***************
     C                     MOVELW#ITEM    W0KEY            ** Database  **
     C                     Z-ADD3         W0ERNB           ** error 3.  **
     C                     MOVE 'MEM5002' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
     C*
     C                     ENDIF
     C                     ENDDO
     C*
     C           EXDDTA    TAG                             <<<=== EXDDTA
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT                                                                                  CCG015
      *****************************************************************                       CCG015
      *  SRDXML - Subroutine to Delete Archived XML Data              *                       CCG015
      *                                                               *                       CCG015
      *  Called by: SRITEM                                            *                       CCG015
      *****************************************************************                       CCG015
      *                                                                                       CCG015
     C           SRDXML    BEGSR                                                              CCG015
      *                                                                                       CCG015
      ** Add subroutine name to stack:                                                        CCG015
      *                                                                                       CCG015
     C                     ADD  1         Q                                                   CCG015
     C                     MOVEL'SRDXML'  @STK,Q                                              CCG015
      *                                                                                       CCG015
     C           W#ITEM    SETLLCGXXMLL0                                                      CCG015
      *                                                                                       CCG015
     C           *IN94     DOUEQ*ON                                                           CCG015
     C           W#ITEM    READECGXXMLL0                 94                                   CCG015
      *                                                                                       CCG015
     C           *IN94     IFEQ *OFF                                                          CCG015
     C                     DELETCGXXMLL0               95                                     CCG015
      *                                                                                       CCG015
     C           *IN95     IFEQ *OFF                                                          CCG015
     C                     ADD  1         #CMDEL                                              CCG015
     C                     ELSE                                                               CCG015
     C                     MOVEL'CGXXMLPD'W0FILE                                              CCG015
     C                     MOVELW#ITEM    W0KEY                                               CCG015
     C                     Z-ADD6         W0ERNB                                              CCG015
     C                     MOVE 'MEM5002' W0MSGD                                              CCG015
     C                     MOVEL'MIDAS  ' W0MSGF                                              CCG015
     C                     EXSR SRERR                                                         CCG015
     C                     ENDIF                                                              CCG015
      *                                                                                       CCG015
     C                     ENDIF                                                              CCG015
     C                     ENDDO                                                              CCG015
      *                                                                                       CCG015
     C           EXDXML    TAG                                                                CCG015
      *                                                                                       CCG015
      ** Unwind subroutine stack:                                                             CCG015
      *                                                                                       CCG015
     C                     MOVE *BLANKS   @STK,Q                                              CCG015
     C                     SUB  1         Q                                                   CCG015
      *                                                                                       CCG015
     C                     ENDSR                                                              CCG015
     C********************************************************************                    CCG015
     C/EJECT
     C********************************************************************
     C**  Subroutine SRAUD produces an audit report.                    **
     C********************************************************************
     C           SRAUD     BEGSR                           * S R A U D   *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRAUD '  @STK,Q
     C*
     C* Prepare parameters for the report:
     C*
     C                     MOVEL'CG2010AU'I#SPLN
     C                     MOVEL'CG2010AU'I#REF
     C                     MOVE 'CAD1019' I#TITL
     C                     MOVE 'CAD1020' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C**CGUDCRPD:                                                         079829
     C* CGXDCRPD:                                                         079829
     C           1         DO   2         @C      30
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE TXT,@C    I#TEXT
     C                     MOVELFIL,@C    I#SUB
     C                     Z-ADDCNT,@C    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           W1DTA
     C                     PARM           W1RSQN  5
     C*
     C                     ENDDO
     C* Skip a line:
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN
     C                     PARM           W1ACT
     C                     PARM           W1DTA
     C                     PARM           W1RSQN
     C*...................................................................
     C/EJECT
     C*...................................................................
     C**CGUDCLPD:                                                         079829
     C* CGXDCLPD:                                                         079829
     C                     Z-ADD3         @C
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE TXT,@C    I#TEXT
     C                     MOVELFIL,@C    I#SUB
     C                     Z-ADDCNT,@C    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN
     C                     PARM           W1ACT
     C                     PARM           W1DTA
     C                     PARM           W1RSQN
     C* Skip a line:
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN
     C                     PARM           W1ACT
     C                     PARM           W1DTA
     C                     PARM           W1RSQN
     C* Deleted extracts:
      *                                                                                       CCG015
     C           CCG015    IFEQ 'N'                                                           CCG015
      *                                                                                       CCG015
     C           4         DO   7         @C
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE TXT,@C    I#TEXT
     C                     MOVELFIL,@C    I#SUB
     C                     Z-ADDCNT,@C    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN
     C                     PARM           W1ACT
     C                     PARM           W1DTA
     C                     PARM           W1RSQN
     C*
     C                     ENDDO
      *                                                                                       CCG015
     C                     ELSE                                                               CCG015
      *                                                                                       CCG015
      ** CGXXMLPD                                                                             CCG015
      *                                                                                       CCG015
     C                     Z-ADD8         @C                                                  CCG015
     C                     MOVE '*LINE   'W1ACT                                               CCG015
     C                     MOVE TXT,@C    I#TEXT                                              CCG015
     C                     MOVELFIL,@C    I#SUB                                               CCG015
     C                     Z-ADDCNT,@C    I#QTY                                               CCG015
     C                     Z-ADD*ZERO     I#DECS                                              CCG015
     C                     MOVE '1'       I#EDIT                                              CCG015
      *                                                                                       CCG015
     C                     CALL 'CGZAUDIT'                                                    CCG015
     C                     PARM           W1RTN                                               CCG015
     C                     PARM           W1ACT                                               CCG015
     C                     PARM           W1DTA                                               CCG015
     C                     PARM           W1RSQN                                              CCG015
      *                                                                                       CCG015
     C                     ENDIF                                                              CCG015
      *                                                                   082402
      **  Skip a line.                                                    082402
     C                     MOVE '*SKIP   'W1ACT                           082402
     C*                                                                   082402
     C                     CALL 'CGZAUDIT'                                082402
     C                     PARM           W1RTN                           082402
     C                     PARM           W1ACT                           082402
     C                     PARM           W1DTA                           082402
     C                     PARM           W1RSQN                          082402
      *                                                                   082402
      **  Close the Audit Print File.                                     082402
     C                     CLEARW1DTA                                     082402
     C                     MOVEL'CG2000  'I#REF                           082402
     C                     MOVE 'CAD1019' I#TITL                          082402
     C                     MOVE 'CAD1020' I#TUL                           082402
     C                     MOVEL'CAD1008' I#TEXT                          082402
     C                     MOVEL'CGUSRMSG'I#FILE                          082402
      *                                                                   082402
     C                     CALL 'CGZAUDIT'                                082402
     C                     PARM *BLANKS   W1RTN                           082402
     C                     PARM '*CLOSE  'W0ACT   8                       082402
     C                     PARM           W1DTA                           082402
     C                     PARM           W0RSQN  5                       082402
     C*
     C*
     C*
     C           EXAUD     TAG                             <<<=== EXAUD
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
      *
      *****************************************************************   082402
      * Subroutine  :  SRPCUS                                         *   082402
      * Function    :  To write new header as necessary               *   082402
      *                                                               *   082402
      * Called by   :  SRUDCR                                         *   082402
      * Calls       :  None                                           *   082402
      *****************************************************************   082402
     C           SRPCUS    BEGSR                                          082402
      *                                                                   082402
      ** If count equals 4 write new page                                 082402
      *                                                                   082402
     C           #COUNT    IFEQ 7                                         082402
     C           #FIRST    ORNE 'Y'                                       082402
     C                     MOVEL'Y'       #FIRST  1                       082402
     C                     WRITEHEADER                                    082402
     C                     Z-ADD1         #COUNT                          082402
     C                     ENDIF                                          082402
      *                                                                   082402
     C                     MOVE DRITEM    D1ITEM                          082402
     C                     MOVE DRDCOR    D1DCOR                          082402
     C                     MOVE DRPCOR    D1PCOR                          082402
     C                     MOVE DRBRCA    D1BRCA                          082402
     C                     MOVE DRORBR    D1ORBR                          082402
     C                     MOVE DROVBR    D1OVBR                          082402
     C                     MOVE DRMODI    D1MODI                          082402
     C                     MOVE DRMTRN    D1MTRN                          082402
     C                     MOVE DRMACT    D1MACT                          082402
     C                     MOVE DRISTS    D1ISTS                          082402
     C                     MOVE DRPTYP    D1PTYP                          082402
     C                     MOVE DRPSTP    D1PSTP                          082402
     C                     MOVE DRATRM    D1ATRM                          082402
     C           DRPRTD    IFGT 0                                         082402
     C                     CALL 'ZDATE2'               90                 082402
     C                     PARM           DRPRTD  50       Day No.        082402
     C                     PARM AGDFF     W2DTFM  1        Date Format    082402
     C                     PARM *ZEROS    W2DATE  60       Date           082402
     C                     PARM *BLANKS   D1PRTD  7        DDMMMYY        082402
     C                     ELSE                                           082402
     C                     MOVE 'NO DATE' D1PRTD                          082402
     C                     ENDIF                                          082402
     C                     MOVE DRLGID    D1LGID                          082402
      *                                                                   082402
      ** Write details and record one to counter                          082402
      *                                                                   082402
     C                     WRITEDETAIL                                    082402
     C                     ADD  1         #COUNT  10                      082402
      *                                                                   082402
     C                     ENDSR                                          082402
      *****************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRINIT provides initialisation and definitions.    **
     C********************************************************************
     C           SRINIT    BEGSR                           * S R I N I T *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C* Move copyright statement:
     C*
     C                     MOVE CPYR@#    ACT@   80
     C*
     C* Tag subroutine as executed:
     C*
     C                     MOVE 'Y'       W0INIT  1
     C*
     C* Get the date (from the RUNDAT data area);
     C*  calculate the number of the previous day:
     C*
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     SUB  1         AGRDNB
     C*
     C* Define "True", "False" and the item number comparison field:
     C*
     C                     MOVE 'T'       #TRUE   1
     C                     MOVE 'F'       #FALSE  1
     C           *LIKE     DEFN DRITEM    W#ITEM
     C*...................................................................
     C/EJECT
     C*...................................................................
     C* Get text for "Deleted" (message identifier CAD1264):
     C*
     C                     CALL 'CGC1000'              9090
     C                     PARM 'CGD1264' #MSGID  7
     C                     PARM 'CGUSRMSG'#MSGF  10
     C                     PARM           #MSDTA
     C                     PARM *BLANKS   #MSTX1
     C                     PARM *BLANKS   #MSTX2
     C*
     C                     MOVEL#MSTX1    W#DEL  25
     C*
     C* Initialise the counters:
     C*
     C                     Z-ADD*ZERO     #CRIN            CGUDCRPD in
     C                     Z-ADD*ZERO     #CRDEL           CGUDCRPD del
     C                     Z-ADD*ZERO     #CLDEL           CGUDCLPD del
     C                     Z-ADD*ZERO     #C1DEL           CGXDT1PD del
     C                     Z-ADD*ZERO     #C2DEL           CGXDT2PD del
     C                     Z-ADD*ZERO     #C3DEL           CGXDT3PD del
     C                     Z-ADD*ZERO     #CADEL           CGXDTAPD del
     C                     Z-ADD*ZERO     #CMDEL                                              CCG015
      *                                                                   082402
      *  Extract bank details                                             082402
      *                                                                   082402
     C                     CALL 'AOBANKR0'             9090               082402
     C                     PARM *BLANKS   P@RTCD  7        B:Return code  082402
     C                     PARM '*FIRST ' P@OPTN  7        I:Option       082402
     C           SDBANK    PARM *BLANKS   DSFDY                           082402
      *  If return with an error (LR seton in called program) then        082402
      *  process for database error.                                      082402
     C           *IN90     IFEQ '1'                                       082402
     C           P@RTCD    OREQ '*ERROR*'                                 082402
     C                     MOVEL'AOBANKR0'W0FILE                          082402
     C                     MOVEL'*CALL'   W0KEY            ************** 082402
     C                     Z-ADD4         W0ERNB           *DB ERROR 4  * 082402
     C                     MOVEL'MEM5003' W0MSGD           ************** 082402
     C                     MOVEL'MIDAS  ' W0MSGF                          082402
     C                     EXSR SRERR                                     082402
     C                     END                                            082402
      *                                                                                       CCG015
      ** Determine if Correspondence Manager Phase 1 Development                              CCG015
      ** on iSeries is installed                                                              CCG015
      *                                                                                       CCG015
     C                     CALL 'AOSARDR0'                                                    CCG015
     C                     PARM *BLANKS   PRTCD   7                                           CCG015
     C                     PARM '*VERIFY' POPTN   7                                           CCG015
     C                     PARM 'CCG015'  PSARD   6                                           CCG015
     C           SCSARD    PARM SCSARD    DSFDY                                               CCG015
      *                                                                                       CCG015
     C           PRTCD     IFEQ *BLANKS                                                       CCG015
     C                     MOVE 'Y'       CCG015  1                                           CCG015
     C                     ELSE                                                               CCG015
     C                     MOVE 'N'       CCG015                                              CCG015
      *                                                                                       CCG015
     C           PRTCD     IFNE '*NRF    '                                                    CCG015
     C                     MOVEL'SCSARDPD'W0FILE                                              CCG015
     C                     MOVEL'*CALL'   W0KEY     P                                         CCG015
     C                     Z-ADD7         W0ERNB                                              CCG015
     C                     MOVEL'MEM5003' W0MSGD                                              CCG015
     C                     MOVEL'MIDAS  ' W0MSGF                                              CCG015
     C                     EXSR SRERR                                                         CCG015
     C                     ENDIF                                                              CCG015
      *                                                                                       CCG015
     C                     ENDIF                                                              CCG015
      *                                                                   082402
      ** Report ref.                                                      082402
     C                     MOVEL##PGM     H1REFN                          082402
      *                                                                   082402
      *  Retrieve title for report.                                       082402
      *                                                                   082402
     C                     CALL 'CGC1000'              9090               082402
     C                     PARM 'CGD1785' #MSGID                          082402
     C                     PARM 'CGUSRMSG'#MSGF                           082402
     C                     PARM           #MSDTA                          082402
     C                     PARM *BLANKS   #MSTX1                          082402
     C                     PARM *BLANKS   #MSTX2                          082402
      *                                                                   082402
     C                     MOVEL#MSTX1    H2TITL 60                       082402
      *                                                                   082402
      *  Retrieve title underline                                         082402
     C                     CALL 'CGC1000'              9090               082402
     C                     PARM 'CGD1786' #MSGID                          082402
     C                     PARM 'CGUSRMSG'#MSGF                           082402
     C                     PARM           #MSDTA                          082402
     C                     PARM *BLANKS   #MSTX1                          082402
     C                     PARM *BLANKS   #MSTX2                          082402
      *                                                                   082402
     C                     MOVEL#MSTX1    H2TUL  60                       082402
      *                                                                   082402
     C*                                                                   082402
     C*** Open report and log to RCF                                      082402
     C                     OPEN CG2010P1                                  082402
     C*                                                                   082402
     C                     MOVE PFILE     SFILE                           082402
     C                     Z-ADDPFNUM     ZSNUM                           082402
     C                     MOVE *BLANKS   ZSENTY                          082402
     C                     EXSR SPLF                                      082402
     C*
     C           EXINIT    TAG                             <<<=== EXINIT
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*                                                                   082402
     C*****************************************************************   082402
     C*                                                               *   082402
     C* SPLF   -  Log Spool File to RCF via ZSFILE                    *   082402
     C*                                                               *   082402
     C* Called by: SRINIT                                             *   082402
     C*                                                               *   082402
     C* Calls: ZSFILE(pgm), SRERR                                     *   082402
     C*                                                               *   082402
     C*****************************************************************   082402
     C*                                                                   082402
     C           SPLF      BEGSR                                          082402
     C*                                                                   082402
     C*** Set up subroutine stack name                                    082402
     C                     ADD  1         Q       50                      082402
     C                     MOVEL'SPLF'    @STK,Q                          082402
     C*                                                                   082402
     C*................................................................   082402
     C*                                                                   082402
     C*                                                                   082402
     C                     CALL 'ZSFILE'                                  082402
     C                     PARM *BLANKS   ZSSEQ   5                       082402
     C                     PARM           ZSENTY  3                       082402
     C                     PARM           SFILE  10                       082402
     C                     PARM           ZSNUM   60                      082402
     C                     PARM           ZSERR   1                       082402
     C*                                                                   082402
     C           ZSERR     IFEQ 'Y'                                       082402
     C                     MOVEL'ZSFILE  'W0FILE           ************** 082402
     C                     MOVEL'ONLY    'W0KEY            *DB ERROR 5  * 082402
     C                     Z-ADD5         W0ERNB           ************** 082402
     C                     MOVEL'MEM5003' W0MSGD                          082402
     C                     MOVEL'MIDAS  ' W0MSGF                          082402
     C                     EXSR SRERR                                     082402
     C                     END                                            082402
     C*                                                                   082402
     C*                                                                   082402
     C*                                                                   082402
     C*................................................................   082402
     C*                                                                   082402
     C**  Unwind subroutine stack name                                    082402
     C                     MOVEA*BLANKS   @STK,Q                          082402
     C                     SUB  1         Q                               082402
     C*                                                                   082402
     C                     ENDSR                                          082402
      *****************************************************************   082402
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* /Copy point for calculations:
     C*
     C/COPY WNCPYSRC,CG2010CPG
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C** Subroutine *PSSR handles program errors.                       **
     C********************************************************************
     C*
     C/COPY CGCPYSRC,SRERRPSSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* /Copy point for error-handling subroutine:
     C*
     C/COPY CGCPYSRC,SRERRC
     C*
     C********************************************************************
     O/EJECT
     O********************************************************************
     O* /Copy point for output specifications:
     O*
     O/COPY WNCPYSRC,CG2010OPG
     O*
     O********************************************************************
** CPY@ -- Copyright statement
(c) Misys International Banking Systems Ltd. 2001
