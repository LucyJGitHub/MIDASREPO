     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CSD053
/*STD *  RPGBNOCVT                                                    *                       CSD053
/*EXS *  RPGCVTOPT1                                                   *                       CSD053
/*EXI *  TEXT('Midas CG UDC print address labels')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG3500 - Print address labels                                *
      *                                                               *
      *  Function:  This program prints address labels for any item   *
      *             of correspondence where the destination's 'print  *
      *             label' indication is set.                         *
      *                                                               *
      *  Called By: UDC print process job                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CCG024             Date 29Sep16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD053             Date 01Jun06               *
      *                 CSW037A            Date 02May05               *
      *                 BUG6198            Date 04Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. 190022             Date 15Jun99               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 15Dec94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG024 - Use Atlernate Name and Address in Correspondence    *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  BUG6198- Cannot save new Details for CUSD (Recompile)        *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  190022 - Recompiled due to increased size of OVRPRTF string  *
      *           in PF/CGPRTPPD and PF/CGPGENPD files.               *
      *  S01522 - User Defined Correspondence                         *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * Note:                                                         *
      * -----                                                         *
      *                                                               *
      * Label stationery is defined to this program using eight       *
      * numeric parameters (all 5,0) defined in alpha format on       *
      * data area CGDTA, beginning at position 116:                   *
      *                                                               *
      *  o SIZX   - the width of the stationery, in characters.       *
      *  o SIZY   - the length of the stationery, in lines.           *
      *  o NOX    - the number of labels across the page.             *
      *  o NOY    - the number of labels down the page.               *
      *  o MAX    - the left/right margin: the number of characters   *
      *             from the edge of the page to the first label.     *
      *  o MAY    - the top/bottom margin: the number of lines        *
      *             from the top of the page to the first label.      *
      *  o LABX   - the width of each label, in characters.           *
      *  o LABY   - the length of each label, in lines.               *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Indicator usage                                              *
      *  ~~~~~~~~~~~~~~~                                              *
      *                                                               *
      *  01   -    XX                                                 *
      *  50   -    EoF, LF/CGPGENLn Print generator driver            *
      *  55   -    NrF, LF/CGCORRL0 Correspondent details             *
      *  60   -    NrF, LF/SDCUSTL0 Customer details                  *
      *  65   -    NrF, LF/CGUDCRL0 UDC reference                     *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     FCGPGENL2IF  E           K        DISK                           UC
     F                                              KINFSR SRFILE
     FCGPGENL3IF  E           K        DISK                           UC
     F                                              KINFSR SRFILE
     FCGCORRL0IF  E           K        DISK
     F                                              KINFSR SRFILE
     FCGCORRL4IF  E           K        DISK         KINFSR SRFILE                             CCG024
     FCGUDCRL2IF  E           K        DISK
     F                                              KINFSR SRFILE
     FSDCUSTL0IF  E           K        DISK
     F                                              KINFSR SRFILE
     F/COPY WNCPYSRC,CG3500F001
     FCG3500P1O   E                    PRINTER      KINFDS SPOOLP     UC
     F                                              KINFSR SRFILE
      /EJECT
     E                    CPY@    1   1 80
      ** Copyright statement
      *
     E**********          @ADA        8 50                                                    CSD053
     E                    @ADA        8100                                                    CSD053
      ** Address data
      *
     E/COPY WNCPYSRC,CG3500E001
     E                    @C        200  1
      ** Column of page
      *
     E                    @X1       100  5 0
     E                    @Y1       100  5 0
     E                    @X2       100  5 0
     E                    @Y2       100  5 0
      ** Arrays of label co-ordinates on page
      *
      /COPY CGCPYSRC,SRERRE
      /EJECT
     ICPYR@#      DS
     I                                        1  80 CPY@
      ** Copyright statement
      *
     ISPOOLP      DS
     I                                       83  92 PFILE
     I                                    B 123 1240PFNUM
      **  File information data structure
      *
     I##KEY       DS
     I                                    P   1   50B2ITEM
     I                                        6  15 B2DCOR
      ** DS for reporting database errors
      *
     I##ADDR      DS
     I**********                              1  50 ##ADD1                                    CSD053
     I**********                              1  50 CDADD1                                    CSD053
     I**********                              1  35 BBCNA1                                    CSD053
     I**********                             51 100 ##ADD2                                    CSD053
     I**********                             51 100 CDADD2                                    CSD053
     I**********                             51  85 BBCNA2                                    CSD053
     I**********                            101 150 ##ADD3                                    CSD053
     I**********                            101 150 CDADD3                                    CSD053
     I**********                            101 135 BBCNA3                                    CSD053
     I**********                            151 200 ##ADD4                                    CSD053
     I**********                            151 200 CDADD4                                    CSD053
     I**********                            151 185 BBCNA4                                    CSD053
     I**********                            201 250 ##ADD5                                    CSD053
     I**********                            201 250 CDADD5                                    CSD053
     I**********                            251 300 ##ADD6                                    CSD053
     I**********                            251 300 CDADD6                                    CSD053
     I**********                            301 350 ##ADD7                                    CSD053
     I**********                            301 350 CDADD7                                    CSD053
     I**********                            351 400 ##ADD8                                    CSD053
     I**********                            351 400 CDADD8                                    CSD053
     I**********                              1 400 @ADA                                      CSD053
     I                                        1 100 ##ADD1                                    CSD053
     I                                        1 100 CDADD1                                    CSD053
     I                                        1  35 BBCNA1                                    CSD053
     I                                      101 200 ##ADD2                                    CSD053
     I                                      101 200 CDADD2                                    CSD053
     I                                      101 135 BBCNA2                                    CSD053
     I                                      201 300 ##ADD3                                    CSD053
     I                                      201 300 CDADD3                                    CSD053
     I                                      201 235 BBCNA3                                    CSD053
     I                                      301 400 ##ADD4                                    CSD053
     I                                      301 400 CDADD4                                    CSD053
     I                                      301 335 BBCNA4                                    CSD053
     I                                      401 500 ##ADD5                                    CSD053
     I                                      401 500 CDADD5                                    CSD053
     I                                      501 600 ##ADD6                                    CSD053
     I                                      501 600 CDADD6                                    CSD053
     I                                      601 700 ##ADD7                                    CSD053
     I                                      601 700 CDADD7                                    CSD053
     I                                      701 800 ##ADD8                                    CSD053
     I                                      701 800 CDADD8                                    CSD053
     I                                        1 800 @ADA                                      CSD053
      ** Address fields
      *
     I/COPY WNCPYSRC,CG3500I001
     I##LINE      DS                        200 200
     I                                        1 200 @C
      ** Line of page
      *
     ICGDATA    E DSCGDTA
      ** Data Area for UDC (used for next item identifier)
      * Data structure for AOSVALR0 string                                                    CCG024
     I            DS                                                                          CCG024
     I                                        1 200 SVAL1                                     CCG024
     I                                        1   1 SVAL11                                    CCG024
      *                                                                                       CCG024
      * System Value                                                                          CCG024
     I            DS                                                                          CCG024
     I I            'RecipientSpecialChar'    1  20 SVALK1                                    CCG024
      *                                                                                       CCG024
      *
      /COPY CGCPYSRC,SRERRI
      /EJECT
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   SRINIT - Initial processing                                 *
      *   SRREAD - Access next item requiring label                   *
      *   SRPRNT - Print label page                                   *
      *   SRFILL - Fill label with correspondent address              *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      ** Perform initial process
     C                     EXSR SRINIT
      *
      ** Read the Print Process Driver file
     C                     EXSR SRREAD
      *
      ** DoWhile not End_of_File
     C           *IN50     DOWEQ'0'
      *
      ** If this is first record, open printer file
     C           ##FRST    IFEQ 'Y'
     C                     OPEN CG3500P1
     C                     ENDIF
      *
      ** If change of entity or end of page, output current page. Set
      ** end_of_page indication to ensure page is not printed again
      ** on end_of_file.
     C                     MOVEL'N'       ##EPAG  1
     C           #SAVBR    IFNE B2BRCA
     C           ##ENTY    ANDEQ'Y'
     C           ##FRST    ANDEQ'N'
     C           #L        OREQ #C
     C                     EXSR SRPRNT
     C                     Z-ADD0         #L
     C                     ENDIF
      *
      ** If change of entity, close and open spool file
     C           #SAVBR    IFNE B2BRCA
     C           ##ENTY    ANDEQ'Y'
     C           ##FRST    ANDEQ'N'
     C                     CLOSECG3500P1
     C                     OPEN CG3500P1
     C                     ENDIF
      *
      ** Access next label
     C                     ADD  1         #L
     C/COPY WNCPYSRC,CG3500C001
      *
      ** Fill next label with this correspondent's address
     C                     EXSR SRFILL
      *
      ** Read next item
     C                     EXSR SRREAD
      *
      ** Set off first_item_requiring_label indication
     C                     MOVEL'N'       ##FRST
      *
      ** EndDoWhile: last record read
     C                     ENDDO
      *
      ** Print final page
     C           ##FRST    IFEQ 'N'
     C                     EXSR SRPRNT
     C                     ENDIF
      *
     C                     SETON                     LR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRREAD                                         *
      * Purpose     :  Access next item requiring label               *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           SRREAD    BEGSR
      *
      ** Save current branch code
     C                     MOVELB2BRCA    #SAVBR  3
      *
      ** Read file until record found for which label is required
     C           *IN50     DOUEQ'1'
     C           ##RFND    OREQ 'Y'
      *
     C           P#ACCP    IFEQ 'CGPGENL2'
     C                     READ CGPGENL2                 50
     C                     ELSE
     C/COPY WNCPYSRC,CG3500C005
     C                     READ CGPGENL3                 50
     C/COPY WNCPYSRC,CG3500C006
     C                     ENDIF
      *
      ** If record found, check whether label is required
     C           *IN50     IFEQ '0'
      *
      ** Initialise valid_record_found indication
     C                     MOVEL'Y'       ##RFND  1
      *
      ** Access reference record
     C           K#KEY     CHAINCGUDCRL2             65
      *
      ** Database error if reference not found
     C           *IN65     IFEQ '1'
     C                     MOVEL'CGUDCRL2'W0FILE
     C                     MOVEL##KEY     W0KEY            ***************
     C                     Z-ADD1         W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *
      ** If status is not PPFN this is a copy, so no label required
     C           DRISTS    IFNE 'PPFN'
     C                     MOVEL'N'       ##RFND
     C                     ELSE
      *
      ** If feature CCG024 is on and special character is found in DRDCOR                     CCG024
     C                     MOVEL'N'       XFOUND  1                                           CCG024
     C           CCG024    IFEQ 'Y'                                                           CCG024
     C           SVAL11    SCAN B2DCOR                   99                                   CCG024
     C           *IN99     IFEQ *ON                                                           CCG024
     C                     MOVELB2DCOR    XXCORR  6                                           CCG024
     C           XXCORR    CHAINCGCORRL4             51                                       CCG024
     C           *IN51     IFEQ *ON                                                           CCG024
     C                     MOVEL'CGCORRL4'W0FILE                                              CCG024
     C                     MOVELB2DCOR    W0KEY            ***************                    CCG024
     C                     Z-ADD11        W0ERNB           * DB ERROR 11 *                    CCG024
     C                     MOVEL'MEM5004' W0MSGD           ***************                    CCG024
     C                     MOVEL'MIDAS  ' W0MSGF                                              CCG024
     C                     EXSR SRERR                                                         CCG024
     C                     ENDIF                                                              CCG024
     C                     MOVEL'Y'       XFOUND                                              CCG024
     C                     ENDIF                                                              CCG024
     C                     ENDIF                                                              CCG024
      *                                                                                       CCG024
     C           XFOUND    IFEQ 'N'                                                           CCG024
      ** Check whether label required for this destination
      ** correspondent
     C           B2DCOR    CHAINCGCORRL0             55
      *
      ** Database error if correspondent not found
     C           *IN55     IFEQ '1'
     C                     MOVEL'CGCORRL0'W0FILE
     C                     MOVELB2DCOR    W0KEY            ***************
     C                     Z-ADD2         W0ERNB           * DB ERROR 02 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
     C                     ENDIF                                                              CCG024
      *
      ** If 'print label' indication off, set off valid_record_found
     C           CDPLAB    IFNE 'Y'
     C                     MOVEL'N'       ##RFND
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C/COPY WNCPYSRC,CG3500C002
     C           EXREAD    ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRFILL                                         *
      * Purpose     :  Fill label with correspondent address          *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           SRFILL    BEGSR
      *
      ** If address field from correspondent details is blank, access
      ** customer details
     C           CDADD1    IFEQ *BLANKS
     C                     CLEAR##ADDR
     C           CDCUST    CHAINSDCUSTL0             60
     C                     ENDIF
      *
      ** Fill current label with address information
     C                     Z-ADD@X1,#L    #X1
     C                     Z-ADD@Y1,#L    #Y1
     C                     Z-ADD@X2,#L    #X2
     C                     Z-ADD@Y2,#L    #Y2
      *
      ** For each line on the label (but no more than 8)...
     C           #LABY     IFLT 8
     C                     Z-ADD#LABY     #WYM    50
     C                     ELSE
     C                     Z-ADD8         #WYM
     C                     ENDIF
     C           1         DO   #WYM      #Y
     C           #Y1       ADD  #Y        #WY     50
     C           #WY       OCUR ##LINE
      *
      ** Get next line of address
     C**********           MOVEA@ADA,#Y   #WK50  50                                           CSD053
     C                     MOVEA@ADA,#Y   #WK50 100                                           CSD053
      *
      ** If address line non-blank, process
     C           #WK50     IFNE *BLANKS
      *
      ** For each column...
     C           1         DO   #LABX     #X
     C           #X1       ADD  #X        #WX     50
      *
      ** Get next byte of address line and move to page
     C           1         SUBST#WK50:#X  #WK1    1
     C                     MOVEL#WK1      @C,#WX
      *
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           EXFILL    ENDSR
     C/COPY WNCPYSRC,CG3500C003
      /EJECT
      *****************************************************************
      * Subroutine  :  SRPRNT                                         *
      * Purpose     :  Print page of labels                           *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           SRPRNT    BEGSR
      *
      ** Read through page data structure, and write to spool file
     C           #SIZY     SUB  #MAY      #WY
     C           1         DO   #WY       #Y
     C           #Y        OCUR ##LINE
     C                     MOVEL##LINE    #PLIN
     C                     CLEAR##LINE
      *
      ** Write line of output
     C                     WRITECG3500F1
     C                     ENDDO
      *
      ** Throw a page
     C                     WRITECG3500F2
      *
     C           EXPRNT    ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Purpose     :  Initial processing                             *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright statement
     C                     MOVE CPYR@#    ACT@   80
      *
      ** Entry parameters: return code and member name
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
     C                     PARM           P#ACCP 10
     C                     PARM           P#RBCH  3
      *
      ** Key list for CHAIN to CGUDCRL2
     C           K#KEY     KLIST
     C                     KFLD           B2DCOR           Destination Cor
     C                     KFLD           B2ITEM           Item Identifier
      *
      ** Set entity_level indication from branch parameter: if branch
      ** is 'ALL', need to open and close spool file on change
     C           P#RBCH    IFEQ 'ALL'
     C                     MOVEL'Y'       ##ENTY  1
     C                     ELSE
     C                     MOVEL'N'       ##ENTY  1
     C                     ENDIF
      *
      ** Set first_item_requiring_label indication
     C                     MOVEL'Y'       ##FRST  1
      *
      ** Access data structure for label stationery description
     C           *NAMVAR   DEFN CGDTA     CGDATA
     C                     IN   CGDATA
      *
      ** Set up program work fields from stored values
     C                     MOVELDASIZX    #SIZX   50       Page sixe  X
     C                     MOVELDASIZY    #SIZY   50       Page size  Y
     C                     MOVELDANOX     #NOX    50       No. labels X
     C                     MOVELDANOY     #NOY    50       No. labels Y
     C                     MOVELDAMAX     #MAX    50       Margin     X
     C                     MOVELDAMAY     #MAY    50       Margin     Y
     C                     MOVELDALABX    #LABX   50       Label size X
     C                     MOVELDALABY    #LABY   50       Label size Y
      *
      ** Build arrays of label begin and end co-ordinates, based on
      ** stationery description. Result is 4 arrays (@X1, @X2, @Y1,
      ** @Y2) which describe the top-left, and bottom-right corners
      ** of each label.
      **  o Initialise work variables
     C                     Z-ADD0         #C      50
     C                     Z-ADD0         #L      50
     C                     Z-ADD0         #X1     50
     C                     Z-ADD0         #Y1     50
     C                     Z-ADD0         #X2     50
     C                     Z-ADD0         #Y2     50
      *
      **  o calculate gutter sizes from stationery description
     C           #MAX      MULT 2         #WKMA   50
     C           #LABX     MULT #NOX      #WKLA   50
     C           #SIZX     SUB  #WKMA     #GUTX   50
     C           #GUTX     SUB  #WKLA     #GUTX
     C           #NOX      IFGT 1
     C           #NOX      SUB  1         #WKNO   50
     C           #GUTX     DIV  #WKNO     #GUTX
     C                     ENDIF
      *
     C           #MAY      MULT 2         #WKMA   50
     C           #LABY     MULT #NOY      #WKLA   50
     C           #SIZY     SUB  #WKMA     #GUTY   50
     C           #GUTY     SUB  #WKLA     #GUTY
     C           #NOY      IFGT 1
     C           #NOY      SUB  1         #WKNO   50
     C           #GUTY     DIV  #WKNO     #GUTY
     C                     ENDIF
      *
      **  o Loop, incrementing X and Y, to calculate defining
      **    co-ordinates for every label on the page. Note: #C
      **    will be the total number of labels on a page.
     C           1         DO   #NOX      #X      50
     C           #X        IFEQ 1
     C                     Z-ADD#MAX      #X1
     C                     ELSE
     C           #X2       ADD  #GUTX     #X1
     C                     ENDIF
     C           #X1       ADD  #LABX     #X2
     C           1         DO   #NOY      #Y      50
     C           #Y        IFEQ 1
     C                     Z-ADD#MAY      #Y1
     C                     ELSE
     C           #Y2       ADD  #GUTY     #Y1
     C                     ENDIF
     C           #Y1       ADD  #LABY     #Y2
      *
     C                     ADD  1         #C
      *
     C                     Z-ADD#X1       @X1,#C
     C                     Z-ADD#Y1       @Y1,#C
     C                     Z-ADD#X2       @X2,#C
     C                     Z-ADD#Y2       @Y2,#C
      *
     C                     ENDDO
     C                     ENDDO
      *
     C/COPY WNCPYSRC,CG3500C004
      ** Open the relevant access path
     C           P#ACCP    IFEQ 'CGPGENL2'
     C                     OPEN CGPGENL2
     C                     ELSE
     C/COPY WNCPYSRC,CG3500C007
     C                     OPEN CGPGENL3
     C/COPY WNCPYSRC,CG3500C008
     C                     ENDIF
      *
      *                                                                                       CCG024
      * Check if CCG024 is switched on                                                        CCG024
     C                     CALL 'AOSARDR0'                                                    CCG024
     C                     PARM           P@RTCD  7                                           CCG024
     C                     PARM '*VERIFY' P@OPTN  7                                           CCG024
     C                     PARM 'CCG024'  P@SARD  6                                           CCG024
      *                                                                                       CCG024
     C           P@RTCD    IFEQ *BLANKS                                                       CCG024
     C                     MOVE 'Y'       CCG024  1                                           CCG024
     C                     ELSE                                                               CCG024
     C                     MOVE 'N'       CCG024                                              CCG024
     C                     ENDIF                                                              CCG024
      *                                                                                       CCG024
      * Check System Value RecipientSpecialChar                                               CCG024
     C           CCG024    IFEQ 'Y'                                                           CCG024
      *                                                                                       CCG024
     C                     CALL 'AOSVALR0'                                                    CCG024
     C                     PARM *BLANKS   RTNCDE  7                                           CCG024
     C                     PARM           SVALK1 20                                           CCG024
     C                     PARM           SVAL1 200                                           CCG024
     C                     PARM *BLANKS   SVALK2 20                                           CCG024
     C                     PARM           SVAL2 200                                           CCG024
     C                     PARM *BLANKS   SVALK3 20                                           CCG024
     C                     PARM           SVAL3 200                                           CCG024
     C                     PARM *BLANKS   SVALK4 20                                           CCG024
     C                     PARM           SVAL4 200                                           CCG024
     C                     PARM *BLANKS   SVALK5 20                                           CCG024
     C                     PARM           SVAL5 200                                           CCG024
     C                     PARM *BLANKS   SVALK6 20                                           CCG024
     C                     PARM           SVAL6 200                                           CCG024
     C                     PARM *BLANKS   SVALK7 20                                           CCG024
     C                     PARM           SVAL7 200                                           CCG024
     C                     PARM *BLANKS   SVALK8 20                                           CCG024
     C                     PARM           SVAL8 200                                           CCG024
     C                     PARM *BLANKS   SVALK9 20                                           CCG024
     C                     PARM           SVAL9 200                                           CCG024
     C                     PARM *BLANKS   SVALK0 20                                           CCG024
     C                     PARM           SVAL10200                                           CCG024
      *                                                                                       CCG024
     C           RTNCDE    IFNE *BLANKS                                                       CCG024
     C           SVAL1     IFEQ '*NRF'                                                        CCG024
     C                     MOVE SVALK1    DBKEY                                               CCG024
     C                     ENDIF                                                              CCG024
      *                                                                                       CCG024
     C                     MOVEL'SDSVALR0'W0FILE                                              CCG024
     C                     MOVEL'*CALL'   W0KEY                                               CCG024
     C                     Z-ADD21        W0ERNB                                              CCG024
     C                     MOVEL'MEM5003' W0MSGD                                              CCG024
     C                     MOVEL'MIDAS  ' W0MSGF                                              CCG024
     C                     EXSR SRERR                                                         CCG024
     C                     ENDIF                                                              CCG024
      *                                                                                       CCG024
     C                     ENDIF                                                              CCG024
      *                                                                                       CCG024
     C           EXINIT    ENDSR
      /EJECT
      /COPY CGCPYSRC,SRERRC
      /COPY CGCPYSRC,SRERRPSSR
** CPY@ Copyright statement
(c) Finastra International Limited 2001
