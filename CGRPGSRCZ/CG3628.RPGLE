     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2002')
      *****************************************************************
/*STD *  RPGBASEMOD                                                   *
/*EXI *  TEXT('Midas CG FTP command generator')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG3628 - Midas CG FTP Command Generator                      *
      *                                                               *
      *  Function:  This module will format a chain of FTP commands   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. BA6020             Date 20Jan23               *
      *  Prev Amend No. CSD101             Date 07Dec18               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 CCG015  *CREATE    Date 21Aug01               *
      *                                    Date DDMmmYY               *
      *
      *---------------------------------------------------------------*
      *                                                               *
      *  BA6020 - Add specific header field to CM XML Output          *
      *  CSD101 - Password Encryption                                 *
      *  MD046248 - Finastra Rebranding                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      * *PSSR - Error processing                                      *
      * *INZSR - Initialise                                           *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).  Similarly        *
      *    D-spec initialisation only happens the first time.  Use    *
      *    RESET for subsequent passes.                               *
      *                                                               *
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ F-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+

     FCGFTPCPD  O    E             DISK

      *****************************************************************
      /EJECT
      *****************************************************************
      ** +--------------------------------------+
      ** ¦ D-specs                              ¦
      ** ¦ =======                              ¦
      ** +--------------------------------------+
     D LDA           E DS           256    EXTNAME(LDA)
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      **                                    184 193 DBMOD
      **                                    194 203 DBPROC
      *
      ** Data Area giving Installation Control Details
      *
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.

     D/COPY ZACPYSRC,PSDS
      *
      ** Program Status Data Structure

      ** +--------------------------------------+
      ** ¦ Named constants                      ¦
      ** ¦ ===============                      ¦
      ** +--------------------------------------+
      * QUIT FTP command
     D Quit            C                   'QUIT'

      ** +--------------------------------------+
      ** ¦ Arrays and Data Structures           ¦
      ** ¦ ==========================           ¦
      ** +--------------------------------------+

      ** +--------------------------------------+
      ** ¦ Declared variables                   ¦
      ** ¦ ==================                   ¦
      ** +--------------------------------------+

     D @RUN            S              1

      * Entry Parameters
     D RtnCode         S             10
     D*SourceDir***    S            100                                                       BA6020
     D*DestDir*****    S            100                                                       BA6020
     D SourceDir       S            150                                                       BA6020
     D DestDir         S            150                                                       BA6020
     D Action          S             10
     D Option          S             50    DIM(15)
     D User            S             20
     D****Password        S             20                                                    CSD101
     D Password        S            128                                                       CSD101

      * Index for option array
     D i               S              2  0

      * Work variable
     D*WOpt*****       S            104                                                       BA6020
     D WOpt            S            154                                                       BA6020
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+

      *********************************************************************
      * MAIN PROCEDURE                                                    *
      *********************************************************************

      * Login Information
     C                   EVAL      FCCMD = %TRIM(User) + ' ' + %TRIM(Password)
     C                   WRITE(E)  CGFTPCD0

     C                   IF        %ERROR
     C                   EVAL      RtnCode = '*ERROR'
     C                   EVAL      DBFILE =  'CGFTPCPD'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  001
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EXSR      *PSSR
     C                   ENDIF

      * Options (eg ASCII..)
     C                   EVAL      i = 1

     C                   DOW       Option(i) <> *BLANKS

     C                   EVAL      FCCMD = %TRIM(Option(i))
     C                   WRITE(E)  CGFTPCD0

     C                   IF        %ERROR
     C                   EVAL      RtnCode = '*ERROR'
     C                   EVAL      DBFILE =  'CGFTPCPD'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  002
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EXSR      *PSSR
     C                   ENDIF

     C                   EVAL      i = i + 1
     C                   ENDDO

      * Command to retrieve/send data from/to destination directory
     C                   EVAL      WOpt = %TRIM(Action) + ' ' + %TRIM(SourceDir)
     C                   EVAL      FCCMD = %TRIM(WOpt) + ' ' + %TRIM(DestDir)

     C                   WRITE(E)  CGFTPCD0

     C                   IF        %ERROR
     C                   EVAL      RtnCode = '*ERROR'
     C                   EVAL      DBFILE =  'CGFTPCPD'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  003
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EXSR      *PSSR
     C                   ENDIF

      * Quit FTP command
     C                   EVAL      FCCMD = Quit

     C                   WRITE(E)  CGFTPCD0

     C                   IF        %ERROR
     C                   EVAL      RtnCode = '*ERROR'
     C                   EVAL      DBFILE =  'CGFTPCPD'
     C                   EVAL      DBPGM  =  PSProcPgm
     C                   EVAL      DBASE  =  004
     C                   EVAL      DBMOD  =  PSProcMod
     C                   EXSR      *PSSR
     C                   ENDIF

     C                   SETON                                        LR


      **********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *INZSR - Program Initialisation routine                       *
      *                                                               *
      * Called by: Implicitly on program activation                   *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************

     C     *INZSR        BEGSR

     C     *entry        PLIST
     C                   PARM                    RtnCode
     C                   PARM                    SourceDir
     C                   PARM                    DestDir
     C                   PARM                    Action
     C                   PARM                    Option
     C                   PARM                    User
     C                   PARM                    Password

     C                   ENDSR

      *****************************************************************

      *********************************************************************
      /EJECT
      *********************************************************************
      *                                                                   *
      * *PSSR  - Program exception error routine                          *
      *          Called automatically if a program error occurs,          *
      *          or directly by the program code using EXSR.              *
      *          This subroutine DUMPs the program just once.             *
      *                                                                   *
      * Called by: (**calling routines**)                                 *
      *                                                                   *
      * Calls: None                                                       *
      *                                                                   *
      *********************************************************************

     C     *PSSR         BEGSR

     C                   DUMP

     C                   IF        @RUN = *BLANK
     C                   EVAL      @RUN = 'Y'

     C                   CALLB     'DBERRCTL'

     C                   ENDIF

     C                   EVAL      *INU7 = *ON
     C                   EVAL      *INU8 = *ON
     C                   EVAL      *INLR = *ON
     C                   RETURN

     C                   ENDSR

      ********************************************************************
