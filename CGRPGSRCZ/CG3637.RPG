     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Email address validation')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG3637 - E-mail adress validation                            *
      *                                                               *
      *  Function:  This program validates an e-mail adress           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CCG015             Date 20Mar02               *
      *  Prev Amend No. xxxxxx             Date DDMthYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCG015 - Correspondence Manager Phase 1                      *
      *                                                               *
      *****************************************************************
     E                    @EMAIL     60  1
     E                    CPY@    1   1 80
      *
     C           *ENTRY    PLIST
     C                     PARM           PEML   60
     C                     PARM           PRTN   10
     C                     PARM           PTYP    2
      ** '01' Embedded blanks
      ** '02' More than 1 '@' in the email adress
      ** '03' Character '.' cannot be just after the '@'
      *
     C                     MOVEACPY@      ACT@   80
     C                     Z-ADD0         X       20
     C                     Z-ADD0         Y       20
     C                     Z-ADD0         Z       20
     C                     MOVEL*BLANK    PTYP
     C                     MOVEL*BLANK    PRTN
      *
      ** No imbedded blank is allowed.
      *
     C                     MOVEAPEML      @EMAIL
     C           ' '       CHECKPEML      X
     C                     MOVE *BLANKS   PEML
     C                     MOVEA@EMAIL,X  PEML
     C           ' '       SCAN PEML      Y
     C           Y         IFGT 0
     C           Y         ANDLT60
     C           Y         ADD  1         Y
     C           ' '       CHECKPEML:Y    Z
      *
     C           Z         IFGT 0
     C                     MOVEL'*ERROR'  PRTN
     C                     MOVEL'01'      PTYP
     C                     ENDIF
     C                     ENDIF
      *
      ** E-mail Address must be separated by only 1 '@' and '.' must
      ** be present somewhere after the '@'.
      *
     C           PRTN      IFEQ *BLANK
     C                     Z-ADD0         X
     C                     Z-ADD0         Y
     C                     Z-ADD0         Z
     C           '@'       SCAN PEML      Y
     C           Y         IFGT 1
     C           Y         ANDLT60
     C           Y         ADD  1         Y
     C           '@'       SCAN PEML:Y    Z
      *
     C           Z         IFGT 0
     C                     MOVEL'*ERROR'  PRTN
     C                     MOVEL'02'      PTYP
     C                     ELSE
     C           '.'       SCAN PEML:Y    Z
     C           Z         IFLE Y
     C                     MOVEL'*ERROR'  PRTN
     C                     MOVEL'03'      PTYP
     C                     ENDIF
     C                     ENDIF
     C                     ELSE
     C                     MOVEL'*ERROR'  PRTN
     C                     MOVEL'02'      PTYP
     C                     ENDIF
     C                     ENDIF
      *
     C                     SETON                     LR
** CPY@
(c) Misys International Banking Systems Ltd. 2001
