     H        1
     F*****************************************************************
/********RPG_IGN_NO                                                   *   131960
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Dealing input cycle confirmations driver')    *
      *****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG4000 - Dealing Input Cycle Confirmations - Driver          *
     F*                                                               *
     F*  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
     F*  (phase(s))                                                   *
     F*                                                               *
     F*  Called By: mmCnnnn - (program name)                          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CDL099             Date 06Oct17               *
      *                 MD041695           Date 28Sep16               *
      *                 CSW216             Date 14Mar16               *
      *                 CGL165             Date 17Feb15               *
      *                 CSW214             Date 25Nov14               *
      *                 CDL096             Date 24Nov14               *
      *                 CDL094             Date 11Jun14               *
      *                 CSW213             Date 03Jun13               *
      *                 254652             Date 22Nov12               *
      *                 CSW212             Date 03May12               *
      *                 CRE073             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER054             Date 07Apr09               *
      *                 CER048             Date 19May08               *
      *                 CER020             Date 19May08               *
      *                 CER043             Date 19May08               *
      *                 250885             Date 10Oct07               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 248689             Date 25Jun07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 239078             Date 16Sep06               *
      *                 BUG11112           Date 07Jun06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CAS009             Date 16May05               *
      *                 CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      *                 BUG6604            Date 20Apr05               *
      *                 CAS008             Date 16Jun04               *
      *                 CGL014             Date 20Oct03               *
      *                 CSC022             Date 24Feb04               *
      *                 217684             Date 12May03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 210973             Date 22Oct02               *
      *                 183477             Date 28Sep00               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             Date 14Sep99               *
     F*                 143058             Date 05Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 131960             Date 09Jun98               *
     F*                 134006             Date 19May98               *
     F*                 CSW005             Date 01Aug96               *
     F*                 088652             Date 31May95               *
     F*                 082060             DATE 24JAN95               *
     F*                 081961             DATE 12JAN95               *
     F*                 S01522             DATE 22NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  MD041695 - No Trade Repository confirmation generated when   *
      *             the print schedule of FXDL/SIRS/CIRS/FRA is       *
      *             'DAILY'.                                          *
      *           - Pattern fix from MD-23402 which adds a record for *
      *             Trade Repository in CONEXCY.                      *
      *  CSW216 - SWIFT Changes 2016 (Recompile)                      *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CSW214 - SWIFT Changes 2014 (Recompile)                      *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance Receive Settlement Instructions             *
      *           (Recompiled)                                        *
      *  CSW213 - SWIFT Changes 2013                                  *
      *  254652 - Reverse deal does not populate the cancel/amend ind.*
      *           Fix to repopulate CHTP with the value from DTRIXDD  *
      *           before calling CG4010.                              *
      *         - Applied for AR1034502 (Child: AR1034503)            *
      *  CSW212 - SWIFT 2012 Changes (Recompile)                      *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax (Recompile)            *
      *  CER048 - German Features - Taxes (Recompile)                 *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  250885 - Confirmation not produced for FX if Repo is         *
      *           processed first.                                    *
      *  248689 - Cancellations (MT292) are not produced when confir- *
      *           mations are not generated. Force output to CONEXCY  *
      *           in case of amend or delete.                         *
      *  239078 - Prevent the generation of SWIFT confirmation for    *
      *           internal deal types PS, ID and IL.                  *
      *  BUG11112 - Correspondence Manager changes for CDL033         *
      *             enhancement. (Recompile)                          *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  BUG6604- Reinstate SETON LR                                  *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  CGL014 - Collaterals Processing (Recompile)                  *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  210973 - UDC conf not produced for deals entered with repos  *
      *           indicator set to blanks                             *
      *  183477 - Process records in DTRIXL1 with amended short term  *
      *           rate (CNRQ = 'S').                                  *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CIR005 - FRA/IRS Business Day Conventions in DEALSDG         *
     F*  CSE006 - Repurchase Agreements (REPOs) enhancement           *
     F*  143058 - Applied fix 109681:                                 *
     F*           Produce Cancellation Conf. when deal deleted        *
     F*           the same day it was inserted.                       *
     F*  131960 - Recompiled with IGNDECERR (*YES)  creation paramet. *
     F*  134006 - Always write to CONEX if Confirmation Matching ON.  *
     F*           Confirmations for network 'PAPER' were not being    *
     F*           downloaded to TRAM because no CONEX record was      *
     F*           being written if auto transmission flag = 'N'.      *
     F*           The auto transmission flag is set to 'N' in CG4010  *
     F*           if network = 'PAPER' and ISO confirmations required *
     F*           flag = 'Y' on Message Management ICD.               *
     F*  CSW005 - MG34n and MG36n message generation                  *
     F*  088652 - Set conf processed to S if not created in UDC.      *
     F*  082060 - Redefine Binary fields as Decimal (9,0 Packed)      *
     F*  081961 - User Defined Correspondence - Additions             *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  01   -    Deal read from DEALSDB                             *
     F*  02   -    Deal read from DEALSDC                             *
     F*  03   -    Deal read from DEALSDD                             *
     F*  04   -    Deal read from DEALSDE                             *
     F*  05   -    Deal read from DEALSDG                             *
     F*                                                               *
     F*  98   -    Reversed deal                                      *   143058
     F*  99   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
     FDTRIXL1 UF  E           K        DISK
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     FDEALS   UF  E           K        DISK
     F                                              KINFSR SRFILE
     F                                              KCOMIT                081961
     FCONEXCY O   E                    DISK                      A
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     FCONEXCZ UF  E                    DISK                      A
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     FDLRPIFL1IF  E           K        DISK                                                   CSW213
     F/COPY WNCPYSRC,CG4000F001
     E/SPACE 5
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     E/COPY WNCPYSRC,CG4000E002
     E/COPY CGCPYSRC,SRERRE
     E/COPY WNCPYSRC,CG4000E001
     I/SPACE 5
     IDEALSDBF    01
     IDEALSDCF    02
     IDEALSDDF    03
     IDEALSDEF    04
     IDEALSDGF    05
     I*
     IDTRIXDF                                                                                 248689
     I              CHTP                            #CHTP                                     248689
      *                                                                                       248689
     ISDMMOD    E DSSDMMODPD                                              134006
      **  External DS for Module details                                  134006
      *                                                                   CSE006
     ISCSARD    E DSSCSARDPD                                              CSE006
      ** Extenal DS for SAR details                                       CSE006
      *                                                                   134006
     IDSFDY     E DSDSFDY                                                 134006
      **  First DS for access programs, short data structure              134006
      *                                                                   134006
     IDSPARM    E DSDTRIXDD
     I**  External DS for parameter passed to CG4010
     I**  External DS for parameter passed to CG4020                      CSE006
     I*
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I*
     IW0FMT     E DSCGUDTAPD
     I**  Record format of PF/CGUDTAPD for use as a parameter
     I/COPY CGCPYSRC,CGAUDTI
     I/COPY CGCPYSRC,SRERRI
     IRUNDT     E DSRUNDAT                                                143058
      **  Data Area RUNDAT                                                143058
     IDSSDY     E DSDSSDY                                                                   MD041695
      ** Second DS for access programs, long data structure                                 MD041695
     ISDCUST    E DSSDCUSTPD                                                                MD041695
      ** External DS for customer details                                                   MD041695
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRINZ  - Initialise Fields on Each Invocation  *
      *                SRMAIN - Main Processing                       *
      *                SRAUDT - Audit Report Processing               *
      *****************************************************************
      *
     C/COPY WNCPYSRC,CG4000C001
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      **  Initialisation processing
     C                     EXSR SRINZ
      *
      **  Main processing
     C                     EXSR SRMAIN
      *
      **  Audit Processing
     C                     EXSR SRAUDT
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C***********          SETON                     LR                   082060
     C                     SETON                     LR                                      BUG6604
     C                     RETRN
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CG4010 - Dealing Input Cycle Confirmations     *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
      ** Read from the Deal Transaction Index File
      ** (This logical selects DTRIXDD records with CNPR = 'N')
      *
     C           *LOVAL    SETLLDTRIXL1
     C                     READ DTRIXL1                  99*
      *
      ** Read until End of File
      *
     C           *IN99     DOWEQ'0'
      *
      ** If Confirmation Requested
      *
     C           CNRQ      IFEQ 'Y'                        * DEAL INPUT
     C           CNRQ      OREQ 'S'                                       183477
      *
      **  Reset DRID                                                                          250885
     C                     MOVEL*BLANKS   DRID                                                250885
      *                                                                                       250885
      **  Increment the count of DTRIXDD records read.
     C                     ADD  1         ##DREC  50
     C/COPY WNCPYSRC,CG4000C002
      *                                                                   CSE006
      *** If switchable feature, CSE006, is on and deal has 'R', 'P',     CSE006
      *** 'B', call extract program CG4020                                CSE006
      *                                                                   CSE006
     C           CSE006    IFEQ 'Y'                                       CSE006
     C                     EXSR SRDEAL                                    CSE006
     C           DRID      IFEQ 'R'                                       CSE006
     C           DRID      OREQ 'P'                                       CSE006
     C           DRID      OREQ 'B'                                       CSE006
      *                                                                   CSE006
      ** Produce Confirmation                                             CSE006
      *                                                                   CSE006
     C                     CALL 'CG4020'                                  CSE006
     C                     PARM *BLANKS   ##RTCD  7                       CSE006
     C                     PARM W0CMT     ##CMT   3                       CSE006
     C                     PARM *BLANK    ##COPD  1                       CSE006
     C                     PARM *BLANK    ##AUTO  1                       CSE006
     C                     PARM           DSPARM                          CSE006
      *                                                                   CSE006
      **  Error in called program.                                        CSE006
     C           ##RTCD    IFNE *BLANK                                    CSE006
      *                                                                   CSE006
     C                     EXSR SRAUDT                                    CSE006
      *                                                                   CSE006
      ** Database error                                                   CSE006
     C                     MOVEL'CG4020  'W0FILE           ***************CSE006
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 05 *CSE006
     C                     Z-ADD05        W0ERNB           ***************CSE006
     C                     MOVEL'MEM5003' W0MSGD                          CSE006
     C                     MOVEL'MIDAS  ' W0MSGF                          CSE006
     C                     EXSR SRERR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C                     ELSE                                           210973
      *                                                                                       254652
     C                     MOVE CHTP      CHTPSV  1                                           254652
     C                     MOVE #CHTP     CHTP                                                254652
      *                                                                   210973
      ** Produce Confirmation                                             210973
      *                                                                   210973
     C                     CALL 'CG4010'                                  210973
     C                     PARM *BLANKS   ##RTCD  7                       210973
     C                     PARM W0CMT     ##CMT   3                       210973
     C                     PARM *BLANK    ##COPD  1                       210973
     C                     PARM *BLANK    ##AUTO  1                       210973
     C                     PARM           DSPARM                          210973
      *                                                                                       254652
     C                     MOVE CHTPSV    CHTP                                                254652
      *                                                                   210973
      **  Error in called program.                                        210973
     C           ##RTCD    IFNE *BLANK                                    210973
      *                                                                   210973
     C                     EXSR SRAUDT                                    210973
      *                                                                   210973
      ** Database error                                                   210973
     C                     MOVEL'CG4010  'W0FILE           ***************210973
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 06 *210973
     C                     Z-ADD06        W0ERNB           ***************210973
     C                     MOVEL'MEM5003' W0MSGD                          210973
     C                     MOVEL'MIDAS  ' W0MSGF                          210973
     C                     EXSR SRERR                                     210973
     C                     END                                            210973
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C                     ELSE                                           CSE006
     C/COPY WNCPYSRC,CG4000C005
      *
      ** Produce Confirmation
      *
     C                     MOVE CHTP      CHTPSV                                              254652
     C                     MOVE #CHTP     CHTP                                                254652
     C                     CALL 'CG4010'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM W0CMT     ##CMT   3
     C                     PARM *BLANK    ##COPD  1
     C                     PARM *BLANK    ##AUTO  1
     C                     PARM           DSPARM
     C/COPY WNCPYSRC,CG4000C006
     C                     MOVE CHTPSV    CHTP                                                254652
      *
      **  Error in called program.
     C           ##RTCD    IFNE *BLANK
      *
     C                     EXSR SRAUDT
      *
      ** Database error
     C                     MOVEL'CG4010  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 01 *
     C                     Z-ADD01        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *                                                                   CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C/COPY WNCPYSRC,CG4000C003
      *
      ** Confirmation Produced?
      *
     C           ##COPD    IFEQ 'Y'
      *
      ** Access Deal
      *
     C                     EXSR SRDEAL
      *
      ** Update Confirmation Printed Bit Settings on relevant DEALS File
     C           RECI      IFEQ 'R'                                       143058
     C           ORED      ANDEQAGRDNB                                    143058
     C           *IN98     ANDEQ'0'                                       143058
     C                     SETON                     98                   143058
     C                     ELSE                                           143058
      *
     C                     BITON'07'      CNFI
     C                     SETOF                     98                   143058
     C                     END                                            143058
     C                     ADD  1         TNLU
     C   01                EXCPTUDEALB
     C   02                EXCPTUDEALC
     C   03                EXCPTUDEALD
     C   04                EXCPTUDEALE
     C   05                EXCPTUDEALG
      *
      ** Write to CONEXCY if auto-transmission of confirmation
      ** (and update CONEXCZ)
      *
     C           ##AUTO    IFEQ 'Y'
      *                                                                   134006
      ** ... OR if Confirmation Matching module is switched ON. This      134006
      ** ensures that paper confirmations are downloaded to TRAM.         134006
      *                                                                   134006
     C           BGCFMT    OREQ 'Y'                        * CF MOD IS ON 134006
     C           #CHTP     OREQ 'R'                                                           248689
     C           #CHTP     OREQ 'A'                                                           248689
      *                                                                   134006
     C           DTYP      IFNE 'PS'                                                          239078
     C           DTYP      ANDNE'ID'                                                          239078
     C           DTYP      ANDNE'IL'                                                          239078
     C                     EXSR SRWCON
      *                                                                                       CSW213
     C                     ELSE                                                               CSW213
      *                                                                                       CSW213
     C                     EXSR SRCONX                                                        CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              239078
     C                     END
      *********************                                               081961
     C*********************END                             *##COPD IFEQ Y 081961
      *
      ** Update Confirmation Printed indicator on DTRIXDD
      *
     C                     MOVEL'Y'       CNPR
     C                     EXCPTUDTRIX
      *                                                                   081961
     C                     ELSE                                           088652
      *                                                                   088652
      * Set to S to show suppressed by UDC                                088652
      *                                                                   088652
     C                     MOVEL'S'       CNPR                            088652
     C                     EXCPTUDTRIX                                    088652
      *                                                                   088652
     C                     END                             *##COPD IFEQ Y 081961
      *
     C                     END                             * CNRQ IFEQ 'N'
      *
      ** Commit
     C                     COMIT
      *
      ** Read next record from DTRIXDD
      *
     C                     READ DTRIXL1                  99*
     C                     END                             * *IN99 DOWEQ '0'
      *
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRDEAL                                         *
      * Function    :  Access Deal                                    *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRDEAL    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRDEAL'  @STK,Q
      *
      *................................................................
      *
     C                     MOVEA'00000'   *IN,01
     C           DLNO      CHAINDEALS                99
      *
      ** If deal not found, or invalid format accessed
      *
     C           *IN99     IFEQ '1'
     C           *IN01     ORNE '1'
     C           *IN02     ANDNE'1'
     C           *IN03     ANDNE'1'
     C           *IN04     ANDNE'1'
     C           *IN05     ANDNE'1'
      *
     C                     EXSR SRAUDT
      *
      ** Database error
     C                     MOVEL'DEALS   'W0FILE           ***************
     C                     MOVELDLNO      W0KEY            * DB ERROR 02 *
     C                     Z-ADD02        W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
     C           EXDEAL    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRWCON                                         *
      * Function    :  Write to CONEX                                 *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRWCON    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRWCON'  @STK,Q
      *
      *................................................................
      *
     C                     MOVEL'D'       RECI
     C                     MOVEL'D'       RCDT
      *
      ** ROLLED OVER DEAL HAS A RECORD TYPE OF 'R' ON CONEXCY
      ** (TO DRIVE CANCELLATION OF PREVIOUSLY GENERATED MESSAGES)
      *
     C   02      RODN      IFNE *ZERO
     C                     MOVEL'R'       RCDT
     C                     END
      *
     C                     Z-ADDDLNO      DLNO
     C                     Z-ADDVDAT      VDAT
     C                     Z-ADDDASN      DASN
     C                     MOVELCNRQ      CNRQ
     C   02                Z-ADDTOTI      INTA
     C  N02                Z-ADD*ZERO     INTA
     C                     MOVEL*BLANK    MGEN
     C                     MOVELBRCA      BRCA
      *                                                                   CSW005
      ** Initialise new CONEXCY fields (CSW005)                           CSW005
      *                                                                   CSW005
     C                     Z-ADD*ZERO     INTO                            CSW005
     C                     Z-ADD*ZERO     UFIR                            CSW005
     C                     Z-ADD*ZERO     TFIR                            CSW005
     C                     MOVE 'I'       CHTP                            CSW005
      *
     C                     WRITECONEXCYF
      *
     C           1         CHAINCONEXCZF             99    *
     C           *IN99     IFEQ '1'
     C                     MOVEL'T'       RECI
     C                     Z-ADD1         NORE1
     C                     WRITECONEXCZF
     C                     ELSE
     C                     ADD  1         NORE1
     C                     UPDATCONEXCZF
     C                     END
      *                                                                                       CSW213
     C                     EXSR SRCON2                                                        CSW213
      *
      *................................................................
      *
     C           EXWCON    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINZ                                          *
      * Function    :  Initialise Fields on Program Invocation        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINZ '  @STK,Q
      *
      *................................................................
      *
      **  Open the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4000AU'I#SPLN
     C                     MOVEL'CG4000  'I#REF
     C                     MOVE 'CGD1510' I#TITL
     C                     MOVE 'CGD1511' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*OPEN   'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      **  Retrieve the RUNDAT data area.                                  143058
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           143058
     C                     IN   RUNDT                                     143058
      *
      *................................................................
      *
     C           EXINZ     TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *................................................................
      ** CALL ACCESS PROGRAM FOR MIDAS MODULES DETAILS                    134006
      *                                                                   134006
     C                     CALL 'AOMMODR0'                                134006
     C                     PARM *BLANKS   ##RTCD                          134006
     C                     PARM '*FIRST  '##OPTN  7                       134006
     C           SDMMOD    PARM SDMMOD    DSFDY                           134006
     C*                                                                   134006
     C           ##RTCD    IFNE *BLANK                                    134006
      *                                                                   134006
      ** DATABASE ERROR                                                   134006
      *                                                                   134006
     C                     MOVEL'AOMMODR0'W0FILE                          134006
     C                     MOVEL'*CALL'   W0KEY            ***************134006
     C                     Z-ADD03        W0ERNB           * DB ERROR 03 *134006
     C                     MOVEL'MEM5003' W0MSGD           ***************134006
     C                     MOVEL'MIDAS  ' W0MSGF                          134006
     C                     EXSR SRERR                                     134006
     C                     END                                            134006
      *                                                                   CSE006
      **  Access SAR details file to see if CSE006 SAR is installed.      CSE006
      *                                                                   CSE006
     C                     CALL 'AOSARDR0'                                CSE006
     C                     PARM *BLANKS   ##RTCD                          CSE006
     C                     PARM '*VERIFY' ##OPTN                          CSE006
     C                     PARM 'CSE006'  ##SARD  6                       CSE006
     C           SCSARD    PARM SCSARD    DSFDY                           CSE006
      *                                                                   CSE006
     C           ##RTCD    IFNE *BLANKS                                   CSE006
     C           ##RTCD    ANDNE'*NRF'                                    CSE006
      *                                                                   CSE006
      ** DATABASE ERROR                                                   CSE006
      *                                                                   CSE006
     C                     MOVEL'AOSARDR0'W0FILE                          CSE006
     C                     MOVEL'*CALL'   W0KEY            ***************CSE006
     C                     Z-ADD04        W0ERNB           * DB ERROR 04 *CSE006
     C                     MOVEL'MEM5003' W0MSGD           ***************CSE006
     C                     MOVEL'MIDAS  ' W0MSGF                          CSE006
     C                     EXSR SRERR                                     CSE006
     C                     ENDIF                                          CSE006
      *                                                                   CSE006
     C           ##RTCD    IFEQ *BLANK                                    CSE006
     C                     MOVE 'Y'       CSE006  1                       CSE006
     C                     ELSE                                           CSE006
     C                     MOVE 'N'       CSE006                          CSE006
     C                     ENDIF                                          CSE006
      *                                                                                       CSW213
      ** Check if Swift 2013 Changes is installed.                                            CSW213
      *                                                                                       CSW213
     C                     CALL 'SWIF2013'                                                    CSW213
     C                     PARM *BLANKS   @RTCD   7                                           CSW213
     C*                                                                                       CSW213
     C           @RTCD     IFEQ 'CSW2013'                                                     CSW213
     C                     MOVE 'Y'       CSW213  1                                           CSW213
     C                     ELSE                                                               CSW213
     C                     MOVE 'N'       CSW213                                              CSW213
     C                     END                                                                CSW213
      *                                                                                       CSW213
     C           RPIFKY    KLIST                                                              CSW213
     C                     KFLD           KDTYP                                               CSW213
     C                     KFLD           KDLNO                                               CSW213
      *                                                                   134006
      *................................................................   134006
      *                                                                   134006
      *
      **  Initialise work fields
     C                     Z-ADD*ZEROS    ##DREC
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
      *
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRAUDT                                         *
      * Function    :  Audit Report Processing                        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *                CG9020   - Write Data to UDC Data Files - Audit*
      *****************************************************************
     C           SRAUDT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
      *
      *................................................................
      *
      **  Output the Count of records read
     C                     CLEARI#DTA
     C                     MOVEL'CG4000  'I#REF
     C                     MOVE 'CGD1510' I#TITL
     C                     MOVE 'CGD1511' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE *BLANKS   I#SUB
     C                     MOVEL'DTRIXDD 'I#SUB
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADD##DREC    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Skip a line.
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Produce the Audit Report.
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD
     C                     PARM '*AUDIT  'W0ACT
     C                     PARM *BLANKS   W0PATH256
     C                     PARM           W0FMT
     C                     PARM 'CGD1510' W0TITL  7
     C                     PARM 'CGD1511' W0ULIN  7
     C                     PARM           W0CMT
      *
      **  Close the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4000  'I#REF
     C                     MOVE 'CGD1510' I#TITL
     C                     MOVE 'CGD1511' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      *................................................................
      *
     C           EXAUDT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /EJECT                                                                                  CSW213
     C*****************************************************************                       CSW213
      *                                                               *                       CSW213
      *  SRCON2 - If a record exists in DLRPFPD for the deal and Trade*                       CSW213
      *           repository id is not blanks, output a second record *                       CSW213
      *           in ECONXCY with same value as for first CONEXCY     *                       CSW213
      *           record but with RPTI set to 'Y'                     *                       CSW213
      *                                                               *                       CSW213
      *****************************************************************                       CSW213
     C           SRCON2    BEGSR                                                              CSW213
      *                                                                                       CSW213
     C                     MOVELDTYP      KDTYP   2                                           CSW213
     C                     MOVELDLNO      KDLNO  16                                           CSW213
      *                                                                                       CSW213
     C           CSW213    IFEQ 'Y'                                                           CSW213
     C********** BGCFMT    ANDEQ'Y'                                                  CSW213 MD041695
      *                                                                                       CSW213
     C           RPIFKY    CHAINDLRPIFL1             99                                       CSW213
      *                                                                                       CSW213
     C           *IN99     IFEQ '0'                                                           CSW213
     C           RICFRQ    ANDEQ'Y'                                                         MD041695
      *                                                                                     MD041695
     C           RITRID    IFNE *BLANKS                                                     MD041695
     C                     CALL 'AOCUSTR0'                                                  MD041695
     C                     PARM '*MSG    'PRTCD   7                                         MD041695
     C                     PARM '*KEY    'POPTN   7                                         MD041695
     C                     PARM RITRID    PCNUM   6                                         MD041695
     C                     PARM *BLANKS   PKEY    7                                         MD041695
     C           SDCUST    PARM SDCUST    DSSDY                                             MD041695
      *                                                                                     MD041695
     C           BBCSID    IFNE *BLANKS                                                     MD041695
     C                     MOVEL'A'       RPTI                                              MD041695
     C                     WRITECONEXCYF                                                    MD041695
     C                     MOVEL*BLANKS   RPTI                                              MD041695
      *                                                                                       CSW213
     C           1         CHAINCONEXCZF             99                                       CSW213
      *                                                                                       CSW213
     C           *IN99     IFEQ '1'                                                           CSW213
      *                                                                                       CSW213
     C                     MOVEL'T'       RECI                                                CSW213
     C                     Z-ADD1         NORE1                                               CSW213
     C                     WRITECONEXCZF                                                      CSW213
     C                     ELSE                                                               CSW213
     C                     ADD  1         NORE1                                               CSW213
     C                     UPDATCONEXCZF                                                      CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                     MD041695
     C                     ENDIF                                                            MD041695
     C                     ENDIF                                                            MD041695
      *                                                                                     MD041695
     C           RIPRED    IFNE *BLANKS                                                     MD041695
     C                     CALL 'AOCUSTR0'                                                  MD041695
     C                     PARM '*MSG    'PRTCD                                             MD041695
     C                     PARM '*KEY    'POPTN                                             MD041695
     C                     PARM RIPRED    PCNUM                                             MD041695
     C                     PARM *BLANKS   PKEY                                              MD041695
     C           SDCUST    PARM SDCUST    DSSDY                                             MD041695
      *                                                                                     MD041695
     C           BBCSID    IFNE *BLANKS                                                     MD041695
     C                     MOVEL'B'       RPTI                                              MD041695
     C                     WRITECONEXCYF                                                    MD041695
     C                     MOVEL*BLANKS   RPTI                                              MD041695
      *                                                                                     MD041695
     C           1         CHAINCONEXCZF             99                                     MD041695
      *                                                                                     MD041695
     C           *IN99     IFEQ '1'                                                         MD041695
      *                                                                                     MD041695
     C                     MOVEL'T'       RECI                                              MD041695
     C                     Z-ADD1         NORE1                                             MD041695
     C                     WRITECONEXCZF                                                    MD041695
     C                     ELSE                                                             MD041695
     C                     ADD  1         NORE1                                             MD041695
     C                     UPDATCONEXCZF                                                    MD041695
     C                     ENDIF                                                            MD041695
      *                                                                                     MD041695
     C                     ENDIF                                                            MD041695
     C                     ENDIF                                                            MD041695
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDSR                                                              CSW213
     C*****************************************************************                       CSW213
      /EJECT                                                                                  CSW213
     C*****************************************************************                       CSW213
      *                                                               *                       CSW213
      *  SRCONX - If record is not to be output in CONEXCY, force     *                       CSW213
      *           output of CONEXCY if CF is ON and CSW213 = 'Y' and  *                       CSW213
      *           if a record exists in DLRPIFPD for the deal         *                       CSW213
      *                                                               *                       CSW213
      *****************************************************************                       CSW213
     C           SRCONX    BEGSR                                                              CSW213
      *                                                                                       CSW213
     C           CSW213    IFEQ 'Y'                                                           CSW213
     C           BGCFMT    ANDEQ'Y'                                                           CSW213
      *                                                                                       CSW213
     C                     MOVELDTYP      KDTYP                                               CSW213
     C                     MOVELDLNO      KDLNO                                               CSW213
      *                                                                                       CSW213
     C           RPIFKY    CHAINDLRPIFL1             99                                       CSW213
      *                                                                                       CSW213
     C           *IN99     IFEQ '0'                                                           CSW213
      *                                                                                       CSW213
     C                     EXSR SRWCON                                                        CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDSR                                                              CSW213
     C*****************************************************************                       CSW213
     C/COPY WNCPYSRC,CG4000C004
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRPSSR
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRC
     O/SPACE 5
     ODEALSDBFE                UDEALB
     O                         CNFI
     O                         LCD
     O                         TNLU
     ODEALSDCFE                UDEALC
     O                         CNFI
     O                         LCD
     O                         TNLU
     ODEALSDDFE                UDEALD
     O                         CNFI
     O                         LCD
     O                         TNLU
     ODEALSDEFE                UDEALE
     O                         CNFI
     O                         LCD
     O                         TNLU
     ODEALSDGFE                UDEALG
     O                         CNFI
     O                         LCD
     O                         TNLU
     ODTRIXDF E                UDTRIX
     O                         CNPR
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
     X/COPY WNCPYSRC,CG4000X001
