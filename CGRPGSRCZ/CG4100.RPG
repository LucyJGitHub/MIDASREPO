     H        1
     F*****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Dealing end of day confirmations - driver')   *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG4100 - Dealing End of Day Confirmations - Driver           *
     F*                                                               *
     F*  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
     F*  (phase(s))                                                   *
     F*                                                               *
     F*  Called By: mmCnnnn - (program name)                          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD041695           Date 28Sep16               *
      *                 CSW216             Date 14Mar16               *
      *                 CSW214             Date 25Nov14               *
      *                 CSW213             Date 03Jun13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG6604            Date 20Apr05               *
     F*                 BUG5758            Date 03Feb05               *
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 171686             Date 14Jun00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW097             Date 02Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 125460             Date 09Jan98               *
     F*                 CSW011             Date 01Apr97               *
     F*                 CSW005             Date 01Aug96               *
     F*                 082060             Date 24Jan95               *
     F*                 082060             Date 24Jan95               *
     F*                 S01522             DATE 22NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD041695 - No Trade Repository confirmation generated when   *
      *             the print schedule of FXDL/SIRS/CIRS/FRA is       *
      *             'DAILY'. Fix is to add a record for Trade         *
      *             Repository in ECONXCY.                            *
      *  CSW216 - SWIFT Changes 2016 (Recompile)                      *
      *  CSW214 - SWIFT Changes 2014 (Recompile)                      *
      *  CSW213 - SWIFT Changes 2013                                  *
     F*  BUG6604- Reinstate SETON LR                                  *
      *  BUG5758- System is generating MT362 everyday for OIS deals.  *
      *         - MT362 should only be generated during settlement or *
      *           interest payment date. Applied 221860               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  171686 - Output correct RCDT in ECONXCY file. Applied fix    *
     F*           154553.                                             *
     F*  CSW097 - MT340s and MT360s message generation.               *
     F*  125460 - One parameter missing on SRDAIN call of CG4110.     *
     F*  CSW011 - Generate FRA MT34n messages only.                   *
     F*  CSW005 - MG34n and MG36n message generation                  *
     F*  087337 - Ignore if CNRQ is N                                 *
     F*  082060 - Redefine Binary fields as Decimal (9,0 Packed)      *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  99   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
     FDLINDDM UF  E                    DISK
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     F            DTRIEPHF                          KRENAMEDLINDDMF
     FDAINDDL UF  E                    DISK
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     F            DTRIEPHF                          KRENAMEDAINDDLF
     FECONXCY O   E                    DISK                      A
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     FECONXCZ UF  E                    DISK                      A
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     FDLRPIFL4IF  E           K        DISK                                                   CSW213
     F/COPY WNCPYSRC,CG4100F001
     E/SPACE 5
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     E/COPY CGCPYSRC,SRERRE
     I/SPACE 5
     I/COPY WNCPYSRC,CG4100I001
     IDSPARM    E DSDTRIEPH
     I**  External DS for parameter passed to CG4110
     I*
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I/COPY WNCPYSRC,CG4100I002
     I*
     IW0FMT     E DSCGUDTAPD
     I**  Record format of PF/CGUDTAPD for use as a parameter
     ISDMMOD    E DSSDMMODPD                                                                  CSW213
     IDSFDY     E DSDSFDY                                                                     CSW213
     I/COPY CGCPYSRC,CGAUDTI
     I/COPY CGCPYSRC,SRERRI
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - only once        *
      *                SRINZ  - Initial Processing                    *
      *                SRDLIN - Process PF/DLINDDM                    *
      *                SRDAIN - Process PF/DAINDDL                    *
      *                SRAUDT - Audit Report Processing               *
      *****************************************************************
      *
     C/COPY WNCPYSRC,CG4100C001
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      **  Initialisation processing
     C                     EXSR SRINZ
      *
      ** Main processing
     C                     EXSR SRDLIN                     PF/DLINDDM
     C                     EXSR SRDAIN                     PF/DAINDDL
      *
      ** Audit processing
     C                     EXSR SRAUDT
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C***********          SETON                     LR                   082060
     C                     SETON                     LR                                      BUG6604
     C                     RETRN
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRDLIN                                         *
      * Function    :  Process PF/DLINDDM                             *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CG4110 - Dealing End of Day Confirmations      *
      *****************************************************************
     C           SRDLIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRDLIN'  @STK,Q
      *
      *................................................................
      *
      ** Read from Deals Index Detail
      *
     C                     READ DLINDDM                  99*
      *
      ** Read until End of File
      *
     C           *IN99     DOWEQ'0'
      *                                                                                      BUG5758
      ** Initialise check field for OIS deals                                                BUG5758
      *                                                                                      BUG5758
     C                     MOVE *BLANKS   NOOIS   1                                          BUG5758
      *
      *
      ** If confirmation required and not yet printed
      *
     C           CNRQ      IFNE *BLANK
     C           CNRQ      ANDNE'N'                                       087337
     C           CNPR      ANDNE'Y'
     C/COPY WNCPYSRC,CG4100C007
      *                                                                   CSW097
     C                     Z-ADDEVDAT     WVDAT   50                      CSW097
      *
      **  Increment the count of DLINDDM records read.
     C                     ADD  1         ##DREC
     C/COPY WNCPYSRC,CG4100C002
      *                                                                   171686
      ** Store value of RCDT from DLINDDM file.                           171686
      *                                                                   171686
     C                     MOVELRCDT      WRCDT   1                       171686
      *
      ** Produce Confirmation
      *
     C                     CALL 'CG4110'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM W0CMT     ##CMT   3
     C                     PARM *BLANK    ##COPD  1
     C                     PARM *BLANK    ##AUTO  1
     C                     PARM *ZEROS    ##INTA 130
     C                     PARM           DSPARM
     C                     PARM *BLANK    FRAIRS  1                       CSW005
     C                     PARM           NOOIS   1                                          BUG5758
      *
      **  Error in called program.
     C           ##RTCD    IFNE *BLANK
      *
     C                     EXSR SRAUDT
      *
      ** Database error
     C                     MOVEL'CG4110  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 01 *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
     C/COPY WNCPYSRC,CG4100C003
      ** Confirmation Produced?
      *
     C           ##COPD    IFEQ 'Y'
      *
      ** Write to ECONXCY if auto-transmission of confirmation
      ** (and update ECONXCZ)
      *
     C           ##AUTO    IFEQ 'Y'
     C           NOOIS     ANDEQ*BLANKS                                                      BUG5758
     C                     MOVELWRCDT     RCDT                            171686
     C                     EXSR SRWECN
      *                                                                                       CSW213
     C                     ELSE                                                               CSW213
      *                                                                                       CSW213
     C                     EXSR SRCONX                                                        CSW213
      *                                                                                       CSW213
     C                     END
      *
      ** Update Confirmation Printed indicator on DLINDDM
      *
     C                     MOVEL'Y'       CNPR
     C                     EXCPTUDLIND
      *
     C                     ENDIF                           * ##COPD IFEQ 'Y'
     C                     ENDIF                           * CNPR IFNE 'Y'
      *
      ** Commit
     C                     COMIT
      *
      ** Read next record from Deals Index Detail
      *
     C                     READ DLINDDM                  99*
      *
     C                     ENDDO
      *
      *................................................................
      *
     C           EXDLIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRDAIN                                         *
      * Function    :  Process PF/DAINDDL                             *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CG4110 - Dealing End of Day Confirmations      *
      *****************************************************************
     C           SRDAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRDAIN'  @STK,Q
      *
      *................................................................
      *
      ** Read from Deal Amendments Index Detail
      *
     C                     READ DAINDDL                  99*
      *
      ** Read until End of File
      *
     C           *IN99     DOWEQ'0'
      *
      ** If confirmation required and not yet printed
      *
     C           CNRQ      IFNE *BLANK
     C           CNPR      ANDNE'Y'
     C/COPY WNCPYSRC,CG4100C004
      *                                                                   CSW097
     C                     Z-ADDEVDAT     WVDAT                           CSW097
      *
      **  Increment the count of DAINDDL records read.
     C                     ADD  1         ##DREC
      *                                                                   171686
      ** Store value of RCDT from DAINDDL file.                           171686
      *                                                                   171686
     C                     MOVELRCDT      WRCDT                           171686
      *
      ** Produce Confirmation
      *
     C                     CALL 'CG4110'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM W0CMT     ##CMT
     C                     PARM *BLANK    ##COPD
     C                     PARM *BLANK    ##AUTO
     C                     PARM *ZEROS    ##INTA
     C                     PARM           DSPARM
     C                     PARM *BLANK    FRAIRS  1                       125460
     C                     PARM           NOOIS   1                                          BUG5758
      *
      **  Error in called program.
     C           ##RTCD    IFNE *BLANK
      *
     C                     EXSR SRAUDT
      *
      ** Database error
     C                     MOVEL'CG4110  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 02 *
     C                     Z-ADD2         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
     C/COPY WNCPYSRC,CG4100C005
      *
      ** Confirmation Produced?
      *
     C           ##COPD    IFEQ 'Y'
      *
      ** Write to ECONXCY if auto-transmission of confirmation
      ** (and update ECONXCZ)
      *
     C           ##AUTO    IFEQ 'Y'
     C                     MOVELWRCDT     RCDT                            171686
     C                     EXSR SRWECN
      *                                                                                       CSW213
     C                     ELSE                                                               CSW213
      *                                                                                       CSW213
     C                     EXSR SRCONX                                                        CSW213
      *                                                                                       CSW213
     C                     END
      *
      ** Update Confirmation Printed indicator on DAINDDL
      *
     C                     MOVEL'Y'       CNPR
     C                     EXCPTUDAIND
      *
     C                     ENDIF                           * ##COPD IFEQ 'Y'
     C                     ENDIF                           * CNPR IFNE 'Y'
      *
      ** Commit
     C                     COMIT
      *
      ** Read next record from Deal Amendments Index Detail
      *
     C                     READ DAINDDL                  99*
      *
     C                     ENDDO
      *
      *................................................................
      *
     C           EXDAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRWECN                                         *
      * Function    :  Write to ECONX                                 *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRWECN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRWECN'  @STK,Q
      *
      *................................................................
      *
     C                     MOVEL'D'       RECI
     C***********CSW005    IFEQ 'Y'                                CSW005 CSW011
     C***********FRAIRS    ANDEQ'Y'                                CSW005 CSW011
     C           FRAIRS    IFEQ 'Y'                                       CSW011
     C                     MOVEL'D'       RCDT                            CSW005
     C                     ELSE                                           CSW005
     C                     MOVELRCDT      RCDT
     C                     ENDIF                                          CSW005
      *
     C                     Z-ADDDLNO      DLNO
     C***********CSW005    IFEQ 'Y'                                CSW005 CSW011
     C***********FRAIRS    ANDEQ'Y'                                CSW005 CSW011
     C           FRAIRS    IFEQ 'Y'                                       CSW011
      *                                                                   CSW097
     C           CNRQ      IFEQ 'F'                                       CSW097
     C           CNRQ      OREQ 'D'                                       CSW097
     C           CNRQ      OREQ 'H'                                       CSW097
     C                     Z-ADDWVDAT     VDAT                            CSW097
     C                     ELSE                                           CSW097
     C                     Z-ADD*ZERO     VDAT                            CSW005
     C                     ENDIF                                          CSW097
      *                                                                   CSW097
     C                     Z-ADD*ZERO     DASN                            CSW005
     C                     ELSE                                           CSW005
     C                     Z-ADDVDAT      VDAT
     C                     Z-ADDDASN      DASN
     C                     ENDIF                                          CSW005
     C                     MOVELCNRQ      CNRQ
      *                                                                   CSW005
     C***********CSW005    IFEQ 'Y'                                CSW005 CSW011
     C***********FRAIRS    ANDEQ'Y'                                CSW005 CSW011
     C           FRAIRS    IFEQ 'Y'                                       CSW011
      *                                                                   CSW005
      *  net the amounts if necessary for RS                              CSW005
      *                                                                   CSW005
     C           PIAMT     IFNE *ZEROS                                    CSW005
     C                     Z-ADDPIAMT     INTA                            CSW005
     C                     ELSE                                           CSW005
     C                     Z-ADD*ZERO     INTA                            CSW005
     C                     ENDIF                                          CSW005
      *                                                                   CSW005
     C                     MOVE *BLANKS   ZZ026                           CSW005
      *                                                                   CSW005
     C                     ELSE                                           CSW005
     C                     Z-ADD##INTA    INTA
     C                     ENDIF                                          CSW005
      *                                                                   CSW005
     C                     MOVEL*BLANK    MGEN
     C                     MOVELBRCA      BRCA
      *                                                                   CSW005
     C                     Z-ADD*ZERO     INTO                            CSW005
      *                                                                   CSW005
     C***********CSW005    IFEQ 'Y'                                CSW005 CSW011
     C***********FRAIRS    ANDEQ'Y'                                CSW005 CSW011
     C           FRAIRS    IFEQ 'Y'                                       CSW011
      *                                                                   CSW005
     C           RIAMT     IFNE *ZEROS                                    CSW005
     C                     Z-ADDRIAMT     INTO                            CSW005
     C                     ENDIF                                          CSW005
      *                                                                   CSW005
     C           CNRQ      IFNE 'D'                                       CSW005
     C           CNRQ      ANDNE'H'                                       CSW005
     C                     Z-ADD0         UFIR                            CSW005
     C                     ENDIF                                          CSW005
      *                                                                   CSW005
     C           CNRQ      IFNE 'F'                                       CSW005
     C           CNRQ      ANDNE'H'                                       CSW005
     C                     Z-ADD0         TFIR                            CSW005
     C                     ENDIF                                          CSW005
      *                                                                   CSW005
     C                     ELSE                                           CSW005
      *                                                                   CSW005
     C                     Z-ADD*ZEROS    UFIR                            CSW005
     C                     Z-ADD*ZEROS    TFIR                            CSW005
      *                                                                   CSW005
     C                     ENDIF                                          CSW005
      *
     C                     WRITECONEXCYF
      *
     C           1         CHAINCONEXCZF             99    *
     C           *IN99     IFEQ '1'
     C                     MOVEL'T'       RECI
     C                     Z-ADD1         NORE1
     C                     WRITECONEXCZF
     C                     ELSE
     C                     ADD  1         NORE1
     C                     UPDATCONEXCZF
     C                     END
      *                                                                 MD041695
     C                     EXSR SRCON2                                  MD041695
      *
      *................................................................
      *
     C           EXWECN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINZ                                          *
      * Function    :  Initialise Fields on Program Invocation        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINZ '  @STK,Q
      *
      *................................................................
      *
      **  Open the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4100AU'I#SPLN
     C                     MOVEL'CG4100  'I#REF
     C                     MOVE 'CGD1510' I#TITL
     C                     MOVE 'CGD1511' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*OPEN   'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *................................................................
      *
     C           EXINZ     TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing                             *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *................................................................
      *
      *                                                                   CSW005
      ** Access SAR details file to determine if CSW005 is installed.     CSW005
      *                                                                   CSW005
     C                     CALL 'AOSARDR0'                                CSW005
     C                     PARM '       ' ##RTCD                          CSW005
     C                     PARM '*VERIFY' ##OPTN  7                       CSW005
     C                     PARM 'CSW005'  ##SARD  6                       CSW005
      *                                                                   CSW005
     C           ##RTCD    IFEQ *BLANK                                    CSW005
     C                     MOVEL'Y'       CSW005  1                       CSW005
     C                     ELSE                                           CSW005
     C                     MOVEL'N'       CSW005                          CSW005
     C                     ENDIF                                          CSW005
      *                                                                                       CSW213
      ** Check if Swift 2013 Changes is installed.                                            CSW213
      *                                                                                       CSW213
     C                     CALL 'SWIF2013'                                                    CSW213
     C                     PARM *BLANKS   @RTCD   7                                           CSW213
      *                                                                                       CSW213
     C           @RTCD     IFEQ 'CSW2013'                                                     CSW213
     C                     MOVE 'Y'       CSW213  1                                           CSW213
     C                     ELSE                                                               CSW213
     C                     MOVE 'N'       CSW213                                              CSW213
     C                     END                                                                CSW213
      *                                                                                       CSW213
      ** Call access program for Midas Modules Details                                        CSW213
      *                                                                                       CSW213
     C                     CALL 'AOMMODR0'                                                    CSW213
     C                     PARM *BLANKS   @RTCD                                               CSW213
     C                     PARM '*FIRST  '##OPTN  7                                           CSW213
     C           SDMMOD    PARM SDMMOD    DSFDY                                               CSW213
      *                                                                                       CSW213
     C           @RTCD     IFNE *BLANK                                                        CSW213
      *                                                                                       CSW213
      ** Database error                                                                       CSW213
      *                                                                                       CSW213
     C                     MOVEL'AOMMODR0'W0FILE                                              CSW213
     C                     MOVEL'*CALL'   W0KEY                                               CSW213
     C                     Z-ADD03        W0ERNB                                              CSW213
     C                     MOVEL'MEM5003' W0MSGD                                              CSW213
     C                     MOVEL'MIDAS  ' W0MSGF                                              CSW213
     C                     EXSR SRERR                                                         CSW213
     C                     END                                                                CSW213
      *                                                                   CSW005
      **  Initialise work fields
     C                     Z-ADD*ZEROS    ##DREC  50
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
      *
     C/COPY WNCPYSRC,CG4100C006
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRAUDT                                         *
      * Function    :  Audit Report Processing                        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *                CG9020   - Write Data to UDC Data Files - Audit*
      *****************************************************************
     C           SRAUDT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
      *
      *................................................................
      *
      **  Output the Count of records read
     C                     CLEARI#DTA
     C                     MOVEL'CG4100  'I#REF
     C                     MOVE 'CGD1510' I#TITL
     C                     MOVE 'CGD1511' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE *BLANKS   I#SUB
     C                     MOVEL'DL/DAIND'I#SUB
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADD##DREC    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Skip a line.
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Produce the Audit Report.
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD
     C                     PARM '*AUDIT  'W0ACT
     C                     PARM *BLANKS   W0PATH256
     C                     PARM           W0FMT
     C                     PARM 'CGD1510' W0TITL  7
     C                     PARM 'CGD1511' W0ULIN  7
     C                     PARM           W0CMT   3
      *
      **  Close the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4100  'I#REF
     C                     MOVE 'CGD1510' I#TITL
     C                     MOVE 'CGD1511' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      *................................................................
      *
     C           EXAUDT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /EJECT                                                                                  CSW213
      *****************************************************************                       CSW213
      *                                                               *                       CSW213
      *  SRCONX - If record is not to be output in CONEXCY, force     *                       CSW213
      *           output of CONEXCY if CF is ON and CSW213 = 'Y' and  *                       CSW213
      *           if a record exists in DLRPIFPD for the deal         *                       CSW213
      *                                                               *                       CSW213
      *****************************************************************                       CSW213
     C           SRCONX    BEGSR                                                              CSW213
      *                                                                                       CSW213
     C           CSW213    IFEQ 'Y'                                                           CSW213
     C           BGCFMT    ANDEQ'Y'                                                           CSW213
      *                                                                                       CSW213
     C                     MOVELDLNO      RPIFKY 16                                           CSW213
      *                                                                                       CSW213
     C           RPIFKY    CHAINDLRPIFL4             99                                       CSW213
      *                                                                                       CSW213
     C           *IN99     IFEQ '0'                                                           CSW213
     C                     EXSR SRWECN                                                        CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDSR                                                              CSW213
      *****************************************************************                       CSW213
      /EJECT                                                                                  CSW213
     C*****************************************************************                       CSW213
      *                                                               *                       CSW213
      *  SRCON2 - If a record exists in DLRPFPD for the deal and Trade*                       CSW213
      *           repository id is not blanks, output a second record *                       CSW213
      *           in ECONXCY with same value as for first CONEXCY     *                       CSW213
      *           record but with RPTI set to 'Y'                     *                       CSW213
      *                                                               *                       CSW213
      *****************************************************************                       CSW213
     C           SRCON2    BEGSR                                                              CSW213
      *                                                                                       CSW213
     C           CSW213    IFEQ 'Y'                                                           CSW213
     C********** BGCFMT    ANDEQ'Y'                                                  CSW213 MD041695
      *                                                                                       CSW213
     C                     MOVELDLNO      RPIFKY                                              CSW213
      *                                                                                       CSW213
     C           RPIFKY    CHAINDLRPIFL4             99                                       CSW213
      *                                                                                       CSW213
     C           *IN99     IFEQ '0'                                                           CSW213
     C           RITRID    ANDNE*BLANKS                                                     MD041695
      *                                                                                     MD041695
     C                     MOVE 'Y'       RPTI                                              MD041695
     C                     WRITECONEXCYF                                                    MD041695
     C                     MOVE *BLANKS   RPTI                                              MD041695
      *                                                                                       CSW213
     C           1         CHAINCONEXCZF             99                                       CSW213
      *                                                                                       CSW213
     C           *IN99     IFEQ '1'                                                           CSW213
      *                                                                                       CSW213
     C                     MOVEL'T'       RECI                                                CSW213
     C                     Z-ADD1         NORE1                                               CSW213
     C                     WRITECONEXCZF                                                      CSW213
     C                     ELSE                                                               CSW213
     C                     ADD  1         NORE1                                               CSW213
     C                     UPDATCONEXCZF                                                      CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDIF                                                              CSW213
      *                                                                                       CSW213
     C                     ENDSR                                                              CSW213
     C*****************************************************************                       CSW213
     C/COPY WNCPYSRC,CG4100C008
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRPSSR
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRC
     O/SPACE 5
     ODLINDDMFE                UDLIND
     O                         CNPR
     ODAINDDLFE                UDAIND
     O                         CNPR
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
