     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Securities Advance Confirmations -Driver')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                  *
     F*                                                               *
     F*  CG4400 - Securities Advance Confirmations - Driver           *
     F*                                                               *
     F*  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
     F*  (phase(s))                                                   *
     F*                                                               *
     F*  Called By: mmCnnnn - (program name)                          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 082060             Date 24Jan95               *
      *                 081961             Date 12Jan95               *
     F*                 S01522             DATE 22NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  082060 - Redefine Binary fields as Decimal (9,0 Packed)      *
     F*  081961 - User Defined Correspondence - Additions             *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*****************************************************************
     FCPEVE   UF  E           K        DISK
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     E/SPACE 5
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     E/COPY CGCPYSRC,SRERRE
     E/COPY ZSRSRC,ZHOLE                                                  081961
     I/SPACE 5
     IDSPARM    E DSCPEVED
     I**  External DS for parameter passed to CG4410
     I*
     IJNSTAT      DS                            200                       081961
     I**  JNSTAT data area for previous run date PRUN                     081961
     I*                                                                   081961
     I                                    P 103 1050PRUN                  081961
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I*
     ISDBANK    E DSSDBANKPD                                              081961
      **  External DS for Bank details                                    081961
      *                                                                   081961
     IDSFDY     E DSDSFDY                                                 081961
      **  First DS for access programs, short data structure              081961
      *                                                                   081961
     IW0FMT     E DSCGUDTAPD
     I**  Record format of PF/CGUDTAPD for use as a parameter
     I/COPY CGCPYSRC,CGAUDTI
     I/COPY CGCPYSRC,SRERRI
     I/COPY ZSRSRC,ZHOLI                                                  081961
     I**  External DS for holiday details                                 081961
     I/COPY ZSRSRC,ZHOLDS                                                 081961
     I**  External DS for holiday details and access objects              081961
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRINZ  - Initialise Fields on Each Invocation  *
      *                SRMAIN - Main Processing                       *
      *                SRAUDT - Audit Report Processing               *
      *****************************************************************
      *
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      ** Initialisation processing
     C                     EXSR SRINZ
      *
      *  Main processing
     C                     EXSR SRMAIN
      *
      ** Audit processing
     C                     EXSR SRAUDT
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C                     RETRN
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CG4410 - Securities Advance Confirmations      *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
      ** Read the Customer Position Events
      *
     C           *LOVAL    SETLLCPEVE
     C                     READ CPEVE                    99*
      *
      ** Read until End of File
      *
     C           *IN99     DOWEQ'0'
      *
      **  Increment the count of CPEVED records read.
     C                     ADD  1         ##CREC  50
      *                                                                   081961
      **  Confirmation Due Date = Event Date - 5 working days             081961
      *                                                                   081961
     C                     Z-ADDCCDT      ZDAYNO                          081961
     C                     MOVE BJLCCY    ZCCY                            081961
     C                     Z-ADD5         ZNRDYS                          081961
     C                     MOVE *BLANKS   ZLOC                            081961
     C                     EXSR ZBKDT                                     081961
      *                                                                   081961
      **  Confirmation required if due date is less than or equal to run  081961
      **  date and if the due date is greater than the previous run date  081961
      *                                                                   081961
     C           ZDYNBR    IFLE BJRDNB                                    081961
     C           ZDYNBR    ANDGTPRUN                                      081961
      *
      ** If confirmation not already produced
      *
     C           CPCF      IFNE 'Y'
      *
      ** If event type to be processed
      *
     C           CCET      IFEQ 'PP'
     C           CCET      OREQ 'CP'
     C           CCET      OREQ 'DV'
     C           CCET      OREQ 'MP'
     C           CCET      OREQ 'MA'
      *
      ** If event amount present
      *
     C           RECI      IFNE '*'
     C           CCEA      ANDNE*ZERO
      *
      ** Produce Confirmation
      *
     C                     CALL 'CG4410'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM W0CMT     ##CMT   3
     C                     PARM *BLANK    ##COPD  1
     C                     PARM *BLANK    ##AUTO  1
     C                     PARM           DSPARM
      *
      **  Error in called program.
     C           ##RTCD    IFNE *BLANK
      *
     C                     EXSR SRAUDT
      *
      ** Database error
     C                     MOVEL'CG4410  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 01 *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      ** Confirmation Produced?
      *
     C           ##COPD    IFEQ 'Y'
      *
      ** Update Confirmation Produced Indicator on CPEVED
      *
     C                     MOVEL'Y'       CPCF
     C                     EXCPTUCPEVE
      *
     C                     END                             * ##COPD IFEQ 'Y'
      *
     C                     END                             * RECI NOT = '*'
     C                     END                             * CCET = 'PP'
     C                     END                             * CPCF NOT = Y
     C                     END                             * ZDYNBR > BJRD081961
      *
      ** Commit
     C                     COMIT
      *
      ** Read next record from CPEVE
      *
     C                     READ CPEVE                    99*
     C                     END                             * *IN99 DOWEQ '0'
      *
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINZ                                          *
      * Function    :  Initialise Fields on Program Invocation        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINZ '  @STK,Q
      *
      *................................................................
      *
      **  Open the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4400AU'I#SPLN
     C                     MOVEL'CG4400  'I#REF
     C                     MOVE 'CGD1568' I#TITL
     C                     MOVE 'CGD1569' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*OPEN   'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      *................................................................
      *
     C           EXINZ     TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *................................................................
      *
      **  Initialise work fields
     C                     Z-ADD*ZEROS    ##CREC
      *                                                                   081961
      ** CALL ACCESS PROGRAM FOR BANK DETAILS                             081961
      *                                                                   081961
     C                     CALL 'AOBANKR0'                                081961
     C                     PARM *BLANKS   ##RTCD                          081961
     C                     PARM '*FIRST  '##OPTN  7                       081961
     C           SDBANK    PARM SDBANK    DSFDY                           081961
     C*                                                                   081961
     C           ##RTCD    IFNE *BLANK                                    081961
      *                                                                   081961
      ** DATABASE ERROR                                                   081961
      *                                                                   081961
     C                     MOVEL'AOBANKR0'W0FILE                          081961
     C                     MOVEL'*CALL'   W0KEY            ***************081961
     C                     Z-ADD2         W0ERNB           * DB ERROR 02 *081961
     C                     MOVEL'MEM5003' W0MSGD           ***************081961
     C                     MOVEL'MIDAS  ' W0MSGF                          081961
     C                     EXSR SRERR                                     081961
     C                     END                                            081961
      *                                                                   081961
      ** RETRIEVE PREVIOUS RUN DATE FROM JNSTAT                           081961
      *                                                                   081961
     C           *NAMVAR   DEFN           JNSTAT                          081961
     C           *LOCK     IN   JNSTAT                                    081961
     C                     OUT  JNSTAT                                    081961
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
      *
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRAUDT                                         *
      * Function    :  Audit Report Processing                        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *                CG9020   - Write Data to UDC Data Files - Audit*
      *****************************************************************
     C           SRAUDT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
      *
      *................................................................
      *
      **  Output the Count of records read
     C                     CLEARI#DTA
     C                     MOVEL'CG4400  'I#REF
     C                     MOVE 'CGD1568' I#TITL
     C                     MOVE 'CGD1569' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE *BLANKS   I#SUB
     C                     MOVEL'CPEVED  'I#SUB
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADD##CREC    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Skip a line.
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Produce the Audit Report.
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD
     C                     PARM '*AUDIT  'W0ACT
     C                     PARM *BLANKS   W0PATH256
     C                     PARM           W0FMT
     C                     PARM 'CGD1568' W0TITL  7
     C                     PARM 'CGD1569' W0ULIN  7
     C                     PARM           W0CMT
      *
      **  Close the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4400  'I#REF
     C                     MOVE 'CGD1568' I#TITL
     C                     MOVE 'CGD1569' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      *................................................................
      *
     C           EXAUDT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5                                                            081961
     C/COPY ZSRSRC,ZACCH                                                  081961
     C/SPACE 5                                                            081961
     C/COPY ZSRSRC,ZBKDT                                                  081961
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRPSSR
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRC
     O/SPACE 5
     OCPEVEDF E                UCPEVE
     O                         CPCF
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
