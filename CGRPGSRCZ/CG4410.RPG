     H        1
     F*****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Securities advance confirmations')
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG4410 - Securities Advance Confirmations                    *
     F*                                                               *
     F*  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
     F*  (phase(s))                                                   *
     F*                                                               *
     F*  Called By: mmCnnnn - (program name)                          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG9646            Date 20Dec05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG3122            Date 17Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 CSC022             Date 24Feb04               *
      *                 CGL029             Date 01Sep03               *
      *                 214731             Date 27May03               *
      *                 170783             Date 09JUL02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CCG015             Date 09Aug01               *
      *                 180707             Date 25Jul00               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 15May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 130862             Date 18Oct98               *
     F*                 CEU014             Date 11Jun98               *
     F*                 089776             Date 17Apr96               *
     F*                 089693             DATE 16APR96               *
     F*                 092961             DATE 04OCT95               *
     F*                 087972             DATE 18MAY95               *
     F*                 086918             DATE 02MAY95               *
     F*                 082060             DATE 24JAN95               *
     F*                 S01522             DATE 22NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG9646- Changes in CGPACKE causes programs not to compile   *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  214731 - Add Portfolio Reference and Investment Name in      *
      *           Securities modules extracts.                        *
      *  170783 - RECOMPILE OVER CHANGED ZLCDZ1.                      *
      *  CCG015 - Correspondence Manager Phase 1                      *
     F*  180707 - No Position Settlements generated. Recompile over   *
     F*           changes in /COPY ZNCDZ3.                            *
     F*  CSD006 - Recompiled over changed SDCURRPD.                   *
     F*  130862 - Recompiled over ZLCDZ1 & ZDAYSZ2 changes.           *
     F*  CEU014 - EMU User Defined Correspondence Enhancement.        *
     F*  089776 - Add 'Account officer' details                       *
     F*  089693 - Add 'ISIN/COMMON CODE' to group set T_SECD          *
     F*  092961 - Recompile over ZDAYSZ2 for 086491                   *
     F*           Wrong number of days calculated for day basis 1     *
     F*           when start or end day is last day of February.      *
     F*  087972 - Place Midas derived sec. desc always in line 3      *
     F*  086918 - Add customer town RDE to Group set CUST.            *
     F*  082060 - Redefine Binary fields as Decimal (9,0 Packed)      *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  02   -    Deal read from DEALSDC                             *
     F*                                                               *
     F*  99   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
     FSECTY   IF  E           K        DISK
     F                                              KINFSR SRFILE
     FINVTP   IF  E           K        DISK
     F                                              KINFSR SRFILE
     F/SPACE 5
     E/COPY CGCPYSRC,CGPACKE
     E/COPY CGCPYSRC,SRERRE
     E**  Number Arrays
     E                    ##W        20  1
     E                    ##NUMA     29  1
     E*
     E* ##GRP = Group/RDE Number               ##RDEA = RDE + Attributes
     E*
     E********************##GRP   1  29 10   ##RDEA 22                    086918
     E************        ##GRP   1  30 10   ##RDEA 22              086918089693
     E********************##GRP   1  31 10   ##RDEA 22              089693CEU014
     E************        ##GRP   1  37 10   ##RDEA 22                                 CEU014 214731
     E                    ##GRP   1  38 10   ##RDEA 22                                        214731
     E*
     E**  Array of Currencies accessed by program
     E                    ##CY      100  3
     E*
     E**  Arrays of Event Type (CCET) and the associated print item sub-type
     E                    ##ET    1   5  2   ##PS   10
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     E/COPY ZSRSRC,ZNCDZ1
     E/COPY ZSRSRC,ZSDESCZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZDAYSZ1
     I*
     I*********** DS                                                      082060
     I***********                         B   1   20##OSEQ                082060
     I****Convert Output Sequence between Numeric and Binary              082060
     I*
     I            DS
     I                                        1  10 ##GRPX
     I                                        1   6 ##GRPS
     I                                        7  100##RDEN
     I**  Look-up string for RDE definition Array
     I*
     I##SDS       DS
     I**********                              15120 ##S                                      BUG9646
     I                                        15632 ##S                                      BUG9646
     I**  Packed data string.
     I*
     I            DS
     I                                        1  20 ##W
     I                                        1  200##WNUM
     I**  Number Field
     I*
     I            DS
     I                                        1   8 ##ITMA
     I                                        1   80##ITEM
     I** Used to convert item reference from character to numeric
     I*
     I            DS
     I                                        1   6 ##NCIT
     I                                        1   3 NMCY
     I                                        4   6 SITP
     I**  For error reporting
     I*
     I*---------------------------------------------------------------------
     I* Named constants
     I*
     I              'SEC_ADV_CF'          C         W#PTYP
     I*---------------------------------------------------------------------
     I##UDCR    E DSCGUDCRPD
      ** External DS for incomplete reference
      *
     ISDBANK    E DSSDBANKPD
      **  External DS for Bank details
      *
     ISDSTRD    E DSSDSTRDPD
      **  External DS for Securities Trading Module details
      *
     ISDBRCH    E DSSDBRCHPD
      **  External DS for branch details
      *
     ISDCUST    E DSSDCUSTPD
      **  External DS for customer details
     I              QQDFAC                          QQDFA1                                    CGL029
      *
     ISDSECS    E DSSDSECSPD
      **  External DS for securities trading customer details
      *
     ISDCURR    E DSSDCURRPD                101
      **  External DS for currency details
      *                                                                   CEU014
     ISDGELR    E DSSDGELRPD                                              CEU014
      **  External DS for General Ledger details                          CEU014
     I              QQACCD                          QQACC1                                    CGL029
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      **  Second DS for access programs, long data structure
      *
     IDSPARM    E DSCPEVED
      **  External DS for parameters based on PF/CPEVED
      *
     IW0FMT     E DSCGUDTAPD
      **  Record format of PF/CGUDTAPD for use as a parameter
      *
     I##SRDT      DS                         20
      **  DS to store subroutine specific data.
      **  - Saved Path
     I                                        1  70 ##SPAT
     I/COPY CGCPYSRC,CGPACKI
      *
     ICPYR        DS
      **  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
      *                                                                   089776
      ** Parameters for call to CG4976.                                   089776
      **                                                                  089776
     IP5PARM      DS                            256                       089776
      * Output Sequence                                                   089776
     I                                        1   90R1OSEQ                089776
      * UDC item number                                                   089776
     I                                       10  170R1ITEM                089776
      * This bind level                                                   089776
     I                                       18  250R1TBIN                089776
      * Previous bind level                                               089776
     I                                       26  330R1PBIN                089776
     IP7PARM      DS                            256                       089776
      * Account Officer                                                   089776
     I                                        1   2 P1ACOF                089776
     I/COPY CGCPYSRC,SRERRI
     I/COPY ZSRSRC,ZNCDZ2
     I/COPY ZSRSRC,ZSDESCZ2
     C/SPACE 5
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRINZ  - Initialise Fields on Each Invocation  *
      *                SRMAIN - Main Processing                       *
      *****************************************************************
      *
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
     C                     PARM           ##COPD  1
     C                     PARM           ##AUTO  1
     C                     PARM           DSPARM
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      ** Initialisation processing
     C                     EXSR SRINZ
      *
      ** Main processing
     C                     EXSR SRMAIN
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C                     RETRN
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
      ** Access Security (and investment type) details
      ** (Set event control date to event date)
      *
     C                     Z-ADDCCDT      ECD     50
     C                     Z-ADDCCDT      ZECD    50
     C                     EXSR SRSECT
      *
      **  Generate reference number by writing to PF/CGUDCRPD
      **  If no confirmation to be produced, then bypass
      *
     C           *LIKE     DEFN CCET      ##CCET
     C                     MOVELCCET      ##CCET
     C                     MOVE 'N'       ##COPD
     C                     EXSR SRGENR
     C           ##COPD    CABEQ'N'       EXMAIN
      *
      ** Extract Advance Confirmations
      *
     C                     EXSR T06ADC
      *
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  T06ADC                                         *
      * Function    :  Extract Advance Confirmations                  *
      *                                                               *
      * Called by   :                                                 *
      * Calls       :                                                 *
      *****************************************************************
     C           T06ADC    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL't06ADC'  @STK,Q
      *
      *................................................................
      *
      **  Set up Path.
     C                     MOVEL'\t06ADC' ##PNAM  7
     C           S#PATH    CAT  ##PNAM:0  S#PATH
      *                                                                                       CCG015
     C                     EXSR PSHGRS                                                        CCG015
      *
      **  Store Path
     C           1         OCUR ##SRDT
     C                     MOVELS#PATH    ##SPAT
      *
      ** Branch Details
     C                     EXSR BRANCH
      *
      ** Security Details
     C                     EXSR T@SECD
      *
      ** Advance Confirmation Details
     C                     EXSR T@ADCD
      *
      ** Customer Details
     C                     EXSR CUST
      *                                                                   089776
      ** Counterparty Account Officer Details                             089776
     C                     EXSR ACOF                                      089776
      *
      *................................................................
      *
     C           EX6ADC    TAG
      *
      **  Unwind subroutine stack name
      *                                                                                       CCG015
     C                     EXSR POPGRS                                                        CCG015
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  BRANCH                                         *
      * Function    :  Branch Details                                 *
      *                                                               *
      * Called by   :                                                 *
      * Calls       :                                                 *
      *****************************************************************
     C           BRANCH    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'BRANCH'  @STK,Q
      *
      *................................................................
      *
      **  Retrieve subroutine specific data.
     C           1         OCUR ##SRDT
     C                     MOVEL##SPAT    S#PATH
      *
      **  Set up Path.
     C                     MOVEL'\BRANCH' ##PNAM
     C           S#PATH    CAT  ##PNAM:0  S#PATH
      *                                                                                       CCG015
     C                     EXSR PSHGRS                                                        CCG015
      *
      **  Retrieve RDE information for RDEs in group set
      *
     C                     MOVEL@STK,Q    ##GRPS
     C                     Z-ADD1         ##RDEN
     C                     EXSR SRRRDE
      *
      ** Get Branch Details
      *
     C           CCBA      IFNE ##BRCD
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG    '##RTCD
     C                     PARM '*KEY    '##OPTN
     C                     PARM CCBA      ##BRCD  3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ** DATABASE ERROR
      *
     C           ##RTCD    IFNE *BLANK
     C                     MOVEL'SDBRCHPD'W0FILE
     C                     MOVELCCBA      W0KEY            ***************
     C                     Z-ADD1         W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      ** Get Customer Details for Branch Internal Customer
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*KEY   ' ##OPTN
     C                     PARM A8BICN    ##KEY1 10
     C                     PARM *BLANKS   ##KYST  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ** DATABASE ERROR
      *
     C           ##RTCD    IFNE *BLANK
     C                     MOVEL'SDCUSTPD'W0FILE
     C                     MOVELA8BICN    W0KEY            ***************
     C                     Z-ADD2         W0ERNB           * DB ERROR 02 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      ** Store Booking Branch and Branch Internal Customer Number
      *
     C           *LIKE     DEFN CCBA      BBBRCA
     C                     MOVELCCBA      BBBRCA
     C           *LIKE     DEFN BBCUST    BBBICN
     C                     MOVELBBCUST    BBBICN
     C                     END
      *
      ** Branch
     C                     MOVELA8BRCD    ##D,1
      *
      ** Branch Name
     C                     MOVELA8BRNM    ##D,2
      *
      **  Accumulate RDE's and associated data and output to
      **  PF/CGUDTAPD.
     C                     EXSR SRADTA
     C                     EXSR SRODTA
      *
      *................................................................
      *
     C           EXBRCH    TAG
      *
      **  Unwind subroutine stack name
      *                                                                                       CCG015
     C                     EXSR POPGRS                                                        CCG015
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  CUST                                           *
      * Function    :  Customer Details                               *
      *                                                               *
      * Called by   :                                                 *
      * Calls       :                                                 *
      *****************************************************************
     C           CUST      BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'CUST  '  @STK,Q
      *
      *................................................................
      *
      **  Retrieve subroutine specific data.
     C           1         OCUR ##SRDT
     C                     MOVEL##SPAT    S#PATH
      *
      **  Set up Path.
     C                     MOVEL'\CUST  ' ##PNAM
     C           S#PATH    CAT  ##PNAM:0  S#PATH
      *                                                                                       CCG015
     C                     EXSR PSHGRS                                                        CCG015
      *
      **  Retrieve RDE information for RDEs in group set
      *
     C                     MOVEL@STK,Q    ##GRPS
     C                     Z-ADD1         ##RDEN
     C                     EXSR SRRRDE
      *
      ** Get Customer Details
      *
     C                     MOVELCCCN      ##KEY1
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*KEY   ' ##OPTN
     C                     PARM           ##KEY1
     C                     PARM *BLANKS   ##KYST
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ** DATABASE ERROR
      *
     C           ##RTCD    IFNE *BLANK
     C                     MOVEL'SDCUSTPD'W0FILE           ***************
     C                     MOVELCCCN      W0KEY            * DB ERROR 03 *
     C                     Z-ADD3         W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      ** Get Securities trading customer details
      *
     C                     CALL 'AOSECSR0'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*KEY    '##OPTN
     C                     PARM BBCUST    ##CUST
     C           SDSECS    PARM SDSECS    DSSDY
      *
      ** DATABASE ERROR
      *
     C           ##RTCD    IFNE *BLANK
     C                     MOVEL'SDSECSPD'W0FILE           ***************
     C                     MOVELBBCUST    W0KEY            * DB ERROR 04 *
     C                     Z-ADD4         W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      ** Store Customer Number and Name
      *
     C           *LIKE     DEFN BBCUST    CPCUST
     C                     MOVELBBCUST    CPCUST
      *
      ** Customer
     C                     MOVELBBCUST    ##D,1
      *
      ** Customer Name
     C                     MOVELBBCRNM    ##D,2
      *
      ** Customer Address Line 1
     C                     MOVELBBCNA1    ##D,3
      *
      ** Customer Address Line 2
     C                     MOVELBBCNA2    ##D,4
      *
      ** Customer Address Line 3
     C                     MOVELBBCNA3    ##D,5
      *
      ** Customer Address Line 4
     C                     MOVELBBCNA4    ##D,6
      *                                                                   086918
      ** Customer Town                                                    086918
     C                     MOVELBBCRTN    ##D,7                           086918
      *
      **  Accumulate RDE's and associated data and output to
      **  PF/CGUDTAPD.
     C                     EXSR SRADTA
     C                     EXSR SRODTA
      *
      *................................................................
      *
     C           EXCUST    TAG
      *
      **  Unwind subroutine stack name
      *                                                                                       CCG015
     C                     EXSR POPGRS                                                        CCG015
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5                                                            089776
      *****************************************************************   089776
      * Subroutine  :  ACOF                                           *   089776
      * Function    :  Account Officer Details                        *   089776
      *                                                               *   089776
      * Called by   :                                                 *   089776
      * Calls       :                                                 *   089776
      *****************************************************************   089776
     C           ACOF      BEGSR                                          089776
      *                                                                   089776
      **  Set up subroutine stack name                                    089776
     C                     ADD  1         Q                               089776
     C                     MOVEL'ACOF  '  @STK,Q                          089776
      *                                                                   089776
      *................................................................   089776
      *                                                                   089776
      **  Retrieve subroutine specific data.                              089776
     C           1         OCUR ##SRDT                                    089776
     C                     MOVEL##SPAT    S#PATH                          089776
      *                                                                   089776
      **  Set up data.                                                    089776
     C                     CLEARP7PARM                                    089776
     C                     MOVELBBACOC    P1ACOF                          089776
      *                                                                   089776
     C                     CLEARP4PARM                                    089776
     C                     MOVELS#PATH    P4PARM                          089776
      *                                                                   089776
     C                     CLEARP5PARM                                    089776
     C                     Z-ADD##OSEQ    R1OSEQ                          089776
     C                     Z-ADD##ITEM    R1ITEM                          089776
     C                     Z-ADDDETBIN    R1TBIN                          089776
     C                     Z-ADDDEPBIN    R1PBIN                          089776
      *                                                                   089776
     C                     CALL 'CG4976'                                  089776
     C                     PARM *BLANKS   W0RTN   7                       089776
     C                     PARM           P7PARM                          089776
     C                     PARM           P4PARM140        Path           089776
     C                     PARM           W0CMT   3                       089776
     C                     PARM           P5PARM                          089776
     C                     PARM           ##REFR                                              CCG015
     C                     PARM           P@ICR                                               CCG015
     C                     PARM 'N'       ##MAST                                              CCG015
      *                                                                   089776
      **  Reset Sequence number.                                          089776
     C                     Z-ADDR1OSEQ    ##OSEQ                          089776
     C                     Z-ADDR1TBIN    DETBIN                          089776
     C                     Z-ADDR1PBIN    DEPBIN                          089776
      *                                                                   089776
      *                                                                   089776
      *                                                                   089776
      *................................................................   089776
      *                                                                   089776
     C           EXACOF    TAG                                            089776
      *                                                                   089776
      **  Unwind subroutine stack name                                    089776
     C                     MOVEA*BLANKS   @STK,Q                          089776
     C                     SUB  1         Q                               089776
      *                                                                   089776
     C                     ENDSR                                          089776
     C*****************************************************************   089776
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  T@ADCD                                         *
      * Function    :  Advance Confirmation Details                   *
      *                                                               *
      * Called by   :                                                 *
      * Calls       :                                                 *
      *****************************************************************
     C           T@ADCD    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'T_ADCD'  @STK,Q
      *
      *................................................................
      *
      **  Retrieve subroutine specific data.
     C           1         OCUR ##SRDT
     C                     MOVEL##SPAT    S#PATH
      *
      **  Set up Path.
     C                     MOVEL'\T_ADCD' ##PNAM
     C           S#PATH    CAT  ##PNAM:0  S#PATH
      *                                                                                       CCG015
     C                     EXSR PSHGRS                                                        CCG015
      *
      **  Retrieve RDE information for RDEs in group set
      *
     C                     MOVEL@STK,Q    ##GRPS
     C                     Z-ADD1         ##RDEN
     C                     EXSR SRRRDE
      *
      ** Run Date
     C                     MOVELBJRDNB    ##D,1
      *
      **  Event Type
      *
     C                     MOVELCCET      ##D,2
      *
      **  Event Date
      *
     C                     MOVELCCDT      ##D,3
      *
      **  Event Currency
      *
     C                     MOVELCCEC      ##D,4
      *
      **  Event Amount
     C                     MOVELCCEC      ##CCY
     C                     EXSR SRDCDT
     C                     MOVELCCEA      ##D,5
     C                     MOVE A6NBDP    ##D,5
     C/COPY WNCPYSRC,CG4410C005
      *
      **  Nominal
      *
     C           CCET      IFEQ 'CP'
     C           CCNP      SUB  CCEP      ##W150
     C                     ELSE
     C                     Z-ADDCCNP      ##W150
     C                     ENDIF
     C/COPY WNCPYSRC,CG4410C001
     C                     MOVEL##W150    ##D,6
     C                     MOVE NMDP      ##D,6
     C/COPY WNCPYSRC,CG4410C002
      *
      **  Pay By Us
      *
     C           CCFC      IFEQ 'T'
     C                     MOVEL'Y'       ##D,7
     C                     ELSE
     C                     MOVEL'N'       ##D,7
     C                     END
      *
      **  Pay By Them
      *
     C           CCFC      IFEQ 'F'
     C                     MOVEL'Y'       ##D,8
     C                     ELSE
     C                     MOVEL'N'       ##D,8
     C                     END
     C*                                                                   CEU014
     C** If EMU UDC Enhancement is switched on then extract EMU           CEU014
     C** equivalent amounts.                                              CEU014
     C*                                                                   CEU014
     C           CEU014    IFEQ 'Y'                                       CEU014
     C*                                                                   CEU014
     C**  first determine the event amount equivalent currency EEQCY      CEU014
     C*                                                                   CEU014
     C                     MOVE *BLANKS   EEQCY   3                       CEU014
     C                     MOVE CCEC      ##CCY                           CEU014
     C                     EXSR SRDCDT                                    CEU014
     C           A6INCY    IFEQ 'Y'                                       CEU014
     C                     MOVE BKEURO    EEQCY                           CEU014
     C                     ENDIF                                          CEU014
     C           CCEC      IFEQ BKEURO                                    CEU014
     C                     MOVELCCCN      ##KEY1                          CEU014
     C                     CALL 'AOCUSTR0'                                CEU014
     C                     PARM *BLANKS   ##RTCD                          CEU014
     C                     PARM '*KEY   ' ##OPTN                          CEU014
     C                     PARM           ##KEY1                          CEU014
     C                     PARM *BLANKS   ##KYST                          CEU014
     C           SDCUST    PARM SDCUST    DSSDY                           CEU014
     C*                                                                   CEU014
     C           ##RTCD    IFNE *BLANK                                    CEU014
     C                     MOVEL'SDCUSTPD'W0FILE                          CEU014
     C                     MOVELCCCN      W0KEY            ************   CEU014
     C                     Z-ADD13        W0ERNB           * DBERR 13 *   CEU014
     C                     MOVEL'MEM5004' W0MSGD           ************   CEU014
     C                     MOVEL'MIDAS  ' W0MSGF                          CEU014
     C                     EXSR SRERR                                     CEU014
     C                     ENDIF                                          CEU014
     C*                                                                   CEU014
     C                     MOVE BBDINC    EEQCY                           CEU014
     C                     ENDIF                                          CEU014
     C*                                                                   CEU014
     C           EEQCY     IFNE *BLANKS                                   CEU014
     C*                                                                   CEU014
     C** Event Amount in Event Equivalent Currency                        CEU014
     C*                                                                   CEU014
     C                     MOVE EEQCY     ##CCY                           CEU014
     C                     EXSR SRDCDT                                    CEU014
     C                     Z-ADDCCEA      P@FRAM                          CEU014
     C                     MOVE CCEC      P@FRCY                          CEU014
     C                     MOVE EEQCY     P@TOCY                          CEU014
     C                     EXSR T@EURO                                    CEU014
     C                     MOVEL##OUTA    ##D,9                           CEU014
     C                     MOVE A6NBDP    ##D,9                           CEU014
     C*                                                                   CEU014
     C** Event Amount Equivalent Currency                                 CEU014
     C                     MOVELEEQCY     ##D,10                          CEU014
     C*                                                                   CEU014
     C** Event Amount Equivalent Currency Description                     CEU014
     C                     MOVELA6CYNM    ##D,11                          CEU014
     C*                                                                   CEU014
     C                     ENDIF                                          CEU014
     C*                                                                   CEU014
     C**  now determine the nominal equivalent currency NEQCY             CEU014
     C*                                                                   CEU014
     C                     MOVE *BLANKS   NEQCY   3                       CEU014
     C                     MOVE NMCY      ##CCY                           CEU014
     C                     EXSR SRDCDT                                    CEU014
     C           A6INCY    IFEQ 'Y'                                       CEU014
     C                     MOVE BKEURO    NEQCY                           CEU014
     C                     ENDIF                                          CEU014
     C           NMCY      IFEQ BKEURO                                    CEU014
     C                     MOVELCCCN      ##KEY1                          CEU014
     C                     CALL 'AOCUSTR0'                                CEU014
     C                     PARM *BLANKS   ##RTCD                          CEU014
     C                     PARM '*KEY   ' ##OPTN                          CEU014
     C                     PARM           ##KEY1                          CEU014
     C                     PARM *BLANKS   ##KYST                          CEU014
     C           SDCUST    PARM SDCUST    DSSDY                           CEU014
     C*                                                                   CEU014
     C           ##RTCD    IFNE *BLANK                                    CEU014
     C                     MOVEL'SDCUSTPD'W0FILE                          CEU014
     C                     MOVELCCCN      W0KEY            ************   CEU014
     C                     Z-ADD14        W0ERNB           * DBERR 14 *   CEU014
     C                     MOVEL'MEM5004' W0MSGD           ************   CEU014
     C                     MOVEL'MIDAS  ' W0MSGF                          CEU014
     C                     EXSR SRERR                                     CEU014
     C                     ENDIF                                          CEU014
     C*                                                                   CEU014
     C                     MOVE BBDINC    NEQCY                           CEU014
     C                     ENDIF                                          CEU014
     C*                                                                   CEU014
     C           NEQCY     IFNE *BLANKS                                   CEU014
     C*                                                                   CEU014
     C** Nominal Amount in Nominal Equivalent Currency                    CEU014
     C*                                                                   CEU014
     C**   NB. convert ##W150 from an outright amount for NMDP decimals   CEU014
     C**   to an outright amount for the number of decimals for NMCY      CEU014
     C**   before calling EU0200.                                         CEU014
     C*                                                                   CEU014
     C                     Z-ADD0         P@FRAM                          CEU014
     C                     Z-ADD0         ##WEQV 224                      CEU014
     C                     SELEC                                          CEU014
     C           NMDP      WHEQ 0                                         CEU014
     C           ##W150    DIV  1         ##WEQV                          CEU014
     C           NMDP      WHEQ 1                                         CEU014
     C           ##W150    DIV  10        ##WEQV                          CEU014
     C           NMDP      WHEQ 2                                         CEU014
     C           ##W150    DIV  100       ##WEQV                          CEU014
     C           NMDP      WHEQ 3                                         CEU014
     C           ##W150    DIV  1000      ##WEQV                          CEU014
     C           NMDP      WHEQ 4                                         CEU014
     C           ##W150    DIV  10000     ##WEQV                          CEU014
     C                     ENDSL                                          CEU014
     C                     SELEC                                          CEU014
     C           A6NBDP    WHEQ 0                                         CEU014
     C                     MULT 1         ##WEQV                          CEU014
     C           A6NBDP    WHEQ 1                                         CEU014
     C                     MULT 10        ##WEQV                          CEU014
     C           A6NBDP    WHEQ 2                                         CEU014
     C                     MULT 100       ##WEQV                          CEU014
     C           A6NBDP    WHEQ 3                                         CEU014
     C                     MULT 1000      ##WEQV                          CEU014
     C                     ENDSL                                          CEU014
     C                     Z-ADD##WEQV    P@FRAM                          CEU014
     C*                                                                   CEU014
     C                     MOVE NEQCY     ##CCY                           CEU014
     C                     EXSR SRDCDT                                    CEU014
     C                     MOVE NMCY      P@FRCY                          CEU014
     C                     MOVE NEQCY     P@TOCY                          CEU014
     C                     EXSR T@EURO                                    CEU014
     C                     MOVEL##OUTA    ##D,12                          CEU014
     C                     MOVE A6NBDP    ##D,12                          CEU014
     C*                                                                   CEU014
     C** Nominal Equivalent Currency                                      CEU014
     C                     MOVELNEQCY     ##D,13                          CEU014
     C*                                                                   CEU014
     C** Nominal Equivalent Currency Description                          CEU014
     C                     MOVELA6CYNM    ##D,14                          CEU014
     C*                                                                   CEU014
     C                     ENDIF                                          CEU014
     C*                                                                   CEU014
     C                     ENDIF                                          CEU014
      *
      **  Accumulate RDE's and associated data and output to
      **  PF/CGUDTAPD.
     C                     EXSR SRADTA
     C                     EXSR SRODTA
      *
      *................................................................
      *
     C           EXADCD    TAG
      *
      **  Unwind subroutine stack name
      *                                                                                       CCG015
     C                     EXSR POPGRS                                                        CCG015
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  T@SECD                                         *
      * Function    :  Security Details                               *
      *                                                               *
      * Called by   :                                                 *
      * Calls       :                                                 *
      *****************************************************************
     C           T@SECD    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'T_SECD'  @STK,Q
      *
      *................................................................
      *
      **  Retrieve subroutine specific data.
     C           1         OCUR ##SRDT
     C                     MOVEL##SPAT    S#PATH
      *
      **  Set up Path.
     C                     MOVEL'\T_SECD' ##PNAM
     C           S#PATH    CAT  ##PNAM:0  S#PATH
      *                                                                                       CCG015
     C                     EXSR PSHGRS                                                        CCG015
      *
      **  Retrieve RDE information for RDEs in group set
      *
     C                     MOVEL@STK,Q    ##GRPS
     C                     Z-ADD1         ##RDEN
     C                     EXSR SRRRDE
      *
      ** Security shortname
     C                     MOVELSESN      ##D,1
      *
      ** Processing type
     C                     MOVELPROT      ##D,2
      *
      ** Nominal currency
     C                     MOVELNMCY      ##D,3
      *
      ** Security investment type
     C                     MOVELSITP      ##D,4
      *
      ** Security descriptions
     C                     MOVE SFN1      ZSFN1
     C                     MOVE SFN2      ZSFN2
     C                     MOVE BVROSD    ZRSFD
     C                     EXSR ZSDESC
      *                                                                   087972
      * Place derived description always in line 3                        087972
      *                                                                   087972
     C                     SELEC                                          087972
     C           SFN1      WHNE *BLANKS                                   087972
     C           SFN2      ANDNE*BLANKS                                   087972
     C                     MOVELZFNM1     ##D,5
     C                     MOVELZFNM2     ##D,6
     C                     MOVELZFNM3     ##D,7
     C           SFN1      WHNE *BLANKS                                   087972
     C           SFN2      ANDEQ*BLANKS                                   087972
     C                     MOVELZFNM1     ##D,5                           087972
     C                     MOVELZFNM2     ##D,7                           087972
     C           SFN1      WHEQ *BLANKS                                   087972
     C           SFN2      ANDEQ*BLANKS                                   087972
     C                     MOVELZFNM1     ##D,7                           087972
     C                     ENDSL                                          087972
      *
      ** Next coupon date
     C                     MOVEL##NCD     ##D,8
      *
      ** Last coupon date
     C                     MOVEL##LCD     ##D,9
      *
      ** Coupon rate
     C                     MOVELCPNR      ##D,10
      *
      ** Security maturity date
     C                     MOVELMATY      ##D,11
      *
      ** Security initial date
     C                     MOVELITLD      ##D,12
      *
      ** Security issue date
     C                     MOVELISSD      ##D,13
      *                                                                   089693
      ** Security ISIN/COMMON Code                                        089693
     C                     MOVELISIN      ##D,14                          089693
      *                                                                   214731
      ** Security Investment Name                                         214731
     C                     MOVELINAM      ##D,15                          214731
      *
      **  Accumulate RDE's and associated data and output to
      **  PF/CGUDTAPD.
     C                     EXSR SRADTA
     C                     EXSR SRODTA
      *
      *................................................................
      *
     C           EXSECD    TAG
      *
      **  Unwind subroutine stack name
      *                                                                                       CCG015
     C                     EXSR POPGRS                                                        CCG015
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRSECT                                         *
      * Function    :  Access security details                        *
      *                                                               *
      * Called by   :  SRMAIN - Main Processing                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRSECT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRSECT'  @STK,Q
      *
      *................................................................
      *
      ** Access the security
      *
     C           CCSS      CHAINSECTYDF              99
      *
      ** DATABASE ERROR
      *
     C           *IN99     IFEQ '1'
     C                     MOVEL'SECTY   'W0FILE           ***************
     C                     MOVELCCSS      W0KEY            * DB ERROR 05 *
     C                     Z-ADD5         W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      ** Determine nominal currency decimal places.
      *
     C                     MOVELNMCY      ##CCY   3
     C                     EXSR SRDCDT
     C                     Z-ADDA6NBDP    NOMCDP  10
      *
      **  If the security is interest bearing
      **  Determine the next and last coupon date
      **  (relative to the event control date)
      *
     C                     Z-ADD*ZERO     ##NCD   50
     C                     Z-ADD*ZERO     ##LCD   50
     C           CD01      IFGT 0
     C                     EXSR ZNCD
     C                     Z-ADDNCD       ##NCD            *next coupon date
     C                     EXSR ZLCD
     C                     Z-ADDZZLCD     ##LCD            *last coupon date
     C                     ENDIF
      *
      ** Access the investment type
      *
     C           INVTPK    KLIST
     C                     KFLD           NMCY
     C                     KFLD           SITP
     C           INVTPK    CHAININVTPDF              99
      *
      ** DATABASE ERROR
      *
     C           *IN99     IFEQ '1'
     C                     MOVEL'INVTP   'W0FILE           ***************
     C                     MOVEL##NCIT    W0KEY            * DB ERROR 06 *
     C                     Z-ADD6         W0ERNB           ***************
     C                     MOVEL'MEM5004' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *................................................................
      *
     C           EXSECT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRGENR                                         *
      * Function    :  Generate reference number                      *
      *                by writing to PF/CGUDCRPD                      *
      *                                                               *
      * Called by   :  SRMAIN - Main Processing                       *
      * Calls       :  CG9010 - Create reference record               *
      *****************************************************************
     C           SRGENR    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRGENR'  @STK,Q
      *
      *................................................................
      *
      **  Generate reference number and check if further processing
      **  required. Set up all available information in the record
      **  format before calling.
      *
     C                     MOVELCCBA      DRBRCA
     C                     MOVELORBR      DRORBR
     C                     MOVEL'SE'      DRMODI
     C                     MOVELCCSS      DRMTRN
     C                     MOVEL*BLANK    DRMACT
      *
     C                     MOVELW#PTYP    DRPTYP           * print type
      *
     C                     Z-ADD1         #1
     C           ##CCET    LOKUP##ET,#1                  99*
     C                     MOVEL##PS,#1   DRPSTP           * print s/type
     C                     MOVEL'N'       DRATRM
      *
     C                     MOVELCCCN      ##CUST
      *
     C                     CALL 'CG9010'
     C                     PARM           ##RTCD  7
     C                     PARM '*GEN'    ##MODE 10
     C                     PARM W0CMT     ##CMT   3
     C                     PARM           ##CUST  6
     C                     PARM           ##UDCR
     C                     PARM           ##ITMA  8
      *
      **  Blank return code implies generate correspondence.
      **  CGD1270 => no error, but suppress output for this transaction.
      *
     C           ##RTCD    IFEQ *BLANK
      *
     C                     MOVE 'Y'       ##COPD
      *                                                                                       CCG015
      ** Pass reference as printtype:subtype                                                  CCG015
      *                                                                                       CCG015
     C           DRPTYP    CAT  ':':0     COLON  11 P                                         CCG015
     C           COLON     CAT  DRPSTP:0  ##REFR    P                                         CCG015
     C                     EXSR WRAPRF                                                        CCG015
      *
     C                     ELSE
      *
     C           ##RTCD    IFNE 'CGD1270'
     C                     MOVEL'CG9010  'W0FILE
     C                     MOVEL##RTCD    W0KEY            ***************
     C                     Z-ADD7         W0ERNB           * DB ERROR 07 *
     C                     MOVEL'CGD1286' W0MSGD           ***************
     C                     MOVEL'CGUSRMSG'W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
     C                     ENDIF
      *
      *................................................................
      *
     C           EXGENR    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRRRDE                                         *
      * Function    :  Retrieve RDE information for RDEs in group set *
      *                                                               *
      * Called by   :  Simple group set subroutines                   *
      * Calls       :  -                                              *
      *****************************************************************
     C           SRRRDE    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRRRDE'  @STK,Q
      *
      *................................................................
      *
      **  Reset
     C                     MOVEL*BLANK    ##R
     C                     MOVEL*BLANK    ##D
     C                     MOVEL*BLANK    ##S
      *
      **  Get information about next 20 RDEs in group set from compile
      **  time arrays
     C                     Z-ADD0         #1
     C           *IN99     DOUEQ'0'
     C           #1        OREQ 20
     C                     Z-ADD1         #2
     C           ##GRPX    LOKUP##GRP,#2                 99*
     C           *IN99     IFEQ '1'
     C                     ADD  1         #1
     C                     MOVEL##RDEA,#2 ##R,#1
     C                     ADD  1         ##RDEN
     C                     ENDIF
     C                     ENDDO
      *
      *................................................................
      *
     C           EXRRDE    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRADTA                                         *
      * Function    :  Accumulate RDEs and associated data for output *
      *                                                               *
      * Called by   :  Simple group set subroutines                   *
      * Calls       :  CG3999 - Format and Pack Data                  *
      *                SRERR  - Database Error                        *
      *****************************************************************
     C           SRADTA    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRADTA'  @STK,Q
      *
      *................................................................
      *
      **  Reformat RDE data
      *
     C                     EXSR SRRFMN
      *
      **  Pack RDEs and associated data into data string
     C                     CLEAR##SDS
      *                                                                                       CCG015
     C           CCG015    IFEQ 'Y'                                                           CCG015
     C                     MOVEL'*NEWARR 'W0ACT                                               CCG015
     C                     MOVELS#PATH    W0SPAT                                              CCG015
     C                     ELSE                                                               CCG015
     C                     MOVEL'*PACK   'W0ACT                                               CCG015
     C                     MOVEL*BLANK    W0SPAT                                              CCG015
     C                     ENDIF                                                              CCG015
      *                                                                                       CCG015
     C                     CALL 'CG3999'
     C                     PARM *BLANK    ##RTCD
     C**********           PARM '*PACK   'W0ACT                                               CCG015
     C                     PARM           W0ACT                                               CCG015
     C                     PARM           ##R
     C                     PARM           ##D
     C                     PARM           ##S
     C                     PARM           W0SPAT 70                                           CCG015
     C                     PARM           ##RN                                                CCG015
     C                     PARM           ##DN                                                CCG015
     C                     PARM           ##FM                                                CCG015
      *
      **  Database error
     C           ##RTCD    IFNE *BLANK
      *
     C                     MOVEL'CG3999  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 08 *
     C                     Z-ADD8         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *                                                                                       CCG015
     C                     EXSR WRTRDE                                                        CCG015
      *
      **  Initialise Write Format Parameter.
     C                     CLEARW0FMT
      *
      **  Set up Control Information.
     C                     Z-ADD##ITEM    DEITEM
     C                     ADD  1         ##OSEQ
     C                     Z-ADD##OSEQ    DEOSEQ
     C                     Z-ADD1         DEPBIN
     C                     Z-ADD1         DETBIN
     C                     MOVE 'FR'      DEFSLI
     C                     MOVE *BLANKS   W0PATH
     C                     MOVELS#PATH    W0PATH
      *
      **  Append Data from pack routine.
     C           W0FMT     CAT  ##SDS:0   W0FMT
      *
      *................................................................
      *
     C           EXADTA    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRRFMN                                         *
      * Function    :  Reformat RDE data                              *
      *                                                               *
      * Called by   :  SRADTA                                         *
      * Calls       :                                                 *
      *****************************************************************
     C           SRRFMN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRRFMN'  @STK,Q
      *
      *................................................................
      *
      ** Loop through RDE's and data
      *
     C                     Z-ADD0         #1
     C           #1        DOUEQ20
     C                     ADD  1         #1
     C                     MOVEL##R,#1    R#DEFN
     C                     MOVEL##D,#1    R#DATA
      *
      ** If data present and RDE is edited
      *
     C           R#DATA    IFNE *BLANK
     C           ##RDEC    ANDNE*BLANK
      *
      * Reformat Amount
     C                     Z-ADD1         #2
     C           *BLANK    LOKUP##NUMA,#2                99*
     C                     Z-ADD20        #3
     C                     Z-ADD0         ##WNUM
     C           #2        DOWGT1
     C           #2        ANDLE20
     C           #3        ANDGT1
     C                     SUB  1         #2
     C                     MOVEL##NUMA,#2 ##W,#3
     C                     SUB  1         #3
     C                     ENDDO
      *
      * Sign the amount
     C           ##SIGN    IFEQ '-'
     C                     Z-SUB##WNUM    ##NUMB
     C                     ELSE
     C                     Z-ADD##WNUM    ##NUMB
     C                     END
      *
      * Default Edit Type
     C           ##EDTT    IFEQ *BLANK
     C                     MOVEL##RDET    ##EDTT
     C                     END
      *
      * Default Decimal Places
     C           ##DCPA    IFEQ *BLANK
     C                     MOVEL##RDED    ##DCPA
     C                     END
      * New RDE data
     C                     MOVELR#DATA    ##D,#1
     C                     ENDIF
     C                     ENDDO
      *
      *................................................................
      *
     C           EXRFMN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * Subroutine  :  SRODTA                                         *
      * Function    :  Output Accumulated data to PF/CGUDTAPD         *
      *                                                               *
      * Called by   :  Simple group set subroutines                   *
      * Calls       :  CG9020 - Output Data to CGUDTAPD               *
      *                SRERR  - Database Error                        *
      *****************************************************************
     C           SRODTA    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRODTA'  @STK,Q
      *
      *................................................................
      *
      **  Output PF/CGUDTAPD Record.
     C           CCG015    IFEQ 'N'                                                           CCG015
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD
     C                     PARM '*WRITE  'W0ACT
     C                     PARM           W0PATH256
     C                     PARM           W0FMT
     C                     PARM *BLANK    W0TITL  7
     C                     PARM *BLANK    W0ULIN  7
     C                     PARM           W0CMT
      *
      **  Database error
     C           ##RTCD    IFNE *BLANK
      *
     C                     MOVEL'CG9020  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 09 *
     C                     Z-ADD9         W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
     C                     ENDIF                                                              CCG015
      *
      *................................................................
      *
     C           EXODTA    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRDCDT                                         *
      * Function    :  Determine Currency Details                     *
      *                                                               *
      * Called by   :                                                 *
      * Calls       :                                                 *
      *****************************************************************
     C           SRDCDT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRDCDT'  @STK,Q
      *
      *................................................................
      *
      **  Access array of Currency Details already loaded
     C                     Z-ADD1         C       30
     C           ##CCY     LOKUP##CY,C                   99
      *
      **  If no match is found determine the next free space in the
      **  loaded currency array.
     C           *IN99     IFEQ *OFF
     C                     Z-ADD1         C
     C           *BLANK    LOKUP##CY,C                   99
      *
      **  If no space is left, set the Currency Data data structure to
      **  the 101st occurrence so that when the new details are
      **  retrieved no existing details are overwritten.
     C           *IN99     IFEQ *OFF
     C           101       OCUR SDCURR
      *
      **  If a space is found, set up the new currency into the array
      **  and position the Currency Data data structure on the
      **  occurrence matching the array index.
     C                     ELSE
     C                     MOVE ##CCY     ##CY,C
     C           C         OCUR SDCURR
     C                     ENDIF
      *
      **  Retrieve the currency details.
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   ##RTCD           B:Return Cd
     C                     PARM '*KEY'    ##OPTN           I:Option
     C                     PARM ##CCY     ##CCYP  3        I:Currency Code
     C           SDCURR    PARM *BLANK    DSSDY            O:Format
      *
      **  Database Error
     C           ##RTCD    IFNE *BLANK
     C                     MOVEL'SDCURRPD'W0FILE
     C                     MOVEL##CCY     W0KEY            ***************
     C                     Z-ADD10        W0ERNB           * DB ERROR 10 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *
      **  If the currency has been retrieved earlier, position the
      **  Currency Data data structure on the relevant occurrence.
     C                     ELSE
     C           C         OCUR SDCURR
     C                     ENDIF
     C/COPY WNCPYSRC,CG4410C003
     C*
     C           EXDCDT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5                                                            CEU014
      *****************************************************************   CEU014
      * Subroutine  :  T@EURO                                         *   CEU014
      * Function    :  EURO Currency Conversion:                      *   CEU014
      * Converts amount P@FRAM in P@FRCY to amount ##OUTA in P@TOCY   *   CEU014
      * Called by   :  T@ADCD                                         *   CEU014
      *                                                               *   CEU014
      * Calls       :  EU0200                                         *   CEU014
      *                SRERR                                          *   CEU014
      *****************************************************************   CEU014
     C           T@EURO    BEGSR                                          CEU014
      *                                                                   CEU014
     C                     Z-ADD0         ##OUTA 150                      CEU014
     C                     CALL 'EU0200'                                  CEU014
     C                     PARM *BLANKS   P@RTCD  7        Return Code    CEU014
     C                     PARM           P@FRAM 183       FROM Ccy AmountCEU014
     C                     PARM           P@FRCY  3        FROM Currency  CEU014
     C                     PARM           P@TOCY  3        TO Currency    CEU014
     C                     PARM 0         P@OUTA 150       Outright AmountCEU014
     C                     PARM 0         P@INTA 183       Interim Amount CEU014
     C                     PARM 0         P@EXRT 159       Exchange Rate  CEU014
     C                     PARM *BLANK    P@MDIN  1        Mult/Div Ind.  CEU014
      *                                                                   CEU014
      ** Database error handling:                                         CEU014
      *                                                                   CEU014
     C           P@RTCD    IFEQ '*ERROR*'                                 CEU014
     C                     MOVEL'EU0200  'W0FILE                          CEU014
     C                     MOVEL'*CALL   'W0KEY            ***************CEU014
     C                     Z-ADD15        W0ERNB           * DB ERROR 15 *CEU014
     C                     MOVEL'MEM5003' W0MSGD           ***************CEU014
     C                     MOVEL'MIDAS  ' W0MSGF                          CEU014
     C                     EXSR SRERR                                     CEU014
      *                                                                   CEU014
      ** Otherwise, store amount in settlement currency as ##OUTA         CEU014
      *                                                                   CEU014
     C                     ELSE                                           CEU014
     C                     MOVE P@OUTA    ##OUTA                          CEU014
     C                     ENDIF                                          CEU014
      *                                                                   CEU014
     C                     ENDSR                                          CEU014
      *****************************************************************   CEU014
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
     C/COPY WNCPYSRC,CG4410C004
      *
      *................................................................
      *
      ** CALL ACCESS PROGRAM FOR BANK DETAILS
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM '*FIRST  '##OPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           ##RTCD    IFNE *BLANK
      *
      ** DATABASE ERROR
      *
     C                     MOVEL'AOBANKR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD11        W0ERNB           * DB ERROR 11 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
     C*
     C           BJDFIN    COMP 'M'                      98MMDDYY, 98 ON
     C*
     C** CALL ACCESS PROGRAM FOR SECURITIES TRADING DATA DETAILS
     C*
     C                     CALL 'AOSTRDR0'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*FIRST  '##OPTN
     C           SDSTRD    PARM SDSTRD    DSFDY
     C*
     C           ##RTCD    IFNE *BLANK
      *
      ** DATABASE ERROR
      *
     C                     MOVEL'AOSTRDR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD12        W0ERNB           * DB ERROR 12 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      ** Call access program for General Ledger ICD Details               CEU014
      *                                                                   CEU014
     C**********           CALL 'AOGELRR0'                                             CEU014 CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   ##RTCD                          CEU014
     C                     PARM '*FIRST  '##OPTN                          CEU014
     C********** SDGELR    PARM SDGELR    DSFDY                                        CEU014 CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C*                                                                   CEU014
      ** Database error                                                   CEU014
     C           ##RTCD    IFNE *BLANK                                    CEU014
     C                     MOVEL'AOGELRR0'W0FILE                          CEU014
     C                     MOVEL'*CALL'   W0KEY            ***************CEU014
     C                     Z-ADD16        W0ERNB           * DB ERROR 16 *CEU014
     C                     MOVEL'MEM5003' W0MSGD           ***************CEU014
     C                     MOVEL'MIDAS  ' W0MSGF                          CEU014
     C                     EXSR SRERR                                     CEU014
     C                     ENDIF                                          CEU014
      *                                                                   CEU014
     C** Access switchable features file to determine if                  CEU014
     C** EMU User Defined Correspondence enhancement is switched on.      CEU014
     C*                                                                   CEU014
     C                     CALL 'AOSARDR0'                                CEU014
     C                     PARM '       ' ##RTCD                          CEU014
     C                     PARM '*VERIFY' ##OPTN                          CEU014
     C                     PARM 'CEU014'  @SARD   6                       CEU014
      *                                                                   CEU014
      ** Database error                                                   CEU014
      *                                                                   CEU014
     C           ##RTCD    IFNE *BLANK                                    CEU014
     C           ##RTCD    ANDNE'*NRF   '                                 CEU014
     C                     MOVEL'SCSARDPD'W0FILE           ***************CEU014
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 17 *CEU014
     C                     Z-ADD17        W0ERNB           ***************CEU014
     C                     MOVEL'MEM5003' W0MSGD                          CEU014
     C                     MOVEL'MIDAS  ' W0MSGF                          CEU014
     C                     EXSR SRERR                                     CEU014
     C                     ENDIF                                          CEU014
     C           ##RTCD    IFEQ *BLANK                                    CEU014
     C                     MOVE 'Y'       CEU014  1                       CEU014
     C                     ELSE                                           CEU014
     C                     MOVE 'N'       CEU014                          CEU014
     C                     ENDIF                                          CEU014
      *                                                                                       CCG015
      ** Access SAR details file to determine if CCG015 is installed.                         CCG015
      *                                                                                       CCG015
     C                     CALL 'AOSARDR0'                                                    CCG015
     C                     PARM *BLANKS   PRTCD   7                                           CCG015
     C                     PARM '*VERIFY' POPTN   7                                           CCG015
     C                     PARM 'CCG015'  PSARD   6                                           CCG015
      *                                                                                       CCG015
      ** Database Error                                                                       CCG015
      *                                                                                       CCG015
     C           PRTCD     IFNE *BLANK                                                        CCG015
     C           PRTCD     ANDNE'*NRF   '                                                     CCG015
     C                     MOVEL'SCSARDPD'W0FILE                                              CCG015
     C                     MOVEL'*CALL'   W0KEY                                               CCG015
     C                     Z-ADD18        W0ERNB                                              CCG015
     C                     MOVEL'MEM5003' W0MSGD                                              CCG015
     C                     MOVEL'MIDAS  ' W0MSGF                                              CCG015
     C                     EXSR SRERR                                                         CCG015
     C                     ENDIF                                                              CCG015
      *                                                                                       CCG015
     C           PRTCD     IFEQ *BLANKS                                                       CCG015
     C                     MOVE 'Y'       CCG015  1                                           CCG015
     C                     ELSE                                                               CCG015
     C                     MOVE 'N'       CCG015                                              CCG015
     C                     ENDIF                                                              CCG015
     C*                                                                   CEU014
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINZ                                          *
      * Function    :  Initialise Fields on Program Invocation        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINZ '  @STK,Q
      *
      *................................................................
      *
      **  Initialise work fields
     C                     Z-ADD*ZEROS    ##W050  50
     C                     Z-ADD*ZEROS    ##W150 150
     C                     Z-ADD*ZEROS    #1      60
     C                     Z-ADD*ZEROS    #2      60
     C                     Z-ADD*ZEROS    #3      60
     C                     MOVE *BLANKS   S#PATH256
     C           *LIKE     DEFN DEOSEQ    ##OSEQ                          082060
      *
      **  Initialise output sequence (to CGUDTAPD)
     C                     Z-ADD*ZEROS    ##OSEQ
      *                                                                                       CCG015
      ** Initialise XML increment                                                             CCG015
      *                                                                                       CCG015
     C                     EXSR INIXML                                                        CCG015
      *
     C           EXINZ     TAG
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRPSSR
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRC
     C/SPACE 5
     C/COPY ZSRSRC,ZNCDZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZLCDZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZSDESCZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE1Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE2Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZDAYSZ2
     C/SPACE 5                                                                                CCG015
     C/COPY CGCPYSRC,CGNWEX                                                                   CCG015
** ##GRP/##RDEA
BRANCH0001 BRANCH
BRANCH0002 BRANCH NAM
CUST  0001 CUSTOMER
CUST  0002 CUST_NAME
CUST  0003 CUST_ADR1
CUST  0004 CUST_ADR2
CUST  0005 CUST_ADR3
CUST  0006 CUST_ADR4
CUST  0007 CUST_TOWN                                                      086918
T_ADCD0001 RUNDAT     Date
T_ADCD0002 EVENT_TYPE
T_ADCD0003 EVENT_DATE Date
T_ADCD0004 EVENT_CCY
T_ADCD0005 EVENT_AMNT AmountL
T_ADCD0006 NOMINAL    AmountL
T_ADCD0007 PAY BYUS
T_ADCD0008 PAY BYTHEM
T_ADCD0009 EVENT_EQCY AmountL                                             CEU014
T_ADCD0010 E_EQV_CCY                                                      CEU014
T_ADCD0011 E_EQV_CCYD                                                     CEU014
T_ADCD0012 NOMNL_EQCY AmountL                                             CEU014
T_ADCD0013 N_EQV_CCY                                                      CEU014
T_ADCD0014 N_EQV_CCYD                                                     CEU014
T_SECD0001 SEC_SHTNAM
T_SECD0002 PROC_TYPE
T_SECD0003 SEC_NOMCCY
T_SECD0004 SEC_INVTYP
T_SECD0005 SEC_DESCP1
T_SECD0006 SEC_DESCP2
T_SECD0007 SEC_DESCP3
T_SECD0008 SEC_NXTCDT Date
T_SECD0009 SEC_LSTCDT Date
T_SECD0010 SEC_COUPRT Rate  L7
T_SECD0011 SEC_MATUDT Date
T_SECD0012 SEC_INITDT Date
T_SECD0013 SEC_ISSUDT Date
T_SECD0014 SEC_ISIN                                                       089693
T_SECD0015 SEC_INVNAM                                                                         214731
**  Table of Event Type vs related Print Item Sub Type
PPPART_PAYMT
CPCOUPON
DVDIVIDEND
MPMORT_REPAY
MAMATURITY
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
