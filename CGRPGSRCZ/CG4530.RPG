     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Interest statements extract driver')
      *****************************************************************
      *                                                               *
      *   Midas - Correspondence Manager                              *
      *                                                               *
      *   CG4530 -  - Trading statements extract driver               *
      *                                                               *
      *   Function:   This program identifies  the correspondence     *
      *                items to be produced and calls the extract     *
      *                program (CG4720) for each one. It prepares     *
      *                the environment for the extract  and takes     *
      *                care of audit processing.                      *
      *                                                               *
      *   Called By:  DLC0767 - Report on Time/Call Statement Control *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL012  *CREATE    Date 06Oct03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL012 - Interest Statements for Call and Time Deposits.     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *                                                               *
      *   Indicator usage                                             *
      *   ~~~~~~~~~~~~~~~                                             *
      *                                                               *
      *   90 -- Work indicator -- typically I/O error.                *
      *   91 -- EOF on file CGTCSTL0                                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   Subroutines list                                            *
      *   ~~~~~~~~~~~~~~~~                                            *
      *   SRINIT -- Prepare and initialise.                           *
      *   SRPARN -- Process parent customer                           *
      *   SRCHLD -- Process child customer                            *
      *   SRAUDT -- Produce the audit report.                         *
      *                                                               *
      *   *PSSR  -- Program error handling.                           *
      *   SRFILE -- File error handling.                              *
      *   SRERR  -- Database error handling.                          *
      *                                                               *
      *****************************************************************
     FCGTCSTL0IF  E           K        DISK         KINFSR SRFILE
      ** Time/Call Deposits Statement Driver File
      *
      *-------------------------------------------------------------------
     E/COPY CGCPYSRC,SRERRE
     E                    ##CPY   1   1 80               Copyright text.
      *-------------------------------------------------------------------
     I/SPACE
      *-------------------------------------------------------------------
     I/COPY CGCPYSRC,CGAUDTI
      *
      * Record format of PF/CGUDTAPD for use as a parameter to CGZAUDIT
      *-------------------------------------------------------------------
     I/COPY CGCPYSRC,SRERRI
      *
      * Error-processing fields
      *-------------------------------------------------------------------
     IP3FMT     E DSCGUDTAPD
      *
      ** UDC extracted data
     I              'DL_INTSTMT'          C         W#PTYP
      ** Print Type Constant
      ********************************************************************
     C/EJECT
      ********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           P0CMT   3
     C                     PARM           P0PAR   1
     C                     PARM           P0DAT  18
      *
      ** Add subroutine name to stack:
      *
     C                     Z-ADD1         Q
     C                     MOVEL'@Main@'  @STK,Q
      *
      ** Initialise:
     C                     EXSR SRINIT
      *
      ** Parent Customer
     C           P0PAR     IFEQ 'Y'
     C                     EXSR SRPARN
      *
      ** Child Customer
     C                     ELSE
     C                     EXSR SRCHLD
     C                     ENDIF
      *
      ** Produce the audit report:
      *
     C                     EXSR SRAUDT
      *
     C           EXMAIN    TAG
      *
      ** Remove subroutine name from stack:
      *
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     RETRN
      ********************************************************************
     C/EJECT
      ********************************************************************
      **  SRPARN - Parent Requested                                      *
      ********************************************************************
     C           SRPARN    BEGSR
      *
      ** Add subroutine name to stack:
      *
     C                     ADD  1         Q
     C                     MOVEL'SRPARN'  @STK,Q
      *------------------------------------------------------------------
      *
     C           *LOVAL    SETLLCGTCSTL0
     C                     READ CGTCSTL0                 91
     C           *IN91     DOWEQ*OFF
      *
     C                     MOVELGBRCD     SVBRCD  3
     C           *IN91     DOWEQ*OFF
     C           GBRCD     ANDEQSVBRCD
      *
     C                     MOVEL'PARENT'  P1STYP
     C                     MOVELGBRCD     P1BRCD
     C                     MOVE P0DAT     P1DATE
     C                     CALL 'CG4540'
     C                     PARM *BLANK    P1RTCD  7
     C                     PARM           P1PTYP 10
     C                     PARM           P1STYP 10
     C                     PARM           P1BRCD  3
     C                     PARM           P1DATE 18
     C                     PARM           P1SUPP  1
      *
     C           P1RTCD    IFNE *BLANKS
     C                     MOVEL'CG4540  'W0FILE
     C                     Z-ADD1         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *
      * Count all items requested.
      * If the item was not suppressed, count it:
      *
     C                     ADD  1         W0CNT1
     C           P1SUPP    IFNE 'Y'
     C                     ADD  1         W0CNT2
     C                     ENDIF
      *
     C                     READ CGTCSTL0                 91
     C                     ENDDO
      *
     C                     ENDDO
      *
      *------------------------------------------------------------------
     C           EXPARN    TAG
      * Remove subroutine name from stack:
      *
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      ********************************************************************
     C/EJECT
      ********************************************************************
      **  SRCHLD - Child Requested                                       *
      ********************************************************************
     C           SRCHLD    BEGSR
      *
      ** Add subroutine name to stack:
      *
     C                     ADD  1         Q
     C                     MOVEL'SRCHLD'  @STK,Q
      *------------------------------------------------------------------
      *
     C           *LOVAL    SETLLCGTCSTL0
     C                     READ CGTCSTL0                 91
     C           *IN91     DOWEQ*OFF
      *
     C                     MOVELGBRCD     SVBRCD  3
     C                     MOVELGCUST     SVCUST  6
     C           *IN91     DOWEQ*OFF
     C           GBRCD     ANDEQSVBRCD
     C           GCUST     ANDEQSVCUST
      *
     C                     MOVEL'CHILD '  P1STYP
     C                     MOVELGBRCD     P1BRCD
     C                     MOVELGCUST     P1CUST
     C                     MOVE P0DAT     P1DATE
     C                     CALL 'CG4550'
     C                     PARM *BLANK    P1RTCD  7
     C                     PARM           P1PTYP 10
     C                     PARM           P1STYP 10
     C                     PARM           P1BRCD  3
     C                     PARM           P1CUST  6
     C                     PARM           P1DATE 18
     C                     PARM           P1SUPP  1
      *
     C           P1RTCD    IFNE *BLANKS
     C                     MOVEL'CG4550  'W0FILE
     C                     Z-ADD2         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *
      * Count all items requested.
      * If the item was not suppressed, count it:
      *
     C                     ADD  1         W0CNT1
     C           P1SUPP    IFNE 'Y'
     C                     ADD  1         W0CNT2
     C                     ENDIF
      *
     C                     READ CGTCSTL0                 91
     C                     ENDDO
      *
     C                     ENDDO
      *
      *------------------------------------------------------------------
     C           EXCHLD    TAG
      * Remove subroutine name from stack:
      *
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      ********************************************************************
     C/EJECT
      ********************************************************************
      **  Subroutine SRAUDT prints the audit report.                    **
      ********************************************************************
     C           SRAUDT    BEGSR                           * S R A U D T *
      *
      * Add subroutine name to stack:
      *
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
      *
      * Output the count of records read:
      *
     C                     CLEARI#DTA
     C                     MOVEL'CG4530  'I#REF
     C                     MOVE 'CGD1698' I#TITL
     C                     MOVE 'CGD1699' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'P2ACT
     C                     MOVEL'CGTCSTL0'I#SUB     P
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADDW0CNT1    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM           P2RTN   7
     C                     PARM           P2ACT   8
     C                     PARM           I#DTA
     C                     PARM           P2RSQN  5
      *
      * Skip a line:
      *
     C                     MOVE '*SKIP   'P2ACT
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM           P2RTN
     C                     PARM           P2ACT
     C                     PARM           I#DTA
     C                     PARM           P2RSQN
      *...................................................................
     C/EJECT
      *...................................................................
      * Output the count of records processed:
      *
     C                     CLEARI#DTA
     C                     MOVEL'CG4530  'I#REF
     C                     MOVE 'CGD1698' I#TITL
     C                     MOVE 'CGD1699' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'P2ACT
     C                     MOVEL'CGTCSTL0'I#SUB     P
     C                     MOVEL'CGD1816' I#TEXT
     C                     Z-ADDW0CNT2    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM           P2RTN
     C                     PARM           P2ACT
     C                     PARM           I#DTA
     C                     PARM           P2RSQN
      *
      * Skip a line:
      *
     C                     MOVE '*SKIP   'P2ACT
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM           P2RTN
     C                     PARM           P2ACT
     C                     PARM           I#DTA
     C                     PARM           P2RSQN
     C*...................................................................
     C/EJECT
      *...................................................................
      * Produce the audit report:
      *
     C**                   CALL 'CG9020'
     C**                   PARM *BLANK    P3RTN   7
     C**                   PARM '*AUDIT  'P3ACT   8
     C**                   PARM *BLANKS   P3PATH256
     C**                   PARM           P3FMT
     C**                   PARM 'CGD1698' P3TITL  7
     C**                   PARM 'CGD1699' P3ULIN  7
     C**                   PARM           P0CMT
      *
      * Close the audit print file:
      *
     C                     CLEARI#DTA
     C                     MOVEL'CG4530  'I#REF
     C                     MOVE 'CGD1698' I#TITL
     C                     MOVE 'CGD1699' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   P2RTN
     C                     PARM '*CLOSE  'P2ACT
     C                     PARM           I#DTA
     C                     PARM           P2RSQN
      *
     C           EXAUDT    TAG                             * <<<=== EXAUDT
      *
      * Remove subroutine name from stack:
      *
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      ********************************************************************
     C/EJECT
      ********************************************************************
      **  Subroutine SRINIT handles initialisation requirements.        **
      ********************************************************************
     C           SRINIT    BEGSR                           * S R I N I T *
      *
      ** Add subroutine name to stack:
      *
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
      *
      ** Clear the return code:
      *
     C                     MOVE *BLANKS   P0RTN
      *
      ** Initialise the count fields:
      *
     C                     Z-ADD*ZERO     W0CNT1  50
     C                     Z-ADD*ZERO     W0CNT2  50
      *
      ** Move the copyright statement to *NULL:
      *
     C                     MOVE ##CPY,1   W0NULL 80
      *
      ** Set the print item type
      *
     C                     MOVELW#PTYP    P1PTYP
      *
      ** Remove subroutine name from stack:
      *
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      ********************************************************************
     C/EJECT
      ********************************************************************
     C/COPY CGCPYSRC,SRERRPSSR
      ********************************************************************
     C/EJECT
      ********************************************************************
     C/COPY CGCPYSRC,SRERRC
      ********************************************************************
** ##CPY -- Copyright statement
(c) Finastra International Limited 2003
