     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Fut & Opts Confirmations(Start)- Driver')
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG4599 - Future And Options Confirmations - Driver           *
      *                                                               *
      *  Function:
      *             produced, and for each one calls the Confirmation *
      *             Extract Process.                                  *
      *                                                               *
      *  Called By: mmCnnnn - (program name)                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 111546             Date 26FEB97               *
      *                 CFF004             Date 02OCT96               *
      *                 087274             DATE 09MAY95               *
      *                 S01522             DATE 01JAN95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  111546 - Decimal data error.                                 *
      *  CFF004 - Increase in size of Price Fields,(Recompiled Only)  *
      *  087274 - Correct set up of Print Type/Sub-Type               *
      *  S01522 - User Defined Correspondence                         *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicator usage                                              *
      *  ~~~~~~~~~~~~~~~                                              *
      *                                                               *
      *  90   -    ROLBK indicator                                    *
      *                                                               *
      *                                                               *
      *****************************************************************
      *................................................................
      *
     FCNFRP   IF  E           K        DISK         KINFSR SRFILE
      /EJECT
      *
      **  Copyright array.
     E                    ##CPY   1   1 80
      /COPY CGCPYSRC,SRERRE
      /EJECT
      *
     IDSPARM    E DSCNFRPD
      *                                                                   S03220
      **  External DS for parameters
      *
     ICPYR        DS                                                      S03220
      **  Data Structure for Compilation - Copyright Insertion            S03220
     I                                        1  80 ##CPY                 S03220
      *
     IW0FMT     E DSCGUDTAPD
      **  Record format of PF/CGUDTAPD for use as a parameter
      *
      /COPY CGCPYSRC,CGAUDTI
      /COPY CGCPYSRC,SRERRI
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   SRMAIN - Main processing                                    *
      *   SRINZ  - Initialise Fields on Each Invocation               *
      *   SRINIT - Initial processing - On First Call                 *
      *   SRAUDT - Audit Report Processing                            *
      *   *PSSR  - Exception/Error Handling                           *
      *****************************************************************
      /EJECT
      *****************************************************************
      * Process     :  MAINLINE                                       *
      * Function    :  Mainline processing                            *
      *                                                               *
      * Called by   :  None                                           *
      * Calls       :  SRINIT - Initial Processing - On First Call    *
      *                SRINZ  - Initialise Fields on Each Invocation  *
      *                SRMAIN - Main Processing                       *
      *                SRAUDT - Audit Report Processing               *
      *****************************************************************
      *
      **  Parameter list for current program invocation.
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
      *
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      **  Initialisation processing
     C                     EXSR SRINZ
      *
      **  Detail process
     C                     EXSR SRMAIN
      *
      **  Audit Process
     C                     EXSR SRAUDT
      *
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C                     RETRN
      *****************************************************************
      /EJECT
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CG4600 - Futures & Opt Confirmation Extract    *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
      **  Access the Confirmations driver file .......
      *
     C           *LOVAL    SETLLCNFRP
     C                     READ CNFRP                    55
      *
      **  For each record found ...
      *
     C           *IN55     DOWEQ*OFF                       B1
      *
      **  Increment the count of confirmation records read
      *
     C                     ADD  1         ##SREC  50        1
      *                                                                   087274
      **  Set up the Print Type according to the Put/Call flag            087274
      **  If PCAL = ' ', Instrument is a Future.                          087274
     C           PCAL      IFEQ *BLANKS                                   087274
     C                     MOVEL'FUTURES' ##PTYP 10 P                     087274
     C                     ELSE                                           087274
     C                     MOVEL'OPTIONS' ##PTYP    P                     087274
     C                     ENDIF                                          087274
      *
      **  Set up the parameters and call the Extract program.
      *
     C                     CALL 'CG4600'                    1
     C                     PARM *BLANKS   ##RTCD  7         1
     C                     PARM W0CMT     ##CMT   3         1
     C                     PARM           DSPARM            1
     C                     PARM           ##PTYP                          087274
      *
      **  Error in called program.
     C           ##RTCD    IFNE *BLANK                     B2
     C                     MOVEL##RTCD    W0KEY             2
      *
     C                     EXSR SRAUDT                      2
      *                                                    ***************
     C                     MOVEL'CG4600  'W0FILE           * DB ERROR 01 *
     C                     Z-ADD01        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD            2
     C                     MOVEL'MIDAS  ' W0MSGF            2
     C                     EXSR SRERR                       2
     C                     ENDIF                           E2
      *
      **  COMIT successful transactions.
      *
     C                     COMIT                            1
      *
      **  Read next Confirmation
      *
     C                     READ CNFRP                    55 1
      *
     C                     ENDDO                           E1
      *
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * Subroutine  :  SRINZ                                          *
      * Function    :  Initialise Fields on Program Invocation        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINZ '  @STK,Q
      *
      *................................................................
      *
      **  Open the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4600AU'I#SPLN
     C                     MOVEL'CG4600  'I#REF
     C                     MOVE 'CGD1412' I#TITL
     C                     MOVE 'CGD1413' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*OPEN   'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      *................................................................
      *
     C           EXINZ     TAG
      *
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *................................................................
      *
      **  Initialise work fields
      *
     C                     Z-ADD*ZEROS    ##SREC
      *
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
      *
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * Subroutine  :  SRAUDT                                         *
      * Function    :  Audit Report Processing                        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *                CG9020   - Write Data to UDC Data Files        *
      *****************************************************************
     C           SRAUDT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
      *
      *................................................................
      *
      **  Output the Count of records read
     C                     CLEARI#DTA
     C                     MOVEL'CG4600  'I#REF
     C                     MOVE 'CGD1412' I#TITL
     C                     MOVE 'CGD1413' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE *BLANKS   I#SUB
     C                     MOVEL'CNFRP   'I#SUB
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADD##SREC    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Skip a line.
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Produce the Audit Report.
      *
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD
     C                     PARM '*AUDIT  'W0ACT
     C                     PARM *BLANKS   W0PATH256
     C                     PARM           W0FMT
     C                     PARM 'CGD1412' W0TITL  7
     C                     PARM 'CGD1413' W0ULIN  7
     C                     PARM           W0CMT
      *
      **  Close the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4600  'I#REF
     C                     MOVE 'CGD1412' I#TITL
     C                     MOVE 'CGD1413' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      *................................................................
      *
     C           EXAUDT    TAG
      *
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /COPY CGCPYSRC,SRERRPSSR
      /EJECT
      /COPY CGCPYSRC,SRERRC
** ##CPY  **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
