     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Correspondence Manager Module                        *
     F*                                                               *
     F*  CG4861 - RE INTEREST CAPITALISATION STATEMENT (CM) - Driver  *
     F*                                                               *
     F*  Called By: REC3761                                           *
     F*  Calls    : CG4871                                            *
     F*                                                               *
     F*  (c) Finastra International Limited 2021                      *
     F*                                                               *
     F*  Last Amend No. EMP108  *Create    Date 09Feb21               *
     F*  Prev Amend No. xxxxxx             Date ddmmmyy               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  EMP108 - Retail Interest Capitalisation Statement via CM     *
     F*                                                               *
     F*****************************************************************
     FRECAPML IF  E           K        DISK
     FRECOM   IF  E           K        DISK
     F*****************************************************************
     E**  Copyright array.
     E                    ##CPY   1   1 80
     E/COPY CGCPYSRC,SRERRE
     IDSPARM    E DSRECAPM
     I**  External DS for parameter passed to CG4871
     IW0FMT     E DSCGUDTAPD
     I**  Record format of PF/CGUDTAPD for use as a parameter
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     IRKEY2       DS
     I                                        1  24 RKEY
     I                                        1   3 XBRCA
     I*************************************** 4   90XCNUM                        EMP108
     I                                        4   9 XCNUM                        EMP108
     I                                       10  12 XCCY
     I                                       13  220XACOD
     I                                       23  240XACSQ
     I                                       25  290WDATE
     I                                        1   3 CBRCA2
     I*
     IRUNDT     E DSRUNDAT
      **  Data Area RUNDAT
     I/COPY CGCPYSRC,CGAUDTI
     I/COPY CGCPYSRC,SRERRI
      *
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
     C                     PARM           LANG    2
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *................................................................
      *
      **  Initial processing - Once Only
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
      *
      **  Initialisation processing
     C                     EXSR SRINZ
      *
      **  Main processing
     C                     EXSR SRMAIN
      *
      **  Audit Processing
     C                     EXSR SRAUDT
      *................................................................
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      **  Terminate program
     C                     RETRN
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing - First Call Only           *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  None                                           *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
      *...........................................................
      *
      **  Initialise work fields
     C                     Z-ADD*ZEROS    ##DREC  50
      *
      **  Parameter "Language" for MSGF-override in CG4871 (hardcoded to "GB")
     C                     MOVELLANG      ##LG
      *
      **  Copyright
     C                     MOVEA##CPY     ##BIS  80
      *
     C**  KLIST TO CHAIN ON RECOM
     C*
     C           KLIST0    KLIST
     C                     KFLD           CBRCA
     C                     KFLD           CCNUM
     C                     KFLD           CCCY
     C                     KFLD           CACOD
     C                     KFLD           CACSQ
      *
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      * Subroutine  :  SRINZ                                          *
      * Function    :  Initialise Fields on Program Invocation        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRINZ '  @STK,Q
      *
      *................................................................
      **  Initialise work fields
     C                     Z-ADD*ZEROS    ##DREC
      *
      **  Open the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'REPHS   'I#SPLN
     C                     MOVEL'REU     'I#REF
     C                     MOVE 'CGD3600' I#TITL
     C                     MOVE 'CGD1679' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*OPEN   'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      **  Retrieve the RUNDAT data area.
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      *................................................................
     C           EXINZ     TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
      * Subroutine  :  SRMAIN                                         *
      * Function    :  Main processing                                *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRMAIN    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
      *
      *................................................................
      *
     C                     READ RECAPML                  LR
     C*
     C           *INLR     DOWEQ'0'                        - B1
     C*
     C                     EXSR PRNO
      *
      **  Increment the count of TRADSD records read.
     C                     ADD  1         ##DREC  50
      *
     C                     CALL 'CG4871'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM 'YES'     ##CMT   3
     C                     PARM 'GB'      ##LG    2
     C                     PARM           DSPARM
     C*
     C                     READ RECAPML                  LR
     C                     END                             - E1
     C*
      *................................................................
      *
     C           EXMAIN    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C/SPACE 5
     C********************************************************************
     C** PRNO   SUBROUTINE TO DECIDE IF RECORD TO PRINT OR NO
     C**
     C**     CALLED BY       START
     C**     CALLS           -
     C********************************************************************
     C           PRNO      BEGSR                           **PRNO   BEGSR*
     C*
     C                     MOVE 'N'       PRYES
     C                     MOVE '0'       *IN70
     C                     MOVELCBRCA     XBRCA
     C******************** Z-ADDCCNUM     XCNUM            EMP108
     C                     MOVELCCNUM     XCNUM            EMP108
     C                     MOVELCCCY      XCCY
     C                     Z-ADDCACOD     XACOD
     C                     Z-ADDCACSQ     XACSQ
     C*
     C*  For management or demand print
     C*
     C           CDICI     IFEQ 'M'                        - B1
     C           CCICI     OREQ 'M'
     C           CDICI     OREQ 'D'
     C           CCICI     OREQ 'D'
     C                     MOVE 'Y'       PRYES   1
     C                     END                             - E1
      *
     C           CDICI     IFEQ 'M'                        - B1
     C           CCICI     OREQ 'M'
     C                     MOVE '1'       *IN70
     C                     END                             - E1
     C*
     C*  Access RECOM for Commission details
     C*
     C           KLIST0    CHAINRECOM                99
     C           *IN99     IFEQ '1'                        *****************
     C                     Z-ADD21        DBASE            ** DB ERROR 21 **
     C                     MOVE 'RECOM  ' DBFILE           *****************
     C                     MOVELRKEY      DBKEY
     C                     END
      *
     C           CDICI     IFEQ 'C'
     C           CDICI     OREQ ' '
     C                     Z-ADD*ZERO     FXDC
     C                     END
      *
     C           CDICA     SUB  CCDIC     CDICA
     C           ACCC      SUB  CACCC     ACCC
     C           CHDBC     SUB  CCHDBC    CHDBC
     C           CMBC      SUB  CCMBC     CMBC
     C*
     C*  IF CAPITALISATION OCCURS AT LEAST ONE AMOUNT MUST BE DIFFERENT
     C*  OF ZERO TO BE PRINTED.
     C*
     C           CDICI     IFNE ' '                        - B1
     C           CDAIC     IFNE *ZERO                      - B2
     C           CPMADI    ORNE *ZERO
     C           CTMADI    ORNE *ZERO
     C           CDMINT    ORNE *ZERO
     C           CSGADI    ORNE *ZERO
     C           FXDC      ORNE *ZERO
     C           ACCC      ORNE *ZERO
     C           CHDBC     ORNE *ZERO
     C           CMBC      ORNE *ZERO
     C           CDICA     ORNE *ZERO
     C                     MOVE 'Y'       PRYES   1
     C                     END                             - E2
     C                     END                             - E1
     C*
     C           CCICI     IFNE ' '                        - B1
     C           CCAIC     IFNE *ZERO
     C           CPMACI    ORNE *ZERO
     C           CTMACI    ORNE *ZERO
     C           CCMINT    ORNE *ZERO
     C           CSGACI    ORNE *ZERO
     C           CCHGP     ORNE *ZERO
     C                     MOVE 'Y'       PRYES   1
     C                     END                             - E2
     C                     END                             - E1
      *
     C** ALWAYS PRINTED
     C*
     C                     MOVE 'Y'       PRYES   1
      *
     C                     ENDSR
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRAUDT                                         *
      * Function    :  Audit Report Processing                        *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :  CGZAUDIT - Audit Processing                    *
      *                CG9020   - Write Data to UDC Data Files - Audit*
      *****************************************************************
     C           SRAUDT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
      *
      *................................................................
      **  Output the Count of records read
     C                     CLEARI#DTA
     C                     MOVEL'CG4862  'I#REF
     C                     MOVE 'CGD3600' I#TITL
     C                     MOVE 'CGD3601' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE *BLANKS   I#SUB
     C                     MOVEL'RECAPM  'I#SUB
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADD##DREC    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Skip a line.
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
      *
      **  Produce the Audit Report.
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD  7
     C                     PARM '*AUDIT  'W0ACT
     C                     PARM *BLANKS   W0PATH256
     C                     PARM           W0FMT
     C                     PARM 'CGD3600' W0TITL  7
     C                     PARM 'CGD3601' W0ULIN  7
     C                     PARM           W0CMT
      *
      **  Close the Audit Print File.
     C                     CLEARI#DTA
     C                     MOVEL'CG4862  'I#REF
     C                     MOVE 'CGD3600' I#TITL
     C                     MOVE 'CGD3601' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
      *
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
      *
      *................................................................
     C           EXAUDT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRPSSR
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRC
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited  2021
