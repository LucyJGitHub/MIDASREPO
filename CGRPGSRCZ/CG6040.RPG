     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Lending Rate Change Advices - Driver')        *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG6040 - Lending Rate Change Advices - Driver                *
     F*                                                               *
     F*  Function:  Lending Rate Change driver will read LF/INTSR     *
     F*             which carries the details of loan rate changes    *
     F*             occuring today.  Only records with a bill/telex   *
     F*             indicator of 'B' will be processed.  CG6045 will  *
     F*             be called passing the rate change details from    *
     F*             RATESRT.  The UDC data extracted will be for      *
     F*             group set l08RC.                                  *
     F*                                                               *
     F*  Called By: CGC6005 - Interest Rate Change Notices.           *
     F*                                                               *
     F*  Calls:     CG6045  - Lending Rate Change Advices             *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      *                 AR873891           Date 09Dec11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 200744             Date 02Jan02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCG008 *CREATE     Date 30Oct95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  AR873891 - Rate Change Advice is not generated during Base   *
      *             Rate change.                                      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  200744 - Suppress generation of rate change advise for       *
      *           Rollovers.                                          *
     F*  CCG008 - User Defined Correspondence - Lending               *
     F*                                                               *
     F*****************************************************************
     FINTSR   UF  E           K        DISK
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     F            RATESHHF                          KIGNORE
     F            RATESZXF                          KIGNORE
     F/COPY WNCPYSRC,CG6040F001
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N  C T I O N    O F    I N D I C A T O R S               *
     F*                                                               *
     F*  50   -    EOF indicator for INTSR                            *
     F*  90   -    General work indicator                             *
     F*                                                               *
     F*****************************************************************
      /TITLE SUBROUTINE INDEX
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E    I N D E X                             *
     F*                                                               *
     F*  Main routine                                                 *
     F*  SRINIT  -  Initial Processing                                *
     F*  SRINZF  -  Initialise Fields on Program Invocation           *
     F*  SRMAIN  -  Main Processing                                   *
     F*  SRAUDT  -  Audit Report Processing                           *
     F*  *PSSR   -  Error Subroutine                                  *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*****************************************************************
     E*
     E**  Copyright array.
     E                    CPY@    1   1 80
     E/COPY CGCPYSRC,SRERRE
     I*
     IDSPARM    E DSRATESRT
     I**  External DS for parameter passed to CG6045
     I*
     ISDBANK    E DSSDBANKPD
     I**  External DS for Bank details
     I*
     IDSSDY     E DSDSSDY
     I**  Second DS for access programs, long data structure
     I*
     IW0FMT     E DSCGUDTAPD
     I**  Record format of PF/CGUDTAPD for use as a parameter
     I/COPY CGCPYSRC,CGAUDTI
     I/COPY CGCPYSRC,SRERRI
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  Process     :  MAINLINE                                      *
     C*  Function    :  Mainline processing.                          *
     C*                                                               *
     C*  Called by   :  None                                          *
     C*  Calls       :  SRINIT - Initial Processing - On First Call   *
     C*                 SRINZF - Initialise Fields on Each Invocation *
     C*                 SRMAIN - Main Processing                      *
     C*                 SRAUDT - Audit Report Processing              *
     C*                                                               *
     C*****************************************************************
     C*
     C**  Parameter list for current program invocation.
     C*
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
     C*
     C**  Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
     C*
     C**  Initial processing - Once Only
     C*
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     ENDIF
     C*
     C**  Initialisation processing
     C*
     C                     EXSR SRINZF
     C*
     C**  Main processing
     C*
     C                     EXSR SRMAIN
     C*
     C**  Audit Processing
     C*
     C                     EXSR SRAUDT
     C*
     C**  Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C**  Terminate program
     C*
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  Process     :  SRMAIN                                         *
     C*  Function    :  Main Processing                               *
     C*                                                               *
     C*  Called by   :  Mainline                                      *
     C*  Calls       :  CG6045 - Lending Rate Change Advices.         *
     C*                                                               *
     C*****************************************************************
     C           SRMAIN    BEGSR
     C*
     C**  Set up subroutine stack name
     C*
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
     C*
     C** Read through INTSR file.
     C*
     C           *LOVAL    SETLLRATESRTF
     C                     READ INTSR                    50
     C*
     C** Read until End of File
     C*
     C           *IN50     DOWEQ'0'
     C*
     C***If Bill/Telex indicator = 'B' then
     C** process record.
     C*
     C           BTIN      IFEQ 'B'
     C/COPY WNCPYSRC,CG6040C004
     C*
      **  Do not generate 'LOAN RATCH' for Rollovers.                                         200744
      *                                                                                       200744
     C********** AMTP      IFEQ 'RO'                                                 200744 AR873891
     C           AMTP      IFNE 'RO'                                                        AR873891
     C*                                                                                       200744
     C**  Increment the count of INTSR records read.
     C                     ADD  1         ##DREC  50
     C/COPY WNCPYSRC,CG6040C001
     C*
     C** Produce Confirmation
     C*
     C                     CALL 'CG6045'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM W0CMT     ##CMT   3
     C                     PARM *BLANK    ##COPD  1
     C                     PARM           DSPARM
     C*
     C**  Error in called program.
     C*
     C           ##RTCD    IFNE *BLANK
     C*
     C                     EXSR SRAUDT
     C*
     C** Database error
     C*
     C                     MOVEL'CG6045  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 02 *
     C                     Z-ADD02        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
     C*
     C** Confirmation Produced?
     C*
     C/COPY WNCPYSRC,CG6040C002
     C           ##COPD    IFEQ 'Y'
     C*
     C** Update Confirmation indicator on INTSR
     C*
     C                     MOVEL' '       BTIN
     C                     UPDATRATESRTF
     C*
     C                     END                             * ##COPD= 'Y'
     C*
     C                     END                             * AMTP=Rollover                    200744
     C                     END                             * VDAT=Today/BTIN='B'
     C*
     C** Commit
     C*
     C                     COMIT
     C*
     C** Read next record from INTSR
     C*
     C                     READ INTSR                    50
     C                     END                             * *IN50 DOWEQ '0'
     C*
     C           EXMAIN    TAG
     C*
     C**  Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  Process     :  SRINZF                                        *
     C*  Function    :  Initialise Fields on Program Invocation       *
     C*                                                               *
     C*  Called by   :  Mainline                                      *
     C*  Calls       :  CGZAUDIT - Audit Processing                   *
     C*                                                               *
     C*****************************************************************
     C           SRINZF    BEGSR
     C*
     C**  Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRINZF'  @STK,Q
     C*
     C**  Open the Audit Print File.
     C*
     C                     CLEARI#DTA
     C                     MOVEL'CG6040AU'I#SPLN
     C                     MOVEL'CG6040  'I#REF
     C                     MOVE 'CGD2533' I#TITL
     C                     MOVE 'CGD2534' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*OPEN   'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
     C*
     C           EXINZF    TAG
     C*
     C**  Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  Process     :  SRINIT                                        *
     C*  Function    :  Initial Processing - First Call Only          *
     C*                                                               *
     C*  Called by   :  Mainline                                      *
     C*  Calls       :  None                                          *
     C*                                                               *
     C*****************************************************************
     C           SRINIT    BEGSR
     C*
     C**  Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C** Call access program for Bank details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM '*FIRST  '##OPTN  7
     C           SDBANK    PARM SDBANK    DSSDY
     C*
     C           ##RTCD    IFNE *BLANK
     C*
     C** Database error
     C*
     C                     MOVEL'AOBANKR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD01        W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
     C*
     C**  Initialise work fields
     C*
     C                     Z-ADD*ZEROS    ##DREC
     C*
     C**  Copyright
     C*
     C                     MOVEACPY@      ACT@   80
     C/COPY WNCPYSRC,CG6040C003
     C*
     C           EXINIT    TAG
     C*
     C**  Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  Process    :  SRAUDT                                         *
     C*  Function   :  Audit Report Processing                        *
     C*                                                               *
     C*  Called by   :  Mainline                                      *
     C*  Calls       :  CGZAUDIT - Audit Processing                   *
     C*                 CG9020   - Write Data to UDC Data Files-Audit *
     C*                                                               *
     C*****************************************************************
     C           SRAUDT    BEGSR
     C*
     C**  Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
     C*
     C**  Output the Count of records read
     C*
     C                     CLEARI#DTA
     C                     MOVEL'CG6040  'I#REF
     C                     MOVE 'CGD2533' I#TITL
     C                     MOVE 'CGD2534' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE *BLANKS   I#SUB
     C                     MOVEL'INTSR   'I#SUB
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADD##DREC    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
     C*
     C**  Skip a line.
     C*
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
     C*
     C**  Produce the Audit Report.
     C*
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD
     C                     PARM '*AUDIT  'W0ACT
     C                     PARM *BLANKS   W0PATH256
     C                     PARM           W0FMT
     C                     PARM 'CGD2533' W0TITL  7
     C                     PARM 'CGD2534' W0ULIN  7
     C                     PARM           W0CMT
     C*
     C**  Close the Audit Print File.
     C*
     C                     CLEARI#DTA
     C                     MOVEL'CG6040  'I#REF
     C                     MOVE 'CGD2533' I#TITL
     C                     MOVE 'CGD2534' I#TUL
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
     C*
     C           EXAUDT    TAG
     C*
     C**  Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C/COPY CGCPYSRC,SRERRPSSR
     C/EJECT
     C/COPY CGCPYSRC,SRERRC
     C/EJECT
** CPY@  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
