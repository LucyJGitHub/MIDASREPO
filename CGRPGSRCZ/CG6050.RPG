     H        1
     F*****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Lending Past Due Notices - Driver')           *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG6050 - Lending Past Due Notices - Driver                   *
     F*                                                               *
     F*  Function:  This program will read through file PDUEDU which  *
     F*  ( COB )    carries details of loan past due repayments.      *
     F*                                                               *
     F*  Called By: CGC6006 - Past Due Notices, Report and Foreign    *
     F*                       Credits.                                *
     F*                                                               *
     F*  Calls:     CG6055  - Lending Past Due Notices                *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 166243             Date 05Apr00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCG008 *CREATE     Date 30Oct95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  166243 - Replace messages CGD2541 and CGD2542 with CGD2535   *
     F*           CGD2536 respectively, as CGD2541 and CGD2542 do     *
     F*           not exist in CGUSRMSG.                              *
     F*  CCG008 - User Defined Correspondence - Lending               *
     F*                                                               *
     F*****************************************************************
     FPSTCU   UF  E           K        DISK         KCOMIT
     F                                              KINFSR SRFILE
     F            PDUEHHF                           KIGNORE
     F            PDUEZXF                           KIGNORE
     F/COPY WNCPYSRC,CG6050F001
     F/TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N    O F    I N D I C A T O R S                *
     F*                                                               *
     F*  50    -    End of file PSTCU                                 *
     F*                                                               *
     F*****************************************************************
     F/TITLE SUBROUTINE INDEX
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E    I N D E X                             *
     F*                                                               *
     F*  Main routine                                                 *
     F*  SRINIT  -  Initial Processing - On First Call                *
     F*  SRINZ   -  Initialise Fields on Each Invocation              *
     F*  SRMAIN  -  Main Processing                                   *
     F*  SRAUDT  -  Audit Report Processing                           *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*****************************************************************
     E                    ##CPY   1   1 80
     E/COPY CGCPYSRC,SRERRE
     I/EJECT
     I*
     IDSPARM    E DSPDUEDU
     I** External DS for parameter passed to CG6055
     I*
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I*
     IDSFDY     E DSDSFDY
     I**  First DS for access programs, short data structure
     I*
     I*
     IW0FMT     E DSCGUDTAPD
     I** Record format of PF/CGUDTAPD for use as a parameter
     I*
     ISDBANK    E DSSDBANKPD
     I** Data Structure for Bank Details Access Program
     I*
     I/COPY CGCPYSRC,CGAUDTI
     I/COPY CGCPYSRC,SRERRI
     C/EJECT
     C*****************************************************************
     C* Process     :  MAINLINE                                       *
     C* Function    :  Mainline processing                            *
     C*                                                               *
     C* Called by   :  None                                           *
     C* Calls       :  SRINIT - Initial Processing - On First Call    *
     C*                SRINZ  - Initialise Fields on Each Invocation  *
     C*                SRMAIN - Main Processing                       *
     C*                SRAUDT - Audit Report Processing               *
     C*****************************************************************
     C*
     C** Parameter list for current program invocation.
     C*
     C           *ENTRY    PLIST
     C                     PARM           W0RTN
     C                     PARM           W0CMT   3
     C*
     C** Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
     C*
     C**  Initial processing - Once Only
     C*
     C           ##INIT    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     MOVE 'Y'       ##INIT  1
     C                     END
     C*
     C** Initialisation processing
     C*
     C                     EXSR SRINZ
     C*
     C** Main processing
     C*
     C                     EXSR SRMAIN
     C*
     C** Audit Processing
     C*
     C                     EXSR SRAUDT
     C*
     C** Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C** Terminate program
     C*
     C                     RETRN
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C* Subroutine  :  SRMAIN                                         *
     C* Function    :  Main processing                                *
     C*                                                               *
     C* Called by   :  Mainline                                       *
     C* Calls       :  CG6055 - Lending Past Due Notices              *
     C*****************************************************************
     C           SRMAIN    BEGSR
     C*
     C** Set up subroutine stack name
     C*
     C                     ADD  1         Q       50
     C                     MOVEL'SRMAIN'  @STK,Q
     C*
     C** Read through PSTCU
     C*
     C                     READ PSTCU                    50
     C*
     C** Read until End of File
     C*
     C                     MOVE *OFF      *IN30
     C           PPDU      IFEQ 0
     C           INPD      ANDEQ0
     C                     MOVE *ON       *IN30
     C                     ENDIF
     C*
     C           *IN50     DOWEQ'0'                        B1
     C*
     C** If Confirmation Requested and Past Due amounts not nil,
     C*
     C           PDRQ      IFEQ 'Y'
     C           *IN30     ANDEQ*OFF
     C/COPY WNCPYSRC,CG6050C004
     C*
     C** Increment the count of PSTCU records read
     C*
     C                     ADD  1         ##DREC  50
     C/COPY WNCPYSRC,CG6050C001
     C*
     C** Call CG6055, Lending Past Due Notices
     C*
     C                     CALL 'CG6055'
     C                     PARM *BLANKS   ##RTCD  7
     C                     PARM W0CMT     ##CMT   3
     C                     PARM           ##COPD  1
     C                     PARM           DSPARM
     C*
     C** Error in called program.
     C*
     C           ##RTCD    IFNE *BLANK                     B3
     C*
     C                     EXSR SRAUDT
     C*
     C** Database error
     C*
     C                     MOVEL'CG6055  'W0FILE           ***************
     C                     MOVEL##RTCD    W0KEY            * DB ERROR 01 *
     C                     Z-ADD01        W0ERNB           ***************
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END                             E3
     C*
     C** If past due notices are produced by CG6055, set the past due
     C** notices indicator on PSTCU to 'N'
     C*
     C/COPY WNCPYSRC,CG6050C002
     C           ##COPD    IFEQ 'Y'                        B3
     C                     MOVEL'N'       PDRQ
     C                     UPDATPDUEDUF
     C                     END                             E3
     C*
     C                     END                             E2
     C*
     C** Commit
     C*
     C                     COMIT
     C*
     C                     READ PSTCU                    50
     C*
     C                     END                             E1
     C*
     C           EXMAIN    TAG
     C*
     C** Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C* Subroutine  :  SRINZ                                          *
     C* Function    :  Initialise Fields on Program Invocation        *
     C*                                                               *
     C* Called by   :  Mainline                                       *
     C* Calls       :  CGZAUDIT - Audit Processing                    *
     C*****************************************************************
     C           SRINZ     BEGSR
     C*
     C** Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRINZ '  @STK,Q
     C*
     C** Open the Audit Print File
     C*
     C                     CLEARI#DTA
     C                     MOVEL'CG6050AU'I#SPLN
     C                     MOVEL'CG6050  'I#REF
     C**********           MOVE 'CGD2541' I#TITL                          166243
     C**********           MOVE 'CGD2542' I#TUL                           166243
     C                     MOVE 'CGD2535' I#TITL                          166243
     C                     MOVE 'CGD2536' I#TUL                           166243
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*OPEN   'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
     C*
     C           EXINZ     TAG
     C*
     C** Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C* Subroutine  :  SRINIT                                         *
     C* Function    :  Initial processing - First Call Only           *
     C*                                                               *
     C* Called by   :  Mainline                                       *
     C* Calls       :  None                                           *
     C*****************************************************************
     C           SRINIT    BEGSR
     C*
     C**  Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C**  Initialise work fields
     C*
     C                     Z-ADD*ZEROS    ##DREC
     C*
     C** Set up copyright parameter
     C*
     C                     MOVEA##CPY     ##MKI  80
     C*
     C** Access bank details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           ##RTCD    IFNE *BLANK
     C*
     C** Database error
     C*
     C                     MOVEL'AOBANKR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD01        W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
     C/COPY WNCPYSRC,CG6050C003
     C*
     C           EXINIT    TAG
     C*
     C**  Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C* Subroutine  :  SRAUDT                                         *
     C* Function    :  Audit Report Processing                        *
     C*                                                               *
     C* Called by   :  Mainline                                       *
     C* Calls       :  CGZAUDIT - Audit Processing                    *
     C*                CG9020   - Write Data to UDC Data Files - Audit*
     C*****************************************************************
     C           SRAUDT    BEGSR
     C*
     C** Set up subroutine stack name
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRAUDT'  @STK,Q
     C*
     C** Output the Count of records read
     C*
     C                     CLEARI#DTA
     C                     MOVEL'CG6050  'I#REF
     C**********           MOVE 'CGD2541' I#TITL                          166243
     C**********           MOVE 'CGD2542' I#TUL                           166243
     C                     MOVE 'CGD2535' I#TITL                          166243
     C                     MOVE 'CGD2536' I#TUL                           166243
     C                     MOVEL'CGUSRMSG'I#FILE
     C                     MOVE '*LINE   'W1ACT
     C                     MOVE *BLANKS   I#SUB
     C                     MOVEL'PSTCU   'I#SUB
     C                     MOVEL'CAD1021' I#TEXT
     C                     Z-ADD##DREC    I#QTY
     C                     Z-ADD*ZERO     I#DECS
     C                     MOVE '1'       I#EDIT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
     C*
     C** Skip a line.
     C*
     C                     MOVE '*SKIP   'W1ACT
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM           W1RTN   7
     C                     PARM           W1ACT   8
     C                     PARM           I#DTA
     C                     PARM           W1RSQN  5
     C*
     C** Produce the Audit Report.
     C*
     C                     CALL 'CG9020'
     C                     PARM *BLANK    ##RTCD
     C                     PARM '*AUDIT  'W0ACT
     C                     PARM *BLANKS   W0PATH256
     C                     PARM           W0FMT
     C**********           PARM 'CGD2541' W0TITL  7                       166243
     C**********           PARM 'CGD2542' W0ULIN  7                       166243
     C                     PARM 'CGD2535' W0TITL  7                       166243
     C                     PARM 'CGD2536' W0ULIN  7                       166243
     C                     PARM           W0CMT
     C*
     C** Close the Audit Print File.
     C*
     C                     CLEARI#DTA
     C                     MOVEL'CG6050  'I#REF
     C**********           MOVE 'CGD2541' I#TITL                          166243
     C**********           MOVE 'CGD2542' I#TUL                           166243
     C                     MOVE 'CGD2535' I#TITL                          166243
     C                     MOVE 'CGD2536' I#TUL                           166243
     C                     MOVEL'CGUSRMSG'I#FILE
     C*
     C                     CALL 'CGZAUDIT'
     C                     PARM *BLANKS   ##RTCD
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA
     C                     PARM           W0RSQN  5
     C*
     C           EXAUDT    TAG
     C*
     C** Unwind subroutine stack name
     C*
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C/COPY CGCPYSRC,SRERRPSSR
     C/EJECT
     C/COPY CGCPYSRC,SRERRC
     C/EJECT
     C********************************************************************
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
