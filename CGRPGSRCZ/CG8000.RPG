     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Generate RDE data for a group set.')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                  *
     F*                                                               *
     F*  CG8000 - Place RDE data in QTEMP                             *
     F*                                                               *
     F*  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
     F*  (phase(s))                                                   *
     F*                                                               *
     F*  Called By: mmCnnnn - (program name)                          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 083679             Date 15Mar95               *
      *                 S01522             Date 15Mar95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  083679 - Duplicate CGW1RDE2                                  *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     FCGLAYOL1IF  E           K        DISK
     F                                              KINFSR SRFILE
     F*-------------------------------------------------------------------
     E/EJECT
     E*
     E*  Array containing Copyright statement
     E*
     E                    CPY@    1   1 80
     E/COPY CGCPYSRC,SRERRE
     E*
     E*  Copysource for error processing
     E*
      /EJECT
     I/COPY CGCPYSRC,SRERRI
     I*
     I*  End of Program Error Processing copysource
     I*
     I*
     IRUNDTA    E DSRUNDAT
     I*
     I* Get Rundate - Rundate  *
     I*
     IMMODDS    E DSSDMMODPD
     I*
     I* Modules Data Structure *
     I*
     IDSFDY     E DSDSFDY                                                 S01194
      *
     ICGLDA     E DSCGLDA                                                 S01194
      *
     I            DS
     I                                        1   40L1
     I                                        5   80L2
     I                                        9  120L3
     I                                       13  160L4
     I                                       17  200L5
      *
     I            DS
     I                                        1   40S1
     I                                        5   80S2
     I                                        9  120S3
     I                                       13  160S4
     I                                       17  200S5
      *
     I            DS
     I                                        1   40Z1
     I                                        5   80Z2
     I                                        9  120Z3
     I                                       13  160Z4
     I                                       17  200Z5
     I*...................................................................
     I* Message translation fields:
     I*
     IDSMTR       DS
     I                                        1 132 #MSDTA
     I                                      133 264 #MSTX1
     I#MSTX2      DS
     I                                        1 256 #MSTXA
     I                                      257 512 #MSTXB
     I*...................................................................
      *
     I              'CGWPRDE1  '          C         WKF1
     I              'CGWPRDE2  '          C         WKF2
     I              'CGW1RDE2  '          C         WKF3                  083679
      *
     I              '*LIBL'               C         LIBL
      *
     I              'OVRDBF FILE('        C         CMDS11
     I              ') TOFILE(QTEMP/'     C         CMDS12
     I              ')'                   C         CMDS13
      *
     I              'OVRDBF FILE('        C         CMDS21
     I              ') MBR('              C         CMDS22
     I              ')'                   C         CMDS23
      /EJECT
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      * Entry parameters
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
      *
     C           PL1020    PLIST
     C                     PARM *BLANKS   P@RTCD  7
     C                     PARM '*RDEDATA'P@ACTN  8
     C                     PARM #@GRPS    P@FGRP
     C                     PARM *BLANKS   P@PATH240
      *
     C           *LIKE     DEFN LYGRPS    P@FGRP
      *
     C           PL8301    PLIST
     C                     PARM *BLANKS   P@RTCD  7
      *
     C           *NAMVAR   DEFN *LDA      CGLDA
      *
      *  Reset return code
      *
     C                     MOVEL*BLANKS   P0RTN
      *
     C                     IN   CGLDA
     C           #@LAYO    CHAIN@LAYOL1              90
     C  N90                MOVE LYGRPS    #@GRPS
     C                     MOVEL'CG8100'  #@NEXT    P
     C                     MOVEL'CG8100'  #@LAST    P
     C                     OUT  CGLDA
      *
      *  Initialise layout data
      *
     C                     EXSR SRLAYI
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *  Return to calling program
      *
     C                     RETRN
     C/EJECT
     C           *INZSR    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'*INZSR'  @STK,Q
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      *  Get Rundate information
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
     C*
     C* Get modules information
     C*
     C                     CALL 'AOMMODR0'             9090
     C                     PARM *BLANKS   P@RTCD  7        B:Return code
     C                     PARM '*FIRST ' P@OPTN  7        I:Option
     C           MMODDS    PARM *BLANKS   DSFDY            O:Module Flg
      *
      *  If return with an error (LR seton in called program) then
      *  process for database error.
      *
     C           *IN90     IFEQ '1'
     C           P@RTCD    OREQ '*ERROR*'
     C                     MOVEL'AOMMODR0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD1         W0ERNB           * DB ERROR 1. *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
     C*
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRLAYI   : Initialise Layout Caches (eg. QTEMP)              *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *  OFFSET   : 100                                               *
      *                                                               *
      *****************************************************************
     C           SRLAYI    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRLAYI'  @STK,Q
     C*
     C* Show "Initialising layout caches (1)":
     C*
     C                     CALL 'CG8700'
     C                     PARM 'CGD1624' #STSID  7
     C*
     C                     CALL 'CGC3003'              9090
     C                     PARM *BLANKS   P@RTCD  7        B:Return code
     C                     PARM WKF1      P@WRKF 10                    e
      *
      *  If return with an error (LR seton in called program) then
      *  process for database error.
      *
     C           *IN90     IFEQ '1'
     C           P@RTCD    OREQ '*ERROR*'
     C                     MOVEL'CGC3003' W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD2         W0ERNB           * DB ERROR 2. *
     C                     MOVEL'PGI0002' W0MSGD           ***************
     C                     MOVEL'CGUSRMSG'W0MSGF
     C                     EXSR SRERR
     C                     END
     C*
     C                     CLEARCMDSTR
     C           CMDS11    CAT  WKF1:0    CMDSTR
     C           CMDSTR    CAT  CMDS12:0  CMDSTR
     C           CMDSTR    CAT  WKF1:0    CMDSTR
     C           CMDSTR    CAT  CMDS13:0  CMDSTR
     C*
     C                     CALL 'QCMDEXC'
     C                     PARM           CMDSTR256
     C                     PARM 256       CMDLEN 155
     C*
     C* Show "Initialising layout caches (2)":
     C*
     C                     CALL 'CG8700'
     C                     PARM 'CGD1625' #STSID
     C*
     C                     CALL 'CGC3003'              9090
     C                     PARM *BLANKS   P@RTCD  7        B:Return code
     C                     PARM WKF2      P@WRKF 10                    e
      *
      *  If return with an error (LR seton in called program) then
      *  process for database error.
      *
     C           *IN90     IFEQ '1'
     C           P@RTCD    OREQ '*ERROR*'
     C                     MOVEL'CGC3003' W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD3         W0ERNB           * DB ERROR 3. *
     C                     MOVEL'PGI0002' W0MSGD           ***************
     C                     MOVEL'CGUSRMSG'W0MSGF
     C                     EXSR SRERR
     C                     END
     C*
     C                     CLEARCMDSTR
     C           CMDS11    CAT  WKF2:0    CMDSTR
     C           CMDSTR    CAT  CMDS12:0  CMDSTR
     C           CMDSTR    CAT  WKF2:0    CMDSTR
     C           CMDSTR    CAT  CMDS13:0  CMDSTR
     C*
     C                     CALL 'QCMDEXC'
     C                     PARM           CMDSTR256
     C                     PARM 256       CMDLEN 155
      *                                                                   083679
      * Duplicate CGW1RDE2                                                083679
      *                                                                   083679
     C                     CALL 'CGC3003'              9090               083679
     C                     PARM *BLANKS   P@RTCD  7        B:Return code  083679
     C                     PARM WKF3      P@WRKF 10                    e  083679
      *                                                                   083679
      *  If return with an error (LR seton in called program) then        083679
      *  process for database error.                                      083679
      *                                                                   083679
     C           *IN90     IFEQ '1'                                       083679
     C           P@RTCD    OREQ '*ERROR*'                                 083679
     C                     MOVEL'CGC3003' W0FILE                          083679
     C                     MOVEL'*CALL'   W0KEY            ***************083679
     C                     Z-ADD4         W0ERNB           * DB ERROR 4. *083679
     C                     MOVEL'PGI0002' W0MSGD           ***************083679
     C                     MOVEL'CGUSRMSG'W0MSGF                          083679
     C                     EXSR SRERR                                     083679
     C                     END                                            083679
     C*                                                                   083679
     C                     CLEARCMDSTR                                    083679
     C           CMDS11    CAT  WKF3:0    CMDSTR                          083679
     C           CMDSTR    CAT  CMDS12:0  CMDSTR                          083679
     C           CMDSTR    CAT  WKF3:0    CMDSTR                          083679
     C           CMDSTR    CAT  CMDS13:0  CMDSTR                          083679
     C*                                                                   083679
     C                     CALL 'QCMDEXC'                                 083679
     C                     PARM           CMDSTR256                       083679
     C                     PARM 256       CMDLEN 155                      083679
      *
      * Show "Loading group set data":
      *
     C                     CALL 'CG8700'
     C                     PARM 'CGD1626' #STSID
      *
      *  Load Group-Set data into work-files
      *
     C                     CALL 'CGC1020' PL1020
      *
      * Show "Initialising Conditions work-files"
      *
     C                     CALL 'CG8700'
     C                     PARM 'CGD1764' #STSID
      *
      *  Clear work-files used by CG8300
      *
     C                     CALL 'CG8301'  PL8301
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
     C********************************************************************
      * File and Program Error Processing
      *
     C/COPY CGCPYSRC,SRERRC
      *
     E/COPY CGCPYSRC,SRERRPSSR
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
