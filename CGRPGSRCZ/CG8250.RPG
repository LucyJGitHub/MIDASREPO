     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Changes confirmation request.')
/*OVRF*: OVRDBF (File in program) (file on system)                  : *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                  *
     F*                                                               *
     F*  CG8250  - - - - Changes confirmation request.                *
     F*                                                               *
     F*  Function:       This program displays a screen which asks    *
     F*                   the user to confirm a change (update,       *
     F*                   deletion, or whatever). If F5 is pressed,   *
     F*                   a COMIT is executed and/or a value of "Y"   *
     F*                   is returned in parameter P0RPLY; if F12     *
     F*                   is pressed, a ROLBK is executed, and/or     *
     F*                   (NO) is returned.                           *
     F*                                                               *
     F*  Note:           Parameter P0MSG is used to define the        *
     F*                   message to be displayed; if this value      *
     F*                   is blank, message CGD1616 is shown.         *
     F*                                                               *
     F*---------------------------------------------------------------*
     F/EJECT
     F*---------------------------------------------------------------*
     F*  Notes on parameter P0RPLY (incoming)                         *   084478
     F*  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~                         *   084478
     F*  If parameter P0RPLY contains a value of "C" (commit) on      *   084478
     F*   invocation, the user prompt is omitted and the commit       *   084478
     F*   is executed.                                                *   084478
     F*                                                               *   084478
     F*  If parameter P0RPLY contains a value of "R" (rollback) on    *   084478
     F*   invocation, the user prompt is omitted and the rollback     *   084478
     F*   is executed.                                                *   084478
     F*                                                               *   084478
     F*  If parameter P0RPLY contains a value of "Q" (question) on    *   084478
     F*   invocation, the user prompt is displayed, but neither       *   084478
     F*   the commit nor the rollback is executed.                    *   084478
     F*                                                               *   084478
     F*  The purpose of these special values is to allow the          *   084478
     F*   calling program to provide final updates before the         *   084478
     F*   commit or rollback (as appropriate), or to execute          *   084478
     F*   the commit or rollback unconditionally.                     *   084478
     F*                                                               *   084478
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  Called By: mmCnnnn - (program name)                          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 084478             Date 09Mar95               *
      *                 S01522             Date 01Jan95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  084478 - Provide centralised commitment control.             *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*  05 -- F5 (Confirm updates).                                  *
     F*  12 -- F12 (Previous / cancel).                               *
     F*  29 -- Valid command key.                                     *
     F*  90 -- Error detected.                                        *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Subroutine usage                                             *
     F*  ~~~~~~~~~~~~~~~~                                             *
     F*  SRWARN -- Show a reminder of unconfirmed changes.            *
     F*  SRCONF -- Handle F5 (confirm changes).                       *
     F*  SRCAN  -- Handle F12 (cancel changes).                       *
     F*  SRINIT -- Provide initialisation and definitions.            *
     F*                                                               *
     F*  Copied-in routines:                                          *
     F*                                                               *
     F*  *PSSR  -- Program error routine.                             *
     F*  SRFILE -- File error routine.                                *
     F*  SRERR  -- Error reporting routine.                           *
     F*                                                               *
     F*****************************************************************
     F/SPACE
     F*-------------------------------------------------------------------
     F* Copied-in file definitions:
     F*
     F/COPY WNCPYSRC,CG8250FPG
     F*===================================================================
     FCG8250DFCF  E                    WORKSTN
     F                                              KINFDS INF1DS
     F                                              KINFSR SRFILE
     F* Window display file
     F*-------------------------------------------------------------------
     E/EJECT
     E*-------------------------------------------------------------------
     E* Error processing array:
     E*
     E/COPY CGCPYSRC,SRERRE
     E*
     E                    CPY@    1   1 80               Copyright
     E*
     E* Copied-in array definitions:
     E*
     E/COPY WNCPYSRC,CG8250EPG
     E*
     E*-------------------------------------------------------------------
     I/SPACE
     I*-------------------------------------------------------------------
     I/COPY WNCPYSRC,CG8250IPG
     I*
     I* Copied-in data structures
     I*
     I*...................................................................
     I/COPY CGCPYSRC,SRERRI
     I*
     I* Error processing data structures
     I*
     I*...................................................................
     IBIGLDA    E DSCGLDA
     I*
     I* Local data area
     I*
     I*...................................................................
     IINF1DS    E DSY2I#DSP
     I*
     I* Display file information data structure for CG8250DF
     I*
     I*...................................................................
     I* Message translation fields:
     I*
     IDSMTR       DS
     I                                        1 132 #MSDTA
     I                                      133 264 #MSTX1
     I#MSTX2      DS
     I                                        1 256 #MSTXA
     I                                      257 512 #MSTXB
     I********************************************************************
     C/EJECT
     C********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C           P0INP     PARM           P0RPLY  1
     C                     PARM           P0MSG   7
     C*
     C* Add subroutine name to stack:
     C*
     C                     Z-ADD1         Q
     C                     MOVEL'@Main@'  @STK,Q
     C*
     C* Initialise:
     C                     EXSR SRINIT
     C*
     C* Value is "C" -- commit changes without displaying the screen:     084478
     C*                                                                   084478
     C           P0INP     CASEQ'C'       SRCONF                          084478
     C*                                                                   084478
     C* Value is "R" -- roll back changes without displaying the screen:  084478
     C*                                                                   084478
     C           P0INP     CASEQ'R'       SRCAN                           084478
     C*                                                                   084478
     C* Otherwise ("Q" or blank), display the window:                     084478
     C*                                                                   084478
     C                     CAS            SRSHOW                          084478
     C                     ENDCS                                          084478
     C*                                                                   084478
     C* Define the incoming control value:                                084478
     C*                                                                   084478
     C                     MOVE P0INP     P0INP   1                       084478
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     RETRN
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRSHOW warns of unconfirmed changes.               **
     C********************************************************************
     C           SRSHOW    BEGSR                           * S R S H O W *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRSHOW'  @STK,Q
     C*
     C* Do until F5 (confirm) or F12 (cancel):
     C*
     C           *IN05     DOUEQ*ON
     C           *IN12     OREQ *ON
     C                     EXFMT#WINDOW
     C*
     C* If F5, commit the changes; or if F12, roll the changes back;      084478
     C*  in both cases only if the control value is not "Q" (question):   084478
     C*
     C           *IN05     CASEQ*ON       SRCONF
     C           *IN12     CASEQ*ON       SRCAN
     C                     ENDCS
     C*
     C                     ENDDO
     C*
     C           EXSHOW    TAG                             <<<=== EXSHOW
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRCONF handles F5 (confirm changes).               **
     C********************************************************************
     C           SRCONF    BEGSR                           * S R C O N F *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRCONF'  @STK,Q
     C*
     C* Commit changes if not "Q" (question only):                        084478
     C*                                                                   084478
     C           P0INP     IFNE 'Q'                                       084478
     C                     COMIT
     C                     ENDIF                                          084478
     C*                                                                   084478
     C                     MOVE 'Y'       P0RPLY                          084478
     C*
     C           EXCONF    TAG                             <<<=== EXCONF
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRCAN handles F12 (cancel changes).                **
     C********************************************************************
     C           SRCAN     BEGSR                           * S R C A N   *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRCAN '  @STK,Q
     C*
     C* Rollback changes (if not "Q" (question only)):                    084478
     C*                                                                   084478
     C           P0INP     IFNE 'Q'                                       084478
     C                     ROLBK
     C                     ENDIF                                          084478
     C*                                                                   084478
     C                     MOVE 'N'       P0RPLY                          084478
     C*
     C           EXCAN     TAG                             <<<=== EXCAN
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRINIT provides initialisation.                    **
     C********************************************************************
     C           SRINIT    BEGSR                           * S R I N I T *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C* Define and clear error return code:
     C*
     C                     MOVE *BLANKS   P0RTN
     C*
     C* Select the ID of the message to be shown:
     C*
     C                     MOVE 'CGD1616' #MSGID           Default
     C           P0MSG     IFNE *BLANKS
     C                     MOVE P0MSG     #MSGID
     C                     ENDIF
     C*
     C* Get the message text:
     C*
     C                     CALL 'CGC1000'              9090
     C                     PARM           #MSGID                          084478
     C                     PARM 'CGUSRMSG'#MSGF
     C                     PARM           #MSDTA
     C                     PARM *BLANKS   #MSTX1
     C                     PARM *BLANKS   #MSTX2
     C*
     C                     MOVEL#MSTX1    #1WARN
     C*
     C* Move the copyright parameter:
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C           EXINIT    TAG                             <<<=== EXINIT
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C** Subroutine *PSSR handles program errors.                       **
     C********************************************************************
     C*
     C/COPY CGCPYSRC,SRERRPSSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* File and Program Error Processing
     C*
     C/COPY CGCPYSRC,SRERRC
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* /Copy point for calculation specifications:
     C*
     C/COPY WNCPYSRC,CG8250CPG
     C*
     C********************************************************************
     O/EJECT
     O********************************************************************
     C* /Copy point for output:
     C*
     O/COPY WNCPYSRC,CG8250DOPG
     C*
     O********************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2001
