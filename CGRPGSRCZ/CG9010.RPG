     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CSD053
/*STD *  RPGBNOCVT                                                    *                       CSD053
/*EXS *  RPGCVTOPT1                                                   *                       CSD053
/*EXI *  TEXT('Midas CG Create item reference records')
/*OVRF*: OVRDBF FILE(EXUDCRPD) TOFILE(CGUDCRPD)                     : *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG9010 - Create item reference records                       *
      *                                                               *
      *  Function:  This program creates UDC item reference records   *
      *             for primary and secondary recipients, and passes  *
      *             back the generated item identifier to the         *
      *             calling extract program.                          *
      *                                                               *
      *             It runs in two modes:                             *
      *              *GEN - generate reference records                *
      *              *REF - verify whether a record would be          *
      *                     generated - but don't generate            *
      *                                                               *
      *  Called By: All UDC extract processes                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD043920           Date 02Feb17               *
      *  Prev Amend No. MD043686           Date 20Jan17               *
      *                 MD043637           Date 20Jan17               *
      *                 CCG024             Date 29Sep16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD053             Date 01Jun06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 107390             Date 09Apr97               *
      *                 S01408             Date 19Aug96               *
      *                 CCG004             DATE 01JUN95               *
      *                 087834             DATE 15MAY95               *
      *                 087831             DATE 05MAY95               *
      *                 087283             DATE 03MAY95               *
      *                 084316             DATE 21FEB95               *
      *                 082705             DATE 21FEB95               *
      *                 080068             DATE 09DEC94               *
      *                 079997             DATE 18DEC94               *
      *                 079847             DATE 07DEC94               *
      *                 S01522             DATE 22NOV94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD043920 - Customer Lending default Address Ref to 'B'.      *
      *             Change program to ignore it.                      *
      *  MD043686 - Use retail number instead if account is not found *
      *  MD043637 - Type BUY_SELL was overlooked.                     *
      *  CCG024 - Use Atlernate Name and Address in Correspondence    *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  107390 - Advices are being created although there are no     *
      *           schedules set up for them.                          *
      *  S01408 - Core Hook CG9010CCP3 Added
      *         - Core Hook CG9010CCP2 Added
      *         - Core Hook CG9010CCP1 Added
      *  CCG004 - Add processing for Copy user                        *
      *  087834 - Only one copy coming out, regardless of             *
      *           correspondent schedule set up.                      *
      *  087831 - Secondary items sent to correspondent type          *
      *           correspondent, not secondary destination.           *
      *  087283 - Use arrays and occur data structures to reduce acces*
      *  084316 - Use L1 logical files to exclude deleted records     *
      *  082705 - Add correspondent type level default                *
      *  080068 - Incorrect defaulting for branch/account code        *
      *  079997 - Inconsistent formatting of account id.              *
      *  079847 - System level defaults corrupted if already accessed *
      *           and program runs a second time without closing down *
      *  S01522 - User Defined Correspondence                         *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicator usage                                              *
      *  ~~~~~~~~~~~~~~~                                              *
      *                                                               *
      *  01   -    XX                                                 *
      *  50   -    NrF, LF/CGCORRLn Correspondent details             *
      *  51   -    NrF, LF/CGCSCHLn Correspondent schedule            *
      *  52   -    NrF, LF/CGPSCHL0 Schedule details                  *
      *  55   -    Work                                               *
      *                                                               *
      *****************************************************************
      *
     F*GCORRL0IF  E           K        DISK                               084316
     FCGCORRL1IF  E           K        DISK                               084316
     F                                              KINFSR SRFILE
     FCGCORRL2IF  E           K        DISK
     F                                              KINFSR SRFILE
     FCGCORRL3IF  E           K        DISK
     F                                              KINFSR SRFILE
     FCGCORRL4IF  E           K        DISK
     F                                              KINFSR SRFILE
     FCGCORRLCIF  E           K        DISK                               082705
     F                                              KINFSR SRFILE         082705
     F*GCSCHL2IF  E           K        DISK                               080068
     F********************************************* KINFSR SRFILE         080068
     FCGCSCHL9IF  E           K        DISK                               080068
     F                                              KINFSR SRFILE         080068
     FCGCSCHL3IF  E           K        DISK
     F                                              KINFSR SRFILE
     FCGCSCHL5IF  E           K        DISK
     F                                              KINFSR SRFILE
     FCGCSCHL2IF  E           K        DISK                               087283
     F                                              KINFSR SRFILE         087283
     F*GPSCHL0IF  E           K        DISK                               084316
     FCGPSCHL1IF  E           K        DISK                               084316
     F                                              KINFSR SRFILE
     FCGUDCRL0IF  E           K        DISK                               082705
     F                                              KINFSR SRFILE         082705
     FCGUDCRPDO   E           K        DISK                           UC
     F                                              KINFSR SRFILE
     F                                              KCOMIT
     FEXUDCRPDO   E           K        DISK                           UC
     F            @UDCRPD                           KRENAME@UDCRPDX
     F                                              KINFSR SRFILE
     FACCNT   IF  E           K        DISK         KINFSR SRFILE                             CCG024
     F            ACCNTAAF                          KIGNORE                                   CCG024
     F            ACCNTACF                          KIGNORE                                   CCG024
     FCLOAN   IF  E           K        DISK         KINFSR SRFILE                             CCG024
     F            CLOANHHF                          KIGNORE                                   CCG024
     F            CLOANCKF                          KIGNORE                                   CCG024
     F            CLOANZ1F                          KIGNORE                                   CCG024
     FCLINT   IF  E           K        DISK         KINFSR SRFILE                             CCG024
     F            CLINTCAF                          KIGNORE                                   CCG024
     F            CLINTCBF                          KIGNORE                                   CCG024
     F            CLINTCCF                          KIGNORE                                   CCG024
     F            CLINTC1F                          KIGNORE                                   CCG024
     FACCNTL1 IF  E           K        DISK         KINFSR SRFILE                           MD043686
     F            ACCNTABF                          KRENAMERETAILF                          MD043686
      /EJECT
     E                    CPY@    1   1 80
      ** Copyright statement
      *
     E                    TAB1    1   2 10   TAB2    4
      ** Table of print schedules against item statuses
      *
     E                    #SCSQ   1   4  4                                080068
      ** Search criteria - 1                                              080068
      *                                                                   080068
     E                    #SCLT       4  1                                080068
      ** Search criteria - 2                                              080068
      *                                                                   080068
     E                    #CUS       20  6                                087283
      ** Store of last twenty customers                                   087283
      *                                                                   087283
     E                    #COR       20 10                                087283
      ** Store of last twenty correspondents                              087283
      *                                                                   087283
     E                    #PSC       10  6                                087283
      ** Store of last ten print schedules                                087283
      *                                                                   087283
     E                    #BRC       10  3                                087283
      ** Store of last ten branches                                       087283
      *                                                                   087283
     E                    #CTY       20 10                                087283
      ** Store of last twenty correspondent types                         087283
      *                                                                   087283
      ** Store alternate address ref                                                          CCG024
     E                    #ADD       10  1                                                    CCG024
      *                                                                                       CCG024
      /COPY CGCPYSRC,SRERRE
      /EJECT
     ICPYR@#      DS
     I                                        1  80 CPY@
      ** Copyright statement
      *
     I*#DEFT***** DS                          6                           082705
     ID#DEFT      DS                          8                           082705
     I                                        1   50D#PRTD
     I                                        6  100D#RETP
     I                                       11  12 D#LGID
     I                                       13  18 D#PSCH
     I                                       19  21 D#OVBR
     I                                       22  22 D#APDF
      ** Multiple occurrence data structure used to hold print schedule
      ** information for system, branch and entity
      *
     I            DS
     I                                        1   8 ##ITMA
     I                                        1   80##ITMN
      ** Used to convert item reference from character to numeric
      *
     I            DS                                                      079997
     I**********                              1  18 ##MACT                                    CGL029
     I                                        1  24 ##MACT                                    CGL029
     I************************************** 13  16 ##ACOD                079997
     I**********                             10  13 ##ACOD                             079997 CGL029
     I                                       10  19 ##ACOD                                    CGL029
      ***Used*to extract account code from account id                     079997
      *
     I#KEY9D      DS                                                      080068
     I                                        1  10 ##PTYP                080068
     I                                       11  20 ##PSTP                080068
     I**********                             21  24 ##ACCD                             080068 CGL029
     I**********                             25  42 K#MACT                             080068 CGL029
     I                                       21  30 ##ACCD                                    CGL029
     I                                       31  54 K#MACT                                    CGL029
      ** Key for schedule details                                         080068
      *
     I##UDCR    E DSCGUDCRPD
      ** External DS for incomplete reference
     I                                      180 185 ##CNUM                                    CCG024
     I                                      186 188 ##CCY                                     CCG024
     I                                      189 1980##ACO                                     CCG024
     I                                      199 2000##ACSQ                                    CCG024
     I                                      201 203 ##BRCA                                    CCG024
     I                                      180 1890##ACNO                                  MD043686
     IAD          DS                                                                          CCG024
     I                                        1   8 AD1                                       CCG024
     I                                        9  10 AD2                                       CCG024
      *
     ICGDATA    E DSCGDTA
      ** Data Area for UDC (used for next item identifier)
      *
     IRUNDTA    E DSRUNDAT
      ** Data Area for run-date
      *
     ISCSARD    E DSSCSARDPD                                              CCG004
      **  Sar Text details                                                CCG004
      *                                                                   CCG004
     IDSSDY     E DSDSSDY                                                 CCG004
      **  Data Structure used by Access Programs                          CCG004
      *                                                                   CCG004
     ICGCORR    E DSCGCORRPD                                              087283
      ** External DS for Correspondent Details                            087283
      *                                                                   087283
     I***CSTORE*      DS                         71 800                                087283 CSD053
     I**********                                800 800 ##NFC                          087283 CSD053
     ICSTORE      DS                         711273                                           CSD053
     I                                     12731273 ##NFC                                     CSD053
      ** Occur data structure for stored reads                            087283
      *                                                                   087283
     ICGPSCH    E DSCGPSCHPD                                              087283
      ** External DS for Correspondent Details                            087283
      *                                                                   087283
     IPSTORE      DS                         10 256                       087283
     I                                      256 256 ##NFP                 087283
      ** Occur data structure for stored reads                            087283
      *                                                                   087283
      * System Value                                                                          CCG024
     I            DS                                                                          CCG024
     I I            'RecipientSpecialChar'    1  20 SVALK1                                    CCG024
      *                                                                                       CCG024
     I              'STATEMENT '          C         C#STMT                                    CCG024
     I              'LOAN COMMC'          C         C#LOCM                                    CCG024
     I              'LOAN REPAY'          C         C#LORP                                    CCG024
     I              'LOAN ROLLO'          C         C#LORL                                    CCG024
     I              'LOAN FEE'            C         C#LOFE                                    CCG024
     I              'LOAN INTER'          C         C#LOIT                                    CCG024
     I              'LOAN RATCH'          C         C#LORT                                    CCG024
     I              'LOAN PSTDU'          C         C#LOPD                                    CCG024
     I              'CURR_EXCHG'          C         C#RECX                                    CCG024
     I              'DEALINGGEN'          C         C#REDL                                    CCG024
     I              'RETAILADVS'          C         C#READ                                    CCG024
     I              'INT_CAP'             C         C#REIC                                    CCG024
     I              'LENDINGGEN'          C         C#RELE                                    CCG024
     I              'RATE_CHG'            C         C#RERC                                    CCG024
     I              'STAND_ORDR'          C         C#RESO                                    CCG024
     I              'TRN_CHRG'            C         C#RETC                                    CCG024
     I              'RTL_ADVG'            C         C#REAG                                    CCG024
     I              'RETAIL'              C         C#RETL                                    CCG024
     I              'SEC_ADV_CF'          C         C#SEAD                                    CCG024
     I              'SEC_POS_CF'          C         C#SEPO                                    CCG024
     I              'BUY_SELL'            C         C#SEBS                                  MD043637
      /COPY CGCPYSRC,SRERRI
      /EJECT
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   SRINIT - Initial processing                                 *
      *   SRPRIM - Processing for primary recipient                   *
      *   SRSECO - Processing for secondary recipient(s)              *
      *   SRPRSC - Create reference records for secondaries           *
      *   SRDETL - Derive details for correspondent                   *
      *   SRDEFT - Derive schedule details for system/branch/entity   *
      *   SRIDGN - Generate next item reference number                *
      *                                                               *
      *****************************************************************
      /EJECT
     C           *ENTRY    PLIST
      *
      ** Return code
     C                     PARM           W0MSGD
      *
      ** Input: mode, commitment control indicator, primary customer
      ** incomplete UDC reference record
     C                     PARM           ##MODE 10
     C                     PARM           ##CMT   3
     C                     PARM           ##CUST  6
     C                     PARM           ##UDCR
      *
      ** Output: generated item reference (alpha format)
     C                     PARM           ##ITEM  8
      *
      ** Initial processing; only perform once per job
     C           RUNDTA    IFEQ *BLANK
     C                     EXSR SRINIT
     C                     END
      *
      ** Initialise generate_item indication and item_id_generated
      ** indication
     C                     MOVEL'N'       ##GENI  1
     C                     MOVEL'N'       ##IDGN  1
     C*                                                                   S01408
     C/COPY WNCPYSRC,CG9010CCP1                                           S01408
     C*                                                                   S01408
      *                                                                   084316
      ** Ensure correspondent exists for this customer before going any   084316
      ** further - return 'NOCR' in status to indicate no correspondent   084316
      *                                                                   087283
      * Look up customer in array                                         087283
      *                                                                   087283
     C                     Z-ADD1         X       50                      087283
     C           ##CUST    LOKUP#CUS,X                   90               087283
      *                                                                   087283
      * If found access details from CSTORE                               087283
      *                                                                   087283
     C           *IN90     IFEQ '1'                                       087283
     C           X         ADD  31        X                               087283
     C           X         OCUR CSTORE                                    087283
      *                                                                   087283
      * If record found move to CGCORRPD set 50 off else set 50 on        087283
      *                                                                   087283
     C           ##NFC     IFNE 'Y'                                       087283
     C                     SETOF                     50                   087283
     C                     MOVELCSTORE    CGCORR                          087283
     C                     ELSE                                           087283
     C                     SETON                     50                   087283
     C                     ENDIF                                          087283
      *                                                                   087283
      * Customer not in store so chain to find details                    087283
      *                                                                   087283
     C                     ELSE                                           087283
     C           ##CUST    CHAINCGCORRL4             50                   084316
      *                                                                   087283
      * Set up new off set, move contents into array                      087283
      *                                                                   087283
     C                     ADD  1         ##COFS  50                      087283
     C           ##COFS    IFGT 20                                        087283
     C                     Z-ADD1         ##COFS                          087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     Z-ADD##COFS    X                               087283
     C                     MOVEA##CUST    #CUS,X                          087283
     C           ##COFS    ADD  31        X                               087283
     C           X         OCUR CSTORE                                    087283
     C                     MOVEL*BLANKS   CSTORE                          087283
      *                                                                   087283
      * If record found move in CGCORRPD else ##NFC to Y                  087283
      *                                                                   087283
     C           *IN50     IFEQ *OFF                                      087283
     C                     MOVELCGCORR    CSTORE                          087283
     C                     MOVEL*BLANKS   ##NFC                           087283
     C                     ELSE                                           087283
     C                     MOVEL'Y'       ##NFC                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     ENDIF                                          087283
     C           *IN50     IFEQ '1'                                       084316
     C                     MOVEL'N'       ##GENI                          084316
     C                     MOVEL'NOCR'    DRISTS                          084316
     C                     ELSE                                           084316
      *
      ** Create record for primary correspondent
     C                     EXSR SRPRIM
      *
      ** Create records for secondary correspondents
     C                     EXSR SRSECO
      *                                                                   CCG004
      * If copy user present see if extras needed                         CCG004
      * only do when in generation                                        CCG004
      *                                                                   CCG004
     C           ##CUSR    IFEQ 'Yes'                                     CCG004
     C           ##MODE    ANDEQ'*GEN'                                    CCG004
      *                                                                   CCG004
      ** Create record for Copy correspondent                             CCG004
     C                     EXSR SRCOPY                                    CCG004
      *                                                                   CCG004
      ** Create records for secondary Copy correspondents                 CCG004
     C                     EXSR SRCSEC                                    CCG004
     C                     ENDIF                                          CCG004
      *                                                                   084316
     C                     ENDIF                                          084316
      *
      ** Set up parameters for return: msgid blank => all OK, and at
      ** least one reference record generated; msgid CGD1270 => no
      ** error, but no item generated.
     C           ##GENI    IFEQ 'N'
     C                     CLEAR##ITEM
     C                     MOVEL'CGD1270' W0MSGD
     C                     ELSE
     C                     CLEARW0MSGD
     C                     ENDIF
      *
      ** Return to calling program (without setting on LR)
     C                     RETRN
      /EJECT
      *****************************************************************
      * Subroutine  :  SRPRIM                                         *
      * Purpose     :  Processing for primary recipient               *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  SREDTL                                         *
      *                SRIDGN                                         *
      *****************************************************************
      *
     C           SRPRIM    BEGSR
      *
      ** Call SRDETL to obtain details for this customer
     C                     MOVEL##CUST    Z#CUST
     C                     CLEARZ#CORR
     C                     CLEARZ#PRIM                                    080068
     C                     EXSR SRDETL
      *
      ** Store returned correspondent id of primary
     C                     MOVELZ#CUID    Z#PRIM
     C           *LIKE     DEFN CDCTYP    ##CTYP                          CCG004
     C                     MOVELZ#CTYP    ##CTYP                          CCG004
      *
      ** Setup details for primary
     C******************** CLEARDRPCOR                                    082705
     C                     MOVELZ#PRIM    DRPCOR                          082705
     C                     MOVELZ#PRIM    DRDCOR
     C                     MOVELZ#PRTD    DRPRTD
     C                     MOVELZ#RETP    DRDDAT
     C                     MOVELZ#LGID    DRLGID
     C                     MOVELZ#ISTS    DRISTS
     C                     MOVELZ#OVBR    DROVBR
     C                     Z-ADDAGRDNB    DRRDNB
     C                     MOVELAGMRDT    DRARDT
     C                     MOVEL'D'       DRRECI
     C                     MOVEL'I'       DRAACT
     C                     TIME           DRATIM
     C                     MOVEL##JOB     DRAJOB
     C                     MOVEL##USR     DRAUSR
      *
      ** If suppress not indicated, and mode is '*GEN', generate
      ** next sequence number and write reference record
     C           Z#SUPR    IFNE 'Y'
     C                     MOVEL'Y'       ##GENI
     C           ##MODE    IFEQ '*GEN'
     C                     EXSR SRIDGN
     C                     Z-ADD##ITMN    DRITEM
      *                                                                   CCG024
     C                     MOVEL'Y'       TOWRT   1                       CCG024
      ** If CCG024 is on, look for alternate address                      CCG024
     C           CCG024    IFEQ 'Y'                                       CCG024
     C           ##MODE    ANDEQ'*GEN'                                    CCG024
      *                                                                   CCG024
      ** For GL statement                                                 CCG024
     C           DRPTYP    IFEQ C#STMT                                    CCG024
     C           KMACT     CHAINACCNTABF             22                   CCG024
     C           *IN22     IFEQ '0'                                       CCG024
     C                     MOVELADREF1    AD1                             CCG024
     C                     MOVELADREF2    AD2                             CCG024
     C                     MOVEAAD        #ADD                            CCG024
     C                     DO   10        C       20                      CCG024
     C           #ADD,C    IFNE *BLANKS                                   CCG024
     C           #ADD,C    IFNE 'B'                                       CCG024
     C           ##CUST    CAT  SVAL1     DRDCOR    P                     CCG024
     C                     CAT  #ADD,C:0  DRDCOR                          CCG024
     C                     ENDIF                                          CCG024
     C                     MOVEL'N'       TOWRT                           CCG024
     C           ##CMT     IFEQ 'YES'                                     CCG024
     C                     WRITE@UDCRPD                                   CCG024
     C                     ELSE                                           CCG024
     C                     WRITE@UDCRPDX                                  CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
     C                     ENDDO                                          CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
      *                                                                   CCG024
      ** For RE advices                                                   CCG024
     C           DRPTYP    IFEQ C#RECX                                    CCG024
     C           DRPTYP    OREQ C#REDL                                    CCG024
     C           DRPTYP    OREQ C#READ                                    CCG024
     C           DRPTYP    OREQ C#REIC                                    CCG024
     C           DRPTYP    OREQ C#RELE                                    CCG024
     C           DRPTYP    OREQ C#RERC                                    CCG024
     C           DRPTYP    OREQ C#RESO                                    CCG024
     C           DRPTYP    OREQ C#RETC                                    CCG024
     C           DRPTYP    OREQ C#REAG                                    CCG024
     C           DRPTYP    OREQ C#RETL                                    CCG024
     C           KMACT     CHAINACCNTABF             22                   CCG024
     C           *IN22     IFEQ '1'                                     MD043686
     C           ##ACNO    CHAINRETAILF              22                 MD043686
     C                     ENDIF                                        MD043686
     C           *IN22     IFEQ '0'                                       CCG024
     C                     MOVELADREF1    AD1                             CCG024
     C                     MOVELADREF2    AD2                             CCG024
     C                     MOVEAAD        #ADD                            CCG024
     C                     DO   10        C       20                      CCG024
     C           #ADD,C    IFNE *BLANKS                                   CCG024
     C           #ADD,C    IFNE 'B'                                       CCG024
     C           ##CUST    CAT  SVAL1     DRDCOR    P                     CCG024
     C                     CAT  #ADD,C:0  DRDCOR                          CCG024
     C                     ENDIF                                          CCG024
     C                     MOVEL'N'       TOWRT                           CCG024
     C           ##CMT     IFEQ 'YES'                                     CCG024
     C                     WRITE@UDCRPD                                   CCG024
     C                     ELSE                                           CCG024
     C                     WRITE@UDCRPDX                                  CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
     C                     ENDDO                                          CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
      *                                                                   CCG024
      ** For LE confirmations                                             CCG024
     C           DRPTYP    IFEQ C#LOCM                                    CCG024
     C           DRPTYP    OREQ C#LORP                                    CCG024
     C           DRPTYP    OREQ C#LORL                                    CCG024
     C           DRPTYP    OREQ C#LOFE                                    CCG024
     C           DRPTYP    OREQ C#LOIT                                    CCG024
     C           DRPTYP    OREQ C#LORT                                    CCG024
     C           DRPTYP    OREQ C#LOPD                                    CCG024
     C                     MOVELDRMTRN    KLNRF                           CCG024
     C                     MOVEL'A'       KRCDT                           CCG024
     C           KLOAN     CHAINCLOAN                22                   CCG024
     C           *IN22     IFEQ '0'                                       CCG024
     C           NACD      ANDNE*BLANKS                                   CCG024
     C           NACD      ANDNE'B'                                     MD043920
     C           ##CUST    CAT  SVAL1     DRDCOR    P                     CCG024
     C                     CAT  NACD:0    DRDCOR                          CCG024
     C                     MOVEL'N'       TOWRT                           CCG024
     C           ##CMT     IFEQ 'YES'                                     CCG024
     C                     WRITE@UDCRPD                                   CCG024
     C                     ELSE                                           CCG024
     C                     WRITE@UDCRPDX                                  CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
      *                                                                   CCG024
      ** For SE confirmations                                             CCG024
     C           DRPTYP    IFEQ C#SEAD                                    CCG024
     C           DRPTYP    OREQ C#SEPO                                    CCG024
     C           DRPTYP    OREQ C#SEBS                                  MD043637
     C           ##CUST    CHAINCLINTSEF             22                   CCG024
     C           *IN22     IFEQ '0'                                       CCG024
     C           C1AN      ANDNE*BLANKS                                   CCG024
     C           ##CUST    CAT  SVAL1     DRDCOR    P                     CCG024
     C                     CAT  C1AN:0    DRDCOR                          CCG024
     C                     MOVEL'N'       TOWRT                           CCG024
     C           ##CMT     IFEQ 'YES'                                     CCG024
     C                     WRITE@UDCRPD                                   CCG024
     C                     ELSE                                           CCG024
     C                     WRITE@UDCRPDX                                  CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
     C                     ENDIF                                          CCG024
      *                                                                   CCG024
     C                     ENDIF                                          CCG024
     C*                                                                   S01408
     C/COPY WNCPYSRC,CG9010CCP2                                           S01408
     C*                                                                   S01408
     C           TOWRT     IFEQ 'Y'                                       CCG024
     C           ##CMT     IFEQ 'YES'
     C                     WRITE@UDCRPD
     C                     ELSE
     C                     WRITE@UDCRPDX
     C                     ENDIF
     C                     ENDIF                                          CCG024
     C*                                                                   S01408
     C/COPY WNCPYSRC,CG9010CCP3                                           S01408
     C*                                                                   S01408
     C                     ENDIF
     C                     ENDIF
      *
     C           EXPRIM    TAG
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRSECO                                         *
      * Purpose     :  Processing for secondary recipient(s)          *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  SRPRSC                                         *
      *****************************************************************
      *
     C           SRSECO    BEGSR
      *
      ** Access all secondary recipients for this item; loop around
      ** file three times, each time removing an element of the key.
     C******************** MOVELZ#PRIM    ##PRIM                          087834
     C******************** MOVELDRPTYP    ##PTYP                          087834
     C******************** MOVELDRPSTP    ##PSTP                          087834
     C                     MOVELZ#PRIM    S#PRIM                          087834
     C                     MOVELDRPTYP    S#PTYP                          087834
     C                     MOVELDRPSTP    S#PSTP                          087834
      *
     C***********1         DO   3         ##C     50                      087834
     C           1         DO   3         ##S     50                      087834
      *
     C***********##C       IFEQ 2                                         087834
     C           ##S       IFEQ 2                                         087834
     C******************** CLEAR##PSTP                                    087834
     C                     CLEARS#PSTP                                    087834
     C                     ENDIF
     C***********##C       IFEQ 3                                         087834
     C           ##S       IFEQ 3                                         087834
     C******************** CLEAR##PTYP                                    087834
     C                     CLEARS#PTYP                                    087834
     C                     ENDIF
      *
     C           ##KSTS    CHAINCGCSCHL5             51
     C           *IN51     DOWEQ'0'
     C/COPY WNCPYSRC,CG9010C001
      *
      ** If correspondent different from primary, and does not include
      ** account details (which are processed later), process secondary
     C           CPPCOR    IFNE CPCORR
     C           CPMACT    ANDEQ*BLANKS
     C/COPY WNCPYSRC,CG9010C002
     C                     EXSR SRPRSC
     C                     END
      *
     C           ##KSTS    READECGCSCHL5                 51
      *
     C                     ENDDO                           DoW
      *
     C                     ENDDO                           Do 3
      *
      ** If account non-blank (ie. this is a statement) access
      ** secondary correspondents for account as well
     C           DRMACT    IFNE *BLANKS
     C******************** MOVELDRMACT    ##MACT                          087834
     C                     MOVELDRMACT    S#MACT                          087834
     C           ##KSAC    CHAINCGCSCHL3             51
      *
     C           *IN51     DOWEQ'0'
     C*                                                                   107390
     C                     MOVE 'N'       FLD1    1                       107390
     C                     MOVE 'N'       FLD2    1                       107390
     C                     MOVE 'N'       FLD3    1                       107390
     C           CPPTYP    IFEQ DRPTYP                                    107390
     C           CPPSTP    ANDEQ*BLANKS                                   107390
     C                     MOVE 'Y'       FLD1                            107390
     C                     END                                            107390
     C           CPPTYP    IFEQ DRPTYP                                    107390
     C           CPPSTP    ANDEQDRPSTP                                    107390
     C                     MOVE 'Y'       FLD2                            107390
     C                     END                                            107390
     C           CPPTYP    IFEQ *BLANKS                                   107390
     C           CPPSTP    ANDEQ*BLANKS                                   107390
     C                     MOVE 'Y'       FLD3                            107390
     C                     END                                            107390
     C*                                                                   107390
      *
      ** If correspondent different from primary, process as secondary
     C           CPPCOR    IFNE CPCORR
     C*                                                                   107390
     C           FLD1      IFEQ 'Y'                                       107390
     C           FLD2      OREQ 'Y'                                       107390
     C           FLD3      OREQ 'Y'                                       107390
     C                     EXSR SRPRSC
     C                     END                                            107390
     C*                                                                   107390
     C                     END
      *
     C           ##KSAC    READECGCSCHL3                 51
      *
     C                     ENDDO                           DoW
      *
     C                     ENDIF
      *
     C           EXSECO    TAG
     C                     ENDSR
      /EJECT
      *****************************************************************   CCG004
      * Subroutine  :  SRCOPY                                         *   CCG004
      * Purpose     :  Processing for Copy correspondent              *   CCG004
      *                                                               *   CCG004
      * Called by   :  Main process                                   *   CCG004
      * Calls       :  SREDTL                                         *   CCG004
      *                SRIDGN                                         *   CCG004
      *****************************************************************   CCG004
      *                                                                   CCG004
     C           SRCOPY    BEGSR                                          CCG004
      *                                                                   CCG004
      ** Call SRDETL to obtain details for this copy correspondent        CCG004
      *                                                                   CCG004
     C                     SELEC                                          CCG004
     C           ##COPY    WHEQ 'BRANCH'                                  CCG004
     C                     MOVEL'COPY'    Z#CORR    P                     CCG004
     C                     MOVE DRBRCA    Z#CORR                          CCG004
     C           ##COPY    WHEQ 'CORTYPE'                                 CCG004
     C                     MOVEL'COPY'    Z#CORR    P                     CCG004
     C           5         SUBST##CTYP:1  ##005A  5                       CCG004
     C                     MOVE ##005A    Z#CORR                          CCG004
     C                     OTHER                                          CCG004
     C                     MOVEL'COPY'    Z#CORR    P                     CCG004
     C                     ENDSL                                          CCG004
      *                                                                   CCG004
     C                     CLEARZ#CUST                                    CCG004
     C                     EXSR SRDETL                                    CCG004
     C                     MOVELZ#CUID    Z#CORR                          CCG004
      *                                                                   CCG004
      ** Setup details for primary                                        CCG004
     C                     MOVELZ#PRIM    DRPCOR                          CCG004
     C                     MOVELZ#CORR    DRDCOR                          CCG004
     C                     MOVELZ#PRTD    DRPRTD                          CCG004
     C                     MOVELZ#RETP    DRDDAT                          CCG004
     C                     MOVELZ#LGID    DRLGID                          CCG004
     C                     MOVELZ#ISTS    DRISTS                          CCG004
     C                     MOVELZ#OVBR    DROVBR                          CCG004
     C                     Z-ADDAGRDNB    DRRDNB                          CCG004
     C                     MOVELAGMRDT    DRARDT                          CCG004
     C                     MOVEL'D'       DRRECI                          CCG004
     C                     MOVEL'I'       DRAACT                          CCG004
     C                     TIME           DRATIM                          CCG004
     C                     MOVEL##JOB     DRAJOB                          CCG004
     C                     MOVEL##USR     DRAUSR                          CCG004
      *                                                                   CCG004
      ** If suppress not indicated, and mode is '*GEN', generate          CCG004
      ** next sequence number and write reference record                  CCG004
     C           Z#SUPR    IFNE 'Y'                                       CCG004
     C                     MOVEL'Y'       ##GENI                          CCG004
     C           ##MODE    IFEQ '*GEN'                                    CCG004
     C           ##CMT     IFEQ 'YES'                                     CCG004
     C                     WRITE@UDCRPD                                   CCG004
     C                     ELSE                                           CCG004
     C                     WRITE@UDCRPDX                                  CCG004
     C                     ENDIF                                          CCG004
     C                     ENDIF                                          CCG004
     C                     ENDIF                                          CCG004
      *                                                                   CCG004
     C           EXCOPY    TAG                                            CCG004
     C                     ENDSR                                          CCG004
      /EJECT                                                              CCG004
      *****************************************************************   CCG004
      * Subroutine  :  SRCSEC                                         *   CCG004
      * Purpose     :  Processing for secondary copy correspondents   *   CCG004
      *                                                               *   CCG004
      * Called by   :  Main process                                   *   CCG004
      * Calls       :  SRPRSC                                         *   CCG004
      *****************************************************************   CCG004
      *                                                                   CCG004
     C           SRCSEC    BEGSR                                          CCG004
      *                                                                   CCG004
      ** Access all secondary recipients for this item; loop around       CCG004
      ** file three times, each time removing an element of the key.      CCG004
     C                     MOVELZ#CORR    S#PRIM                          CCG004
     C                     MOVELDRPTYP    S#PTYP                          CCG004
     C                     MOVELDRPSTP    S#PSTP                          CCG004
      *                                                                   CCG004
     C           1         DO   3         ##S     50                      CCG004
      *                                                                   CCG004
     C           ##S       IFEQ 2                                         CCG004
     C                     CLEARS#PSTP                                    CCG004
     C                     ENDIF                                          CCG004
     C           ##S       IFEQ 3                                         CCG004
     C                     CLEARS#PTYP                                    CCG004
     C                     ENDIF                                          CCG004
      *                                                                   CCG004
     C           ##KSTS    CHAINCGCSCHL5             51                   CCG004
     C           *IN51     DOWEQ'0'                                       CCG004
      *                                                                   CCG004
      ** If correspondent different from primary, and does not include    CCG004
      ** account details (which are processed later), process secondary   CCG004
     C           CPPCOR    IFNE CPCORR                                    CCG004
     C           CPMACT    ANDEQ*BLANKS                                   CCG004
     C                     EXSR SRPRSC                                    CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
     C           ##KSTS    READECGCSCHL5                 51               CCG004
      *                                                                   CCG004
     C                     ENDDO                           DoW            CCG004
      *                                                                   CCG004
     C                     ENDDO                           Do 3           CCG004
      *                                                                   CCG004
      ** If account non-blank (ie. this is a statement) access            CCG004
      ** secondary correspondents for account as well                     CCG004
     C           DRMACT    IFNE *BLANKS                                   CCG004
     C                     MOVELDRMACT    S#MACT                          CCG004
     C           ##KSAC    CHAINCGCSCHL3             51                   CCG004
      *                                                                   CCG004
     C           *IN51     DOWEQ'0'                                       CCG004
     C*                                                                   107390
     C                     MOVE 'N'       FLD1                            107390
     C                     MOVE 'N'       FLD2                            107390
     C                     MOVE 'N'       FLD3                            107390
     C           CPPTYP    IFEQ DRPTYP                                    107390
     C           CPPSTP    ANDEQ*BLANKS                                   107390
     C                     MOVE 'Y'       FLD1                            107390
     C                     END                                            107390
     C           CPPTYP    IFEQ DRPTYP                                    107390
     C           CPPSTP    ANDEQDRPSTP                                    107390
     C                     MOVE 'Y'       FLD2                            107390
     C                     END                                            107390
     C           CPPTYP    IFEQ *BLANKS                                   107390
     C           CPPSTP    ANDEQ*BLANKS                                   107390
     C                     MOVE 'Y'       FLD3                            107390
     C                     END                                            107390
     C*                                                                   107390
      *                                                                   CCG004
      ** If correspondent different from primary, process as secondary    CCG004
     C           CPPCOR    IFNE CPCORR                                    CCG004
     C*                                                                   107390
     C           FLD1      IFEQ 'Y'                                       107390
     C           FLD2      OREQ 'Y'                                       107390
     C           FLD3      OREQ 'Y'                                       107390
     C                     EXSR SRPRSC                                    CCG004
     C                     END                                            107390
     C*                                                                   107390
     C                     END                                            CCG004
      *                                                                   CCG004
     C           ##KSAC    READECGCSCHL3                 51               CCG004
      *                                                                   CCG004
     C                     ENDDO                           DoW            CCG004
      *                                                                   CCG004
     C                     ENDIF                                          CCG004
      *                                                                   CCG004
     C           EXCSEC    TAG                                            CCG004
     C                     ENDSR                                          CCG004
      /EJECT                                                              CCG004
      *****************************************************************
      * Subroutine  :  SRPRSC                                         *
      * Purpose     :  Create reference records for secondaries       *
      *                                                               *
      * Called by   :  SRSECO                                         *
      * Calls       :  SRDETL                                         *
      *                SRIDGN                                         *
      *****************************************************************
      *
     C           SRPRSC    BEGSR
      *
      ** Obtain details via subroutine SRDETL
     C                     CLEARZ#CUST
     C                     MOVELCPCORR    Z#CORR
     C                     MOVELCPCORR    D#CORR                          087831
     C                     EXSR SRDETL
      *
      ** If suppress not indicated, set generate_item indication. If
      ** mode is '*GEN', generate item.
     C           Z#SUPR    IFNE 'Y'
     C                     MOVEL'Y'       ##GENI
     C           ##MODE    IFEQ '*GEN'
      *
      ** If no item id created (check item_id_generated indication),
      ** generate item id.
     C           ##IDGN    IFNE 'Y'
     C                     EXSR SRIDGN
     C                     ENDIF
      *
     C                     Z-ADD##ITMN    DRITEM
     C******************** MOVELZ#CORR    DRDCOR                          087831
     C                     MOVELD#CORR    DRDCOR                          087831
     C                     MOVELZ#PRIM    DRPCOR
     C                     MOVELZ#PRTD    DRPRTD
     C                     MOVELZ#RETP    DRDDAT
     C                     MOVELZ#LGID    DRLGID
     C                     MOVELZ#ISTS    DRISTS
     C                     MOVELZ#OVBR    DROVBR
      *                                                                   082705
      ** Check item does not already exist (owing to a duplication        082705
      ** on the schedules file) - if not, write new record                082705
     C           ##KUDC    SETLLCGUDCRL0                 60               082705
      *
     C           *IN60     IFEQ '0'                                       082705
      *                                                                   082705
     C           ##CMT     IFEQ 'YES'
     C                     WRITE@UDCRPD
     C                     ELSE
     C                     WRITE@UDCRPDX
     C                     ENDIF
      *
     C                     ENDIF
      *                                                                   082705
     C                     ENDIF                                          082705
     C                     ENDIF
      *
     C           EXPRSC    TAG
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRDETL                                         *
      * Purpose     :  Dervive details for correspondent              *
      *                                                               *
      * Called by   :  SRPRIM                                         *
      *                SRPRSC                                         *
      * Calls       :  SRDEFT                                         *
      * Parms IN    :  Z#CUST - Entity: customer number (primary)     *
      *                Z#CORR -         -or- correspondent id         *
      *       OUT   :  Z#CUID - Correspondent id of customer Z#CUST   *
      *                Z#PRTD - Scheduled print date                  *
      *                Z#RETP - Retention period                      *
      *                Z#LGID - Language id                           *
      *                Z#SUPR - Suppress_output indication            *
      *                Z#ISTS - Item status                           *
      *                Z#OVBR - Item status                           *
      *****************************************************************
      *
     C           SRDETL    BEGSR
      *
      ** Initialise output fields
     C                     MOVEL'N'       Z#INAC  1
     C                     MOVEL'N'       Z#SUPR  1
     C                     CLEARZ#PRTD
     C                     CLEARZ#RETP
     C                     CLEARZ#LGID
     C                     CLEARZ#PSCH
     C                     CLEARZ#ISTS
     C                     CLEARZ#OVBR
      *
      ** If this is primary, get defaults for system and branch
     C           Z#CORR    IFEQ *BLANKS
     C                     MOVELDRBRCA    Z#BRCA
     C           1         DO   3         Z#LEVL  50
     C                     EXSR SRDEFT
     C                     ENDDO2
     C                     ENDIF
      *
      ** Access entity level defaults
     C******************** Z-ADD5         Z#LEVL                          082705
     C                     Z-ADD7         Z#LEVL                          082705
     C                     EXSR SRDEFT
     C                     MOVELZ#CORR    Z#CUID
      *
      ** Access type level defaults                                       082705
     C                     Z-ADD5         Z#LEVL                          082705
     C                     EXSR SRDEFT                                    082705
      *
      ***Loop*6*times,*starting*at*highest*level*of*default*(entity/      082705
      ** Loop 8 times, starting at highest level of default (entity/      082705
      ** correspondent) stepping back to lowest level (system). Note
      ** that the 'defaults must be applied' indicator will over-ride
      ** previous values.
     C********** 1         DO   6         ##C                             082705
     C********** 7         SUB  ##C       ##D     50                      082705
     C           1         DO   8         ##C     50                      082705
     C           9         SUB  ##C       ##D     50                      082705
     C           ##D       OCUR D#DEFT
      *
      **  o print date
     C           Z#PRTD    IFEQ *ZEROS
     C           D#APDF    OREQ 'Y'
     C           D#PRTD    ANDNE*ZEROS
     C                     Z-ADDD#PRTD    Z#PRTD
     C                     ENDIF
      *
      **  o retention period
     C           Z#RETP    IFEQ *ZEROS
     C           D#APDF    OREQ 'Y'
     C           D#RETP    ANDNE*ZEROS
     C                     Z-ADDD#RETP    Z#RETP
     C                     ENDIF
      *
      **  o language identifier
     C           Z#LGID    IFEQ *BLANKS
     C           D#APDF    OREQ 'Y'
     C           D#LGID    ANDNE*BLANKS
     C                     MOVELD#LGID    Z#LGID
     C                     ENDIF
      *
      **  o print schedule
     C           Z#PSCH    IFEQ *BLANKS
     C           D#APDF    OREQ 'Y'
     C           D#PSCH    ANDNE*BLANKS
     C                     MOVELD#PSCH    Z#PSCH
     C                     ENDIF
      *
      **  o overide branch
     C           Z#OVBR    IFEQ *BLANKS
     C           D#APDF    OREQ 'Y'
     C           D#OVBR    ANDNE*BLANKS
     C                     MOVELD#OVBR    Z#OVBR
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** Inactive correspondent indicator; if correspondent inactive
      ** suppress output
     C           Z#INAC    IFEQ 'Y'
     C                     MOVEL'Y'       Z#SUPR
     C                     ENDIF
      *
      ** Get frequency code for schedule
     C********** Z#PSCH    CHAINCGPSCHL0             52                   084316
      *                                                                   087283
      * Look up schedule in array                                         087283
      *                                                                   087283
     C                     Z-ADD1         X       50                      087283
     C           Z#PSCH    LOKUP#PSC,X                   90               087283
      *                                                                   087283
      * If found access details from PSTORE                               087283
      *                                                                   087283
     C           *IN90     IFEQ '1'                                       087283
     C           X         OCUR PSTORE                                    087283
      *                                                                   087283
      * If record found move to CGPSCHPD set 50 off else set 50 on        087283
      *                                                                   087283
     C           ##NFP     IFNE 'Y'                                       087283
     C                     SETOF                     52                   087283
     C                     MOVELPSTORE    CGPSCH                          087283
     C                     ELSE                                           087283
     C                     SETON                     52                   087283
     C                     ENDIF                                          087283
      *                                                                   087283
      * Schedule not in store so chain to find details                    087283
      *                                                                   087283
     C                     ELSE                                           087283
     C           Z#PSCH    CHAINCGPSCHL1             52                   084316
      *                                                                   087283
      * Set up new off set, move contents into array                      087283
      *                                                                   087283
     C                     ADD  1         ##POFS  50                      087283
     C           ##POFS    IFGT 10                                        087283
     C                     Z-ADD1         ##POFS                          087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     Z-ADD##POFS    X                               087283
     C                     MOVEAZ#PSCH    #PSC,X                          087283
     C           X         OCUR PSTORE                                    087283
     C                     MOVEL*BLANKS   PSTORE                          087283
      *                                                                   087283
      * If record found move in CGCORRPD else ##NFC to Y                  087283
      *                                                                   087283
     C           *IN52     IFEQ *OFF                                      087283
     C                     MOVELCGPSCH    PSTORE                          087283
     C                     MOVEL*BLANKS   ##NFP                           087283
     C                     ELSE                                           087283
     C                     MOVEL'Y'       ##NFP                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     ENDIF                                          087283
      *
      ** Database error
     C           *IN52     IFEQ '1'
     C******************** MOVEL'CGPSCHL0'W0FILE                          084316
     C                     MOVEL'CGPSCHL1'W0FILE                          084316
     C                     MOVELZ#PSCH    W0KEY            ***************
     C                     Z-ADD1         W0ERNB           * DB ERROR 01 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *
      ** If frequency code is SUPPRESS, set suppress_output indication,
      ** otherwise determine appropriate status code
     C           STFREQ    IFEQ 'SUPPRESS'
     C                     MOVEL'Y'       Z#SUPR
     C                     ELSE
     C           STFREQ    LOKUPTAB1      TAB2           55
     C           *IN55     IFEQ '1'
     C                     MOVELTAB2      Z#ISTS
     C                     ELSE
     C                     MOVEL'SCHD'    Z#ISTS
     C                     ENDIF
     C                     ENDIF
      *
     C           EXDETL    TAG
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRDEFT                                         *
      * Purpose     :  Derive schedule details for system/branch/     *
      *                entity                                         *
      *                                                               *
      *                This subroutine writes schedule details (D#*)  *
      *****************to*one*of*six*occurrences*of*a*multiple*********   082705
      *                to one of eight occurrences of a multiple      *   082705
      *                occurence data structure:                      *
      *                                                               *
      *                 1  - System                                   *
      *                 2  - System/correspondent schedule details    *
      *                 3  - Branch                                   *
      *                 4  - Branch/correspondent schedule details    *
      ******************5**-*Entity************************************   082705
      ******************6**-*Entity/correspondent schedule details*****   082705
      *                 5  - Correspondent type                       *   082705
      *                 6  - Correspondent type/schedule details      *   082705
      *                 7  - Entity                                   *   082705
      *                 8  - Entity/correspondent schedule details    *   082705
      *                                                               *
      * Called by   :  SRDETL                                         *
      *                SRINIT                                         *
      * Calls       :  -                                              *
      **Parms*IN****:**Z#LEVL*-*1*=>*System;*3*=>*Branch;*5*=>*Entity**   082705
      * Parms IN    :  Z#LEVL - 1 => System; 3 => Branch; 5 => c/type *   082075
      *                7 => entity                                    *   082075
      *                Z#BRCA - Branch code                           *
      *                Z#CUST - Entity: customer number -or-          *
      *                Z#CORR -         correspondent id              *
      *       OUT   :  D#PRTD - Scheduled print date                  *
      *                D#RETP - Retention period                      *
      *                D#LGID - Language id                           *
      *                D#PSCH - Print schedule                        *
      *                D#OVBR - Override branch                       *
      *                Z#INAC - Inactive correspondent indication     *
      *                         (entity level only)                   *
      *****************************************************************
      *
     C           SRDEFT    BEGSR
      *
      ** Access DS occurence for this level, and clear
     C           Z#LEVL    OCUR D#DEFT
     C                     CLEARD#DEFT
      *
      ** Initialise level_found indication
     C                     MOVEL'Y'       ##LFND  1
      *
      ** Access correspondent for approriate level -
      ***System*-*only*if*not*already*accessed*************************   079847
      ** System                                                           079847
     C           Z#LEVL    IFEQ 1
     C********** ##SYST    ANDNE'Y'                                       079847
      *                                                                   079847
     C******************** MOVEL'Y'       ##SYST
      *                                                                   087283
      * Access occurrance number 1 for system details                     087283
      *                                                                   087283
     C           1         OCUR CSTORE                                    087283
     C                     MOVELCSTORE    CGCORR                          087283
      *                                                                   087283
      * If Correspondent blank get details                                087283
      *                                                                   087283
     C           CDCORR    IFNE *BLANKS                                   087283
     C                     MOVELCSTORE    CGCORR                          087283
      *                                                                   087283
      *  Correspondent is blank get details                               087283
      *                                                                   087283
     C                     ELSE                                           087283
     C                     MOVEL'SYSTEM'  W0KEY
     C           '   '     CHAINCGCORRL3             50
      *
      ** Database error
     C           *IN50     IFEQ '1'
     C                     MOVEL'CGCORRL3'W0FILE
     C                     MOVEL'SYSTEM'  W0KEY            ***************
     C                     Z-ADD2         W0ERNB           * DB ERROR 02 *
     C                     MOVEL'MEM5004' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *                                                                   087283
      *  Move in corresondent infromation                                 087283
      *                                                                   087283
     C                     MOVELCGCORR    CSTORE                          087283
     C                     ENDIF                                          087283
      *
     C                     ENDIF
      *
      ** Branch (not mandatory hence no database error processing)
     C           Z#LEVL    IFEQ 3
      *                                                                   087283
      * Look up Branch in array                                           087283
      *                                                                   087283
     C                     Z-ADD1         X       50                      087283
     C           Z#BRCA    LOKUP#BRC,X                   90               087283
      *                                                                   087283
      * If found access details from CSTORE                               087283
      *                                                                   087283
     C           *IN90     IFEQ '1'                                       087283
     C           X         ADD  1         X                               087283
     C           X         OCUR CSTORE                                    087283
      *                                                                   087283
      * If record found move to CGCORRPD set 50 off else set 50 on        087283
      *                                                                   087283
     C           ##NFC     IFNE 'Y'                                       087283
     C                     SETOF                     50                   087283
     C                     MOVELCSTORE    CGCORR                          087283
     C                     ELSE                                           087283
     C                     SETON                     50                   087283
     C                     ENDIF                                          087283
      *                                                                   087283
      * Branch not in store so chain to find details                      087283
      *                                                                   087283
     C                     ELSE                                           087283
     C           Z#BRCA    CHAINCGCORRL2             50
      *                                                                   087283
      * Set up new off set, move contents into array                      087283
      *                                                                   087283
     C                     ADD  1         ##BOFS  50                      087283
     C           ##BOFS    IFGT 10                                        087283
     C                     Z-ADD1         ##BOFS                          087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     Z-ADD##BOFS    X                               087283
     C                     MOVEAZ#BRCA    #BRC,X                          087283
     C           ##BOFS    ADD  1         X                               087283
     C           X         OCUR CSTORE                                    087283
     C                     MOVEL*BLANKS   CSTORE                          087283
      *                                                                   087283
      * If record found move in CGCORRPD else ##NFC to Y                  087283
      *                                                                   087283
     C           *IN50     IFEQ *OFF                                      087283
     C                     MOVELCGCORR    CSTORE                          087283
     C                     MOVEL*BLANKS   ##NFC                           087283
     C                     ELSE                                           087283
     C                     MOVEL'Y'       ##NFC                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     ENDIF                                          087283
      *
      ** if not found, set level_found indication to prevent further
      ** processing
     C           *IN50     IFEQ '1'
     C           4         OCUR D#DEFT
     C                     CLEARD#DEFT
     C                     MOVEL'N'       ##LFND
     C                     ENDIF
      *
     C                     ENDIF
      *                                                                   082705
      ** Correspondent type                                               082705
     C           Z#LEVL    IFEQ 5                                         082705
      *                                                                   087283
      * Look up Type in array                                             087283
      *                                                                   087283
     C                     Z-ADD1         X       50                      087283
     C           Z#CTYP    LOKUP#CTY,X                   90               087283
      *                                                                   087283
      * If found access details from CSTORE                               087283
      *                                                                   087283
     C           *IN90     IFEQ '1'                                       087283
     C           X         ADD  11        X                               087283
     C           X         OCUR CSTORE                                    087283
      *                                                                   087283
      * If record found move to CGCORRPD set 50 off else set 50 on        087283
      *                                                                   087283
     C           ##NFC     IFNE 'Y'                                       087283
     C                     SETOF                     50                   087283
     C                     MOVELCSTORE    CGCORR                          087283
     C                     ELSE                                           087283
     C                     SETON                     50                   087283
     C                     ENDIF                                          087283
      *                                                                   087283
      * Branch not in store so chain to find details                      087283
      *                                                                   087283
     C                     ELSE                                           087283
     C           Z#CTYP    CHAINCGCORRLC             50                   082705
      *                                                                   087283
      * Set up new off set, move contents into array                      087283
      *                                                                   087283
     C                     ADD  1         ##TOFS  50                      087283
     C           ##TOFS    IFGT 10                                        087283
     C                     Z-ADD1         ##TOFS                          087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     Z-ADD##TOFS    X                               087283
     C                     MOVEAZ#CTYP    #CTY,X                          087283
     C           ##TOFS    ADD  11        X                               087283
     C           X         OCUR CSTORE                                    087283
     C                     MOVEL*BLANKS   CSTORE                          087283
      *                                                                   087283
      * If record found move in CGCORRPD else ##NFC to Y                  087283
      *                                                                   087283
     C           *IN50     IFEQ *OFF                                      087283
     C                     MOVELCGCORR    CSTORE                          087283
     C                     MOVEL*BLANKS   ##NFC                           087283
     C                     ELSE                                           087283
     C                     MOVEL'Y'       ##NFC                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     ENDIF                                          087283
     C           *IN50     IFEQ '1'                                       082705
     C           6         OCUR D#DEFT                                    082705
     C                     CLEARD#DEFT                                    082705
     C                     MOVEL'N'       ##LFND                          082705
     C                     ENDIF                                          082705
     C                     ENDIF                                          082705
      *
      ** Entity
     C***********Z#LEVL    IFEQ 5                                         082705
     C           Z#LEVL    IFEQ 7                                         082705
      *
     C           Z#CUST    IFNE *BLANKS
      *                                                                   087283
      * Look up customer in array                                         087283
      *                                                                   087283
     C                     Z-ADD1         X       50                      087283
     C           Z#CUST    LOKUP#CUS,X                   90               087283
      *                                                                   087283
      * If found access details from CSTORE                               087283
      *                                                                   087283
     C           *IN90     IFEQ '1'                                       087283
     C           X         ADD  31        X                               087283
     C           X         OCUR CSTORE                                    087283
      *                                                                   087283
      * If record found move to CGCORRPD set 50 off else set 50 on        087283
      *                                                                   087283
     C           ##NFC     IFNE 'Y'                                       087283
     C                     SETOF                     50                   087283
     C                     MOVELCSTORE    CGCORR                          087283
     C                     ELSE                                           087283
     C                     SETON                     50                   087283
     C                     ENDIF                                          087283
      *                                                                   087283
      * Customer not in store so chain to find details                    087283
      *                                                                   087283
     C                     ELSE                                           087283
     C                     MOVELZ#CUST    W0KEY
     C           Z#CUST    CHAINCGCORRL4             50
      *                                                                   087283
      * Set up new off set, move contents into array                      087283
      *                                                                   087283
     C                     ADD  1         ##COFS  50                      087283
     C           ##COFS    IFGT 20                                        087283
     C                     Z-ADD1         ##COFS                          087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     Z-ADD##COFS    X                               087283
     C                     MOVEAZ#CUST    #CUS,X                          087283
     C           ##COFS    ADD  31        X                               087283
     C           X         OCUR CSTORE                                    087283
     C                     MOVEL*BLANKS   CSTORE                          087283
      *                                                                   087283
      * If record found move in CGCORRPD else ##NFC to Y                  087283
      *                                                                   087283
     C           *IN50     IFEQ *OFF                                      087283
     C                     MOVELCGCORR    CSTORE                          087283
     C                     MOVEL*BLANKS   ##NFC                           087283
     C                     ELSE                                           087283
     C                     MOVEL'Y'       ##NFC                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     ENDIF                                          087283
     C                     ELSE
     C********** Z#CORR    CHAINCGCORRL0             50                   084316
      *                                                                   087283
      * Look up correspondent in array                                    087283
      *                                                                   087283
     C                     Z-ADD1         X       50                      087283
     C           Z#CORR    LOKUP#COR,X                   90               087283
      *                                                                   087283
      * If found access details from CSTORE                               087283
      *                                                                   087283
     C           *IN90     IFEQ '1'                                       087283
     C           X         ADD  51        X                               087283
     C           X         OCUR CSTORE                                    087283
      *                                                                   087283
      * If record found move to CGCORRPD set 50 off else set 50 on        087283
      *                                                                   087283
     C           ##NFC     IFNE 'Y'                                       087283
     C                     SETOF                     50                   087283
     C                     MOVELCSTORE    CGCORR                          087283
     C                     ELSE                                           087283
     C                     SETON                     50                   087283
     C                     ENDIF                                          087283
      *                                                                   087283
      * Customer not in store so chain to find details                    087283
      *                                                                   087283
     C                     ELSE                                           087283
     C           Z#CORR    CHAINCGCORRL1             50                   084316
      *                                                                   087283
      * Set up new off set, move contents into array                      087283
      *                                                                   087283
     C                     ADD  1         ##ROFS  50                      087283
     C           ##ROFS    IFGT 20                                        087283
     C                     Z-ADD1         ##ROFS                          087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     Z-ADD##ROFS    X                               087283
     C                     MOVEAZ#CORR    #COR,X                          087283
     C           ##ROFS    ADD  51        X                               087283
     C           X         OCUR CSTORE                                    087283
     C                     MOVEL*BLANKS   CSTORE                          087283
      *                                                                   087283
      * If record found move in CGCORRPD else ##NFC to Y                  087283
      *                                                                   087283
     C           *IN50     IFEQ *OFF                                      087283
     C                     MOVELCGCORR    CSTORE                          087283
     C                     MOVEL*BLANKS   ##NFC                           087283
     C                     ELSE                                           087283
     C                     MOVEL'Y'       ##NFC                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     ENDIF                                          087283
     C                     MOVELZ#CORR    W0KEY
     C                     ENDIF
      *
      ***Database*error                                                   080068
      ** If no correspondent details set inactive indication and return   080068
     C           *IN50     IFEQ '1'                                       080068
     C                     MOVEL'N'       ##LFND                          080068
     C                     MOVEL'Y'       Z#INAC                          080068
     C********** 6         OCUR D#DEFT                              080068082705
     C           8         OCUR D#DEFT                                    082705
     C                     CLEARD#DEFT                                    080068
     C**********           MOVEL'CGCORRLn'W0FILE           ***************080068
     C**********           Z-ADD3         W0ERNB           * DB ERROR 03 *080068
     C**********           MOVEL'MEM5004' W0MSGD           ***************080068
     C**********           MOVEL'MIDAS  ' W0MSGF                          080068
     C**********           EXSR SRERR                                     080068
     C                     ENDIF                                          080068
     C                     ENDIF
      *
      ** If level found, continue processing
     C           ##LFND    IFEQ 'Y'
      *
      ** Move correspondent id to Z#CORR in order to identify
      ** additional schedule information
     C                     MOVELCDCORR    Z#CORR
      *
      ** Move type id to Z#CTYP for next lookup (level 5)
     C                     MOVELCDCTYP    Z#CTYP
      *
      ** Store correspondent type for this entity
     C                     MOVELCDCTYP    Z#CTYP
      *
      ** For entity level, check correspondent is active. If it is not
      ** set inactive indication and end subroutine
     C           AGRDNB    IFLT CDATDT
     C***********Z#LEVL    ANDEQ5                                         087283
     C           Z#LEVL    ANDEQ7                                         087283
     C           AGRDNB    ORGT CDDADT
     C           *ZEROS    ANDNECDDADT
     C***********Z#LEVL    ANDEQ5                                         087283
     C           Z#LEVL    ANDEQ7                                         087283
      *
     C                     MOVEL'Y'       Z#INAC  1
     C***********6         OCUR D#DEFT                                    082705
     C           8         OCUR D#DEFT                                    082705
     C                     CLEARD#DEFT
      *
     C                     ELSE
      *
      ** Set up defaults at this level
     C                     MOVELCDOBRC    D#OVBR
     C                     Z-ADDCDDPDT    D#PRTD
     C                     Z-ADDCDDRET    D#RETP
     C                     MOVELCDLGID    D#LGID
     C                     MOVELCDDPRT    D#PSCH
      *
      ** Access next occurence of data structure for additional details
     C           Z#LEVL    ADD  1         ##C
     C           ##C       OCUR D#DEFT
     C                     CLEARD#DEFT
      *
      *****************************************************************   080068
      ***Check*whether*the*correspondent*has*any*additional*schedule***   080068
      ***details*for*this*type*of*item.*If*the*account*id*passed*by*the   080068
      ***calling*program*is*non-blank,*check*on*the*basis*of*account***   080068
      ***followed*by*account*code;*if*no*record*found,*or*account*is***   080068
      ***blank,*check*on*type/sub-type.********************************   080068
     C**********           MOVELZ#CORR    ##CORR                          080068
     C**********           MOVELZ#CORR    ##SAME                          080068
     C**********           MOVELDRPTYP    ##PTYP                          080068
     C**********           MOVELDRPSTP    ##PSTP                          080068
      **********                                                          080068
      ***Account*non-blank;*check*on*account,*account*code*************   080068
     C********** DRMACT    IFNE *BLANKS                                   080068
     C**********           MOVELDRMACT    ##MACT                          080068
     C********** ##KSCA    CHAINCGCSCHL3             51                   080068
     C********** *IN51     IFEQ '1'                                       080068
     C********** ##KSCC    CHAINCGCSCHL2             51                   080068
     C**********           ENDIF                                          080068
     C**********           ENDIF                                          080068
      **********                                                          080068
      ***If*no*record*found*for*account,*or*account*is*blank,*check*on*   080068
      ***print*type/sub-type*******************************************   080068
     C********** DRMACT    IFEQ *BLANKS                                   080068
     C********** *IN51     OREQ '1'                                       080068
      **********                                                          080068
     C********** ##KSCT    CHAINCGCSCHL5             51                   080068
      **********                                                          080068
      ***If*none*found,*check*for*generic*entry*-*first*for*sub-type***   080068
      ***only,*then*for*type*and*sub-type******************************   080068
     C********** *IN51     IFEQ '1'                                       080068
     C**********           CLEAR##PTYP                                    080068
     C********** ##KSCT    CHAINCGCSCHL5             51                   080068
     C********** *IN51     IFEQ '1'                                       080068
     C**********           CLEAR##PSTP                                    080068
     C********** ##KSCT    CHAINCGCSCHL5             51                   080068
     C**********           ENDIF                                          080068
     C**********           ENDIF                                          080068
      **********                                                          080068
     C**********           ENDIF                                          080068
      *                                                                   080068
      ** Access best-fit additional details for this entity:              080068
      **  o Set up fixed key values; correspondent and secondary          080068
      **    correspondent and account code (via DS)                       080068
     C                     MOVELZ#CORR    ##CORR                          080068
     C           Z#PRIM    IFEQ *BLANKS                                   080068
     C                     MOVELZ#CORR    ##PCOR                          080068
     C                     ELSE                                           080068
     C                     MOVELZ#PRIM    ##PCOR                          080068
     C                     ENDIF                                          080068
     C                     MOVELDRMACT    ##MACT                          080068
      *                                                                   080068
      **  o Loop using pre-defined search pattern (array SCSQ), filling   080068
      **    elements of the key until a match is found                    080068
     C                     Z-ADD0         #W      50                      080068
     C           #W        DOUEQ4                                         080068
     C           *IN51     OREQ '0'                                       080068
      *                                                                   080068
      **  o increment pointer to next selection, and move to work array   080068
     C                     ADD  1         #W                              080068
     C                     MOVEA#SCSQ,#W  #SCLT                           080068
      *                                                                   080068
      **  o Clear variable key values, and set up from array              080068
     C                     CLEAR#KEY9D                                    080068
     C           #SCLT,1   IFEQ 'Y'                                       080068
     C                     MOVELDRPTYP    ##PTYP                          080068
     C                     ENDIF                                          080068
     C           #SCLT,2   IFEQ 'Y'                                       080068
     C                     MOVELDRPSTP    ##PSTP                          080068
     C                     ENDIF                                          080068
     C           #SCLT,3   IFEQ 'Y'                                       080068
      *                                                                   087283
      * If either no records for account codes or blank exit              087283
      *                                                                   087283
     C           ##L2NR    IFEQ 'None'                                    087283
     C           DRMACT    OREQ *BLANKS                                   087283
     C                     MOVEL'1'       *IN51                           087283
     C                     ITER                                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     MOVEL##ACOD    ##ACCD                          080068
     C                     ENDIF                                          080068
     C           #SCLT,4   IFEQ 'Y'                                       080068
      *                                                                   087283
      * If either no records for account codes or blank exit              087283
      *                                                                   087283
     C           ##L3NR    IFEQ 'None'                                    087283
     C           DRMACT    OREQ *BLANKS                                   087283
     C                     MOVEL'1'       *IN51                           087283
     C                     ITER                                           087283
     C                     ENDIF                                          087283
      *                                                                   087283
     C                     MOVELDRMACT    K#MACT                          080068
     C                     ENDIF                                          080068
      *                                                                   080068
      **  o Access file                                                   080068
     C           ##KSCH    CHAINCGCSCHL9             51                   080068
      *                                                                   080068
     C                     ENDDO                                          080068
      *                                                                   080068
      ** If record found on schedule details set up defaults in data
      ** structure occurence
     C           *IN51     IFEQ '0'
     C                     MOVELCPAPDF    D#APDF
     C******************** Z-ADDCDDPDT    D#PRTD                          082705
     C                     Z-ADDCPNPDT    D#PRTD                          082705
     C                     Z-ADDCPRETP    D#RETP
     C                     MOVELCPLGID    D#LGID
     C                     MOVELCPPSCH    D#PSCH
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           EXDEFT    TAG
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRIDGN                                         *
      * Purpose     :  Generate next item reference number            *
      *                                                               *
      * Called by   :  SRPRIM                                         *
      *                SRSECO                                         *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           SRIDGN    BEGSR
      *
      ** Get last item reference from data area, increment to create
      ** new item reference, and write back to data area. If data area
      ** is blank, start at 1.
     C           *NAMVAR   DEFN CGDTA     CGDATA
     C           *LOCK     IN   CGDATA
     C                     MOVELDAITMA    ##ITMA
     C           ##ITMA    IFEQ *BLANKS
     C                     Z-ADD1         ##ITMN
     C                     ELSE
     C                     ADD  1         ##ITMN
     C                     ENDIF
     C                     MOVEL##ITMA    DAITMA
     C                     OUT  CGDATA
     C                     MOVEL##ITMA    ##ITEM
      *
      ** Set item_id_generated indication
     C                     MOVEL'Y'       ##IDGN
      *
     C           EXIDGN    TAG
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Purpose     :  Initial processing                             *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Access run-date
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
      *
      ** Initialise system_level_already_accessed indication
     C                     MOVEL'N'       ##SYST  1
      *
      ** Open appropriate version of reference file, depending on
      ** whether commitment control is active or not
     C           ##CMT     IFEQ 'YES'
     C                     OPEN CGUDCRPD
     C                     ELSE
     C                     OPEN EXUDCRPD
     C                     ENDIF
      *
      ** Set up copyright statement
     C                     MOVE CPYR@#    ACT@   80
      *
      ** Key lists:
      **  o additional schedule information; print item type/sub-type
     C***********##KSCT*** KLIST                                          080068
     C******************** KFLD           ##CORR                          080068
     C******************** KFLD           ##PTYP                          080068
     C******************** KFLD           ##PSTP                          080068
     C******************** KFLD           ##SAME                          080068
      *
      **  o additional schedule information; account id
     C***********##KSCA*** KLIST                                           08068
     C******************** KFLD           ##MACT                           08068
     C******************** KFLD           ##CORR                           08068
     C******************** KFLD           ##PTYP                           08068
     C******************** KFLD           ##PSTP                           08068
      *
      ****o*additional*schedule information; account code                  08068
     C***********##KSCC*** KLIST                                           08068
     C******************** KFLD           ##ACOD                           08068
     C******************** KFLD           ##CORR                           08068
     C******************** KFLD           ##PTYP                           08068
     C******************** KFLD           ##PSTP                           08068
      *
      **  o additional schedule information for correspondent
     C           ##KSCH    KLIST
     C                     KFLD           ##CORR
     C                     KFLD           ##PTYP
     C                     KFLD           ##PSTP
     C                     KFLD           ##ACCD
     C                     KFLD           K#MACT
     C                     KFLD           ##PCOR
      *
      **  o additional schedule information; secondary recipients
      **    by print type/sub-type
     C           ##KSTS    KLIST
     C******************** KFLD           ##PRIM                          087834
     C******************** KFLD           ##PTYP                          087834
     C******************** KFLD           ##PSTP                          087834
     C                     KFLD           S#PRIM                          087834
     C                     KFLD           S#PTYP                          087834
     C                     KFLD           S#PSTP                          087834
      *
      **  o additional schedule information; secondary recipients
      **    by account id
     C           ##KSAC    KLIST
     C******************** KFLD           ##MACT                          087834
     C                     KFLD           S#MACT                          087834
      *                                                                   082705
      **  o UDC reference file; to check for duplicates                   082705
     C           ##KUDC    KLIST                                          082705
     C                     KFLD           DRITEM                          082705
     C                     KFLD           DRDCOR                          082705
      *
     C           KMACT     KLIST                                          CCG024
     C                     KFLD           ##CNUM                          CCG024
     C                     KFLD           ##CCY                           CCG024
     C                     KFLD           ##ACO                           CCG024
     C                     KFLD           ##ACSQ                          CCG024
     C                     KFLD           ##BRCA                          CCG024
      *                                                                   CCG024
     C           KLOAN     KLIST                                          CCG024
     C                     KFLD           KLNRF   6                       CCG024
     C                     KFLD           KRCDT   1                       CCG024
      *                                                                   CCG024
      ** *LIKE definitions
     C           *LIKE     DEFN DRDCOR    Z#PRIM
     C           *LIKE     DEFN DRPRTD    Z#PRTD
     C           *LIKE     DEFN DRDDAT    Z#RETP
     C           *LIKE     DEFN DRLGID    Z#LGID
     C           *LIKE     DEFN CPPSCH    Z#PSCH
     C           *LIKE     DEFN DROVBR    Z#OVBR
     C           *LIKE     DEFN DRBRCA    Z#BRCA
     C           *LIKE     DEFN ##CUST    Z#CUST
     C           *LIKE     DEFN DRDCOR    Z#CORR
     C           *LIKE     DEFN DRISTS    Z#ISTS
     C           *LIKE     DEFN DRDCOR    Z#CUID
     C           *LIKE     DEFN DRDCOR    ##CORR
     C           *LIKE     DEFN DRDCOR    ##PCOR
     C           *LIKE     DEFN DRDCOR    ##PRIM
     C           *LIKE     DEFN DRDCOR    ##SAME
     C           *LIKE     DEFN CDCTYP    Z#CTYP                          082075
     C           *LIKE     DEFN DRDCOR    D#CORR                          087834
     C           *LIKE     DEFN DRPTYP    S#PTYP                          087834
     C           *LIKE     DEFN DRPSTP    S#PSTP                          087834
     C           *LIKE     DEFN DRMACT    S#MACT                          087834
     C           *LIKE     DEFN DRPCOR    S#PRIM                          087834
      *                                                                   087283
      * Check if any schedules for account code                           087283
      *                                                                   087283
     C           *LOVAL    SETLL@CSCHL2                                   087283
     C                     READ @CSCHL2                  90               087283
     C                     MOVEL*BLANKS   ##L2NR  4                       087283
     C           *IN90     IFEQ *ON                                       087283
     C                     MOVEL'None'    ##L2NR                          087283
     C                     ENDIF                                          087283
      *                                                                   087283
      * Check if any schedules for account                                087283
      *                                                                   087283
     C           *LOVAL    SETLL@CSCHL3                                   087283
     C                     READ @CSCHL3                  90               087283
     C                     MOVEL*BLANKS   ##L3NR  4                       087283
     C           *IN90     IFEQ *ON                                       087283
     C                     MOVEL'None'    ##L3NR                          087283
     C                     ENDIF                                          087283
      *                                                                   CCG004
      * Check if copy users are present                                   CCG004
      *                                                                   CCG004
     C                     MOVEL'COPY'    CDCORR    P                     CCG004
     C           CDCORR    SETLL@CORRL1                                   CCG004
     C                     READ @CORRL1                  90               CCG004
      *                                                                   CCG004
      * If found check that 'COPY   ' is in record read                   CCG004
      *                                                                   CCG004
     C                     SELEC                                          CCG004
     C           *IN90     WHEQ *OFF                                      CCG004
     C           7         SUBSTCDCORR:1  ####7A  7                       CCG004
     C           ####7A    IFEQ 'COPY   '                                 CCG004
      *                                                                   CCG004
      **  Determine which SARs are in effect ...                          CCG004
      **  ... CCG004 - Copy by System                                     CCG004
     C                     CALL 'AOSARDR0'             91                 CCG004
     C                     PARM '       ' ##RTCD  7                       CCG004
     C                     PARM '*VERIFY' ##OPTN  7                       CCG004
     C                     PARM 'CCG004'  ##SARD  6                       CCG004
     C           SCSARD    PARM *BLANKS   DSSDY                           CCG004
      *                                                                   CCG004
      **  If the Access Object returns an error code, process a           CCG004
      **  database error.                                                 CCG004
     C           ##RTCD    IFNE *BLANK                                    CCG004
     C           ##RTCD    ANDNE'*NRF   '                                 CCG004
     C           *IN91     OREQ *ON                                       CCG004
     C                     MOVEL'AOSARDR0'W0FILE           ***************CCG004
     C                     MOVEL'CCG004'  W0KEY            * DB ERROR 04 *CCG004
     C                     Z-ADD04        W0ERNB           ***************CCG004
     C                     MOVEL'MEM5003' W0MSGD                          CCG004
     C                     MOVEL'MIDAS  ' W0MSGF                          CCG004
     C                     EXSR SRERR                                     CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
     C           ##RTCD    IFEQ *BLANK                                    CCG004
     C                     MOVEL'SYSTEM'  ##COPY  8                       CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
      *                                                                   CCG004
      **  Determine which SARs are in effect ...                          CCG004
      **  ... CCG005 - Copy by Branch                                     CCG004
     C                     CALL 'AOSARDR0'             91                 CCG004
     C                     PARM '       ' ##RTCD                          CCG004
     C                     PARM '*VERIFY' ##OPTN                          CCG004
     C                     PARM 'CCG005'  ##SARD                          CCG004
     C           SCSARD    PARM *BLANKS   DSSDY                           CCG004
      *                                                                   CCG004
      **  If the Access Object returns an error code, process a           CCG004
      **  database error.                                                 CCG004
     C           ##RTCD    IFNE *BLANK                                    CCG004
     C           ##RTCD    ANDNE'*NRF   '                                 CCG004
     C           *IN91     OREQ *ON                                       CCG004
     C                     MOVEL'AOSARDR0'W0FILE           ***************CCG004
     C                     MOVEL'CCG005'  W0KEY            * DB ERROR 05 *CCG004
     C                     Z-ADD05        W0ERNB           ***************CCG004
     C                     MOVEL'MEM5003' W0MSGD                          CCG004
     C                     MOVEL'MIDAS  ' W0MSGF                          CCG004
     C                     EXSR SRERR                                     CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
     C           ##RTCD    IFEQ *BLANK                                    CCG004
     C                     MOVEL'BRANCH'  ##COPY                          CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
      *                                                                   CCG004
      **  Determine which SARs are in effect ...                          CCG004
      **  ... CCG006 - Copy by Correspondent Type                         CCG004
     C                     CALL 'AOSARDR0'             91                 CCG004
     C                     PARM '       ' ##RTCD                          CCG004
     C                     PARM '*VERIFY' ##OPTN                          CCG004
     C                     PARM 'CCG006'  ##SARD                          CCG004
     C           SCSARD    PARM *BLANKS   DSSDY                           CCG004
      *                                                                   CCG004
      **  If the Access Object returns an error code, process a           CCG004
      **  database error.                                                 CCG004
     C           ##RTCD    IFNE *BLANK                                    CCG004
     C           ##RTCD    ANDNE'*NRF   '                                 CCG004
     C           *IN91     OREQ *ON                                       CCG004
     C                     MOVEL'AOSARDR0'W0FILE           ***************CCG004
     C                     MOVEL'CCG006'  W0KEY            * DB ERROR 06 *CCG004
     C                     Z-ADD06        W0ERNB           ***************CCG004
     C                     MOVEL'MEM5003' W0MSGD                          CCG004
     C                     MOVEL'MIDAS  ' W0MSGF                          CCG004
     C                     EXSR SRERR                                     CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
     C           ##RTCD    IFEQ *BLANK                                    CCG004
     C                     MOVEL'CORTYPE' ##COPY                          CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
      * If ##COPY not blank then copy is being used                       CCG004
      *                                                                   CCG004
     C           ##COPY    IFNE *BLANK                                    CCG004
     C                     MOVEL'Yes'     ##CUSR  3                       CCG004
     C                     ELSE                                           CCG004
     C                     MOVEL'No '     ##CUSR                          CCG004
     C                     END                                            CCG004
      *                                                                   CCG004
     C                     ELSE                                           CCG004
     C                     MOVEL'No '     ##CUSR                          CCG004
     C                     ENDIF                                          CCG004
     C           *IN90     WHEQ *ON                                       CCG004
     C                     MOVEL'No '     ##CUSR                          CCG004
     C                     ENDSL                                          CCG004
      *                                                                                       CCG024
      * Check if CCG024 is switched on                                                        CCG024
     C                     CALL 'AOSARDR0'                                                    CCG024
     C                     PARM           ##RTCD                                              CCG024
     C                     PARM '*VERIFY' ##OPTN                                              CCG024
     C                     PARM 'CCG024'  ##SARD                                              CCG024
      *                                                                                       CCG024
     C           ##RTCD    IFEQ *BLANKS                                                       CCG024
     C                     MOVE 'Y'       CCG024  1                                           CCG024
      *                                                                                       CCG024
      * Check System Value RecipientSpecialChar                                               CCG024
     C                     CALL 'AOSVALR0'                                                    CCG024
     C                     PARM *BLANKS   RTNCDE  7                                           CCG024
     C                     PARM           SVALK1 20                                           CCG024
     C                     PARM           SVAL1 200                                           CCG024
     C                     PARM *BLANKS   SVALK2 20                                           CCG024
     C                     PARM           SVAL2 200                                           CCG024
     C                     PARM *BLANKS   SVALK3 20                                           CCG024
     C                     PARM           SVAL3 200                                           CCG024
     C                     PARM *BLANKS   SVALK4 20                                           CCG024
     C                     PARM           SVAL4 200                                           CCG024
     C                     PARM *BLANKS   SVALK5 20                                           CCG024
     C                     PARM           SVAL5 200                                           CCG024
     C                     PARM *BLANKS   SVALK6 20                                           CCG024
     C                     PARM           SVAL6 200                                           CCG024
     C                     PARM *BLANKS   SVALK7 20                                           CCG024
     C                     PARM           SVAL7 200                                           CCG024
     C                     PARM *BLANKS   SVALK8 20                                           CCG024
     C                     PARM           SVAL8 200                                           CCG024
     C                     PARM *BLANKS   SVALK9 20                                           CCG024
     C                     PARM           SVAL9 200                                           CCG024
     C                     PARM *BLANKS   SVALK0 20                                           CCG024
     C                     PARM           SVAL10200                                           CCG024
      *                                                                                       CCG024
     C           RTNCDE    IFNE *BLANKS                                                       CCG024
     C           SVAL1     IFEQ '*NRF'                                                        CCG024
     C                     MOVE SVALK1    DBKEY                                               CCG024
     C                     ENDIF                                                              CCG024
      *                                                                                       CCG024
     C                     MOVEL'AOSVALR0'W0FILE           ***************                    CCG024
     C                     MOVEL'*CALL'   W0KEY            * DB ERROR 14 *                    CCG024
     C                     Z-ADD21        W0ERNB           ***************                    CCG024
     C                     MOVEL'MEM5003' W0MSGD                                              CCG024
     C                     MOVEL'MIDAS  ' W0MSGF                                              CCG024
     C                     EXSR SRERR                                                         CCG024
     C                     ENDIF                                                              CCG024
      *                                                                                       CCG024
     C                     ELSE                                                               CCG024
     C                     MOVE 'N'       CCG024                                              CCG024
     C                     ENDIF                                                              CCG024
      *
     C           EXINIT    TAG
     C                     ENDSR
      /EJECT
      /COPY CGCPYSRC,SRERRC
      /COPY CGCPYSRC,SRERRPSSR
** CPY@ Copyright statement
(c) Misys International Banking Systems Ltd. 2001
** TAB1 TAB2 (Print schedule v item status)
IMMEDIATE IMED
HOLDMAIL  HELD
** #SCSQ Search criteria for schedule details
YYNY
YYYN
YYNN
YNNN
