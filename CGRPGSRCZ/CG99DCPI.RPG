     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Calcul current principal/interest of deal')   *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                          *
     F*                                                               *
     F*  CG99DCPI - Calculate Current Principal/Interest of Deal      *
     F*                                                               *
     F*  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
     F*  (phase(s))                                                   *
     F*                                                               *
     F*  Called By: mmCnnnn - (program name)                          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD047993           Date 23Sep17               *
      *                 MD035545           Date 11Sep15               *
      *                 MD031353           Date 13Feb15               *
      *                 CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 247544             Date 16May07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CIR013             Date 25Apr05               *
      *                 CDL038             Date 10May05               *
      *                 CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 182370             Date 05Jun01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR004             Date 20Jan00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 088644             Date 31May95               *
      *                 S01522             Date 22Nov94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  MD047993 - Modified MD035545. Removed dependency on Accrue   *
      *             on Last Day indicator.                            *
      *  MD035545 - LE: Interest Calculation in Leap year. Consider   *
      *             Accrue on Last Day Indicator for method 6.        *
      *  MD031353 - CM Interest advice of non-taxable call deposit    *
      *             000129 has wrong/strange gross interest           *
      *           - make sure to get the current base rate and spread *
      *             rate of the deal                                  *
      *  CDL094 - Enhance Receive Settlement Instructions             *
      *           (Recompiled)                                        *
      *  247544 - Increase definition of W@NLP2 to match w/ ZINTDYZ2  *
      *           processing as amended by 242286.  (Recompile)       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *           Recompile over ZDLINT and ZCBDYS.                   *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  182370 - Incorrect calculation of number of days for calcu-  *
      *           lation basis 6. Amended /COPY such that for calcu-  *
      *           lation basis 6, the number of days per year is      *
      *           equal to 366 when the year in question is a leap    *
      *           year, and 365 days/year for non-leap years. Calcu-  *
      *           lation was made so that processing would be in-line *
      *           with that of /COPY ZINTDY.                          *
      *  CIR004 - Dealing Calculation Method Change.                  *
      *         - Recompiled due to ZCBDYS changes                    *
     F*  088644 - Ignore all but live records                         *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     FDEAMS   IF  E           K        DISK
     F                                              KINFSR SRFILE
     F/COPY WNCPYSRC,CG99DCPIF1
     F/SPACE 5
     E/COPY CGCPYSRC,SRERRE
     E/COPY ZSRSRC,ZDATE2Z1
     E*
     E                    ZF29   14  32  5 0             29TH FEB ARRAY
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     E/COPY ZSRSRC,ZDATE9Z1                                               182370
     E/COPY ZSRSRC,ZDAT10Z1                                               182370
     E/SPACE 5
     I***SDGELR*   E DSSDGELRPD                                                    MD035545 MD047993
     I            DS
     I                                        1   80VDSN
     I                                        1   50VDAT
     I                                        6   80DASN
     ICPYR        DS
      **  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
      *                                                                   182370
      ** Data structure for subroutine ZCBDYS                             182370
     I            DS                                                      182370
     I                                        1   80ZZDAT1                182370
     I                                        1   40ZZYYA                 182370
     I                                        5   60ZZMMA                 182370
     I                                        7   80ZZDDA                 182370
      *                                                                   182370
     I            DS                                                      182370
     I                                        1   80ZZDAT2                182370
     I                                        1   40ZZYYB                 182370
     I                                        5   60ZZMMB                 182370
     I                                        7   80ZZDDB                 182370
      *                                                                   182370
     I              'LECalcBasis6LeapYear'C         WPARML                                  MD035545
     I/COPY CGCPYSRC,SRERRI
     I/COPY ZSRSRC,ZDATE9Z2                                               182370
     I/SPACE 5
     I*                                                                                     MD031353
     ISDBSRT    E DSSDBSRTPD                                                                MD031353
      **  External DS for base rate details                                                 MD031353
      *                                                                                     MD031353
     IDSSDY     E DSDSSDY                                                                   MD031353
      **  Second DS for access programs, long data structure                                MD031353
      *                                                                                     MD031353
     C*****************************************************************
      *
      **  Parameter list for current program invocation.
     C           *ENTRY    PLIST
     C                     PARM           ##DLNO  60
     C                     PARM           ##VDAT  50
     C                     PARM           ##DASN  30
     C                     PARM           ##NIDT  50
     C                     PARM           ##IACD  50
     C                     PARM           ##AITC 130
     C                     PARM           ##IPRD 130
     C                     PARM           ##ICTD 130
     C                     PARM           ##BRTT  20                                        MD031353
     C                     PARM           ##BRTE 117                                        MD031353
     C                     PARM           ##RTSP 117                                        MD031353
     C                     PARM           ##INTR 117
     C                     PARM           ##ICBS  10
     C                     PARM           ##PCPL 150       * Principal
     C                     PARM           ##INTA 130       * Interest
     C                     PARM           ##RTCD  7        * Return Code
      *
      ** Initial processing.
      *
     C                     EXSR SRINIT
      *
      ** Calculation of current principal and interest
      *
     C                     EXSR SRDCPI
      *
     C                     SETON                     LR
     C                     RETRN
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRDCPI                                         *
      * Function    :  Calculation of current principal and interest  *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
     C*****************************************************************
     C           SRDCPI    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRDCPI'  @STK,Q
      *
      *................................................................
      *
     C                     MOVEL##VDAT    ##VDSN  80
     C                     MOVE ##DASN    ##VDSN
     C                     MOVEL##ICBS    ZCBCBS  10
     C*
     C           DEAMSK    KLIST
     C                     KFLD           ##DLNO
     C                     KFLD           ##IACD
     C*
     C           DEAMSK    SETLLDEAMSDIF
     C           ##DLNO    READEDEAMS                    99*
      *
      * Whilst deal amendments can be found, apply the changes
      *
     C           *IN99     DOWEQ'0'
     C           VDSN      ANDLT##VDSN
      *                                                                   088644
      * Process live records only                                         088644
      *                                                                   088644
     C           RECI      IFEQ 'D'                                       088644
      *                                                                                     MD031353
      * Get the current base rate                                                           MD031353
      *                                                                                     MD031353
     C           ##BRTT    IFNE 0                                                           MD031353
     C                     MOVELCCY       ##CYCD                                            MD031353
     C                     MOVEL##BRTT    ##BSRC                                            MD031353
     C                     CALL 'AOBSRTR0'                                                  MD031353
     C                     PARM *BLANKS   ##RTCD  7                                         MD031353
     C                     PARM '*KEY   ' ##OPTN  7                                         MD031353
     C                     PARM           ##CYCD  3                                         MD031353
     C                     PARM           ##BSRC  2                                         MD031353
     C           SDBSRT    PARM SDBSRT    DSSDY                                             MD031353
      *                                                                                     MD031353
      ** DATABASE ERROR                                                                     MD031353
      *                                                                                     MD031353
     C           ##RTCD    IFNE *BLANK                                                      MD031353
     C                     MOVEL'SDBSRTPD'W0FILE                                            MD031353
     C                     MOVELBRTT      W0KEY                                             MD031353
     C                     Z-ADD1         W0ERNB                                            MD031353
     C                     MOVEL'MEM5004' W0MSGD                                            MD031353
     C                     MOVEL'MIDAS  ' W0MSGF                                            MD031353
     C                     EXSR SRERR                                                       MD031353
     C                     ELSE                                                             MD031353
     C                     Z-ADDBANBRT    ##BRTE                                            MD031353
     C                     ENDIF                                                            MD031353
     C                     ENDIF                                                            MD031353
      *
      * Calculate the interest between the control date and
      * the value date of the deal amendment
     C*
     C           VDAT      IFGT ##IACD
     C                     Z-ADD##IACD    ZCBSDT
     C                     Z-ADDVDAT      ZCBEDT
     C                     Z-ADD##PCPL    ZDLAMT 130
     C                     Z-ADD##INTR    ZDLRAT
     C/COPY WNCPYSRC,CG99DCPIC4
     C                     EXSR ZDLINT
     C/COPY WNCPYSRC,CG99DCPIC5
     C                     ADD  ZDLOUT    ##AITC
     C                     Z-ADDVDAT      ##IACD
     C                     END
     C*                                                                                     MD031353
     C* Base Rate Change, get the new base rate                                             MD031353
     C*                                                                                     MD031353
     C           AMTP      IFEQ 'BC'                                                        MD031353
     C                     MOVELCCY       ##CYCD                                            MD031353
     C                     MOVELBRTT      ##BSRC                                            MD031353
     C                     CALL 'AOBSRTR0'                                                  MD031353
     C                     PARM *BLANKS   ##RTCD                                            MD031353
     C                     PARM '*KEY   ' ##OPTN                                            MD031353
     C                     PARM           ##CYCD                                            MD031353
     C                     PARM           ##BSRC                                            MD031353
     C           SDBSRT    PARM SDBSRT    DSSDY                                             MD031353
      *                                                                                     MD031353
      ** DATABASE ERROR                                                                     MD031353
      *                                                                                     MD031353
     C           ##RTCD    IFNE *BLANK                                                      MD031353
     C                     MOVEL'SDBSRTPD'W0FILE                                            MD031353
     C                     MOVELBRTT      W0KEY                                             MD031353
     C                     Z-ADD2         W0ERNB                                            MD031353
     C                     MOVEL'MEM5004' W0MSGD                                            MD031353
     C                     MOVEL'MIDAS  ' W0MSGF                                            MD031353
     C                     EXSR SRERR                                                       MD031353
     C                     ELSE                                                             MD031353
     C                     Z-ADDBANBRT    ##BRTE                                            MD031353
     C                     ENDIF                                                            MD031353
     C                     ENDIF                                                            MD031353
     C*
     C* Principal increase, add deal amendment amount to principal
     C*
     C           AMTP      IFEQ 'PI'
     C                     ADD  DAAM      ##PCPL
     C                     END
     C*
     C* Principal decrease, sub deal amendment amount from principal
     C*
     C           AMTP      IFEQ 'PD'
     C                     SUB  DAAM      ##PCPL
     C                     END
     C*
     C* If the deal amendment is a rate change, change the interest rate
     C*
     C           AMTP      IFEQ 'SC'
     C**********           Z-ADDRTSP      ##INTR                                            MD031353
     C                     Z-ADDRTSP      ##RTSP                                            MD031353
     C                     END
     C*
     C* If the deal amendment is settle interest
     C*
     C           AMTP      IFEQ 'SI'
     C                     Z-ADD##AITC    ##INTD
     C                     SUB  ##IPRD    ##INTD
     C                     SUB  ##ICTD    ##INTD
      *
     C                     ADD  ##INTD    ##IPRD
     C                     END
     C*
     C* If the deal amendment is capitalize interest
     C*
     C           AMTP      IFEQ 'CI'
     C                     Z-ADD##AITC    ##INTD
     C                     SUB  ##IPRD    ##INTD
     C                     SUB  ##ICTD    ##INTD
      *
     C                     ADD  ##INTD    ##ICTD
     C                     ADD  ##INTD    ##PCPL
     C                     END
      *                                                                   088644
     C                     ENDIF                                          088644
     C*
     C* Read next record
     C*
     C           ##DLNO    READEDEAMS                    99
     C                     END
      *                                                                                     MD031353
      ** add the base rate and spread rate                                                  MD031353
      *                                                                                     MD031353
     C           ##BRTE    ADD  ##RTSP    ##INTR                                            MD031353
     C*
      * Calculate the interest between the control date and
      * the event date
     C*
     C           ##VDAT    IFGT ##IACD
     C                     Z-ADD##IACD    ZCBSDT
     C                     Z-ADD##VDAT    ZCBEDT
     C                     Z-ADD##PCPL    ZDLAMT
     C                     Z-ADD##INTR    ZDLRAT
     C/COPY WNCPYSRC,CG99DCPIC1
     C                     EXSR ZDLINT
     C/COPY WNCPYSRC,CG99DCPIC2
     C                     ADD  ZDLOUT    ##AITC
     C                     Z-ADD##VDAT    ##IACD
     C                     END
      *
      * The interest due at the event date is the interest to the event date
      * minus the interest paid/received and capitalized to date
      *
     C                     Z-ADD##AITC    ##INTA
     C                     SUB  ##IPRD    ##INTA
     C                     SUB  ##ICTD    ##INTA
      *
      *................................................................
      *
     C           EXCINT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * Subroutine  :  SRINIT                                         *
      * Function    :  Initial processing                             *
      *                                                               *
      * Called by   :  Mainline                                       *
      * Calls       :                                                 *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      **  Set up subroutine stack name
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C                     SETOF                     98
     C*
     C           *LIKE     DEFN ##IPRD    ##INTD
      *****************************************************************            MD035545 MD047993
      ***Call*access*program*for*General*Ledger*details.***************            MD035545 MD047993
      *****************************************************************            MD035545 MD047993
     C**********           CALL 'AOGELRR1'                                         MD035545 MD047993
     C**********           PARM *BLANKS   ##RTCD  7                                MD035545 MD047993
     C**********           PARM '*FIRST  '##OPTN  7                                MD035545 MD047993
     C********** SDGELR    PARM SDGELR    DSSDY                                    MD035545 MD047993
      *                                                                                     MD035545
     C********** ##RTCD    IFNE *BLANK                                             MD035545 MD047993
      *****************************************************************            MD035545 MD047993
      ***Database error************************************************            MD035545 MD047993
      *****************************************************************            MD035545 MD047993
     C**********           MOVEL'AOGELRR0'W0FILE                                   MD035545 MD047993
     C**********           MOVEL'*CALL'   W0KEY                                    MD035545 MD047993
     C**********           Z-ADD20        W0ERNB                                   MD035545 MD047993
     C**********           MOVEL'MEM5003' W0MSGD                                   MD035545 MD047993
     C**********           MOVEL'MIDAS  ' W0MSGF                                   MD035545 MD047993
     C**********           EXSR SRERR                                              MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
      *                                                                                     MD035545
      ** Retrieve system value setting                                                      MD035545
      *                                                                                     MD035545
     C                     MOVELWPARML    P@OP01                                            MD035545
     C                     CALL 'AOSVALR0'                                                  MD035545
     C                     PARM *BLANKS   W0RTCD  7                                         MD035545
     C                     PARM           P@OP01 20                                         MD035545
     C                     PARM           P@VL01200                                         MD035545
     C                     PARM           P@OP02 20                                         MD035545
     C                     PARM           P@VL02200                                         MD035545
     C                     PARM           P@OP03 20                                         MD035545
     C                     PARM           P@VL03200                                         MD035545
     C                     PARM           P@OP04 20                                         MD035545
     C                     PARM           P@VL04200                                         MD035545
     C                     PARM           P@OP05 20                                         MD035545
     C                     PARM           P@VL05200                                         MD035545
     C                     PARM           P@OP06 20                                         MD035545
     C                     PARM           P@VL06200                                         MD035545
     C                     PARM           P@OP07 20                                         MD035545
     C                     PARM           P@VL07200                                         MD035545
     C                     PARM           P@OP08 20                                         MD035545
     C                     PARM           P@VL08200                                         MD035545
     C                     PARM           P@OP09 20                                         MD035545
     C                     PARM           P@VL09200                                         MD035545
     C                     PARM           P@OP10 20                                         MD035545
     C                     PARM           P@VL10200                                         MD035545
      *                                                                                     MD035545
      ** If system value is not found then default value to 'N'                             MD035545
      *                                                                                     MD035545
     C           W0RTCD    IFNE '       '                                                   MD035545
     C                     MOVE 'N'       WCALC6                                            MD035545
     C                     ELSE                                                             MD035545
     C                     MOVELP@VL01    WCALC6                                            MD035545
     C********** WCALC6    IFEQ 'Y'                                                MD035545 MD047993
     C********** BKALDI    ANDEQ'Y'                                                MD035545 MD047993
     C**********           MOVE 'Y'       WCALC6                                   MD035545 MD047993
     C**********           ELSE                                                    MD035545 MD047993
     C**********           MOVE 'N'       WCALC6                                   MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
     C                     ENDIF                                                            MD035545
      *                                                                                     MD035545
     C/COPY WNCPYSRC,CG99DCPIC3
      *
      *................................................................
      *
     C           EXINIT    TAG
      *
      **  Unwind subroutine stack name
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
     C/COPY ZSRSRC,ZDLINTZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZCBDYSZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE2Z2
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRPSSR
     C/SPACE 5
     C/COPY CGCPYSRC,SRERRC
     C/COPY ZSRSRC,ZDATE9Z3                                               182370
     C/COPY ZSRSRC,ZDAT10Z2                                               182370
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
     X/COPY ZSRSRC,ZDATE9Z4                                               182370
