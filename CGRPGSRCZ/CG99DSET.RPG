     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Determine Settlement Instruction Fields')     *
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                      *
     F*                                                               *
     F*  CG99DSET - Determine Settlement Instruction Fields           *
     F*                                                               *
     F*  Function:  To determine the settlement instruction fields    *
     F*             associated with pay/receive instructions          *
     F*                                                               *
     F*  Called By:                                                   *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CRE075             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 BUG7326            Date 31May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE033             Date 02Apr03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 186561             Date 18May01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 163730             Date 05Apr00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 142979             Date 28Apr99               *
     F*                 081964             Date 12Jan95               *
     F*                 S01522             DATE 22NOV94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  BUG7326 - General ledger account incomplete in element       *
      *            <SaleReceiveOurXGeneralLedger> and <SalePayOur     *
      *            XGeneralLedger>. This is due to field ##OGA        *
      *            still 20-char long and not expanded to 24 chars    *
      *            (missed out by CGL029). Also note length of RDEs   *
      *            S_RECOURXG and S_PAYOURXG was increased from 20    *
      *            to 24.                                             *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CLE033 - Settlement method 09                                *
      *  186561 - Account currency and sequence incorrectly extracted.*
      *           Applied fixes 162009, 163703, 173918 and 161405.    *
     F*  163730 - Allow Retail Account No to be extracted when        *
     F*           Settlement Type is '04'.                            *
     F*  142979 - Applied fix 098429:                                 *
     F*           Counterparty Nostro Name returned with only 20 char.*
     F*  081964 - User Defined Correspondence - Additions             *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     E*
     E**  Copyright array.
     E                    ##CPY   1   1 80
     ISDNOST    E DSSDNOSTPD
     I** EXTERNAL DS FOR NOSTRO DETAILS
     ISDCUST    E DSSDCUSTPD
     I** EXTERNAL DS FOR CUSTOMER DETAILS
     ISDCNST    E DSSDCNSTPD
     I** EXTERNAL DS FOR COUNTERPARTY NOSTRO DETAILS
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     IDSSDY     E DSDSSDY
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     ISDACNT    E DSACCNTAB
     I** EXTERNAL DS FOR ACCOUNT DETAILS
     I            DS
     I*  Settlement Account:
     I*  Nostro, Customer/Account code/Account seq,  Retail A/c No. + Branch
     I**********                              1  15 ##SETF                                    CGL029
     I                                        1  21 ##SETF                                    CGL029
     I                                        1   3 ##BRCD
     I**********                              4  15 ##SETA                                    CGL029
     I                                        4  21 ##SETA                                    CGL029
     I                                        4   5 ##NOST
     I                                        4   9 ##CUST
     I**********                             10  13 ##ACOD                                    CGL029
     I**********                             14  15 ##ACSQ                                    CGL029
     I                                       10  19 ##ACOD                                    CGL029
     I                                       20  21 ##ACSQ                                    CGL029
     I            DS
     I                                        1  10 ##RETL
     I            DS                                                      186561
      *                                                                   186561
      ** Settlement Account: Customer/Currency/Acct Code/Acct Seq         186561
      *                                                                   186561
     I**********                              1  18 #ASETF                             186561 CGL029
     I                                        1  24 #ASETF                                    CGL029
     I                                        1   6 #ACUST                186561
     I                                        7   9 #ACCY                 186561
     I**********                             10  13 #AACOD                             186561 CGL029
     I**********                             14  15 #AACSQ                             186561 CGL029
     I**********                             16  18 #ABRCD                             186561 CGL029
     I                                       10  19 #AACOD                                    CGL029
     I                                       20  21 #AACSQ                                    CGL029
     I                                       22  24 #ABRCD                                    CGL029
     I*
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I*
     C           *ENTRY    PLIST
      *
     C** In
      *
     C                     PARM           ##PYRC  1        *Pay/Receive
     C                     PARM           ##CCY   3        *Settlemt currncy
     C                     PARM           ##ROLL  1        *Rollover
     C                     PARM           ##SETP  2        *Settlemt type
     C                     PARM           ##STBR  3        *Settlemt branch
     C                     PARM           ##OURN 12        *Our nostro/ A/C
     C                     PARM           ##THRN  8        *Their nostro
     C                     PARM           ##FACO  8        *For account of
     C                     PARM           ##BKBR  3        *Booking  branch
     C                     PARM           ##BKBB  6        *Booking  br BICN
     C                     PARM           ##CNTP  6        *Counterparty
     C**********           PARM           ##CSAC  4        *Compen.Susp. A/C                  CGL029
     C**********           PARM           ##CADC  4        *Current A/C dflt                  CGL029
     C**********           PARM           ##COMS  4        *Computer Sus A/C                  CGL029
     C**********           PARM           ##DEFA  4        *Tradng Dflt A/C                   CGL029
     C                     PARM           ##CSAC 10                                           CGL029
     C                     PARM           ##CADC 10                                           CGL029
     C                     PARM           ##COMS 10                                           CGL029
     C                     PARM           ##DEFA 10                                           CGL029
     C                     PARM           ##LOCY  3        *Local Currency
     C                     PARM           ##ONOC  1        *Ourselves/Nostro
      *
      ** Out
      *
     C                     PARM           ##OAC1  3        *Our Line 1
     C                     PARM           ##OAC2  3        *Our Line 2
     C                     PARM           ##ONN  30        *Our Nostro Name
     C                     PARM           ##ONT  20        *Our Nostro Town
     C*********************PARM           ##OGA  20        *Our GL A/C                       BUG7326
     C                     PARM           ##OGA  24        *Our GL A/C                       BUG7326
     C                     PARM           ##ORA  20        *Our RE a/C
      *
     C                     PARM           ##TAC1  3        *Thr Line 1
     C                     PARM           ##TAC2  3        *Thr Line 2
     C                     PARM           ##TNN  30        *Thr Nostro Name
     C                     PARM           ##TNT  20        *Thr Nostro Town
      *
     C                     PARM           ##FAL1  3        *FACO Line 1
     C                     PARM           ##FAL2  3        *FACO Line 2
     C                     PARM           ##FAN  30        *FACO Name
     C                     PARM           ##FAT  20        *FACO Town
      *
     C                     PARM           ##RTCD  7        *Return Code
     C/COPY WNCPYSRC,CG99DSETC1
      *
     C/SPACE 5
     C                     MOVEL##OURN    ##SETA
     C                     MOVEL##OURN    ##RETL
     C                     MOVEL##CCY     #ACCY                           186561
      *
      ** Determine 'Our' Details
      *
     C                     EXSR SRDOUR
      *
      ** Determine 'Their' Details
      *
     C                     EXSR SRDTHR
      *
      ** Determine 'For A/c Of' Details
      *
     C           ##PYRC    IFEQ 'P'
     C                     EXSR SRDFAO
     C                     END
      *
     C                     SETON                     LR
      *****************************************************************
      * SRDOUR - DETERMINE 'OUR' DETAILS                              *
      *****************************************************************
     C           SRDOUR    BEGSR
      *
      **  Copyright
      *
     C                     MOVEA##CPY     ##BIS  80
      *
      ** Settlement By Nostro
      *
     C                     MOVEL*BLANK    A7NONB
     C                     MOVEL*BLANK    BBCRNM
     C                     MOVEL*BLANK    BBCRTN
     C           ##SETP    IFEQ '01'
     C           ##SETP    OREQ '02'
     C           ##SETP    OREQ '07'
     C           ##SETP    OREQ '08'
     C           ##SETP    OREQ '12'
      *
      ** Access nostro (if present)
      *
     C           ##OURN    IFNE *BLANK
     C           ##NOST    ANDNE'00'
     C                     EXSR SRNOST
     C                     END
     C                     END
      *
      ** SET OUR A/C INDICATOR
      *
     C                     MOVEL*BLANK    ##OAC   3
     C                     EXSR SRSOAI
      *
      ** SET OUR LINE 1
      *
     C                     MOVEL##OAC     ##OAC1
      *
      ** SET OUR LINE 2
      *
     C                     MOVEL##OAC     ##OAC2
      *
      ** SET OUR NOSTRO NAME
      *
     C                     MOVELBBCRNM    ##ONN
      *
      ** SET OUR NOSTRO TOWN
      *
     C                     MOVELBBCRTN    ##ONT
      *
      ** Determine GL and RE settlement account
      *
     C           ##SETP    IFNE *BLANK
     C           ##SETP    ANDNE'00'
     C                     EXSR SRSETA
     C*
     C** OUR GL A/C
     C*
     C**********           MOVEL##SETF    ##OGA                           186561
     C                     MOVEL#ASETF    ##OGA                           186561
     C*
     C** OUR RE A/C
     C*
     C                     MOVEL@RETL     ##ORA
     C                     END
      *
      ** SETTLEMENT METHOD '09'                                           CLE033
      *                                                                   CLE033
     C           ##SETP    IFEQ '09'                                      CLE033
     C           ##STBR    CAT  ##OURN    ##OGA                           CLE033
     C                     ENDIF                                          CLE033
      *                                                                   CLE033
     C           EXDOUR    TAG
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * SRNOST - ACCESS NOSTRO DETAILS                                *
      *****************************************************************
     C           SRNOST    BEGSR
      *
      ** GET NOSTRO DETAILS
      *
     C                     CALL 'AONOSTR0'                 *
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM *BLANKS   @KEYA   6
     C                     PARM ##CCY     @KEYB   3
     C**********           PARM *BLANKS   @KEYC   4                                           CGL029
     C                     PARM *BLANKS   @KEYC  10                                           CGL029
     C                     PARM *BLANKS   @KEYD   2
     C                     PARM ##NOST    @KEYE   2
     C                     PARM *BLANKS   @KEYF   3
     C                     PARM *BLANKS   @KEYG  10
     C                     PARM *BLANKS   @KEYH   1
     C           SDNOST    PARM SDNOST    DSFDY
      *
      ** IF NOSTRO EXISTS
      *
     C           @RTCD     IFEQ *BLANK
      *
      ** GET CUSTOMER DETAILS OF NOSTRO
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM A7CUST    @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C                     END
      *
     C           EXNOST    TAG
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * SRSOAI - SET OUR ACCOUNT INDICATOR
      *****************************************************************
     C           SRSOAI    BEGSR
      *
      ** 'INSTRUCTIONS TO BE ADVISED'
      ** No settlement type, or settlement by nostro with no nostro present
      *
     C           ##SETP    IFEQ *BLANK
     C           ##SETP    OREQ '00'
     C           ##SETP    OREQ '01'
     C           A7NONB    ANDEQ*BLANK
     C           ##SETP    OREQ '07'
     C           A7NONB    ANDEQ*BLANK
     C           ##SETP    OREQ '08'
     C           A7NONB    ANDEQ*BLANK
     C                     MOVEL'000'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 01, NOSTRO PRESENT..'
      *
     C           ##SETP    IFEQ '01'
     C           A7NONB    ANDNE*BLANK
      *
      ** '..NOT LOCAL CURRENCY AND NOT OURSELVES AS NOSTRO'
      *
     C           ##CCY     IFNE ##LOCY
     C           ##ONOC    ORNE 'O'
     C                     MOVEL'010'     ##OAC
     C                     ELSE
      *
      ** '..LOCAL CURRENCY AND OURSELVES AS NOSTRO'
      *
     C                     MOVEL'011'     ##OAC
     C                     END
     C                     END
      *
      ** 'SETTLEMENT TYPE 02, NOSTRO PRESENT'
      *
     C           ##SETP    IFEQ '02'
     C           A7NONB    ANDNE*BLANK
     C                     MOVEL'020'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 03'
      *
     C           ##SETP    IFEQ '03'
     C                     MOVEL'030'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 04'
      *
     C           ##SETP    IFEQ '04'
     C                     MOVEL'040'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 05'
      *
     C           ##SETP    IFEQ '05'
     C                     MOVEL'050'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 06'
      *
     C           ##SETP    IFEQ '06'
     C                     MOVEL'060'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 07, NOSTRO PRESENT..'
      *
     C           ##SETP    IFEQ '07'
     C           A7NONB    ANDNE*BLANK
      *
      ** '..NOT LOCAL CURRENCY AND NOT OURSELVES AS NOSTRO'
      *
     C           ##CCY     IFNE ##LOCY
     C           ##ONOC    ORNE 'O'
     C                     MOVEL'070'     ##OAC
     C                     ELSE
      *
      ** '..LOCAL CURRENCY AND OURSELVES AS NOSTRO'
      *
     C                     MOVEL'071'     ##OAC
     C                     END
     C                     END
      *
      ** 'SETTLEMENT TYPE 08, NOSTRO PRESENT..'
      *
     C           ##SETP    IFEQ '08'
     C           A7NONB    ANDNE*BLANK
      *
      ** '..NOT LOCAL CURRENCY AND NOT OURSELVES AS NOSTRO'
      *
     C           ##CCY     IFNE ##LOCY
     C           ##ONOC    ORNE 'O'
     C                     MOVEL'080'     ##OAC
     C                     ELSE
      *
      ** '..LOCAL CURRENCY AND OURSELVES AS NOSTRO'
      *
     C                     MOVEL'081'     ##OAC
     C                     END
     C                     END
      *
      ** 'SETTLEMENT TYPE 12'
      *
     C           ##SETP    IFEQ '12'
     C                     MOVEL'120'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 14'
      *
     C           ##SETP    IFEQ '14'
     C                     MOVEL'140'     ##OAC
     C                     END
      *
      ** 'SETTLEMENT TYPE 15'
      *
     C           ##SETP    IFEQ '15'
     C                     MOVEL'150'     ##OAC
     C                     END
      *                                                                   CLE033
      ** 'SETTLEMENT TYPE 09'                                             CLE033
      *                                                                   CLE033
     C           ##SETP    IFEQ '09'                                      CLE033
     C                     MOVEL'151'     ##OAC                           CLE033
     C                     ENDIF                                          CLE033
      *
      ** 'ROLLOVER'
      *
     C           ##ROLL    IFEQ 'Y'
     C                     MOVEL'999'     ##OAC
     C                     END
      *
     C           EXSOAI    TAG
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * SRSETA -                                                      *
      *****************************************************************
     C           SRSETA    BEGSR
      *
     C                     MOVEL*BLANK    @RETL
      *
      ** Settlement across nostro, with nostro present
      *
     C           ##SETP    IFEQ '01'
     C           ##SETP    OREQ '02'
     C           ##SETP    OREQ '07'
     C           ##SETP    OREQ '08'
     C           ##SETP    OREQ '12'
     C           A7NONB    IFEQ *BLANK
     C                     MOVEL*BLANK    ##SETF
     C                     MOVEL*BLANK    #ASETF                          186561
     C                     GOTO EXSETA
     C                     END
     C                     MOVELA7BRCD    ##BRCD           *\
     C                     MOVELA7BRCD    #ABRCD           *\             186561
     C                     MOVELA7CUST    ##CUST           *\
     C                     MOVELA7CUST    #ACUST           *\             186561
     C                     MOVELA7ACCD    ##ACOD           * As on nostro
     C                     MOVELA7ACCD    #AACOD           * As on nostro 186561
     C                     MOVELA7ACSN    ##ACSQ           */
     C                     MOVELA7ACSN    #AACSQ           */             186561
     C                     END
      *
      ** 'SETTLEMENT TYPE 03'
      *
     C           ##SETP    IFEQ '03'
     C                     MOVE ##BKBR    ##BRCD           *Booking branch
     C                     MOVE ##BKBR    #ABRCD           *Booking branch186561
     C                     MOVE ##BKBB    ##CUST           *Booking br BICN
     C                     MOVE ##BKBB    #ACUST           *Booking br BIC186561
     C                     MOVE ##CSAC    ##ACOD           *Compen.Susp. A/C
     C                     MOVE ##CSAC    #AACOD           *Compen.Susp.IA186561
     C**********           MOVE '01'      ##ACSQ                          186561
     C                     MOVE ##ACSQ    ##ACSQ                          186561
     C                     MOVE ##ACSQ    #AACSQ                          186561
     C                     END
      *
      ** 'SETTLEMENT TYPE 04'
      *
     C           ##SETP    IFEQ '04'
     C                     MOVE ##BKBR    ##BRCD           *Booking branch
     C                     MOVE ##BKBR    #ABRCD           *Booking branch186561
     C                     MOVE ##CNTP    ##CUST           *Counterparty
     C                     MOVE ##CNTP    #ACUST           *Booking br BIC186561
     C                     MOVE ##CADC    ##ACOD           *Current A/C dflt
     C                     MOVE ##CADC    #AACOD           *Compen.Susp.IA186561
     C                     MOVE '01'      ##ACSQ
     C                     MOVE '01'      #AACSQ                          186561
      *                                                                   163730
      ** Get retail account                                               163730
      *                                                                   163730
     C                     CALL 'AOACCTR0'                                163730
     C                     PARM *BLANKS   @RTCD                           163730
     C                     PARM '*KEY   ' @OPTN                           163730
     C                     PARM *BLANKS   @RETL                           163730
     C                     PARM           ##CUST                          163730
     C                     PARM ##CCY     @CYCD                           163730
     C                     PARM           ##ACOD                          163730
     C                     PARM           ##ACSQ                          163730
     C                     PARM           ##BRCD                          163730
     C           SDACNT    PARM SDACNT    DSSDY                           163730
      *                                                                   163730
     C           @RTCD     IFEQ '*NRF   '                                 163730
     C                     MOVEL*BLANK    @RETL                           163730
     C                     ELSE                                           163730
     C           @RTCD     IFEQ *BLANK                                    163730
     C                     MOVELACNO      @RETL                           163730
     C                     END                                            163730
     C                     END                                            163730
     C                     END
      *
      ** 'SETTLEMENT TYPE 05'
      *
     C           ##SETP    IFEQ '05'
     C                     MOVE ##STBR    ##BRCD           *Settlmt branch
     C                     MOVE ##STBR    #ABRCD           *Settlmt branch186561
     C                     MOVE ##CUST    ##CUST           *\
     C                     MOVE ##CUST    #ACUST           *\             186561
     C                     MOVE ##ACOD    ##ACOD           * Settlmt A/C
     C                     MOVE ##ACOD    #AACOD           * Settlmt A/C  186561
     C                     MOVE ##ACSQ    ##ACSQ           */
     C                     MOVE ##ACSQ    #AACSQ           */             186561
     C                     END
      *
      ** 'SETTLEMENT TYPE 06'
      *
     C           ##SETP    IFEQ '06'
     C                     MOVE ##BKBR    ##BRCD           *Booking branch
     C                     MOVE ##BKBR    #ABRCD           *Booking branch186561
     C                     MOVE ##BKBB    ##CUST           *Booking br BICN
     C                     MOVE ##BKBB    #ACUST           *Booking br BIC186561
     C                     MOVE ##COMS    ##ACOD           *Computer Sus A/C
     C                     MOVE ##COMS    #AACOD           *Compen.Susp.IA186561
     C**********           MOVE '01'      ##ACSQ                          186561
     C                     MOVE ##ACSQ    ##ACSQ                          186561
     C                     MOVE ##ACSQ    #AACSQ                          186561
     C                     END
      *
      ** 'SETTLEMENT TYPE 14'
      *
     C           ##SETP    IFEQ '14'
      *
      ** Get retail account
      *
     C                     CALL 'AOACCTR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM ##RETL    @RETL  10
     C                     PARM *BLANKS   @CNUM   6
     C                     PARM *BLANKS   @CYCD   3
     C**********           PARM *BLANKS   @ACCD   4                                           CGL029
     C                     PARM *BLANKS   @ACCD  10                                           CGL029
     C                     PARM *BLANKS   @ACSN   2
     C                     PARM *BLANKS   @DSNB   3
     C           SDACNT    PARM SDACNT    DSSDY
     C*
     C           @RTCD     IFEQ '*NRF   '
     C                     MOVEL*BLANK    ##SETF
     C                     MOVEL*BLANK    #ASETF                          186561
     C                     ELSE
     C           @RTCD     IFEQ *BLANK
     C                     MOVELBRCA      ##BRCD           *\
     C                     MOVELBRCA      #ABRCD           *\             186561
     C                     MOVELCNUM      ##CUST           *\
     C                     MOVELCNUM      #ACUST           *\             186561
     C                     MOVELACOD      ##ACOD           * As on account
     C                     MOVELACOD      #AACOD           * As on account186561
     C                     MOVELACSQ      ##ACSQ           */
     C                     MOVELACSQ      #AACSQ           */             186561
     C/COPY WNCPYSRC,CG99DSETC2
     C                     END
     C                     END
     C                     END
      *
      ** 'SETTLEMENT TYPE 15'
      *
     C           ##SETP    IFEQ '15'
     C                     MOVE ##BKBR    ##BRCD           *Booking branch
     C                     MOVE ##BKBR    #ABRCD           *Booking branch186561
     C                     MOVE ##CNTP    ##CUST           *Counterparty
     C                     MOVE ##CNTP    #ACUST           *Counterparty  186561
     C           ##DEFA    IFNE *BLANK                                    081964
     C                     MOVE ##DEFA    ##ACOD           *Tradng Dflt A/C
     C                     MOVE ##DEFA    #AACOD           *Tradng Dflt A/186561
     C                     ELSE                                           186561
     C                     MOVE ##ACOD    ##ACOD           * Settlmt A/C  186561
     C                     MOVE ##ACOD    #AACOD           * Settlmt A/C  186561
     C                     END                                            081964
     C**********           MOVE '01'      ##ACSQ                          186561
     C                     MOVE ##ACSQ    ##ACSQ                          186561
     C                     MOVE ##ACSQ    #AACSQ                          186561
     C                     END
     C/COPY WNCPYSRC,CG99DSETC3
      *
     C           EXSETA    TAG
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * SRDTHR - DETERMINE 'THEIR' DETAILS                            *
      *****************************************************************
     C           SRDTHR    BEGSR
      *
      ** GET CUSTOMER DETAILS OF COUNTERPARTY'S NOSTRO
      *
     C                     MOVEL*BLANK    BBCUST
     C                     MOVEL*BLANK    BBCRNM
     C                     MOVEL*BLANK    BBCRTN
     C                     MOVEL##THRN    @KEY1
      *
      ** If not settlement across a nostro, no counterparty nostro required
      * 'A/C with Bank name' defaults to Cust Report Name if SETP not 01, 142979
      *  07,08, or if THRN=blank. If THRN=*hival, our nostro name is used.142979
      *  Else, SRGCUS retrieves:                                          142979
      *  the a/c with bank's  customer report name (if THRN is a Cnum)    142979
      *  OR the counterparty nostro name (if THRN is a C'pty Nostro       142979
      *  Short Code).                                                     142979
     C                     MOVEL*BLANK    Z30    30                       142979
     C                     MOVELBBCRNM    Z30                             142979
      *
     C           ##SETP    IFNE '01'
     C           ##SETP    ANDNE'07'
     C           ##SETP    ANDNE'08'
     C                     MOVEL'000'     ##TAC   3        *Nothing required
     C                     ELSE
     C           ##THRN    IFNE *BLANK
      *
      ** Counterparty (their) nostro is our nostro (set in SRDOUR)
      *
     C           ##THRN    IFEQ *HIVAL
     C                     MOVEL*HIVAL    BBCUST
     C***********          MOVEL##ONN     BBCRNM                          142979
     C                     MOVEL##ONN     Z30                             142979
     C                     MOVEL##ONT     BBCRTN
     C                     ELSE
     C                     EXSR SRGCUS
     C                     END
     C                     END
      *
      ** CHECK WHETHER CUSTOMER FOUND
      *
     C           BBCUST    IFEQ *BLANK
     C                     MOVEL'001'     ##TAC            *Nothing defined
     C                     ELSE
     C           BBCUST    IFNE ##CNTP
     C                     MOVEL'002'     ##TAC            *C'Party present
     C                     ELSE
     C                     MOVEL'003'     ##TAC            *C'party=Their Ns
     C                     END
     C                     END
     C                     END
      *
      ** SET THEIR LINE 1
      *
     C                     MOVEL##TAC     ##TAC1
      *
      ** SET THEIR LINE 2
      *
     C                     MOVEL##TAC     ##TAC2
      *
      ** SET THEIR NOSTRO NAME
      *
     C***********          MOVELBBCRNM    ##TNN                           142979
     C                     MOVELZ30       ##TNN                           142979
      *
      ** SET THEIR NOSTRO TOWN
      *
     C                     MOVELBBCRTN    ##TNT
      *
     C           EXDTHR    TAG
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * SRDFAO - DETERMINE 'FOR A/C OF' DETAILS                       *
      *****************************************************************
     C           SRDFAO    BEGSR
      *
      ** GET CUSTOMER DETAILS OF 'FOR A/C OF' (BENEFICIARY)
      *
     C                     MOVEL*BLANK    BBCUST
     C                     MOVEL*BLANK    BBCRNM
     C                     MOVEL*BLANK    BBCRTN
     C                     MOVEL##FACO    @KEY1
      *
      ** If not settlement across a nostro, no 'for account of' required
      * 'For A/C of' defaults to Customer Report Name if SETP not 01,     142979
      *  07,08, or if FACO=blank. Else, subroutine SRGCUS will retrieve   142979
      *  the counterparty a/c customer report name (if FACO is a Cnum)    142979
      *  OR the counterparty nostro name (if FACO is a C'pty Nostro       142979
      *  Short Code).                                                     142979
     C                     MOVE *BLANKS   Z30                             142979
     C                     MOVELBBCRNM    Z30                             142979
      *
     C           ##SETP    IFNE '01'
     C           ##SETP    ANDNE'07'
     C           ##SETP    ANDNE'08'
     C                     MOVEL'000'     ##FAL   3        *Nothing required
     C                     ELSE
     C           ##FACO    IFNE *BLANK
     C                     EXSR SRGCUS
     C                     END
      *
      ** CHECK WHETHER CUSTOMER FOUND
      *
     C           BBCUST    IFEQ *BLANK
     C                     MOVEL'001'     ##FAL            *Nothing defined
     C                     ELSE
     C           BBCUST    IFNE ##CNTP
     C                     MOVEL'002'     ##FAL            *C'Party present
     C                     ELSE
     C                     MOVEL'003'     ##FAL            *C'party=FACO
     C                     END
     C                     END
     C                     END
      *
      ** SET FOR A/C OF LINE 1
      *
     C                     MOVEL##FAL     ##FAL1
      *
      ** SET FOR A/C OF LINE 2
      *
     C                     MOVEL##FAL     ##FAL2
      *
      ** SET FOR A/C OF NAME
      *
     C***********          MOVELBBCRNM    ##FAN                           142979
     C                     MOVELZ30       ##FAN                           142979
      *
      ** SET FOR A/C OF TOWN
      *
     C                     MOVELBBCRTN    ##FAT
      *
     C           EXDFAO    TAG
      *
     C                     ENDSR
     C*****************************************************************
     C/SPACE 5
      *****************************************************************
      * SRGCUS - GET CUSTOMER DETAILS
      *****************************************************************
     C           SRGCUS    BEGSR
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @KEY1
     C                     PARM *BLANKS   @KYST
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ** CUSTOMER FOUND, retrieve customer report name:                   142979
     C           @RTCD     IFEQ *BLANK                                    142979
     C                     MOVELBBCRNM    Z30                             142979
     C                     ENDIF                                          142979
      ** CUSTOMER NOT FOUND
      *
     C           @RTCD     IFNE *BLANK
     C                     MOVEL@KEY1     @CBNB
     C                     CALL 'AOCNSTR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @CBNB   8
     C           SDCNST    PARM SDCNST    DSFDY
     C           @RTCD     IFEQ *BLANK
      *
      ** COUNTERPARTY NOSTRO FOUND
      *
     C                     MOVELAWCPNC    BBCUST
     C***********          MOVELAWCPNM    BBCRNM                          142979
     C                     MOVELAWCPNM    Z30                             142979
     C                     MOVELAWCNTN    BBCRTN
     C                     END
     C                     END
      *
     C           EXGCUS    TAG
      *
     C                     ENDSR
     C*****************************************************************
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
