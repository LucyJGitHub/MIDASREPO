     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Determine Message Routing')
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                      *
     F*                                                               *
     F*  CG99DTMR - Determine Message Routing                         *
     F*                                                               *
     F*  Function:  To determine the network for a given sender       *
     F*             and destination.                                  *
     F*                                                               *
     F*  Called By:                                                   *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. CSW218             Date 19Mar18               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW096             Date 03Jun96               *
      *                 S01522             Date 22Nov94               *
     F*                                    DATE ddmmmyy               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSW218 - SWIFT Changes 2018                                  *
      *  MD046248 - Finastra Rebranding                               *
     F*  CSW096 - SWIFT changes 1996                                  *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
      *                                                                                       CSW218
      ** BIC Directory table                                                                  CSW218
     FMEBICDL2IF  E           K        DISK                                                   CSW218
      *                                                                                       CSW218
     E**  Copyright array.
     E                    ##CPY   1   1 80
     E*
     I            DS
     I                                        1  12 #1DCSD
     I                                        8   8 USID                  CSW096
     I                                        9  11 ##BIC
     ICPYR        DS
     I**  Data Structure for Compilation - Copyright Insertion
     I                                        1  80 ##CPY
     I*
     C           *ENTRY    PLIST
      *
     C                     PARM           ##SCSD 12        *Send.SWIFT Add
     C                     PARM           ##SSTD 12        *Send.STTX  Add
     C                     PARM           ##STXA 10        *Send.Telex Add
      *
     C           #1DCSD    PARM           ##DCSD 12        *Dest.SWIFT Add
     C                     PARM           ##DSTD 12        *Dest.STTX  Add
     C                     PARM           ##DTXA 10        *Dest.Telex Add
      *
     C                     PARM           ##DDNW  6        *Dest.Deflt Netw
     C                     PARM           ##MSDL  1        *Midas/SWIFT D/L
     C                     PARM           ##MTMI  1        *Midas/Telx Link
     C                     PARM           ##SWPY  10       *SWIFT conf prty
     C                     PARM           ##TXPY  10       *TELEX conf prty
     C                     PARM           ##NETW  6        *Network
     C                     PARM           ##RTCD  7
      **  Copyright
     C                     MOVEA##CPY     ##ACT  80
      *                                                                   CSW096
      **  Check if SWIFT Changes 1996 (CSW096) is installed               CSW096
      *                                                                   CSW096
     C                     CALL 'SWIFT96'                                 CSW096
     C                     PARM *BLANKS   @RTCD   7                       CSW096
      *                                                                   CSW096
     C           @RTCD     IFEQ 'CSW096'                                  CSW096
     C                     MOVE 'Y'       CSW096  1                       CSW096
     C                     ELSE                                           CSW096
     C                     MOVE 'N'       CSW096                          CSW096
     C                     ENDIF                                          CSW096
      *
      ** Check if Swift 2018 changes is installed.                                            CSW218
      *                                                                                       CSW218
     C                     CALL 'SWIF2018'                                                    CSW218
     C                     PARM           PRTCD   7                                           CSW218
     C           PRTCD     IFEQ 'CSW2018'                                                     CSW218
     C                     MOVEL'Y'       CSW218  1                                           CSW218
     C                     ELSE                                                               CSW218
     C                     MOVEL'N'       CSW218                                              CSW218
     C                     ENDIF                                                              CSW218
      *                                                                                       CSW218
     C           KBICD     KLIST                                                              CSW218
     C                     KFLD           WBICC                                               CSW218
     C                     KFLD           WBICB                                               CSW218
      *
      ** Destination default network defined?
      *
     C           ##DDNW    IFNE *BLANKS
     C                     EXSR NWAD
     C                     ELSE
     C                     EXSR PRAD
     C                     END
      *
     C                     SETON                     LR
      *
     C/SPACE 5
      *****************************************************************
      *   NWAD - determine default network addresses                  *
      *****************************************************************
      *
     C           NWAD      BEGSR
      *
      **  Default is SWIFT
      *
     C           CSW218    IFEQ 'N'                                                           CSW218
      *                                                                                       CSW218
     C           ##MSDL    IFEQ 'Y'
     C           ##DDNW    ANDEQ'SWIFT'
     C           ##DCSD    ANDNE*BLANKS                    *Dest.SWIFT Add
     C           ##BIC     ANDNE'BIC'                      *Dest.Non-SWIFT?
     C           ##SCSD    ANDNE*BLANKS                    *Send.SWIFT Add
     C           CSW096    ANDEQ'N'                                       CSW096
     C           ##MSDL    OREQ 'Y'                                       CSW096
     C           ##DDNW    ANDEQ'SWIFT'                                   CSW096
     C           ##DCSD    ANDNE*BLANKS                    *Dest.SWIFT AddCSW096
     C           USID      ANDNE'1'                        *Dest.Non-SWIFTCSW096
     C           ##SCSD    ANDNE*BLANKS                    *Send.SWIFT AddCSW096
     C           CSW096    ANDEQ'Y'                                       CSW096
     C                     MOVEL'SWIFT '  ##NETW
     C                     END
      *                                                                                       CSW218
     C                     ELSE                                                               CSW218
      ** Check if Destination's SWIFT address is connected                                    CSW218
      ** to SWIFTNet FIN when CSW218 is ON                                                    CSW218
     C                     MOVEL*BLANKS   WSID19  1                                           CSW218
     C           ##DCSD    IFNE *BLANKS                                                       CSW218
      *                                                                                       CSW218
     C                     MOVEL##DCSD    WBICC   8                                           CSW218
     C           3         SUBST##DCSD:9  WBICB   3                                           CSW218
     C           WBICB     IFEQ *BLANKS                                                       CSW218
     C                     MOVEL'XXX'     WBICB                                               CSW218
     C                     ENDIF                                                              CSW218
     C           KBICD     CHAINMEBICDL2             90                                       CSW218
     C           *IN90     IFEQ '0'                                                           CSW218
     C           1         SUBSTBDXINF:19 WSID19                                              CSW218
     C                     ENDIF                                                              CSW218
      *                                                                                       CSW218
      ** If Extra Information field position 19 is equal to 'N'                               CSW218
      ** indicate BIC address as not connected                                                CSW218
     C           ##MSDL    IFEQ 'Y'                                                           CSW218
     C           ##DDNW    ANDEQ'SWIFT'                                                       CSW218
     C           WSID19    ANDNE'N'                                                           CSW218
     C           ##SCSD    ANDNE*BLANKS                                                       CSW218
     C                     MOVEL'SWIFT '  ##NETW                                              CSW218
     C                     ENDIF                                                              CSW218
      *                                                                                       CSW218
     C                     ENDIF                                                              CSW218
     C                     ENDIF                                                              CSW218
      *
      **  Default is STTX
      *
     C           ##MSDL    IFEQ 'Y'
     C           ##DDNW    ANDEQ'STTX'
     C           ##DSTD    ANDNE*BLANKS                    *Dest.STTX Add
     C           ##SSTD    ANDNE*BLANKS                    *Send.STTX Add
     C                     MOVEL'STTX '   ##NETW
     C                     END
      *
      **  Default is TELEX
      *
     C           ##MTMI    IFEQ 'Y'
     C           ##DDNW    ANDEQ'TELEX'
     C           ##DTXA    ANDNE*BLANKS                    *Dest.Telex Add
     C           ##STXA    ANDNE*BLANKS                    *Send.Telex Add
     C                     MOVEL'TELEX '  ##NETW
     C                     END
      *
      **  Default is PAPER
      *
     C           ##DDNW    IFEQ 'PAPER'
     C                     MOVEL'PAPER '  ##NETW
     C                     END
      *
      **  Found to be unroutable
      *
     C           ##NETW    IFEQ *BLANKS
     C                     MOVEL'UNROUT'  ##NETW
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   PRAD - determine priority network addresses                 *
      *   Calls      TXROUT priority routing to STTX/Telex            *
      *              SWROUT priority routing to SWIFT                 *
      *****************************************************************
      *
     C           PRAD      BEGSR
      *
      **  If STTX/Telex has higher priority than SWIFT attempt
      **  STTX/Telex routing first, followed by SWIFT. (nb. 'Higher'
      **  priority is numerically lower ie. '1' is higher than '2')
     C           ##TXPY    IFLT ##SWPY
     C                     EXSR TXROUT
     C                     EXSR SWROUT
      *
      **  otherwise attempt SWIFT routing first
     C                     ELSE
     C                     EXSR SWROUT
     C                     EXSR TXROUT
     C                     END
      *
      **  If failed to route to electronic network, produce paper
     C           ##NETW    IFEQ *BLANKS
     C                     MOVEL'PAPER '  ##NETW
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   TXROUT - Attempt priority routing to STTX/Telex, if not     *
      *            already routed                                     *
      *   Called by   PRAD                                            *
      *   Calls       -                                               *
      *****************************************************************
      *
     C           TXROUT    BEGSR
      *
      **  If not already routed, attempt STTX/Telex routing
     C           ##NETW    IFEQ *BLANKS
      *
      **  Note that STTX and Telex are mutually exclusive - try both
     C           ##MSDL    IFEQ 'Y'
     C           ##DSTD    ANDNE*BLANKS                    *Dest.STTX Add
     C           ##SSTD    ANDNE*BLANKS                    *Send.STTX Add
     C                     MOVEL'STTX '   ##NETW
     C                     END
      *
     C           ##MTMI    IFEQ 'Y'
     C           ##DTXA    ANDNE*BLANKS                    *Dest.Telex Add
     C           ##STXA    ANDNE*BLANKS                    *Send.Telex Add
     C                     MOVEL'TELEX '  ##NETW
     C                     END
      *
     C                     END
      *
     C                     ENDSR                           TXROUT
      *
     C/EJECT
      *****************************************************************
      *   SWROUT - Attempt priority routing to SWIFT if not already   *
      *            routed                                             *
      *   Called by   PRAD                                            *
      *   Calls       -                                               *
      *****************************************************************
      *
     C           SWROUT    BEGSR
      *
      **  If not already routed, attempt SWIFT routing
     C           CSW218    IFEQ 'N'                                                           CSW218
      *                                                                                       CSW218
     C           ##NETW    IFEQ *BLANKS
     C           ##MSDL    ANDEQ'Y'
     C           ##DCSD    ANDNE*BLANKS                    *Dest.SWIFT Add
     C           ##BIC     ANDNE'BIC'                      *Dest.NonSWIFT?
     C           ##SCSD    ANDNE*BLANKS                    *Send.SWIFT Add
     C           CSW096    ANDEQ'N'                                       CSW096
     C           ##NETW    OREQ *BLANKS                                   CSW096
     C           ##MSDL    ANDEQ'Y'                                       CSW096
     C           ##DCSD    ANDNE*BLANKS                    *Dest.SWIFT AddCSW096
     C           USID      ANDNE'1'                        *Dest.NonSWIFT?CSW096
     C           ##SCSD    ANDNE*BLANKS                    *Send.SWIFT AddCSW096
     C           CSW096    ANDEQ'Y'                                       CSW096
     C                     MOVEL'SWIFT '  ##NETW
     C                     END
      *
     C                     ELSE                                                               CSW218
      ** Check if Destination's SWIFT address is connected                                    CSW218
      ** to SWIFTNet FIN when CSW218 is ON                                                    CSW218
     C                     MOVEL*BLANKS   WSID19                                              CSW218
     C           ##DCSD    IFNE *BLANKS                                                       CSW218
      *                                                                                       CSW218
     C                     MOVEL##DCSD    WBICC                                               CSW218
     C           3         SUBST##DCSD:9  WBICB                                               CSW218
     C           WBICB     IFEQ *BLANKS                                                       CSW218
     C                     MOVEL'XXX'     WBICB                                               CSW218
     C                     ENDIF                                                              CSW218
     C           KBICD     CHAINMEBICDL2             90                                       CSW218
     C           *IN90     IFEQ '0'                                                           CSW218
     C           1         SUBSTBDXINF:19 WSID19                                              CSW218
     C                     ENDIF                                                              CSW218
      *                                                                                       CSW218
      ** If Extra Information field position 19 is equal to 'N'                               CSW218
      ** indicate BIC address as not connected                                                CSW218
     C           ##NETW    IFEQ *BLANKS                                                       CSW218
     C           ##MSDL    ANDEQ'Y'                                                           CSW218
     C           WSID19    ANDNE'N'                                                           CSW218
     C           ##SCSD    ANDNE*BLANKS                                                       CSW218
     C                     MOVEL'SWIFT '  ##NETW                                              CSW218
     C                     ENDIF                                                              CSW218
      *                                                                                       CSW218
     C                     ENDIF                                                              CSW218
     C                     ENDIF                                                              CSW218
     C                     ENDSR                           SWROUT
      *****************************************************************
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
