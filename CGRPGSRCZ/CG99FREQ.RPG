     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Frequency processing')                        *
      *****************************************************************
      *                                                               *
      *  Midas - User Defined Correspondence                          *
      *                                                               *
      *  CG99FREQ - Frequency Processing                              *
      *                                                               *
      *  Function:  This program xxxxxxxxxxxxxxxxxxxxxxxxxxxx         *
      *  (phase(s))                                                   *
      *                                                               *
      *  Called By: mmCnnnn - (program name)                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL184             Date 07Dec16               *
      *                 CLE064             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG2330            Date 29Apr04               *
      *                 CSC022             Date 24Feb04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 22Nov94               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL184 - New Frequencies for Account Statements              *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
     F*  BUG2330- CG programs looking at wrong dataarea for CSC022    *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  S01522 - User Defined Correspondence                         *
      *                                                               *
      *****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  50    First cycle                                            *
     F*  90    General work indicator                                 *
     F*  91    General work indicator                                 *
     F*                                                               *
     F*  U7/U8 Error Ocurred                                          *
     F*  LR    End program                                            *
     F*                                                               *
     F*****************************************************************
      /COPY WNCPYSRC,CG99FRDFPG
      *
      *  /Copy point for File Definition
      *
      /EJECT
     E                    CPY@    1   1 80
     E**********          FREQ   13  13  1  A            Frequency                            CGL184
     E**********          FRQ1    8   8  1  A            Frequency                            CGL184
     E                    FREQ   17  17  1  A            Frequency                            CGL184
     E                    FRQ1   11  11  1  A            Frequency                            CGL184
      *
      *  Copyright table
      *
      /COPY WNCPYSRC,CG99FRDEPG
      *
      *  /Copy point for Arrays
      *
      /COPY ZSRSRC,ZHOLE
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZFREQBZ1
      /COPY CGCPYSRC,SRERRE
      *                                                                                       CSC022
      ** Array to hold commitment jobs name                                                   CSC022
      *                                                                                       CSC022
     E                    WCMT       10 10                                                    CSC022
      *
      *  end of Copysource for error processing
      *
      /EJECT
      /COPY WNCPYSRC,CG99FRDIPG
      *
      *  /Copy point for Input specifications
      *
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      /COPY ZSRSRC,ZHOLI
      /COPY CGCPYSRC,SRERRI
      *
      *  End of Program Error Processing copysource member
      *
     ISDGELR    E DSSDGELRPD
      *
      *  Define General Ledger Details
      *
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
     IDSFDY     E DSDSFDY
      *
      * Data Structures used by Access Programs
      *
     IDSSDY     E DSDSSDY
      *
      * Data Structures used by Access Programs
      *
     IWDSJOB    E DSSCCMTJOB                                                                  CSC022
     I              COMITNUM                        WCMTNO                                    CSC022
     I              COMITJOB1                       WJOB01                                    CSC022
     I              COMITJOB2                       WJOB02                                    CSC022
     I              COMITJOB3                       WJOB03                                    CSC022
     I              COMITJOB4                       WJOB04                                    CSC022
     I              COMITJOB5                       WJOB05                                    CSC022
     I              COMITJOB6                       WJOB06                                    CSC022
     I              COMITJOB7                       WJOB07                                    CSC022
     I              COMITJOB8                       WJOB08                                    CSC022
     I              COMITJOB9                       WJOB09                                    CSC022
     I              COMITJOB10                      WJOB10                                    CSC022
     I                                        4 103 WCJOBS                                    CSC022
      ** Commitment Control dataarea                                                          CSC022
     ISCSARD    E DSSCSARDPD                                                                  CSC022
      *                                                                                       CSC022
      * Named constants -- default text strings:
      *
     I              'HOLDMAIL  '          C         ##HOLD
     I              'IMMEDIATE '          C         ##IMMD
     I              'SUPPRESS  '          C         ##SPRS
      /EJECT
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM ZZERR@    W0RTN   7
     C                     PARM           W0ACT   8
     C                     PARM           W0FREQ 10
     C                     PARM ZDAYNO    W0RDNB  50
     C                     PARM           W0MDAY  20
     C                     PARM           W0CCY   3
     C                     PARM           W0LOC   3
     C                     PARM           W0EXT 256
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
      *  Execute Frequency check processing
      *
     C           W0ACT     CASEQ'*NEWDATE'SRFREQ
     C           W0ACT     CASEQ'*VERIFY 'SRVRFY
     C           W0ACT     CASEQ'*VERIFYN'SRVRFY
     C                     CAS            SRACT
     C                     ENDCS
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *  Return to calling program
      *
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRFREQ   : Calculate next date                               *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRFREQ    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRFREQ'  @STK,Q
      *
      *  /Copy point for Calculate next date
      *
     C/COPY WNCPYSRC,CG99FRFREQ
      *
      *  Check standard UDC Frequencies
      *
     C                     SELEC
     C           W0FREQ    WHEQ ##HOLD
     C                     Z-ADD-1        ZDAYNO
     C                     GOTO EXFREQ
     C           W0FREQ    WHEQ ##IMMD
     C                     Z-ADD0         ZDAYNO
     C                     GOTO EXFREQ
     C           W0FREQ    WHEQ ##SPRS
     C                     Z-ADD-1        ZDAYNO
     C                     GOTO EXFREQ
     C                     ENDSL
      *
      *  Set up variables
      *
     C                     MOVELW0FREQ    ZFREQ
     C                     Z-ADDW0RDNB    ZDAYNO
     C                     MOVELW0CCY     ZCCY
     C                     MOVELW0LOC     ZLOC
     C                     Z-ADDW0MDAY    ZMDAY
      *
      *  Call standard subroutine
      *
     C                     EXSR ZFREQB
      *
      *  Unwind subroutine stack name
      *
     C           EXFREQ    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRVRFY   : Verify Frequency is recognised                    *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRVRFY    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRVRY '  @STK,Q
      *
     C                     MOVEL*BLANKS   ZZERR@
     C                     Z-ADD0         ZDAYNO
      *
      *  /Copy point for Verify frequency
      *
     C/COPY WNCPYSRC,CG99FRVRY
      *
      *  Check standard UDC Frequencies
      *
     C           W0FREQ    IFEQ ##HOLD
     C           W0FREQ    OREQ ##IMMD
     C           W0FREQ    OREQ ##SPRS
      *
      *  Day number must be zero
      *
     C           W0ACT     IFEQ '*VERIFY '
     C           W0MDAY    ANDNE0
     C                     MOVEL'CGD1354' ZZERR@
     C                     END
      *
     C                     GOTO EXVRFY
     C                     END
      *
      *  If only one character then check frequency
      *
     C           ' '       CHECKW0FREQ:2                 90
     C           *IN90     IFEQ '1'
     C                     MOVEL'CGD1238' ZZERR@
     C                     GOTO EXVRFY
     C                     END
      *
      *  Check frequency
      *
     C                     MOVELW0FREQ    ##MFRQ  1
     C           ##MFRQ    LOKUPFREQ                     90
     C           *IN90     IFEQ '0'
     C                     MOVEL'CGD1238' ZZERR@
     C                     GOTO EXVRFY
     C                     END
      *
      *  If action is verify day number as well
      *
     C           W0ACT     IFEQ '*VERIFY '
      *
      *  Day number required
      *
     C           W0MDAY    IFLT 1
     C           W0MDAY    ORGT 31
     C           ##MFRQ    LOKUPFRQ1                     90
     C           *IN90     IFEQ '1'
     C                     MOVEL'CGD1353' ZZERR@
     C                     GOTO EXVRFY
     C                     END
     C                     END
      *
      *  Day number NOT required
      *
     C           W0MDAY    IFGT 0
     C           W0MDAY    ANDLT32
     C           ##MFRQ    LOKUPFRQ1                     90
     C           *IN90     IFEQ '0'
     C                     MOVEL'CGD1354' ZZERR@
     C                     GOTO EXVRFY
     C                     END
     C                     END
      *
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXVRFY    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRACT    : Undefined action - error                          *
      *                                                               *
      *  CALLED BY: Main Processing                                   *
      *                                                               *
      *  CALLS    : SRERR   - report error and close down program     *
      *                                                               *
      *****************************************************************
     CSR         SRACT     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRACT '  @STK,Q
      *
      *  Database error undefined action
      *
     C                     MOVEL'*ACTION 'W0FILE           ***************
     C                     MOVELW0ACT     W0KEY            * DB ERROR 01 *
     C                     Z-ADD1         W0ERNB           ***************
     C                     MOVEL'MEM5006' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
      *
      *  Unwind subroutine stack name
      *
     C           EXACT     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
      /EJECT
      /COPY ZSRSRC,ZCHKH
      /EJECT
      /COPY ZSRSRC,ZACCH
      /EJECT
      /COPY ZSRSRC,ZFREQBZ2
      /EJECT
      /COPY ZSRSRC,ZDATE1Z2
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialise and define fields                      *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Get Rundate information
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      *  Set *IN98 depending on Date Format
      *
     C           WUDFF     IFEQ 'D'                        DDMMYY
     C                     MOVEL'0'       *IN98
     C                     ELSE
     C                     MOVEL'1'       *IN98
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *                                                                                       CSC022
      ** Access SAR details file to determine if CSC022 switchable feature                    CSC022
      ** is switched on                                                                       CSC022
      *                                                                                       CSC022
     C                     MOVE 'N'       CSC022  1                                           CSC022
     C                     MOVE 'N'       WCMTSK  1                                           CSC022
      *                                                                                       CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   PRTCD   7                                           CSC022
     C                     PARM '*VERIFY' POPTN   7                                           CSC022
     C                     PARM 'CSC022'  PSARD   6                                           CSC022
     C           SCSARD    PARM SCSARD    DSFDY                                               CSC022
      *                                                                                       CSC022
     C           PRTCD     IFEQ *BLANKS                                                       CSC022
     C                     MOVE 'Y'       CSC022                                              CSC022
     C                     MOVE 'N'       WCMTSK                                              CSC022
      *                                                                                       CSC022
     C                     IN   WDSJOB                                                        CSC022
      *                                                                                       CSC022
     C           WCMTNO    IFGT 0                                                             CSC022
     C                     MOVEAWCJOBS    WCMT                                                CSC022
     C                     Z-ADD1         I       20                                          CSC022
      *                                                                                       CSC022
     C           ##JOB     LOKUPWCMT,I                   51                                   CSC022
     C           *IN50     IFEQ *ON                                                           CSC022
     C                     MOVE 'Y'       WCMTSK                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ENDIF                                                              CSC022
      *                                                                                       CSC022
     C                     ELSE                                                               CSC022
     C           PRTCD     IFNE '*NRF'                                                        CSC022
     C                     MOVEL'CSC022'  W0KEY                                               CSC022
     C                     MOVEL'SCSARDPD'W0FILE                                              CSC022
     C                     Z-ADD21        W0ERNB                                              CSC022
     C                     EXSR SRERR                                                         CSC022
     C                     ENDIF                                                              CSC022
      *                                                                                       CSC022
      *                                                                                       CGL184
      ** Check if CGL184 is switched ON                                                       CGL184
      *                                                                                       CGL184
     C                     MOVE 'N'       CGL184  1                                           CGL184
      *                                                                                       CGL184
     C                     CALL 'AOSARDR0'                                                    CGL184
     C                     PARM *BLANKS   PRTCD   7                                           CGL184
     C                     PARM '*VERIFY' POPTN   7                                           CGL184
     C                     PARM 'CGL184'  PSARD   6                                           CGL184
     C           SCSARD    PARM SCSARD    DSFDY                                               CGL184
      *                                                                                       CGL184
     C           PRTCD     IFEQ *BLANKS                                                       CGL184
     C                     MOVE 'Y'       CGL184                                              CGL184
     C                     ELSE                                                               CGL184
     C           PRTCD     IFNE '*NRF'                                                        CGL184
     C                     MOVEL'CGL184'  W0KEY                                               CGL184
     C                     MOVEL'SCSARDPD'W0FILE                                              CGL184
     C                     Z-ADD97        W0ERNB                                              CGL184
     C                     EXSR SRERR                                                         CGL184
     C                     ENDIF                                                              CGL184
     C                     ENDIF                                                              CGL184
      *                                                                                       CGL184
     C************NAMVAR   DEFN           WDSJOB                                       CSC022BUG2330
     C           *NAMVAR   DEFN SCCMTJOB  WDSJOB                                             BUG2330
      *                                                                                       CSC022
     C                     ENDIF                                                              CSC022
      *                                                                                       CSC022
     CSR                   ENDSR
      /EJECT
      /COPY WNCPYSRC,CG99FRDCPG
      *
      *  /Copy point for Calculations
      *
      /EJECT
      *
      * PSSR Processing
      *
     C/COPY CGCPYSRC,SRERRPSSR
      /EJECT
      *
      *  File and Program Error Processing
      *
      /COPY CGCPYSRC,SRERRC
      /EJECT
      /COPY WNCPYSRC,CG99FRDOPG
      *
      *  /Copy point for Outputs
      *
      /EJECT
**  CPY@ table
(c) Finastra International Limited 2001
**   FREQ - PAYMENT FREQUENCY VALUES
ABDFGHKLMOQRSTWXY                                                                             CGL184
**   FRQ1 - Payment frequencys that need day in month
BGHLMOQRTXY                                                                                   CGL184
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZFMD  -  NUMBER OF DAYS IN THE MONTH
312831303130313130313031
