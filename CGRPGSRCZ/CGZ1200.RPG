     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Standard dates formatting.')
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                  *
     F*                                                               *
     F*  CGZ1200 - - - - Format dates for display or printing.        *
     F*                                                               *
     F*  Function:   - - This program edits a date. The format        *
     F*                   used depends on an input parameter.         *
     F*                                                               *
     F*  Parameters: - - P0RTN  --   7    -- Return code;             *
     F*                  P0VIN  -- 256    -- Input value;             *
     F*                  P0VOUT -- 256    -- Output value;            *
     F*                  P0VLEN --   3(0) -- Output value length;     *
     F*                  P0FTYP --  10    -- Format type;             *
     F*                  P0FOPT --  15    -- Format option.           *
     F*                                                               *
     F*  The date to be edited is assumed to be in the leftmost       *
     F*   eight characters of P0VIN, in the formats YYYYMMDD or       *
     F*   or DDMMYYYY.                                                *
     F*                                                               *
     F*  The leftmost 8 characters of P0FOPT determine the editing    *
     F*   pattern. Valid patterns are:                                *
     F*                                                               *
     F*    TEXT1    -- for example, "24th August 1994";               *
     F*    TEXT2    -- for example, "August 24, 1994";                *
     F*    TEXT3    -- for example, "24AUG94";                        *
     F*    DDMMYY   -- for example, "240894";                         *
     F*    MMDDYY   -- for example, "082494";                         *
     F*****DD-MM-YY*--*for*example,*"24-08-94";************************   193060
     F*****MM-DD-YY*--*for*example,*"08-24-94";************************   193060
     F*    DD MM YY -- for example, "24 08 94";                       *   193060
     F*    MM DD YY -- for example, "08 24 94";                       *   193060
     F*    ISO      -- for example, "940824";                         *
     F*    SWIFT    -- as for ISO.                                    *
     F*                                                               *
     F***The*rightmost*character*of*P0FOPT*determines*the*delimiter****   193060
     F****used*by*formats*DD-MM-YY*and*MM-DD-YY.*It*may*be*any*non-****   193060
     F****blank*character.*The*default*is*"-".*************************   193060
     F* The rightmost character of P0FOPT determines the delimiter    *   193060
     F* used by formats DD MM YY and MM DD YY. It may be any non-     *   193060
     F* blank character.                                              *   193060
     F*                                                               *
     F*  If the format type (P0FTYP) is "Day number",  the input is   *
     F*   taken to be a standard day number.  This value is changed   *
     F*   to a standard date (YYYYMMDD) before processing.            *
     F*                                                               *
     F*  Called by:  - - CGxxxx.                                      *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 193060             Date 18Jul01               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 086231             Date 06Apr95               *
      *                 S01522             Date 01Jan95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  193060 - Add New Format                                      *
     F*  086231 - Format Date as DD MM YY and MM DD YY                *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*  90 -- Error detected.                                        *
     F*  91 -- Date is numeric.                                       *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Subroutine usage                                             *
     F*  ~~~~~~~~~~~~~~~~                                             *
     F*  SRSPLT -- Split the date into its constituent parts.         *
     F*  SRTXT1 -- Edit the date for format TEXT1.                    *
     F*  SRTXT2 -- Edit the date for format TEXT2.                    *
     F*  SRTXT3 -- Edit the date for format TEXT3.                    *
     F*  SRDMY1 -- Edit the date for format DDMMYY.                   *
     F*  SRMDY1 -- Edit the date for format MMDDYY.                   *
     F***SRDMY2*--*Edit*the*date*for*format*DD-MM-YY.******************   193060
     F***SRMDY2*--*Edit*the*date*for*format*MM-DD-YY.******************   193060
     F*  SRDMY2 -- Edit the date for format DD MM YY.                 *   193060
     F*  SRMDY2 -- Edit the date for format MM DD YY.                 *   193060
     F*  SRISO  -- Edit the date for formats ISO and SWIFT.           *
     F*  SRBADF -- Handle an invalid format flag.                     *
     F*  SRDNUM -- Handle an incoming day number.                     *
     F*  SRINIT -- Provide initialisation and definitions.            *
     F*  SRDMSY -- Edit the date for format DDMMSSYY                  *   193060
     F*  SRMDSY -- Edit the date for format MMDDSSYY                  *   193060
     F*  SRTRA2 -- Change date with "/"                               *   193060
     F*                                                               *
     F*  Copied in routines:                                          *
     F*                                                               *
     F*  *PSSR  -- Program error routine.                             *
     F*  SRFILE -- File error routine.                                *
     F*  SRERR  -- Error reporting routine.                           *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*-------------------------------------------------------------------
     E* Error processing array:
     E*
     E/COPY CGCPYSRC,SRERRE
     E*
     E                    CPY@    1   1 80               Copyright
     E                    OUT       256  1               Date out.
     E                    MN1     1  12  9   LEN     1 0 Long month names.
     E                    MN2     1  12  3               Short months.
     E                    SFX     1  31  2               Day suffixes.
     E*
     E* Copied-in array definitions:
     E*
     E/COPY WNCPYSRC,CGZ1200EPG
     E*
     E*-------------------------------------------------------------------
     I/SPACE
     I*-------------------------------------------------------------------
     I/COPY WNCPYSRC,CGZ1200IPG
     I*
     I* Copied-in data structures
     I*
     I*...................................................................
     I/COPY CGCPYSRC,SRERRI
     I*
     I* Error processing data structures
     I*
     I*...................................................................
     IDTINDS      DS
     I*
     I* The incoming date (yyyymmdd):
     I                                        1   4 YEAR41
     I                                        5   6 MONA1
     I                                        7   8 DAY21
     I*
     I                                        5   60VAL56
     I* The incoming date (ddmmyyyy):
     I                                        1   2 DAY22
     I                                        3   4 MONA2
     I                                        5   8 YEAR42
     IDSRTDS      DS
     I*
     I* The incoming date (sorted):
     I                                        1   2 DAY2
     I                                        2   2 DAY1
     I                                        3   4 MONA
     I                                        5   8 YEAR4
     I                                        7   8 YEAR2
     I*
     I                                        1   20DAY
     I                                        3   40MON
     I*...................................................................
     IEDITDS      DS
     I*
     I* The outgoing date:
     I                                        1 256 OUT
     I*...................................................................
     I* A named constant:
     I*
     I              'Day number'          C         #DAYNO
     I********************************************************************
     C/EJECT
     C********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           P0VIN 256
     C                     PARM           P0VOUT256
     C                     PARM           P0VLEN  30
     C                     PARM           P0FTYP 10
     C                     PARM           P0FOPT 15
     C*
     C* Add subroutine name to stack:
     C*
     C                     Z-ADD1         Q
     C                     MOVEL'@Main@'  @STK,Q
     C*
     C* Initialise:
     C                     EXSR SRINIT
     C*
     C/COPY WNCPYSRC,CGZ1200C11
     C* Split the value:
     C*
     C           P0VIN     IFNE *BLANKS
     C                     EXSR SRSPLT
     C*
     C* Format:
     C           #ETYPE    CASEQ'TEXT1   'SRTXT1           Text 1
     C           #ETYPE    CASEQ'TEXT2   'SRTXT2           Text 2
     C           #ETYPE    CASEQ'TEXT3   'SRTXT3           Text 3
     C           #ETYPE    CASEQ'DDMMYY  'SRDMY1           ddmmyy
     C           #ETYPE    CASEQ'MMDDYY  'SRMDY1           mmddyy
      ** The format is retained so that it is not required to change      193060
      ** the layouts using the format "DD-MM-YY' and "MM-DD-YY".          193060
      *                                                                   193060
     C           #ETYPE    CASEQ'DD-MM-YY'SRDMY2           dd-mm-yy
     C           #ETYPE    CASEQ'MM-DD-YY'SRMDY2           mm-dd-yy
     C/COPY WNCPYSRC,CGZ1200C07
     C           #ETYPE    CASEQ'DD MM YY'SRDMY2           dd mm yy       086231
     C           #ETYPE    CASEQ'MM DD YY'SRMDY2           mm dd yy       086231
     C/COPY WNCPYSRC,CGZ1200C08
     C           #ETYPE    CASEQ'ISO     'SRISO            ISO/SWIFT
     C           #ETYPE    CASEQ'SWIFT   'SRISO            ISO/SWIFT
     C           #ETYPE    CASEQ'DDMMSSYY'SRDMYS           DDMMSSYY       193060
     C           #ETYPE    CASEQ'MMDDSSYY'SRMDYS           MMDDSSYY       193060
     C/COPY WNCPYSRC,CGZ1200C01
     C                     CAS            SRBADF
     C                     ENDCS
      *                                                                   086231
      * Replace - with " "                                                086231
      *                                                                   086231
     C           #ETYPE    CASEQ'DD MM YY'SRTRAN                          086231
     C           #ETYPE    CASEQ'MM DD YY'SRTRAN                          086231
     C                     ENDCS                                          086231
     C*
     C* Return the edited value:
     C*
     C                     MOVE EDITDS    P0VOUT
     C                     ENDIF
     C*
     C           EXMAIN    TAG                             <<<=== EXMAIN
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     RETRN
     C********************************************************************
     C/EJECT
      ********************************************************************086231
      **  Subroutine SRTRAN replaces - with " "                         **086231
      **                                                                **086231
      **                                                                **086231
      **  For example: DD-MM-YY        -->    DD MM YY                  **086231
      ********************************************************************086231
     C           SRTRAN    BEGSR                           * S R T R A N *086231
      *                                                                   086231
      * Add subroutine name to stack:                                     086231
      *                                                                   086231
     C                     ADD  1         Q                               086231
     C                     MOVEL'SRTRAN'  @STK,Q                          086231
      *                                                                   086231
     C           '-':' '   XLATEEDITDS    EDITDS                          086231
      *                                                                   086231
     C           EXTRAN    TAG                             <<<=== EXTRAN  086231
      *                                                                   086231
      * Unwind subroutine stack:                                          086231
      *                                                                   086231
     C                     MOVE *BLANKS   @STK,Q                          086231
     C                     SUB  1         Q                               086231
      *                                                                   086231
     C                     ENDSR                                          086231
     C********************************************************************086231
     C/EJECT
     C********************************************************************
     C**  Subroutine SRSPLT divides the incoming date and               **
     C**   determines various values for later use.                     **
     C********************************************************************
     C           SRSPLT    BEGSR                           * S R S P L T *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRSPLT'  @STK,Q
     C*
     C* Move the input value:
     C*
     C                     MOVELP0VIN     DTINDS
     C*
     C* Ensure that the date is numeric:
     C*
     C                     TESTN          DTINDS     91
     C           *IN91     IFEQ *OFF
     C                     MOVE 'CGD1630' P0RTN
     C                     RETRN
     C                     ENDIF
     C*
     C* Choose between yyyymmdd and ddmmyyyy incoming formats:
     C*
     C           VAL56     IFGT 18
     C                     MOVE YEAR42    YEAR4
     C                     MOVE MONA2     MONA
     C                     MOVE DAY22     DAY2
     C                     ELSE
     C                     MOVE YEAR41    YEAR4
     C                     MOVE MONA1     MONA
     C                     MOVE DAY21     DAY2
     C                     ENDIF
     C*...................................................................
     C/EJECT
     C*...................................................................
     C* Ensure that the date is (approximately) valid:
     C*
     C           MON       IFLT 1
     C           MON       ORGT 12
     C                     MOVE 'CGD1631' P0RTN
     C                     RETRN
     C                     ENDIF
     C*
     C           DAY       IFLT 1
     C           DAY       ORGT 31
     C                     MOVE 'CGD1632' P0RTN
     C                     RETRN
     C                     ENDIF
     C*
     C* Get the month in two formats -- for example, "August" and "AUG",
     C*  with the length of the long month name:
     C*
     C                     MOVE MN1,MON   #MONTH  9
     C                     MOVE MN2,MON   #MON    3
     C                     Z-ADDLEN,MON   #LEN    10
     C/COPY WNCPYSRC,CGZ1200C12
     C*
     C* Get the day suffix -- for example, 3rd or 21st:
     C*
     C                     MOVE SFX,DAY   #SUFF   2
     C/COPY WNCPYSRC,CGZ1200C02
     C*
     C           EXSPLT    TAG                             <<<=== EXSPLT
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRTXT1 formats the date as, for example,           **
     C**   "24th August 1994".                                          **
     C********************************************************************
     C           SRTXT1    BEGSR                           * S R T X T 1 *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRTXT1'  @STK,Q
     C*
     C* Move the day number and the suffix:
     C*
     C                     Z-ADD1         #A      30
     C           DAY       IFLT 10
     C                     MOVE DAY1      OUT,#A
     C                     ADD  1         #A
     C                     ELSE
     C                     MOVEADAY2      OUT,#A
     C                     ADD  2         #A
     C                     ENDIF
     C*
     C                     MOVEA#SUFF     OUT,#A
     C                     ADD  3         #A
     C/COPY WNCPYSRC,CGZ1200C03
     C*
     C* Move the month name:
     C*
     C                     MOVEA#MONTH    OUT,#A
     C                     ADD  #LEN      #A
     C                     ADD  1         #A
     C*
     C* Move the year, and set the length of the result:
     C*
     C                     MOVEAYEAR4     OUT,#A
     C           #A        ADD  3         P0VLEN
     C*
     C           EXTXT1    TAG                             <<<=== EXTXT1
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRTXT2 formats the date as,                        **
     C**   for example, "August 24, 1994".                              **
     C********************************************************************
     C           SRTXT2    BEGSR                           * S R T X T 2 *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRTXT2'  @STK,Q
     C*
     C* Move the month name:
     C*
     C                     Z-ADD1         #A
     C                     MOVEA#MONTH    OUT,#A
     C                     ADD  #LEN      #A
     C                     ADD  1         #A
     C*
     C* Move the day number and a comma:
     C*
     C           DAY       IFLT 10
     C                     MOVE DAY1      OUT,#A
     C                     ADD  1         #A
     C                     ELSE
     C                     MOVEADAY2      OUT,#A
     C                     ADD  2         #A
     C                     ENDIF
     C*
     C                     MOVE ','       OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the year, and set the length of the result:
     C*
     C                     MOVEAYEAR4     OUT,#A
     C           #A        ADD  3         P0VLEN
     C*
     C           EXTXT2    TAG                             <<<=== EXTXT2
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRTXT3 formats the date as,                        **
     C**   for example, "24AUG94".                                      **
     C********************************************************************
     C           SRTXT3    BEGSR                           * S R T X T 3 *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRTXT3'  @STK,Q
     C*
     C* Move the day number:
     C*
     C                     Z-ADD1         #A
     C                     MOVEADAY2      OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the month name:
     C*
     C                     MOVEA#MON      OUT,#A
     C                     ADD  3         #A
     C*
     C* Move the year, and set the length of the result:
     C*
     C                     MOVEAYEAR2     OUT,#A
     C           #A        ADD  1         P0VLEN
     C*
     C           EXTXT3    TAG                             <<<=== EXTXT3
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRDMY1 formats the date as,                        **
     C**   for example, "240894".                                       **
     C********************************************************************
     C           SRDMY1    BEGSR                           * S R D M Y 1 *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRDMY1'  @STK,Q
     C*
     C* Move the day number:
     C*
     C                     Z-ADD1         #A
     C                     MOVEADAY2      OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the month number:
     C*
     C                     MOVEAMONA      OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the year, and set the length of the result:
     C*
     C                     MOVEAYEAR2     OUT,#A
     C           #A        ADD  1         P0VLEN
     C*
     C           EXDMY1    TAG                             <<<=== EXDMY1
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRMDY1 formats the date as,                        **
     C**   for example, "082494".                                       **
     C********************************************************************
     C           SRMDY1    BEGSR                           * S R M D Y 1 *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRMDY1'  @STK,Q
     C*
     C* Move the month number:
     C*
     C                     Z-ADD1         #A
     C                     MOVEAMONA      OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the day number:
     C*
     C                     MOVEADAY2      OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the year, and set the length of the result:
     C*
     C                     MOVEAYEAR2     OUT,#A
     C           #A        ADD  1         P0VLEN
     C*
     C           EXMDY1    TAG                             <<<=== EXMDY1
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT                                                              193060
      *****************************************************************   193060
      *   Subroutine SRDMYS formats the date as,                      *   193060
      *    for example, "24081994".                                   *   193060
      *****************************************************************   193060
     C           SRDMYS    BEGSR                           * S R D M Y S *193060
      *                                                                   193060
      ** Add subroutine name to stack.                                    193060
      *                                                                   193060
     C                     ADD  1         Q                               193060
     C                     MOVEL'SRDMYS'  @STK,Q                          193060
      *                                                                   193060
      ** Move the day number.                                             193060
      *                                                                   193060
     C                     Z-ADD1         #A                              193060
     C                     MOVEADAY2      OUT,#A                          193060
     C                     ADD  2         #A                              193060
      *                                                                   193060
      ** Move the month number.                                           193060
      *                                                                   193060
     C                     MOVEAMONA      OUT,#A                          193060
     C                     ADD  2         #A                              193060
      *                                                                   193060
      ** Move the year, and set the length of the result.                 193060
      *                                                                   193060
     C                     MOVEAYEAR4     OUT,#A                          193060
     C           #A        ADD  3         P0VLEN                          193060
      *                                                                   193060
     C           EXDMYS    TAG                             <<<=== EXDMY1  193060
      *                                                                   193060
      ** Unwind subroutine stack.                                         193060
      *                                                                   193060
     C                     MOVE *BLANKS   @STK,Q                          193060
     C                     SUB  1         Q                               193060
      *                                                                   193060
     C                     ENDSR                                          193060
      *****************************************************************   193060
     C/EJECT                                                              193060
      *****************************************************************   193060
      *   Subroutine SRMDYS formats the date as,                      *   193060
      *    for example, "08241994"                                    *   193060
      *****************************************************************   193060
     C           SRMDYS    BEGSR                           * S R M D Y S *193060
      *                                                                   193060
      ** Add subroutine name to stack.                                    193060
      *                                                                   193060
     C                     ADD  1         Q                               193060
     C                     MOVEL'SRMDY1'  @STK,Q                          193060
      *                                                                   193060
      ** Move the month number.                                           193060
      *                                                                   193060
     C                     Z-ADD1         #A                              193060
     C                     MOVEAMONA      OUT,#A                          193060
     C                     ADD  2         #A                              193060
      *                                                                   193060
      ** Move the day number.                                             193060
      *                                                                   193060
     C                     MOVEADAY2      OUT,#A                          193060
     C                     ADD  2         #A                              193060
      *                                                                   193060
      ** Move the year, and set the length of the result.                 193060
      *                                                                   193060
     C                     MOVEAYEAR4     OUT,#A                          193060
     C           #A        ADD  3         P0VLEN                          193060
      *                                                                   193060
     C           EXMDYS    TAG                             <<<=== EXMDY1  193060
      *                                                                   193060
      ** Unwind subroutine stack.                                         193060
      *                                                                   193060
     C                     MOVE *BLANKS   @STK,Q                          193060
     C                     SUB  1         Q                               193060
      *                                                                   193060
     C                     ENDSR                                          193060
     C********************************************************************193060
     C/EJECT
     C********************************************************************
     C**  Subroutine SRDMY2 formats the date as,                        **
     C**   for example, "24-08-94".                                     **
     C********************************************************************
     C           SRDMY2    BEGSR                           * S R D M Y 2 *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRDMY2'  @STK,Q
     C*
     C* Move the day number and a delimiter:
     C*
     C                     Z-ADD1         #A
     C                     MOVEADAY2      OUT,#A
     C                     ADD  2         #A
     C                     MOVE #DELIM    OUT,#A
     C                     ADD  1         #A
     C*
     C* Move the month number and a delimiter:
     C*
     C                     MOVEAMONA      OUT,#A
     C                     ADD  2         #A
     C                     MOVE #DELIM    OUT,#A
     C                     ADD  1         #A
     C*
     C* Move the year, and set the length of the result:
     C*
     C/COPY WNCPYSRC,CGZ1200C04
     C                     MOVEAYEAR2     OUT,#A
     C           #A        ADD  1         P0VLEN
     C/COPY WNCPYSRC,CGZ1200C05
     C*
     C           EXDMY2    TAG                             <<<=== EXDMY2
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRMDY2 formats the date as,                        **
     C**   for example, "08-24-94".                                     **
     C********************************************************************
     C           SRMDY2    BEGSR                           * S R M D Y 2 *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRMDY2'  @STK,Q
     C*
     C* Move the month number and a delimiter:
     C*
     C                     Z-ADD1         #A
     C                     MOVEAMONA      OUT,#A
     C                     ADD  2         #A
     C                     MOVE #DELIM    OUT,#A
     C                     ADD  1         #A
     C*
     C* Move the day number and a delimiter:
     C*
     C                     MOVEADAY2      OUT,#A
     C                     ADD  2         #A
     C                     MOVE #DELIM    OUT,#A
     C                     ADD  1         #A
     C*
     C* Move the year, and set the length of the result:
     C*
     C                     MOVEAYEAR2     OUT,#A
     C           #A        ADD  1         P0VLEN
     C*
     C           EXMDY2    TAG                             <<<=== EXMDY2
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C/COPY WNCPYSRC,CGZ1200C09
     C********************************************************************
     C**  Subroutine SRISO formats the date as,                         **
     C**   for example, "940824".                                       **
     C********************************************************************
     C           SRISO     BEGSR                           * S R I S O   *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRISO '  @STK,Q
     C*
     C* Move the year:
     C                     Z-ADD1         #A
     C                     MOVEAYEAR2     OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the month number:
     C*
     C                     MOVEAMONA      OUT,#A
     C                     ADD  2         #A
     C*
     C* Move the day number, and set the length of the result:
     C*
     C                     MOVEADAY2      OUT,#A
     C           #A        ADD  1         P0VLEN
     C*
     C           EXISO     TAG                             <<<=== EXISO
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRBADF handles an invalid format flag.             **
     C********************************************************************
     C           SRBADF    BEGSR                           * S R B A D F *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRBADF'  @STK,Q
     C*
     C* Flag the error:
     C                     MOVE 'CGD1629' P0RTN
     C*
     C           EXBADF    TAG                             <<<=== EXBADF
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRDNUM handles a day number.                       **
     C********************************************************************
     C           SRDNUM    BEGSR                           * S R D N U M *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRDNUM'  @STK,Q
     C*
     C                     MOVELP0VIN     P1DAYN
     C                     MOVE 'D'       P1DFMT
     C*
     C                     CALL 'ZA0140'
     C                     PARM           P1DAYN  50
     C                     PARM           P1DFMT  1
     C                     PARM           P1DATE  60
     C                     PARM           P1DATA  7
     C                     PARM           P1RTN   1
     C                     PARM           P1DAT8  80
     C*
     C                     MOVELP1DAT8    P0VIN
     C*
     C           EXDNUM    TAG                             <<<=== EXDNUM
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRINIT provides initialisation.                    **
     C********************************************************************
     C           SRINIT    BEGSR                           * S R I N I T *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C* Define and clear error return code:
     C*
     C                     MOVE *BLANKS   P0RTN
     C*
     C* Move the copyright parameter:
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C* /Copy point for calculations:
     C*
     C/COPY WNCPYSRC,CGZ1200CPG
     C*
     C* Split options value:
     C*
     C                     MOVELP0FOPT    #ETYPE  8
     C                     MOVE P0FOPT    #DELIM  1
     C********** #DELIM    IFEQ *BLANK                                    193060
     C**********           MOVE '-'       #DELIM                          193060
     C**********           ENDIF                                          193060
     C*
     C* Handle a day number:
     C*
     C           P0FTYP    IFEQ #DAYNO
     C                     EXSR SRDNUM
     C                     ENDIF
     C*
     C* Clear the output values and the length:
     C*
     C                     MOVE *BLANKS   P0VOUT
     C                     MOVE *BLANKS   EDITDS
     C                     Z-ADD*ZERO     P0VLEN
     C/COPY WNCPYSRC,CGZ1200C10
     C*
     C           EXINIT    TAG                             <<<=== EXINIT
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/COPY WNCPYSRC,CGZ1200C06
     C/EJECT
     C********************************************************************
     C** Subroutine *PSSR handles program errors.                       **
     C********************************************************************
     C*
     C/COPY CGCPYSRC,SRERRPSSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* File and Program Error Processing
     C*
     C/COPY CGCPYSRC,SRERRC
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* /Copy point for calculation specifications:
     C*
     C/COPY WNCPYSRC,CGZ1200CPG
     C*
     C********************************************************************
     O/EJECT
     O********************************************************************
     C* /Copy point for output:
     C*
     O/COPY WNCPYSRC,CGZ1200OPG
     C*
     O********************************************************************
** CPY@
(c) Finastra International Limited 2001
** MN1 / LEN -- Long month names and their lengths.
January  7
February 8
March    5
April    5
May      3
June     4
July     4
August   6
September9
October  7
November 8
December 8
** MN2 -- Short month names.
JAN
FEB
MAR
APR
MAY
JUN
JUL
AUG
SEP
OCT
NOV
DEC
** SFX -- Day number suffixes.
st  1
nd  2
rd  3
th  4
th  5
th  6
th  7
th  8
th  9
th 10
th 11
th 12
th 13
th 14
th 15
th 16
th 17
th 18
th 19
th 20
st 21
nd 22
rd 23
th 24
th 25
th 26
th 27
th 28
th 29
th 30
st 31
     X/COPY WNCPYSRC,CGZ1200X01
