     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas CG Deal type confirmation narratives.')
     F*****************************************************************
     F*                                                               *
     F*  Midas - User Defined Correspondence                  *
     F*                                                               *
     F*  CGZ2020 - - - - Convert a deal type to a                     *
     F*                   deal confirmation narrative.                *
     F*                                                               *
     F*  Function:   - - This program returns the appropriate         *
     F*                   confirmation narrative for a deal type.     *
     F*                                                               *
     F*  Parameters: - - P0RTN  --   7    -- Return code;             *
     F*                  P0VIN  -- 256    -- Input value;             *
     F*                  P0VOUT -- 256    -- Output value;            *
     F*                  P0VLEN --   3(0) -- Output value length;     *
     F*                  P0FTYP --  10    -- Format type;             *
     F*                  P0FOPT --  15    -- Format option.           *
     F*                                                               *
     F*  Called by:  - - CGxxxx.                                      *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG11112           Date 07Jun06               *
      *  Prev Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01522             Date 01Jan95               *
      *                                    Date ddmmmyy               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  BUG11112 - Correspondence Manager changes for CDL033         *
      *             enhancement.                                      *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  S01522 - User Defined Correspondence                         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*  90 -- Value found.                                           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  Subroutine usage                                             *
     F*  ~~~~~~~~~~~~~~~~                                             *
     F*  SRCONV -- Convert the code to a narrative.                   *
     F*  SRINIT -- Provide initialisation and definitions.            *
     F*                                                               *
     F*  Copied in routines:                                          *
     F*                                                               *
     F*  *PSSR  -- Program error routine.                             *
     F*  SRFILE -- File error routine.                                *
     F*  SRERR  -- Error reporting routine.                           *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*-------------------------------------------------------------------
     E* Error processing array:
     E*
     E/COPY CGCPYSRC,SRERRE
     E*
     E                    CPY@    1   1 80               Copyright
     E**********          #DT     1  10  2               Deal type codes.                   BUG11112
     E                    #DT     1  11  2               Deal type codes.                   BUG11112
     E**********          #N1     1  10 30               Narratives (1).                    BUG11112
     E                    #N1     1  11 30               Narratives (1).                    BUG11112
     E**********          #N2     1  10 30               Narratives (2).                    BUG11112
     E                    #N2     1  11 30               Narratives (2).                    BUG11112
     E*
     E* Copied-in array definitions:
     E*
     E/COPY WNCPYSRC,CGZ2020EPG
     E*
     E*-------------------------------------------------------------------
     I/SPACE
     I*-------------------------------------------------------------------
     I/COPY WNCPYSRC,CGZ2020IPG
     I*
     I* Copied-in data structures
     I*
     I*...................................................................
     I/COPY CGCPYSRC,SRERRI
     I*
     I* Error processing data structures
     I********************************************************************
     C/EJECT
     C********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           P0VIN 256
     C                     PARM           P0VOUT256
     C                     PARM           P0VLEN  30
     C                     PARM           P0FTYP 10
     C                     PARM           P0FOPT 15
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'@Main@'  @STK,Q
     C*
     C* Initialise:
     C                     EXSR SRINIT
     C*
     C* Convert the value:
     C                     EXSR SRCONV
     C*
     C           EXMAIN    TAG                             <<<=== EXMAIN
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     RETRN
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRCONV returns the narrative for a given code.     **
     C********************************************************************
     C           SRCONV    BEGSR                           * S R C O N V *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q
     C                     MOVEL'SRCONV'  @STK,Q
     C*
     C* Move the input value:
     C*
     C                     MOVELP0VIN     #CONUM  9
     C                     MOVEL#CONUM    #CODE   2
     C                     MOVE #CONUM    #NUM    70
     C*
     C* Find the narrative corresponding to the deal type
     C*  (#CODE) and rollover of deal number (#NUM):
     C*
     C                     Z-ADD1         #C      30
     C           #CODE     LOKUP#DT,#C                   90
     C           *IN90     IFEQ *ON
     C           #NUM      IFEQ *ZERO
     C                     MOVEL#N1,#C    P0VOUT
     C                     ELSE
     C                     MOVEL#N2,#C    P0VOUT
     C                     ENDIF
     C                     ENDIF
     C*
     C* Get the length of the returned narrative,
     C*  with a limit of the entered value:
     C*
     C           ' '       CHEKRP0VOUT    P0VLEN
     C           P#VLEN    IFLT P0VLEN
     C           P#VLEN    ANDNE*ZERO
     C                     Z-ADDP#VLEN    P0VLEN
     C                     ENDIF
     C*
     C           EXCONV    TAG                             <<<=== EXCONV
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C**  Subroutine SRINIT provides initialisation.                    **
     C********************************************************************
     C           SRINIT    BEGSR                           * S R I N I T *
     C*
     C* Add subroutine name to stack:
     C*
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
     C*
     C* Define and clear error return code:
     C*
     C                     MOVE *BLANKS   P0RTN
     C*
     C* Move the copyright parameter:
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C* /Copy point for calculations:
     C*
     C/COPY WNCPYSRC,CGZ2020CPG
     C*
     C* Save the input length:
     C*
     C                     Z-ADDP0VLEN    P#VLEN  30
     C*
     C* Clear output length and value:
     C*
     C                     Z-ADD*ZERO     P0VLEN
     C*
     C                     MOVE *BLANKS   P0VOUT
     C*
     C           EXINIT    TAG                             <<<=== EXINIT
     C*
     C* Unwind subroutine stack:
     C*
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C** Subroutine *PSSR handles program errors.                       **
     C********************************************************************
     C*
     C/COPY CGCPYSRC,SRERRPSSR
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* File and Program Error Processing
     C*
     C/COPY CGCPYSRC,SRERRC
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C* /Copy point for calculation specifications:
     C*
     C/COPY WNCPYSRC,CGZ2020CPG
     C*
     C********************************************************************
     O/EJECT
     O********************************************************************
     C* /Copy point for output:
     C*
     O/COPY WNCPYSRC,CGZ2020OPG
     C*
     O********************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2001
** #DT -- Deal types.
CD
CI
CL
DL
IP
IT
LN
TD
TI
ZZ
CF                                                                                          BUG11112
** #N1 -- Narratives where the rollover of deal number is zero.
                       WE CONFIRM YOUR DEPOSIT WITH US VALUE| CD
                 WE CONFIRM OUR ISSUE TO YOU OF A C.D. VALUE| CI
                            WE CONFIRM OUR LOAN TO YOU VALUE| CL
                            WE CONFIRM OUR LOAN TO YOU VALUE| DL
                            WE CONFIRM OUR LOAN TO YOU VALUE| IP
                       WE CONFIRM YOUR DEPOSIT WITH US VALUE| IT
                            WE CONFIRM OUR LOAN TO YOU VALUE| LN
                       WE CONFIRM YOUR DEPOSIT WITH US VALUE| TD
                            WE CONFIRM OUR LOAN TO YOU VALUE| TI
                       WE CONFIRM YOUR DEPOSIT WITH US VALUE| CF                            BUG11112
-------Spare-------                                         |
** #N2 -- Narratives where the rollover of deal number is NOT zero.
            WE CONFIRM RENEWAL OF YOUR DEPOSIT WITH US VALUE| CD
                 WE CONFIRM OUR ISSUE TO YOU OF A C.D. VALUE| CI
                 WE CONFIRM RENEWAL OF OUR LOAN TO YOU VALUE| CL
                 WE CONFIRM RENEWAL OF OUR LOAN TO YOU VALUE| DL
                 WE CONFIRM RENEWAL OF OUR LOAN TO YOU VALUE| IP
            WE CONFIRM RENEWAL OF YOUR DEPOSIT WITH US VALUE| IT
                 WE CONFIRM RENEWAL OF OUR LOAN TO YOU VALUE| LN
            WE CONFIRM RENEWAL OF YOUR DEPOSIT WITH US VALUE| TD
                 WE CONFIRM RENEWAL OF OUR LOAN TO YOU VALUE| TI
            WE CONFIRM RENEWAL OF YOUR DEPOSIT WITH US VALUE| CF                            BUG11112
-------Spare-------                                         |
