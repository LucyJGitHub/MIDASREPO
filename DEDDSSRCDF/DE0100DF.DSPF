      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DE I/c account codes to be extracted maint')
      *****************************************************************
      *                                                               *
      *  Midas - Data Export Module                                   *
      *                                                               *
      *  DE0100DF - Account Codes to be Extracted Maintenance         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CDE002             Date 05Dec00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CDE001  *CREATE    Date 23Nov99               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDE002 - Revenue Analysis                                    *
      *  CDE001 - Data Export - CCRM Limits.                          *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
      *
      *--------------------------*
      ***  Subfile Record Format *
      *--------------------------*
      *
     A          R DE0100S1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: DDACTN')
     A                                      INDTXT(33 'Invalid: DDACOD')
     A                                      INDTXT(34 'Invalid: DDILME')
     A                                      INDTXT(35 'Invalid: DDIRVE')
     A**Start of Change CDE002
     A                                      INDTXT(38 'Invalid: DDASOC')
     A                                      INDTXT(39 'Invalid: DDSELP')
     A**End of Change CDE002
     A  84                                  SFLNXTCHG
     A**Start of Change CDE002
     A**********  #1DBRC        31   H      TEXT('Existing record fields')                    CDE002
      **Start of change CGL029
     A**********  #1DBRC        33   H      TEXT('Existing record fields')                    CGL029
     A            #1DBRC        43   H      TEXT('Existing record fields')
      **End of change CGL029
     A**End of Change CDE002
      **Start of change CGL029
     A**********  #1ACCD         4   H      TEXT('Account Code')                              CGL029
     A            #1ACCD        10   H      TEXT('Account Code')
      **End of change CGL029
     A            #1ACCN        25   H      TEXT('Acount Description')
     A            #1ILME         1   H      TEXT('Inc. in Limits')
     A            #1IRVE         1   H      TEXT('Inc. in Revenue')
     A**Start of Change CDE002
     A            #1ASOC         1   H      TEXT('Assoc. Cust')
     A            #1SELP         1   H      TEXT('Select Postings')
     A            #1WARN         1   H      TEXT('Warning flag')
     A            #1IRVP         1   H      TEXT('Previous Value of IRVE')
      **Start of change CGL029
     A**********  #1ACCP         4   H      TEXT('Previous Value of ACOD')                    CGL029
     A            #1ACCP        10   H      TEXT('Previous Value of ACOD')
      **End of change CGL029
     A**End of Change CDE002
     A            DDACTN         1A  B  8  4VALUES('D')
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL)
     A N79N32                               DSPATR(HI)
     A**Start of Change CDE002
     A**********  DDACOD         4A  B  8 13                                                  CDE002
      **Start of change CGL029
     A**********  DDACOD         4A  B  8 12                                                  CGL029
     A            DDACOD        10A  B  8 10
      **End of change CGL029
     A**End of Change CDE002
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N78N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A**Start of Change CDE002
     A**********  DDDESC        25A  B  8 24                                                  CDE002
     A            DDDESC        25A  B  8 22
     A**End of Change CDE002
     A  87                                  DSPATR(PR)
     A N87                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A**Start of Change CDE002
     A**********  DDILME         1A  B  8 54                                                  CDE002
     A            DDILME         1A  B  8 51
     A**End of Change CDE002
     A N50                                  DSPATR(ND)
     A N50
     AO 87                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A**Start of Change CDE002
     A**********  DDIRVE         1A  B  8 63                                                  CDE002
     A            DDIRVE         1A  B  8 60
     A**End of Change CDE002
     A N50                                  DSPATR(ND)
     A N50
     AO 87                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A**Start of Change CDE002
     A            DDASOC         1A  B  8 67
     A N50                                  DSPATR(ND)
     A N50
     AO 87                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N87N38                               DSPATR(UL)
     A N38                                  DSPATR(HI)
      *
     A            DDSELP         1A  B  8 75
     A N50                                  DSPATR(ND)
     A N50
     AO 87                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N87N39                               DSPATR(UL)
     A N39                                  DSPATR(HI)
     A**End of Change CDE002
      *
     A*=========================================================================
      *
      *--------------------*
      ***  Subfile Control *
      *--------------------*
      *
     A          R DE0100C1                  SFLCTL(DE0100S1)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CA05(05 'Refresh')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: DPACOD')
     A*
     A            SRRND          4S 0H      SFLRCDNBR(CURSOR)
     A            ##USR     R        O  1  2REFFLD(USER)
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'DE0100DF'
     A                                  2 20'Account Codes to be Extracted +
     A                                       Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A N25                                  OVRDTA
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A N25                                  OVRDTA
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action'
     A**Start of Change CDE002
     A**********                        4 10'Account Code'                                    CDE002
     A**********                        4 24'Description'                                     CDE002
     A**50******                        3 52'Inc in'                                          CDE002
     A**50******                        3 60'Inc in'                                          CDE002
     A**50******                        4 52'Limits'                                          CDE002
     A**50******                        4 60'Revenue'                                         CDE002
     A                                  4  9'Account Code'
     A                                  4 29'Description'
     A  50                              3 49'Inc in'
     A  50                              3 57'Inc in'
     A  50                              3 65'Assoc.'
     A  50                              3 72'Select'
     A  50                              4 49'Limits'
     A  50                              4 57'Revenue'
     A  50                              4 65'Cust.'
     A  50                              4 72'Postings'
     A**End of Change CDE002
     A                                  5  2'------'
     A**Start of Change CDE002
     A**********                        5 10'------------'                                    CDE002
     A**********                        5 24'-------------------------'                       CDE002
     A**50******                        5 52'------'                                          CDE002
     A**50******                        5 60'-------'                                         CDE002
     A                                  5  9'------------'
     A                                  5 22'-------------------------'
     A  50                              5 49'------'
     A  50                              5 57'-------'
     A  50                              5 65'------'
     A  50                              5 72'--------'
     A*N89******  DPACOD         4A  B  6 13                                                  CDE002
     A**Start of Change CGL029
     A*N89******  DPACOD         4A  B  6 12                                                  CGL029
     A N89        DPACOD        10A  B  6 10
     A**End of Change CGL029
     A**End of Change CDE002
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A**Start of Change CDE002
     A*N89******  DPDESC        25A  B  6 24                                                  CDE002
     A N89        DPDESC        25A  B  6 22
     A**End of Change CDE002
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A N25                                  OVRATR
     A**Start of Change CDE002
     A*N89******  DPILME         1A  B  6 54                                                  CDE002
     A N89        DPILME         1A  B  6 51
     A**End of Change CDE002
     A N25                                  OVRDTA
     A  36 50                               DSPATR(RI)
     A  36 50                               DSPATR(PC)
     A N36 50                               DSPATR(UL)
     A N36 50                               DSPATR(HI)
     A N50                                  DSPATR(ND PR)
     A N25                                  OVRATR
     A**Start of Change CDE002
     A*N89******  DPIRVE         1A  B  6 63                                                  CDE002
     A N89        DPIRVE         1A  B  6 60
     A**End of Change CDE002
     A N25                                  OVRDTA
     A  37 50                               DSPATR(RI)
     A  37 50                               DSPATR(PC)
     A N37 50                               DSPATR(UL)
     A N37 50                               DSPATR(HI)
     A N50                                  DSPATR(ND PR)
     A N25                                  OVRATR
     A**Start of Change CDE002
     A N89        DPASOC         1A  B  6 67
     A N25                                  OVRDTA
     A  40 50                               DSPATR(RI)
     A  40 50                               DSPATR(PC)
     A N40 50                               DSPATR(UL)
     A N40 50                               DSPATR(HI)
     A N50                                  DSPATR(ND PR)
     A N25                                  OVRATR
      *
     A N89        DPSELP         1A  B  6 75
     A N25                                  OVRDTA
     A  41 50                               DSPATR(RI)
     A  41 50                               DSPATR(PC)
     A N41 50                               DSPATR(UL)
     A N41 50                               DSPATR(HI)
     A N50                                  DSPATR(ND PR)
     A N25                                  OVRATR
     A**End of Change CDE002
     A*
     A*=========================================================================
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R DE0100C2
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A N89                             23 35'F9=Add'
     A  89                             23 35'F9=Change'
     A*=========================================================================
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R #MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A                                      TEXT('Message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A  86                                  OVERLAY PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
