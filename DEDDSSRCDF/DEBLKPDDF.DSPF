      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DE I/c bulk transfer prompt display')
      *****************************************************************
      *                                                               *
      *  Midas - Data Export Module                                   *
      *                                                               *
      *  DEBLKPDDF - I/C Bulk Transfer Prompt Display                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CDE002             Date 14Apr00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CDE001  *CREATE    Date 16Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDE002 - Data Export - CCRM Revenue Analysis.                *
      *  CDE001 - Data Export - CCRM Limits.                          *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A*
     A* Header format
     A*
     A          R DEBLKPD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'DEBLKPD'
      **Start of Change CDE002
     A**********                        1 29'CCRM - LIMITS BULK TRANSFER'                     CDE002
     A  01                              1 29'CCRM - LIMITS BULK TRANSFER'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  02                              1 29'CCRM - REVENUE BULK TRANSFER'
      **End of Change CDE002
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 34'PROMPT PROGRAM'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A*
     A*=========================================================================
     A          R DEBLKPD1
     A N40                                  CA05(05 'Refresh')
     A N40                                  CA12(12 'Cancel')
     A                                      OVERLAY
     A                                  9  7'Departmental Server:'
     A  40                                  DSPATR(ND)
     A            TRANSF         2A  B  9 30
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(HI)
     A N41                                  DSPATR(UL)
     A  40                                  DSPATR(ND)
     A  40                                  DSPATR(PR)
     A                                 23  2'F3=Exit'
     A N40                             23 12'F5=Refresh'
     A N40                             23 25'F12=Prev Scrn'
      *
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A                                      TEXT('Message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  45                                  SFLEND
     A            ##PGM                     SFLPGMQ
