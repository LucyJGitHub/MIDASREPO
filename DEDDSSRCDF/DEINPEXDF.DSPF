      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DE Input extract control information')
      *****************************************************************
      *                                                               *
      *  Midas - Data Export Module                                   *
      *                                                               *
      *  DEINPEXDF  - INPUT EXTRACT CONTROL INFORMATION               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CDE004             Date 02Jul01               *
      *  Prev Amend No. CDE002             Date 16Feb00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CDE001  *CREATE    Date 23Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDE004 - Cross Modular History Extract                       *
      *  CDE002 - CCRM Revenue Analysis, add revenue default base     *
      *           rate code to screen                                 *
      *  CDE001 - Data Export - CCRM Limits.                          *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R DEEXCTS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R DEEXCTS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      SFLCTL(DEEXCTS1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R DEEXCTD1                  TEXT('MAIN DISPLAY SCREEN')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      **Start of Change CDE002
      **End of Change CDE002
     A**Start of change CDE004
     A**End of change CDE004
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'DEINPEXCT'
     A                                  2 25'Input Extract Control Information'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*
     A                                  5 26'Projected Days:'
     A  20                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPCOD         3A  B  5 47
     A                                      OVRDTA
     A  20                                  DSPATR(PR ND)
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(UL)
     A                                  7 26'Projected Months:'
     A  20                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPCOM         2A  B  7 47
     A                                      OVRDTA
     A  20                                  DSPATR(PR ND)
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(UL)
     A                                  9 26'Projected Years:'
     A  20                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPCOY         2A  B  9 47
     A                                      OVRDTA
     A  20                                  DSPATR(PR ND)
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(UL)
     A                                 11 26'Historic Days:'
     A  21                                  DSPATR(ND)
     A                                      OVRATR
     A            DDHCOD         3A  B 11 47
     A                                      OVRDTA
     A  21                                  DSPATR(PR ND)
     A  13                                  DSPATR(RI)
     A  13
     AO 20N21N14                            DSPATR(PC)
     A N13                                  DSPATR(UL)
     A                                 13 26'Departmental Server:'
     A            DDDPSV         2A  B 13 47
     A                                      OVRDTA
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(UL)
      **Start of Change CDE002
     A                                 15 26'Default Funding'
     A  21                                  DSPATR(ND)
     A                                 16 26'Rate:'
     A  21                                  DSPATR(ND)
     A            DDRDFR         2A  B 16 47
     A                                      OVRDTA
     A  21                                  DSPATR(PR ND)
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(UL)
      **End of Change CDE002
      **Start of change CDE004
     A                                 17  4'Transaction History Retention +
     A                                      Days'
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 18  7'Dealing:'
     A N31                                  DSPATR(ND)
     A                                      OVRATR
     A            #1PDDL         4A  B 18 25
     A                                      OVRDTA
     A  31                                  DSPATR(UL)
     A  31 32                               DSPATR(RI PC)
     A N31                                  DSPATR(PR ND)
      *
     A                                 18 40'Futures & Options:'
     A N31                                  DSPATR(ND)
     A                                      OVRATR
     A            #1PDFF         4A  B 18 70
     A                                      OVRDTA
     A  31                                  DSPATR(UL)
     A  31 33                               DSPATR(RI PC)
     A N31                                  DSPATR(PR ND)
      *
     A                                 19  7'FRA/IRS:'
     A                                      OVRATR
     A N31                                  DSPATR(ND)
     A            #1PDFR         4A  B 19 25
     A                                      OVRDTA
     A  31                                  DSPATR(UL)
     A  31 34                               DSPATR(RI PC)
     A N31                                  DSPATR(PR ND)
      *
     A  31                             19 40'Lending Fees:'
     A                                      OVRATR
     A N31                                  DSPATR(ND)
     A            #1PDLF         4A  B 19 70
     A                                      OVRDTA
     A  31                                  DSPATR(UL)
     A  31 35                               DSPATR(RI PC)
     A N31                                  DSPATR(PR ND)
      *
     A                                 20  7'Funds Transfer:'
     A                                      OVRATR
     A N31                                  DSPATR(ND)
     A            #1PDFT         4A  B 20 25
     A                                      OVRDTA
     A  31                                  DSPATR(UL)
     A  31 36                               DSPATR(RI PC)
     A N31                                  DSPATR(PR ND)
      *
     A                                 20 40'Retail Customer Exchanges:'
     A                                      OVRATR
     A N31                                  DSPATR(ND)
     A            #1PDCE         4A  B 20 70
     A                                      OVRDTA
     A  31                                  DSPATR(UL)
     A  31 37                               DSPATR(RI PC)
     A N31                                  DSPATR(PR ND)
      **End of Change CDE004
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R DEEXCTF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 24  1'F3=Exit'
