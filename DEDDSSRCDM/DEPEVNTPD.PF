      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NM                                                        *
/*EXI *  MBR(INT)                                                     *
/*EXI *  TEXT('Midas DE Projected events')
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(ACCT) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(CFCO) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(CUSX) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(EXTR) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(FCLT) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(FEES) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(FRAS) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(FUND) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(FXDL) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(LDNI) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(LOAN) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(NASP) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(OTCC) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(SBPS) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(STRD) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPEVNTPD) MBR(SWAP) SHARE(*YES)                 *
      *****************************************************************
      *                                                               *
      *  Midas - Data Export Module                                   *
      *                                                               *
      *  PF/DEPEVNTPD - Projected Events                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 16Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002             Date 05Dec00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CDE001  *CREATE    Date 17Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements for Phase 1 Priority 1A.       *
      *  CDE002 - Data Export - CCRM Revenue Analysis                 *
      *  CDE001 - Data Export - CCRM Limits                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R DEEVNTP0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            E#MOD          2A         TEXT('Module')
      **Start of change CGL029
     A**********  E#TREF        20A         TEXT('Transaction Reference')                     CGL029
     A            E#TREF        26A         TEXT('Transaction Reference')
      **End of change CGL029
     A            E#ASLI         1A         TEXT('Asset/Liability')
     A            E#PRDT         5P 0       TEXT('Processing Date')
     A            E#PRSQ         2S 0       TEXT('Processing Seq')
     A            E#EVTP         2A         TEXT('Event Type')
     A            E#EVRF         6A         TEXT('Event Reference')
     A*
      * Start of change CSD103
     A**********  E#BRTT         2S 0       TEXT('Base Rate Code')              CSD103
     A            E#BRTT         2A         TEXT('Base Rate Code')
      * End of change CSD103
     A            E#BRTE        11P 7       TEXT('Base Rate')
     A            E#RTSP        11P 7       TEXT('Rate/Spread')
     A            E#SPIN         1A         TEXT('Spread Ind')
     A            E#IRTP         1A         TEXT('Interest Rate Type')
     A            E#IRCH         1A         TEXT('Interest Rate Change')
     A            E#IRAT        11P 7       TEXT('Interest Rate')
     A*
     A            E#FSRP        11P 7       TEXT('Funding Rate/Spread')
     A            E#FSGN         1A         TEXT('Funding Sign')
     A            E#CRTP         1A         TEXT('Cost of Funds Rate Type')
     A            E#CRCH         1A         TEXT('Cost of Funds Rate Change')
     A            E#CRAT        11P 7       TEXT('Cost of Funds Rate')
     A*
     A            E#ICMT         7A         TEXT('Int Calculation Method')
     A            E#INTC         1A         TEXT('Int Capitalization Ind')
     A            E#STAC         1A         TEXT('Stop accruals - Y/blank')
     A*
     A            E#PRAM        15P 0       TEXT('Principal Amount')
     A            E#INTA        15P 0       TEXT('Interest Amount')
     A            E#COFA        15P 0       TEXT('Cost of Funds Amount')
     A            E#FEE         15P 0       TEXT('Fees')
     A            E#CHRG        15P 0       TEXT('Charges')
     A            E#COMM        15P 0       TEXT('Commissions')
     A            E#TAMT        15P 0       TEXT('Total Amount')
     A*
     A            E#NOML        15P 0       TEXT('Nominal')
     A            E#CCY          3A         TEXT('Currency')
     A            E#NCDP         1S 0       TEXT('Ccy Dec Places')
     A            E#NMDP         1S 0       TEXT('Nom Dec Places')
     A*
     A            E#ACIN         1A         TEXT('Accrued Ind.')
     A            E#PRIC        15P 8       TEXT('Price')
     A*
     A            E#CPDP        15P 0       TEXT('Current Partly Paid Price')
     A            E#CFCT        10P 9       TEXT('Current Factor')
     A*
     A            E#SSET         1A         TEXT('Suppress Settlement Y')
     A            E#SETM         2A         TEXT('Settle Method')
      **Start of change CGL029
     A**********  E#SETA        12A         TEXT('Settle A/C')                                CGL029
     A            E#SETA        18A         TEXT('Settle A/C')
      **End of change CGL029
     A            E#SETB         3A         TEXT('Settle Branch')
     A            E#SETC         3A         TEXT('Settle Currency')
     A*
     A            E#PRCD         1A         TEXT('Processed - Y/blank')
     A            E#IO           1A         TEXT('In/Out - I/O/blank')
     A            E#NXCD         5P 0       TEXT('Next Cash Date')
     A            E#CASH        15P 0       TEXT('Next Cash Amount')
      *                                              Begin /CDE002/
     A            E#FEEC         2A         TEXT('Fee Code')
     A            E#FXMG        15P 0       TEXT('FX Margin')
     A            E#MVNO         5S 0       TEXT('Number of Movements')
      *                                              End   /CDE002/
      *                                              Begin /CLE034/
     A            E#STAL         1A         TEXT('Settlement Allocation')
     A            E#LASN         3S 0       TEXT('Loan Amendment Sequence +
     A                                            Number')
      *                                              End   /CLE034/
