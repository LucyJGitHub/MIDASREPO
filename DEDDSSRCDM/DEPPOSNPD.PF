      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NM                                                        *
/*EXI *  MBR(INT)                                                     *
/*EXI *  TEXT('Midas DE Projected positions')
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(ACCT) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(CFCO) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(CUSX) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(EXTR) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(FCLT) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(FEES) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(FRAS) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(FUND) SHARE(*YES)                 *                       CDE002
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(FXDL) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(LDNI) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(LOAN) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(NASP) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(OTCC) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(SBPS) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(STRD) SHARE(*YES)                 *
/*ADD *  ADDPFM FILE(DEPPOSNPD) MBR(SWAP) SHARE(*YES)                 *
      *****************************************************************
      *                                                               *
      *  Midas - Data Export Module                                   *
      *                                                               *
      *  PF/DEPPOSNPD - Projected Positions                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 216909             Date 15Apr03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002             Date 05Dec00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CDE001  *CREATE    Date 17Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  216909 - Addition of field P#BNDP                            *
      *  CDE002 - Data Export - CCRM Revenue Analysis                 *
      *  CDE001 - Data Export - CCRM Limits                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R DEPOSNP0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            P#MOD          2A         TEXT('Module')
      **Start of change CGL029
     A**********  P#TREF        20A         TEXT('Transaction Reference')                     CGL029
     A            P#TREF        26A         TEXT('Transaction Reference')
      **End of change CGL029
     A            P#ASLI         1A         TEXT('Asset/Liability')
     A*
     A            P#PSTP         2A         TEXT('Position Type')
     A*
     A            P#IACD         5P 0       TEXT('Interest Accrl Ctl Date')
     A*
     A            P#AITC        15P 0       TEXT('Accrued Int To Ctl Date')
     A            P#IPRD        15P 0       TEXT('Int Paid To Date')
     A            P#ICTD        15P 0       TEXT('Int Capitalized To Date')
     A            P#IWOD        15P 0       TEXT('Int Written Off To Date')
     A            P#AIAD        15P 0       TEXT('Accrued Int Adjustments')
     A            P#TOTI        15P 0       TEXT('Total Int')
     A            P#INAC        15P 0       TEXT('Int Accrued')
     A*
     A            P#ACAD        15P 0       TEXT('Accrued COF Adjustments')
     A            P#TOTC        15P 0       TEXT('Total COF')
     A            P#CFAC        15P 0       TEXT('COF Accrued')
     A*
     A            P#DIPR        15P 0       TEXT('Discount/Premium')
     A            P#DPAC        15P 0       TEXT('Discount/Premium Accrued')
     A*
     A            P#CASH        15P 0       TEXT('Cash')
     A            P#CSAC        15P 0       TEXT('Cash Accrued')
     A*
     A            P#FEE         15P 0       TEXT('Fee')
     A            P#FEAC        15P 0       TEXT('Fee Accrued')
     A*
     A            P#RLPL        15P 0       TEXT('Realized P/L')
     A            P#RLAC        15P 0       TEXT('Realized P/L Accrued')
     A*
     A            P#SDAT         5P 0       TEXT('Start Date of Int Calc')
     A            P#EDAT         5P 0       TEXT('End Date of Int Calc')
     A            P#NDYS         5P 0       TEXT('Number of Days')
     A            P#YBAS         5P 0       TEXT('Year Basis')
     A*
     A            P#NOML        15P 0       TEXT('Nominal/Principal')
     A            P#XNML        15P 0       TEXT('Ex-Coupon Nominal')
     A            P#CCY          3A         TEXT('Currency')
     A            P#NCDP         1S 0       TEXT('Nominal Ccy Dec Places')
     A            P#NMDP         1S 0       TEXT('Nominal Dec Places')
     A            P#SPDY         2S 0       TEXT('Nominal Ccy Spot Days')
     A*
     A            P#CONS        15P 0       TEXT('Consideration')
     A            P#AVPR        15P 8       TEXT('Average Price')
     A            P#PINT        15P 0       TEXT('Purchased Interest')
     A            P#CHRG        15P 0       TEXT('Charges')
     A            P#COMM        15P 0       TEXT('Commissions')
     A*
     A            P#BRTT         2S 0       TEXT('Base Rate Code')
     A            P#BRTE        11P 7       TEXT('Base Rate')
     A            P#RTSP        11P 7       TEXT('Rate/Spread')
     A            P#SPIN         1A         TEXT('Spread Ind')
     A            P#IRAT        11P 7       TEXT('Interest/Discount Rate')
     A            P#CHIN         1A         TEXT('Change Ind')
     A*
     A            P#FSRP        11P 7       TEXT('Funding Rate/Spread')
     A            P#FSGN         1A         TEXT('Funding Sign')
     A            P#CRAT        11P 7       TEXT('Cost of Funds Rate')
     A*
     A            P#CLRT        11P 7       TEXT('Ceiling Rate')
     A            P#FLRT        11P 7       TEXT('Floor Rate')
     A*
     A            P#YRAT        11P 7       TEXT('Yield Rate')
     A*
     A            P#INPM         1A         TEXT('(Up front) Int Pay Method')
     A            P#INTC         1A         TEXT('Int Capitalization Y/N')
     A            P#ICMT         7A         TEXT('Int Calc Method')
     A            P#IPY          2S 0       TEXT('Int Payments/Year')
     A            P#RDFC         1A         TEXT('Round Down Int')
     A*
     A            P#REPT         1S 0       TEXT('Repayment Type')
     A            P#STAC         1A         TEXT('Stop Accruals')
     A            P#ACNG         1A         TEXT('Accrue Negative Balance')
     A*
     A            P#ACIN         1A         TEXT('Accrue Interest')
     A            P#ACCF         1A         TEXT('Accrue Cost of Funds')
     A            P#ACDP         1A         TEXT('Accrue Discount/Premium')
     A            P#ACCS         1A         TEXT('Accrue Cash')
     A            P#ACFE         1A         TEXT('Accrue Fee')
     A            P#ACRL         1A         TEXT('Accrue Realized P/L')
     A*
     A            P#SECU         1A         TEXT('Security?')
     A            P#SPBS         1A         TEXT('Security Price Basis')
     A            P#YBON         1A         TEXT('Yen Bond?')
     A            P#SCHU         1A         TEXT('Schuldschein?')
     A            P#MORT         1A         TEXT('Mortgage backed bond?')       * YEN BOND ?
     A            P#PART         1A         TEXT('Partly paid bond')            * YEN BOND ?
     A            P#FUPP        15P 0       TEXT('Fully Paid Price')
     A            P#ACUR         1S 0       TEXT('Accuracy')
      *                                              Begin /CDE002/
     A            P#SWPL        15P 0       TEXT('Swap Profit/Loss')
     A            P#SWAC        15P 0       TEXT('Swap Profit/Loss Accrued')
     A            P#ACSW         1A         TEXT('Accrue Swap Profit/Loss')
      *
     A            P#FXMG        15P 0       TEXT('FX Margin')
      * Statistics
     A            P#INPR        15P 0       TEXT('Interest Paid')
     A            P#INCT        15P 0       TEXT('Interest Capitalised')
     A            P#INWO        15P 0       TEXT('Interest Written Off')
     A            P#INAJ        15P 0       TEXT('Interest Adjustments')
     A            P#CFAJ        15P 0       TEXT('COF Adjustments')
     A            P#INCN         1A         TEXT('Increase in Nominal ?')
     A            P#DIFN        15P 0       TEXT('Difference in Nominal')
     A            P#ROTY         1A         TEXT('Rollover Today ?')
     A            P#PITY         1A         TEXT('Principal Increase Today ?')
     A            P#RCTY         1A         TEXT('Rate Change Today ?')
     A            P#MVNO         5S 0       TEXT('Number of Movements')
     A            P#RENO         5S 0       TEXT('Number of Repayments')
      *
     A            P#AUTO         1A         TEXT('Autosettle Indicator')
      *                                              End   /CDE002/
      *                                              Begin /216909/
     A            P#BNDP         1S 0       TEXT('No of d.places of base ccy')
      *                                              End   /216909/
