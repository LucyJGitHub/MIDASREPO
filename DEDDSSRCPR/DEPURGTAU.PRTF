      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DE Purge Generic Files - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Data Export module                                   *
      *                                                               *
      *  DEPURGTAU - Purged Generic Files - Audit                     *
      *                                                               *
      *  Function:  This printer file produces a detailed report of   *
      *             all the deleted records on the projected          *
      *             transaction extract files DEGTRANPD, DEGPOSNPD    *
      *             DEGEVNTPD and DEGVALUPD                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CDE004  *CREATE    Date 02Jul01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDE004 - Cross Modular history extract                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE +
     A                                      DEPURGTRAU'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   104'DATE'
     A            BJMRDT         7       109
     A                                   122'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   104'TIME'
     A                                   109TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'GENERIC FILES +
     A                                      RECORDS DROP'
     A                                      SPACEA(1)
      *
     A                                    50'--------------+
     A                                      ------------'
     A                                      SPACEA(1)
      *
     A  70                                53'FOREIGN EXCHANGE'
     A  71                                49'LOANS/DEPOSITS AND +
     A                                      NA ISSUED'
     A  72                                49'NEGOTIABLE ASSETS +
     A                                      PURCHASED'
     A  73                                53'INTEREST RATE SWAPS'
     A  74                                53'CAPS/COLLARS/FLOORS'
     A  75                                53'FORWARD RATE AGREEMENT'
     A  76                                55'FUNDS TRANSFER'
     A  77                                53'FUTURES & OPTIONS'
     A  78                                53'F & O OTCC OPTIONS'
     A  79                                53'CUSTOMER LENDING FEES'
     A  80                                51'RETAIL CUSTOMER +
     A                                      EXCHANGES'
     A                                   132' '
     A                                      SPACEA(1)
      *
     A  70                                53'----------------'
     A  71                                49'-------------------+
     A                                      ---------'
     A  72                                49'------------------+
     A                                      ---------'
     A  73                                53'-------------------'
     A  74                                53'-------------------'
     A  75                                53'----------------------'
     A  76                                55'--------------'
     A  77                                53'-----------------'
     A  78                                53'------------------'
     A  79                                53'---------------------'
     A  80                                51'----------------+
     A                                      ---------'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A          R NODTLS
     A                                    50'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
