      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DE Purge Generic Files')
      *****************************************************************
      *                                                               *
      *  Midas - Data Export module                                   *
      *                                                               *
      *  DEPURGTP1 - Purged Generic Files Printer File                *
      *                                                               *
      *  Function:  This printer file produces a detailed report of   *
      *             all the deleted records on the projected          *
      *             transaction extract files DEGTRANPD, DEGPOSNPD    *
      *             DEGEVNTPD and DEGVALUPD                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE004  *CREATE    Date 02Jul01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDE004 - Cross Modular history extract                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE +
     A                                      DEPURGTRP1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   104'DATE'
     A            BJMRDT         7       109
     A                                   122'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   104'TIME'
     A                                   109TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'GENERIC FILES +
     A                                      RECORDS DROP'
     A                                      SPACEA(1)
      *
     A                                    50'--------------+
     A                                      ------------'
     A                                      SPACEA(1)
      *
     A  70                                53'FOREIGN EXCHANGE'
     A  71                                49'LOANS/DEPOSITS AND +
     A                                      NA ISSUED'
     A  72                                49'NEGOTIABLE ASSETS +
     A                                      PURCHASED'
     A  73                                53'INTEREST RATE SWAPS'
     A  74                                53'CAPS/COLLARS/FLOORS'
     A  75                                53'FORWARD RATE AGREEMENT'
     A  76                                55'FUNDS TRANSFER'
     A  77                                53'FUTURES & OPTIONS'
     A  78                                53'F & O OTCC OPTIONS'
     A  79                                53'CUSTOMER LENDING FEES'
     A  80                                51'RETAIL CUSTOMER +
     A                                      EXCHANGES'
     A                                   132' '
     A                                      SPACEA(1)
      *
     A  70                                53'----------------'
     A  71                                49'-------------------+
     A                                      ---------'
     A  72                                49'------------------+
     A                                      ---------'
     A  73                                53'-------------------'
     A  74                                53'-------------------'
     A  75                                53'----------------------'
     A  76                                55'--------------'
     A  77                                53'-----------------'
     A  78                                53'------------------'
     A  79                                53'---------------------'
     A  80                                51'----------------+
     A                                      ---------'
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     6'TRANSACTION REFERENCE:'
      **Start of change CGL029
     A**********  RTDRF         20        32                                                  CGL029
     A            RTDRF         26        32
      **End of change CGL029
     A                                      SPACEA(2)
      *
     A          R SUBHD
     A                                      SPACEA(1)
     A N88                                10'POSITIONS'
     A N87                                10'EVENTS'
     A N86                                10'VALUATIONS'
     A                                   132' '
      *
     A                                      SPACEA(1)
     A N88                                10'---------'
     A N87                                10'------'
     A N86                                10'----------'
     A                                   132' '
      *
     A                                      SPACEA(1)
     A N88                                25'ASSET/LIABILITY'
     A N88                                45'POSITION TYPE'
     A N87                                25'ASSET/LIABILITY'
     A N87                                45'PROCESSING DATE'
     A N87                                65'SEQUENCE'
     A N87                                78'EVENT TYPE'
     A N87                                93'EVENT REFERENCE'
     A N86                                25'VALUATION DATE'
     A N86                                50'MARKET VALUE'
     A                                   132' '
      *
     A                                      SPACEA(1)
     A N88                                25'---------------'
     A N88                                45'-------------'
     A N87                                25'---------------'
     A N87                                45'---------------'
     A N87                                65'--------'
     A N87                                78'----------'
     A N87                                93'---------------'
     A N86                                25'--------------'
     A N86                                50'------------'
     A                                   132' '
      *
     A          R DETPOSD                   SPACEA(1)
     A            RPASLI        10        28
     A            RPPSTP         2        50
      *
     A          R DETEVNTD                  SPACEA(1)
     A            REASLI        10        28
     A            REPRDT         7        49
     A            REPRSQ         2        68
     A            REEVTP         2        82
     A            REEVRF         6        97
      *
     A          R DETVALD                   SPACEA(1)
     A            RVVLDT         7        29
     A            RVMKVL        21        46
      *
     A          R TRAILER                   SPACEB(3)
     A                                    45'*** END OF REPORT ***'
