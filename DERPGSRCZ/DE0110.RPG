     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DE Customer lending funding rate utility')       *
/*OVRF*: OVRDBF FILE(LOAMD) TOFILE(LOAMS) SHARE(*NO)                 :*
      *****************************************************************
      *                                                               *
      *  Midas - Data Export Module                                   *
      *                                                               *
      *  DE0110 - Customer Lending Funding Rate Utility               *
      *                                                               *
      *  Function:  This program is used to default funding rate      *
      *             whenever Revenue Analysis (CDE002) is installed   *
      *             on an existing loan and the margin on the loan    *
      *             has been entered but the funding rate has not.    *
      *                                                               *
      *  Called By: DEC0110 - Input Extract Control Information       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE141             Date 08Feb16               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CRE073             Date 06Dec10               *
      *                 CLE143             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CAS011             Date 16May05               *
      *                 CAS009             Date 04May04               *
      *                 CAS005             Date 16Dec02               *
      *                 CLE028             Date 27Jun02               *
      *                 CAS004             Date 26Jun02               *
      *                 208221             Date 11Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002  *CREATE    Date 01Jun01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER050 - Annualised Percentage Rate (Recompile)              *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE141 - Currency and Location Business Day Convention       *
      *           (Recompile)                                         *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CLE143 - Original Loan Contract Date & Effective Interest    *
      *           Rate (Recompile)                                    *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS011 - EUR/AC Accounting Upgrade to MP1                    *
      *           Changes of this are similar to fix 208221.          *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *           (Recompile)                                         *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *           (Recompile)                                         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  208221 - Changes identified during alpha site                *
      *  CDE002 - Date Export - CCRM Revenue Analysis                 *
      *                                                               *
      *****************************************************************
     FCLOAN   UF  E           K        DISK         KINFSR *PSSR
     F            CLOANCKF                          KIGNORE
     F            CLOANHHF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     FLOAMS   UF  E           K        DISK         KINFSR *PSSR A
     F**********                                    KCOMIT                                    208221
     FLEHISSL0UF  E           K        DISK         KINFSR *PSSR
     F**********                                    KCOMIT                                    208221
     F            HISTSHPF                          KIGNORE
     FLOAMD   IF  E           K        DISK
     F            LOAMSDKF                          KRENAMELOAMSD
     F            LOAMSDHF                          KIGNORE
     F            LOAMSZ1F                          KIGNORE
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
     ICLOANCLF
      ** Rename fields for loans file
      *
     I              RECI                            RECIC
     I              LNRF                            LNRFC
     I              VDAT                            VDATC
     I              MRIN                            MRINC
     I              LTYP                            LTYPC
     I              SUTP                            SUTPC
     I              PTYP                            PTYPC
     I              BRTT                            BRTTC
     I              RTSP                            RTSPC
     I              CCY                             CCYC
     I              BRCA                            BRCAC
     I              CNUM                            CNUMC
     I              REPT                            REPTC
     I              AUTO                            AUTOC
     I              FACO                            FACOC
     I              BRTE                            BRTEC
     I              SPIN                            SPINC
     I              WTIN                            WTINC
     I              FCUS                            FCUSC
     I              FTYP                            FTYPC
     I              FSEQ                            FSEQC
     I              INTC                            INTCC
     I              NACD                            NACDC
     I              FCLB                            FCLBC
     I              ORED                            OREDC
     I              LCD                             LCDC
     I              CHTP                            CHTPC
     I              TNLU                            TNLUC
     I              RSTM                            RSTMC
     I              RONS                            RONSC
     I              RIBN                            RIBNC
     I              RIBA                            RIBAC
     I              ROBN                            ROBNC
     I              ROCN                            ROCNC
     I              PSTM                            PSTMC
     I              PONS                            PONSC
     I              PIBN                            PIBNC
     I              PIBA                            PIBAC
     I              POBN                            POBNC
     I              POCN                            POCNC
     I              RCRN                            RCRNC
     I              RCRA                            RCRAC
     I              RVNO                            RVNOC
     I              AWBN                            AWBNC
     I              AWBA                            AWBAC
     I              BENN                            BENNC
     I              BENA                            BENAC
     I              DTP1                            DTP1C
     I              DTP2                            DTP2C
     I              DTP3                            DTP3C
     I              DTP4                            DTP4C
     I              DCHG                            DCHGC
     I              BTB1                            BTB1C
     I              BTB2                            BTB2C
     I              BTB3                            BTB3C
     I              BTB4                            BTB4C
     I              BTB5                            BTB5C
     I              BTB6                            BTB6C
     I              CVMR                            CVMRC
     I              FSRP                            FSRPC
     I              FSGN                            FSGNC
     I              FPRC                            FPRCC
     I              DFTP                            DFTPC
     I              DFST                            DFSTC
     I              MNSG                            MNSGC
     I              GASS                            GASSC
     I              GPRT                            GPRTC
     I              IUSR                            IUSRC
     I              AUSR                            AUSRC
     I              XUSR                            XUSRC
     I              PCRF                            PCRFC
     I              PCOB                            PCOBC
     I              PDDI                            PDDIC
     I              PTDI                            PTDIC
     I              SCCY                            SCCYC
     I              ICCY                            ICCYC
     I              REPI                            REPIC
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     IDEEXT     E DSDEEXTCTL                                                                  208221
     I              DEPRDNB                         PRDNB                                     208221
     I              DEPDNWD                         PDNWD                                     208221
     I              DEHRDNB                         HRDNB                                     208221
     I              DEHDNWD                         HDNWD                                     208221
     I              DEHACCR                         HACCR                                     208221
     I              DEHACCT                         HACCT                                     208221
     I              DEHFCLT                         HFCLT                                     208221
     I              DEHLOAN                         HLOAN                                     208221
     I              DEHSTRD                         HSTRD                                     208221
     I              DEPACCT                         PACCT                                     208221
     I              DEPCFCO                         PCFCO                                     208221
     I              DEPCUSX                         PCUSX                                     208221
     I              DEPEXTR                         PEXTR                                     208221
     I              DEPFEES                         PFEES                                     208221
     I              DEPFRAS                         PFRAS                                     208221
     I              DEPFUND                         PFUND                                     208221
     I              DEPFXDL                         PFXDL                                     208221
     I              DEPLDCL                         PLDCL                                     208221
     I              DEPLDNI                         PLDNI                                     208221
     I              DEPLOAN                         PLOAN                                     208221
     I              DEPNASP                         PNASP                                     208221
     I              DEPOTCC                         POTCC                                     208221
     I              DEPSBPS                         PSBPS                                     208221
     I              DEPSTRD                         PSTRD                                     208221
     I              DEPSWAP                         PSWAP                                     208221
      ** Extract Control Data                                                                 208221
      *                                                                                       208221
     ISDBANK    E DSSDBANKPD
      ** Bank details
      *
     ISDMMOD    E DSSDMMODPD
      ** MIDAS Module Details
      *
     ISCSARD    E DSSCSARDPD
      ** Switchable Features Details
      *
     IDSFDY     E DSDSFDY
      ** Short date structure
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I/COPY ZSRSRC,ZTNLU1Z1
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Perform initial processing
      *
     C                     EXSR SRINIT
      *
      ** Read live loans file
      *
     C           DEDLFR    IFEQ 'Y'                                                           208221
     C                     READ CLOAN                    99
      *
     C           *IN99     DOWEQ*OFF
     C           FSRPC     IFEQ *ZERO
      *
      ** If Lending Backvaluation feature is On and PCA(Analytical
      ** Accounting module) is Off, do this processing
      *
     C           CLE003    IFEQ 'Y'
     C           BGN0ST    ANDEQ'N'
      *
      ** Set lower limits on LOAMD
      *
     C**********           Z-ADDLNRFC     KLNRF                                               CLE148
     C                     MOVELLNRFC     KLNRF                                               CLE148
     C                     Z-ADDVDATC     KVDAT
     C                     Z-ADD0         KLASN
      *
     C           KLOAMS    SETLLLOAMSD                   99
      *
     C                     READ LOAMSD                   99
     C                     Z-ADD0         WKSEQN  30
      *
      ** Find out the next available sequence number
      *
     C           *IN99     DOWEQ*OFF
      *
     C           RECI      IFEQ 'T'
     C                     LEAVE
     C                     ENDIF
      *
     C           LNRF      IFNE LNRFC
     C           VDAT      ORNE VDATC
     C                     LEAVE
     C                     ENDIF
      *
     C                     Z-ADDLASN      WKSEQN
     C                     READ LOAMSD                   99
      *
     C                     ENDDO
      *
      ** SetUp fields for PF/LOAMSDK
      *
     C                     EXSR SRSETF
      *
      ** Write record to file
      *
     C           FSRP      IFNE *ZEROS                                                        208221
     C                     WRITELOAMSDKF               81
      *
     C           *IN81     IFEQ *ON
     C                     MOVEL'LOAMSDK' DBFILE
     C                     Z-ADD02        DBASE
     C                     MOVEL*BLANK    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Setup trailer fields
      *
     C                     EXSR SRTRAL
      *
      ** Update Trailer file
      *
     C                     UPDATLOAMSZ1F               81
      *
     C           *IN81     IFEQ *ON
     C                     MOVEL'LOAMSDK' DBFILE
     C                     Z-ADD03        DBASE
     C                     MOVEL*BLANK    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF                                                              208221
      *                                                                                       208221
     C                     ELSE
      *
      ** If Lending Backvaluation feature is Off and PCA(Analytical
      ** Accounting module) is Off,
      *
     C           CLE003    IFEQ 'N'
     C           BGN0ST    ANDEQ'N'
      *                                                                                       208221
      ** Default funding rate only if processing type is not 70, 71 or 72                     208221
      *                                                                                       208221
     C           PTYPC     IFNE 70                                                            208221
     C           PTYPC     ANDNE71                                                            208221
     C           PTYPC     ANDNE72                                                            208221
      *                                                                                       208221
     C           INTR      SUB  MARG      WKFSRP
     C           WKFSRP    IFLT *ZERO
     C                     Z-ADD0         WKFSRP
     C                     ENDIF
     C                     Z-ADDWKFSRP    FSRPC
      *                                                                                       208221
      ** Update only if funding rate is not zero                                              208221
      *                                                                                       208221
     C           FSRPC     IFNE *ZERO                                                         208221
     C                     UPDATCLOANCLF
      *
      ** Set lower limits on Lending History file, LEHISSL0
      *
     C                     MOVELBRCAC     KBRCA
     C**********           Z-ADDCNUMC     KCNUM                                               CSD027
     C                     MOVE CNUMC     KCNUM                                               CSD027
     C**********           Z-ADDLNRFC     KLNRF                                               CLE148
     C                     MOVELLNRFC     KLNRF                                               CLE148
      *
     C           KLEHIS    SETLLLEHISSL0                 99
      *
     C                     READ LEHISSL0                 99
      *
     C           *IN99     DOWEQ*OFF
      *
     C           LNRF      IFNE LNRFC
     C           BRCA      ORNE BRCAC
     C           CNUM      ORNE CNUMC
     C                     LEAVE
     C                     ELSE
     C                     Z-ADDWKFSRP    FSRP
     C           RCDT      IFEQ 'A'
     C                     UPDATHISTSHMF
     C                     ENDIF
     C           RCDT      IFEQ 'H'
     C                     UPDATHISTSHNF
     C                     ENDIF
     C           RCDT      IFEQ 'E'
     C                     UPDATHISTSHQF
     C                     ENDIF
     C                     ENDIF
      *
     C                     READ LEHISSL0                 99
      *
     C                     ENDDO
      *
     C                     ENDIF                                                              208221
      *                                                                                       208221
     C                     ENDIF                                                              208221
      *                                                                                       208221
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Read the next record
      *
     C                     READ CLOAN                    99
      *
     C                     ENDDO
      *                                                                                       208221
     C                     ENDIF                                                              208221
      *
      ** Return to the calling program
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRINIT - Initial Processing                                   *
      *                                                               *
      * Called by: Main routine                                       *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
     C           *NAMVAR   DEFN DEEXTCTL  DEEXT                                               208221
     C                     IN   DEEXT                                                         208221
     C           *LIKE     DEFN FSRP      WKFSRP
      *
      ** Setup key list for LOAMD
      *
     C           KLOAMS    KLIST
     C**********           KFLD           KLNRF   60                                          CLE148
     C                     KFLD           KLNRF   6                                           CLE148
     C                     KFLD           KVDAT   50
     C                     KFLD           KLASN   30
      *
      ** Setup key list for LEHISSL0
      *
     C           KLEHIS    KLIST
     C                     KFLD           KBRCA   3
     C**********           KFLD           KCNUM   60                                          CSD027
     C                     KFLD           KCNUM   6                                           CSD027
     C                     KFLD           KLNRF
      *
      ** Retrieve bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE
     C                     Z-ADD01        DBASE
     C                     MOVEL'*FIRST'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Access Module Details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDMMODPD'DBFILE
     C                     Z-ADD04        DBASE
     C                     MOVEL'*FIRST'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Access Switchable feature Lending Backvaluations details
      *
     C                     MOVE 'N'       CLE003  1
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*VERIFY' POPTN
     C                     PARM 'CLE003'  PSARD   6
     C           SCSARD    PARM SCSARD    DSFDY
      *
      ** If return code is not blank, nor '*NRF', database error
      *
     C           PRTCD     IFNE *BLANKS
     C           PRTCD     ANDNE'*NRF'
     C           *LOCK     IN   LDA
     C                     MOVEL'SCSARDPD'DBFILE
     C                     Z-ADD05        DBASE
     C                     MOVEL'*VERIFY' DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       CLE003
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRSETF - SetUp the fields to be added to PF/LOAMSDK           *
      *                                                               *
      * Called by: Main routine                                       *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRSETF    BEGSR
      *
     C                     MOVE 'D'       RECI
     C**********           Z-ADDLNRFC     LNRF                                                CLE148
     C                     MOVELLNRFC     LNRF                                                CLE148
     C                     Z-ADDVDATC     VDAT
     C           WKSEQN    ADD  1         LASN
     C                     Z-ADD2         MRIN
     C                     MOVELLTYPC     LTYP
     C                     MOVELSUTPC     SUTP
     C                     MOVELPTYPC     PTYP
     C                     MOVE 'SC'      AMTP
     C                     MOVELCCYC      CCY
     C                     MOVELBRCAC     BRCA
     C                     MOVELCNUMC     CNUM
     C                     MOVELAUTOC     AUTO
     C                     MOVELFCUSC     FCUS
     C                     MOVELFTYPC     FTYP
     C                     MOVELFSEQC     FSEQ
     C                     MOVELFCLBC     FCLB
      *
      ** Retrieve Run Date for Orig. Entry Date & Last Change Date
      *
     C                     Z-ADDBJRDNB    ORED
     C                     Z-ADDBJRDNB    LCD
      *
     C                     MOVE 'I'       CHTP
      *
     C                     EXSR ZTNLU1
      *
     C                     Z-ADDNATN      TNLU
      *
      ***Default*Funding*Rate*with*the*value*of*Margin*Rate***********                        208221
      ** Default Funding Rate with the difference of interest rate                            208221
      ** and margin rate only if processing type is not 70, 71 or 72                          208221
      *
     C           PTYPC     IFNE 70                                                            208221
     C           PTYPC     ANDNE71                                                            208221
     C           PTYPC     ANDNE72                                                            208221
      *                                                                                       208221
     C********** INTR      SUB  MARG      FSRP                                                208221
     C           INTR      SUB  MARG      WKFSRP                                              208221
     C           WKFSRP    IFLT *ZERO
     C                     Z-ADD0         WKFSRP
     C                     ENDIF
     C                     Z-ADDWKFSRP    FSRP                                                208221
      *                                                                                       208221
     C                     ENDIF                                                              208221
      *                                                                                       208221
     C                     MOVE 'N'       IRCF
     C                     MOVE 'Y'       FRCF
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRTRAL - Setup Trailer Fields                                 *
      *                                                               *
      * Called by: Main routine                                       *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRTRAL    BEGSR
      *
      ** Setup key fields
      *
     C                     MOVE '999999'  KLNRF
     C                     MOVE '99999'   KVDAT
     C                     MOVE '999'     KLASN
      *
      ** Find Trailer record
      *
     C           KLOAMS    CHAINLOAMS                79
      *
     C           *IN79     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'LOAMS   'DBFILE
     C                     Z-ADD2         DBASE
     C                     MOVEL'999999'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           NORE      ADD  1         NORE
     C           NINS      ADD  1         NINS
     C           NLRA      ADD  1         NLRA
      *
     C                     Z-ADDNATN      TNLU
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
     C/COPY ZSRSRC,ZTNLU1Z2
      ********************************************************************
      /EJECT
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
