     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2002')
      *****************************************************************
/*STD *  RPGBASEMOD                                                   *
/*EXI *  TEXT('Midas DE Proj Ext of Futures and Options')             *
      *****************************************************************
      *                                                               *
      *  Midas - Data Export module                                   *
      *                                                               *
      *  DEPXTEXTR - Projected Extract for Futures & Options Exchange *
      *              Traded Instrument Transactions                   *
      *                                                               *
      *  Function:  This module is called by DEPXCEXTR. It outputs    *
      *             records to DEPTRANPD, DEPPOSNPD, DEPEVNTPD        *
      *             and DEPCASHPD files. These are used for           *
      *             reporting within CCRM.                            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CAP208             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 16Sep03               *
      *                 219833             Date 07Aug03               *
      *                 217124             Date 22Apr03               *
      *                 216909             Date 14Apr03               *
      *                 CDE005             Date 20Aug02               *
      *                 215528             Date 21Mar03               *
      *                 CDE003             Date 03Dec02               *
      *                 208221             Date 25Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002  *CREATE    Date 05Dec00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP208 - F&O Market Instrument API (Recompile)               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements for Phase 1 Priority 1A        *
      *           (recompiled).                                       *
      *  219833 - Don't produce rcds if backvalued trans (recompile). *
      *  217124 - For close out transactions where maturity date is   *
      *           greater than the rundate, i.e. on a holiday, set    *
      *           transaction deal date to be the same as the         *
      *           rundate.                                            *
      *  216909 - Recompiled due to change in DEPPOSNPD format.       *
      *  CDE005 - Data Export - Reservation ID                        *
      *  215528 - System is associating a wrong premium amount.       *
      *  CDE003 - Data Export - MCR Limits Phase II (Recompiled)      *
      *  208221 - For fee events should clear events array before     *
      *           setting up any event data                           *
      *           Enable DEWRKEDTA to get info from data area         *
      *           On take on date, do not flag any events as processed*
      *  CDE002 - Data Export - CCRM Revenue Analysis.                *
      *                                                               *
      *****************************************************************
 
     FDEPTRANPD O    E           K DISK
     FDEPPOSNPD O    E           K DISK
     FDEPEVNTPD O    E           K DISK
     FDEPCASHPD O    E           K DISK
 
      ** +--------------------------------------+
      ** ¦ Automatically included D-specs       ¦
      ** ¦ ==============================       ¦
      ** +--------------------------------------+
      **
      ** Standard D-specs
      ** ================
      **
      ** The following /COPY line includes the LDA layout,
      ** the copyright array definition,
      ** and the following named constants:
      **    True       logical = *on (for indicator processing)
      **    False      logical = *off (for indicator processing)
      **    DBErrCtl   10A     = 'DBERRCTL' (the name of the database error
      **                                    handler)
      ** and the following variables:
      **    RunBefore  1A (for the PSSR)
 
     D/COPY ZACPYSRC,STD_D_SPEC
 
      * E X T R A C T   F I L E S
     D/COPY DECPYSRC,DEXTFILS
      * P E R I O D   D E T A I L S
     D/COPY DECPYSRC,DEXTPERDD
 
      *
      **  Instrument types
     D TABCDE          S              1S 0 DIM(8) CTDATA PERRCD(1)
     D TABTYP          S             16    DIM(8) ALT(TABCDE)
      *
      *
     D TRANS         E DS                  EXTNAME(TRANSD) PREFIX(TR_)
     D TRSET         E DS                  EXTNAME(TRSETD) PREFIX(TS_)
     D INTYP         E DS                  EXTNAME(INTYPD) PREFIX(IN_)
     D PRICS         E DS                  EXTNAME(PRICSD) PREFIX(PC_)
     D MARKT         E DS                  EXTNAME(MARKTD) PREFIX(MA_)
 
 
     D W#TABT          DS
     D  W#TDES                 1     16
 
 
     D W#SETD          DS
     D  W#SETM                 1      2
     D***W#SETA*                3     14                                                      CGL029
     D***W#SETB*               15     17                                                      CGL029
     D***W#SETC*               18     20                                                      CGL029
     D  W#SETA                 3     20                                                       CGL029
     D  W#SETB                21     23                                                       CGL029
     D  W#SETC                24     26                                                       CGL029
 
 
      ** Instrument type
     D FFISPT          S              1S 0
      ** Contract type
     D FFCNTT          S              1S 0
 
      ** Transaction number work date
     D WK_TNBRn        S              6S 0
     D WK_TNBRa        S              6A
 
     D BaseCDP         S              1  0
     D W#IO            S              1
     D MktVal          S                   LIKE(FFPREM)
     D W#AMT           S                   LIKE(P#NOML)
     D W#ASLI          S                   LIKE(P#ASLI)
 
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
      ** External DS for BANK details
     D SDGELR        E DS                  EXTNAME(SDGELRPD)
      ** External DS for GENERAL LEDGER details
     D SDCURR        E DS                  EXTNAME(SDCURRPD)
      ** External DS for CURRENCY details
     D SDFODA        E DS                  EXTNAME(SDFODAPD)
     D   ##ACCD      E                     EXTFLD(QQACCD)                                     CGL029
      ** External DS for FUTURES AND OPTIONS ICD details
     D SCSARD        E DS                  EXTNAME(SCSARDPD) PREFIX(SF_)                      CDE005
      ** External DS for Switchable Features details                                          CDE005
     D DSFDY         E DS                  EXTNAME(DSFDY)
     D DSSDY         E DS                  EXTNAME(DSSDY)
 
     D DEEXTCTL      E DS                  EXTNAME(DEEXTCTL)                                  208221
      ** Extract Control Data                                                                 208221
                                                                                              208221
      *
      * INPUT PARAMETERS
      *
     C     *ENTRY        PLIST
     C                   PARM                    I#RTCD            7            * RETURN CODE
     C                   PARM                    I#ERMS           30            * ERROR MESSAGE
      * INPUTS
     C                   PARM                    I#ACTN            1            * ACTION CODE
     C                   PARM                    I#EXTT            4            * EXTRACT TYPE
     C                   PARM                    I#PH              1            * PROJ/HIST
     C                   PARM                    I#EOB             1            * END OF BOOK
     C                   PARM                    I#RDNB            5 0          * RUN DATE
     C                   PARM                    I#DNWD            5 0          * DATE OF NXT W DAY
     C                   PARM                    I#HCOD            5 0          * HIST CUT-OFF DATE
     C                   PARM                    I#EXTD            5 0          * EXTRACT DATE
     C                   PARM                    I#EVCD            5 0          * EVENT CTL DATE
     C                   PARM                    I#PCOD            5 0          * PROJECTION DATE
     C                   PARM                    PeriodEDT                      * PERIOD END DATES
     C                   PARM                    TRANS
     C                   PARM                    TRSET
     C                   PARM                    INTYP
     C                   PARM                    PRICS
     C                   PARM                    MARKT
     C                   PARM                    Dataarea                                     208221
     C                   PARM                    I#RSRV           10                          CDE005
      *
      * INITIAL
      *
     C                   MOVEL     Dataarea      DEEXTCTL                                     208221
     C                   EXSR      INIT
      *
      ** MAIN
      *
     C                   IF        I#ACTN <> 'D'
     C                   EVAL      W#IO = 'I'
     C                   EXSR      MAIN
      *
     C                   IF        IN_ISPT = 2 or IN_ISPT = 5
     C                   EVAL      W#IO = 'O'
     C                   EXSR      MAIN
     C                   ENDIF
      *
     C                   ENDIF
      *
      * COMMITMENT PROCESSING FOR F&O EXCHANGE TRADED INSTRUMENT
      *
     C                   EXSR      COMIT
      *
      * RETURN
      *
     C                   RETURN
      *
      *****************************************************************
      * INITIAL PROCESSING FOR FUTURES AND OPTIONS OPTION
      *****************************************************************
     C     INIT          BEGSR
      *
      * Clear Transaction Details
     C                   CLEAR                   T#TRAN
      *
      * Clear Cash Details
     C                   CLEAR                   C#CASH
      *
      * Module
     C                   MOVEL     'FF'          T#MOD
      *
      * Transaction Reference (replace sign with 'M' if negative)
     C                   IF        TR_TNBR < 0
     C                   EVAL      WK_TNBRn = TR_TNBR * -1
     C                   MOVE      WK_TNBRn      WK_TNBRa
     C                   EVAL      T#TREF = WK_TNBRa + 'M'
     C                   ELSE
     C                   MOVEL     TR_TNBR       T#TREF
     C                   ENDIF
      *
      * Transaction Type
     C                   MOVEL     TR_TRTY       T#TRTP
      *
      * Transaction Sub-Type
     C                   MOVEL     TR_PCAL       T#TRST
      *
      * Transaction Description
     C     IN_ISPT       LOOKUP    TABCDE        TABTYP                   99    *
     C                   MOVEL     TABTYP        W#TABT
     C                   MOVEL     W#TDES        T#TDES
      *
      * Transaction deal date
     C                   Z-ADD     TR_TRSD       T#DDAT
      *                                                                                       217124
      ** If Transaction date is greater than rundate, then set transaction date               217124
      ** to be the same as the rundate to avoid error in DEWRKEDTA                            217124
      *                                                                                       217124
     C                   IF        T#DDAT > BJRDNB                                            217124
     C                   EVAL      T#DDAT = BJRDNB                                            217124
     C                   ENDIF                                                                217124
      *
      * Value Date
     C                   Z-ADD     TR_TRSD       T#VDAT
      *
      * Maturity Date
     C                   CALLB     'FFDATE'
     C                   PARM                    ReturnCode
     C     T#MDAT        PARM      0             @FFDAY            5 0
     C                   PARM      IN_FTDF       FFDATC            7
     C                   PARM      TR_MTHN       FFMTH             2 0
     C                   PARM      TR_YRNO       FFYR              2 0
     C                   PARM      MA_MKLC       FFCCY1            3
     C                   PARM      MA_MLOC       FFLOC             3
     C                   PARM      IN_ISCY       FFCCY2            3
     C                   PARM      IN_OTHC       FFCCY3            3
     C                   PARM                    BJDFIN            1
      *
      * If Maturity Date less than Transaction Date, set to Transaction Date
     C                   IF        T#MDAT < TR_TRSD
     C                   EVAL      T#MDAT = TR_TRSD
     C                   ENDIF
      *
      * Booking Branch
     C                   MOVEL     TR_BRCA       T#BRCA
      *
      * Book
     C                   MOVEL     TR_BOKC       T#BOOK
      *
      * Counterparty
     C                   MOVEL     TR_CUSC       T#CPTY
      *
      * Risk Customer
     C                   MOVEL     TR_CUSC       T#RCST
      *
      * Market value
      *
      * If the New Price is 0, set Market Value to 0
     C                   IF        PC_NEWP = 0
     C                   EVAL      T#MKVL = *Zero
 
     C                   ELSE
 
      * Otherwise, calculate premium payable based on New Price
     C                   EVAL      FFPRIC = PC_NEWP
     C                   EXSR      FFPMCL
 
     C                   EVAL      MktVal = FFPREM
 
      * calculate premium payable based on Contract Price
     C                   EVAL      FFPRIC = TR_COPR
     C                   EXSR      FFPMCL
 
     C                   SELECT
      * Set Market Value according to Transaction Type
     C                   WHEN      TR_TRTY = 'P'
     C                   EVAL      T#MKVL = MktVal - FFPREM
 
     C                   WHEN      TR_TRTY = 'S'
     C                   EVAL      T#MKVL = FFPREM - MktVal
 
     C                   WHEN      TR_TRTY = 'E'
     C                   EVAL      T#MKVL = *Zero
 
     C                   ENDSL
 
     C                   ENDIF
 
      * Transaction valuation currency
     C                   EVAL      T#VCCY = IN_ISCY
 
      * Instrument currency decimal places
     C                   EVAL      @CYCD = IN_ISCY
     C                   EXSR      ACSCCY
     C                   EVAL      T#VCDP = A6NBDP
 
      * Action code
     C                   EVAL      T#ACTN = I#ACTN
      *                                                                                       CDE005
      * Reservation ID                                                                        CDE005
      *                                                                                       CDE005
     C     CDE005        IFEQ      'Y'                                                        CDE005
     C                   MOVEL     I#RSRV        T#RSRV           10                          CDE005
     C                   ENDIF                                                                CDE005
 
      * Import transaction details
     C     I#ACTN        IFNE      'D'
     C                   MOVEL     '*IMPTRAN'    W#MODE
     C                   EXSR      WRKEDTA
     C                   ENDIF
      *
      **  Store settlement method, account & branch
     C                   MOVE      TS_CSLT       W#SETM                         * pay method
     C                   EVAL      W#SETA = TS_CSLA                             * pay account
     C                   EVAL      W#SETB = TS_BRSC                             * pay branch
     C                   EVAL      W#SETC = IN_ISCY
      *
     C                   ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * IMPORT OPENING POSITION AND EVENTS
      *****************************************************************
     C     IMPORT        BEGSR
      *
      ** SET UP WORK FIELDS WITH INSTRUMENT OR OTHER CURRENCY DETAILS
      *
     C     W#IO          CASEQ     'I'           INSCCY
     C     W#IO          CASEQ     'O'           OTHCCY
     C                   ENDCS
      *
      * IMPORT OPENING BALANCE
      *
     C                   EXSR      IMP_OPBL
      *
      * IMPORT FEE (MARGIN) PAYMENT EVENT
      *
     C     W#IO          IFEQ      'I'
     C                   EXSR      IMP_FE
     C                   ENDIF
 
      * IMPORT MATURITY DATE EVENT
 
     C                   EXSR      IMP_MT
 
      * IMPORT PERIOD END DATES
 
     C                   EXSR      IMP_PE
 
     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * IMPORT OPENING BALANCE
      *****************************************************************
     C     IMP_OPBL      BEGSR
 
      * Clear Position
 
     C                   CLEAR                   P#POSN
 
      * Module, Transaction Reference
 
     C                   MOVEL     T#TKEY        P#TKEY
 
      * Asset/Liability
     C                   MOVEL     W#ASLI        P#ASLI
 
      * Interest Accrl Ctl Date
     C                   Z-ADD     TR_TRSD       P#IACD                         *
 
      * Nominal/Principal
     C                   Z-ADD     W#AMT         P#NOML                         *
     C     I#PH          IFEQ      'P'
     C     I#EOB         ANDEQ     'N'
     C                   Z-ADD     P#NOML        P#DIFN
     C                   ENDIF
 
      * Currency
     C                   MOVEL     W#CCY         P#CCY
 
      * Nominal Ccy Dec Places
     C                   MOVEL     A6NBDP        P#NCDP
 
      * Nominal Dec Places
     C                   MOVEL     A6NBDP        P#NMDP
 
      * Nominal Ccy Spot Days
     C                   Z-ADD     A6SPDY        P#SPDY
 
      * Stop Accruals
     C                   MOVEL     'Y'           P#STAC
 
      * Import opening balance
     C                   MOVEL     '*IMPOPBL'    W#MODE
     C                   EXSR      WRKEDTA
 
     C
     C                   ENDSR
      **************************************************************************
     C/SPACE 5
      *****************************************************************
      * IMPORT FEE PAYMENT DATE EVENT
      *****************************************************************
     C     IMP_FE        BEGSR
                                                                                              208221
      * Clear Event                                                                           208221
     C                   CLEAR                   E#EVNT                                       208221
      * Commission and Charges
     C     TS_CCMA       SUB       TS_BCMA       E#COMM
     C     BXCCC         IFEQ      'Y'
     C     TS_CCHA       SUB       TS_BCHA       E#CHRG
     C                   ELSE
     C                   Z-ADD     TS_CCHA       E#CHRG
     C                   ENDIF
      * If there is a Charge, Commission or Premium Paid, process event
     C     E#CHRG        IFNE      0
     C     E#COMM        ORNE      0
     C     TS_PRMP       ORNE      0
      ************                                                                            208221
      **Clear*Event***************************************************************************208221
     C************       CLEAR                   E#EVNT                                       208221
      *
      * Module, Transaction Reference, Asset/Liability
      *
     C                   MOVEL     P#TKEY        E#TKEY
      *
      * Processing Date
     C                   Z-ADD     TR_TRSD       E#PRDT
      *
      * Event Type
     C                   MOVEL     'FE'          E#EVTP
      *
      * Fees = premium paid
      *
     C                   Z-ADD     TS_PRMP       E#FEE
      *
      * Settlement currency decimal places
      *
     C                   MOVEL     T#VCDP        E#NCDP
      *
      ** Set up settlement details
      *
     C                   MOVEL     W#SETD        E#SETD                         * Rec details
      *
      * Processed?
      *
     C     TR_ECPI       IFEQ      'Y'
     C                   MOVE      'Y'           E#PRCD
     C     I#EOB         IFEQ      'N'                                                        208221
     C     DETKON        ANDEQ     I#RDNB                                                     208221
     C                   MOVE      *BLANKS       E#PRCD                                       208221
     C                   END                                                                  208221
     C                   ENDIF
      *
      ** Incoming/outgoing
      *
     C     TR_TRTY       IFEQ      'P'
     C                   MOVEL     'O'           E#IO
     C                   ELSE
     C                   MOVEL     'I'           E#IO
     C                   ENDIF
      *
      * Import fee payment date event
      *
     C                   MOVEL     '*IMPEVNT'    W#MODE
     C                   EXSR      WRKEDTA
      *
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
      * IMPORT MATURITY DATE EVENT
      *****************************************************************
     C     IMP_MT        BEGSR
      *
      * CLEAR EVENT
     C                   CLEAR                   E#EVNT
      *
      * Module, Transaction Reference, Asset/Liability
      *
     C                   MOVEL     P#TKEY        E#TKEY
      *
      * Processing Date
     C                   Z-ADD     T#MDAT        E#PRDT
      *
      * Event Type
     C                   MOVEL     'MT'          E#EVTP
      *
      * Suppress Settlement Y
      *
     C                   MOVEL     'Y'           E#SSET
      *
      ** Import maturity date event
      *
     C                   MOVEL     '*IMPEVNT'    W#MODE
     C                   EXSR      WRKEDTA
      *
     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * SET UP INSTRUMENT CURRENCY DETAILS
      ********************************************************************
     C     INSCCY        BEGSR
 
      * Instrument Currency
     C                   MOVE      IN_ISCY       W#CCY             3
 
      * Instrument Type 2 or 5
     C     IN_ISPT       IFEQ      2
     C     IN_ISPT       OREQ      5
 
      * Instrument Type 2
     C     IN_ISPT       IFEQ      2
 
      * Work Amount = Contingent Amount * Number of Open Contracts
     C     IN_CTAM       MULT      TR_NOCO       W#AMT
 
      * Transaction Type P
     C     TR_TRTY       IFEQ      'P'
      * Liability
     C                   MOVE      'L'           W#ASLI
     C                   ELSE
      * Asset
     C                   MOVE      'A'           W#ASLI
     C                   ENDIF
 
      * Instrument Type 5
     C                   ELSE
 
      * Work Amount = Contingent Amount * Strike Price
     C     IN_CTAM       MULT      TR_STRP       W#AMT
 
      * Transaction Type P or S
     C     TR_TRTY       IFEQ      'P'
     C     TR_TRTY       OREQ      'S'
 
      * Put Call C
     C     TR_PCAL       IFEQ      'C'
     C                   MOVE      'L'           W#ASLI
     C                   ELSE
     C                   MOVE      'A'           W#ASLI
     C                   ENDIF
      * Transaction Type not P or S
     C                   ELSE
 
     C     TR_PCAL       IFEQ      'P'
     C                   MOVE      'L'           W#ASLI
     C                   ELSE
     C                   MOVE      'A'           W#ASLI
     C                   ENDIF
 
     C                   ENDIF
 
     C                   ENDIF
      * Instrument Type not 2 or 5
     C                   ELSE
      * Work Amount = Contingency Amount
     C                   Z-ADD     IN_CTAM       W#AMT
      * Instrument Type 1 3 or 7
     C     IN_ISPT       IFEQ      1
     C     IN_ISPT       OREQ      3
     C     IN_ISPT       OREQ      7
      * Transaction Type P
     C     TR_TRTY       IFEQ      'P'
     C                   MOVE      'A'           W#ASLI
     C                   ELSE
     C                   MOVE      'L'           W#ASLI
     C                   ENDIF
      * Instrument Type not 1 3 or 7
     C                   ELSE
      * Transaction Type P or S
     C     TR_TRTY       IFEQ      'P'
     C     TR_TRTY       OREQ      'S'
      * Put Call C
     C     TR_PCAL       IFEQ      'C'
     C                   MOVE      'A'           W#ASLI
     C                   ELSE
     C                   MOVE      'L'           W#ASLI
     C                   ENDIF
      * Transaction Type not P or S
     C                   ELSE
      * Put Call P
     C     TR_PCAL       IFEQ      'P'
     C                   MOVE      'A'           W#ASLI
     C                   ELSE
     C                   MOVE      'L'           W#ASLI
     C                   ENDIF
 
     C                   ENDIF
 
     C                   ENDIF
 
     C                   ENDIF
 
      * Get Currency Details
     C                   MOVEL     W#CCY         @CYCD
     C                   EXSR      ACSCCY
 
     C                   ENDSR
      **************************************************************************
      /SPACE 5
      *****************************************************************
      * SET UP OTHER CURRENCY DETAILS
      ********************************************************************
     C     OTHCCY        BEGSR
 
      * Other Currency
     C                   MOVE      IN_OTHC       W#CCY
     C                   Z-ADD     IN_CTAM       W#AMT
 
      * Instrument Type 2
     C     IN_ISPT       IFEQ      2
 
      * Transaction Type P
     C     TR_TRTY       IFEQ      'P'
     C                   MOVE      'A'           W#ASLI
     C                   ELSE
     C                   MOVE      'L'           W#ASLI
     C                   ENDIF
 
      * Instrument Type not 2
     C                   ELSE
 
      * Transaction Type P or S
     C     TR_TRTY       IFEQ      'P'
     C     TR_TRTY       OREQ      'S'
 
      * Put Call C
     C     TR_PCAL       IFEQ      'C'
     C                   MOVE      'A'           W#ASLI
     C                   ELSE
     C                   MOVE      'L'           W#ASLI
     C                   ENDIF
 
      * Transaction Type P or S
     C                   ELSE
 
      * Put Call P
     C     TR_PCAL       IFEQ      'P'
     C                   MOVE      'A'           W#ASLI
     C                   ELSE
     C                   MOVE      'L'           W#ASLI
     C                   ENDIF
 
     C                   ENDIF
 
     C                   ENDIF
 
      * Get Currency Details
     C                   MOVEL     W#CCY         @CYCD
     C                   EXSR      ACSCCY
 
     C                   ENDSR
      **************************************************************************
      /SPACE 5
      ********************************************************************
      * PREMIUM CALCULATION
      ********************************************************************
     C     FFPMCL        BEGSR
 
     C                   CALLB     'FFPMCL'
     C                   PARM      *ZERO         FFPREM           13 0
     C                   PARM      TR_NOCO       FFNOC             5 0
     C                   PARM                    FFPRIC           15 8
     C                   PARM      IN_TKDM       FFTKDM            3 0
     C                   PARM      IN_MNPF       FFMNPF           15 8
     C                   PARM      IN_TKVL       FFTKVL           14 5
     C                   PARM      IN_ISPT       FFISPT
     C                   PARM      IN_CNTT       FFCNTT
     C                   PARM      IN_CTAM       FFCTAM           13 0
     C                   PARM      TR_STRP       FFSTRP           15 8                        215528
 
     C                   ENDSR
      **************************************************************************
      /SPACE 5
      ********************************************************************
      * ACCESS CURRENCY DETAILS
      ********************************************************************
     C     ACSCCY        BEGSR
      *
     C                   CALLB     'AOCURRR0'
     C                   PARM      '*DBERR  '    @RTCD             7
     C                   PARM      '*KEY    '    @OPTN             7
     C                   PARM                    @CYCD             3
     C     SDCURR        PARM      SDCURR        DSSDY
      *
     C                   ENDSR
      ********************************************************************
      /SPACE 5
      ********************************************************************
      * *INZSR
      ********************************************************************
     C     *INZSR        BEGSR
      *
      **  Access Bank Details
      *
     C                   CALLB     'AOBANKR0'
     C                   PARM      '*DBERR  '    @RTCD             7
     C                   PARM      '*FIRST  '    @OPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
 
     C                   If        @RTCD <> *Blanks
     C                   Eval      I#RTCD = '*ERROR'
     C                   Eval      I#ERMS = 'AOBANKR0'
     C                   Eval      *INLR = *ON
     C                   Return
     C                   Endif
      *
      **  Access General Ledger details
      *
     C**********         CALLB     'AOGELRR0'                                                 CGL029
     C                   CALLB     'AOGELRR1'                                                 CGL029
     C                   PARM      '*DBERR  '    @RTCD             7
     C                   PARM      '*FIRST  '    @OPTN             7
     C*****SDGELR        PARM      SDGELR        DSFDY                                        CGL029
     C     SDGELR        PARM      SDGELR        DSSDY                                        CGL029
      *
     C                   If        @RTCD <> *Blanks
     C                   Eval      I#RTCD = '*ERROR'
     C                   Eval      I#ERMS = 'AOGELRR0'
     C                   Eval      *INLR = *ON
     C                   Return
     C                   Endif
 
      **  Access Base Currency Decimal Places
      *
     C                   MOVEL     BJCYCD        @CYCD
 
     C                   EXSR      ACSCCY
 
     C                   If        @RTCD <> *Blanks
     C                   Eval      I#RTCD = '*ERROR'
     C                   Eval      I#ERMS = 'AOCURRR0, Base Currency:' +
     C                             @CYCD
     C                   Eval      *INLR = *ON
     C                   Return
     C                   Endif
 
     C                   MOVEL     A6NBDP        BaseCDP
      *
      **  Access Futures and Options ICD data
      *
      *
     C                   CALL      'AOFODAR0'
     C                   PARM      '*DBERR  '    @RTCD
     C                   PARM      '*FIRST  '    @OPTN
     C     SDFODA        PARM      SDFODA        DSFDY
      *
     C                   If        @RTCD <> *Blanks
     C                   Eval      I#RTCD = '*ERROR'
     C                   Eval      I#ERMS = 'AOFODAR0'
     C                   Eval      *INLR = *ON
     C                   Return
     C                   Endif
      *                                                                                       208221
      **  Set up Type for DEWRKEDTA                                                           208221
      *                                                                                       208221
     C                   EVAL      W#TYPE = 'PEXTR'                                           208221
 
      ** Determine if Data Export - Reservation ID is active                                  CDE005
     C                   CALL      'AOSARDR0'                                                 CDE005
     C                   PARM      *BLANKS       @RTCD             7                          CDE005
     C                   PARM      '*VERIFY'     @OPTN             7                          CDE005
     C                   PARM      'CDE005'      @SARD             6                          CDE005
     C     SCSARD        PARM      SCSARD        DSFDY                                        CDE005
      *                                                                                       CDE005
     C     @RTCD         IFEQ      *BLANK                                                     CDE005
     C                   MOVEL     'Y'           CDE005            1                          CDE005
     C                   ELSE                                                                 CDE005
     C                   MOVEL     'N'           CDE005                                       CDE005
     C                   ENDIF                                                                CDE005
      *                                                                                       CDE005
     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * M A I N   P R O C E S S I N G
      /COPY DECPYSRC,DEXTMAINO
      *****************************************************************
      * C O M M I T M E N T   P R O C E S S I N G
      /COPY DECPYSRC,DEXTCOMMO
      *********************************************************************
      * I M P O R T   P E R I O D   E N D   D A T E S
      /COPY DECPYSRC,DEXTPERDI
      ********************************************************************
      * W O R K   W I T H   E X T R A C T   D A T A
      *****************************************************************
      /COPY DECPYSRC,DEXTWEDTA
      *********************************************************************
      * * P S S R  --- A B N O R M A L   E R R O R   C O N D I T I O N S
      /COPY DECPYSRC,DEPSSR
      *********************************************************************
      /SPACE 5
**  CPY@
(c) Finastra International Limited 2001
** TABSEQ/TABTYP. DEAL TYPES
1INTR RATE FUTURE
2EXCH RATE FUTURE
3COMMODITY FUTURE
4INTR RATE OPTION
5EXCH RATE OPTION
6COMMODITY OPTION
7INTR RATE FUTURE
8INTR RATE OPTION
