      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Accounting Rules Generated Entries Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      ***PRTF/GL000584P1*-*Midas*OF*Accounting*Rules*Generated*********                     MD029739
      *  PRTF/DI000584P1 - Midas OF Accounting Rules Generated        *                     MD029739
      *                    Entries Report                             *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD029739           Date 15Aug14               *
      *                 MD024772           Date 03Feb14               *
      *                 MD024513           Date 23Jan14               *
      *                 MD023743           Date 29Nov13               *
      *                 CGL135A  *CREATE   Date 10Jul13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD029739 - File renaming for integration layer.              *
      *  MD024772 - Problems with Accounting Interface postings and   *
      *             enquiries.                                        *
      *  MD024513 - Add Description field (30A) to the Accounting     *
      *             Rules.                                            *
      *  MD023743 - Matching Rules Check Changes to include DR/CR ind *
      *             Amend Posting and Value date fields               *
      *  CGL135A - Accounting Rules Process                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                     1
     A                                      'Reference GL000584P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    40
     A                                   +26
     A                                      'Date'
     A            BJMRDT    R    7   O    +1
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
      *----------------------*
      ***  Report Header     *
      *----------------------*
      *
     A          R HEADER2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    51
     A                                      'Accounting Rules Process Report'
     A                                    51
     A                                      '-------------------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A                                     1
     A                                      'Batch ID'
     A            PEBTID        60A  O    +1
     A                                    +5
     A                                      'Status'
     A            PESTAT        45A  O    +3
     A                                     1
     A                                      'Booking Branch'
     A                                      SPACEB(001)
     A            PEBKBR         3A  O    +1
     A                                    +1
     A                                      'Deparment'
     A            PEDPCD         3A  O    +1
     A                                    +2
     A                                      'Currency'
     A            PECCY          3A  O    +1
     A                                    +1
     A                                      'Event Amount'
     A            PEEVTA        19A  O    61
     A                                    +1
     A                                      'DR/CR'
     A            PEDCIP         1A  O    +1
     A                                    +2
     A                                      'Posting Date'
     A            PEPSTD         6S 0O    +1
     A                                      EDTWRD('  /  /  ')
     A                                    +3
     A                                      'Event Date'
     A            PEEVTD         6S 0O    +1
     A                                      EDTWRD('  /  /  ')
     A                                     2
     A                                      'Narrative'
     A                                      SPACEB(001)
     A            PENARR        30A  O    +1
     A            PENAR2        65A  O    12
     A                                      SPACEB(001)
     A            PENAR3        65A  O    12
     A                                      SPACEB(001)
     A            PENAR4        65A  O    12
     A                                      SPACEB(001)
     A            PENAR5        65A  O    12
     A                                      SPACEB(001)
     A            PENAR6        65A  O    12
     A                                      SPACEB(001)
     A            PENAR7        65A  O    12
     A                                      SPACEB(001)
     A                                     1
     A                                      'Sett A/c DR'
     A                                      SPACEB(001)
     A            PESADR        24A  O    +1
     A                                    40
     A                                      'Sett A/c CR'
     A            PESACR        24A  O    +1
     A                                     1
     A                                      'Customers:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      'Counterparty'
     A            PECTPY        10A  O    +1
     A                                    +3
     A                                      'Depot'
     A            PEDPCN        10A  O    +1
     A                                    +3
     A                                      'Issuing'
     A            PEISCN        10A  O    +1
     A                                    +3
     A                                      'Associated'
     A            PEASCN        10A  O    +1
     A                                     1
     A                                      'MatchField1'
     A                                      SPACEB(001)
     A            PESMT1        25A  O    +1
     A                                    +2
     A                                      'MatchField2'
     A            PESMT2        25A  O    +1
     A                                    +2
     A                                      'MatchField3'
     A            PESMT3        25A  O    +1
     A                                     1
     A                                      'MatchField4'
     A                                      SPACEB(001)
     A            PESMT4        25A  O    +1
     A                                    +2
     A                                      'MatchField5'
     A            PESMT5        25A  O    +1
     A                                    +2
     A                                      'MatchField6'
     A            PESMT6        25A  O    +1
     A                                     1
     A                                      'MatchField7'
     A                                      SPACEB(001)
     A            PESMT7        25A  O    +1
     A                                    +2
     A                                      'MatchField8'
     A            PESMT8        25A  O    +1
     A                                    +2
     A                                      'MatchField9'
     A            PESMT9        25A  O    +1
     A                                     1
     A                                      'MatchField10'
     A                                      SPACEB(001)
     A            PESMT10       25A  O    +1
     A                                    +1
     A                                      'DR/CR'
     A            PEDCRD         1A  O    +1
     A            PEPFCT         4A  O    15
     A                                      SPACEB(001)
     A                                     1
     A                                      'Profit Centre'
     A                                    +9
     A                                      'SWIFT Ref'
     A            PESWFR        16A  O    +1
     A                                    +3
     A                                      'Cheque No'
     A            PECHQN         8A  O    +1
     A                                    +3
     A                                      'Book Code'
     A            PEBKCD         2A  O    +1
     A                                      SPACEA(001)
     A  80                                 1
     A                                      'Customer Source A/c Id'
     A  80        PECSAI        25A  O    +1
     A                                    +3
     A                                      'Source Customer'
     A            PESRCN         6A  O    +1
      *
      ** Start of change MD024513
     A                                     1
     A                                      'Unique ID Indicator'
     A                                      SPACEB(001)
     A            PEUNID        32A  O    +1
     A                                    +3
     A                                      'Description'
      *
      ** Start  change MD024772
     A**********  PEDESC        30A  O    +2
     A            PEDESC        30A  O    +1
      ** End of change MD024772
      *
      ** End of change MD024513
      *
      *
      ** Start  change MD024772
     A                                     1
     A                                      'Customer Account from Interface'
     A                                      SPACEB(001)
     A            PICUSA        24A  O    +1
     A                                    +3
     A                                      'Transaction Sub Type from Interfac-
     A                                      e'
     A            PITSTY        10A  O    +1
      ** End of change MD024772
      *
     A                                      SPACEA(001)
     A  80                                 1
     A                                      'Operation Type'
     A  80        PEOPTP        20A  O    +1
     A  80                                +3
     A                                      'Forecast Ref'
     A  80        PEFCRF        30A  O    +1
     A                                    +3
     A                                      'Reversal Ind'
     A            PERVID         1A  O    +1
      *
      *-----------------*
      ***  Debit/Credit *
      *-----------------*
      *
     A          R DBCR01
     A                                      SPACEB(001)
     A            PETTLE         6A  O     1
     A                                     8
     A                                      '- Retail Transaction Type:'
     A            PETTDR         5A  O    35
      *
      *-----------------*
      ***  Detail 01    *
      *-----------------*
      *
     A          R DET01
     A                                      SPACEB(001)
     A            PETITL        13A  O     1
     A                                     1
     A                                      'Definition'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'Branch/Customer/Currency/Account C-
     A                                      ode/Account Sequence Narrative Code-
     A                                      :'
     A            PENRCD         2A  O    +1
     A                                    +1
     A                                      'Received Narrative'
     A            PERCNR         1A  O    +1
     A                                    +1
     A                                      'Posting Reference'
     A            PEPSRF         1A  O    +1
     A            PEBRCH         3A  O    14
     A                                      SPACEB(001)
     A            PECUST         6S 0O    +4
     A                                      EDTCDE(L)
     A            PECURR         3A  O    +3
     A            PEACOD        10S 0O    +5
     A                                      EDTCDE(L)
     A            PESEQN         2S 0O    +8
     A                                      EDTCDE(L)
     A            PEAMNT        19A  O    +7
     A            PEDRCR         2A  O    88
     A            PETEXT        30A  O    +1
     A                                    13
     A*** Begin MD023743
     A**************************************'Posting date'
     A                                      'Posting date:'
     A*** End   MD023743
     A                                      SPACEB(001)
     A*** Begin MD023743
     A************PEPSDT         7A  O    26
     A            PEPSDT         6A  O    27
     A*** End   MD023743
     A                                    37
     A                                      'Value date:'
     A*** Begin MD023743
     A************PEVLDT         7A  O    49
     A            PEVLDT         6A  O    49
     A*** End   MD023743
     A                                    57
     A                                      'Transaction type:'
     A            PETRAN         6A  O    75
     A                                    85
     A                                      'Transaction reference'
     A            PERFUP         3A  O   107
     A                                   111
     A                                      '/'
     A            PERFDW         3A  O   113
     A                                    13
     A                                      'Extended narratives:'
     A                                      SPACEB(001)
     A            PENAR1        95A  O    +1
      *
      *-----------------*
      ***  Detail 02    *
      *-----------------*
      *
     A          R DET02
     A                                      SPACEB(001)
     A            PENRR2        95A  O    34
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
      *
