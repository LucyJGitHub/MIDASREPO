/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas DL Update for FX netting deals')                */
/*********************************************************************/
/*                                                                   */
/*  Midas - Dealing Module                                           */
/*                                                                   */
/*  DLC0607NT - EM Update for FX Netting Deals Component             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4 --------------- Base -----------------------------*/
/* Midas DBA 3.00 ---------------- Base -----------------------------*/
/*       Last AMEND NO. CDL002            DATE  17APR97              */
/*       PREV AMEND NO. xxxxxx            DATE  xxxxxxx              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       CDL002 -  Created for CDL002 FX Netting                     */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&STAT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&PRE)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2001')
 
             SNDPGMMSG  MSG('DLC0607NT - EM Update for Netting +
                          Deals') TOMSGQ(MRUNQ)
 
/* Get system prefix */
 
             RTVDTAARA  DTAARA(SDSTAT) RTNVAR(&SDSTAT)
 
             CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
 
             CHGVAR     VAR(&TOLIB) VALUE(&PRE *CAT 'DPLIB')
 
             CHGJOB     SWS(00000000)
 
/* Get restart status */
 
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
 
/* Not a Restart? */
 
             IF         COND(&STAT *NE 'Y') THEN(DO)
 
/* Take security copy */
 
                CPYF       FROMFILE(EMXT2T1) TOFILE(&TOLIB/XEMXT2T1) +
                             MBROPT(*NONE) CRTFILE(*YES)
 
                CPYF       FROMFILE(EMXT2D2) TOFILE(&TOLIB/XEMXT2D2) +
                             MBROPT(*NONE) CRTFILE(*YES)
 
             ENDDO
 
/* Restart? */
 
             IF         COND(&STAT *EQ 'Y') THEN(DO)
 
/* Use security copy */
 
                CPYF       FROMFILE(&TOLIB/XEMXT2T1) TOFILE(EMXT2T1) +
                             MBROPT(*REPLACE) CRTFILE(*NO)
 
                CPYF       FROMFILE(&TOLIB/XEMXT2D2) TOFILE(EMXT2D2) +
                             MBROPT(*REPLACE) CRTFILE(*NO)
 
             ENDDO
 
/* Update restart status */
 
             CHGVAR     VAR(&STAT) VALUE('Y')
 
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/* Clear FX Netting Event Files */
 
             CLRPFM     FILE(FXNEVTPD)
 
             CLRPFM     FILE(FXNEVTPA)
 
/* EM Update for FX Netting Deals */
 
             CALL       PGM(DL0403)
 
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
 
/* Update restart status */
 
               CHGVAR     VAR(&STAT) VALUE('N')
 
               CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/* Remove security copies */
 
               DLTF       FILE(&TOLIB/XEMXT2T1)
 
               DLTF       FILE(&TOLIB/XEMXT2D2)
 
               GOTO       CMDLBL(END)
 
             ENDDO
 
/* Error */
 
             SNDMSG     MSG('DLC0607NT Terminated - database in +
                          error') TOMSGQ(MOPERQ MRUNQ)
 
END:
             ENDPGM
