/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas DL Clear APR Calculation file in QTEMP')        */
/*********************************************************************/
/*                                                                   */
/*  Midas   - Dealing Module                                         */
/*                                                                   */
/*  DLC6004 - Midas DL Clear APR Calculation File in QTEMP           */
/*                                                                   */
/*       (c) Finastra International Limited 2016                     */
/*                                                                   */
/*        Last Amend No. CER050  *Create    Date 16Jun19             */
/*        Prev Amend No. xxxxxx             Date ddmmmyy             */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CER050 - Annualised Percentage Rate                         */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&ERR &DLNO)

             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited 2016')
             DCL        VAR(&ERR) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DLNO) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')

             CHGJOB     SWS(XXXXXX00)

            STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
            MONMSG     MSGID(CPF8351)

/* Ensure QTEMP is at the top of the library list                    */
             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)

             ADDLIBLE   LIB(QTEMP) POSITION(*FIRST)
             MONMSG     MSGID(CPF0000)

               RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
               CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)

/* Ensure PF/GLAPRCTD is in QTEMP and empty                          */
             CHKOBJ     OBJ(QTEMP/GLAPRCTD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
               CPYF       FROMFILE(&DPLIB/GLAPRCTD) TOFILE(QTEMP/GLAPRCTD) +
                            CRTFILE(*YES)
             ENDDO

             CLRPFM     FILE(QTEMP/GLAPRCTD)

             OVRDBF     FILE(DEAMS) SHARE(*NO)
             OVRDBF     FILE(DEALS) SHARE(*NO)

/* Call the LE APR Calculation program DL6004                        */
             CALL       DL6004 PARM(&ERR &DLNO)
             COMMIT

/* Ensure QTEMP is at the bottom of the library list                 */
             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)

             ADDLIBLE   LIB(QTEMP) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)

             CPYF       FROMFILE(QTEMP/GLAPRCTD) +
                          TOFILE(&DPLIB/GLAPRCTD) MBROPT(*REPLACE) +
                          CRTFILE(*NO)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2869)

             ENDPGM
