/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas DL Daily update history with deal event')       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Dealing Module                                      */
/*                                                                   */
/*       DLC6009 - PROGRAM TO CONTROL THE HISTORY FILE UPDATE OF     */
/*                 DEAL EVENTS.                                      */
/*                                                                   */
/*     (C) Copyright Finastra International Limited 2016             */
/*                                                                   */
/*       Last Amend No. CER050  *Create   Date 16Jun19               */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CER050 - Annualised Percentage Rate                         */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*********************************************************************/
/*       C R E A T I O N     P A R A M E T E R S                     */
/*                                                                   */
/*********************************************************************/
      PGM        PARM(&CNAM &CSEQ)

             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&BISCPY) TYPE(*CHAR) LEN(64) VALUE('(C) +
                          Finastra International Limited 2016')
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)

             CHGJOB     SWS(00000000)

             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SST(&DPLIB 1 2)) VALUE(&SYSID)

             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STATUS)

             IF         COND(&STATUS *EQ 'Y') THEN(DO)
                CPYF       FROMFILE(&DPLIB/XDLHISTTD) +
                             TOFILE(DLHISTTD) MBROPT(*REPLACE)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(DLHISTTD))
             ENDDO

             IF         COND(&STATUS *NE 'Y') THEN(DO)
                DLTF       FILE(&DPLIB/XDLHISTTD)
                MONMSG     MSGID(CPF2105)
                CPYF       FROMFILE(*LIBL/DLHISTTD) +
                             TOFILE(&DPLIB/XDLHISTTD) +
                             MBROPT(*REPLACE) CRTFILE(*YES)
                MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                             EXEC(CLRPFM FILE(&DPLIB/XDLHISTTD))
             ENDDO

             CALL       PGM(CB0150) PARM(&CNAM &CSEQ 'Y')

             CLRPFM     FILE(HSTRVAA)

             OVRDBF     FILE(DKEYSDP) SHARE(*YES)
             OPNQRYF    FILE((DKEYSDP)) QRYSLT('%SST(AKEY 1 2) *EQ +
                          "TI" *OR %SST(AKEY 1 2) *EQ "IP" *OR +
                          %SST(AKEY 1 2) *EQ "TD" *OR %SST(AKEY 1 +
                          2) *EQ "CD" *OR %SST(AKEY 1 2) *EQ "CL"') +
                          KEYFLD((CNUM) (DLNO) (AKEY *DESCEND))

             CALL       PGM(DL6009)

             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                             CMDLBL(ABNOR))

             CLOF       OPNID(DKEYSDP)
             DLTOVR     FILE(DKEYSDP)

             CALL      PGM(CB0150) PARM(&CNAM &CSEQ 'N')

             GOTO       CMDLBL(END)

/*-----------------------------------------------------------------*/
 ABNOR:      SNDPGMMSG  MSG('DLC6009 ended abnormally') TOPGMQ(*EXT) +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)

/*-----------------------------------------------------------------*/
 END:        CHGVAR     VAR(&BISCPY) VALUE('(C) Finastra International +
                          Limited 2016')
             ENDPGM
