      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member of DL0280DBAK                           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG11189 *CREATE   Date 08May06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG11189 - Deal amendment 'SC' should be applied to FRCD     *
      *             deals.  Changes made in /COPY DL0280CSFR.         *
      *             Also moved into a /COPY SR/DLBACK to free up      *
      *             space in this program.                            *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  DLBACK - To calculate rate fix date for FRA/IRS              *
      *                                                               *
      *****************************************************************
     C     DLBACK        BEGSR
      *
      **  Initialise Work Field
      *
     C                   Z-ADD     ZINDT         ZINDT             5 0
     C                   Z-ADD     ZRFDY         ZRFDY             5 0
     C                   MOVE      ZCCY          ZCCY              3
      *
     C                   Z-ADD     0             ZWKFLD            5 0
      *
      **  Set off *IN99
      *
     C                   MOVE      '0'           *IN(99)
      *
      **  Set Rate Fix Date equal to Input Date
      *
     C                   Z-ADD     ZINDT         ZRFDT             5 0
      *
      **  Perform the following processing until the work field is
      **  greater than or equal to the Rate Fix Days
      *
     C     ZWKFLD        DOUGE     ZRFDY
      *
      **  If the Rate Fix Days are not equal to zero subtract 1
      **  from the Rate Fix Date
     C     ZRFDY         IFNE      0
     C                   SUB       1             ZRFDT
     C                   ENDIF
      *
      **  The following is repeated until the resultant date is
      **  proved to be a working day in the relevant currency.
      **  ZDATE5 checks the Rate Fix Date for a holiday. The option
      **  to find a working day in ZDATE5 is not taken.
      *
     C     ZIND          DOUEQ     'W'
      *
     C                   Z-ADD     ZRFDT         ZDAYNO
     C                   MOVE      BJSLCD        ZLOC
     C                   EXSR      ZCHKH
      *
      **  If the date is a holiday (ZIND = 'H')
      **    subtract 1 from the Rate Fix Date
      *
     C     ZIND          IFEQ      'H'
     C                   SUB       1             ZRFDT
     C                   ENDIF
     C                   ENDDO
      *
      **  When a working day has been found, add 1 to the Work Field
      *
     C                   ADD       1             ZWKFLD
     C                   ENDDO
      *
     C                   ENDSR
