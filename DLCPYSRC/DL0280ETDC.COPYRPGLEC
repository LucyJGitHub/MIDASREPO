      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  DL0280 - DL0280ETDC                                          *
      *                                                               *
      *  Last Amend No. MD024874A          Date 19Aug14               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER054             Date 07Apr09               *
      *                 BUG23123           Date 09Mar09               *
      *                 CER048A            Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 233324  *CREATE    Date 02May05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD024874A - MM deal tax calculated twice for Maturity Date.  *
      *              Create SDCSINL10 logical file similar to         *
      *              LF/SDCSINL6 but with new key TSEDAT.             *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax                        *
      *  BUG23123 - Report: Does not show Retail Account Number       *
      *                     Date is not Midas Standard Date           *
      *                     Period should be actual int. calc. period *
      *                     Secondary Tax amount is not printed       *
      *                     Added field TSSLID                        *
      *                     (Recompile)                               *
      *  CER048A - German Features - Taxes                             *
      *  233324 - Add new fields in DEALSDC for EU Tax.               *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRETDC - EU Tax processing for Money Market & Loans.         *
      *                                                               *
      *****************************************************************
     C     SRETDC        BEGSR
      *
     C                   MOVE      'D'           WKMODI
     C                   Z-ADD     0             WKTSRS
     C                   Z-ADD     0             WKTXSC
     C                   Z-ADD     0             WKTXSB
      *                                                                                      CER048A
     C                   Z-ADD     *ZEROS        WKStxb           15 0                       CER048A
     C                   Z-ADD     *ZEROS        WKStxo           15 0                       CER048A
     C                   Z-ADD     *ZEROS        WKStxt           15 0                       CER048A
     C                   Z-ADD     *ZEROS        WKTtxb           15 0                        CER054
     C                   Z-ADD     *ZEROS        WKTtxo           15 0                        CER054
     C                   Z-ADD     *ZEROS        WKTtxt           15 0                        CER054
      *
     C*****KYEUTI        SETLL     SDCSINL6                           42                   MD024874A
     C*****KYEUTI        READE     SDCSINL6                               42               MD024874A
     C     KYEUTI        SETLL     SDCSINL10                          42                   MD024874A
     C     KYEUTI        READE     SDCSINL10                              42               MD024874A
      *
      ** Process if customer status is 'T'.
      *
     C     *IN42         DOWEQ     '0'
      *
     C     TSSTAT        IFEQ      'T'
      *
      ** Accumulate Tax Amount in original currency if Original Ccy
      ** is equal to tax currency
      *
     C     TSOCCY        IFEQ      TSTXCY
     C                   ADD       TSTXSR        WKTSRS
      *                                                                                      CER048A
     C                   EVAL      WKStxo = WKStxo + TSSTDO                                  CER048A
     C                   EVAL      WKTtxo = WKTtxo + TSTTDO                                   CER054
      *                                                                                      CER048A
     C                   ENDIF
      *
      ** If original currency is different from tax currency,
      ** accumulate tax in original currency,
      ** accumulate tax in tax currency,
      ** accumulate tax in base currency.
      *
     C     TSOCCY        IFNE      TSTXCY
     C                   ADD       TSTXSR        WKTSRS
     C                   ADD       TSTXSC        WKTXSC
     C                   ADD       TSTXSB        WKTXSB
      *                                                                                      CER048A
      ** Accumulate secondary tax in original currency                                       CER048A
      ** Accumulate secondary tax in tax currency                                            CER048A
      ** Accumulate secondary tax in base currency                                           CER048A
      *                                                                                      CER048A
     C                   EVAL      WKStxo = WKStxo + TSSTDO                                  CER048A
     C                   EVAL      WKStxt = WKStxt + TSSTDT                                  CER048A
     C                   EVAL      WKStxb = WKStxb + TSSTDB                                  CER048A
      *                                                                                      CER048A
      ** Accumulate third tax in original currency                                            CER054
      ** Accumulate third tax in tax currency                                                 CER054
      ** Accumulate third tax in base currency                                                CER054
      *                                                                                       CER054
     C                   EVAL      WKTtxo = WKTtxo + TSTTDO                                   CER054
     C                   EVAL      WKTtxt = WKTtxt + TSTTDT                                   CER054
     C                   EVAL      WKTtxb = WKTtxb + TSTTDB                                   CER054
      *                                                                                       CER054
     C                   ENDIF
     C                   ENDIF
      *
     C*****KYEUTI        READE     SDCSINL6                               42               MD024874A
     C     KYEUTI        READE     SDCSINL10                              42               MD024874A
     C                   ENDDO
      *
      ** EU Tax in Original Ccy Deducted to Date
      *
     C                   ADD       WKTSRS        EUTO
      *
      ** EU Tax in Base Ccy Deducted to Date
      *
     C                   ADD       WKTXSB        EUTB
      *
      ** EU Tax in Tax Ccy Deducted to Date
      *
     C                   ADD       WKTXSC        EUTX
      *
      ** Tax Currency
      *
     C                   MOVE      @@TXCY        EUCY
      *
      ** Country of Location
      *
     C                   MOVE      @@COLC        COLC
      *
     C                   EVAL      STXO  = WKStxo + STXO                                     CER048A
     C                   EVAL      STXT  = WKStxt + STXT                                     CER048A
     C                   EVAL      STXB  = WKStxb + STXB                                     CER048A
      *                                                                                      CER048A
     C                   EVAL      TTXO  = WKTtxo + TTXO                                      CER054
     C                   EVAL      TTXT  = WKTtxt + TTXT                                      CER054
     C                   EVAL      TTXB  = WKTtxb + TTXB                                      CER054
      *                                                                                       CER054
     C                   ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * End of /COPY DL0280ETDC                                       *
      *****************************************************************
