      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member of DL0280MKEY                           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG6293            Date 04Oct04               *
      *  Prev Amend No. BUG3012            Date 01Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG6293- Move subroutine to a /COPY to free up space in      *
      *           DL0280 for other changes.                           *
      *  BUG3012- Account keys are not being output for multiple      *
      *           settlement accounts.                                *
      *                                                               *
      *****************************************************************                CDL018BUG4426
      *                                                               *                CDL018BUG4426
      *  MLTKEY - Generate Multiple Settlement Account Keys           *                CDL018BUG4426
      *                                                               *                CDL018BUG4426
      *****************************************************************                CDL018BUG4426
     C     MLTKEY        BEGSR
      *                                                                                CDL018BUG4426
      **  Get details of multiple settlement accounts                                  CDL018BUG4426
     C     DLNO          CHAIN     DLFSACL0                           90
      *                                                                                CDL018BUG4426
      **  Database error if not found                                                  CDL018BUG4426
     C     *IN90         IFEQ      *ON
     C                   MOVE      'DLFSACPD'    DBFILE                         *************
     C                   MOVEL     '054'         DBASE                          *DBERROR 054*
     C                   MOVEL     'DLNO'        DBKEY                          *************
     C                   EXSR      *PSSR
     C                   ENDIF
      *                                                                                CDL018BUG4426
      **  Save details on original account key generated                               CDL018BUG4426
     C                   Z-ADD     KYAMT         ##PSTA           13 0
     C                   MOVEL     KYSTBR        ##OSBR            3
     C                   MOVEL     KYOSTL        ##OSTL           12
      *                                                                                CDL018BUG4426
      **  Reset amount work fields                                                     CDL018BUG4426
     C                   Z-ADD     0             ##INTA           13 0
     C                   Z-ADD     0             ##PAMT           13 0
     C                   Z-ADD     0             ##TOTE           13 0
      *                                                                                CDL018BUG4426
      **  Set amounts to be split                                                      CDL018BUG4426
     C                   SELECT
     C     AKY(3)        WHENEQ    'V'
     C                   Z-ADD     0             ##INTA
     C                   Z-ADD     KYAMT         ##PAMT
     C     AKY(3)        WHENEQ    'I'
     C                   Z-ADD     WORKD         ##INTA
     C                   Z-ADD     0             ##PAMT
     C     AKY(3)        WHENEQ    'M'
     C     FUID          IFNE      'Y'
     C     IPFR          ANDNE     'N'
     C     IPDS          ANDNE     'Y'
     C                   Z-ADD     WORKD         ##INTA
     C                   ENDIF
     C     PAMT          ADD       @RBDA         ##PAMT
     C                   ENDSL
      *                                                                                CDL018BUG4426
      **  Process receipt settlement accounts                                          CDL018BUG4426
     C     RPIND         IFEQ      'R'
     C                   EXSR      RSETAC
     C                   ENDIF
      *                                                                                CDL018BUG4426
      **  Process pay settlement accounts                                              CDL018BUG4426
     C     RPIND         IFEQ      'P'
     C                   EXSR      PSETAC
     C                   ENDIF
      *                                                                                CDL018BUG4426
      **  Update DLFSACPD if not shadow posting generation                             CDL018BUG4426
     C     *INU1         IFEQ      *ON
     C                   UPDATE    DLFSACD0
     C                   ELSE
     C                   UNLOCK    DLFSACL0
     C                   ENDIF
      *                                                                                CDL018BUG4426
     C                   ENDSR
