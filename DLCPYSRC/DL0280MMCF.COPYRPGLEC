      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member of DL0280MMCF                           *
      *                                                               *
      *  Last Amend No. CDL093             Date 14Apr14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG13075           Date 26Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG12910           Date 18Dec06               *
      *                 CSD027             Date 09Mar06               *
      *                 232928             Date 08Apr05               *
      *                 CAS011  *CREATE    Date 16May05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL093 - MM Interest calculation type 9.                     *
      *  BUG13075 - Do not allow double SAMT cash flow type in file   *
      *             MMCFLSPD for a deal.                              *
      *  BUG12910 - Update cash flow files if not shadow posting      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  232928 - Amortise all fees non-linearly, EIR calculation,    *
      *           Non-linear amortisation calculation & adjustment    *
      *  CAS011 - EIR/AC Accounting Upgrade to MP1                    *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  SRMMCF - Record a cash flow event.                           *
      *                                                               *
      *****************************************************************
     C     SRMMCF        BEGSR
      *
     C     CAS009        IFEQ      'Y'
     C     *INU1         ANDEQ     *ON                                                      BUG12910
      *
     C     DTYP          IFEQ      'IP'
     C     DTYP          OREQ      'TI'
     C     DTYP          OREQ      'CL'
     C     DTYP          OREQ      'DL'
     C     DTYP          OREQ      'IT'
     C     DTYP          OREQ      'TD'
     C     DTYP          OREQ      'CI'
     C     DTYP          OREQ      'CD'
     C     DTYP          OREQ      'C2'
     C     DTYP          OREQ      'BP'
     C     DTYP          OREQ      'BD'
     C     DTYP          OREQ      'TB'
     C     DTYP          OREQ      'DA'
     C     DTYP          OREQ      'TA'
     C     DTYP          OREQ      'CS'
     C     DTYP          OREQ      'BS'
     C     DTYP          OREQ      'BR'
     C     DTYP          OREQ      'TS'
     C     DTYP          OREQ      'RA'
     C     DTYP          OREQ      'TR'
      *
     C                   MOVE      'N'           NAIND             1
      *
     C     DTYP          IFEQ      'C2'
     C     DTYP          OREQ      'BP'
     C     DTYP          OREQ      'BD'
     C     DTYP          OREQ      'TB'
     C     DTYP          OREQ      'DA'
     C     DTYP          OREQ      'TA'
     C     DTYP          OREQ      'CS'
     C     DTYP          OREQ      'BS'
     C     DTYP          OREQ      'BR'
     C     DTYP          OREQ      'TS'
     C     DTYP          OREQ      'RA'
     C     DTYP          OREQ      'TR'
     C                   MOVE      'Y'           NAIND
     C                   ENDIF
      *
     C     CFLSKY        KLIST                                                              BUG13075
     C                   KFLD                    DLNO                                       BUG13075
     C                   KFLD                    EVCD                                       BUG13075
     C                   EVAL      WSAMT = 'N'                                              BUG13075
      *                                                                                     BUG13075
      ** Change WSAMT value if SAMT cash flow type is being pro-                            BUG13075
      ** cessed. If not, write to cash flow file will always pro-                           BUG13075
      ** ceed because WSAMT is always 'N'.                                                  BUG13075
      *                                                                                     BUG13075
     C                   IF        WCFTP  = 'SAMT'                                          BUG13075
     C     CFLSKY        SETLL     MMCFLSL0                                                 BUG13075
     C     CFLSKY        READE     MMCFLSL0                                                 BUG13075
     C                   DOW       NOT%EOF(MMCFLSL0)                                        BUG13075
     C                   IF        MFFTYP = 'SAMT'                                          BUG13075
     C                   EVAL      WSAMT = 'Y'                                              BUG13075
     C                   ENDIF                                                              BUG13075
     C     CFLSKY        READE     MMCFLSL0                                                 BUG13075
     C                   ENDDO                                                              BUG13075
     C                   ENDIF                                                              BUG13075
     C                   CLEAR                   MMCFLSD0
      *
     C                   Z-ADD     DLNO          MFDLNO
     C                   Z-ADD     0             MFFSEQ
     C                   MOVE      DTYP          MFDTYP
     C                   MOVE      DLST          MFDLST
     C                   Z-ADD     KYDAT         MFFLDT
      *
     C                   MOVE      *BLANK        MFDATE
     C                   Z-ADD     KYDAT         ZDAYNO
     C                   EXSR      ZDATE2
     C                   MOVE      ZADATE        MFDATE
      *
     C                   Z-ADD     KYAMT         MFCAMT
     C                   MOVE      WCFTP         MFFTYP
      *
     C     DTYP          IFEQ      'IT'
     C     DTYP          OREQ      'TD'
     C     DTYP          OREQ      'CI'
     C     DTYP          OREQ      'CD'
     C     DTYP          OREQ      'CS'
     C     DTYP          OREQ      'BS'
     C     DTYP          OREQ      'BR'
     C     DTYP          OREQ      'TS'
     C     DTYP          OREQ      'RA'
     C     DTYP          OREQ      'TR'
     C     AKY(3)        IFEQ      'V'
      *
      ** If Deposit or NA sold and a Value date event, cash flow is incoming.
      *
     C                   MOVE      'I'           MFIOIN
     C                   ELSE
      *
      ** Else, cash flow is outgoing.
      *
     C                   MOVE      'O'           MFIOIN
     C                   ENDIF
     C                   ELSE
     C     AKY(3)        IFEQ      'V'
      *
      ** If Loan or NA bought and a Value date event, cash flow is outgoing.
      *
     C                   MOVE      'O'           MFIOIN
     C                   ELSE
      *
      ** Else, cash flow is incoming.
      *
     C                   MOVE      'I'           MFIOIN
     C                   ENDIF
     C                   ENDIF
      *
     C     KYDAT         SUB       BJRDNB        MFNDYP
     C                   MOVE      ICBS          MFICBS
      *
      ** Calculation type '9'                                                                 CDL093
      *                                                                                       CDL093
     C                   If        ICBS  = 9                                                  CDL093
      *                                                                                       CDL093
     C                   Z-ADD     BJRDNB        @@DAYN                                       CDL093
     C                   EXSR      ZDATE9                                                     CDL093
     C                   Z-ADD     @@VDT         ZZStartDate                                  CDL093
      *                                                                                       CDL093
     C                   Z-ADD     KYDAT         @@DAYN                                       CDL093
     C                   EXSR      ZDATE9                                                     CDL093
     C                   Z-ADD     @@VDT         ZZEndDate                                    CDL093
                                                                                              CDL093
      ** Check if the period contains a Feb 29                                                CDL093
                                                                                              CDL093
     C                   EXSR      #FEB29                                                     CDL093
      *                                                                                       CDL093
     C                   ENDIF                                                                CDL093
      *                                                                                       CDL093
     C                   SELECT
      *
     C     ICBS          WHENEQ    1
     C     ICBS          OREQ      4
     C     ICBS          OREQ      9                                                          CDL093
     C     #29FEB        ANDNE     'Y'                                                        CDL093
     C                   Z-ADD     365           MFNDYY
      *
     C     ICBS          WHENEQ    2
     C     ICBS          OREQ      3
     C     ICBS          OREQ      5
     C                   Z-ADD     360           MFNDYY
      *
     C     ICBS          WHENEQ    6
     C     ICBS          OREQ      9                                                          CDL093
     C     #29FEB        ANDEQ     'Y'                                                        CDL093
     C                   Z-ADD     366           MFNDYY
      *
     C                   ENDSL
      *
     C                   Z-ADD     INTR          MFIRAT
     C**********         Z-ADD     CNUM          MFCNUM                                       CSD027
     C                   MOVEL     CNUM          MFCNUM                                       CSD027
     C                   MOVE      BRCA          MFBRCA
     C                   MOVE      KYCCY         MFCYCD
     C                   MOVE      BOKC          MFBKCD
      *
     C     NAIND         IFNE      'Y'
     C                   Z-ADD     PAMT          MFCPAM
     C     MFFTYP        IFEQ      'PINC'
     C                   ADD       KYAMT         MFCPAM
     C                   ENDIF
     C     MFFTYP        IFEQ      'PDEC'
     C                   SUB       KYAMT         MFCPAM
     C                   ENDIF
     C     MFFTYP        IFEQ      'CINT'
     C                   ADD       KYAMT         MFCPAM
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     FVAL          MFCPAM
     C                   ENDIF
     C                   IF        WSAMT = 'N'                                              BUG13075
      *
     C                   WRITE     MMCFLSD0
      *                                                                                       232928
      ** Write to History File                                                                232928
      *                                                                                       232928
     C                   EXSR      SRHIST                                                     232928
     C                   ENDIF                                                              BUG13075
      *
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   ENDSR
