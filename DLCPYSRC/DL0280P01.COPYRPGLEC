      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  DL0280 - DL0280P01                                           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 205045             Date 02Jan07               *
      *  Prev Amend No. CPK025  *CREATE    Date 18Jul06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  205045 - Rollover deal not found in DEALSDC, while           *
      *           processing deal for Portfolio Management. Change    *
      *           the LF to look to include historic deals DLCHISPD.  *
      *  CPK025 - Externalise P01.                                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P01 - TO CALCULATE FX CROSS RATE FOR DEAL                    *
      *                                                               *
      *****************************************************************
     C     P01           BEGSR                                                  ** P01    **   S0148
     C*                                                                   S01486
     C** OBTAIN PORTFOLIO CURRENCY                                        S01486
     C                   EXSR      P03                                                         S0148
     C     WWERRF        CABEQ     'Y'           P01END                                        S0148
     C*                                                                   S01486
     C     RODN          IFEQ      0                                                           S0148
     C                   MOVE      PCCY          ZCCYF                                         S0148
     C                   MOVE      WWPCCY        ZCCYT                                         S0148
     C                   Z-ADD     DDAT          ZSDATE                                        S0148
     C                   EXSR      ZBAKRT                                                      S0148
     C*                                                                   S01486
     C** DATABASE ERROR                                                   S01486
     C     ZERROR        IFEQ      'Y'                                                         S0148
     C                   SETON                                        U7U8LR                   S0148
     C                   MOVEL     '10'          DBASE             3             **DBERR 1**   S0148
     C**********         EXCEPT    ERROR                                              S0148 AR547995
     C                   MOVE      'Y'           WWERRF            1                           S0148
     C                   EXSR      *PSSR                                                    AR547995
     C                   GOTO      P01END                                                      S0148
     C                   END                                                                   S0148
     C*                                                                   S01486
     C                   Z-ADD     ZCRSRT        FXRD                                          S0148
     C                   END                                                                   S0148
     C*                                                                   S01486
     C     RODN          IFNE      0                                                           S0148
     C*****RODN          CHAIN     MMDELULL                           18                S0148 205045
     C     RODN          CHAIN     DEALSALL                           18                      205045
     C*                                                                   S01486
     C** DATABASE ERROR                                                   S01486
     C     *IN18         IFEQ      '1'                                                         S0148
     C                   SETON                                        U7U8LR                   S0148
     C                   MOVEL     '11'          DBASE             3             **DBERR 1**   S0148
     C**********         EXCEPT    ERROR                                              S0148 AR547995
     C                   MOVE      'Y'           WWERRF                                        S0148
     C                   EXSR      *PSSR                                                    AR547995
     C                   GOTO      P01END                                                      S0148
     C                   END                                                                   S0148
      *                                                                   CEU015
      ** Convert RODA to Euros equivalent                                 CEU015
      *                                                                   CEU015
     C     *LIKE         DEFINE    RODA          @RODA                                         CEU01
     C                   Z-ADD     RODA          @RODA                                         CEU01
      *                                                                   CEU015
     C     CEU015        IFEQ      'Y'                                                         CEU01
     C     RTFC          ANDNE     *BLANKS                                                     CEU01
      *                                                                   CEU015
     C                   CALL      'EU0200'                                                    CEU01
     C                   PARM      *BLANKS       P@RTCD            7            RETURN CODE    CEU01
     C                   PARM      @RODA         P@FRAM           18 3          TRANS AMT      CEU01
     C                   PARM      RTFC          P@FRCY            3            FROM CCY       CEU01
     C                   PARM      PCCY          P@TOCY            3            TO   CCY       CEU01
     C                   PARM                    P@OUTA           15 0          AMOUNT         CEU01
     C                   PARM                    P@INTA           18 3          INT SET AMT    CEU01
     C                   PARM                    P@EXRT           15 9          EXCH RATE      CEU01
     C                   PARM                    P@MDIN            1            M/D IND        CEU01
      *                                                                   CEU015
     C     P@RTCD        IFEQ      '*ERROR*'                                                   CEU01
     C                   MOVEL     'EU0200'      DBFILE           10             ************* CEU01
     C                   MOVEL     '21'          DBASE             3             *DBERROR 021* CEU01
     C                   MOVE      *BLANKS       DBOPTN            7             ************* CEU01
     C                   MOVEL     RTFC          DBKEY                                         CEU01
     C                   SETON                                        U7U8                     CEU01
     C                   EXSR      *PSSR                                                       CEU01
     C                   ELSE                                                                  CEU01
     C                   Z-ADD     P@OUTA        @RODA                                         CEU01
     C                   ENDIF                                                                 CEU01
      *                                                                   CEU015
     C                   ENDIF                                                                 CEU01
      *                                                                   CEU015
     C*                                                                   S01486
     C** CONVERT ROLLOVER DEAL AMOUNT INTO POSITIVE FIGURE                S01486
     C     @RODA         IFLT      0                                                           CEU01
     C                   Z-SUB     @RODA         WWROD1           15 0                         CEU01
     C                   ELSE                                                                  S0148
     C                   Z-ADD     @RODA         WWROD1           15 0                         CEU01
     C                   END                                                                   S0148
     C*                                                                   S01486
     C     PAMT          IFGT      WWROD1                                                      S0148
     C                   MOVE      PCCY          ZCCYF                                         S0148
     C                   MOVE      WWPCCY        ZCCYT                                         S0148
     C                   MOVE      DDAT          ZSDATE                                        S0148
     C                   EXSR      ZBAKRT                                                      S0148
     C*                                                                   S01486
     C** DATABASE ERROR                                                   S01486
     C     ZERROR        IFEQ      'Y'                                                         S0148
     C                   SETON                                        U7U8LR                   S0148
     C                   MOVEL     '12'          DBASE             3             **DBERR 1**   S0148
     C**********         EXCEPT    ERROR                                              S0148 AR547995
     C                   MOVE      'Y'           WWERRF                                        S0148
     C                   EXSR      *PSSR                                                    AR547995
     C                   GOTO      P01END                                                      S0148
     C                   END                                                                   S0148
     C*                                                                   S01486
     C** CALCULATE NEW FX RATE TO BE OUTPUT TO DEALS                      S01486
     C                   Z-ADD     0             WWOLFX                                        S0148
     C                   Z-ADD     0             WWPRNC                                        S0148
     C                   Z-ADD     0             WWNWFX                                        S0148
     C                   Z-ADD     0             WWFXCO                                        S0148
     C     DLFXRD        MULT(H)   WWROD1        WWOLFX           15 0                         S0148
     C     PAMT          SUB       WWROD1        WWPRNC           15 0                         S0148
     C     WWPRNC        MULT(H)   ZCRSRT        WWNWFX           15 0                         S0148
     C     WWOLFX        ADD       WWNWFX        WWFXCO           15 0                         S0148
     C     WWFXCO        DIV(H)    PAMT          FXRD                                          S0148
     C*                                                                   S01486
     C** IF PRINCIPAL HAS DECREASED FX RATE SHOULD BE SET TO OLD RATE     S01486
     C                   ELSE                                                                  S0148
     C                   Z-ADD     DLFXRD        FXRD                                          S0148
     C*                                                                   S01486
     C                   END                                                                   S0148
     C*                                                                   S01486
     C                   END                                                                   S0148
     C*                                                                   S01486
     C     P01END        ENDSR                                                  ** P01 END *   S0148
      *****************************************************************
      /EJECT
      *****************************************************************
      * End of /COPY DL0280P01                                        *
      *****************************************************************
