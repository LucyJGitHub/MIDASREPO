      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member (RPG)                                   *
      *                                                               *
      *  DL0280 - DL0280RSAC                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPK024  *CREATE    Date 05Apr06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPK024 - Move coding for SR/SRRSAC                           *
      *           into copy member for DL0280 to make room            *
      *           for other changes.                                  *
      *                                                               *
      *****************************************************************                       CAS005
      *                                                               *                       CAS005
      *  SRRSAC - Calculate Accrual Amounts for IRS                   *                       CAS005
      *                                                               *                       CAS005
      *****************************************************************                       CAS005
     C     SRRSAC        BEGSR
      *                                                                                       CAS005
      ** If deal type = RS, RX or (RR/RP/RF + ave. rate method = N) calc                      CAS005
      ** interest from and including interest accrual control date                            CAS005
      *                                                                                       CAS005
     C     DTYP          IFNE      'RR'
     C     DTYP          ANDNE     'RP'
     C     DTYP          ANDNE     'RF'
     C     TZAVRM        ORNE      'Y'
      *                                                                                       CAS005
      ** If cash flow schedule, determine proportion of cash flow accrued                     CAS005
      *                                                                                       CAS005
     C     TZIPFC        IFEQ      'C'
      *                                                                                       CAS005
      ** Use SR/ZINTDY to determine the number of days from the last                          CAS005
      ** pay date to the event control date.                                                  CAS005
      *                                                                                       CAS005
     C                   Z-ADD     TZSLID        ZZBEG
     C                   Z-ADD     TZECD         ZZEND
     C                   MOVEL     TZCBS         ZZCALC
      *                                                                                       CAS005
     C     TZLDA         IFNE      'Y'
     C     ZZEND         ADD       1             ZZEND
     C                   ENDIF
      *                                                                                       CAS005
     C                   EXSR      ZINTDY
      *                                                                                       CAS005
      ** For calculation Method '6' use new field INT6DY introduced by                        CAS005
      ** core #126017 in ZINTDY.                                                              CAS005
      *                                                                                       CAS005
     C     ZZCALC        IFEQ      '6'
     C     ZZCALC        OREQ      '9'                                                        CIR013
     C     #29FEB        ANDEQ     'Y'                                                        CIR013
     C                   Z-ADD     INT6DY        ZPARPD
     C                   ELSE
      *                                                                                       CAS005
     C                   Z-ADD     ZZINDY        ZPARPD
     C                   ENDIF
      *                                                                                       CAS005
      ** Use SR/ZINTDY to determine the number of days from the last                          CAS005
      ** pay date to the next payment date.                                                   CAS005
      *                                                                                       CAS005
     C                   Z-ADD     TZSLID        ZZBEG
     C                   Z-ADD     TZNIPD        ZZEND
     C                   MOVEL     TZCBS         ZZCALC
      *                                                                                       CAS005
     C                   EXSR      ZINTDY
      *                                                                                       CAS005
      ** For calculation Method '6' use new field INT6DY introduced by                        CAS005
      ** core #126017 in ZINTDY.                                                              CAS005
      *                                                                                       CAS005
     C     ZZCALC        IFEQ      '6'
     C     ZZCALC        OREQ      '9'                                                        CIR013
     C     #29FEB        ANDEQ     'Y'                                                        CIR013
     C                   Z-ADD     INT6DY        ZFULPD
     C                   ELSE
      *                                                                                       CAS005
     C                   Z-ADD     ZZINDY        ZFULPD
     C                   ENDIF
      *                                                                                       CAS005
      ** Also set up ZIEND as this is used by #LDNCF to get the                               CAS005
      ** cash-flow amount on the next payment date.                                           CAS005
      *                                                                                       CAS005
     C                   Z-ADD     TZNIPD        ZIEND
      *                                                                                       CAS005
      ** Determine payment due on next date (ZIEND) from cash flow                            CAS005
      ** schedule                                                                             CAS005
      *                                                                                       CAS005
     C     AKY(9)        IFEQ      'P'
     C                   MOVEL     'U'           OTIN
     C                   ELSE
     C                   MOVEL     'T'           OTIN
     C                   ENDIF
     C                   EXSR      #LDNCF
      *                                                                                       CAS005
      ** Pro-rate the payment amount in this accrual period                                   CAS005
      *                                                                                       CAS005
     C     CFLA          MULT      ZPARPD        WINTST
     C     ZFULPD        IFNE      *ZERO
     C                   DIV(H)    ZFULPD        WINTST
     C                   ELSE
     C                   Z-ADD     *ZERO         WINTST
     C                   ENDIF
      *                                                                                       CAS005
     C                   ELSE
      *                                                                                       CAS005
     C                   Z-ADD     TZIACD        ZIBEG
     C                   Z-ADD     TZECD         ZIEND
      *                                                                                       CAS005
      ** If last day accrual ind. = Y calc interest up to but                                 CAS005
      ** excluding the event control date otherwise calc up to                                CAS005
      ** and including event control date                                                     CAS005
      *                                                                                       CAS005
     C     TZLDA         IFNE      'Y'
     C     ZIEND         ADD       1             ZIEND
     C                   ENDIF
      *                                                                                       CAS005
     C                   Z-ADD     TZPPL         ZIAMT
     C                   Z-ADD     TZCBS         ZICALC
      *                                                                                       CAS005
     C                   EXSR      GLINTC
      *                                                                                       CAS005
      ** To replace a line removed from the internally described GLINTC                       CAS005
      *                                                                                       CAS005
     C     *IN02         IFEQ      *ON
     C     ZIRDFC        ANDEQ     'Y'
     C     *IN03         OREQ      *ON
     C     JRDFC         ANDEQ     'Y'
     C     *IN04         OREQ      *ON
     C     JRDFC         ANDEQ     'Y'
     C                   Z-ADD     ZINTR         WINTST
     C                   ELSE
     C                   Z-ADD(H)  ZINTR         WINTST
     C                   ENDIF
      *                                                                                       CAS005
     C                   ENDIF
      *                                                                                       CAS005
     C                   ENDIF
      *                                                                                       CAS005
      ** Else if deal type = RR/RP/RF + ave. rate method = Y, calc                            CAS005
      ** int. from and including start date current ave. rate period                          CAS005
      *                                                                                       CAS005
     C     DTYP          IFEQ      'RR'
     C     TZAVRM        ANDEQ     'Y'
     C     DTYP          OREQ      'RF'
     C     TZAVRM        ANDEQ     'Y'
     C     DTYP          OREQ      'RP'
     C     TZAVRM        ANDEQ     'Y'
     C                   Z-ADD     SZSDCR        ZIBEG
     C                   Z-ADD     TZECD         ZIEND
      *                                                                                       CAS005
      ** If last day accrual ind. = Y calc interest up to but                                 CAS005
      ** excluding the event control date otherwise calc up to                                CAS005
      ** and including event control date                                                     CAS005
      *                                                                                       CAS005
     C     TZLDA         IFNE      'Y'
     C     ZIEND         ADD       1             ZIEND
     C                   ENDIF
      *                                                                                       CAS005
     C                   Z-ADD     TZPPL         ZIAMT
     C                   Z-ADD     TZCBS         ZICALC
      *                                                                                       CAS005
     C                   EXSR      GLINTC
      *                                                                                       CAS005
      ** To replace a line removed from the internally described GLINTC                       CAS005
      *                                                                                       CAS005
     C     *IN02         IFEQ      *ON
     C     ZIRDFC        ANDEQ     'Y'
     C     *IN03         OREQ      *ON
     C     JRDFC         ANDEQ     'Y'
     C     *IN04         OREQ      *ON
     C     JRDFC         ANDEQ     'Y'
     C                   Z-ADD     ZINTR         WINTST
     C                   ELSE
     C                   Z-ADD(H)  ZINTR         WINTST
     C                   ENDIF
      *                                                                                       CAS005
     C                   Z-ADD     WINTST        TZIWRK           13 0
     C                   ENDIF
      *                                                                                       CAS005
      ** Add interest calculated, interest accrued to control date,                           CAS005
      ** accrued interest adjustments not posted and accured int                              CAS005
      ** adjustments posted = accrued interest to date                                        CAS005
      *                                                                                       CAS005
     C     WINTST        ADD       TZAICD        TZAITD           13 0
     C     TZAITD        ADD       TZIANP        TZAITD
     C     TZAITD        ADD       TZAIAP        TZAITD
      *                                                                                       CAS005
      ** Calculate accrued interest to post                                                   CAS005
      *                                                                                       CAS005
     C     TZAITD        SUB       TZIPTD        TZAITP           13 0
      *                                                                                       CAS005
      ** If our side of the deal and our side of the deal is an OIS or                        CAS005
      ** their side of the deal and their side of the deal is an OIS                          CAS005
      ** call DL1651.                                                                         CAS005
      *                                                                                       CAS005
     C     CIR007        IFEQ      'Y'
      *                                                                                       CAS005
     C     AKY(9)        IFEQ      'P'
     C     UICFR         ANDEQ     'O'
     C     AKY(9)        OREQ      'R'
     C     TICFR         ANDEQ     'O'
      *                                                                                       CAS005
     C     AKY(9)        IFEQ      'P'
     C                   MOVE      'O'           PSIDE
     C                   ELSE
     C                   MOVE      'T'           PSIDE
     C                   ENDIF
      *                                                                                       CAS005
      ** If First Day Accruals, subtract 1 from ZIEND as ZIEND increased                      CAS005
      ** by 1 in core code for First Day Accruals.                                            CAS005
      *                                                                                       CAS005
     C     TZLDA         IFNE      'Y'
     C     ZIEND         SUB       1             PTODT
     C                   Z-ADD     TZSLID        PFRDT
     C                   ENDIF
      *                                                                                       CAS005
      ** If Last Day Accruals, add 1 to the from date to give the                             CAS005
      ** correct number of days.                                                              CAS005
      *                                                                                       CAS005
     C     TZLDA         IFEQ      'Y'
     C     WFXDY         IFNE      0
     C                   Z-ADD     TZSLID        PFRDT
     C                   ELSE
     C     TZSLID        ADD       1             PFRDT
     C                   ENDIF
     C                   Z-ADD     ZIEND         PTODT
     C                   ENDIF
      *                                                                                       CAS005
     C                   CALL      'DL1651'
     C                   PARM      DLNO          PDLNO
     C                   PARM                    PSIDE
     C                   PARM                    PFRDT
     C                   PARM                    PTODT
     C     PAMNT         PARM      *ZERO         PAMNT
     C                   PARM      *BLANK        PRTRN
     C                   PARM      *BLANK        PTOAC
      *                                                                                       CAS005
     C     WFXDY         IFNE      0
     C                   Z-ADD     PTODT         ZIEND
     C                   Z-ADD     PTODT         KYDAT
     C                   ENDIF
      *                                                                                       CAS005
      ** Add Accrued Interest Adjusted not Posted, and Accrued Interest                       CAS005
      ** Adjusted and Posted to the Amount returned from DL1651.                              CAS005
      ** Add Interest Paid/Received to date to get a figure for the                           CAS005
      ** total amount of interest accrued for this deal from the start.                       CAS005
      *                                                                                       CAS005
     C     PAMNT         ADD       TZIANP        TZAITD
     C     TZAITD        ADD       TZAIAP        TZAITD
     C     TZAITD        ADD       TZIPRD        TZAITD
      *                                                                                       CAS005
      ** Subtract total amount of interest accrued for this deal from                         CAS005
      ** the start to arrive at the accrual amount to post today.                             CAS005
      *                                                                                       CAS005
     C     TZAITD        SUB       TZIPTD        TZAITP
      *                                                                                       CAS005
     C                   ENDIF
      *                                                                                       CAS005
     C                   ENDIF
      *                                                                                       CAS005
      ** Add the accrued interest adjustments not posted to                                   CAS005
      ** accrued interest adjustments posted                                                  CAS005
      *                                                                                       CAS005
     C     TZIANP        ADD       TZAIAP        TZAIAP
      *                                                                                       CAS005
      ** Add accrued interest to post to accrued interest posted                              CAS005
      ** to date                                                                              CAS005
      *                                                                                       CAS005
     C     TZAITP        ADD       TZIPTD        TZIPTD
      *                                                                                       CAS005
      ** Add the interest calculated to interest accrued to                                   CAS005
      ** control date                                                                         CAS005
      *                                                                                       CAS005
     C     WINTST        ADD       TZAICD        TZAICD
      *                                                                                       CAS005
      ** Zeroise accrued interest adjustments not posted                                      CAS005
      *                                                                                       CAS005
     C                   Z-ADD     0             TZIANP
      *                                                                                       CAS005
      ** Update the interest accrual control date to the end date                             CAS005
      ** used by GLINTC                                                                       CAS005
      *                                                                                       CAS005
     C                   Z-ADD     ZIEND         TZIACD
      *                                                                                       CAS005
     C                   ENDSR
      *****************************************************************                       CAS005
