      *****************************************************************
      *                                                               *
      *  Midas - /COPY Member of DL0280SREX                           *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 BUG6293            Date 04Oct04               *
      *                 BUG3012            Date 01Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  BUG6293- Move subroutine to a /COPY to free up space in      *
      *           DL0280 for other changes.                           *
      *  BUG3012- Account keys are not being output for multiple      *
      *           settlement accounts.                                *
      *                                                               *
      *****************************************************************
      *                                                               *                       CIR007
      *  SREXCP -    Write to Postings File                           *                       CIR007
      *                                                               *                       CIR007
      *  Called by:  Main Processing                                  *                       CIR007
      *                                                               *                       CIR007
      *  Calls:  None                                                 *                       CIR007
      *                                                               *                       CIR007
      *****************************************************************                       CIR007
      *                                                                                       CIR007
     C     SREXCP        BEGSR
      *                                                                                       CIR007
      ** Save field settings so that they can be restored later.                              CIR007
      *                                                                                       CIR007
     C                   Z-ADD     ACSQ          WACSQ             2 0
     C                   Z-ADD     REVI          WREVI             1 0
     C                   Z-ADD     EXRT          WEXRT            13 8
     C                   Z-ADD     SLID          WSLID             5 0
     C                   Z-ADD     MDAT          WMDAT             5 0
     C                   Z-ADD     BRTT          WWBRTT            2 0
     C                   Z-ADD     RTSP          WWRTSP           11 7
     C                   Z-ADD     BRTE          WWBRTE           11 7
     C                   Z-ADD     INTR          WINTR            11 7
     C                   Z-ADD     YRAT          WYRAT            11 7
     C**********         Z-ADD     CHNA          WCHNA             6 0                        CSD027
     C                   MOVEL     CHNA          WCHNA             6                          CSD027
     C                   MOVEL     FACOA         WFACOA           10
     C                   MOVE      PTFR          XPTFR             4
      *                                                                                       CIR007
      ** Test indicators for possible field setting changes.                                  CIR007
      *                                                                                       CIR007
     C                   MOVEL     'D'           LNRECI
      *                                                                                       CIR007
     C     *IN39         IFEQ      *ON
     C                   Z-ADD     2             ACSQ
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN47         IFEQ      *ON
     C                   Z-ADD     1             REVI
     C                   ELSE
     C                   Z-ADD     0             REVI
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN02         IFEQ      *ON
     C     *IN30         ANDEQ     *ON
     C                   Z-ADD     0             EXRT
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN02         IFEQ      *OFF
     C                   Z-ADD     0             EXRT
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN02         IFEQ      *ON
     C                   Z-ADD     0             SLID
     C                   Z-ADD     0             MDAT
     C                   Z-ADD     0             BRTT
     C                   Z-ADD     0             RTSP
     C                   Z-ADD     0             BRTE
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN04         IFEQ      *ON
     C     *IN28         ANDEQ     *ON
     C                   Z-ADD     INTR          YRAT
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN10         IFEQ      *ON
     C                   Z-ADD     FINTR         INTR
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN02         IFEQ      *ON
     C     *IN51         ANDEQ     *ON
     C                   Z-ADD     ICIR          INTR
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN03         IFEQ      *OFF
     C**********         Z-ADD     0             CHNA                                         CSD027
     C                   MOVE      *BLANK        CHNA                                         CSD027
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN05         IFEQ      *OFF
     C                   MOVEL     *BLANKS       FACOA
     C                   ENDIF
      *                                                                                       CIR007
     C     *IN04         IFEQ      *ON
     C     *IN05         OREQ      *ON
     C     *IN10         OREQ      *ON
     C                   MOVEL     *BLANKS       PTFR
     C                   ENDIF
      *                                                                                       CIR007
     C                   Z-ADD     WODAT         L3VDAT
     C                   Z-ADD     WODAT         KYDAT
     C                   Z-ADD     WODAT         KYFED
     C                   MOVE      'N'           L3PIND
     C                   MOVE      'N'           L3SIND
      *                                                                                       CIR007
     C                   MOVEL     AKY(1)        AKEY1
     C                   MOVEL     AKY(2)        AKEY2
     C                   MOVEL     AKY(3)        AKEY3
     C                   MOVEL     AKY(4)        AKEY4
     C                   MOVEL     AKY(5)        AKEY5
     C                   MOVEL     AKY(6)        AKEY6
     C                   MOVEL     AKY(7)        AKEY7
     C                   MOVEL     AKY(8)        AKEY8
     C                   MOVEL     AKY(9)        AKEY9
     C                   MOVEL     AKY(10)       AKEY0
      *                                                                                       CIR007
     C                   MOVE      KYCCY         L3YCCY
     C                   Z-ADD     KYSETL        L3SETL
     C                   MOVE      KYOSTL        L3OSTL
     C                   Z-ADD     SLID          L3SLID
     C                   Z-ADD     BRTT          L3BRTT
     C                   Z-ADD     BRTE          L3BRTE
     C                   Z-ADD     RTSP          L3RTSP
     C                   MOVE      KYSTBR        L3STBR
     C                   MOVE      KYSCY         L3YSCY
     C                   Z-ADD     EXRT          L3EXRT
     C                   MOVEA     AKY           L3AKEY                                       CDL038
     C                   MOVEL     DTYP          L3AKEY                                       CDL038
     C                   MOVE      CLAS          L3AKEY                                       CDL038
      *                                                                                       CIR007
      ** Create and write a new record to the OIS Posting File                                CIR007
      *                                                                                       CIR007
     C                   WRITE     DLOPOSD0
      *                                                                                       CIR007
      ** Restore field settings.                                                              CIR007
      *                                                                                       CIR007
     C                   Z-ADD     WACSQ         ACSQ
     C                   Z-ADD     WREVI         REVI
     C                   Z-ADD     WEXRT         EXRT
     C                   Z-ADD     WSLID         SLID
     C                   Z-ADD     WMDAT         MDAT
     C                   Z-ADD     WWBRTT        BRTT
     C                   Z-ADD     WWRTSP        RTSP
     C                   Z-ADD     WWBRTE        BRTE
     C                   Z-ADD     WINTR         INTR
     C                   Z-ADD     WYRAT         YRAT
     C**********         Z-ADD     WCHNA         CHNA                                         CSD027
     C                   MOVEL     WCHNA         CHNA                                         CSD027
     C                   MOVEL     WFACOA        FACOA
     C                   MOVE      XPTFR         PTFR
      *                                                                                       CIR007
     C                   ENDSR
