      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Bulk Short Term Amendments')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL000040DF - Bulk Short Term Rate Amendments                 *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL027  *CREATE    Date 10Feb05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL027 - Back-valued base rate changes                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
      *
      * -------------------------------------------------------------------*
      *
     A          R DL0040R1
     A                                      OVERLAY
     A                                      BLINK
      *
     A            #1USER        10A  O  1  2
     A            #1PGM         10A  O  1 14
     A            BJMRDT         7A  O  1 73
     A            #1WID         10A  O  2  2
     A            #1TITL        35A  O  2 24DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  7 16'Currency Code..:'
     A                                  8 16'Base Rate Code.:'
     A                                  9 16'Short Term Rate:'
     A                                 10 16'Value Date.....:'
      *
     A            #1CCY          3   B  7 35
     A       N21                            DSPATR(HI UL)
     A        21                            DSPATR(RI PC)
     A            #1CCYD        35   O  7 40
     A            #1BSRC         2   B  8 35
     A       N22                            DSPATR(HI UL)
     A        22                            DSPATR(RI PC)
     A            #1BSRN        30   O  8 40
     A            #1STRT        12   B  9 35
     A       N23                            DSPATR(HI UL)
     A        23                            DSPATR(RI PC)
     A            #1VDAT         6   B 10 35
     A       N24                            DSPATR(HI UL)
     A        24                            DSPATR(RI PC)
      *
     A                                 23  2'F3=Exit'
      *
      * -------------------------------------------------------------------*
      *
     A          R DL0040S1                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A            #2DLNO    R     A  O 13  2REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            #2DTYP    R        O 13 12REFFLD(DTPE)
     A                                      DSPATR(HI)
     A            #2OUTH         5   O 13 18TEXT('OUR/THEIR')
     A                                      DSPATR(HI)
     A            #2PAMT        17A  O 13 30TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A            #2BRCD         2   H
     A            #2LIFD         5  0H
     A            #2SIDE         1   H
      *
      * -------------------------------------------------------------------*
      *
     A          R DL0040C1                  SFLCTL(DL0040S1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      CF09
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      BLINK
     A            #1SFRN         4S 0H      SFLRCDNBR
      *
     A            #1USER        10A  O  1  2
     A            #1PGM         10A  O  1 14
     A            BJMRDT         7A  O  1 73
     A            #1WID         10A  O  2  2
     A            #1TITL        35A  O  2 24DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  9'Currency'
     A                                  4 21'Base Rate'
     A                                  4 35'Short Term'
     A                                  4 49'Value Date'
     A                                  5 11'Code'
     A                                  5 23'Code'
     A                                  5 39'Rate'
     A                                 10  2'Deal'
     A                                 10 11'Deal'
     A                                 10 18'Our/'
     A                                 10 36'Principal'
     A                                 11  2'Number'
     A                                 11 11'Type'
     A                                 11 18'Their'
     A                                 11 38'Amount'
      *
     A            #1CCY          3A  O  7 11DSPATR(HI)
     A            #1BSRC         2A  O  7 24DSPATR(HI)
     A            #1STRT        12A  O  7 35DSPATR(HI)
     A            #2VDAT         6Y 0O  7 50DSPATR(HI)
     A                                      EDTCDE(Y)
      *
      * -------------------------------------------------------------------*
      *
     A          R DL0040F1
     A N66                                  CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
      *
     A                                 23  2'F3=Exit'
     A N66                             23 13'F9=Apply Base Rate Change'
     A                                 23 42'F12=Cancel'
      *
      * ---------------------------------------------------------------
      *
     A          R DL0040MS                  SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      * ---------------------------------------------------------------
      *
     A          R DL0040MC                  SFLCTL(DL0040MS)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N94                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A            PGMQ                      SFLPGMQ
      *
      * ---------------------------------------------------------------
      *
     A          R DL0040MB
     A            BOTTOM         1     24  1
