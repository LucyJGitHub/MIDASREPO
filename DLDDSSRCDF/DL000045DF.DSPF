     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Historic Base Rate Amendments')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL000045DF - Bulk Short Term Rate Amendments                 *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CDL034  *CREATE    Date 10Feb05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL034 - Back-valued base rate changes                       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
     A*
     A* -------------------------------------------------------------------*
     A*
     A          R DL0045R1
     A                                      OVERLAY
     A                                      BLINK
     A*
     A            #1USER        10A  O  1  2
     A            #1PGM         10A  O  1 14
     A            BJMRDT         7A  O  1 73
     A            #1WID         10A  O  2  2
     A            #1TITL        35A  O  2 24DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A*
     A                                  7 16'Currency Code..:'
     A*
     A            #1CCY          3A  B  7 35
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A            #1CCYD        35A  O  7 40
     A                                  8 16'Base Rate Code.:'
     A            #1BSRC         2A  B  8 35
     A N22                                  DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A            #1BSRN        30A  O  8 40
     A                                  9 16'Base Rate......:'
     A            #1STRT        12A  B  9 35
     A N23                                  DSPATR(HI)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A                                 10 16'Value Date.....:'
     A            #1VDAT         6A  B 10 35
     A N24                                  DSPATR(HI)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A*
     A                                 23  2'F3=Exit'
     A*
     A* -------------------------------------------------------------------*
     A*
     A          R DL0045S1                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      SFLNXTCHG
     A            #2BRCD         2A  H
     A            #2PCPL        13S 0H
     A            #2RTSP        11S 7H
     A            #2ICBS         1S 0H
     A            #2DLNO    R     A  O 13  2REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            #2DTYP    R        O 13 12REFFLD(DTPE)
     A                                      DSPATR(HI)
     A            #2DLST         2A  O 13 18DSPATR(HI)
     A            #2SLID         6Y 0O 13 29DSPATR(HI)
     A                                      EDTCDE(Y)
     A            #2PAMT        17A  O 13 45TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A*
     A          R DL0045C1                  SFLCTL(DL0045S1)
     A                                      CF09
     A                                      CF12
     A N96                                  ROLLUP(98)
     A                                      BLINK
     A                                      OVERLAY
     A  95                                  SFLDSP
     A                                      SFLDSPCTL
     A  97                                  SFLCLR
     A  96                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A            #1SFRN         4S 0H      SFLRCDNBR
     A*
     A            #1USER        10A  O  1  2
     A            #1PGM         10A  O  1 14
     A            BJMRDT         7A  O  1 73
     A            #1WID         10A  O  2  2
     A            #1TITL        35A  O  2 24DSPATR(HI)
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A*
     A                                  4  9'Currency'
     A                                  4 21'Base Rate'
     A                                  4 35'Base Rate'
     A                                  4 49'Value Date'
     A                                  5 11'Code'
     A                                  5 23'Code'
     A*
     A            #1CCY          3A  O  7 11DSPATR(HI)
     A            #1BSRC         2A  O  7 24DSPATR(HI)
     A            #1STRT        12A  O  7 35DSPATR(HI)
     A            #2VDAT         6Y 0O  7 50DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                 10  2'Deal'
     A                                 10 11'Deal'
     A                                 10 18'Deal'
     A                                 10 29'Start/Last'
     A                                 10 50'Principal'
     A                                 11  2'Number'
     A                                 11 11'Type'
     A                                 11 18'Sub-Type'
     A                                 11 29'Interest Date'
     A                                 11 50'Amount'
      *
      * -------------------------------------------------------------------*
      *
     A          R DL0045F1
     A N66                                  CF09
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
      *
     A                                 23  2'F3=Exit'
     A N66                             23 13'F9=Apply Base Rate Change'
     A                                 23 42'F12=Cancel'
      *
      * ---------------------------------------------------------------
      *
     A          R DL0045MS                  SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      * ---------------------------------------------------------------
      *
     A          R DL0045MC                  SFLCTL(DL0045MS)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N94                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A            PGMQ                      SFLPGMQ
      *
      * ---------------------------------------------------------------
      *
     A          R DL0045MB
     A            BOTTOM         1     24  1
