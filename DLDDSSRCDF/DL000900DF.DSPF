      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL COB report prompts screen 2')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL000900DF - Dealing COB Report Prompts Screen 2             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 226165             Date 31Mar04               *
      *  Prev Amend No. CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  226165 - Limit UP/UL Generation in DL COB Prompt             *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
     A                                      CA05(05)
     A                                      CA12(12)
     A                                      MSGLOC(24)
      *
     A          R DL000900F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
      *
     A                                  1 13'CLOSE OF BUSINESS REPORTING -
     A                                      PARAMETERS PROMPT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 60TIME
     A            #0WID         10A  O  1 70
     A                                  2 18'DEALING MODULE - NPV-
     A                                       RE-VALUATIONS'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
      **Start of change 226165
     A**********                        7 18'FX/Interest Forward -                            226165
     A N30                              7 18'FX/Interest Forward -
      **End of change 226165
     A                                      Revaluation'
      **Start of change 226165
     A**********                        8 39'NPV Method -                                     226165
     A N30                              8 39'NPV Method -
      **End of change 226165
     A                                      (N,F,B,D, ,) :'
     A            #0NPVM         1A  B  8 65TEXT('FX/Int Fwd Rev NPV Method')
      **Start of change 226165
     A**20******                            DSPATR(RI PC)                                     226165
     A*N20******                            DSPATR(HI)                                        226165
     A  20N30                               DSPATR(RI PC)
     A N20N30                               DSPATR(HI)
     A  30                                  DSPATR(ND PR)
      **End of change 226165
     A                                      OVRATR
     A                                      OVRDTA
      *
     A                                  9 18'FX Base Currency Yield -
     A                                      Curve                 :'
     A            #0BCYC         5A  B  9 65TEXT('FX Base Ccy Yield Curve')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      **Start of change CAS005
     A  30                             13 18'FX/Interest Forward Revaluation +
     A                                      NPV Method for'
     A  30                             14 18'UP/UL A/c Generation'
     A  30                             15 39'NPV Method -
     A                                      (N,F,B,D, ,) :'
     A            #0NPV2         1A  B 15 65TEXT('FX/Int Fwd Rev NPV Method')
     A  30 31                               DSPATR(RI PC)
     A  30N31                               DSPATR(HI)
     A N30                                  DSPATR(ND PR)
     A                                      OVRATR
     A                                      OVRDTA
      **End of change CAS005
      *
     A                                 23  2'PRESS ENTER TO CONTINUE'
     A                                 23 28'F3=MAIN MENU'
     A                                 23 43'F5=Refresh'
     A                                 23 56'F12=Previous'
      *
     A          R DL000900F1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
     A          R DL000900F2                TEXT('Program messages')
     A                                      SFLCTL(DL000900F1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
