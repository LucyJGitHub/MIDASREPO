      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Fees activity enquiry - display')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/DL001150DF - DL Fee Activity Enquiry                    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(LEFEED)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R SFEEM1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R SFEEM0                    TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(SFEEM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *  LOAN FEES SUBFILE                                                 *
      * -------------------------------------------------------------------*
      *
     A          R SFEES1                    SFL
     A  91                                  SFLNXTCHG
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDDLNO         6   H
     A            DDOPT          1   B  9  4
     A  92                                  DSPATR(PC RI)
     A N93N92                               DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDRECI         1   O  9  9
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCUST         6   O  9 13
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFSEQ         2   O  9 22
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFCOD         2   O  9 27
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSCCY         3   O  9 31
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSAMT        14   O  9 37
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSIGN         1   O  9 52
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSDAT         7   O  9 56
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPTFI         1   O  9 67
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      *
      * -------------------------------------------------------------------*
      *   LOAN FEES SUBFILE CONTROL                                        *
      * -------------------------------------------------------------------*
     A
     A          R SFEES0                    SFLCTL(SFEES1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2
     A                                  1 13'DL001150'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 30'DEAL FEES ACTIVITY'
     A                                       DSPATR(UL HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
      *
     A                                  4  2 'Branch'
     A                                       DSPATR(HI)
     A            DDBRCA         3      4 10
     A                                  4 15'Deal Reference:'
     A                                       DSPATR(HI)
     A            DDDLNO         6      4 32
     A                                  4 39'-'
     A            DDCNUM         6      4 41
     A            DDCNM2        10      4 49
     A            DDCNAM        20      4 60
      *
     A                                  6 21'Fee'
     A                                       DSPATR(HI)
     A                                  6 26'Fee'
     A                                       DSPATR(HI)
     A                                  6 37'Last'
     A                                       DSPATR(HI)
     A                                  6 56'Settled/'
     A                                       DSPATR(HI)
     A                                  6 66'Fee'
     A                                       DSPATR(HI)
     A                                  7  8'Sts'
     A                                       DSPATR(HI UL)
     A                                  7 12'Customer'
     A                                       DSPATR(HI UL)
     A                                  7 21'Seq.'
     A                                       DSPATR(HI UL)
     A                                  7 26'Code'
     A                                       DSPATR(HI UL)
     A                                  7 31'Ccy'
     A                                       DSPATR(HI UL)
     A                                  7 37'Settled Amount'
     A                                       DSPATR(HI UL)
     A                                  7 56'Due Date'
     A                                       DSPATR(HI UL)
     A                                  7 66'Ind'
     A                                       DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      *  SELECT SCREEN FOOTER                                              *
      * -------------------------------------------------------------------*
      *
     A          R SFEEF1                    TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 21  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 22  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A N79
     AO 80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'E=Enquire  F3=Exit  F12=Previous'
      *
      * -------------------------------------------------------------------*
      *  LOAN FEE DETAIL SCREEN                                            *
      * -------------------------------------------------------------------*
      *
     A          R DETAIL                    CF12
     A                                      CF03
     A                                      CF14
      *
     A            DDUSER        10A  O  1  2
     A                                  1 13'DL001150'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 32'DEAL FEE DETAILS'
     A                                       DSPATR(UL HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
      *
     A                                  5  6'Customer Reference'
     A            DTCNUM         6      5 26
     A                                  6  6'Deal'
     A            DTDEAL         6      6 26
     A                                  6 38'Fee Sequence'
     A            DTFSEQ         2      6 55
     A            DTPTFN        10      6 66
     A                                  7  6'Branch'
     A            DTBRCA         3      7 26
     A                                  8  6'Fee Code'
     A            DTFCOD         2      8 26
     A                                  9  6'Fee Amount'
     A            DTFCCY         3      9 26
     A            DTAMNT        14      9 33
     A            DTSIGN         1      9 47
      *
     A                                 10  6'O/S Fee Amount'
     A            DTOAMT        14     10 33
     A            DTSGN2         1     10 47
      *
     A                                 11  6'Total Accrued to Date'
     A            DTTACR        14     11 33
     A            DTSGN3         1     11 47
      *
     A                                 12  6'Settled Amount'
     A            DTSAMT        14     12 33
     A            DTSGN4         1     12 47
      *
     A                                 13  6'Fee Period          Start'
     A            DTSDAT         7     13 32
     A                                 14 26'End'
     A            DTEDAT         7     14 32
      *
     A                                 16  6'Payment On'
     A            DTPYON         1     16 17
      *
     A                                 16 21'Next Payment Date'
     A            DTNPYD         7     16 40
      *
     A                                 16 50'Frequency'
     A            DTFREQ         1     16 61
      *
     A                                 16 64'Day in Month'
     A            DTDYIM         2     16 77
      *
     A                                 23 02'F3=Exit, F12=Previous, +
     A                                      F14=Settlements'
