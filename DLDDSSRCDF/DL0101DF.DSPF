      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Accrued Interest Adjustments Selection')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DL0101DF  - Midas DL Accrued Interest Adjustments Selection  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG7067            Date 13May05               *
      *  Prev Amend No. BUG6696            Date 25Apr05               *
      *                 CDL026 *Create     Date 11NOV04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * BUG7067 - F9 Fails from 'No Recs to Display' screen           *
      * BUG6696 - Allow more than one adjustment to the same deal on  *
      *           the same day.                                       *
      * CDL026- Authorisation on Dealing Accrued Interest Adjustments.*
      *                                                               *
      *****************************************************************
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Reset.')
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
      *
      ** Subfile Record Format
      *
     A          R DL0101S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A  94                                  SFLNXTCHG
     A            DDOPTN         1A  B  8  3TEXT('Option')
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI UL)
     A            DDDLNO         6A  O  8  7DSPATR(HI)
     A            DDCNUM         6A  O  8 15DSPATR(HI)
     A            DDMAAM        13A  O  8 27DSPATR(HI)
     A            DDSIGN         1A  O  8 43DSPATR(HI)
     A            DDCCY          3A  O  8 49DSPATR(HI)
     A            DDINRS         1A  O  8 54DSPATR(HI)
     A            DDSTAT        12A  O  8 58DSPATR(HI)
      *
      ** Start of BUG6696
     A            DDIPTM         6A  H
     A            DDRDNB         5A  H
      ** End of BUG6696
      *
      ** Subfile Control
      *
     A          R DL0101C0                  TEXT('Subfile control.')
     A                                      SFLCTL(DL0101S0)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF09(09 'Add mode.')
     A            @@RRN          4  0H      SFLRCDNBR
     A            DDUSR         10A  O  1  2OVRDTA
     A                                  1 13'DL0101DF'
     A                                  1 28'Accrued Interest Adjustments'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation Id')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A            DDTIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  7'Deal'
     A                                  4 15'Customer'
     A                                  4 27'Adustment'
     A                                  4 42'Sign'
     A                                  4 49'Ccy'
     A                                  4 53'O/T'
     A                                  4 58'Status'
      *
     A            DKDLNO         6   B  6  7TEXT('Deal')
     A N25                                  OVRDTA
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A            DKCNUM         6   B  6 15TEXT('Customer')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      ** Command Line
      *
     A          R DL0101F0                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY PUTOVR
     A                                 23  2'D=Delete'
     A                                 23 11'X=Authorise F3=Exit'
     A                                 23 31'F5=Refresh F9=Go to Add Mode'
      *
      ** No Record found
      *
     A          R DL0101NR                  TEXT('No Record found')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start BUG7067
     A                                      CF09(09 'Add mode.')
      ** End BUG7067
     A            DDUSR         10A  O  1  2OVRDTA
     A                                  1 13'DL0101DF'
     A                                  1 27'Accrued Interest Adjustments'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 23'NO UNAUTHORISED TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
      ** Start BUG7067
     A********                         23  2'Press enter to end this function'
     A********                         23 36'or F9=Go to Add Mode'
     A                                 23  2'F3=Exit   F9=Go to Add Mode'
      ** End BUG7067
      *
      ** Message Record Format
      *
     A          R DL0101S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            ##MSGK                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ** Message Control
      *
     A          R DL0101C1                  TEXT('Program messages')
     A                                      SFLCTL(DL0101S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGMQ                    SFLPGMQ
