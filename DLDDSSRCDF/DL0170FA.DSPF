      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas IRS Additional Interest Details')                *
      *****************************************************************
      *                                                               *
      *  Midas - FRA/IRS Dealing Module                               *
      *                                                               *
      *  DSPF/DL0170FA - Midas IRS Additional Interest Details        *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD030928           Date 25Nov14               *
      *                 AR859056 *CREATE   Date 10Apr13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD030928 - Repository ID field text incorrectly displayed in *
      *             additional details screen for IRS deals           *
      *  AR859056 - Additional interest details for IRS deals.        *
      *             (Child: AR859981)                                 *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA08
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      **Start of change MD030928
     A**********R DL0170F1                  TEXT('(c) Finastra International +              MD030928
     A          R DL0170FA1                 TEXT('(c) Finastra International +
      **End of change MD030928
     A                                      Limited +
     A                                      2013')
      *
     A                                      PUTOVR
      *
      *
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'IRSADI'
     A                                  1 28'Additional Interest +
     A                                       Details'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            STIME          6  0   2 73EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  8'Deal Number: '
     A            SDLNM          6      5 22DSPATR(HI)
     A                                      OVRDTA
     A                                  7  8'Our Details'
     A                                      DSPATR(UL)
     A                                  8  8'Currency'
     A            DUCCY          3      8 18DSPATR(HI)
     A                                  9  8'Our accrued interest +
     A                                       posted'
     A            DUAIP         21      9 50DSPATR(HI)
     A                                 10  8'Our accrued interest +
     A                                       not posted'
     A            DUANP         21     10 50DSPATR(HI)
     A                                 11  8'Our interest paid/ +
     A                                       received to date'
     A            DUIPR         21     11 50DSPATR(HI)
      *
     A                                 14  8'Their Details'
     A                                      DSPATR(UL)
     A                                 15  8'Currency'
     A            DTCCY          3     15 18DSPATR(HI)
     A                                 16  8'Their accrued interest +
     A                                       posted'
     A            DTAIP         21     16 50DSPATR(HI)
     A                                 17  8'Their accrued interest +
     A                                       not posted'
     A            DTANP         21     17 50DSPATR(HI)
     A                                 18  8'Their interest paid/ +
     A                                       received to date'
     A            DTIPR         21     18 50DSPATR(HI)
      *
     A                                 23  3'F3=End'
     A                                 23 11'F8=Next Screen'
     A                                 23 28'F12=Previous'
