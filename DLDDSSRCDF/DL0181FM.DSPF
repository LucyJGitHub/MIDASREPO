      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Deal amendments')                             *
      *===============================================================*
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0181FM - DEALS AMENDMENTS BY DEAL ENQUIRY                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 - 117162           Date - 26mAY0              *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 23Aug96               *
      *                 S01311             Date 07Feb91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  117162 - On The Deal Amendment Eqy Screen added text         *
      *         - 'Option 1=Select Details' as not previously showing.*
      *  CDL006 - Dealing Fiduciary.                                  *
      *  S01408 - Hook DL0181FM01 added                               *
      *         - Hook DL0181FM02 added                               *
      *         - Hook DL0181FM03 added                               *
      *         - Hook DL0181FM04 added                               *
      *  S01311 - Re-written For Exposure Management linked enquiry   *
      *                                                               *
      *===============================================================*
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A********************************************************************
     A*
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A  60                                  CF01
     A                                      CF03
     A  60                                  CF12
     A          R DL0181F1                  OVERLAY
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            USER          10A  O 01 02
     A                                 01 13'DL0181'
     A                                 01 29'DEAL AMENDMENTS/DEAL ENQUIRY'
     A                                      DSPATR(UL HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 71TIME
     A            MSG           79A  O 22 02
     A  10                                  DSPATR(RI HI)
     A N10                                  DSPATR(ND)
     A  60                             23 02'F1=Initial Enquiry'
     A N60                             23 02'F3=Exit'
     A  60                             23 23'F3=Exit'
     A  60                             23 33'F12=Previous'
     A                                 24 02'Or enter deal number'
     A            DEAL           6A  B 24 24DSPATR(PC)
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A          R DL0181F2                  OVERLAY
     A            USER          10A  O 01 02
     A                                 01 13'DL0181'
     A                                 01 29'DEAL AMENDMENTS/DEAL ENQUIRY'
     A                                      DSPATR(UL HI)
     A            BJMRDT         7A  O 01 72
     A            WSID          10A  O 02 02
     A                                 02 71TIME
     A                                 03 02'Deal No'
     A            SDEAL          6A  O 03 11
     A                                      DSPATR(HI)
     A                                 03 21'Type'
     A            SNARR         30A  O 03 27
     A                                      DSPATR(HI)
     A                                 04 04'Customer'
     A            SCNUM          6A  O 04 16
     A                                      DSPATR(HI)
     A            SBCRNM        20A  O 04 26
     A                                      DSPATR(HI)
     A            SBCRTN        10A  O 04 51
     A                                      DSPATR(HI)
      **Start of change CER043
     A            SBCSSN        10A  O 04 66TEXT('Customer Short Name')
     A  57                                  DSPATR(ND)
     A N57                                  DSPATR(HI)
      **End of change CER043
     A                                 05 04'Currency'
     A            SCCY           3A  O 05 16
     A                                      DSPATR(HI)
     A                                 05 26'Principal'
     A            SPCPL         17A  O 05 44
     A                                      DSPATR(HI)
     A                                 06 26'Base rate/spread'
     A            SBRTT          2A  O 06 44
     A                                      DSPATR(HI)
     A            SRTSP         12A  O 06 48
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,DL0181FM01                                          S01408
     A                                 07 02'Deal Amendments'
     A                                 08 04'Value Date  Seq  Type of amendmen-
     A                                      t'
     A                                 08 52'Principal   Base rate/spread'
      *                                              Begin /CDL006/
     A  56                             09 21'Charge Code'
     A  56                             09 47'Charge Amount'
      *                                              End   /CDL006/
     A*/COPY WNCPYSRC,DL0181FM02                                          S01408
      *
     A          R DL0181SF                  SFL
     A*/COPY WNCPYSRC,DL0181FM05
      *                                              Begin /CDL006/
     A*********** OPTION         1A  B 09 02DSPATR(UL HI)
     A*********** REVIND         1A  O 09 04
     A**50*******                           DSPATR(RI)
     A***********                           DSPATR(HI)
     A*********** ZADATE         7A  O 09 06DSPATR(HI)
     A            OPTION         1A  B 10 02DSPATR(UL HI)
     A  51                                  DSPATR(ND PR)
     A            REVIND         1A  O 10 04
     A  50                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            ZADATE         7A  O 10 06DSPATR(HI)
      *                                              End   /CDL006/
     A*/COPY WNCPYSRC,DL0181FM03                                          S01408
      *                                              Begin /CDL006/
     A*********** SDASN          3Y 0O 09 16DSPATR(HI)
     A*********** STABTP        23A  O 09 21DSPATR(HI)
     A*********** SDAAM         17A  O 09 45DSPATR(HI)
     A*********** BRTTS          2A  O 09 64DSPATR(HI)
     A*********** RTSPS         12A  O 09 68DSPATR(HI)
     A            SDASN          3Y 0O 10 16DSPATR(HI)
     A  51                                  DSPATR(ND)
     A            STABTP        23A  O 10 21DSPATR(HI)
     A            SDAAM         17A  O 10 45DSPATR(HI)
     A            BRTTS          2A  O 10 64DSPATR(HI)
     A            RTSPS         12A  O 10 68DSPATR(HI)
      *                                              End   /CDL006/
     A*/COPY WNCPYSRC,DL0181FM04                                          S01408
     A*
     A          R DL0181SC                  SFLCTL(DL0181SF)
     A                                      BLINK
     A  21                                  SFLDSPCTL
     A  22                                  SFLDSP
     A                                      SFLSIZ(12)
     A                                      SFLPAG(12)
     A N21N22                               SFLCLR
     A  23                                  SFLEND
     A                                      ROLLUP(24)
     A                                      ROLLDOWN(25)
     A                                      TEXT('SUBFILE CONTROL REC')
     A                                      OVERLAY
     A            RCDNBR         3  0H      SFLRCDNBR
     A            MSG           79A  O 22 02
     A  20                                  DSPATR(HI)
     A N20                                  DSPATR(ND)
     A  20                                  DSPATR(RI)
     A  60                             23 02'F1=Initial Enquiry'
     A N60                             23 02'F3=Exit'
     A  60                             23 23'F3=Exit'
     A  60                             23 33'F12=Previous'
      * ***/Start of 117162/***
     A N60                             23 15'Option 1=Select Details'
     A  60                             23 67'1=Sel Details'
      * ***/End of 117162/***
     A                                 23 48'Rollup'
     A                                 23 57'Rolldown'
     A                                 24 02'Or enter deal number'
     A            DEAL           6A  B 24 24
     A                                      DSPATR(UL HI)
     A                                 24 32'For new enquiry'
