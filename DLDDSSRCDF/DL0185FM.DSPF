      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('angas DL Deal amendments enquiry')                     *
     A********************************************************************
     A*                                                                  *
     A*  Midas - Dealing Module
     A*                                                                  *
     A*    DL0185FM - DEAL  AMENDMENTS ENQUIRY.                          *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG8057            Date 08Aug05               *
      *                 CDL022             Date 08Apr04               *
      *                 CDL020             Date 08Apr04               *
      *                 CDL017             Date 03Feb04               *
      *                 CAAA03             Date 22Apr04               *
      *                 CGL029             Date 19Apr04               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU003             Date 05Nov97               *
      *                 S01408             Date 23Aug96               *
     A*                 CAC001             DATE 01FEB96               *
     A*                 S01311             DATE 14FEB91               *
     A*                 S01253             DATE 17APR90               *
     A*                   E00044     DATE 14NOV89                        *
     A*                   S01117     DATE 15AUG89                        *
     A*                   S01199     DATE 11APR89                        *
     A*                                                                  *
     A********************************************************************
     A*
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  BUG8057 - Wrong Early Maturiy Date on Enquiry Screen         *
      *  CDL022 - Deal Amendment Changes                              *
      *  CDL020 - Apply Base Rate at Value Date                       *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *           Add Field 'Penalty Pay/Receive' on DL0185F1         *
      *           Add Field 'Early Maturity Date' on DL0185F1         *
      *           Add Field 'Base Rate code' on DL0185F1              *
      *  CAAA03 - Webfacing Changes.                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL006 - Dealing Fiduciary.                                  *
     A*  CEU003  - EMU Dealing Settlement Ccy Conversion              *
      *  S01408  - Hook DL0185FM01 added                              *
      *          - Hook DL0185FM02 added                              *
     A*  CAC001  -  Profit Centre Accounting Development:             *
     A*             Display new field 'Funding Rate/Spread' for       *
     A*             Spread Rate deal amendments if Analytical         *
     A*             Accounting is switched on.                        *
     A*      S01311 - Amended for Exposure Management linked Enquiry     *
     A*      S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS.           *
     A*      E00044 - POSITIONING OF CURSOR ON FIELD IN ERROR         *
     A*      S01117   - MULTIBRANCHING                                   *
     A*      S01199 - HELP SYSTEM.                                       *
     A*
     A********************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
     A********************************************************************
     A*/COPY WNCPYSRC,DLH00294
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A          R DL0185F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,DLH00295
     A                                      CLRL(24)
     A*/COPY WNCPYSRC,DLH00296
     A                                      CF03
      *                                                     /S01311/
     A  51                                  CF01
     A  51                                  CF12
      *                                                     /S01311/
     A            SCNID          2A  B 01 02DSPATR(PR ND)
     A                                 01 31'DEAL AMENDMENT ENQUIRY'
     A                                      DSPATR(UL HI)
     A            TID            8A  O 01 65
     A                                 01 74'WID'
     A            WSID           3A  O 01 78
     A                                 03 02'Deal no.'
     A  44                                  DSPATR(ND)
     A            DLNO           8A  O 03 11
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 04 04'Customer'
     A  44                                  DSPATR(ND)
     A            CNUM           6A  O 04 16
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            CRNM          20A  O 04 26
      **Start of change CER043
     A**44******                            DSPATR(ND)                                        CER043
     A**********                            DSPATR(HI)                                        CER043
     A  48 44                               DSPATR(ND)
     A N48N44                               DSPATR(HI)
      **End of change CER043
     A            CTWN          10A  O 04 51
      **Start of change CER043
     A**44******                            DSPATR(ND)                                        CER043
     A**********                            DSPATR(HI)                                        CER043
     A  48 44                               DSPATR(ND)
     A N48N44                               DSPATR(HI)
      **End of change CER043
     A                                 06 04'Value date'
     A  44                                  DSPATR(ND)
     A            ZADATE         7A  O 06 21
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              Begin /CDL006/
     A***********                      07 04'Sequence no'
     A N45                             07 04'Sequence no'
     A  44                                  DSPATR(ND)
     A  45                             06 32'Sequence no'
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
      *                                              Begin /CDL006/
     A*********** DASN           3A  O 07 21
     A N45        DASN           3A  O 07 21
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45        SDASN          3A  O 06 48
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              Begin /CDL006/
     A***********                      08 04'Type'
     A N45                             08 04'Type'
     A  44                                  DSPATR(ND)
     A  45                             07 04'Type'
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
      *                                              Begin /CDL006/
     A*********** AMTYPE        23A  O 08 21
     A N45        AMTYPE        23A  O 08 21
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45        SAMTYP        23A  O 07 21
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              Begin /CDL006/
     A***********                      09 04'Base rate'
     A N45                             09 04'Base rate'
     A  44                                  DSPATR(ND)
     A  45                             08 04'Base rate'
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
      *                                              Begin /CDL006/
     A*********** BRTT           2A  O 09 21
     A N45        BRTT           2A  O 09 21
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45        SBRTT          2A  O 08 21
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              Begin /CDL006/
     A***********                      10 04'Rate/spread'
     A N45                             10 04'Rate/spread'
     A  44                                  DSPATR(ND)
     A  45                             08 27'Rate/spread'
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
      *                                              Begin /CDL006/
     A*********** RTSP          12A  O 10 21
     A N45        RTSP          12A  O 10 21
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45        SRTSP         12A  O 08 43
      *                                              End   /CDL006/
     A*                                              Begin /CAC001/
     A**44*******                           DSPATR(ND)
     A  44
     AO 30 32                               DSPATR(ND)
     A*                                              End   /CAC001/
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,DL0185FM01                                          S01408
     A*                                              Begin /CAC001/
     A                                 10 40'Funding Rate/Spread'
     A  44
     AON30                                  DSPATR(ND)
     A            FSRP          12A  O 10 62
     A  44
     AON30
     AO 31                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*                                              End   /CAC001/
      *                                              Begin /CDL006/
     A***********                      11 04'Currency'
     A N45                             11 04'Currency'
     A  44                                  DSPATR(ND)
     A  45                             09 04'Currency'
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
      *                                              Begin /CDL006/
     A*********** CCY            3A  O 11 21
     A N45        CCY            3A  O 11 21
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45        SCCY           3A  O 09 21
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              Begin /CDL006/
     A***********                      12 04'Amount'
      *                                              Begin /CDL017/
     A N45N46                          12 04'Amount'
     A  44                                  DSPATR(ND)
     A N44N45 46                       12 04'Amount/Pnty Fee '
      *                                              End   /CDL017/
     A  45                             09 28'Amount'
      *                                              End   /CDL006/
     A  44                                  DSPATR(ND)
      *                                              Begin /CDL006/
     A*********** DAAM          17A  O 12 21
     A N45        DAAM          17A  O 12 21
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45        SDAAM         17A  O 09 37
      *                                              End   /CDL006/
     A*/COPY WNCPYSRC,DL0185FM02                                          S01408
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,DL0185FM03
      *                                              Begin /CDL006/
     A  45                             11 04'Fiduciary Charge Code'
     A  44                                  DSPATR(ND)
     A  45        SFCGC          2A  O 11 29
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45        SFCGD         20A  O 11 32
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                               Begin /CDL017/
     A N44N45 46                       11 41'Penalty Pay/Receive '
     A N44N45 46  DDPNTY         1A  O 11 63 DSPATR(HI)
     A  44                                   DSPATR(ND)
      *
     A N44N45 46                       12 41'Early Maturity Date '
      **Start of change BUG8057
     A*N44N45*46  DDEMDT         6A  O 12 63 DSPATR(HI)                                      BUG8057
     A N44N45 46  DDEMDT         7A  O 12 63 DSPATR(HI)
      **End of change BUG8057
     A  44                                   DSPATR(ND)
      *                                               End   /CDL017/
      *                                               Start /CDL022/
     A N44N45                          13 04'Base Rate Code'
     A N44N45     DDBSRT         2A  O 13 21 DSPATR(HI)
     A  44                                   DSPATR(ND)
      *                                               End   /CDL022*/
      *                                               Start /CDL020/
     A N44N45 47                       13 41'Actual Interest Rate'
     A N44N45 47  DDAIR         12A  O 13 63 DSPATR(HI)
     A  44                                   DSPATR(ND)
      *                                               End   /CDL020*/
     A  45                             12 12'Charge Amount'
     A  44                                  DSPATR(ND)
     A  45        SFCGA         17A  O 12 37
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A  45                             13 12'Tax on Charge Amount'
     A  44                                  DSPATR(ND)
     A  45        SFCGT         13A  O 13 41
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *                                              End   /CDL006/
     A                                 14 02'Settlement instructions'
     A  44                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A                                 15 10'-type'
     A  44                                  DSPATR(ND)
     A            SETP          20A  O 15 16
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*                                              Begin /CEU003/
     A                                 15 40'-currency'
     A  44
     AON08                                  DSPATR(ND)
     A            STCY           3A  O 15 51
     A  44
     AON08                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*                                              End   /CEU003/
     A                                 16 11'-our'
     A  44                                  DSPATR(ND)
      **Start of change CGL029
     A**********  OSAC          12A  O 16 16                                                  CGL029
     A            OSAC          18A  O 16 16
      **End of change CGL029
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A******                                               /S01117/
     A            OSBR           3A  O 16 30
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A******                                               /S01117/
     A                                 17 09'-their'
     A  44                                  DSPATR(ND)
     A            TSEN          10A  O 17 16
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 18 05'For a/c of'
     A  44                                  DSPATR(ND)
     A            FACO          10A  O 18 16
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 19 05'Special instructions'
     A  44                                  DSPATR(ND)
     A            SPI           35A  O 20 06
     A  44                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*                                              Begin /CEU003/
     A                                 21 05'''In'' currency in Field 72'
     A  44
     AON08                                  DSPATR(ND)
     A            IC72           3A  O 21 32
     A  44
     AON08                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A*                                               End  /CEU003/
     A            MSG           78A  O 22 02
     A  43                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI HI)                                     CAAA03
     A N43                                  DSPATR(RI)
     A                                      DSPATR(HI)
      ** End of Change CAAA03
      *                                                     /S01311/
     A***********                      23 02'Press F3 to end'
     A N51                             23 02'F3=Exit'
     A  51                             23 02'F1=Initial Enquiry'
     A  51                             23 23'F3=Exit'
     A  51                             23 33'F12=Previous'
      *                                                     /S01311/
     A                                 24 02'Or enter'
     A                                 24 11'Deal number'
     A            DLNUM          6A  B 24 23DSPATR(HI)
     A  20                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A                                 24 30', Value date'
     A            VDATE          6A  B 24 43DSPATR(HI)
     A**                                                   /*E00044/
     A**99                                  DSPATR(RI)
     A  99                                  DSPATR(RI PC)
     A**                                                   /*E00044/
     A N99                                  DSPATR(UL)
     A                                 24 50', and Sequence no.'
     A            DASN1          3A  B 24 69DSPATR(HI)
     A**                                                   /*E00044/
     A**22                                  DSPATR(RI)
     A  22                                  DSPATR(RI PC)
     A**                                                   /*E00044/
     A                                      CHECK(FE)
     A N22                                  DSPATR(UL)
      **Start of Change CER043
     A            DDCSSN        10A  O 04 66TEXT('Customer Short Name')
     A  48                                  DSPATR(ND)
     A N48                                  DSPATR(HI)
      **End of Change CER043
     A*/COPY WNCPYSRC,DLH00297
