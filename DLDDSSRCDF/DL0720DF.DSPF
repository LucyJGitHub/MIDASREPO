      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Deals due for Automatic Rollover Today')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0720DF - MM Deals due for Automatic Rollover Today         *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CDL016 *C *CREATE  Date 03Feb04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL016 - Automatic Rollover of Fixed Term Loans/Dep          *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    10  - Revaluation report selected for FRA                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'F3 To Exit')
      *
      * Record Format for titles and Input fields.
     A          R DL0720P1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      OVERLAY
      *
     A                                  1 26'Money Market Deals due for'
     A                                      DSPATR(UL HI)
     A                                  2 27'Automatic Rollover Today'
     A                                      DSPATR(UL HI)
      *
     A                                  4 33'REPORT PROMPT'
     A                                      DSPATR(UL HI)
      *
     A            USER          10A  O  1  2
     A                                  1 13'DL0720A'
     A            JOB           10A  O  2  2
      *
     A                                  1 67'DATE '
     A            DATE           7A  O  1 73
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
      *
     A                                  7  4'Select Money Market Deals +
     A                                      Report by:'
      *
     A                                 10 22'Deal Type'
     A            WDTYPE         2A  B 10 38
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
      *
     A                                 10 43'(Enter blanks for ALL)'
      *
     A                                 12 22'Deal SubType'
     A            WDSTYP         2A  B 12 38
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
      *
     A                                 12 43'(Enter blanks for ALL)'
      **Start of change CDL038
     A                                 14 22'Deal Class'
     A N65                                  DSPATR(ND)
     A            WDCLAS         4A  B 14 38
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A  65                                  DSPATR(UL)
     A N65                                  DSPATR(PR ND)
      *
     A                                 14 43'(Enter blanks for ALL)'
      **End of change CDL038
      *
     A                                 21  3'F3=Exit'
      *
      * Subfile File for Display of Error Msgs on Pgm Message Queue.
     A          R DL0720S0
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A
     A          R DL0720C0
     A                                      SFLCTL(DL0720S0)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N99                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
