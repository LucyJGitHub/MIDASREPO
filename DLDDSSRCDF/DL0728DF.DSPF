/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Fixed Deposit Rollover History')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module                                       *
     A*                                                               *
     A*  DSPF/DL0728DF - Fixed Deposit Rollover History Promt         *
     A*                                                               *
      *  (c) Finastra International Limited 2004                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL024  *CREATE    Date 31Mar04               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CDL024 - Fixed Deposits Register                             *
     A*                                                               *
     A*====================================================================
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A*
     A* Header format
     A*
     A          R DL0728F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'DL0728P'
     A                                  1 22'FIXED DEPOSIT ROLLOVER HISTORY +
     A                                      PROMT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A*
     A*=========================================================================
     A          R DL0728F1
     A                                      OVERLAY
     A                                  7  7'Deal Number..........:'
     A            #DLNO          6A  B  7 31
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(HI)
     A N41                                  DSPATR(UL)
     A                                  8  7'Rollover type (F/P/A).:'
     A            #TYPE          1A  B  8 31
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(HI)
     A N42                                  DSPATR(UL)
     A                                 23  2'F3=Exit'
      *
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A                                      TEXT('Message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  45                                  SFLEND
     A            ##PGM                     SFLPGMQ
