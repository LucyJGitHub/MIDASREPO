      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Prompt for Time/Call deposits statements')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/DL0769DF - Prompt for Time/Call Deposit Statements      *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG1988            Date 28Apr04               *
      *                 CDL012  *CREATE    Date 12Aug03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG1988- Correct conditioning indicator for SFLEND           *
      *  CDL012 - Statements for Time/Call Deposits                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03(03)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
     A          R DL0769F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      OVERLAY
     A                                      CF05(05)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'DL0769'
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 73TIME
      *
     A                                  2 25'Statements for Time/Call Deposits'
     A                                      DSPATR(HI UL)
      *
     A                                  6  3'Period'
     A                                  6 10'From......:'
     A            SPRDFR         6A  B  6 23
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                  8  3'Period'
     A                                  8 10'To........:'
     A            SPRDTO         6A  B  8 23
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                 10  3'Customer'
     A                                 10 12'Number..:'
     A            SCUSNO         6A  B 10 23
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                      DSPATR(UL)
      *
     A                                 23  2'F3=Exit   F5=Refresh'
      *
     A          R DL0769S1
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
     A          R DL0769C1                  SFLCTL(DL0769S1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
      *                                              Begin /BUG1988/
     A**15******                            SFLEND                                           BUG1988
     A N15
     AO 15                                  SFLEND
      *                                              End   /BUG1988/
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
