      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL OIS Compounding Interest Rate Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/DL1652DF - OIS Interest Rate Amendment Display File     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 226723             Date 12May04               *
      *                 220859             Date 10Sep03               *
      *                 214440             Date 30Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007  *CREATE    Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  226723 - Allow the specification of negative base rate for   *
      *           OIS deals.  Applied fix 219286.                     *
      *  220859 - Increase the size of Compounding Rate from          *
      *           7 to 15 decimal places.                             *
      *           Applied fix 220258.                                 *
      *  214440 - Add functionality to Amend Applicable Days field,   *
      *           and Insert and Delete records. (OIS)                *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT
     A                                      DSPSIZ(24 80 *DS3)
     A  06                                  CA06(06 'Confirm')
      **Start of change 214440
     A  10                                  CA10(10 'Delete')
      **End of change 214440
     A                                      CA12(12 'Previous')
     A                                      MSGLOC(24)
      *
------*-------------------------------------------------------------------------
      ***  Screen Header
------*-------------------------------------------------------------------------
      *
     A          R DL1652F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY
     A  86                                  PUTOVR
      *
     A            #0USER        10A  O  1  2
     A                                  1 13'DL1652DF'
      **Start of change 214440
     A**********                        1 24'OIS Compounding Interest +                       214440
     A**********                             Rate Amend'                                      214440
     A N30N31                           1 24'OIS Compounding Interest +
     A                                       Rate - Amend'
      **End of change 214440
     A                                      DSPATR(UL HI)
      **Start of change 214440
     A  30                              1 24'OIS Compounding Interest +
     A                                       Rate - Insert'
     A                                      DSPATR(UL HI)
     A  31                              1 24'OIS Compounding Interest +
     A                                       Rate - Delete'
     A                                      DSPATR(UL HI)
      **End of change 214440
     A                                  1 67'DATE '
     A            #0MRDT         7A  O  1 73
      *
     A            #0WSID        10A  O  2  2
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                  5 17'Deal        :'
     A            #0DLNO         6A  B  5 34
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
      *
     A                                  5 49'Side :'
     A            #0SIDE         1A  B  5 56OVRDTA
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
      *
      **Start of change 214440
     A  30                              8  6'NEW'
     A  31                              8  3'DELETE'
      **End of change 214440
     A                                  8 17'Record Date :'
     A            #0PDAT         6A  B  8 34
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 11 17'Rate Type   :'
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A            #0RTYP        13A  B 11 34
     A N60                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
      *
     A                                 14 17'New Rate    :'
     A  80
     AON60                                  DSPATR(ND)
     A                                      OVRATR
     A            #0NRAT        11A  B 14 34
     A  80
     AON60                                  DSPATR(ND)
     A N42 60N80                            DSPATR(UL)
      **Start of change 214440
     A**********                            DSPATR(HI)                                        214440
     A  42                                  DSPATR(HI)
      **End of change 214440
     A  80
     AON60
     AO 42                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A                                      OVRDTA
      *
      **Start of change 226723
      *
     A                                 14 46'+/-'
     A  80
     AON60                                  DSPATR(ND)
     A                                      OVRATR
     A            #0NSGN         1A  B 14 50
     A  80
     AON60                                  DSPATR(ND)
     A N47 60N80                            DSPATR(UL)
     A  47                                  DSPATR(HI)
     A  80
     AON60
     AO 47                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A                                      OVRDTA
      *
      **End of change 226723
      *
     A                                 15 17'New Rate    :'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
      *
     A*                                              Begin /220859/
     A**********  #0CRAT        13A  B 15 34
     A            #0CRAT        19A  B 15 34
      *                                              End   /220859/
     A N80                                  DSPATR(ND)
     A N44 80                               DSPATR(UL)
      **Start of change 214440
     A**********                            DSPATR(HI)                                        214440
     A  44                                  DSPATR(HI)
      **End of change 214440
     A N80
     AO 44                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A                                      OVRDTA
      *
      **Start of change 226723
      *
     A                                 15 46'+/-'
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A            #0CSGN         1A  B 15 50
     A N80                                  DSPATR(ND)
     A N48 80                               DSPATR(UL)
     A  48                                  DSPATR(HI)
     A N80
     AO 48                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A                                      OVRDTA
      *
      **End of change 226723
      *
      **Start of change 214440
     A  60
     AO 80                             17 13'Applicable Days :'
     A            #0APDY         3A  B 17 34
     A N60N80                               DSPATR(ND)
     A  45                                  DSPATR(HI)
     A N45 60
     AON45 80                               DSPATR(UL)
     A N60N80
     AO 45                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A                                      OVRDTA
      **End of change 214440
      *
------*-------------------------------------------------------------------------
      ***  FOOTER     Allowed Function Keys
------*-------------------------------------------------------------------------
      *
     A          R DL1652F1
      *
     A                                 23  2'F12=Previous'
     A  06                             23 16'F6=Confirm Update'
      **Start of change 214440
     A  10                             23 16'F10=Confirm Delete'
      **End of change 214440
      *
------*-------------------------------------------------------------------------
      ***  Message Subfile
------*-------------------------------------------------------------------------
      *
     A          R DL1652S0                  TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
------*-------------------------------------------------------------------------
      ***  Message Subfile  Control
------*-------------------------------------------------------------------------
      *
     A          R DL1652C0                  TEXT('Program messages')
      *
     A                                      SFLCTL(DL1652S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
      *
