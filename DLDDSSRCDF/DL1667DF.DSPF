      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL OIS Deal Bulk Rate Correction')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/DL1667DF - OIS Deal Bulk Rate Correction                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 226723             Date 12May04               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CIR007 *CREATE     Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  226723 - Allow the insertion/amendment of negative base rate *
      *           for OIS deals.  Applied fix 219286.                 *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A N13                                  CA05
     A  13                                  CA06
      *
     A          R DL1667F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
     A  86                                  PUTOVR
      *
     A            #0USER        10A  O  1  2TEXT('User Id')
     A                                  1 13'DL1667DF'
     A                                  1 28'M I D A S  Banking System'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUND         7A  O  1 73
     A            #0WSID        10A  O  2  2TEXT('Workstation Id')
     A                                  2 26'OIS Deal Bulk Rate Correction'
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  7'This function will change the Over-
     A                                      night Index Swaps Interest Rate'
     A                                  5  7'Table for all Overnight Index Swap-
     A                                       deals denominated in the currency'
     A                                  6  7'specified below.'
     A                                  8  7'The Base Rate on the RECORD with +
     A                                      the date and base rate code +
     A                                      specified'
     A                                  9  7'(ie not the Base Rate for the +
     A                                      date and base rate code specified) +
     A                                      will be'
     A                                 10  7'updated for each deal, and the +
     A                                      Interest Rate and Compounding Rate +
     A                                      on'
     A                                 11  7'each succeeding record for each +
     A                                      deal will also be updated.'
     A                                 13  7'For Overnight Index Swap deals set-
     A                                      tling today, where the rate has '
     A                                 14  7'already been'
     A                                 17 20'Base Rate Currency'
     A            #0BRTC         3A  B 17 40OVRDTA
     A N42                                  DSPATR(UL)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  13                                  DSPATR(PR)
     A                                 18 20'Record Date   '
     A            #0BRTD         6A  B 18 40OVRDTA
     A N43                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  13                                  DSPATR(PR)
     A                                 19 20'Base Rate Code'
     A            #0BRCO         2A  B 19 40OVRDTA
     A N45                                  DSPATR(UL)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  13                                  DSPATR(PR)
      *
      **Start of change 226723
      *
     A                                 20 52'+/-'
     A            #0SIGN         1A  B 20 56OVRDTA
     A N48                                  DSPATR(UL)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A  13                                  DSPATR(PR)
      *
      **End of change 226723
      *
     A                                 20 20'New Base Rate'
     A            #0NBRT        11A  B 20 40OVRDTA
     A N44                                  DSPATR(UL)
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  13                                  DSPATR(PR)
     A N13                             22  3'F3=Exit  F5=Refresh  F12=Previous'
     A  13                             22  3'F3=Exit  F6=Confirm Update   +
     A                                      F12=Previous'
     A                                 10 45'each'
     A                                 10 50'succeeding'
     A                                 10 61'record'
     A                                 10 68'for'
     A                                 11  7'each'
     A                                 11 12'deal'
     A                                 11 17'will'
     A                                 11 22'also'
     A                                 11 27'be'
     A                                 11 30'updated.'
     A                                 14 20'fixed,'
     A                                 14 27'the'
     A                                 14 31'net'
     A                                 14 35'settlement'
     A                                 14 46'amount'
     A                                 14 53'will'
     A                                 14 58'be'
     A                                 14 61'recalculated'
     A                                 15  7'and'
     A                                 15 11'updated.'
      *
     A          R DL1667S0                  TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R DL1667C0                  TEXT('Program messages')
      *
     A                                      SFLCTL(DL1667S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
      *
