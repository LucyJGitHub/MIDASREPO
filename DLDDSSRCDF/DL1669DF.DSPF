      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL OIS Deal Settlement Control Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/DL1669DF - OIS Deal Settlement Control Display File.    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CIR007 *CREATE     Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Initial Screen')
     A                                      CF05(05 'Refresh')
      *
     A          R DL1669F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'DL1669DF'
     A                                  1 28'OIS Deal Settlement Control'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R DL1669F1                  OVERLAY
      *
     A                                  3 34'Currency'
     A            #0CCY          3A  B  3 43
     A                                      DSPATR(UL)
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A                                 23  2'F3=Exit   F5=Refresh'
     A                                 23 24'F12=Previous'
      *
     A          R DL1669F2                  OVERLAY
      *
     A                                  3 34'Currency'
     A            #0CCY          3A  O  3 43
      *
     A          R DL1669F3                  OVERLAY
      *
     A                                 10 24'***   NO DETAILS TO DISPLAY   ***'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Initial Screen'
      *
     A          R DL1669S0                  SFL
      *
     A            #0SEL          1   B  8  2
     A  02                                  DSPATR(RI)
     A N02                                  DSPATR(HI)
     A            #0SERR         1   H
     A            #0DLNO         6   O  8  4DSPATR(HI)
     A            #0CNAM        20   O  8 13DSPATR(HI)
     A            #0BRCH         3   O  8 37DSPATR(HI)
     A            #0CCYS         3   O  8 44DSPATR(HI)
     A            #0WARN         1   H
     A            #0RFIN         1   H
      *
     A          R DL1669C0                  SFLCTL(DL1669S0)
      *
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0010)
     A  33                                  CF13(13 'Select All')
     A                                      OVERLAY
     A N75                                  SFLDSP
     A N75                                  SFLDSPCTL
     A  75                                  SFLCLR
     A  40                                  SFLEND
      *
     A            #0RRN          4S 0H      SFLRCDNBR(CURSOR)
     A                                  5  4'Deal'
     A                                  5 13'Customer'
     A                                  5 37'Branch'
     A                                  5 44'Currency'
     A                                  6  4'Number'
     A                                      DSPATR(UL)
     A                                  6 13'Name'
     A                                      DSPATR(UL)
     A                                  6 37'Code'
     A                                      DSPATR(UL)
     A                                  6 44'Code'
     A                                      DSPATR(UL)
      *
     A          R DL1669S1                  SFL
      *
     A            #1SEL          1   H
     A            #1SERR         1   H
     A            #1DLNO         6   O  8  4DSPATR(HI)
     A            #1CNAM        20   O  8 13DSPATR(HI)
     A            #1BRCH         3   O  8 37DSPATR(HI)
     A            #1CCYS         3   O  8 44DSPATR(HI)
     A            #1WARN         1   H
     A            #1RFIN         1   H
      *
     A          R DL1669C1                  SFLCTL(DL1669S1)
      *
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A N75                                  SFLDSP
     A N75                                  SFLDSPCTL
     A  75                                  SFLCLR
     A  40                                  SFLEND
      *
     A                                  3  2'Press Enter to confirm your +
     A                                      choices'
     A                                      DSPATR(HI)
     A                                  5  4'Deal'
     A                                  5 13'Customer'
     A                                  5 37'Branch'
     A                                  5 44'Currency'
     A                                  6  4'Number'
     A                                      DSPATR(UL)
     A                                  6 13'Name'
     A                                      DSPATR(UL)
     A                                  6 37'Code'
     A                                      DSPATR(UL)
     A                                  6 44'Code'
     A                                      DSPATR(UL)
      *
     A          R DL1669F4                  OVERLAY
      *
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Initial Screen'
     A  33                             23 34'F13=Select All'
     A                                 23 52'X=Select'
     A                                 23 63'Rollup/Rolldown'
      *
     A          R DL1669F5                  OVERLAY
      *
     A                                 23  2'F12=Cancel'
      *
     A          R DL1669S2                  TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            #2MSGK                    SFLMSGKEY
     A            #2PGM                     SFLPGMQ
      *
     A          R DL1669C2                  TEXT('Program messages')
      *
     A                                      SFLCTL(DL1669S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #2PGM                     SFLPGMQ
      *
