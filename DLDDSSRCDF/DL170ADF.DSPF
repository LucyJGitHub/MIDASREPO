      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER MM Loans/Deposit/ N/A Purchased Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL170ADF - Loans/Deposit NA Purchased Screen                 *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
     A                                      CF12
      *-------------------------------------------------------------------------
     A          R DL170AF1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  2 24'European Reporting - Luxembourg :'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  5  2'Lump-Sum Code.................... -
     A                                      :'
     A                                      OVRATR
     A            #0LUSU         2Y 0B  5 38
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  44N15                               DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  5 66'         '
     A                                      OVRATR
     A                                  5 79' '
     A                                      OVRATR
     A                                  6  2'LMI Circular 96 / 127............ -
     A                                      :'
     A                                      OVRATR
     A            #0IM93         1Y 0B  6 38OVRDTA
     A                                      OVRATR
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  35N15                               DSPATR(PC)
     A                                  7  2'Claims Type...................... -
     A                                      :'
     A            #0CLAT         1Y 0B  7 38OVRDTA
     A                                      OVRATR
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  36N15                               DSPATR(PC)
     A                                  7 65'          '
     A                                      OVRATR
     A                                  7 79' '
     A                                      OVRATR
     A                                  8  2'Correction Amount............ :'
     A                                      OVRATR
     A            #0COAM        15A  B  8 38OVRDTA
     A                                      OVRATR
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  48N15                               DSPATR(PC)
     A                                  8 79' '
     A                                      OVRATR
     A                                  9  2'Subordinate...................... -
     A                                      :'
     A                                      OVRATR
     A            #0SUBR         1A  B  9 38OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                  9 65'          '
     A                                      OVRATR
     A                                  9 79' '
     A                                      OVRATR
     A                                 10  2'Mobilisable Customer............. -
     A                                      :'
     A                                      OVRATR
     A            #0MOBL        10A  B 10 38OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A                                 10 74' '
     A                                      OVRATR
     A                                 10 79' '
     A                                      OVRATR
     A                                 11  2'Risk Code........................ -
     A                                      :'
     A                                      OVRATR
     A            #0RISC         2Y 0B 11 38OVRDTA
     A                                      OVRATR
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  25N15                               DSPATR(PC)
     A                                 11 66'         '
     A                                      OVRATR
     A                                 11 79' '
     A                                      OVRATR
     A                                 12  2'Rating Office Code............... -
     A                                      :'
     A                                      OVRATR
     A            #0ROFC         3A  B 12 38OVRDTA
     A                                      OVRATR
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  26N15                               DSPATR(PC)
     A                                 12 67'        '
     A                                      OVRATR
     A                                 12 79' '
     A                                      OVRATR
     A                                 13  2'Rating Code...................... -
     A                                      :'
     A                                      OVRATR
     A            #0RATC        12A  B 13 38OVRDTA
     A                                      OVRATR
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  27N15                               DSPATR(PC)
     A                                 13 79' '
     A                                      OVRATR
     A                                 14  2'Quoted/not Quoted................ -
     A                                      :'
     A                                      OVRATR
     A            #0SECQ         1A  B 14 38OVRDTA
     A                                      OVRATR
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  28N15                               DSPATR(PC)
     A                                 14 65'          '
     A                                      OVRATR
     A                                 14 79' '
     A                                      OVRATR
     A                                 15  2'Security Type.................... -
     A                                      :'
     A                                      OVRATR
     A            #0SECT         2Y 0B 15 38OVRDTA
     A                                      OVRATR
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  29N15                               DSPATR(PC)
     A                                 15 66'         '
     A                                      OVRATR
     A                                 15 79' '
     A                                      OVRATR
     A                                 16  2'Consumer Credit Code............. -
     A                                      :'
     A                                      OVRATR
     A            #1GUAC         1Y 0B 16 38OVRDTA
     A                                      OVRATR
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  30N15                               DSPATR(PC)
     A                                 16 65'          '
     A                                      OVRATR
     A                                 16 79' '
     A                                      OVRATR
     A                                 17  2'Evaluation method ............... -
     A                                      :'
     A                                      OVRATR
     A            #0EVMT         1Y 0B 17 38OVRDTA
     A                                      OVRATR
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  33N15                               DSPATR(PC)
     A                                 17 65'          '
     A                                      OVRATR
     A                                 17 79' '
     A                                      OVRATR
     A                                 18  2'Operation Type................... -
     A                                      :'
     A                                      OVRATR
     A            #0OPTP         3Y 0B 18 38OVRDTA
     A                                      OVRATR
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  34N15                               DSPATR(PC)
     A                                 23  1'F12=Previous'
      *-------------------------------------------------------------------------
     A          R DL170AF2
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  2 24'European Reporting - Luxembourg :'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  5  2'Lump-Sum Code.....................-
     A                                      . :'
     A                                      OVRATR
     A            #0LUSU         2Y 0B  5 40
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  44N15                               DSPATR(PC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  6  2'LMI Circular 96 / 127.............-
     A                                      . :'
     A                                      OVRATR
     A            #0IM93         1Y 0B  6 40OVRDTA
     A                                      OVRATR
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  33N15                               DSPATR(PC)
     A                                  7  2'Claims Type.......................-
     A                                      . :'
     A                                      OVRATR
     A            #0CLAT         1Y 0B  7 40OVRDTA
     A                                      OVRATR
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  34N15                               DSPATR(PC)
     A                                  8  2'Correction Amount............. :'
     A                                      OVRATR
     A            #0COAM        15A  B  8 35OVRDTA
     A                                      OVRATR
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  32N15                               DSPATR(PC)
     A                                  9  2'Subordinate.......................-
     A                                      . :'
     A                                      OVRATR
     A            #0SUBR         1A  B  9 40OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                 10  2'Mobilisable Customer..............-
     A                                      . :'
     A                                      OVRATR
     A            #0MOBL        10A  B 10 40OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A                                 11  2'Risk Code.........................-
     A                                      . :'
     A                                      OVRATR
     A            #0RISC         2Y 0B 11 40OVRDTA
     A                                      OVRATR
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  25N15                               DSPATR(PC)
     A                                 12  2'Consumer Credit Code..............-
     A                                      . :'
     A                                      OVRATR
     A            #1GUAC         1Y 0B 12 40OVRDTA
     A                                      OVRATR
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  28N15                               DSPATR(PC)
     A                                 13  2'Import / Export Code..............-
     A                                      . :'
     A                                      OVRATR
     A            #0IMEX         1Y 0B 13 40OVRDTA
     A                                      OVRATR
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  31N15                               DSPATR(PC)
     A                                 14  2'Property Sector Code..............-
     A                                      . :'
     A                                      OVRATR
     A            #0PPSC         6Y 0B 14 40OVRDTA
     A                                      OVRATR
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  26N15                               DSPATR(PC)
     A                                 23  1'F12=Previous'
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
