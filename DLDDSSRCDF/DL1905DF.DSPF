      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL FRA book positions enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module
     A*                                                               *
     A* DSPF/DL1905DF - FRA BOOK POSITIONS ENQUIRY                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 29Mar04               *
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CEU012             Date 16Feb98               *
     A*                 112563             Date 29Jan97               *
     A*                 104199             Date 27Jun96               *
     A*                 046114             DATE 14JUL93               *
     A*                 E42616             DATE 09SEP92               *
     A*                 S01253             DATE 17APR90               *
     A*                  S01199/E22695          DATE 19MAR90          *   S01199
     A*                  S01117                 DATE 08FEB90          *   S01117
     A*                  S01203                 DATE 06/04/89         *
     A*                  S01157                 DATE 15/06/88         *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CAAA03 - Webfacing Changes                                   *
      *         - Move the Fxx keys display to position 23 for proper *
      *           display. Prompts were also adjusted to be higher    *
      *           So that it will not overwrite the Fxx keys display. *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           Cause Web page to redisplay frequently and the RPG  *
      *           will accumulate the total elapsed time and exit     *
      *           when this matches the time from the OVRDSPF         *
     A*  CEU012 - Position/Risk Enquiries                             *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  104199 - Add program name to DSPF for GUI identification     *
     A*  046114 - FRA book position enquiry - F12 takes you to        *
     A*           another enquiry screen.                             *
     A*  E42616  -  FRA BOOK POSITIONS ENQUIRY - MONTH NAME ENTRY.    *
     A*             THE ENQUIRY ON FRA BOOK POSITIONS ASKS FOR THE    *
     A*             DATE TO BE INPUT IN FORMAT 'JUN0791' INSTEAD OF   *
     A*             '060791'. THIS DOES NOT MEET THE STANDARDS FOR    *
     A*             THE REST OF THE SYSTEM.                           *
     A*   S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS             *
     A*   S01199/E22695  -  HELP SYSTEM.  (E22695 WAS APPLIED FOR *       S01199
     A*                     R2 TO THIS FILE AT THE SAME TIME AS       *   S01199
     A*                     S01199 WAS APPLIED FOR R10 - THEREFORE    *   S01199
     A*                     THEY MAY NOT BE INDENTICAL CHANGES -      *   S01199
     A*                     FOR INSTANCE, S01199 SAW NO NEED TO ADD   *   S01199
     A*                     FORMAT DL1905C2).                         *   S01199
     A*   S01117  -  MULTI-BRANCHING CHANGES                          *   S01117
     A*   S01203  -  INCORPORATION CHANGES                            *
     A*   S01157  -  NEW DISPLAY FILE WRITTEN FOR FRA/IRS             *
     A*                                                               *
     A*****************************************************************
     A*
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A*
      *****************************************************************
      *                                                               *
      * INDICATORS TO BE RECEIVED FROM THE SCREEN - COMMANDS          *
      *                                                               *
      *     03   -  EXIT PROGRAM                                      *
      *     05   -  REFRESH SCREEN                                    *
      *     07   -  Toggles between 'Display Euro Equivalent'         *   CEU012
      *             and 'Display Ccy Amount'                          *   CEU012
      ******10***-**HELP KEY HAS BEEN PRESSED                         *   S01199
      *     12   -  RETURN TO PREVIOUS SCREEN                         *
      *     25   -  ROLLUP REQUESTED                                  *
      *     26   -  ROLLDOWN REQUESTED                                *
      *                                                               *
      *     27   -  INHIBIT ROLL/REFRESH/SELECT KEYS & TEXT/ENQ.TITLES*
      *     28   -  ACTIVATE THE OVRDTA KEYWORD FOR KEYS/TEXT & TITLES*
      *     29   -  ACTIVATE THE OVRATR KEYWORD FOR THE SELECT FIELDS *
      *     24   -  ACTIVATE THE OVRDTA KEYWORD FOR THE SELECT FIELDS *
      *                                                               *
      *     31   -  Position/Risk Enquiries & Reports Feature Ind     *   CEU012
      *     32   -  Display Euro Equivalent Indicator                 *   CEU012
      * INDICATORS TO CONTROL THE ENQUIRY SUBFILE                     *
      *                                                               *
      *     30   -  ACTIVATE SUBFILE END                              *
      *     35   -  ACTIVATE SUBFILE DISPLAY                          *
      *     36   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
      *     (N35N36   -  ACTIVATE SUBFILE CLEAR)                      *
      *     50   -  MBIN = 'N'; NON-DISPLAY OF BRANCH FIELD & TEXT    *
      *                                                               *
      * INDICATORS TO CONTROL THE MESSAGE SUBFILE                     *
      *                                                               *
      *     40   -  ACTIVATE SUBFILE END                              *
      *     46   -  ACTIVATE SUBFILE DISPLAY CONTROL                  *
      *                                                               *
      * INDICATORS TO REPORT ERRORS ON VALIDATION OF PROMPT SCREEN    *
      *                                                               *
      *     60   -  ERROR ON CURRENCY SELECTED                        *
      *     61   -  ERROR ON BRANCH CODE SELECTED                     *
      *     62   -  ERROR ON VALUE DATE SELECTED                      *
      *     63   -  ERROR ON BOOK CODE SELECTED                       *
      *     68   -  NO RECORD ON FILE FOR GIVEN PROMPTS               *
      *     69   -  GENERAL ERROR ON VALIDATION OF PROMPT SCREEN      *
      *                                                               *
      *****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A**********                            CA01(01 'Cmd 1 to Exit')
     A*N27******                            CA02(02 'Cmd 2 to Return')
     A*N27******                            CA05(05 'Cmd 5 to Refresh')
     A                                      CA03(03 'F3 to Exit')
     A N27                                  CA12(12 'F12 to Return')
     A N27                                  CA05(05 'F5 to Refresh')
      *                                              Begin /CEU012/
     A  31                                  CA07(07 'F7 to disp In/Euro Equiv')
      *                                              End   /CEU012/
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*
     A*
     A          R DL1905F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A* Main Title always displayed. Time updated when new Enquiry
     A* Subfile displayed. Book Code etc. only displayed when Enquiry
     A* Subfile displayed.
     A*
     A*                                                    /*S01117*/
     A*********************             1  2'FRA DEALING POSITIONS'
     A                                  1  2'FRA BOOK POSITIONS'
     A*                                                    /*S01117*/
     A                                      DSPATR(UL HI)
     A*                                              Start /*104199*/
     A********************              1 26'BOOK:'
     A**27****************                  DSPATR(ND)
     A**28****************                  OVRATR
     A********************
     A************@TBKCD**       2   O  1 32TEXT('Book Code')
     A********************                  DSPATR(HI)
     A**27****************                  DSPATR(ND)
     A**28****************                  OVRATR
     A********************                  OVRDTA
     A********************
     A************@TCRCY**       3   O  1 41TEXT('Currency')
     A********************                  DSPATR(HI)
     A**27****************                  DSPATR(ND)
     A**28****************                  OVRATR
     A********************                  OVRDTA
     A*
     A                                  1 23'DL1905'
     A**
     A                                  1 32'BOOK:'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A*
     A            @TBKCD         2   O  1 38TEXT('Book Code')
     A                                      DSPATR(HI)
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                      OVRDTA
     A*
     A            @TCRCY         3   O  1 43TEXT('Currency')
     A                                      DSPATR(HI)
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                      OVRDTA
     A*                                               End  /* 104199 */
     A            @TDATE         7   O  1 51TEXT('Todays Date')
     A                                      OVRDTA
     A            @TTIME         6  0O  1 60TEXT('Time')
     A                                      EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A*
     A                                  1 70'WID:'
     A            @TWID          3   O  1 75TEXT('Workstation Id')
     A*
     A*                                                    /*S01117*/
     A*********************             2  2'BRANCH:'
     A                                  2  2'BOOKING BRANCH:'
     A*                                                    /*S01117*/
     A  50
     AO 27                                  DSPATR(ND)
     A  28                                  OVRATR
     A*                                                    /*S01117*/
     A************@TBRCH***      3   O  2 10TEXT('Branch Code')
     A            @TBRCH         3   O  2 18TEXT('Branch Code')
     A*                                                    /*S01117*/
     A                                      DSPATR(HI)
     A  50
     AO 27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                      OVRDTA
     A*
     A*
     A*
     A* Subfile Titles - only shown when Enquiry Subfile output and
     A* once displayed never re-displayed (*IN28 is turned off)
     A* - except when program recalled.
     A*
     A                                  3  5'Value'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 15'Tenor'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 21'Base'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 26'Net(000`s)'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 37'Average'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 47'Market'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 55'Open'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 64'Closed'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A                                  3 72'Maturity'
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
     A*
     A*
     A          R DL1905F1                  TEXT('Command Keys Text')
     A                                      OVERLAY
     A*
     A**Text*-*Cmd*2/Roll/Refresh*only*allowed*with*enquiry*subfile*/* S01203 */
     A* Text - F12/Roll/Refresh only allowed with enquiry subfile   /* S01203 */
     A*
     A*                                                             /* S01203 */
      *                                              Begin /CEU012/
      ** Start of change CAAA03
     A**31N32***                       20  2'F7=Display Euro Equivalent'                      CAAA03
     A  31N32                          23  2'F7=Display Euro Equivalent'
     A**31*32***                       20  2'F7=Display Ccy Amount     '                      CAAA03
     A  31 32                          23  2'F7=Display Ccy Amount     '
     A**31*32***                       20 55'EURO EQUIVALENT'                                 CAAA03
     A  31 32                          23 55'EURO EQUIVALENT'
      ** End of change CAAA03
     A  31 32                               DSPATR(HI)
      *                                              End   /CEU012/
     A**27*****                        21  2'Cmd/1-end or Help'
     A*N27*****                        21  2'Cmd/1-end, Cmd/2-previous screen,'
     A*N27*****                        21 36'Cmd/5-refresh,'
     A**                                             Begin /112563/
     A* 27 ******                      21  2'F3-end or Help'
      ** Start of change CAAA03
     A**27******                       21  2'F3=Exit     or Help'                             CAAA03
     A  27                             22  2'F3=Exit'
      ** End of change CAAA03
     A**                                             End   /112563/
     A*                                                    /*S01117*/
     A*N27*****************            21  2'F3-end, F12-previous      screen,'
     A**********             Start of change               /046114/
     A*N27******                       21  2'F3-end, F12-previous screen,'
     A**                                             Begin /112563/
     A*N27 ******                      21  2'F3-end, F12-further summary,'
      ** Start of change CAAA03
     A*N27******                       21  2'F3=Exit  F12=Further Summary'                    CAAA03
     A N27                             22  2'F3=Exit  F12=Further Summary'
      ** End of change CAAA03
     A**                                             End   /112563/
     A*                      End of change                 /046114/
     A*N27*****************            21 36'F5-refresh,'
     A**                                             Begin /112563/
     A*N27 ******                      21 31'F5-refresh,'
      ** Start of change CAAA03
     A*N27******                       21 32'F5=Refresh'                                      CAAA03
     A N27                             22 32'F5=Refresh'
      ** End of change CAAA03
     A**                                             End   /112563/
     A*                                                    /*S01117*/
     A*                                                             /* S01203 */
     A*                                                    /*S01117*/
     A*N27*****************            21 51'Rollup, Rolldown'
     A*N27*****************            21 68'or Help'
     A**                                             Begin /112563/
     A*N27 ******                      21 43'Rollup, Rolldown or Help'
      ** Start of change CAAA03
     A*N27******                       21 50'Rollup/Rolldown   or Help'                       CAAA03
     A N27                             22 50'Rollup/Rolldown'
      ** End of change CAAA03
     A**                                             End   /112563/
     A*                                                    /*S01117*/
     A*
     A*
     A          R DL1905S0                  TEXT('Message Subfile')
     A*
     A* Message Subfile
     A*
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A          R DL1905C0                  TEXT('Message Subfile Control')
     A*
     A* Message Subfile Control
     A*
     A                                      SFLCTL(DL1905S0)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  46                                  SFLDSPCTL
     A                                      SFLINZ
     A  40                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*
     A          R DL1905S1                  TEXT('Enquiry Subfile')
     A*
     A* Subfile Record Format and a select field
     A*
     A                                      SFL
     A            SSSLCT         1A  I  4  2TEXT('Select Field')
     A                                      DSPATR(HI)
     A*
     A            DDVDAT         7A     4  4TEXT('Value Date')
     A                                      DSPATR(HI)
     A            DDVLDT         7   H      TEXT('Value Date')
     A            DDMSDT         5  0H      TEXT('Midas Date')
     A            DDPERD         3A     4 12TEXT('Period')
     A                                      DSPATR(HI)
     A            DDTENR         3  0   4 16TEXT('Tenor')
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            DDBASE         5A     4 20TEXT('Base')
     A                                      DSPATR(HI)
     A            DDNETP         9A     4 26TEXT('Net')
     A                                      DSPATR(HI)
     A            DDAVER         8A     4 36TEXT('Average')
     A                                      DSPATR(HI)
     A            DDMRKT         8A     4 45TEXT('Market')
     A                                      DSPATR(HI)
     A            DDOPEN         8A     4 54TEXT('Open')
     A                                      DSPATR(HI)
     A*
     A            DDCLSD         8A     4 63TEXT('Closed')
     A                                      DSPATR(HI)
     A            DDMDAT         7A     4 73TEXT('Maturity')
     A                                      DSPATR(HI)
     A*
     A          R DL1905C1                  TEXT('Enquiry Subfile Control')
     A*
     A* Enquiry Subfile Control and prompts
     A*
     A                                      SFLCTL(DL1905S1)
     A  35                                  SFLDSP
     A  36                                  SFLDSPCTL
     A N35N36                               SFLCLR
     A                                      SFLSIZ(16)
     A                                      SFLPAG(16)
     A  30                                  SFLEND
     A                                      OVERLAY
     A N27                                  ROLLUP(25)
     A N27                                  ROLLDOWN(26)
      ** Start of CAAA02
     A**36**************                    INVITE
      ** End of CAAA02
     A                                      PUTOVR
     A*                                                    /*S01199*/
     A*
     A*
     A*
     A*
     A*
     A*
     A*                                                    /*S01199*/
     A*
     A* Prompts - all highlighted. Reversed image and cursor positioned
     A* on it for error in field. (Since UL, RI and HI all together
     A* have the effect of ND then NO UL has to be specified when error
     A* in field.)
     A*
      ** Start of change CAAA03
     A**********                       22  2'Currency'                                        CAAA03
     A                                 20  2'Currency'
     A**********  @SCRCY         3A  B 22 11TEXT('Currency')                                  CAAA03
     A            @SCRCY         3A  B 20 11TEXT('Currency')
      ** End of change CAAA03
     A                                      DSPATR(HI)
     A N60N68                               DSPATR(UL)
     A  60
     AO 68                                  DSPATR(RI PC)
     A                                      DSPATR(MDT)
     A  29                                  OVRATR
     A  24                                  OVRDTA
     A*
      ** Start of change CAAA03
     A**********                       22 24'Book'                                            CAAA03
     A                                 20 24'Book'
     A**********  @SBKCD         2A  B 22 29TEXT('Book Code')                                 CAAA03
     A            @SBKCD         2A  B 20 29TEXT('Book Code')
      ** End of change CAAA03
     A                                      DSPATR(HI)
     A N63N68                               DSPATR(UL)
     A  63
     AO 68                                  DSPATR(RI PC)
     A                                      DSPATR(MDT)
     A  29                                  OVRATR
     A  24                                  OVRDTA
     A*
      ** Start of change CAAA03
     A**********                       22 41'Start Date'                                      CAAA03
     A                                 20 41'Start Date'
     A*********        START OF CHANGE                     /E42616/
     A*********   @SVLDT         7A  B 22 52TEXT('Start Date')
     A**********  @SVLDT         6A  B 22 52TEXT('Start Date')                                CAAA03
     A            @SVLDT         6A  B 20 52TEXT('Start Date')
      ** End of change CAAA03
     A*                END OF CHANGE                       /E42616/
     A                                      DSPATR(HI)
     A N62N68                               DSPATR(UL)
     A  62
     AO 68                                  DSPATR(RI PC)
     A                                      DSPATR(MDT)
     A  29                                  OVRATR
     A  24                                  OVRDTA
     A*
     A*                                                    /*S01117*/
     A*********************            22 70'Branch'
      ** Start of change CAAA03
     A**********                       22 67'Bk.Branch'                                       CAAA03
     A                                 20 67'Bk.Branch'
      ** End of change CAAA03
     A*                                                    /*S01117*/
     A  50                                  DSPATR(ND)
     A*                                                    /*S01117*/
     A************@SDBCH         3A  B 22 77TEXT('Branch')
      ** Start of change CAAA03
     A************@SDRCA         3A  B 22 77TEXT('Branch')                                    CAAA03
     A            @SDRCA         3A  B 20 77TEXT('Branch')
      ** End of change CAAA03
     A*                                                    /*S01117*/
     A                                      DSPATR(HI)
     A N61N68                               DSPATR(UL)
     A  61
     AO 68                                  DSPATR(RI PC)
     A  50                                  DSPATR(ND PR)
     A                                      DSPATR(MDT)
     A  29                                  OVRATR
     A  24                                  OVRDTA
     A*
      ** Start of change CAAA03
     A**********                       23  2'or enter "1" against +                           CAAA03
     A**********                             value date for Daily +                           CAAA03
     A**********                             Deals'                                           CAAA03
     A                                 21  2'or enter "1" against +
     A                                       value date for Daily +
     A                                       Deals'
      ** End of change CAAA03
     A  27                                  DSPATR(ND)
     A  28                                  OVRATR
 
      ** Start of CAAA02 (NOTE: The following lines ARE meaningful)
     A*%%WB 6 FLD <!-- -->\r\n\r\n+
     A*%%WB +<!-- Script to cause auto refresh -->\r\n\r\n+
     A*%%WB +<script language="JavaScript">\r\n+
     A*%%WB +\tsetTimeout("validateAndSubmit('ENTER')",10000);+
     A*%%WB +\r\n</script>\r\n\r\n
      ** End of CAAA02
 
     A*
     A* THIS FORMAT IS USED TO CLEAR THE SCREEN OF DEAL DETAILS
     A* IN THE INSTANCE OF THE DEALS BEING DELETED BY ANOTHER USER
     A* BEFORE THE PGM REFRESHES THE SCREEN
     A*
     A          R DCLEAR                    CLRL(15)
     A            DUMMY          1A  I  4  2
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A*
