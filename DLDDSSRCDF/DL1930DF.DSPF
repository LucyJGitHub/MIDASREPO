      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL FRA/IRS Rates Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module
     A*                                                               *
     A*   DL1930DF - FRA/IRS RATES MAINTENANCE.                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 18Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 29Jan97               *
      *                 S01328             Date 14Mar91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CAAA03 - Separate DL1930S0 to 2 record formats to stop non-  *
      *           display / errors.                                   *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*   S01328 - FRA/IRS Revaluation - New Display file             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(22)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A  60                                  CF10
     A          R DL1930S1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R DL1930S0                  SFLCTL(DL1930S1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A                                      TEXT('MAINTENANCE SCREEN')
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      CF08
     A                                      CF07
     A                                      PUTOVR
      **Start of change CAAA03
     A**********                            ROLLUP(51)                                        CAAA03
     A**********                            ROLLDOWN(52)                                      CAAA03
     A                                      OVERLAY
      **End of change CAAA03
     A*
     A            DDPGMQ                    SFLPGMQ
      **Start of change CAAA03
     A          R DL1930D1                  TEXT('MAIN SCREEN HEADER')
     A                                      PUTOVR
     A                                      OVERLAY
      **End of change CAAA03
     A            DDUSER        10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'DL1930'
     A            DDHEAD         9A     1 37DSPATR(HI)
     A                                      OVRDTA
     A            BJMRDT         7A  O  1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDJOB         10A  O  2  2TEXT('*JOB')
     A                                      OVRDTA
     A            DDTIME         6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Action:'
     A            DDACTN         1A  B  4 11TEXT('ACTION CODE')
     A                                      OVRDTA
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A                                  4 13'(I,A,D,E)'
     A                                  6  2'Currency:'
     A                                      DSPATR(HI)
     A            DDCCY     R        B  6 12REFFLD(CCY)
     A                                      OVRDTA
     A  21                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A            DDCCNM    R        O  6 18REFFLD(CCNM)
     A                                      OVRDTA
     A                                  7  2'Base Rate:'
     A                                      DSPATR(HI)
     A            DDBRTT         2   B  7 13
     A                                      OVRDTA
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A            DDBSRN    R        O  7 18REFFLD(BSRN)
     A                                      OVRDTA
     A                                  9  2'Period from Spot'
     A            DDNARR         8   O  9 27
     A                                 10  2'Years  Months Days'
     A                                 10 27'Date'
     A                                 10 40'Rate'
     A*
     A          R DL1930D0                  TEXT('MAIN SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                      CF08
     A                                      CF07
     A*
     A**                                             Begin /112563/
     A***********                      23  2'F10 - CONFIRM DELETE'
     A                                 23  2'F10=CONFIRM DELETE'
     A**                                             End   /112563/
     A  60                                  DSPATR(HI)
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A**                                             Begin /112563/
     A***********                      24  1'F3=End'
     A***********                      24  9'F12=Previous Screen'
     A***********                      24 30'F8/F7=Next/Previous Record'
     A***********                      24 58'Rollup/Rolldown'
     A                                 24  2'F3=Exit'
     A                                 24 11'F12=Previous Screen'
     A                                 24 32'F8=Next Record'
     A                                 24 48'F7=Previous Record'
     A                                 23 51'Rollup/Rolldown'
     A**                                             End   /112563/
     A                                      OVRATR
     A*
     A          R DL1930S5                  SFL
     A                                      TEXT('SUBFILE FOR PERIODS')
     A            DDPDYR         2A  B 11  2TEXT('PERIOD YY')
     A  30                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A            DDPDMN         2A  B 11  9TEXT('PERIOD MM')
     A  31                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A*
     A            DDPDDY         2A  B 11 16TEXT('PERIOD DD')
     A  32                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A*
     A            DDVDMY         7A  O 11 27TEXT('DDMMMYY')
     A            DDRATE        12A  B 11 40TEXT('RATE')
     A  33                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A*
     A          R DL1930S4                  SFLCTL(DL1930S5)
     A                                      TEXT('CONTROL FOR PERIODS')
     A                                      BLINK
     A                                      PUTOVR
      **Start of change CAAA03
     A                                      OVERLAY
      **End of change CAAA03
     A                                      SFLDSPCTL
     A N45                                  SFLDSP
     A  45                                  SFLCLR
     A  10                                  SFLEND
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A  50        RECNO          2  0H      SFLRCDNBR(CURSOR)
