     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL FRA/IRS Report Prompt for Currency')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module
     A*                                                               *
     A*  DL1934DF - FRA/IRS CURRENCY PROMPT SCREEN                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01328             Date 25Mar91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01328 - FRA/IRS Revaluations - New Display File             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*    20  - PROGRAM CALLED FOR FRA OR IRS RATES FILE LIST        *
     A*    21  - PROGRAM CALLED FOR MM RATES AND DISCOUNT FACTORS     *
     A*/COPY WNCPYSRC,DL1934DF01
     A*                                                               *
     A*    40  - ERROR ON INPUT CURRENCY                              *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'F3 To Exit')
     A                                      CA12(12 'F12 To Cancel')
     A*
     A** Record Format for titles and Input fields.
     A*
     A          R DL1934P1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      OVERLAY
     A*
     A*/COPY WNCPYSRC,DL1934DF02
     A  20                              1 28'FRA/IRS RATES +
     A                                       FILE LIST PROMPT'
     A                                      DSPATR(UL HI)
     A*
     A  21                              1 22'MM RATES AND DISCOUNT +
     A                                       FACTORS REPORT PROMPT'
     A                                      DSPATR(UL HI)
     A*
     A*/COPY WNCPYSRC,DL1934DF03
     A            USER          10A  O  1  2
     A                                  1 13'DL1934'
     A            JOB           10A  O  2  2
     A*
     A                                  1 67'DATE '
     A            DATE           7A  O  1 73
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*
     A                                 10  7'Select Report by:'
     A*
     A                                 12 27'Currency'
     A            WCCY           3A  B 12 38
     A                                      DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL)
     A*
     A                                 12 43'(Enter blanks for ALL)'
     A*
     A                                 21  3'F3=Exit  F12=Previous +
     A                                      Screen'
     A*
     A** Subfile File for Display of Error Msgs on Pgm Message Queue.
     A*
     A          R DL1934S0
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A
     A          R DL1934C0
     A                                      SFLCTL(DL1934S0)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N99                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
