      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL IR Interest Rate Swap Rates Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module                                       *
     A*                                                               *
     A*  DL1935DF - INTEREST RATE SWAP RATES MAINTENANCE              *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. 250739             Date 17Feb22               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 AR1058442          Date 02Jul15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAAA03             Date 19Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 112563             Date 29Jan97               *
      *                 100317             Date 04Mar96               *
     A*                 CIR001             DATE 13JUN95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  250739 - Increase subfile size to 60                         *
      *  MD046248 - Finastra Rebranding                               *
      *  AR1058442 - Allow insertion/amendment of Swap Rates to be    *
      *              negative. (Child: AR1058443)                     *
      *  CAAA03 - Separate DL1935S0 to 2 record formats to stop non-  *
      *           display / errors.                                   *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  100317 - Add additional action code of 'C' for 'COPY' and    *
     A*           'To Interest calculation method' fields             *
     A*  CIR001 - Interest Rate Calendars                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(22)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A  60                                  CF10
     A          R DL1935S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R DL1935S0                  SFLCTL(DL1935S1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A                                      TEXT('MAINTENANCE SCREEN')
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      CF08
     A                                      CF07
     A                                      PUTOVR
      **Start of change CAAA03
     A**********                            ROLLUP(51)                                        CAAA03
     A**********                            ROLLDOWN(52)                                      CAAA03
     A                                      OVERLAY
      **End of change CAAA03
     A*
     A            DDPGMQ                    SFLPGMQ
      **Start of change CAAA03
     A          R DL1935D1                  TEXT('MAIN SCREEN HEADER')
     A                                      PUTOVR
     A                                      OVERLAY
      **End of change CAAA03
     A            DDUSER        10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 13'DL1935'
     A            DDHEAD        10A     1 37DSPATR(HI)
     A                                      OVRDTA
     A            BJMRDT         7A  O  1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            DDJOB         10A  O  2  2TEXT('*JOB')
     A                                      OVRDTA
     A            DDTIME         6Y 0O  2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Action:'
     A            DDACTN         1A  B  4 11TEXT('ACTION CODE')
     A                                      OVRDTA
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A*
     A** 100317 - Begin
     A***********                       4 13'(I,A,D,E)'
     A                                  4 13'(I,A,C,D,E)'
     A** 100317 - End
     A                                  6  2'Currency:'
     A                                      DSPATR(HI)
     A            DDCCY     R        B  6 15REFFLD(CCY)
     A                                      OVRDTA
     A  21                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A            DDCCNM    R        O  6 19REFFLD(CCNM)
     A                                      OVRDTA
     A                                  7  2'Int.Cal.Mth:'
     A                                      DSPATR(HI)
     A            DDINCL         1   B  7 15
     A                                      OVRDTA
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A** 100317 - Begin
     A*********** DDINCN        20   O  7 19
     A            DDINCN        22   O  7 19
     A** 100317 - End
     A                                      OVRDTA
     A                                  8  2'Frequency:'
     A                                      DSPATR(HI)
     A            DDFREQ         1   B  8 15
     A                                      OVRDTA
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A            DDFRQN        20   O  8 19
     A                                      OVRDTA
     A** 100317 - Begin
     A                                  8 40'To Int.Cal.Mth:'
     A                                      DSPATR(HI)
     A            TOINCL         1   B  8 56
     A                                      OVRDTA
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A            TOINCN        22   O  8 59
     A                                      OVRDTA
     A** 100317 - End
     A                                 10  2'Period from Spot'
     A            DDNARR         8   O 10 27
     A                                 11  2'Years  Months Days'
     A                                 11 27'Date'
     A                                 11 40'Rate'
     A*
     A          R DL1935D0                  TEXT('MAIN SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                      CF08
     A                                      CF07
     A*
     A**                                             Begin /112563/
     A***********                      23  2'F10 - CONFIRM DELETE'
     A                                 23  2'F10=CONFIRM DELETE'
     A**                                             End   /112563/
     A  60                                  DSPATR(HI)
     A N60                                  DSPATR(ND)
     A                                      OVRATR
     A**                                             Begin /112563/
     A***********                      24  1'F3=End'
     A***********                      24  9'F12=Previous Screen'
     A***********                      24 30'F8/F7=Next/Previous Record'
     A***********                      24 58'Rollup/Rolldown'
     A                                 24  2'F3=Exit'
     A                                 24 11'F12=Previous Screen'
     A                                 24 32'F8=Next Record'
     A                                 24 48'F7=Previous Record'
     A                                 23 51'Rollup/Rolldown'
     A**                                             End   /112563/
     A                                      OVRATR
     A*
     A          R DL1935S5                  SFL
     A                                      TEXT('SUBFILE FOR PERIODS')
     A            DDPDYR         2A  B 12  2TEXT('PERIOD YY')
     A  30                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A            DDPDMN         2A  B 12  9TEXT('PERIOD MM')
     A  31                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A*
     A            DDPDDY         2A  B 12 16TEXT('PERIOD DD')
     A  32                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A*
     A            DDMDMY         7A  O 12 27TEXT('DDMMMYY')
      *
      ** Start of change AR1058442
      *
     A**********  DDRATE        12A  B 12 40TEXT('RATE')
     A            DDRATE        13A  B 12 40TEXT('RATE')
      *
      ** End of change AR1058442
      *
     A  33                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A*
     A          R DL1935S4                  SFLCTL(DL1935S5)
     A                                      TEXT('CONTROL FOR PERIODS')
     A                                      BLINK
     A                                      PUTOVR
      **Start of change CAAA03
     A                                      OVERLAY
      **End of change CAAA03
     A                                      SFLDSPCTL
     A N45                                  SFLDSP
     A  45                                  SFLCLR
     A  10                                  SFLEND
      ** Start of change 250739
     A**********                            SFLSIZ(0030)                                  /*250739*/
     A                                      SFLSIZ(0060)                                  /*250739*/
      ** End of change 250739
     A                                      SFLPAG(0010)
     A  50        RECNO          2  0H      SFLRCDNBR(CURSOR)
