      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL FRA/IRS revaluation report prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module                                       *
     A*                                                               *
     A*  DL1953DF - FRA/IRS REVALUATION REP CURRENCY AND BOOK PROMPT  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CIR012             Date 02Apr03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01328             Date 12Mar91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CIR012 - Allow report selection by deal.                     *
     A*  S01328 - FRA/IRS Revaluations - New Display File.            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*    10  - Revaluation report selected for FRA                  *
     A*    20  - Revaluation report selected for Single Currency IRS  *
     A*    30  - Revaluation report selected for Cross Currency IRS   *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'F3 To Exit')
     A                                      CA12(12 'F12 To Cancel')
     A*
     A* Record Format for titles and Input fields.
     A*
     A          R DL1953P1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A*
     A  10                              1 34'FRA REVALUATIONS'
     A                                      DSPATR(UL HI)
     A*
     A  20                              1 26'IRS SINGLE CURRENCY +
     A                                      REVALUATIONS'
     A                                      DSPATR(UL HI)
     A*
     A  30                              1 26'IRS CROSS CURRENCY +
     A                                      REVALUATIONS'
     A                                      DSPATR(UL HI)
     A*
     A                                  3 35'REPORT PROMPT'
     A                                      DSPATR(UL HI)
     A*
     A            USER          10A  O  1  2
     A                                  1 13'DL1953'
     A            JOB           10A  O  2  2
     A*
     A                                  1 67'DATE '
     A            DATE           7A  O  1 73
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*
     A                                  7  7'Select Revaluations +
     A                                      Report by:'
     A*
     A N30                             10 27'Currency'
     A N30        WCCY           3A  B 10 38
     A N30                                  DSPATR(HI)
     A N30 40                               DSPATR(RI PC)
     A N30N40                               DSPATR(UL)
     A*
     A N30                             10 43'(Enter blanks for ALL)'
     A*
     A                                 12 27'Book'
     A            WBOOK          2A  B 12 38
     A                                      DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(UL)
     A*
     A                                 12 43'(Enter blanks for ALL)'
     A*
      * Start of change CIR012
     A                                 14 27'Deal Number'
     A            WDEAL          6A  B 14 40
     A                                      DSPATR(HI)
     A  60                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
      *
     A                                 14 48'(Enter blanks for ALL)'
      * End of change CIR012
     A                                 21  3'F3=Exit  F12=Previous +
     A                                      Screen'
     A*
     A* Subfile File for Display of Error Msgs on Pgm Message Queue.
     A*
     A          R DL1953S0
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A
     A          R DL1953C0
     A                                      SFLCTL(DL1953S0)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N99                                  SFLEND
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
