      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Fiduciary deposits input enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/DL2100DF - Fiduciary Deposits Enquiry                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
      *
      ** Format DL2100S0 : Subfile Record Error Message
      *
     A          R DL2100S0                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFLMSGRCD(24)
     A            SDSFKY                    SFLMSGKEY
     A            SDPGMQ                    SFLPGMQ
      *
      ** Format DL2100C0 : Subfile Control Error Message
      *
     A          R DL2100C0                  SFLCTL(DL2100S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            SDPGMQ                    SFLPGMQ
      *
      ** Format subfile for All Deal
      *
     A          R DL2100S1                  SFL
     A  78                                  SFLNXTCHG
     A            S1DLNO         6A  O  9  7DSPATR(HI)
     A            S1SSEL         1A  B  9  3DSPATR(HI)
     A  62                                  DSPATR(PR ND)
     A N78                                  DSPATR(UL)
     A  78                                  DSPATR(PC RI)
     A            S1DTYP         2A  O  9 16DSPATR(HI)
     A            S1STYP         2A  O  9 21DSPATR(HI)
     A            S1CURR         3A  O  9 26DSPATR(HI)
     A            S1AMNT        15A  O  9 32DSPATR(HI)
     A            S1VDAT         6A  O  9 50DSPATR(HI)
     A            S1MDAT         6A  O  9 58DSPATR(HI)
     A            S1CNUM         6A  O  9 66DSPATR(HI)
     A            S1STAT         1A  O  9 75DSPATR(HI)
     A            S1DELT         1A  O  9 77DSPATR(HI)
     A                                      COLOR(RED)
     A            S1LNKN         6A  H
      *
      ** Format subfile control for All Deal
      *
     A          R DL2100C1                  SFLCTL(DL2100S1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A N49                                  SFLEND(*MORE)
     A N61                                  SFLDSPCTL
     A N61N65                               SFLDSP
     A  61                                  SFLCLR
     A  49                                  ROLLUP(25)
     A            SWRRN1         4S 0H      SFLRCDNBR
     A            SUSID         10A  O  1  3
     A                                  1 14'DL2100'
     A            SMRDT          7A  O  1 73TEXT('Run Date')
     A            SWSID         10A  O  2  3
     A                                  2 73TIME
     A                                  1 31'FIDUCIARY ENQUIRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 58'Review From :'
     A            SDREVI         6A  B  4 72DSPATR(HI)
     A                                  6  2'Sel'
     A                                  6  7'Deal'
     A                                  7  7'No'
     A                                  6 16'Deal'
     A                                  7 16'Type'
     A                                  6 21'Sub'
     A                                  7 21'Type'
     A                                  6 26'Ccy'
     A                                  6 50'Value'
     A                                  7 50'Date'
     A                                  6 57'Maturity'
     A                                  7 57'/Notice'
     A                                  6 66'Cust'
     A                                  7 66'Number'
     A                                  6 72'Status'
     A                                  6 32'Amount'
     A                                  7  2'(E)'
      *
      ** Format trailer for SFL Enquiry
      *
     A          R DL2100F1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
