      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Fiduciary deposits input enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DSPF/DL210BDF  - Fiduciary Deposits Enquiry                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
     A                                      CA12(12)
      *
      ** Format DL210BS0 : Subfile Record Error Message
      *
     A          R DL210BS0                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFLMSGRCD(24)
     A            SDSFKY                    SFLMSGKEY
     A            SDPGMQ                    SFLPGMQ
      *
      ** Format DL210BC0 : Subfile Control Error Message
      *
     A          R DL210BC0                  SFLCTL(DL210BS0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            SDPGMQ                    SFLPGMQ
      *
      ** Format subfile for Taking Deal
      *
     A          R DL210BS1                  SFL
     A            SFDLNO         6A  O 13  3DSPATR(HI)
     A            SFCNUM         6A  O 13 12DSPATR(HI)
     A            SFPTFR         4A  O 13 21DSPATR(HI)
     A  81                                  DSPATR(ND)
     A            SFAMNT        15A  O 13 28DSPATR(HI)
     A**Start of change CGL029
     A**********  SFACCD         4A  O 13 45DSPATR(HI)                                        CGL029
     A**********  SFASEQ         2A  O 13 52DSPATR(HI)                                        CGL029
     A**********  SFCSSN        10A  O 13 57DSPATR(HI)                                        CGL029
     A            SFACCD        10A  O 13 44DSPATR(HI)
     A            SFASEQ         2A  O 13 55DSPATR(HI)
     A            SFCSSN        10A  O 13 59DSPATR(HI)
     A**End of change CGL029
     A            SFTRAS         1A  O 13 72DSPATR(HI)
     A            SFDELT         1A  O 13 75DSPATR(HI)
     A                                      COLOR(RED)
      *
      ** Format subfile control for Placing Deal
      *
     A          R DL210BC1                  SFLCTL(DL210BS1)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      OVERLAY
     A N48                                  SFLEND(*MORE)
     A N61                                  SFLDSPCTL
     A N61N64                               SFLDSP
     A  61                                  SFLCLR
     A            SWRRN1         4S 0H      SFLRCDNBR
     A            SUSID         10A  O  1  2
     A                                  1 13'DL2100'
     A            SDMRDT         7A  O  1 73TEXT('Run Date')
     A            SWSID         10A  O  2  2
     A                                  2 73TIME
     A            SDDLNO         6A  O  4 11TEXT('Deal Number')
     A                                      DSPATR(HI)
     A            SDCNUM         6A  O  4 37TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            SDCSSN        10A  O  4 44TEXT('Customer Name')
     A                                      DSPATR(HI)
     A                                  4 67'DELETED'
     A  21                                  DSPATR(HI)
     A  21                                  COLOR(RED)
     A N21                                  DSPATR(ND)
     A                                  1 27'FIDUCIARY DEPOSITS +
     A                                      ENQUIRY'
     A                                      DSPATR(HI)
     A                                  4 19'Placed-with Bank:'
     A                                  7  6'Deal Date:'
     A            SDCCY          3A  O  6 17DSPATR(HI)
     A            SDDATE         6A  O  7 17DSPATR(HI)
     A                                  6  6'Currency:'
     A                                  7 25'Value Date:'
     A                                  6 25'Amount:'
     A            SDAMNT        15A  O  6 33DSPATR(HI)
     A            SDVDAT         6A  O  7 37DSPATR(HI)
     A                                  9 49'Booking Branch:'
     A                                  8 49'Notice Days:'
     A                                  7 49'Maturity Date:'
     A                                  6 49'Interest Rate:'
     A            SDRATE         9A  O  6 64DSPATR(HI)
     A            SDMDAT         7A  O  7 64DSPATR(HI)
     A            SDNOTD         3A  O  8 64DSPATR(HI)
     A            SDBRCA         3A  O  9 65DSPATR(HI)
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                      DSPATR(HI)
     A                                  9  6'Deal Type:'
     A            SDTYPE         2A  O  9 17DSPATR(HI)
     A                                  9 25'Sub-Type:'
     A            SDSTYP         2A  O  9 35DSPATR(HI)
     A                                 11  3'Deal No'
     A                                 11 12'Customer'
     A                                 11 22'Portf.'
     A  81                                  DSPATR(ND)
     A                                 11 29'Amount'
     A**Start of change CGL029
     A**********                       11 45'Account'                                         CGL029
     A**********                       12 45'Code'                                            CGL029
     A**********                       12 52'Seq'                                             CGL029
     A**********                       11 57'Customer'                                        CGL029
     A**********                       12 57'Shortname'                                       CGL029
     A                                 11 44'Account'
     A                                 12 44'Code'
     A                                 12 55'Seq'
     A                                 11 59'Customer'
     A                                 12 59'Shortname'
     A**End of change CGL029
     A                                 11 70'Trust'
     A                                 12 70'Status'
     A                                  4  2'Deal No:'
     A                                  1 66'DATE'
     A                                  2 66'TIME'
      *
      ** Format trailer for SFL Enquiry Taking
      *
     A          R DL210BF1
     A                                      OVERLAY
     A                                 23  2'F3=End'
     A                                 23 12'F12=Initial Screen'
