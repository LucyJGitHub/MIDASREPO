      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Outstanding Deals by Customer Prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas Dealing Module                            *
     A*                                                               *
     A*       DL2485DF - OUTSTANDING DEALS BY CUSTOMER PROMPT         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CDL021             Date 03Feb04               *
      *                      DFDR10             Date 19Nov02          *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                   No DT0204             Date 10Jun91          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *       CDL038 - Extended Deal Sub Type                         *
      *       CDL021 - Reports by Deal Type                           *
      *       DFDR10 - Dealing Fixed Deposit R5 Upgrade               *
      *                core hooks amended: DL2485DF01, DL2485DF02     *
     A*       DT0204 - New program produced to standardise RCF        *
     A*                processing for report DL2480                   *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA03(03)
     A                                      CA12(12)
     A  46                                  CA10(10)
     A          R DL2485F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A            USRID         10A  O  1  2
     A            PGMNM         10A  O  1 13
     A            SRUNA          7A  O  1 73
     A            WSID          10A  O  2  2
     A*/COPY WNCPYSRC,DL2485DF01
     A                                  2 22'OUTSTANDING DEALS BY CUSTOMER'
     A                                      DSPATR(UL HI)
     A*/COPY WNCPYSRC,DL2485DF02
     A                                  2 72TIME
     A*
     A                                  7 30'CUSTOMER -'
     A            SCUST         10A  B  7 46
     A  45                                  DSPATR(PR)
     A  30N45                               DSPATR(RI PC)
     A N30N45                               DSPATR(UL HI)
     A*
     A                                 10 30'LIVE DEALS -'
     A            SLIV           1   B 10 46
     A  45                                  DSPATR(PR)
     A  31N45                               DSPATR(RI PC)
     A N31N45                               DSPATR(UL HI)
     A                                 13 30'MATURE DEALS -'
     A*
     A            SMAT           1   B 13 46
     A  45                                  DSPATR(PR)
     A  32N45                               DSPATR(RI PC)
     A N32N45                               DSPATR(UL HI)
     A*
     A*/Begin CDL021*/
      **Start of change CDL038
     A*******************************O 16 30'DEAL TYPE / SUBTYPE'                             CDL038
     A N65                           O 16 30'DEAL TYPE / SUBTYPE'                             CDL038
     A  46                                  DSPATR(ND)                                        CDL038
     A  65                           O 16 30'DEAL TYPE/SUBTYPE/CLASS'                         CDL038
      **End of change CDL038
     A  46                                  DSPATR(ND)
     A*
      **Start of change CDL038
     A************TYP1           2   B 16 50TEXT('Data Field')                                CDL038
     A            TYP1           2   B 16 54TEXT('Data Field')                                CDL038
      **End of change CDL038
     A  46                                  DSPATR(ND)
     A  45                                  DSPATR(PR)
     A  33N45                               DSPATR(RI PC)
     A N33N45                               DSPATR(UL HI)
      **Start of change CDL038
     A************TYP2           2   B 16 53TEXT('Data Field')                                CDL038
     A            TYP2           2   B 16 57TEXT('Data Field')                                CDL038
      **End of change CDL038
     A  46                                  DSPATR(ND)
     A  45                                  DSPATR(PR)
     A  33N45                               DSPATR(RI PC)
     A N33N45                               DSPATR(UL HI)
     A*/End   CDL021*/
      **Start of change CDL038
     A            TYP3           4   B 16 60TEXT('Data Field')                                CDL038
     A N65                                                                                    CDL038
     AO 46                                  DSPATR(ND)                                        CDL038
     A N65                                                                                    CDL038
     AO 45                                  DSPATR(PR)                                        CDL038
     A  33N45 65                            DSPATR(RI PC)                                     CDL038
     A N33N45 65                            DSPATR(UL HI)                                     CDL038
      **End of change CDL038
     A N46                             23  2'F3=Exit F12=Previous'
     A  46                             23  2'F3=Exit F10=Delete F12=Previous'
     A*
     A          R DL2485S0                  SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A*
     A          R DL2485C0                  SFLCTL(DL2485S0)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  50                                  SFLEND
     A                                      SFLINZ
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            PGMQ                      SFLPGMQ
