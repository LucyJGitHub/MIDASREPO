     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL IR Cash Flow Schedule Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas DEALING MODULE                                         *
     A*                                                               *
     A*  DSPF/DL4010DF - CASH FLOW SCHEDULE MAINTENANCE               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CIR001  *CREATE    DATE 13JUN95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     F*  CIR001 - Interest Rate Calendars.                            *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      CF03
     A  25                                  CF08
     A N50                                  CF10
     A                                      CF12
     A N50                                  CF16
     A*/COPY WNCPYSRC,DL4010DF05
      *
     A          R DL4010S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            SACTN1    R        I 11  4REFFLD(IND)
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR ND)
     A N10                                  DSPATR(UL)
     A                                      AUTO(RA)
     A            SPRDT     R        O 11 10REFFLD(ZZ006)
     A                                      DSPATR(HI)
     A            SDYWK     R        O 11 17REFFLD(ZZ003)
     A                                      DSPATR(HI)
     A            SCOHO1    R        O 11 22REFFLD(IND)
     A                                      DSPATR(HI)
     A            SCFEA     R        O 11 32REFFLD(ZZ014)
     A                                      DSPATR(HI)
     A            SDTPR     R        O 11 53REFFLD(ZZ003)
     A                                      DSPATR(HI)
      *
     A          R DL4010C0                  SFLCTL(DL4010S0)
     A                                      OVERLAY
     A                                      BLINK
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLUP(05)
     A                                      ROLLDOWN(06)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'DL4010'
     A            SNARR     R        O  1 25REFFLD(NARR)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  2'DEAL NUMBER'
     A            SDLNO     R        O  4 14REFFLD(DLNO)
     A                                      DSPATR(HI)
     A                                  4 65'VALUE'
     A            SCVDAT    R        O  4 74REFFLD(DTSA)
     A                                      DSPATR(HI)
     A                                  5 65'MATURITY'
     A            SCMDAT    R        O  5 74REFFLD(DTSA)
     A                                      DSPATR(HI)
     A                                  8  2'ACTION'
     A  50                                  DSPATR(ND)
     A                                  8 11'DATE'
     A                                  7 18'CONFIRMED'
     A                                  8 18' HOLIDAY'
     A                                  8 35'AMOUNT'
     A                                  8 50'PROCESSED'
     A            SACTN     R        B  9  4REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N11                               DSPATR(UL)
     A N50 11                               DSPATR(RI PC)
     A            SSELD     R        B  9 10REFFLD(ZZ007)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N12                               DSPATR(UL)
     A N50 12                               DSPATR(RI PC)
     A            SCOHO     R        B  9 22REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N13                               DSPATR(UL)
     A N50 13                               DSPATR(RI PC)
     A            SCFLA     R        B  9 32REFFLD(ZZ014)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N14                               DSPATR(UL)
     A N50 14                               DSPATR(RI PC)
     A*
     A          R DL4010F0                  TEXT('RS - OUR CASH FLOW')
      *
     A*/COPY WNCPYSRC,DL4010DF01
     A                                 22  2'F3=Exit'
     A                                 22 11'F12=Previous screen'
     A  25                             22 32'F8=Next screen'
     A                                 22 32'F10=Confirm Single Delete'
     A  50                                  DSPATR(ND)
     A                                 23  2'Rollup  Rolldown  Enter'
     A                                 23 27'F16=Delete ALL'
     A  50                                  DSPATR(ND)
      *
     A          R DL4010F1                  TEXT('RS - THEIR CASH FLOW')
      *
     A*/COPY WNCPYSRC,DL4010DF02
     A                                 22  2'F3=Exit'
     A                                 22 11'F12=Previous screen'
     A  25                             22 32'F8=Next screen'
     A                                 22 32'F10=Confirm Single Delete'
     A  50                                  DSPATR(ND)
     A                                 23  2'Rollup  Rolldown  Enter'
     A                                 23 27'F16=Delete ALL'
     A  50                                  DSPATR(ND)
     A          R DL4010F2                  TEXT('RX - OUR CASH FLOW')
      *
     A*/COPY WNCPYSRC,DL4010DF03
     A                                 22  2'F3=Exit'
     A                                 22 11'F12=Previous screen'
     A  25                             22 32'F8=Next screen'
     A                                 22 32'F10=Confirm Single Delete'
     A  50                                  DSPATR(ND)
     A                                 23  2'Rollup  Rolldown  Enter'
     A                                 23 27'F16=Delete ALL'
     A  50                                  DSPATR(ND)
     A          R DL4010F3                  TEXT('RX - THEIR CASH FLOW')
      *
     A*/COPY WNCPYSRC,DL4010DF04
     A                                 22  2'F3=Exit'
     A                                 22 11'F12=Previous screen'
     A  25                             22 32'F8=Next screen'
     A                                 22 32'F10=Confirm Single Delete'
     A  50                                  DSPATR(ND)
     A                                 23  2'Rollup  Rolldown  Enter'
     A                                 23 27'F16=Delete ALL'
     A  50                                  DSPATR(ND)
      *
     A          R #MSGRCD                   TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Message SFLCTL')
     A                                      SFLCTL(#MSGRCD )
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A                                      OVERLAY
     A                                      PUTOVR
     A  81
     AON81                                  SFLEND
     A            ##PGM                     SFLPGMQ
