     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL IR Principal Change Schedule Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas DEALING MODULE                                         *
     A*                                                               *
     A*  DSPF/DL4020DF - Principal Change Schedule Maintenance        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP056             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 167561             Date 20Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPL002             Date 08Mar99               *
     A*                 CIR001 *CREATE     DATE 13JUN95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP056 - Automatic Authorisation of Interface deals          *
     A*  167561 - Plato Information should not appear when module OFF *
     A*  CPL002 - Midas-Plato Interface                               *
     F*  CIR001 - Interest Rate Calendars.                            *
     A*                                                               *
     A*****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
      *
     A                                      CF03
     A  25                                  CF08
     A                                      CF12
     A N50                                  CF10
     A N50                                  CF16
      *
     A          R DL4020S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            SACTN1    R        I 11  4REFFLD(IND)
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR ND)
     A N10                                  DSPATR(UL)
     A                                      AUTO(RA)
     A            SPRDT     R        O 11 10REFFLD(ZZ006)
     A                                      DSPATR(HI)
     A            SDYWK     R        O 11 17REFFLD(ZZ003)
     A                                      DSPATR(HI)
     A            SCOHO1    R        O 11 22REFFLD(IND)
     A                                      DSPATR(HI)
     A            SPCEA     R        O 11 37REFFLD(ZZ016)
     A                                      DSPATR(HI)
      *                                              Begin /CPL002/
     A            SIND1     R        O 11 59REFFLD(IND)
     A                                      DSPATR(HI)
     A*********** SDTPR     R        O 11 58REFFLD(ZZ003)
     A            SDTPR     R        O 11 70REFFLD(ZZ003)
      *                                              End   /CPL002/
     A                                      DSPATR(HI)
      *
     A          R DL4020C0                  SFLCTL(DL4020S0)
     A                                      OVERLAY
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLUP(05)
     A                                      ROLLDOWN(06)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'DL4020'
     A            SNARR     R        O  1 25REFFLD(NARR)
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4 65'VALUE'
     A            SCVDAT    R        O  4 74REFFLD(DTSA)
     A                                      DSPATR(HI)
     A                                  5  2'DEAL NUMBER'
     A            SDLNO     R        O  5 14REFFLD(DLNO)
     A                                      DSPATR(HI)
     A                                  5 65'MATURITY'
     A            SCMDAT    R        O  5 74REFFLD(DTSA)
     A                                      DSPATR(HI)
     A                                  6 36'PRINCIPAL AMOUNT AT +
     A                                      MATURITY'
     A            SMPAMT    R        O  6 65REFFLD(ZZ016)
     A                                      DSPATR(HI)
     A                                  7 18'CONFIRMED'
      *                                              Begin /CPL002/
      *                                              Begin /167561/
     A************                      7 55'INCREASE/'
     A  53                              7 55'INCREASE/'
      *                                              End   /167561/
      *                                              End   /CPL002/
     A                                  8  2'ACTION'
     A  50                                  DSPATR(ND)
     A                                  8 11'DATE'
     A                                  8 18' HOLIDAY'
     A                                  8 40'AMOUNT'
      *                                              Begin /CPL002/
      *                                              Begin /167561/
     A************                      8 55'DECREASE'
     A  53                              8 55'DECREASE'
      *                                              End   /167561/
      *
     A***********                       8 55'PROCESSED'
     A                                  8 67'PROCESSED'
      *                                              End   /CPL002/
     A            SACTN     R        B  9  4REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N11                               DSPATR(UL)
     A N50 11                               DSPATR(RI PC)
     A            SSELD     R        B  9 10REFFLD(ZZ007)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N12                               DSPATR(UL)
     A N50 12                               DSPATR(RI PC)
     A            SCOHO     R     A  B  9 22REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N13                               DSPATR(UL)
     A N50 13                               DSPATR(RI PC)
     A            SPAMT     R        B  9 37REFFLD(ZZ016)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N15                               DSPATR(UL)
     A N50 15                               DSPATR(RI PC)
      *                                              Begin /CPL002/
      *                                              Begin /167561/
     A************SIDIND         1A  B  9 59TEXT('Inc/Dec Ind')
     A  53        SIDIND         1A  B  9 59TEXT('Inc/Dec Ind')
      *                                              End   /167561/
     A                                      DSPATR(HI)
      *                                              Begin /167561/
     A**50********                          DSPATR(PR ND)
      **Start of CAP056
     A**50N53*****                          DSPATR(PR ND)                                     CAP056
     A  50                                  DSPATR(PR ND)
      **End of CAP056
      *                                              End   /167561/
     A N50N15                               DSPATR(UL)
     A N50 15                               DSPATR(RI PC)
      *                                              End   /CPL002/
      *
     A          R DL4020F0                  TEXT('RS - PRINCIPAL CHANGE')
      *
     A*/COPY WNCPYSRC,DL4020DF01
     A                                 22  2'F3=Exit'
     A                                 22 11'F12=Previous screen'
     A  25                             22 32'F8=Next screen'
     A                                 22 32'F10=Confirm Single Delete'
     A  50                                  DSPATR(ND)
     A                                 23  2'Rollup  Rolldown  Enter'
     A                                 23 27'F16=Delete ALL'
     A  50                                  DSPATR(ND)
      *
     A          R DL4020F1                  TEXT('RX - OUR PRINCIPAL CHANGE')
      *
     A*/COPY WNCPYSRC,DL4020DF02
     A                                 22  2'F3=Exit'
     A                                 22 11'F12=Previous screen'
     A  25                             22 32'F8=Next screen'
     A                                 22 32'F10=Confirm Single Delete'
     A  50                                  DSPATR(ND)
     A                                 23  2'Rollup  Rolldown  Enter'
     A                                 23 27'F16=Delete ALL'
     A  50                                  DSPATR(ND)
      *
     A          R DL4020F2                  TEXT('RX - THEIR PRINCIPAL CHANGE')
      *
     A*/COPY WNCPYSRC,DL4020DF03
     A                                 22  2'F3=Exit'
     A                                 22 11'F12=Previous screen'
     A  25                             22 32'F8=Next screen'
     A                                 22 32'F10=Confirm Single Delete'
     A  50                                  DSPATR(ND)
     A                                 23  2'Rollup  Rolldown  Enter'
     A                                 23 27'F16=Delete ALL'
     A  50                                  DSPATR(ND)
     A*
      *
     A          R #MSGRCD                   TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Message SFLCTL')
     A                                      SFLCTL(#MSGRCD )
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A                                      OVERLAY
     A                                      PUTOVR
     A  81
     AON81                                  SFLEND
     A            ##PGM                     SFLPGMQ
