/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Dealing Fees details - browse')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DLDLFEBRDF - Dealing fees browse                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A* -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A          R DLDLFES1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPTN         1A  B  8  3TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDDLNO         6A  O  8  8TEXT('Deal Number')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFSEQ         2A  O  8 19TEXT('Fee Sequence')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSDAT         7A  O  8 24TEXT('Start Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDEDAT         7A  O  8 32TEXT('End Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFCCY         3A  O  8 42TEXT('Currency')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFAMT        15A  O  8 48TEXT('Amount')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSTAT        10A  O  8 65TEXT('Status')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R DLDLFES0                  SFLCTL(DLDLFES1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'DLDLFEB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 24'Dealing Fees +
     A                                      Details Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'Sel'
     A                                  6  9'Deal'
     A                                  6 19'Fee'
     A                                  6 25'Start'
     A                                  6 33'End'
     A                                  6 40'Currency'
     A                                  6 52'Amount'
     A                                  6 67'Status'
     A                                  7  8'Number'
     A                                  7 16'Sequence'
     A                                  7 25'Date'
     A                                  7 33'Date'
     A*
     A* -------------------------------------------------------------------*
     A* FRONT OFFICE ID SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R DLDLFES3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPTN         1A  B  8  3TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFRNT        20A  O  8  7TEXT('Front Office ID')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDLNO         6A  O  8 28TEXT('Deal Number')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFSEQ         2A  O  8 35TEXT('Fee Sequence')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSDAT         7A  O  8 39TEXT('Start Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDEDAT         7A  O  8 47TEXT('End Date')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFCCY         3A  O  8 57TEXT('Currency')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDFAMT        15A  O  8 64TEXT('Amount')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * FRONT OFFICE ID SUBFILE CONTROL                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R DLDLFES2                  SFLCTL(DLDLFES3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'DLDLFEB'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 24'Dealing Fees +
     A                                      Details Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'Sel'
     A                                  6  9'Front Office Id'
     A                                  6 29'Deal'
     A                                  6 35'Fee'
     A                                  6 40'Start'
     A                                  6 48'End'
     A                                  6 55'Currency'
     A                                  6 68'Amount'
     A                                  7 28'Number'
     A                                  7 35'Seq'
     A                                  7 40'Date'
     A                                  7 48'Date'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R DLDLFEF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
