/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Dealing Fees details - screen')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DLDLFEDDDF - Dealing Fees details display                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R DLDLFES1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R DLDLFES0                  TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      CF07
     A                                      CF08
     A                                      CF12
     A  71                                  CF10
     A                                      SFLCTL(DLDLFES1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R DLDLFED1
     A                                      CF03
     A                                      CF07
     A                                      CF08
     A  71                                  CF10
     A                                      CF11
     A                                      CF12
     A                                      CF14
     A                                      CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 14'DLDLFE'
     A                                  1 36'FEES INPUT'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A            DDTIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  4  3'Action'
     A            DDACTN         1   B  4 12TEXT('Action Code')
     A  12                                  DSPATR(RI)
     A  12
     AO 69                                  DSPATR(PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  4 17'Status'
     A N10                                  DSPATR(ND)
     A            DDSTAT        16   O  4 26TEXT('Status')
     A N10                                  DSPATR(ND)
     A  10                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  6  3'Customer'
     A            DDCUST         6   B  6 12TEXT('Customer')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  6 25'Deal no'
     A            DDDLNO         6   B  6 35TEXT('Deal number')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  6 44'Fee Sequence'
     A            DDFSEQ         2   B  6 59TEXT('Fee Sequence')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8  3'Fee Code'
     A            DDFCOD         2   B  8 12TEXT('Fee Code')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8 20'Currency'
     A            DDFCCY         3   B  8 29TEXT('Currency')
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8 40'Amount'
     A            DDFAMT        14   B  8 47TEXT('Amount')
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A            DDFSGN         1   B  8 62TEXT('Sign')
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9  3'Fee Period'
     A                                  9 20'Start'
     A            DDSDAT         6   B  9 29TEXT('Start')
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  9 40'End'
     A            DDEDAT         6   B  9 47TEXT('End')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10  3'Payment On'
     A            DDFIND         1   B 10 14TEXT('Payment On')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12  3'Next Payment Date'
     A            DDNPYD         6   B 12 24TEXT('Next Payment Date')
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12 32'Frequency'
     A            DDFREQ         1   B 12 43TEXT('Frequency')
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12 48'Day in Month'
     A            DDPDAY         2   B 12 62TEXT('Day in Month')
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R DLDLFEF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF11
     A  75                                  CF14
     A  76                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit F12=Cancel'
     A                                 23 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 40'F14=Settlements'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 57'F15=Select'
     A N76                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFOTX        49A    24  2OVRDTA
     A                                      DSPATR(HI)
