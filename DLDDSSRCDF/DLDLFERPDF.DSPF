/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas DL Invalid Dealing Fees - browse')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DLDLFERPDF - Invalid Dealing Fees Data Browse Display        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A          R DLDLFES0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
      *
     A            DDOPT          1A  B  7  3TEXT('Select Option')
     A                                      DSPATR(UL)
     A            DDACTN         1A  O  7  8TEXT('Action')
     A                                      DSPATR(HI)
     A            DDFRNT        20A  O  7 12TEXT('Front Office ID')
     A                                      DSPATR(HI)
     A            DDCUST         6A  O  7 35TEXT('Customer')
     A                                      DSPATR(HI)
     A            DDFCCY         3A  O  7 44TEXT('Currency')
     A                                      DSPATR(HI)
     A            DDFAMT        14A  O  7 49TEXT('Amount')
     A                                      DSPATR(HI)
     A            DDSDAT         6A  O  7 66TEXT('Start Date')
     A                                      DSPATR(HI)
     A            DDEDAT         6A  O  7 74TEXT('End Date')
     A                                      DSPATR(HI)
      *
     A            DDTMST    R        H      REFFLD(TIMESTMP)
      *
     A          R DLDLFEC0                  SFLCTL(DLDLFES0)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A                                      TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  21                                  SFLEND
     A  22                                  SFLCLR
     A N21                                  ROLLUP(23)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'DLDLFERPB'
     A                                  1 27'Invalid Dealing Fees +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 40'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Act'
     A                                  4 12'Front Office Id'
     A                                  4 34'Customer'
     A                                  4 44'Ccy'
     A                                  4 58'Amount'
     A                                  4 66'Start'
     A                                  4 74'End'
     A                                  5 66'Date'
     A                                  5 74'Date'
      *
     A          R DLDLFEF1                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A          R DLDLFEEND                 TEXT('End Message')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'DLDLFERPB'
     A                                  1 27'Invalid Dealing Fees +
     A                                      Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #0WID         10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 40'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this Function'
      *
