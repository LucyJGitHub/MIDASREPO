      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
      *****************************************************************
      *                                                               *
      *  Midas  - DL                                                  *
      *                                                               *
      *  PF/DEALSCX0 - Midas DL DEALSDC Extension File                *
      *                                                               *
      ***(C)*MKI*LIMITED*1988,*1998                                   *                       LUC109
      *  (c) Misys International Banking Systems Ltd. 2016            *                       LUC109
      *                                                               *
      *  Last Amend No. LUC109             DATE 23Aug16               *
      *  Prev Amend No. 167412             DATE 25OCT99               *
      *                 MMI043     *Create DATE 27Nov98               *
      *                 Xxxxxx             DATE DdMmmYy               *
      *****************************************************************
      *                                                               *
      *  LUC109 - HO Reporting (Upgrade to FB Midas 2.1)              *
      *  167412 - Calculate monthly averages differently              *
      *  MMI043 - Interface to Bank Of Italy Reporting                *
      *                                                               *
      *                                                               *
      *****************************************************************
     A          R DEALCFX0                  TEXT('(C) Copyright MKI +
     A                                      International Ltd 1988, 1998')
      *
     A            MDLNO          6S 0       COLHDG('Deal Number')
      *
     A            MPCT          20A         COLHDG('PCT Head Office' +
     A                                             'Security Code')
      *
     A            MSUAM         13  0       COLHDG('Eligible Amt for' +
     A                                             'Subordinated Lns')
      *
     A            MDAVX         15  7       COLHDG('DR Rate MTD')
      *
     A            MDAVR         15  7       COLHDG('DR Rate' +
     A                                             'Mthly Average')
      *
     A            MCAVX         15  7       COLHDG('CR Rate MTD')
      *
     A            MCAVR         15  7       COLHDG('CR Rate' +
     A                                             'Mthly Average')
      *
     A            MCAPV         15  0       COLHDG('Int on CR Bals at' +
     A                                             'end of prev mth')
      *
     A            MCACM         15  0       COLHDG('Int on CR Bals'    +
     A                                             'accrued this mth')
      *
     A            MDAPV         15  0       COLHDG('Int on DR Bals at' +
     A                                             'end of prev mth')
      *
     A            MDACM         15  0       COLHDG('Int on DR Bals'    +
     A                                             'accrued this mth')
      *
     A            MDALX         15  0       COLHDG('DR Acct Bal MTD')
      *
     A            MDALB         15  0       COLHDG('DR Acct Bal' +
     A                                             'Mthly Average')
      *
     A            MCALX         15  0       COLHDG('CR Acct Bal MTD')
      *
     A            MCALB         15  0       COLHDG('CR Acct Bal' +
     A                                             'Mthly Average')
      *
     A            MDACX         15  0       COLHDG('DR Avail Bal MTD')
      *
     A            MDACB         15  0       COLHDG('DR Avail Bal' +
     A                                             'Mthly Average')
      *
     A            MCACX         15  0       COLHDG('CR Avail Bal MTD')
      *
     A            MCACB         15  0       COLHDG('CR Avail Bal' +
     A                                             'Mthly Average')
      *
     A            MTOTI         15  0       COLHDG('Tot Int until' +
     A                                             'Maturity Date' +
     A                                             'not yet paid')
      *
     A            MQDIX         15  0       COLHDG('DR Int Movements' +
     A                                             'at end of last' +
     A                                             'quarter')
      *
     A            MQDIM         15  0       COLHDG('DR Int Movements' +
     A                                             'this quarter')
      *
     A            MQCIX         15  0       COLHDG('CR Int Movements' +
     A                                             'at end of last' +
     A                                             'quarter')
      *
     A            MQCIM         15  0       COLHDG('CR Int Movements' +
     A                                             'this quarter')
      *
     A            MMTDY          3  0       COLHDG('MTD Working Days')
      *
      ** Start of fix 167412
      *
     A            MPCPLX        15  0       COLHDG('Previous Principal' +
     A                                             'amount ')
      *
     A            MINTRX        15  7       COLHDG('Previous Interest' +
     A                                             'amount ')
      *
     A            MDRCRX         1B         COLHDG('Previous DR/CR Ind')
      *
      ** End of fix 167412
      *
     A            MXTRNO        25A         COLHDG('Murex Transaction' +
                                                   'ID')
      *
     A            MSCCY          3A         COLHDG('Currency for Eleg' +
                                                   'ible Amount')
      *
     A            MXCNTL        25A         COLHDG('Contract ID')
      *
