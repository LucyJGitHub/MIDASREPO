      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas DL DEALS FILE - NEGT. ASSETS SOLD')
     A**************************************************************
     A*                                                            *
     A*  Midas - Dealing Module
     A*                                                            *
     A*   PF/DEALSDE    Deals File - Negotiable Assets Sold        *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 25Nov05               *
      *                 CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU003             Date 05Nov97               *
      *                 S01378             Date 9APr92                *
     A*                 E22405             Date 18JUN90               *
     A*                      S01117           DATE 05/12/88        *
     A*                      S01177           DATE 02/09/88        *
     A*                      S01165           DATE 16/05/88        *      S01165
     A*                                                            *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CEU003 - EMU Dealing Settlement Ccy Conversion               *
     A*   S01378 -  NA parcels. Allows partial sale of a parcel       *
     A*             of NA's which have previously been entered as     *
     A*             a single NA purchased transaction. (was MOF53)    *
     A*             On sold transaction parcel size defines the       *
     A*             number of purchased parcels being sold by this    *
     A*             transaction.                                      *
     A*                                                               *
     A*     E22405 - NEW FIELD FOR COVER MT202 CHANGE.                *
     A*     S01117 - MULTI BRANCHING                                  *
     A*     S01177 - EXTRA SETTLEMENT FIELDS ADDED                    *
     A*     S01165 - Two new fields (Book code and linked             *   S01165
     A*              reference number) have been added to this file.  *   S01165
     A*                                                               *
     A**************************************************************
     A                                      REF(MFRFL)
     A          R DEALSDEF                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            RECI      R
     A            DLNO      R
     A            RCDT      R
     A            RCDC      R
      * Begin /CSD027/
     A**********  SCUS      R                                                                 CSD027
     A            SCUS           6A
     A                                      TEXT('Sold to customer no')
     A                                      COLHDG('Sold to             ' +
     A                                      'Cust no')
      * End /CSD027/
     A*                                                  /S01117/
     A*********** BRCD      R
     A            BRCA      R
     A*                                                  /S01117/
     A            DTYP      R
     A            DLST      R
     A            DDAT      R
     A            VDAT      R
     A            ZZ005     R
     A            BRKC      R
     A            BAGE      R
     A            CCY       R
     A            SAPR      R
     A            ZZ001     R
     A            YRAT      R
     A            ZZ014     R
     A            INTR      R
     A            ZZ003     R
     A            RNNA      R
      * Begin /CSD027/
     A**********  ISCN      R                                                                 CSD027
     A            ISCN           6A
     A                                      TEXT('Issued by customer number')
     A                                      COLHDG('Issued              ' +
     A                                      'By cust             ' +
     A                                      'Number')
      * End /CSD027/
     A            ZZ005A    R               REFFLD(ZZ005 )
     A            FVAL      R
     A            BCXR      R
     A            LCXR      R
     A            SETP      R
     A**Start of change CGL029
     A**********  OSAC      R                                                                 CGL029
     A            OSACQQ        12A
     A**End of change CGL029
     A            TSEN      R
     A*                                                  /S01117/
     A*********   ZZ123     R
     A            ORBR      R
     A************OSBR      R
     A            ROBR      R
     A            PRFC      R
     A            ZZ106     R
     A            PUPR      R
     A*                                                  /S01117/
     A            RPDN      R
     A            PMDT      R
     A            ZZ076     R
     A            DNSI      R
     A            DSTI      R
     A            CNFI      R
     A            ACEI      R
     A            TLXI      R
     A            ZZ010     R
     A            LSWC      R
     A            LSWS      R
     A            ZZ002     R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A*                                       /S01165/
     A            BOKC      R
     A            LNKDN     R
     A*                                       /S01165/
     A*                                                    /*S01177*/
     A            RSTM      R
     A**Start of change CGL029
     A**********  RONS      R                                                                 CGL029
     A            RONSQQ        12A
     A**End of change CGL029
     A            RIBN      R
     A            RIBA      R
      * Begin /CSD027/
     A**********  ROBN      R                                                                 CSD027
     A**********  ROCN      R                                                                 CSD027
     A            ROBN           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ROCN           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            PSTM      R
     A**Start of change CGL029
     A**********  PONS      R                                                                 CGL029
     A            PONSQQ        12A
     A**End of change CGL029
     A            PIBN      R
     A            PIBA      R
      * Begin /CSD027/
     A**********  POBN      R                                                                 CSD027
     A**********  POCN      R                                                                 CSD027
     A            POBN           6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            POCN           6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A***********                                          /*S01177*/
     A***********                                        /S01117/
     A*********** ROBR      R
     A*********** POBR      R
     A***********                                        /S01117/
     A*                                                     /E22405/
     A            CVMR      R
     A*                                                     /E22405/
     A*                                                     /S01378/
     A** Parcel fields                                      /S01378/
     A*  parcel indicator ('P' or ' ')                      /S01378/
     A            PCLI      R
     A*  parcel size (i.e. number of assets in parcel)      /S01378/
     A*  (must refer to 5,0 field, not older 3,0 field)     /S01378/
     A            PSZE      R               REFFLD(PSIZ)
     A*                                                     /S01378/
     A*                                              Begin /CEU003/
     A            STCY           3          TEXT('Settlement Currency')
     A                                      COLHDG('Settlement' +
     A                                             'Currency')
     A            IC72           3          TEXT('In Currency in +
     A                                           Field 72')
     A                                      COLHDG('In Currency in' +
     A                                             'Field 72')
     A*                                              End   /CEU003/
     A**Start of change CGL029
     A            OSAC          18A
     A            RONS          18A
     A            PONS          18A
     A**End of change CGL029
     A*                                              Begin /CDL038/
     A            CLAS           4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
