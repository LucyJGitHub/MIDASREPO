      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas DL DEAL AMENDMENTS FILE - AMENDs')               *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *   PF/DEAMSDI    DEAL AMENDMENTS FILE - AMENDMENT              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CDL094             Date 30May14               *
      *                 CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 25Nov05               *
      *                 CDL038             Date 10May05               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP051             Date 12Nov99               *
      *                 CPB001             Date 01Jun99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU003             Date 05Nov97               *
      *                 CAC001             Date 01Feb96               *
      *                 E49965             Date 01Feb93               *
      *                 E22405             Date 14Aug90               *
      *                     S01177            DATE 26/07/89           *   S01177
      *                     S01117            DATE 02/06/89           *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL094 - Enhance Receive Settlement Instructions.            *
      *           Add Receive Bank to Bank Info Line 1 - 6.           *
      *  CRE073 - Interest Rate Rounding                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL020 - Apply Base Rate at Value Date                       *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP051 - Automatic Authorisation (MM Part)                   *
      *  CPB001 - Meridian DBA Middleware Replication Customization.  *
      *           Add Replication Indicator.                          *
      *  CDL006 - Dealing Fiduciary                                   *
      *  CEU003 - EMU Dealing Settlement Ccy Conversion               *
      *  CAC001 - Profit Centre Accounting Development:               *
      *           Additional fields for Analytical Accounting module  *
      *   E49965 - REPLACE FIELD FOR BOOKING BRANCH                   *
      *   E22405 - NEW FIELD FOR COVER MT202 CHANGE.                  *
      *   S01177 - EXTRA SETTLEMENT FIELDS ADDED                      *   S01177
      *   S01117 - MULTIBRANCHING - R10                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R DEAMSDIF
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            RECI      R
     A            DLNO      R
     A            VDAT      R               REFFLD(VDAT0 )
     A            DASN      R
     A            DTYP      R
     A            DLST      R
     A            AMTP      R
     A            PSEQ      R
      ** Start of change CSD103
     A**********  BRTT      R
     A            BRTT           2A
      ** End of change CSD103
     A            RTSP      R
     A            CCY       R
     A            DAAM      R
     A            MOGI      R
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            SETP      R
     A**Start of change CGL029
     A**********  OSAC      R                                                                 CGL029
     A            OSACQQ        12A
     A**End of change CGL029
     A            TSEN      R
     A            FACO      R
     A            SPI       R
     A***                                                  /*S01117/
     A***         ZZ022     R
     A            OSBR      R
     A*********** BRCA      R
     A*********** ZZ016     R
     A*                                                     /*E49965*/
     A*******     ZZ019     R
     A            BRCA      R
     A            ZZ016     R
     A*                                                     /*E49965*/
     A***                                                  /*S01117/
     A            NAAD      R
     A            NASN      R
     A            DAIN      R
     A            ZZ043     R
     A            LSWC      R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A*                                                    /*S01177*/
     A            RSTM      R
     A**Start of change CGL029
     A**********  RONS      R                                                                 CGL029
     A            RONSQQ        12A
     A**End of change CGL029
     A            RIBN      R
     A            RIBA      R
      * Begin /CSD027/
     A**********  ROBN      R                                                                 CSD027
     A**********  ROCN      R                                                                 CSD027
     A            ROBN           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ROCN           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            PSTM      R
     A**Start of change CGL029
     A**********  PONS      R                                                                 CGL029
     A            PONSQQ        12A
     A**End of change CGL029
     A            PIBN      R
     A            PIBA      R
      * Begin /CSD027/
     A**********  POBN      R                                                                 CSD027
     A**********  POCN      R                                                                 CSD027
     A            POBN           6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            POCN           6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A*                                                    /*S01177*/
     A*                                                     /E22405/
     A            CVMR      R
     A*                                                     /E22405/
      *                                              Begin /CAC001/
     A            FSRP      R               REFFLD(RTSP)
     A                                      TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding' +
     A                                             'Spread' +
     A                                             'Rate')
     A            IRCF      R               REFFLD(SPIN)
     A                                      TEXT('Interest Rate Change Flag')
     A                                      COLHDG('Interest Rate' +
     A                                             'Change' +
     A                                             'Flag')
      *
     A            FRCF      R               REFFLD(SPIN)
     A                                      TEXT('Funding Rate Change Flag')
     A                                      COLHDG('Funding Rate' +
     A                                             'Change' +
     A                                             'Flag')
      *                                              End   /CAC001/
     A*                                              Begin /CEU003/
     A            STCY           3          TEXT('Settlement Currency')
     A                                      COLHDG('Settlement' +
     A                                             'Currency')
     A            IC72           3          TEXT('In Currency in +
     A                                           Field 72')
     A                                      COLHDG('In Currency' +
     A                                             'in Field 72')
     A*                                              End   /CEU003/
      *                                              Begin /CDL006/
     A            FCGC           2A         TEXT('Fiduciary Charge +
     A                                      Code')
     A                                      COLHDG('Fiduciary Charge' +
     A                                           'Code')
     A            FCGD          20A         TEXT('Fiduciary Charge +
     A                                      Code Description')
     A                                      COLHDG('Fiduciary Charge' +
     A                                           'Code Description')
     A            FCGA          13P 0       TEXT('Fiduciary Charge +
     A                                      Amount')
     A                                      COLHDG('Fiduciary Charge' +
     A                                           'Amount')
     A            FCGT           9P 0       TEXT('Fiduciary Tax on+
     A                                      Charge Amount')
     A                                      COLHDG('Fiduciary Tax on' +
     A                                           'Charge Amount')
      *                                              End   /CDL006/
      *                                              Begin /CPB001/
     A            REPI           1          TEXT('Replication Indicator')
     A                                      COLHDG('Replication' 'Indicator')
      *                                              End   /CPB001/
      *                                              Begin /CAP051/
     A            AUTH           1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
      *                                              End   /CAP051/
     A**Start of change CGL029
     A            OSAC          18A
     A            RONS          18A
     A            PONS          18A
     A**End of change CGL029
      **Begin CDL017
     A            PENLTY         1A         TEXT('Penalty Pay/Receive')
     A                                      COLHDG('Penalty' 'Pay/Receive')
     A            EMDAT          5P 0       TEXT('Early Maturity Date')
     A                                      COLHDG('Early' 'Maturity' 'Date')
     A            INPUSR        10A         TEXT('Input User')
     A                                      COLHDG('Input User')
     A            AUTUSR        10A         TEXT('Authorising User')
     A                                      COLHDG('Authorising User')
      **End   CDL017
      **Begin CDL020
     A            BRTE          11P 7       TEXT('Base Rate')
     A                                      COLHDG('Base Rate')
      **End   CDL020
     A*                                              Begin /CDL038/
     A            CLAS           4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
      **Start of change CRE073
     A            CNSP          11P 7       TEXT('Contractual Spread')
     A                                      COLHDG('Contractual' 'Spread')
     A            CNSG           1A         TEXT('Contractual Spread Sign')
     A                                      COLHDG('Contractual' 'Spread' +
     A                                      'Sign')
     A            RDMT           7A         TEXT('Rounding Method')
     A                                      COLHDG('Rounding' 'Method')
     A            RDFD           4A         TEXT('Rounding Fraction/Decimal')
     A                                      COLHDG('Rounding' 'Fraction/+
     A                                      Decimal')
      **End of change CRE073
      ** Start of change CDL094
     A            RBTB1         35          TEXT('Recv Bank to Bank Info 1')
     A                                      COLHDG('Recv Bank' 'to Bank Info 1')
     A            RBTB2         35          TEXT('Recv Bank to Bank Info 2')
     A                                      COLHDG('Recv Bank' 'to Bank Info 2')
     A            RBTB3         35          TEXT('Recv Bank to Bank Info 3')
     A                                      COLHDG('Recv Bank' 'to Bank Info 3')
     A            RBTB4         35          TEXT('Recv Bank to Bank Info 4')
     A                                      COLHDG('Recv Bank' 'to Bank Info 4')
     A            RBTB5         35          TEXT('Recv Bank to Bank Info 5')
     A                                      COLHDG('Recv Bank' 'to Bank Info 5')
     A            RBTB6         35          TEXT('Recv Bank to Bank Info 6')
     A                                      COLHDG('Recv Bank' 'to Bank Info 6')
      ** End of change CDL094
