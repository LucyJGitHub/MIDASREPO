      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas DL Historic NAP Deals')
     A*****************************************************************
     A*                                                               *
     A*   Midas - Dealing Module                                  *
     A*                                                               *
     A*   PF/DLDHISPD   Historic NAP Deals                            *
     A*                                                               *
     A*   Created for: S01444                                         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL049             Date 30Mar06               *
      *  Prev Amend No. CSD027             Date 25Nov05               *
      *                 CAS009             Date 04May04               *
      *                 CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU003             Date 05Nov97               *
      *                 CAC001             Date 01Feb96               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CDL049 - Addition of a Reference Rate field                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
     A*  CEU003 - EMU Dealing Settlement Ccy Conversion               *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           Additional fields for Analytical Accounting module  *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R DLDHISD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            RECI      R
     A            DLNO      R
     A            RCDT      R
     A            RCDC      R
      * Begin /CSD027/
     A**********  ISCN      R                                                                 CSD027
     A            ISCN           6A
     A                                      TEXT('Issued by customer number')
     A                                      COLHDG('Issued              ' +
     A                                      'By cust             ' +
     A                                      'Number')
      * End /CSD027/
     A            BRCA      R
     A            DTYP      R
     A            DLST      R
     A            DDAT      R
     A            VDAT      R
     A            MDAT      R
     A            IDAS      R
     A            BRKC      R
     A            BAGE      R
     A            CCY       R
     A            PUPR      R
     A            ZZ001     R
     A            YRAT      R
     A            ZZ008     R
     A            RTSP      R
     A            INTR      R
     A            ZZ003     R
     A            RNNA      R
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            FACT      R
     A            FCNO      R
     A            FVAL      R
     A            BCXR      R
     A            LCXR      R
     A            SDST      R
     A**Start of change CGL029
     A**********  OSDA      R                                                                 CGL029
     A            OSDAQQ        12A
     A**End of change CGL029
     A            TSTN      R
     A            MDST      R
     A**Start of change CGL029
     A**********  OMDA      R                                                                 CGL029
     A            OMDAQQ        12A
     A**End of change CGL029
     A            TMAN      R
      * Begin /CSD027/
     A**********  BDID      R                                                                 CSD027
     A            BDIDX     R               TEXT('REDUNDANT')
     A                                      COLHDG('REDUNDANT')
     A                                      REFFLD(BDID)
      * End /CSD027/
     A            ZZ002A    R               REFFLD(ZZ002 )
     A            DISA      R
     A            ZZ006     R
     A            DPTD      R
     A            ICBS      R
     A            IPFR      R
     A            TOTI      R
     A            NIDT      R
     A            IBDT      R
     A            SLID      R
     A            AIPD      R
     A            DLRC      R
     A            IACD      R
     A            AITC      R
     A            AIAN      R
     A            AIAP      R
     A            IPRD      R
     A            IPRM      R
     A            SAPR      R
     A            ASDN      R
     A            SADT      R
     A            DAVD      R
     A            DASN      R
     A            FACO      R
     A            SPI       R
     A            INTF      R
     A            MDBX      R
     A            PINT1     R
     A            ZZ015     R
     A            DNSI      R
     A            DSTI      R
     A            CNFI      R
     A            ACEI      R
     A            TLXI      R
     A            ZZ010     R
     A            LSWC      R
     A            LSWS      R
     A            ZZ002     R
     A            ORED      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            BOKC      R
     A            LNKDN     R
     A            RSTM      R
     A**Start of change CGL029
     A**********  RONS      R                                                                 CGL029
     A            RONSQQ        12A
     A**End of change CGL029
     A            RIBN      R
     A            RIBA      R
      * Begin /CSD027/
     A**********  ROBN      R                                                                 CSD027
     A**********  ROCN      R                                                                 CSD027
     A            ROBN           6A
     A                                      TEXT('receipt - ordering bank no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            ROCN           6A
     A                                      TEXT('receipt - ordering customer +
     A                                      no')
     A                                      COLHDG('rcpt                ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            PSTM      R
     A**Start of change CGL029
     A**********  PONS      R                                                                 CGL029
     A            PONSQQ        12A
     A**End of change CGL029
     A            PIBN      R
     A            PIBA      R
      * Begin /CSD027/
     A**********  POBN      R                                                                 CSD027
     A**********  POCN      R                                                                 CSD027
     A            POBN           6A
     A                                      TEXT('pay - ordering bank no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'bank')
     A            POCN           6A
     A                                      TEXT('pay - ordering customer no')
     A                                      COLHDG('pay                 ' +
     A                                      'ord                 ' +
     A                                      'cust')
      * End /CSD027/
     A            RCRN      R
     A            RCRA      R
     A            RVNO      R
     A            AWBN      R
     A            AWBA      R
     A            BENN      R
     A            BENA      R
     A            DTP1      R
     A            DTP2      R
     A            DTP3      R
     A            DTP4      R
     A            DCHG      R
     A            BTB1      R
     A            BTB2      R
     A            BTB3      R
     A            BTB4      R
     A            BTB5      R
     A            BTB6      R
     A            ORBR      R
     A            PRFC      R
     A            ROBR      R
     A            POBR      R
     A            CVMR      R
     A            JRDFC     R
     A            NSDD      R               REFFLD(NNSD)
     A            PCLI      R
     A            PSZE      R               REFFLD(PSIZ)
     A            OPUP      R
     A            NSLD      R                REFFLD(NSOL)
     A*
      *                                              Begin /CAC001/
     A            FSRP      R               REFFLD(RTSP)
     A                                      TEXT('Funding Spread/Rate')
     A                                      COLHDG('Funding' +
     A                                             'Spread' +
     A                                             'Rate')
     A            FSGN      R               REFFLD(SPIN)
     A                                      TEXT('Funding Sign')
     A                                      COLHDG('Funding' +
     A                                             'Sign')
      *
     A            FPRC      R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
     A                                      COLHDG('Funding' +
     A                                             'Profit' +
     A                                             'Centre')
      *
     A            TCOF      R               REFFLD(AABC)
     A                                      TEXT('Total Cost of Funds')
     A                                      COLHDG('Total' +
     A                                             'Cost of' +
     A                                             'Funds')
      *
     A            ACPD      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Posted to Date')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'Posted To Date')
      *
     A            ACDA      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF to Control Date')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'to Control Date')
      *
     A            ACAP      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Adjustmnt Posted')
     A                                      COLHDG('Accrued' +
     A                                             'Cost of Funds' +
     A                                             'Adjustment Posted')
      *
     A            ACAN      R               REFFLD(AABC)
     A                                      TEXT('Accrued COF Adj Not Posted')
     A                                      COLHDG('Accrued Cost of' +
     A                                             'Funds Adjustment' +
     A                                             'Not Posted')
      *
     A            COFD      R               REFFLD(AABC)
     A                                      TEXT('COF Paid/Received to Date')
     A                                      COLHDG('Cost of Funds' +
     A                                             'Paid/Received' +
     A                                             'to Date')
      *
     A            COFM      R               REFFLD(AABC)
     A                                      TEXT('COF Paid/Received this Month')
     A                                      COLHDG('Cost of Funds' +
     A                                             'Paid/Received' +
     A                                             'this Month')
      *                                              End   /CAC001/
     A*                                              Begin /CEU003/
     A            STCY           3          TEXT('Settlement Currency')
     A                                      COLHDG('Settlement' +
     A                                             'Currency')
     A            IC72           3          TEXT('In Currency in +
     A                                           Field 72')
     A                                      COLHDG('In Currency in' +
     A                                             'Field 72')
     A*                                              End   /CEU003/
      **Start of change CAS001
     A            FSOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
     A            FSURPL        13P 0       TEXT('Unrealized Profit/Loss')
     A                                      COLHDG('Unrealized' +
     A                                             'Profit/Loss')
     A            FSTUPL        13P 0       TEXT('Today''s Unrealized P/L')
     A                                      COLHDG('Today''s' +
     A                                             'Unrealized P/L')
     A            FSPUPL        13P 0       TEXT('Unrealized P/L Posted +
                                                 to Date')
     A                                      COLHDG('Unrealized P/L' +
     A                                             'Posted to Date')
     A            FSYUPL        13P 0       TEXT('Unrealized P/L at Year End')
     A                                      COLHDG('Unrealized P/L' +
     A                                             'at Year End')
      **End of change CAS001
      **Start of change CAS005
     A            FSEFUP        13P 0       TEXT('Effective Fair Value +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Effective Fair Value' +
     A                                            'Hedge Unrealized' +
     A                                            'Profit/Loss Posted')
     A            FSECUP        13P 0       TEXT('Effective Cash Flow +
     A                                            Hedge Unrealized +
     A                                            Profit/Loss Posted')
     A                                      COLHDG('Effective Cash Flow' +
     A                                            'Hedge Unrealized' +
     A                                            'Profit/Loss Posted')
     A            FSLQPR        11P 7       TEXT('Liquidity/Premium Rate')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Rate')
     A            FSLQPS         1A         TEXT('Liquidity/Premium Sign')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Sign')
     A            FSAIPN    R               REFFLD(AIPD)
     A                                      TEXT('AIPD Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AIPD Using' +
     A                                           'Net-Treasury-Rate')
     A            FSIPRN    R               REFFLD(IPRD)
     A                                      TEXT('IPRD Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('IPRD Using' +
     A                                           'Net-Treasury-Rate')
     A            FSAIAN    R               REFFLD(AIAP)
     A                                      TEXT('AIAP Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AIAP Using' +
     A                                           'Net-Treasury-Rate')
     A            FSAITN    R               REFFLD(AITC)
     A                                      TEXT('AITC Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AITC Using' +
     A                                           'Net-Treasury-Rate')
     A            FSAINN    R               REFFLD(AIAN)
     A                                      TEXT('AIAN Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('AIAN Using' +
     A                                           'Net-Treasury-Rate')
     A            FSIPMN    R               REFFLD(IPRM)
     A                                      TEXT('IPRM Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('IPRM Using' +
     A                                           'Net-Treasury-Rate')
     A            FSTOTN    R               REFFLD(TOTI)
     A                                      TEXT('TOTI Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('TOTI Using' +
     A                                           'Net-Treasury-Rate')
     A            FSPINN    R               REFFLD(PINT1)
     A                                      TEXT('PINT1 Using +
     A                                            Net-Treasury-Rate')
     A                                      COLHDG('PINT1 Using' +
     A                                           'Net-Treasury-Rate')
      **End of change CAS005
     A**Start of change CGL029
     A            OSDA          18A
     A            OMDA          18A
     A            RONS          18A
     A            PONS          18A
     A**End of change CGL029
     A*                                              Begin /CDL038/
     A            CLAS           4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
      **Start of change CAS009
     A            TFDP          15P 0       TEXT('Total Fees/Discount/Premium')
     A                                      COLHDG('Total Fees/Discount/' +
     A                                      'Premium')
     A            TFDPA         15P 0       TEXT('Total Fees/Discount/Premium +
     A                                       Adj. to Yield')
     A                                      COLHDG('Total Fees/Discount/' +
     A                                      'Premium Adj. to' +
     A                                      'Yield')
     A            TFAM          15P 0       TEXT('Total Amortising Fee Adj. +
     A                                       to Yield')
     A                                      COLHDG('Total Amortising Fee' +
     A                                      'Adj. to Yield')
     A            TFAC          15P 0       TEXT('Total Accruing Fee Adj. +
     A                                       to Yield')
     A                                      COLHDG('Total Accruing Fee' +
     A                                      'Adj. to Yield')
     A            TFOF          15P 0       TEXT('Total One off Fee Adj. +
     A                                       to Yield')
     A                                      COLHDG('Total One off Fee' +
     A                                      'Adj. to Yield')
     A            AFDP          15P 0       TEXT('Non-Linear Amortise F/D/P +
     A                                       to Date')
     A                                      COLHDG('Non-Linear Amortise' +
     A                                      'F/D/P to Date')
     A            CFDP          15P 0       TEXT('Accrued F/D/P to Date')
     A                                      COLHDG('Accrued F/D/P' +
     A                                      'to Date')
     A            AMDS          15P 0       TEXT('Non-Linear Amortised +
     A                                       Discount to Date')
     A                                      COLHDG('Non-Linear Amortised' +
     A                                      'Discount to Date')
     A            AMADJ         15P 0       TEXT('Non-Linear Amortisation +
     A                                       Adjustment Posted')
     A                                      COLHDG('Non-Linear' +
     A                                      'Amortisation' +
     A                                      'Adjustment Posted')
     A            ANADJ         15P 0       TEXT('Non-Linear Amortisation +
     A                                       Adjustment not yet Posted')
     A                                      COLHDG('Non-Linear' +
     A                                      'Amortisation' +
     A                                      'Adj. not yet Posted')
     A            DSAM          15P 0       TEXT('Discount amount to be +
     A                                       amortised for the EIR period')
     A                                      COLHDG('Discount amount' +
     A                                      'to be amortised for' +
     A                                      'the EIR period')
     A            AMCOST        15P 0       TEXT('Amortised Cost')
     A                                      COLHDG('Amortised Cost')
     A            YMCOST        15P 0       TEXT('Yesterday Amortised Cost')
     A                                      COLHDG('Yesterday Amortised ' +
     A                                      'Cost')
     A            YAINT         15P 0       TEXT('UP/UL Non Linear Amort')
     A                                      COLHDG('UP/UL Non Linear ' +
     A                                      'Amortisation')
      **End of change CAS009
      * Begin /CSD027/
     A            BDID           6A         TEXT('Bill Depot Id')
     A                                      COLHDG('Bill' 'Depot')
      * End /CSD027/
      **Start of change CDL049
     A            FSRRAT        13P 8       TEXT('Reference Rate')
     A                                      COLHDG('Reference Rate')
      **End of change CDL049
