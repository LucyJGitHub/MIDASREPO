      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas DL Base Rate Change Deal Amend Extract File')    *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PF/DLBCAMPD - Extract File for BaseRateChange Deal Amendment *
      *                and Backward-Looking Rate Parameters           *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CDL102  *CREATE    Date 01Jun21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL102 - LIBOR Deregulation - Dealing                        *
      *                                                               *
      *****************************************************************
     A          R DLBCAMD0                  TEXT('(c) Finastra +
     A                                      International imited +
     A                                      2021')
      *
     A            DLDLNO         6A         TEXT('Deal Reference')
     A                                      COLHDG('Deal' 'Reference')
     A            DLAMDT         5S 0       TEXT('Amendment Date')
     A                                      COLHDG('Amendment' 'Date')
     A            DLBLRT         1A         TEXT('Backward +
     A                                            Looking Rate')
     A                                      COLHDG('Bckwrd' +
     A                                             'Looking' +
     A                                             'Rate')
     A            DLRFRR        10A         TEXT('Risk Free Rate')
     A                                      COLHDG('Risk Free' 'Rate')
     A            DLCALM         4A         TEXT('Calculation Method')
     A                                      COLHDG('Calc' 'Method')
     A            DLBADJ        11P 7       TEXT('Benchmark Adjustment')
     A                                      COLHDG('Benchmark' +
     A                                             'Adjustment')
     A                                      EDTCDE(L)
     A            DLFLOR        11P 7       TEXT('Floor')
     A                                      COLHDG('Floor')
     A                                      EDTCDE(L)
     A            DLLBDY         2S 0       TEXT('Lookback Days')
     A                                      COLHDG('Lookback' 'Days')
     A            DLLODY         2S 0       TEXT('Lockout Days')
     A                                      COLHDG('Lockout' 'Days')
     A            DLOPSH         1A         TEXT('Observation +
     A                                            Period Shift')
     A                                      COLHDG('Observ' +
     A                                             'Period'
     A                                             'Shift')
     A            DLRRDP         2S 0       TEXT('Rate Rounding +
     A                                            Decimal Places')
     A                                      COLHDG('Rate' +
     A                                             'Rounding' +
     A                                             'Dec Places')
     A            DLDBAV         1S 0       TEXT('Day Basis +
     A                                            for Average')
     A                                      COLHDG('Day' +
     A                                             'Basis' +
     A                                             'Average')
      *****************************************************************
