      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas DL Available for rate re-fixing')
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation Module                            *
      *                                                               *
      *  LF/DLDLDGLI - Available for Rate Re-fixing                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 227642             Date 24Aug04               *
      *                 CDL038             Date 10May05               *
      *                 CSD027             Date 05Dec05               *
      *                 CDL028             Date 07Feb05               *
      *                 BUG6872            Date 28Apr05               *
      *                 CAS008             Date 16Jun04               *
      *                 CGL029             Date 01Sep03               *
      *                 217684             Date 12May03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007             Date 11May01               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 171312             Date 02Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 20May99               *
      *                 CPL002             Date 08Mar99               *
      *                 CAP002             Date 08May98               *
      *                 CEU003             Date 02Feb98               *
      *                 CSW005  *Create    Date 01Aug96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data.    *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  227642 - Select records with rate fix indicator 'A' and 'B'. *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  BUG6872 - Make Deal Authorisation Status (FIDSTS)            *
      *            available to filter out unauthorised deals.        *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *           (Recompile)                                         *
      *  CIR007 - Overnight Index Swaps                               *
      *  CIR005 - FRA/IRS Business Day Conventions. (Recompile)       *
      *  171312 - SWIFT 1997 Changes for FRAIRS MT34* and MT36*.      *
      *           (Remaining objects delivery)                        *
      *           On part 2 of delivery, this file is now changed     *
      *           to add 2 fields.                                    *
      *  CSW097 - SWIFT 1997 Changes for FRAIRS MT34* and MT36*.      *
      *           Recompiled over changed DEALSDG.                    *
      *  CPL002 - Midas-Plato Interface (Recomplied)                  *
      *  CAP002 - Recompiled for converion of Midas inputs to APIs    *
      *  CEU003 - EMU Dealing Settlement Ccy Conversion (Recompile)   *
      *  CSW005 - MG34n and MG36n Message Generation                  *
      *                                                               *
      *****************************************************************
     A          R DLDLDGIF1                 PFILE(DEALSDG)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            RECI
     A            BRCA
     A            DTYP
     A            VDAT
     A            CCY                       RENAME(UCUCY)
     A            BRTT                      RENAME(UBRTT)
     A            RFIN                      RENAME(URFIN)
     A            DLNO
     A            CNUM
     A            INFD                      RENAME(UINFD)
     A            NICD                      RENAME(UNICD)
     A            XNICD                     RENAME(TNICD)
     A            EINR                      RENAME(UEINR)
     A            FRRF                      RENAME(UFRRF)
     A            OFRF                      RENAME(TFRRF)
     A            ORFI                      RENAME(TRFIN)
     A*                                                       Begin /171312/
     A            DNSI                      RENAME(UDNSI)
     A            ACEI
     A*                                                       End   /171312/
      **Start of change CIR007
     A            ICFR                      RENAME(UICFR)
      **End of change CIR007
      ** Start of change BUG6872
     A            FIDSTS
      ** End of change BUG6872
     A*
     A          K BRCA
     A          K DTYP
     A          K CCY
     A          K BRTT
     A          K RFIN
     A          K DLNO
     A*
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'F')
      **Start of change CIR007
     A            ICFR                      COMP(NE 'O')
      **End of change CIR007
      ** Start of change BUG6872
     A            FIDSTS                    COMP(EQ 'A')
      ** End of change BUG6872
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'X')
      **Start of change CIR007
     A            ICFR                      COMP(NE 'O')
      **End of change CIR007
      ** Start of change BUG6872
     A            FIDSTS                    COMP(EQ 'A')
      ** End of change BUG6872
      **Start of change 227642
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'A')
     A            ICFR                      COMP(NE 'O')
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'B')
     A            ICFR                      COMP(NE 'O')
      **End of change 227642
     A*
     A          R DLDLDGIF2                 PFILE(DEALSDG)
     A            RECI
     A            BRCA
     A            DTYP
     A            VDAT
     A            CCY                       RENAME(TCUCY)
     A            BRTT                      RENAME(TBRTT)
     A            RFIN                      RENAME(TRFIN)
     A            DLNO
     A            CNUM
     A            INFD                      RENAME(TINFD)
     A            NICD                      RENAME(TNICD)
     A            XNICD                     RENAME(UNICD)
     A            EINR                      RENAME(TEINR)
     A            FRRF                      RENAME(TFRRF)
     A            OFRF                      RENAME(UFRRF)
     A            ORFI                      RENAME(URFIN)
     A*                                                       Begin /171312/
     A            DNSI                      RENAME(TDNSI)
     A            ACEI
     A*                                                       End   /171312/
      **Start of change CIR007
     A            ICFR                      RENAME(TICFR)
      **End of change CIR007
      ** Start of change BUG6872
     A            FIDSTS
      ** End of change BUG6872
     A*
     A          K BRCA
     A          K DTYP
     A          K CCY
     A          K BRTT
     A          K RFIN
     A          K DLNO
      *
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'F')
      **Start of change CIR007
     A            ICFR                      COMP(NE 'O')
      **End of change CIR007
      ** Start of change BUG6872
     A            FIDSTS                    COMP(EQ 'A')
      ** End of change BUG6872
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'X')
      **Start of change CIR007
     A            ICFR                      COMP(NE 'O')
      **End of change CIR007
      ** Start of change BUG6872
     A            FIDSTS                    COMP(EQ 'A')
      ** End of change BUG6872
      **Start of change 227642
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'A')
     A            ICFR                      COMP(NE 'O')
     A          S RECI                      COMP(EQ 'D')
     A            RFIN                      COMP(EQ 'B')
     A            ICFR                      COMP(NE 'O')
      **END of change 227642
