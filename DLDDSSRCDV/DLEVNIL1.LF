      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas DL Merged events incl SE events')                *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  LF/DLEVNIL1  -  MIDAS MERGED EVENTS INCLUDING SE EVENTS      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CLE172             Date 01Oct20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE154             Date 06Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CAP204             Date 11Feb10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CLE042             Date 06Sep05               *
      *                 CDL038             Date 10May05               *
      *                 CSD027             Date 05Dec05               *
      *                 CLE025             Date 20Oct03               *
      *                 TDA110             Date 01Apr04               *
      *                 CGL029             Date 01Sep03               *
      *                 218992             Date 17Jul03               *
      *                 CGL015             Date 15Apr02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 188455             Date 25Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 163486             Date 13Oct99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU011  *CREATE    Date 11Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CLE172 - LIBOR Deregulation - Lending (Recompile)            *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE154 - Loan Repayment Schedule Enhancement (Recompile)     *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CAP204 - Retail Account Transfer                             *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  TDA110 - Include book code for FT transactions for reval     *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  218992 - Add Extended settlement fields in LVENTEL(recompile)*
      *  CGL015 - Net Amounts on Events by Nostro (Recompiled)        *
      *  188455 - Exclude Inter Branch Entries                        *
      *  163486 - Added IFXC and new SIAC field in LVENTEL.           *
      *  CDL006 - Dealing Fiduciary. Recompiled.                      *
      *  CEU011 - EMU Position/Risk Reporting.                        *
      *                                                               *
      ********************************************************************
     A          R EVNTXECF                  PFILE(EVNTXEC)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          K RECI
     A          R EVNTXEDF                  PFILE(EVNTXED)
     A            RECI
     A            DLNO
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            EBRC
     A            EXRT
     A            NGII
     A            NOTD
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            LCEQ
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            ZZ003
     A            FIPI
     A            INOI
     A            FCTI
     A            RLVI
     A            PTNU
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            DASN
     A            FFVD
     A            FFIN
     A            FACT
     A            FCNO
     A            FACO
     A            SPI
     A            CPCN
     A            FSLI
     A            ICIR
     A            ICAM
     A            RBDA
     A            OCFF
     A            ORBR
     A            COOB
     A            ECPY
     A            IBRE
     A            ICYE
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
     A            OSBR
     A            TAXI
     A            FUID
     A            RBDN
     A            IPSI
     A            IFXS
     A            SIAC
     A*                                              Begin /CDL038/
     A            CLAS
     A*                                              End   /CDL038/
      *
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          S IBRE                      VALUES('N')
      *
     A            DTYP                      COMP(NE 'IL')
     A            DTYP                      COMP(NE 'ID')
      *
     A          R CEVNXCCF                  PFILE(CEVNXCC)
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          R LVENTELF                  PFILE(LVENTEL)
     A            RECI
     A            LNRF
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            LTYP
     A            SUTP
     A            EBRC
     A            INTR
     A            NGII
     A            OTHD
     A            OTHA
     A            BCEQ
     A            DFCE
     A            SETP
     A            OSAC
     A            TSEN
     A            ECDP
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            INOI
     A            CRSK
     A            CDOM
     A            REGC
     A            PARI
     A            PTNU
     A            RCSI
     A            FACT
     A            FCNO
     A            RDOM
     A            LASN
     A            FREO
     A            WTIN
     A            WTRT
     A            CPAM
     A            AUTO
     A            MRIN
     A            ACOF
     A            OSBR
     A            ECPY
     A            IBRE
     A            ICYE
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
     A            BRCA
     A            COBB
     A            OLNO
      *                                                  Begin /163486/
     A            IFXC
     A            SIAC
      *                                                    End /163486/
     A*                                              Begin /CLE042/
     A            CLAS
     A*                                              End   /CLE042/
      *
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          O AUTO                      COMP(NE 'Y')
     A          S IBRE                      VALUES('N')
     A          O                           ALL
      *
     A          R BVNTXEDF                  PFILE(BVNTXED)
     A            RECI
     A            LCNO           6
     A            CNUM
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            BRCA
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            FACO
     A            SPI
     A            FIPI
     A            INOI
     A            CPCN
     A            CPAR
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            FFIN
     A            LCEQ
     A            OSBR
     A            ZZ070
     A            TIRF
     A            TICM
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          R FPVNTDDF
     A                                      PFILE(FPVNTDD)
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          S IBRE                      VALUES('N')
      *
     A          R FOEVEDF                   PFILE(FOEVED)
     A                                      TEXT('FO EVENTS DETAIL')
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          R TREVEDF                   PFILE(TREVED)
     A          K *NONE
     A          K ECSS
     A          K TRSC
     A          K TRVD
     A          K TREC
      * Start of 188455
     A          S IBRE                      VALUES('N')
      * End of 188455
      *
     A          R CPEVEDF                   PFILE(CPEVED)
     A          K *NONE
     A          K ECSS
     A          K CCEC
     A          K CPYD
     A          K CCCE
      *
     A          O CCCE                      VALUES('    ')
      * Start of 188455
     A          S IBRE                      VALUES('N')
      * End of 188455
      *
     A          R DPEVEDF                   PFILE(DPEVED)
     A          K *NONE
     A          K ECSS
     A          K CCEC
     A          K CCPD
     A          K CCCE
      *
     A          O CCCE                      VALUES('    ')
      * Start of 188455
     A          S IBRE                      VALUES('N')
      * End of 188455
      *
     A          R BPEVEDF                   PFILE(BPEVED)
     A          K *NONE
     A          K ECSS
     A          K CCEC
     A          K CCPD
     A          K CCCE
      *
     A          O CCCE                      VALUES('    ')
      ** Start of CAP204
     A          R GLEVNTDF                  PFILE(GLEVNTPD)
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          O IBRE                      VALUES('Y')
      ** End of CAP204
      *
     A          R EVNTXZZF                  PFILE(EVNTXZZ)
     A          K RECI
     A          R CEVNXZZF                  PFILE(CEVNXZZ)
     A          K RECI
     A          R LVENTZZF                  PFILE(LVENTZZ)
     A          K RECI
      *
     A          R BVNTXEZF
     A                                      PFILE(BVNTXEZ)
     A          K RECI
      *
     A          R FPVNTZZF                  PFILE(FPVNTZZ)
     A          K RECI
      *
     A          R FOEVEZZF                  PFILE(FOEVEZZ)
     A          K RECI
      ** Start of CAP204
     A          R GLEVNTZF                  PFILE(GLEVNTZZ)
     A          K RECI
      ** End of CAP204
