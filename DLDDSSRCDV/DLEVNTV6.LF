      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas DL Merged events file')                          *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  LF/DLEVNTV6 - MERGED EVENTS FILE USED                        *
      *                                                               *
      *  Note:                                                        *
      *  This local LF/DLEVNTV6 is based on a copy of LF/DLEVNTL6.    *
      *  Any changes to the core LF/DLEVNTL6 must also be applied     *
      *  to this local LF.                                            *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CLE174   *Create   Date 07Aug23               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CLE174 - Include all Loans in Cash Flow Report               *
      *            (Autosettle Ind)                                   *
      *                                                               *
      *****************************************************************
      *  All of the enhancements below are included in this new LF:   *
      *                                                               *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  TDA110 - Include book code for FT transactions for reval     *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  218992 - Add Extended settlement fields in LVENTEL(recompile)*
      *  223572 - Events By Nostro Rprt is showing the All In and     *
      *           NTR Events                                          *
      *  CGL015 - Net Amounts on Events by Nostro (Recompiled)        *
      *  188455 - Exclude Interbranch entries from DPEVED/TREVED      *
      *  163486 - Added IFXC, & omit new SIAC = Y field in LVENTEL.   *
      *  CDL006 - Dealing Fiduciary. Recompiled.                      *
      *  151083 - IGNORE RECORDS WITH SIAC EQUAL Y                    *
      *  CEU011 - EMU Position/Risk Reporting.                        *
      *                                                               *
      *****************************************************************
     A          R EVNTXECF                  PFILE(EVNTXEC)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2023')
     A          K RECI
     A          R EVNTXEDF                  PFILE(EVNTXED)
     A            RECI
     A            DLNO
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            EBRC      R
     A            EXRT
     A            NGII
     A            NOTD
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            LCEQ
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            ZZ003
     A            FIPI
     A            INOI
     A            FCTI
     A            RLVI
     A            PTNU
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            DASN
     A            FFVD
     A            FFIN
     A            FACT
     A            FCNO
     A            FACO
     A            SPI
     A            CPCN
     A            FSLI
     A            ICIR
     A            ICAM
     A            RBDA
     A            OCFF
     A            ORBR
     A            COOB
     A            ECPY
     A            IBRE
     A            ICYE
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
     A            OSBR
     A            TAXI
     A            FUID
     A            PTFR
     A            IFXS
     A            SIAC
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          O ETYP                      VALUES('10V1' '10V2' +
     A                                      '14V3' '14V4' '14V5' '14V6' '14V7' +
     A                                      '14V8' '17M3' '17S1' '20V7' '20V8' +
     A                                      '42M8' '42S1' '61D1' '61R5' '62D1' +
     A                                      '62R5' '63D1' '63V2' '64D1' '64R5' +
     A                                      '65D1' '65V2' '66D1' '66R5' '67D1' +
     A                                      '67V2' '70D1' '70M1' '70V1' '71F2' +
     A                                      '80A1' '81A1' '81D2' '81F1' '82M3' +
     A                                      '83A1' '83M1' )
     A          O ETYP                      VALUES('20S1' '20S2' +
     A                                      '21S1' '21S2' '21S3' +
     A                                      '22S1' '22S2' '22S3' '22S4' +
     A                                      '23S1' '23S2' +
     A                                      '24S1' '24S2')
      *
     A          O ETYP                      VALUES('15V6' '15I6' '15M6' '15M8' +
     A                                      '16V6' '16I6' '16M6' '16M8' +
     A                                      '17I6' '17M6' '17M8' '18M6' '61V4' +
     A                                      '61V6' '61I6' '61R4' '61R6' '61M6' +
     A                                      '62V4' '62V6' '62I6' '62R4' '62R6' +
     A                                      '62M6' '63V6' '63R6' '64V4' '64V6' +
     A                                      '64I6' '64R4' '64R6' '65M6' '65V6' +
     A                                      '65R6' '66V4' '66V6' '66I6' '66R4' +
     A                                      '66R6' '66M6' '67V6' '67R6' '68V4' +
     A                                      '68V6' '68I6' '68R4' '68R6' '68M6' +
     A                                      '69V4' '69V6' '69I6' '69R4' '69R6' +
     A                                      '69M6')
      *
      ** OMIT INTERBRANCH EVENTS TO AVOID MULTIPLE RECORDS
     A          O IBRE                      COMP(EQ 'Y')
     A          O DTYP                      VALUES('IL' 'ID')
      *
     A          O SIAC                      COMP(EQ 'Y')
      *
     A          S                           ALL
      *
     A          R CEVNXCCF                  PFILE(CEVNXCC)
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          O ETYP                      VALUES('10V1' '10V2' +
     A                                      '14V3' '14V4' '14V5' '14V6' '14V7' +
     A                                      '14V8' '17M3' '17S1' '20V7' '20V8' +
     A                                      '42M8' '42S1' '61D1' '61R5' '62D1' +
     A                                      '62R5' '63D1' '63V2' '64D1' '64R5' +
     A                                      '65D1' '65V2' '66D1' '66R5' '67D1' +
     A                                      '67V2' '70D1' '70M1' '70V1' '71F2' +
     A                                      '80A1' '81A1' '81D2' '81F1' '82M3' +
     A                                      '83A1' '83M1' )
      *
     A          O ETYP                      VALUES('15V6' '15I6' '15M6' '15M8' +
     A                                      '16V6' '16I6' '16M6' '16M8' +
     A                                      '17I6' '17M6' '17M8' '18M6' '61V4' +
     A                                      '61V6' '61I6' '61R4' '61R6' '61M6' +
     A                                      '62V4' '62V6' '62I6' '62R4' '62R6' +
     A                                      '62M6' '63V6' '63R6' '64V4' '64V6' +
     A                                      '64I6' '64R4' '64R6' '65M6' '65V6' +
     A                                      '65R6' '66V4' '66V6' '66I6' '66R4' +
     A                                      '66R6' '66M6' '67V6' '67R6' '68V4' +
     A                                      '68V6' '68I6' '68R4' '68R6' '68M6' +
     A                                      '69V4' '69V6' '69I6' '69R4' '69R6' +
     A                                      '69M6')
      *
     A          S                           ALL
      *
     A          R LVENTELF                  PFILE(LVENTEL)
     A            RECI
     A            LNRF
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            LTYP
     A            SUTP
     A            EBRC      R
     A            OTHD
     A            OTHA
     A            BCEQ
     A            DFCE
     A            NGII
     A            INTR
     A            SETP
     A            OSAC
     A            TSEN
     A            ECDP
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            INOI
     A            CRSK
     A            CDOM
     A            REGC
     A            AUTO
     A            PARI
     A            PTNU
     A            IBRE
     A            RCSI
     A            FACT
     A            FCNO
     A            PTFR
     A            RDEL
      *
     A            IFXC
     A            SIAC
      *
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          O ETYP                      VALUES('10V1' '10V2' +
     A                                      '14V3' '14V4' '14V5' '14V6' '14V7' +
     A                                      '14V8' '17M3' '17S1' '20V7' '20V8' +
     A                                      '42M8' '42S1' '61D1' '61R5' '62D1' +
     A                                      '62R5' '63D1' '64D1' '64R5' +
     A                                      '65D1' '66D1' '66R5' '67D1' +
     A                                      '70D1' '70M1' '70V1' +
     A                                      '80A1' '81A1' '81D2' '81F1' '82M3' +
     A                                      '83A1' '83M1' '61X2' '62X2' '64X2' +
     A                                      '66X2' '70X2' )
      *
     A          O ETYP                      VALUES('15V6' '15I6' '15M6' '15M8' +
     A                                      '16V6' '16I6' '16M6' '16M8' +
     A                                      '17I6' '17M6' '17M8' '18M6' '61V4' +
     A                                      '61V6' '61I6' '61R4' '61R6' '61M6' +
     A                                      '62V4' '62V6' '62I6' '62R4' '62R6' +
     A                                      '62M6' '63V6' '63R6' '64V4' '64V6' +
     A                                      '64I6' '64R4' '64R6' '65M6' '65V6' +
     A                                      '65R6' '66V4' '66V6' '66I6' '66R4' +
     A                                      '66R6' '66M6' '67V6' '67R6' '68V4' +
     A                                      '68V6' '68I6' '68R4' '68R6' '68M6' +
     A                                      '69V4' '69V6' '69I6' '69R4' '69R6' +
     A                                      '69M6')
      *
     A          O SIAC                      COMP(EQ 'Y')
      *
      ** OMIT INTERBRANCH EVENTS TO AVOID MULTIPLE RECORDS
     A          O IBRE                      COMP(EQ 'Y')
      *
     A          R BVNTXEDF                  PFILE(BVNTXED)
     A            RECI
     A            LCNO
     A            CNUM
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            BRCA
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            FACO
     A            SPI
     A            FIPI
     A            INOI
     A            CPCN
     A            CPAR
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            FFIN
     A            LCEQ
     A            OSBR
     A            ZZ070
     A            TIRF
     A            TICM
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
     A          O ETYP                      VALUES('10V1' '10V2' +
     A                                      '14V3' '14V4' '14V5' '14V6' '14V7' +
     A                                      '14V8' '17M3' '17S1' '20V7' '20V8' +
     A                                      '42M8' '42S1' '61D1' '61R5' '62D1' +
     A                                      '62R5' '63D1' '63V2' '64D1' '64R5' +
     A                                      '65D1' '65V2' '66D1' '66R5' '67D1' +
     A                                      '67V2' '70D1' '70M1' '70V1' '71F2' +
     A                                      '80A1' '81A1' '81D2' '81F1' '82M3' +
     A                                      '83A1' '83M1' )
      *
     A          O ETYP                      VALUES('15V6' '15I6' '15M6' '15M8' +
     A                                      '16V6' '16I6' '16M6' '16M8' +
     A                                      '17I6' '17M6' '17M8' '18M6' '61V4' +
     A                                      '61V6' '61I6' '61R4' '61R6' '61M6' +
     A                                      '62V4' '62V6' '62I6' '62R4' '62R6' +
     A                                      '62M6' '63V6' '63R6' '64V4' '64V6' +
     A                                      '64I6' '64R4' '64R6' '65M6' '65V6' +
     A                                      '65R6' '66V4' '66V6' '66I6' '66R4' +
     A                                      '66R6' '66M6' '67V6' '67R6' '68V4' +
     A                                      '68V6' '68I6' '68R4' '68R6' '68M6' +
     A                                      '69V4' '69V6' '69I6' '69R4' '69R6' +
     A                                      '69M6')
      *
     A          S                           ALL
      *
     A          R FPVNTDDF
     A                                      PFILE(FPVNTDD)
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          R FOEVEDF                   PFILE(FOEVED)
     A                                      TEXT('FO EVENTS DETAIL')
     A          K *NONE
     A          K ECSS
     A          K ECCY
     A          K EDAT
     A          K ETYP
      *
     A          R TREVEDF                   PFILE(TREVED)
     A          K *NONE
     A          K ECSS
     A          K TRSC
     A          K TRVD
     A          K TREC
     A          O TREC                      VALUES('    ')
      *
     A          S IBRE                      VALUES('N')
      *
      *
     A          R CPEVEDF                   PFILE(CPEVED)
     A          K *NONE
     A          K ECSS
     A          K CCEC
     A          K CPYD
     A          K CCCE
     A          O CCCE                      VALUES('    ')
      *
     A          R DPEVEDF                   PFILE(DPEVED)
     A          K *NONE
     A          K ECSS
     A          K CCEC
     A          K CCPD
     A          K CCCE
     A          O CCCE                      VALUES('    ')
      *
     A          S IBRE                      VALUES('N')
      *
      *
     A          R EVNTXZZF                  PFILE(EVNTXZZ)
     A          K RECI
      *
     A          R CEVNXZZF                  PFILE(CEVNXZZ)
     A          K RECI
      *
     A          R LVENTZZF                  PFILE(LVENTZZ)
     A          K RECI
