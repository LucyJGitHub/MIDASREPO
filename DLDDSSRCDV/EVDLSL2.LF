     A*****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas DL Events for deposits ladder')                  *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module                                       *
     A*                                                               *
     A*  LF/EVDLSL2 - Events for Deposits Ladder, used by DL2685      *
     A*               if the Midas Leo Integration feature is on      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CSD103             Date 01Oct20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE154             Date 06Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CDL038             Date 10May05               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 218992             Date 17Jul03               *
      *                 CGL015             Date 15Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 136242             Date 10Nov00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 163486             Date 13Oct99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU004             Date 02Feb98               *
      *                 CEU003             Date 05Nov97               *
     A*                 CLE004             Date 02Dec96               *
     A*                 CTI001             Date 15Jul97               *
     A*                 CAC001             Date 01Feb96               *
     A*                 066679             Date 26APR96               *
     A*                 S01486             Date 17JAN95               *
     A*                   S01473           DATE  05JUL94              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE154 - Loan Repayment Schedule Enhancement (Recompile)     *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  218992 - Add Extended settlement fields in LVENTEL(recompile)*
      *  CGL015 - Net Amounts on Events by Nostro (Recompiled)        *
      *  136242 - Took out processing for Internal Contracts: events  *
      *           20V1 and 20V2. Should only be shown on FX Ladder.   *
      *  163486 - Added IFXC and new SIAC field in LVENTEL.           *
      *  CDL006 - Dealing Fiduciary. Recompiled.                      *
     A*  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
     A*           Recompiled due to changes in file LVENTEL.          *
     A*  CEU003 - EMU Dealing Settlement Ccy Conversion               *
     A*  CLE004 - Customer Lending Enhancements - Syndications        *
     A*  CTI001 - Midas / Trade Innovation Interface Phase 1          *
     A*           Recompiled over BVNTXED.                            *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           Recompiled only.                                    *
     A*  066679 - INCLUDE PROCESSING FOR PRINCIPAL INCREASE FOR       *
     A*           LOANS, THAT IS, EVENT TYPE 62V3                     *
     A*  S01486 - PORTFOLIO MANAGEMENT UPGRADE (RECOMPILE)            *
     A*  S01473 - Leo Midas Integration Project. New file with        *
     A*           Leo DL events file BVNTXED for the Lending          *
     A*           Exposure Interface added. Based on LF/EVDLS         *
     A*           Any fixes to this file not involving PF/BVNTXED     *
     A*           should probably be added to LF/EVDLS                *
     A*                                                               *
     A*****************************************************************
     A          R EVNTXECF                  PFILE(EVNTXEC)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          K *NONE
     A          K *NONE
     A          K *NONE
      *
     A          R EVNTXEDF                  PFILE(EVNTXED)
     A            RECI
     A            DLNO
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            BRCA
     A            DTYP
     A            OTHA
     A            FSLI
     A            ICAM
     A            IBRE
     A*                                              Begin /CEU003/
     A            IFXS
     A            SIAC
     A*                                              End   /CEU003/
     A*/COPY WNCPYSRC,EVDLSL2001
     A          K ECCY
     A          K EDAT
     A          K RECI
     A          S RECI                      CMP(EQ 'D')
     A**Start of change 136242
     A**********  ETYP                      VALUES('20V1' '20V2' '15I2' +
     A**********                            '15M1' '15M4' '15V3' '16V1' +
     A**********                            '16I2' '16M1' '16V3' '16M4' +
     A**********                            '17V3' '17I2' '17M3' '18M1' +
     A**********                            '15V1')
     A            ETYP                      VALUES('15I2' '15M1' '15M4' +
     A                                      '15V3' '16V1' '16I2' '16M1' +
     A                                      '16V3' '16M4' '17V3' '17I2' +
     A                                      '17M3' '18M1' '15V1')
     A**End of change 136242
     A            IBRE                      COMP(EQ 'N')
     A*/COPY WNCPYSRC,EVDLSL2002
     A          S RECI                      CMP(EQ 'D')
     A            ETYP                      VALUES('17M1' '17V1')
     A            DTYP                      CMP(EQ 'C1')
     A            IBRE                      COMP(EQ 'N')
     A*/COPY WNCPYSRC,EVDLSL2003
     A          O                           ALL
      *
     A          R LVENTELF                  PFILE(LVENTEL)
     A            RECI
     A            LNRF
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            BRCA
     A            PRNM
     A            INOI
     A            OTHA
     A            IBRE
      *                                                  Begin /163486/
     A            IFXC
     A            SIAC
      *                                                    End /163486/
     A          K ECCY
     A          K EDAT
     A          K RECI
     A          S RECI                      CMP(EQ 'L')
     A            ETYP                      VALUES('61V1' '61I2' '61R1' '61M1' +
     A                                      '62V1' '62I2' '62R1' '62M1' +
     A                                      '63V1' '63M1' +
     A                                      '64V1' '64I2' '64R1' '64M1' +
     A                                      '65V1' '65M1' +
     A*                                              BEGIN /*066679*/
     A*********                             '66V1' '66I2' '66R1' '66M1')
      *                                              Begin /CLE004/
     A***********                           '66V1' '66I2' '66R1' '66M1' '62V3')
     A                                      '66V1' '66I2' '66R1' '66M1' +
     A                                      '62V3' '68V1' '68I2' '68R1' +
     A                                      '68M1' '69V1' '69I2' '69R1' +
     A                                      '69M1' '72V1' '72I2' '72R1' +
     A                                      '72M1' '61R7' '61V5' '61R9' +
     A                                      '61V7' '62R7' '62V5' '62R9' +
     A                                      '62V7' '63R7' '63V5' '63R9' +
     A                                      '63V7' '64R7' '64V5' '64R9' +
     A                                      '64V7' '65R7' '65V5' '65R9' +
     A                                      '65V7' '66R7' '66V5' '66R9' +
     A                                      '66V7' '67R7' '67V5' '67R9' +
     A                                      '67V7')
      *                                              End   /CLE004/
     A*                                                END /*066679*/
     A            IBRE                      COMP(EQ 'N')
     A          O                           ALL
      *
     A          R LVNTEL1F                  PFILE(LVENTEL)
     A            RECI
     A            LNRF
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            BRCA
     A            PRNM
     A            INOI
     A            OTHA
     A            IBRE
      *                                                  Begin /163486/
     A            IFXC
     A            SIAC
      *                                                    End /163486/
     A          K ECCY
     A          K EDAT
     A          K RECI
     A          S RECI                      CMP(EQ 'L')
     A            ETYP                      VALUES('67V1' '67M1')
     A            IBRE                      COMP(EQ 'N')
     A          O                           ALL
      *                                                    /*S01473*/
     A          R BVNTXEDF                  PFILE(BVNTXED)
     A            RECI
     A            LCNO
     A            CNUM
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            BRCA
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            SETP
     A            OSAC1
     A            OSAC2
     A            TSEN
     A            FACO
     A            SPI
     A            FIPI
     A            INOI
     A            CPCN
     A            CPAR
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            FFIN
     A            LCEQ
     A            OSBR
     A*
     A          K ECCY
     A          K EDAT
     A          K RECI
     A          S RECI                      CMP(EQ 'B')
     A            ETYP                      VALUES('81A2' '81M1')
     A          O                           ALL
      *                                                    /*S01473*/
