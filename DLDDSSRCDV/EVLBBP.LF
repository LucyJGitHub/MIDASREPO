      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas DL Exp evts-cus. par (brch lvl) by book brc')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module
     A*                                                               *
     A*  LF/EVLBBP - EXPOSURE EVENTS FOR PARENT BY BOOKING BRANCH     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 05Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CGP001             Date 23May03               *
      *                 CGL013             Date 19Feb02               *
      *                 CGL015             Date 15Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CRT004             DATE 04MAY00               *
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CGL011             Date 20Sep99               *
      *                 CFT004             Date 09Jul99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU003             Date 05Nov97               *
      *                 CTI001             Date 15Jul97               *
     A*                 CDL002             Date 17Sep97               *
     A*                 CLE004             Date 07MAR97               *
     A*                 CAC001             Date 01FEB96               *
     A*                 CRT003             Date 12JUN96               *
     A*                 S01486             Date 20JUL94               *
     A*                 S01433            DATE 07OCT93                *
     A*                 S01449            DATE  8SEP93                *
     A*                 S01437            DATE  8SEP93                *
     A*                 S01434            DATE 26JUL93                *
     A*                 S01378            DATE 15OCT92                *
     A*                 S01393            DATE 30JUL92                *        *
     A*                 S01392            DATE 20JUL92                *
     A*                 S01383            DATE 27APR92                *
     A*                 S01328            DATE 02APR91                *
     A*                 LN0226            DATE 05JUN90                *
     A*                 E205              DATE 22FEB90                *
     A*                 S01157            DATE 09MAR90                *
     A*                 E20               DATE 17/11/89               *
     A*                 E57               DATE 17/11/89               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CGP001 - Global Processing (recompile)                       *
      *  CGL013 - MT94x Message Generation.                           *
      *           Recompiled over SDBRCHPD Changes                    *
      *  CGL015 - Net Amounts on Events by Nostro (Recompiled)        *
      *  CRT004 - Cashier Midas TCP/IP interface.                     *
      *           Recompiled over SDBRCHPD Changes                    *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  CGL011 - Collateral Processing for Midas. Recompile.         *
     A*  CFT004 - Straight Through Processing Phase 2                 *
     A*         - International Bank Account Numbers (Recompile)      *
      *  CDL006 - Dealing Fiduciary. Recompiled.                      *
     A*  CEU003 - EMU Dealing Settlement Ccy Conversion               *
      *  CTI001 - Midas/Trade Innovation Interface Phase 1            *
      *           Recompiled over SDBRCHPD.                           *
     A*  CDL002 - FX Netting. Add Deal No, add customer to key fields *
      *  CLE004 - Lending Enhancements:                               *
      *           Recompiled only.                                    *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           Recompiled only.                                    *
     A*  CRT003 - Branch Automation Interface; additional fields      *
     A*           added to SDBRCHPD - recompile                       *
     A*  S01486 - Recompiled over changed SDBRCHPD.                   *
     A*  S01433 -  Add source for 'Money Market' and                  *
     A*            'Precious Metals'                                  *
     A*  S01449 - Recompiled over changed SDBRCHPD.                   *
     A*  S01437 - Recompiled over changed SDBRCHPD.                   *
     A*                                                               *
     A*   S01434 -  Recompiled - Enhanced Rollovers                   *
     A*                                                               *
     A*   S01378 -  NA parcels. Allows partial sale of a parcel
     A*             of NA's which have previously been entered as
     A*             a single NA purchased transaction. (was MOF53)
     A*             Recompile over changed EVNTXED format.
     A*                                                               *
     A*  S01393 - Recompiled for Strategic Risk Management            *        *
     A*  S01392 - RECOMPILED DUE TO JAP.SUB-MODULE INCORPORATION      *
     A*  S01383 - RECOMPILED DUE TO BASIC RATE TAX INCORPORATION      *
     A*  S01328 - FRA/IRS Revaluations - New fields added to EVNTXED  *
     A*           - Recompile all logicals.                           *
     A*  LN0226 - INCORPORATE FURTHER CHANGES MADE FOR R10 - FRA/IRS. *
     A*           S01117, S01194, S01157 ETC.                         *
     A*  E205   - SHOULD NOT INCLUDE EXTRA EVENTS ADDED FOR INTER-    *
     A*           BRANCH EVENTS BECAUSE THEY ARE CASH FLOW TYPE EVENTS*
     A*           NOT EXPOSURE EVENTS.                                *
     A*  S01157 - FRA/IRS ENHANCEMENT                                 *   S01157
     A*  E20    - CHANGE CRT PARM  MAINT TO *DLY                      *
     A*  E57    - CHANGE KEY FIELD                                    *
     A*                                                               *
      *****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R EVLPF                     JFILE(EVNTXED +
     A                                      SDCLLMPD SDBRCHPD +
      *                                              Begin /S01433/
     A                                      SDLIMTPD SDLIMTPD +
      *                                              End   /S01433/
     A                                      SDLIMTPD SDLIMTPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A** JOIN PARENT TO BRANCH LIMIT
     A          J                           JOIN(1 2)
     A                                      JFLD(PTNU DZCUST)
     A                                      JFLD(ELVB DZLEVL)
     A                                      JFLD(ENTITY DZBCCD)
     A** JOIN BRANCH LIMIT TO BRANCH TABLE
     A          J                           JOIN(1 3)
     A                                      JFLD(ENTITY A8BRCD)
     A** JOIN BRANCH LIMIT TYPES TO LIMIT TYPES TABLE-FX
     A          J                           JOIN(2 4)
     A                                      JFLD(DZFXLT A2LMTY)
     A** JOIN BRANCH LIMIT TYPES TO LIMIT TYPES TABLE-LENDING
     A          J                           JOIN(2 5)
     A                                      JFLD(DZLLTY A2LMTY)
      *                                              Begin /S01433/
     A** JOIN BRANCH LIMIT TYPES TO LIMIT TYPES TABLE-MONEY MARKET
     A          J                           JOIN(2 6)
     A                                      JFLD(DZMMTY A2LMTY)
     A** JOIN BRANCH LIMIT TYPES TO LIMIT TYPES TABLE-PRECIOUS METALS
     A          J                           JOIN(2 7)
     A                                      JFLD(DZPLTY A2LMTY)
      *                                              End   /S01433/
     A**
     A** EVENT FIELDS
      *                                                Begin /*CDL002*/
     A            ENTITY         3A         RENAME(BRCA) JREF(1)
      *                                                End   /*CDL002*/
     A            PASK
     A            PTNU
      *                                                Begin /*CDL002*/
     A            CASK
      *                                                End   /*CDL002*/
     A            CUNR                      JREF(1)
     A            EDAT
     A            LCEQ
     A            EXEI
     A            IBRE
     A            ICYE
      *                                                Begin /*CDL002*/
     A            DLNO
      *                                                End   /*CDL002*/
      *                                              Begin /S01433/
     A            ECCY
     A            OTHC
      *                                              End   /S01433/
     A*                                              Begin /CEU003/
     A            IFXS
     A            SIAC
     A*                                              End   /CEU003/
     A*                                                      /**E57*/
     A*****       ENTITY         3A         RENAME(EBRC) JREF(1)
      *                                                Begin /*CDL002*/
     A************ENTITY         3A         RENAME(BRCA) JREF(1)
      *                                                End   /*CDL002*/
     A*                                                      /**E57*/
     A            NAME                      RENAME(PRNM) JREF(1)
     A            TOWN                      RENAME(PTWN) JREF(1)
     A** BRANCH TABLE FIELD
     A            ENAM                      RENAME(A8BRNM) JREF(3)
     A** BRANCH LIMIT FIELDS - FX
      *                                              Begin /S01433/
     A*********** DZFXLT
     A*********** DZFLAM
     A*********** DZFLED
     A            DZFXLT                    JREF(2)
     A            DZFLAM                    JREF(2)
     A            DZFXFA                    JREF(2)
     A            DZFLED                    JREF(2)
     A** BRANCH LIMIT FIELDS - LENDING
     A*********** DZLLTY
     A*********** DZLLAM
     A*********** DZLLED
     A            DZLLTY                    JREF(2)
     A            DZLLAM                    JREF(2)
     A            DZLLED                    JREF(2)
     A** BRANCH LIMIT FIELDS - MONEY MARKET
     A            DZMMTY                    JREF(2)
     A            DZMMAM                    JREF(2)
     A            DZMMED                    JREF(2)
     A** BRANCH LIMIT FIELDS - PRECIOUS METALS
     A            DZPLTY                    JREF(2)
     A            DZPFAM                    JREF(2)
     A            DZPSAM                    JREF(2)
     A            DZPLED                    JREF(2)
     A*                                                *** S01157 ***
     A** BRANCH LIMIT FIELDS - FRA
     A*********** DZFRLA
     A*********** DZFRED
     A            DZFRLA                    JREF(2)
     A            DZFRED                    JREF(2)
     A** BRANCH LIMIT FIELDS - IRS
     A*********** DZIRLM
     A*********** DZIRED
     A            DZIRLM                    JREF(2)
     A            DZIRED                    JREF(2)
      *                                              End   /S01433/
     A*                                                *** S01157 ***
     A** BRANCH LIMIT TYPES - FX
     A            FLEP1                     RENAME(A2E1LP) JREF(4)
     A            FLEP2                     RENAME(A2E2LP) JREF(4)
     A            FLEP3                     RENAME(A2E3LP) JREF(4)
     A            FLEP4                     RENAME(A2E4LP) JREF(4)
     A            FLEP5                     RENAME(A2E5LP) JREF(4)
     A            FLEP6                     RENAME(A2E6LP) JREF(4)
     A            FLPC1                     RENAME(A2PL1P) JREF(4)
     A            FLPC2                     RENAME(A2PL2P) JREF(4)
     A            FLPC3                     RENAME(A2PL3P) JREF(4)
     A            FLPC4                     RENAME(A2PL4P) JREF(4)
     A            FLPC5                     RENAME(A2PL5P) JREF(4)
     A            FLPC6                     RENAME(A2PL6P) JREF(4)
     A** BRANCH LIMIT TYPES - LENDING
     A            LLEP1                     RENAME(A2E1LP) JREF(5)
     A            LLEP2                     RENAME(A2E2LP) JREF(5)
     A            LLEP3                     RENAME(A2E3LP) JREF(5)
     A            LLEP4                     RENAME(A2E4LP) JREF(5)
     A            LLEP5                     RENAME(A2E5LP) JREF(5)
     A            LLEP6                     RENAME(A2E6LP) JREF(5)
     A            LLPC1                     RENAME(A2PL1P) JREF(5)
     A            LLPC2                     RENAME(A2PL2P) JREF(5)
     A            LLPC3                     RENAME(A2PL3P) JREF(5)
     A            LLPC4                     RENAME(A2PL4P) JREF(5)
     A            LLPC5                     RENAME(A2PL5P) JREF(5)
     A            LLPC6                     RENAME(A2PL6P) JREF(5)
      *                                              Begin /S01433/
     A** BRANCH LIMIT TYPES - MONEY MARKET
     A            MLEP1                     RENAME(A2E1LP) JREF(6)
     A            MLEP2                     RENAME(A2E2LP) JREF(6)
     A            MLEP3                     RENAME(A2E3LP) JREF(6)
     A            MLEP4                     RENAME(A2E4LP) JREF(6)
     A            MLEP5                     RENAME(A2E5LP) JREF(6)
     A            MLEP6                     RENAME(A2E6LP) JREF(6)
     A            MLPC1                     RENAME(A2PL1P) JREF(6)
     A            MLPC2                     RENAME(A2PL2P) JREF(6)
     A            MLPC3                     RENAME(A2PL3P) JREF(6)
     A            MLPC4                     RENAME(A2PL4P) JREF(6)
     A            MLPC5                     RENAME(A2PL5P) JREF(6)
     A            MLPC6                     RENAME(A2PL6P) JREF(6)
     A** BRANCH LIMIT TYPES - PRECIOUS METALS
     A            PLEP1                     RENAME(A2E1LP) JREF(7)
     A            PLEP2                     RENAME(A2E2LP) JREF(7)
     A            PLEP3                     RENAME(A2E3LP) JREF(7)
     A            PLEP4                     RENAME(A2E4LP) JREF(7)
     A            PLEP5                     RENAME(A2E5LP) JREF(7)
     A            PLEP6                     RENAME(A2E6LP) JREF(7)
     A            PLPC1                     RENAME(A2PL1P) JREF(7)
     A            PLPC2                     RENAME(A2PL2P) JREF(7)
     A            PLPC3                     RENAME(A2PL3P) JREF(7)
     A            PLPC4                     RENAME(A2PL4P) JREF(7)
     A            PLPC5                     RENAME(A2PL5P) JREF(7)
     A            PLPC6                     RENAME(A2PL6P) JREF(7)
      *                                              End   /S01433/
     A          K ENTITY
     A          K PASK
     A          K PTNU
     A          S EXEI                      COMP(NE ' ')
     A            PTNU                      COMP(NE ' ')
     A            PTNU                      COMP(NE '000000')
      *                                                               /*E205*/
     A            IBRE                      COMP(NE 'Y')
      *                                                               /*E205*/
