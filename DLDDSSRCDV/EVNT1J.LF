      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas DL Joint EVNTXED and DEALSDG - FRA/IRS')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module                                       *
     A*                                                               *
     A*     JLF/EVNT1J - JOIN LOGICAL OF PF/EVNTXED & PF/DEALSDG      *
     A*                  FOR FRA/IRS ENHANCEMENTS TO DEALING          *
     A*                  USED BY DL0660                               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CAS008             Date 16Jun04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 217684             Date 12May03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 CGL015             Date 15Apr02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW097             Date 20May99               *
      *                 CPL002             Date 08Mar99               *
      *                 CAP002             Date 08May98               *
     A*                 CEU003             Date 05Nov97               *
     A*                 CSW005             Date 01Aug96               *
     A*                 CAC001             Date 01Feb96               *
     A*                 CIR002             Date 13Jun95               *
     A*                 CIR001             Date 13Jun95               *
     A*                 S01486             DATE 06JUN94               *
     A*                 S01434             DATE 27JUL93               *
     A*                 S01378             DATE 15OCT92               *
     A*                 S01393             DATE 29JUL92               *
     A*                 S01392             DATE 20JUL92               *
     A*                 AUS021             DATE 06AUG92               *
     A*                 S01354             DATE 09JAN92               *
     A*                 S01328             DATE 02APR91               *
     A*                 S01157             DATE 30/10/89              *
     A*                 S01153             DATE 06/07/88              *
     A*                 S01117             DATE 05/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CGL015 - Net Amounts on Events by Nostro (Recompiled)        *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *           (Recompile)                                         *
      *  CIR005 - FRA/IRS Business Day Conventions. (Recompile)       *
      *  CDL006 - Dealing Fiduciary. Recompiled.                      *
     A*  CSW097 - SWIFT 1997 Changes for FRAIRS MT34* and MT36*.      *
     A*           Recompiled over changed DEALSDG.                    *
     A*  CPL002 - Midas-Plato Interface (Recomplied)                  *
      *  CAP002 - Recompiled for converion of Midas inputs to APIs    *
     A*  CEU003 - EMU Dealing Settlement Ccy Conversion               *
     A*  CSW005 - MG34n and MG36n Message Generation                  *
     A*           Recompiled over changed DEALSDG                     *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           Recompiled only.                                    *
     A*  CIR002 - Compounding of Interest.  Recompiled.               *
     A*  CIR001 - Interest Rate Calendars.                            *
     A*   S01486 -  Portfolio Management Upgrade (RECOMPILE)          *
     A*   S01434 -  Recompiled - Enhanced Rollovers                   *
     A*                                                               *
     A*   S01378 -  NA parcels. Allows partial sale of a parcel
     A*             of NA's which have previously been entered as
     A*             a single NA purchased transaction. (was MOF53)
     A*             Include Parcel flag from changed  EVNTXED format.
     A*  S01393 - Recompiled for Strategic Risk Management            *
     A*  S01392 - RECOMPILED DUE TO JAP.SUB-MODULE INCORPORATION      *
     A*   AUS021 - AUSTRALIAN PRODUCT. Includes two new settlement    *
     A*            formulas for FRAs. Initially includes one for      *
     A*            Australia and one for USA.                         *
     A*            RECOMPILED OVER DEALSDG                            *
     A*                                                               *
     A*  S01354 - Select Internal Loans                               *
     A*  S01328 - FRA/IRS Revaluations - New fields added to          *
     A*           EVNTXED and DEALSDG - Recompile all logicals.       *
     A*  S01157 - RECOMPILED FOR FRA/IRS ENHANCEMENTS                 *
     A*  S01153 - CLOSE OF BUSINESS IMPROVEMENTS - ADDITION OF        *
     A*           CHANGES FOR S01119 - BASE RATE TYPE FIELD ADDED     *
     A*           FOR FRA/IRS.                                        *
     A*  S01117 - RELEASE 10 MULTIBRANCHING + CHG MAINT OPTION        *
     A*                                                               *
     A*****************************************************************
     A***                                                 /*S01153*/
     A                                      DYNSLT
     A***                                                 /*S01153*/
     A                                      JDFTVAL
     A          R EVNT1JF                   JFILE(EVNTXED DEALSDG)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          J                           JOIN(1 2)
     A                                      JFLD(DLNO DLNO)
     A*
     A**  EVENTS FILE FIELDS
     A*
     A            RECI                      JREF(1)
     A            DLNO                      JREF(1)
      *                                                   /S01117/
     A************CNUM                      JREF(1)
     A            CUNR                      JREF(1)
      *                                                   /S01117/
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            DTYP                      JREF(1)
     A            DLST                      JREF(1)
     A            EXRT
     A            NOTD
     A            OTHD
     A            OTHA
     A            OTHC
     A            RLVI
     A            CRNM
     A            CTWN
     A            CASK
     A            FFIN
     A            FSLI
     A            ICIR
     A            ICAM
     A            RBDA
     A            FFVD
     A            TAXI                      JREF(1)
     A*
     A**  FRA/IRS FILE FIELDS
     A*
     A            UBRTT
     A            TBRTT
     A            UEINR
     A            TEINR
     A            AACRT
     A            CLRAT
     A            FLRAT
     A            UDSTI
     A            TDSTI
     A            UDNSI
     A            TDNSI
      *                                                               /S01117/
     A            IBRE
      *                                                   /S01378/
     A            PCLI
      *                                                   /S01378/
      *                                                               /S01117/
     A*                                              Begin /CEU003/
     A            RSCY
     A            PSCY
     A            IC72
     A            IFXS
     A            SIAC
     A*                                              End   /CEU003/
     A*                                              Begin /CDL038/
     A            CLAS                      JREF(1)
     A*                                              End   /CDL038/
     A*
     A          K CASK
      *                                                   /S01117/
     A**********K CNUM
     A          K CUNR
      *                                                   /S01117/
     A          K DLNO
     A          K ETYP
     A*
     A***                                                 /*S01153*/
     A          S ETYP                      VALUES('11V1' '12V1'  +
     A                                      '13V1' '14V1'  '20V1' +
     A                                      '16M1' '18M1' '15M1' +
     A                                      '17M1' '17S1' '21O1' +
     A                                      '22O1' '23O1' '24O1')
      *                                                   /S01117/
      ** OMIT INTERBRANCH EVENTS TO AVOID MULTIPLE RECORDS
     A            IBRE                      COMP(EQ 'N')
     A*                                              BEGIN /S01354/
     A          S DTYP                      COMP(EQ 'IL')
     A            ETYP                      COMP(EQ '15M1')
     A          S DTYP                      COMP(EQ 'ID')
     A            ETYP                      COMP(EQ '16M1')
     A*                                              END   /S01354/
      *                                                   /S01117/
     A          O                           ALL
     A***                                                 /*S01153*/
