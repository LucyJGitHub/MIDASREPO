      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
/*EXI *  TEXT('Midas DL Member added by copy file')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module
     A*                                                               *
     A*  LF/EVNTL5 - SORT OF DL EVENTS - EVENT DATE SEQUENCE          *
     A*              USED BY DL0670                                 *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL015             Date 15Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU003             Date 05Nov97               *
      *                 CAC001             Date 01Feb96               *
     A*                 S01486             Date 06Jun94               *
     A*                 S01434             Date 27Jul93               *
     A*                 S01378        DATE  15OCT92                   *
     A*                 S01393        DATE  29JUL92                   *
     A*                 S01392        DATE  20JUL92                   *
     A*                 S01328        DATE  02APR91                   *
     A*                 E20           DATE  16/11/89                  *
     A*                 E57           DATE  16/11/89                  *
     A*                 S01211        DATE  30/10/89                  *
     A*                 S01117        DATE  05/12/88                  *
     A*                                                               *
     A*---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CGL015 - Net Amounts on Events by Nostro (Recompiled)        *
      *  CDL006 - Dealing Fiduciary. Recompiled.                      *
     A*  CEU003 - EMU Dealing Settlement Ccy Conversion               *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           Recompiled only.                                    *
     A*   S01486 -  Portfolio Management Upgrade                      *
     A*   S01434 -  Recompiled - Enhanced Rollovers                   *
     A*                                                               *
     A*   S01378 -  NA parcels. Allows partial sale of a parcel
     A*             of NA's which have previously been entered as
     A*             a single NA purchased transaction. (was MOF53)
     A*             Recompile over changed EVNTXED format.
     A*  S01393 - Book Code added to PF/EVNTXED for Strategic         *
     A*           Risk Management Release 10.                         *
     A*           Also two new events omitted from the processing.    *
     A*  S01392 - RECOMPILED DUE TO JAP.SUB-MODULE INCORPORATION      *
     A*  S01328 - FRA/IRS Revaluations - New fields added to EVNTXED. *
     A*           Remove ZZ005 and recompile all logicals.            *
     A*  E20    - CHANGE CRT PARM  MAINT TO *DLY                      *
     A*  E57    - ERROR IN SELECT/OMIT                                *
      *  S01211 - Omit new FRA/IRS events generated for SRM.          *
     A*  S01117 - RELEASE 10 MULTIBRANCHING                           *
     A*                                                               *
      *****************************************************************
     A          R EVNTXECF                  PFILE(EVNTXEC)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          K RECI
     A***
     A          R EVNTXEDF                  PFILE(EVNTXED)
     A            RECI
     A            DLNO
     A************CNUM                                                    S01117
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A*                                                  /S01117/
     A*********** BRCD
      *                                                                   S01117
     A**********  BRCA      R
     A            EBRC      R
      *                                                                   S01117
     A*                                                  /S01117/
     A            EXRT
     A            NGII
     A            NOTD
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            LCEQ
     A            SETP           2A
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            ZZ003
     A            FIPI
     A            INOI
     A            FCTI
     A            RLVI
     A            PTNU
     A*********** CPAR                                                    S01117
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            DASN
     A            FFVD
     A            FFIN
     A            FACT
     A            FCNO
     A            FACO
     A            SPI
     A            CPCN
     A            FSLI
     A            ICIR
     A            ICAM
     A            RBDA
     A            OCFF
     A*                                                    /S01117/
     A            ORBR
     A**********  COBB
     A            COOB
     A            ECPY
     A            IBRE
     A            ICYE
      *                                                                   S01117
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
      *                                                                   S01117
     A            OSBR
     A**********  ZZ007
     A*                                              START /S01328/
     A************ZZ005
     A*                                              END   /S01328/
      *                                                                   S01117
     A************ZZ023
      *                                                                   S01117
     A*                                                    /S01117/
     A            TAXI
      *                                              Begin /S01486/
     A            PTFR
      *                                              End   /S01486/
     A*                                              Begin /CEU003/
     A            IFXS
     A            SIAC
     A*                                              End   /CEU003/
     A***
     A          K *NONE
     A*                                                    /S01117/
     A          K EBRC
     A*                                                    /S01117/
     A          K EDAT
     A          K FFVD
     A          K CASK
     A          K CUNR
     A**********K CNUM                                                    S01117
     A          K DLNO
     A***
     A          S FIPI                      COMP(EQ 'N')
     A            SETP                      COMP(NE '03')
     A            SETP                      COMP(NE '06')
     A            ETYP                      COMP(NE '12V1')
     A            ETYP                      COMP(NE '12V2')
      * S01211
     A            ETYP                      COMP(NE '21S1')
     A            ETYP                      COMP(NE '21S2')
     A            ETYP                      COMP(NE '21S3')
     A            ETYP                      COMP(NE '22S1')
     A            ETYP                      COMP(NE '22S2')
     A            ETYP                      COMP(NE '23S1')
     A            ETYP                      COMP(NE '23S2')
     A            ETYP                      COMP(NE '24S1')
     A            ETYP                      COMP(NE '24S2')
      * S01211
      * S01393
     A            ETYP                      COMP(NE '22S3')
     A            ETYP                      COMP(NE '22S4')
      * S01393
     A*                                                      /**E57*/
     A            IBRE                      COMP(EQ 'N')
     A*                                                      /**E57*/
     A          S FIPI                      COMP(EQ 'N')
     A            SETP                      COMP(NE '03')
     A            SETP                      COMP(NE '06')
     A            ETYP                      VALUES('12V1')
     A            EAMT                      COMP(NE 0)
     A*                                                      /**E57*/
     A            IBRE                      COMP(EQ 'N')
     A*                                                      /**E57*/
     A          S FIPI                      COMP(EQ 'N')
     A            SETP                      COMP(NE '03')
     A            SETP                      COMP(NE '06')
     A            ETYP                      VALUES('12V2')
     A            EAMT                      COMP(NE 0)
     A*                                                      /**E57*/
     A            IBRE                      COMP(EQ 'N')
     A*                                                      /**E57*/
     A          O                           ALL
