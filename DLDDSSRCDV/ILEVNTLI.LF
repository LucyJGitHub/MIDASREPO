      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_DL                                                        *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  LF/ILEVNTLI -   MIDAS MERGED EVENTS FILE                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. MD053008           Date 12Mar18               *
      *  Prev Amend No. DUG503             Date 04Apr18               *
      *                 DUG405             Date 29Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 163486             Date 13Oct99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU011  *CREATE    Date 11Mar98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD053008 - Misaligned details in report GLD956P1             *
      *           - Recompile due to changed (IEVNTXED)               *
      *  DUG503 - UGB FED CARS Upgrade                                *
      *           DUG405 - Events by Value Date Reporting.            *
      *           Based on DLEVNIL9. Order of keys changed as in      *
      *           S20039.                                             *
      *  163486 - Added IFXC and new SIAC field in LVENTEL.           *
      *  CDL006 - Dealing Fiduciary. Recompiled.                      *
      *  CEU011 - EMU Position/Risk Reporting.                        *
      *                                                               *
      *****************************************************************
     A          R EVNTXECF                  PFILE(IEVNTXEC)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A          K RECI
     A          R EVNTXEDF                  PFILE(IEVNTXED)
     A            RECI
     A            DLNO
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            BRCA
     A            EBRC
     A            EXRT
     A            NGII
     A            NOTD
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            LCEQ
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            ZZ003
     A            FIPI
     A            INOI
     A            FCTI
     A            RLVI
     A            PTNU
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            DASN
     A            FFVD
     A            FFIN
     A            FACT
     A            FCNO
     A            FACO
     A            SPI
     A            CPCN
     A            FSLI
     A            ICIR
     A            ICAM
     A            RBDA
     A            OCFF
     A            ORBR
     A            COOB
     A            ECPY
     A            IBRE
     A            ICYE
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
     A            OSBR
     A            TAXI
     A            FUID
     A            RBDN
     A            IFXS
     A            SIAC
      *
     A          K *NONE
     A          K EBRC
     A****      K ECSS                                                          DUG405
     A****      K ECCY                                                          DUG405
     A          K EDAT
     A          K ECSS                                                          DUG405
     A          K ECCY                                                          DUG405
     A          K ETYP
     A          O DTYP                      VALUES('IL' 'ID')
     A            IBRE                      COMP(EQ 'Y')
      *
     A**        R CEVNXCCF                  PFILE(CEVNXCC)
     A**        K *NONE
     A**        K BRCA
     A****      K ECSS
     A****      K ECCY
     A**        K EDAT
     A**        K ECSS
     A**        K ECCY
     A**        K ETYP
      *
     A          R LVENTELF                  PFILE(LVENTEL)
     A            RECI
     A            LNRF
     A            CUNR
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            LTYP
     A            SUTP
     A            EBRC
     A            INTR
     A            NGII
     A            OTHD
     A            OTHA
     A            BCEQ
     A            DFCE
     A            SETP
     A            OSAC
     A            TSEN
     A            ECDP
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            INOI
     A            CRSK
     A            CDOM
     A            REGC
     A            PARI
     A            PTNU
     A            RCSI
     A            FACT
     A            FCNO
     A            RDOM
     A            LASN
     A            FREO
     A            WTIN
     A            WTRT
     A            CPAM
     A            AUTO
     A            MRIN
     A            ACOF
     A            ECPY
     A            IBRE
     A            ICYE
     A            EXEI
     A            ELVB
     A            ELVC
     A            ELVS
     A            BRCA
     A            COBB
     A            OLNO
      *                                                  Begin /163486/
     A            IFXC
     A            SIAC
      *                                                    End /163486/
      *
     A          K *NONE
     A          K EBRC
     A****      K ECSS                                                          DUG405
     A****      K ECCY                                                          DUG405
     A          K EDAT
     A          K ECSS                                                          DUG405
     A          K ECCY                                                          DUG405
     A          K ETYP
     A          S AUTO                      COMP(EQ 'Y')
     A          O                           ALL
      *
     A          R BVNTXEDF                  PFILE(BVNTXED)
     A            RECI
     A            LCNO
     A            CNUM
     A            ETYP
     A            EDAT
     A            EAMT
     A            ECCY
     A            ECSS
     A            DTYP
     A            DLST
     A            BRCA
     A            OTHD
     A            OTHA
     A            OTHC
     A            DBCE
     A            BCEQ
     A            DBCR
     A            SETP
     A            OSAC                      CONCAT(OSAC1 OSAC2)
     A            TSEN
     A            FACO
     A            SPI
     A            FIPI
     A            INOI
     A            CPCN
     A            CPAR
     A            PARI
     A            CRNM
     A            CTWN
     A            CASK
     A            PRNM
     A            PTWN
     A            PASK
     A            CCTZ
     A            CLOC
     A            REGC
     A            FFIN
     A            LCEQ
     A            OSBR
     A            ZZ070
     A            TIRF
     A            TICM
     A          K *NONE
     A          K BRCA
     A****      K ECSS                                                          DUG405
     A****      K ECCY                                                          DUG405
     A          K EDAT
     A          K ECSS                                                          DUG405
     A          K ECCY                                                          DUG405
     A          K ETYP
      *
     A          R FPVNTDDF
     A                                      PFILE(FPVNTDD)
     A          K *NONE
     A          K BRCA
     A****      K ECSS                                                          DUG405
     A****      K ECCY                                                          DUG405
     A          K EDAT
     A          K ECSS                                                          DUG405
     A          K ECCY                                                          DUG405
     A          K ETYP
     A          O IBRE                      COMP(EQ 'Y')
      *
     A          R FOEVEDF                   PFILE(FOEVED)
     A                                      TEXT('FO EVENTS DETAIL')
     A          K *NONE
     A          K EBRC
     A****      K ECSS                                                          DUG405
     A****      K ECCY                                                          DUG405
     A          K EDAT
     A          K ECSS                                                          DUG405
     A          K ECCY                                                          DUG405
     A          K ETYP
      *
     A          R TREVEDF                   PFILE(ITREVED)
     A          K *NONE
     A          K TRBA
     A****      K ECSS
     A****      K TRSC
     A          K TRVD
     A          K ECSS
     A          K TRSC
     A          K TREC
      *
     A          R CPEVEDF                   PFILE(CPEVED)
     A          K *NONE
     A          K CCBA
     A****      K ECSS
     A****      K CCEC
     A          K CPYD
     A          K ECSS
     A          K CCEC
     A          K CCCE
      *
     A          O CCCE                      VALUES('    ')
      *
     A          R DPEVEDF                   PFILE(DPEVED)
     A          K *NONE
     A          K CCBA
     A****      K ECSS
     A****      K CCEC
     A          K CCPD
     A          K ECSS
     A          K CCEC
     A          K CCCE
      *
     A          O CCCE                      VALUES('    ')
      *
     A          R BPEVEDF                   PFILE(BPEVED)
     A          K *NONE
     A          K CCBA
     A****      K ECSS
     A****      K CCEC
     A          K CCDT
     A          K ECSS
     A          K CCEC
     A          K CCCE
     A          O CCCE                      VALUES('    ')
      *
     A          R EVNTXZZF                  PFILE(IEVNTXZZ)
     A          K RECI
     A*          R CEVNXZZF                  PFILE(CEVNXZZ)
     A*          K RECI
     A          R LVENTZZF                  PFILE(LVENTZZ)
     A          K RECI
      *
     A          R BVNTXEZF
     A                                      PFILE(BVNTXEZ)
     A          K RECI
      *
      **  FUNDS TRANSFER
     A          R FPVNTZZF                  PFILE(FPVNTZZ)
     A          K RECI
     A          R FOEVEZZF                  PFILE(FOEVEZZ)
     A          K RECI 