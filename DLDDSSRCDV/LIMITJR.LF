      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas DL Region limits j/to limit types')
     A**************************************************************
     A*                                                            *
     A*  Midas - Dealing Module
     A*                                                            *
     A*   LF/LIMITJR -REGION  LIMITS BY REGION / ENTITY            *
     A*               LIMITS JOINED TO LIMIT TYPES                 *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMENDMENT NO. 00            DATE 00/00/00           *
     A*                                                            *
     A*****************************************************************
     A*
     A                                      JDFTVAL
     A          R LIMITJRF                  JFILE(SDRGLMPD +
     A                                      SDLIMTPD SDLIMTPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A**  JOIN FX LIMIT TYPE TO LIMIT TYPES TABLE
     A          J                           JOIN(1 2)
     A                                      JFLD(DYFXLT A2LMTY)
     A*
     A**  JOIN LENDING LIMIT TYPE TO LIMIT TYPES TABLE
     A          J                           JOIN(1 3)
     A                                      JFLD(DYLLTY A2LMTY)
     A*
     A**  REGION   LIMIT FIELDS
     A            DYRGCD
     A            DYLEVL
     A            DYBCCD
     A**  REGION  LIMIT FIELDS
     A            DYFLAM
     A            DYFXLT
     A            DYFLED
     A            DYLLAM
     A            DYLLTY
     A            DYLLED
     A*
     A*
     A* FRA/IRA LIMIT FIELDS
     A            DYFRLA
     A            DYIRLM
     A            DYFRED
     A            DYIRED
     A*
     A*
     A**  FX LIMIT  TYPES FIELDS
     A            FLEP1                     RENAME(A2E1LP) JREF(2)
     A            FLEP2                     RENAME(A2E2LP) JREF(2)
     A            FLEP3                     RENAME(A2E3LP) JREF(2)
     A            FLEP4                     RENAME(A2E4LP) JREF(2)
     A            FLEP5                     RENAME(A2E5LP) JREF(2)
     A            FLEP6                     RENAME(A2E6LP) JREF(2)
     A            FLPC1                     RENAME(A2PL1P) JREF(2)
     A            FLPC2                     RENAME(A2PL2P) JREF(2)
     A            FLPC3                     RENAME(A2PL3P) JREF(2)
     A            FLPC4                     RENAME(A2PL4P) JREF(2)
     A            FLPC5                     RENAME(A2PL5P) JREF(2)
     A            FLPC6                     RENAME(A2PL6P) JREF(2)
     A*
     A*
     A**  LENDING LIMIT  TYPES FIELDS
     A            LLEP1                     RENAME(A2E1LP) JREF(2)
     A            LLEP2                     RENAME(A2E2LP) JREF(2)
     A            LLEP3                     RENAME(A2E3LP) JREF(2)
     A            LLEP4                     RENAME(A2E4LP) JREF(2)
     A            LLEP5                     RENAME(A2E5LP) JREF(2)
     A            LLEP6                     RENAME(A2E6LP) JREF(2)
     A            LLPC1                     RENAME(A2PL1P) JREF(2)
     A            LLPC2                     RENAME(A2PL2P) JREF(2)
     A            LLPC3                     RENAME(A2PL3P) JREF(2)
     A            LLPC4                     RENAME(A2PL4P) JREF(2)
     A            LLPC5                     RENAME(A2PL5P) JREF(2)
     A            LLPC6                     RENAME(A2PL6P) JREF(2)
     A*
     A*
     A          K DYRGCD
     A          K DYLEVL
     A          K DYBCCD
