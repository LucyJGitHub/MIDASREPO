      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas DL Initialisation program - Audit file')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/DL000018AU - Audit Report for initialisation program    *
      *                    DL000018                                   *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS008  *CREATE    Date 16Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS008 - IAS39 Enhancements                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE DL000018AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    35'INITIALIZE CROSS CURRENCY I+
     A                                      RS DEALS - AUDIT REPORT'
      *
     A                                      SPACEA(1)
     A                                    40'---------------------------+
     A                                      -------------------'
     A                                      SPACEA(3)
      *
      *----------------------*
      ***  File Controls     *
      *----------------------*
      *
     A          R CONTROL
     A                                    84'CALCULATED'
     A                                      SPACEA(1)
     A                                    84'----------'
     A                                      SPACEA(1)
      *
     A                                     6'TOTAL NUMBER OF LIVE IRS +
     A                                      RECORDS:'
     A            WCOUNTF        9  0     85EDTCDE(3) SPACEA(2)
      *
     A                                     6'NO. OF RECORDS UPDATED IN +
     A                                      PF/DEALSDG (IRS Back Office)  :'
     A            WCOUNTDG       9  0     85EDTCDE(3) SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS                    SPACEB(3)
     A                                    52'*** No Details to Report ***'
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      *----------------------*
      ***  Database Error    *
      *----------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
