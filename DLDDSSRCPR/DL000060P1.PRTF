      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Exchange Rate Expected Detail Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL000060P1 - Exchange Rate Expected Detail Report       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CIR014  *CREATE    Date 11May06               *
      *  Prev Amend No. Xnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CIR014 - Mark to Market Cross-currency Interest Rate Swaps   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2006')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                   121
     A                                      'PAGE'
     A                                     3
     A                                      'REFERENCE DL000060P1'
     A            BJURPT    R             40
     A                                      REFFLD(URPT)
     A                                   104
     A                                      'DATE'
     A            BJMRDT    R            110
     A                                      REFFLD(MRDT)
     A                                   121
     A                                      'PAGE'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   104
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   109
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    52
     A                                      'EXCHANGE RATE EXPECTED REPORT'
     A                                      SPACEB(002)
     A                                    99
     A                                      ' '
     A                                    52
     A                                      '-----------------------------'
     A                                      SPACEB(001)
      *
     A  37                                 3
     A                                      'BRANCH - '
     A                                      SPACEB(002)
     A  37        RBRCD     R             12
     A                                      REFFLD(BRCA)
     A  37        RBRNM     R             16
     A                                      REFFLD(BRNM)
     A                                     5
     A                                      'Deal'
     A                                      SPACEB(002)
     A                                    11
     A                                      'Deal Type'
     A                                    23
     A                                      'Value'
     A                                    31
     A                                      'Maturity'
     A                                    41
     A                                      'Our/Their'
     A                                    59
     A                                      'Our/Their'
     A                                    70
     A                                      'Principal to'
     A                                    86
     A                                      'Days Before'
     A                                   102
     A                                      'Next Interest'
     A                                   117
     A                                      'Frequency'
     A                                   128
     A                                      'Day'
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    11
     A                                      'Sub-Type'
     A                                    23
     A                                      'Date'
     A                                    33
     A                                      'Date'
     A                                    41
     A                                      'Currency'
     A                                    59
     A                                      'Principal'
     A                                    70
     A                                      'Revalue Ind'
     A                                    84
     A                                      'Interest Payment'
     A                                   102
     A                                      'Payment Date'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEA(001)
     A            RDLNO     R              3
     A                                      REFFLD(DLNO)
     A            RDLTP     R             13
     A                                      REFFLD(DTYP)
     A            RDLST     R             16
     A                                      REFFLD(DLST)
     A            RVALD          7        22
     A                                      TEXT('Value date')
     A            RMATD          7        31
     A                                      TEXT('Maturity Date')
     A            ROTCY     R             43
     A                                      REFFLD(BUYC)
     A            RPAMT         19   O    51
     A            RPRNI          1A  O    75
     A            RDAYB          7S 0O    89
     A                                      EDTCDE(4)
     A            RNXTD          7       105
     A                                      TEXT('Next Interest Payment Date')
     A            RFREQ         11A  O   116
     A            RDAY           2S 0O   128
     A                                      EDTCDE(4)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                48
     A                                      '***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(001)
     A  37        RBRCD     R             78
     A                                      REFFLD(BRCA)
     A                                    82
     A                                      '***'
     A          R TRAILP2
     A  37                                55
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(001)
