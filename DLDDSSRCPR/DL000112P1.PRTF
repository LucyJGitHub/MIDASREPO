      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Cancelled Deals List')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL000112P1 - Midas DL Cancelled Deals List                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2017            *
      *                                                               *
      *  Last Amend No. CDL098 *CREATE     Date 06Oct17               *
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL098 - Cancelled Deals List                                *
      *                                                               *
      *****************************************************************
      *
     A          R DL000112H1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2017')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'DL000112P1'
     A            TITL          53        40
     A                                   101'DATE'
     A            RUNA           7       106
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    49'DEAL CANCELLATION LIST'
     A                                      SPACEA(1)
     A                                    49'----------------------'
     A                                      SPACEA(2)
     A                                     4'DEAL NUMBER'
     A                                    31'AUTHORISATION'
     A                                    64'ORIGINAL ENTRY DATE'
     A                                    86'LAST CHANGE'
     A                                   101'CHANGE TYPE'
     A                                      SPACEA(2)
     A          R DL000112D1                SPACEB(3)
     A                                     7'REFERENCE DL000112P1'
     A                                    45'PRINT TERMINATED, '
     A                                    63 'DATABASE IN ERROR.'
     A                                      SPACEA(3)
      *
     A          R DL000112D2
     A            DLNO           6A        7
     A            AUTH          30A       25
     A            VALD           7A       66
     A            LCDA           7A       88
     A            CHTP3          3A      106
     A                                      SPACEA(2)
      *
     A          R DL000112T1                SPACEB(3)
     A                                    15'TOTAL NUMBER OF DEAL '
     A                                    37'CANCELLATIONS - '
     A            NROF           4  0     52EDTCDE(3)
     A                                      SPACEA(3)
      *
     A          R DL000112T2
     A                                      SPACEB(3)
     A                                    57'*** NO DETAILS TO REPORT ***'
     A                                      SPACEA(0)
     A          R DL000112T3
     A                                      SPACEB(3)
     A                                    57'*** END OF REPORT ***'
     A                                      SPACEA(0)
      *
