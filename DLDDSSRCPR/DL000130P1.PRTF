      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Fee accrual account key generation')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL000130P1 - Deal Fee Accruals Account Keys Report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 10Mar06               *
      *  Prev Amend No. BUG9356            DATE 10Dec05               *
      *                 CAS009  *CREATE    DATE 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion of Customer Number to alpha.             *
      *  BUG9356- Extended Deal Sub Type (CDL038)                     *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
      *
     A          R DL000130H1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'DL000130P1'
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    45'NON-CALCULATED FEE ACCOUNT +
     A                                       KEYS GENERATED'
     A                                      SPACEA(1)
     A                                    45'---------------------------+
     A                                       --------------'
     A                                      SPACEA(2)
     A N35                                 6'BRANCH:'
     A N35        STRBRC         3A       14
     A N35        BRNM          30A       18
     A N35                                  SPACEA(2)
     A
     A          R DL000130H2
     A                                     5'CUSTOMER'
     A                                    17'DEAL'
     A                                    25'FEE SEQ'
     A                                    34'ACCOUNT KEY'
     A                                    52'AMOUNT'
     A                                    69'CURRENCY'
     A                                    79'REV.IND.'
     A                                      SPACEA(1)
     A                                     5'--------'
     A                                    17'----'
     A                                    25'-------'
     A                                    34'-----------'
     A                                    52'------'
     A                                    69'--------'
     A                                    79'--------'
     A                                      SPACEA(2)
      *
     A          R DL000130D1                SPACEA(1)
      * Begin /CSD027/
     A**********  KDCNUM         6  0      6                                                  CSD027
     A            KDCNUM         6         6
      * End /CSD027/
     A            KDDLNO         6  0     17
     A            DFFSEQ         2  0     27
      **Start of change BUG9356
     A            KDAKEY        14        32                                                 BUG9356
     A************KDAKEY********10        34                                                 BUG9356
      **End of change BUG9356
     A  30        EAMT0         13  0     47EDTCDE(J)
     A  31        EAMT1         13  1     47EDTCDE(J)
     A  32        EAMT2         13  2     47EDTCDE(J)
     A  33        EAMT3         13  3     47EDTCDE(J)
     A            KDECCY         3        71
     A            KDREVI         1  0     82
     A
     A          R DL000130T1                SPACEB(3)
     A                                    45'TOTAL ACCOUNT KEYS GENERATED'
     A            KDNORE         5  0     +3EDTCDE(Z)
     A                                      SPACEA(3)
     A
     A  35                                40'***  END OF REPORT  ***'
     A
     A N35                                  SPACEB(1)
     A N35                                45'***  END OF REPORT FOR +
     A                                      BRANCH'
     A N35        STRBRC    R             75
     A N35                                80'***'
