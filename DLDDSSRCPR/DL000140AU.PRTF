      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL Dealing Fees Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL000140AU - Midas DL Dealing Fees Audit Report         *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL000140AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    37'DEALING FEES INPUT TODAY +
     A                                      AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    37'-------------------------+
     A                                      ------------'
     A                                      SPACEA(2)
      *
     A          R NODTLS                    SPACEB(3)
     A
     A                                    60'** NO DETAILS TO REPORT **'
     A
     A
     A                                      SPACEA(2)
     A
     A          R CONTROL                   SPACEB(3)
     A                                     6'RUN CONTROLS'
     A                                    37'-------- REPORT --------'
     A                                    70'----- FILE CONTROL -----'
     A
     A                                    47'NUMBER'
     A                                      SPACEB(2)
     A                                    80'NUMBER'
     A                                     9'RECORDS LISTED IN THIS REPORT'
     A                                      SPACEB(2)
     A            RNORE          5  0     50EDTCDE(Z)
     A                                     9'RECORDS BEFORE INPUT CYCLE'
     A                                      SPACEB(2)
     A            DFNRIC         5  0     84EDTCDE(Z)
     A                                     9'RECORDS INSERTED'
     A                                      SPACEB(2)
     A            RINS           5  0     50EDTCDE(Z)
     A            DFNINT         5  0     84EDTCDE(Z)
     A                                     9'RECORDS AMENDED'
     A                                      SPACEB(2)
     A            RAMD           5  0     50EDTCDE(Z)
     A            DFNAMD         5  0     84EDTCDE(Z)
     A                                     9'RECORDS DELETED'
     A                                      SPACEB(2)
     A            RDEL           5  0     50EDTCDE(Z)
     A            DFNDEL         5  0     84EDTCDE(Z)
     A                                     9'RECORDS AFTER INPUT CYCLE'
     A                                      SPACEB(2)
     A            RREC           5  0     50EDTCDE(Z)
     A            DFNORE         5  0     84EDTCDE(Z)
     A  03                                60'**CONTROL TOTAL ERRORS**'
     A                                      SPACEB(2)
     A
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
