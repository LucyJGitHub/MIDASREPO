      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Dealing Fees Input Today Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL000140P1 - Dealing Fee Input Today                    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS011             Date 16May05               *
      *                 CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS011 - EIR/AC Accounting Upgrade to MP1                    *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL000140P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    36'DEALING FEES INPUT TODAY BRANCH'
     A            RBRCA     R             68REFFLD(BRCA)
     A                                    72'-'
     A            RBRNM     R             74REFFLD(BRNM)
     A                                      SPACEA(1)
     A                                    36'------------------------'
     A                                   132' '
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
     A
     A            RCNUM          6   O     5
     A            RCRNM         20   O    12
     A            RCRTN         10   O    33
     A                                      SPACEA(1)
     A                                     5'----------------------+
     A                                      ----------------'
     A                                      SPACEA(1)
     A                                    17'FEE'
     A                                    71'FEE PERIOD'
     A                                    87'PAYMENT'
     A                                    96'NEXT'
     A                                   110'FREQUENCY'
     A                                   122'DAY'
     A                                   132' '
     A                                      SPACEA(1)
     A                                     5'DEAL'
     A                                    13'SEQ'
     A                                    17'CODE'
     A                                    43'CCY'
     A                                    57'AMOUNT'
     A                                    70'START'
     A                                    81'END'
     A                                    89'ON'
     A                                    96'PAYMENT DATE'
     A                                   112'CODE'
     A                                   122'IN MONTH'
     A                                      SPACEA(2)
     A
     A            RDLNO          6   O     5
     A            RFSEQ          2   O    14
     A            RFCOD          2   O    18
     A            AUFENV        20   O    21
     A            RCCY           3   O    43
     A            RAMT          21   O    47
     A            RSDAT          7   O    70
     A            REDAT          7   O    79
     A            RPAYON         5   O    88
     A            RNPYD          7   O    98
     A            RFREQ          1   O   114
     A            RDYIM          2   O   124
      *
     A                                      SPACEA(1)
     A                                     5'INSERTING USER'
     A            RIUSER        10   O    21
     A                                    34'AMENDING USER'
     A            RAUSER        10   O    49
     A                                    61'AUTHORISING USER'
     A            RXUSER        10   O    79
     A                                      SPACEA(2)
      *
     A N02                                 5'RECEIPT SETTLEMENT INSTRUCTIONS'
     A  02                                53'PAYMENT SETTLEMENT INSTRUCTIONS'
     A                                   130' '
     A                                      SPACEA(1)
     A N02                                 5'SETTLEMENT METHOD'
     A N02        RRSTM          2A  O    33
     A  02                                53'SETTLEMENT METHOD'
     A  02        RPSTM          2A  O    82
     A                                   130' '
     A                                      SPACEA(1)
     A N02                                 5'OUR NOSTRO'
     A**Start of change CAS011
     A*N02******  RRONS         12A  O    23                                                  CAS011
     A N02        RRONS         18A  O    23
     A**End of change CAS011
     A  02                                53'OUR NOSTRO'
     A**Start of change CAS011
     A**02******  RPONS         12A  O    82                                                  CAS011
     A  02        RPONS         18A  O    82
     A**End of change CAS011
     A                                   130' '
     A                                      SPACEA(1)
     A N02                                 5'ORDERING CUSTOMER'
     A N02        RROCN          6A  O    29
     A  02                                53'MT202 SUPPLEMENTARY MESSAGE'
     A  02        RCVMR          1A  O    82
     A                                   130' '
     A                                      SPACEA(1)
     A N02                                 5'ORDERING BANK'
     A N02        RROBN          6A  O    29
     A  02                                53'RECEIVER'
     A  02        RRVNO          8A  O    82
     A                                   130' '
     A                                      SPACEA(1)
     A N02                                 5'INTERMEDIARY BANK NO'
     A N02        RRIBN          8A  O    27
     A  02                                53'ORDERING CUSTOMER'
     A  02        RPOCN          6A  O    84
     A                                   130' '
     A                                      SPACEA(1)
     A N02                                 8'A/C LINE'
     A N02        RRIBA         35A  O    17
     A  02                                53'ORDERING BANK'
     A  02        RPOBN          6A  O    84
     A                                   130' '
     A                                      SPACEA(3)
      *
     A          R ENDREP1
     A                                    60'***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A          R ENDBRC1
     A                                    57'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(2)
     A            RBRCA     R             87REFFLD(BRCA)
     A                                    91'***'
      *                                                                                       CAS011
     A          R NODETP1                                                                     CAS011
     A                                    56'*** NO DETAILS TO REPORT ***'                    CAS011
     A                                      SPACEB(3)                                         CAS011
     A
