      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL APPC connect to Citydealer for rates.')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0001AU - Exchange FX/MM rates from CityDealer - Audit *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCM001  *CREATE    Date 22Feb95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCM001 - Citydealer/ Midas Revaluation Rates Link            *
      *                                                               *
      *****************************************************************
     A*
     A*****************
     A** REPORT HEADING
     A*****************
     A          R HEAD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  7'REFERENCE DL0001AU'
     A            BJURPT        53      3 45
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A*
     A******************
     A** DATABASE ERRORS
     A******************
     A          R DBASEO                    SKIPB(5)
     A                                    38'MIDAS/CITYDEALER'
     A                                    54'- RATES TRANSMISSION -'
     A                                    75' COMMS PROBLEM REPORT'
     A                                      SPACEA(1)
     A                                    38'----------------'
     A                                    54'----------------------'
     A                                    75'---------------------'
     A                                      SPACEA(12)
     A                                    38'******************************'
     A                                    68'*****************************'
     A                                    38'***               DATABASE    '
     A                                    70'ERROR                   ***'
     A                                      SPACEA(1)
     A                                    38'******************************'
     A                                    68'*****************************'
     A                                      SPACEA(1)
     A                                    38'*    FILE NAME'
     A            DBFILE         8        55
     A                                    96'*'
     A                                      SPACEA(1)
     A                                    38'*    KEY'
     A            DBKEY         29        55
     A                                    96'*'
     A                                      SPACEA(1)
     A                                    38'*    PROGRAM NAME'
     A            DBPGM         10        55
     A                                    96'*'
     A                                      SPACEA(1)
     A                                    38'*    AT'
     A            DBASE          3  0     55
     A                                    96'*'
     A                                      SPACEA(1)
     A                                    38'******************************'
     A                                    68'*****************************'
     A                                      SPACEA(3)
     A                                    38'JOIN THIS REPORT TO'
     A                                    57' THE MIDAS PROBLEM REPORT '
     A                                      SKIPA(43)
     A                                    55'*** END OF REPORT ***'
      *
     A***************
     A** LOGIN ERROR
     A***************
     A          R LOGERR                    SKIPB(5)
     A                                    38'MIDAS/CITYDEALER'
     A                                    54'- RATES TRANSMISSION -'
     A                                    76' DATABASE ERROR REPORT'
     A                                      SPACEA(1)
     A                                    38'----------------'
     A                                    54'----------------------'
     A                                    75'---------------------'
     A                                      SPACEA(12)
     A                                    38'******************************'
     A                                    68'*****************************'
     A                                      SPACEA(1)
     A                                    38'***                   INVALID '
     A                                    70'LOGIN                   ***'
     A                                      SPACEA(1)
     A            SHEAD         16        40
     A            SIHEAD        20        60
     A                                      SPACEA(1)
     A                                    38'******************************'
     A                                    68'*****************************'
     A                                      SPACEA(3)
     A                                    38'JOIN THIS REPORT TO'
     A                                    57' THE MIDAS PROBLEM REPORT '
     A                                      SKIPA(43)
     A                                    55'*** END OF REPORT ***'
     A************************
     A** INVALID DATA RECEIVED
     A************************
     A          R DATA                      SKIPB(5)
     A                                    38'MIDAS/CITYDEALER'
     A                                    54'- RATES TRANSMISSION -'
     A                                    76' INVALID DATA RECEIVED'
     A                                      SPACEA(1)
     A                                    38'---------------'
     A                                    54'----------------------'
     A                                    75'---------------------'
     A                                      SPACEA(12)
     A                                    38'** DATA :'
     A            FIELDP        80        55
     A                                    95'**'
     A                                      SPACEA(2)
     A                                    38'JOIN THIS REPORT TO'
     A                                    57' THE MIDAS PROBLEM REPORT '
     A                                      SKIPA(43)
     A                                    55'*** END OF REPORT ***'
