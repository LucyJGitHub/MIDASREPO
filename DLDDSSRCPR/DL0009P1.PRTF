      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Citydealer Transmission Failures Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0009P1 - Citydealer Transmission Failures Report      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCM002 *CREATE     DATE 04SEP95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCM002 - Midas/Citydealer Interface (Phases V and VI)        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL0009P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    46'Citydealer Transmission +
     A                                      Failures'
     A            RINTR          9        80TEXT('Interim')
     A                                      SPACEA(1)
      *
     A                                    46'------------------------+
     A                                      --------'
     A                                      SPACEA(2)
     A                                     6'Program'
     A                                    17'Error Ref.'
     A                                    30'Date Error'
     A                                    43'Time Error'
     A                                    56'Message'
     A                                    98'Nostro'
     A                                   107'Transmission'
     A                                      SPACEA(1)
     A                                     6'Ref.'
     A                                    17'Number'
     A                                    30'Occurred'
     A                                    43'Occurred'
     A                                    56'Type'
     A                                    66'Branch'
     A                                    74'Currency'
     A                                    85'Value Date'
     A                                    98'Number'
     A                                   107'Amount'
     A                                      SPACEA(1)
     A                                     6'--------'
     A                                    17'----------'
     A                                    30'----------'
     A                                    43'----------'
     A                                    56'-------'
     A                                    66'------'
     A                                    74'--------'
     A                                    85'----------'
     A                                    98'------'
     A                                   107'---------------------'
     A                                      SPACEA(2)
      *
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R DETAIL
      *
     A            RERRP          8         6TEXT('Program Reference')
     A            RERRN          3  0     20TEXT('Error Ref. Number')
     A            RERRD          6  0     31TEXT('Date Error Occurred')
     A                                      EDTWRD('  /  /  ')
     A            RERRT          6  0     43TEXT('Time Error Occurred')
     A                                      EDTWRD('  :  :  ')
     A            RMTYP          2        58TEXT('Message Type')
     A            RBRCA          3        67TEXT('Branch')
     A            RCCY           3        75TEXT('Currency')
     A            RVDAT          6  0     86TEXT('Value Date')
     A                                      EDTWRD('  /  /  ')
     A            RNOSN          2  0    100TEXT('Nostro Number')
     A            RTAMT         22       107TEXT('Transmission Amount')
     A                                      SPACEA(2)
      *
      *--------------------------------*
      ***  End of Report Record Format *
      *--------------------------------*
      *
     A          R ENDREP
     A                                    52'*** End of Report ***'
     A                                      SPACEB(1)
