/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Daily Withholding Tax Report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL005010P1 - Daily Withholding Tax Report               *
      *                                                               *
      *  (c) Finastra International Limited 2015                      *
      *                                                               *
      *  Last Amend No. MD056986           DATE 27Oct20               *
      *  Prev Amend No. HUT107             Date 28Oct20               *
      *                 MD046248           Date 27Oct17               *
      *                 CGL165  *CREATE    Date 23Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056986 - Daily Withholding Tax Reports Not Separated by    *
      *             Branch                                            *
      *  HUT107 - Additional 3rd Party Trust Indicator                *
      *           (Upgrade HUT016 to FM2.1 SP22)                      *
      *           Addition of hooks HUT107_011 to HUT107_013          *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Page header - 1
      *
     A          R HEAD1                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2015')
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     7
     A                                      DFT('REFERENCE DL005010P1')
     A            TITL          53A  O    41
     A                                   101
     A                                      DFT('DATE')
     A            RUNA           7A  O   107
     A                                   119
     A                                      DFT('PAGE')
      ** Start of change MD056986
     A************PAGE           4S 0O   124                                                MD056986
     A            PNUM           4S 0O   124
      ** End of change MD056986
     A                                      EDTCDE(Z)
      *
     A                                    49
     A                                      DFT('DEALING DAILY WITHHOLDING TAX -
     A                                      REPORT')
     A                                      SPACEB(002)
      *
     A                                    49
     A                                      DFT('-------------------------------
     A                                      ------')
     A                                      SPACEB(001)
      ** Start of change MD056986
     A                                     1
     A                                      DFT('BRANCH   :')
     A                                      SPACEB(002)
     A            A8BRCD         3A  O    12
     A            A8BRNM        30A  O    16
      ** End of change MD056986
      *
      ** Start of change MD056986
     A**********                           1                                                MD056986
     A N25                                 1
      ** End of change MD056986
     A                                      DFT('CURRENCY :')
      ** Start of change MD056986
     A**********                            SPACEB(002)                                     MD056986
     A N25                                  SPACEB(001)
     A N25        CCY       R        O    12
      ** End of change MD056986
     A                                    29
     A                                      DFT('----------- MAIN TAX ----------
     A                                      ---')
     A                                      SPACEB(002)
     A                                    64
     A                                      DFT('--------- SECONDARY TAX -------
     A                                      ---')
     A                                     1
     A                                      DFT('DEAL NUMBER/')
     A                                      SPACEB(001)
     A                                    16
     A                                      DFT('PRINCIPAL')
     A                                    29
     A                                      DFT('CODE      RATE  AMT(IN DEAL CC-
     A                                      Y)/')
     A                                    64
     A                                      DFT('CODE      RATE  AMT(IN DEAL CC-
     A                                      Y)/')
     A                                   103
     A                                      DFT('INTEREST')
     A                                   112
     A                                      DFT('DUE')
     A                                   120
     A                                      DFT('INTEREST')
     A                                   129
     A                                      DFT('PAID')
     **/*OPY*WNCPYSRC,HUT107_011**
     A                                     1
     A                                      DFT('CUSTOMER NAME')
     A                                      SPACEB(001)
      */COPY WNCPYSRC,HUT107_012
     A                                    45
     A                                      DFT('AMT(IN LOCAL CCY)')
     A                                   +18
     A                                      DFT('AMT(IN LOCAL CCY)')
      *
      ** Detail record
      *
     A          R DETAIL
     A                                      TEXT('DETAIL RECORD')
     A                                      SPACEB(002)
     A            DLNO      R        O     1
     A            PCPLAT        17A  O     8
     A            NTRC           2S 0O    29
     A            NTRT           4S 2O    38
     A                                      EDTWRD(' 0.  ')
     A            NTDLAT        17A  O    45
     A            LTRC           2S 0O    64
     A            LTRT           4S 2O    73
     A                                      EDTWRD(' 0.  ')
     A            LTDLAT        17A  O    80
     A            INTDUE        17A  O    98
     A            INTPAD        17A  O   116
     A            CNA1      R   35   O     1
     A                                      SPACEB(001)
      */COPY WNCPYSRC,HUT107_013
     A            NTBSAT        17A  O    45
     A            LTBSAT        17A  O    80
      *
      ** Totals record
      *
     A          R TOTPRT
     A                                      TEXT('TOTALS RECORD')
     A N24                                  SKIPB(003)
     A N24                                  SPACEA(002)
     A N24                                 7
     A                                      DFT('REFERENCE DL005010P1')
     A N24        BJURPT    R        O    41REFFLD(TITL)
     A N24                               101
     A                                      DFT('DATE')
     A N24        BJMRDT    R        O   107REFFLD(RUNA)
     A N24                               119
     A                                      DFT('PAGE')
      ** Start of change MD056986
     A*N24******* PAGE           4S 0O   124                                                MD056986
     A N24        PNUM           4S 0O   124
      ** End of change MD056986
     A                                      EDTCDE(Z)
      *
     A N24                                44
     A                                      DFT('DEALING DAILY WITHHOLDING TAX -
     A                                      REPORT - ')
     A                                      SPACEB(002)
     A N24                                83
     A                                      DFT('FILE CONTROL')
      *
     A N24                                44
     A                                      DFT('-------------------------------
     A                                      ---------------------')
     A                                      SPACEB(001)
     A N24                                49
     A                                      DFT('TOTAL NO OF RECORDS PROCESSED -
     A                                      =')
     A                                      SPACEB(005)
     A N24        CNOR           4  0O    83
     A                                      EDTCDE(3)
     A  24                                54
     A                                      DFT('*** NO DETAILS TO REPORT ***')
     A                                      SPACEB(002)
     A                                    57
     A                                      DFT('*** END OF REPORT ***')
     A                                      SKIPB(054)
      *
      ** Database error print
      *
     A          R DBERRP
     A                                      TEXT('DATABASE ERROR')
     A                                      SPACEB(002)
      *
     A                                     7
     A                                      DFT('DATABASE ERROR IN ')
     A                                    26
     A                                      DFT('FILE ')
     A            DBFILE         8   O    31
     A                                    39
     A                                      DFT(',')
     A                                    41
     A                                      DFT('KEY ')
     A            DBKEY         29   O    45
     A                                    71
     A                                      DFT(',')
     A                                    73
     A                                      DFT('IN ')
     A            DBPGM         10   O    76
     A                                    84
     A                                      DFT(',')
     A                                    86
     A                                      DFT('AT ')
     A            DBASE          3  0O    89
      *****************************************************************
