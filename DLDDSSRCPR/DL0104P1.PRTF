     A*****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas DL Deals Interest & Drawings Report')            *
/*EXT *  PRTF01                                                       *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0104P1 - Deals Interest & Drawings Report             *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. CDL104 *CREATE     Date 02Oct23               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL104 - Principal Decrease events on Fixed Term MM Deals    *
      *                                                               *
      *****************************************************************
      *
      **  Main Header Details
     A          R DL0104H0                  TEXT('(c) Finastra International +
     A                                      Limited 2023')
     A                                  1  3'REFERENCE DL0104'
     A            BJURPT        53      1 40
     A                                  1101'DATE'
     A            BJMRDT         7      1106
     A                                  1119'PAGE:'
     A            PAGE           3  0   1125EDTCDE(Z)
     A                                  3 45'Deals Interest & Drawings +
     A                                      Report'
     A                                  4 45'--------------------------+
     A                                      ------'
      *
     A                                  6  2'Deal'
     A                                  6 26'Current'
     A                                  6 40'Interest to be'
     A                                  6 70'Total'
     A                                  6 91'Total'
      *
     A                                  7  2'Number'
     A                                  7 11'Ccy'
     A                                  7 24'Principal'
     A                                  7 43'Capitalised'
     A                                  7 67'Deposits'
     A                                  7 85'Withdrawals'
     A                                  7 99'Status'
     A                                  7110'Deal Amendment'
      *
     A                                  8  2'------'
     A                                  8 11'---'
     A                                  8 24'---------'
     A                                  8 43'-----------'
     A                                  8 67'--------'
     A                                  8 85'-----------'
     A                                  8 99'------'
     A                                  8110'--------------'
      *
      *
      **  Report Details
     A          R DL0104D0                  SPACEB(2)
     A            L#DLNO         6         2TEXT('Deal Number')
     A            L#CYCD         3        11TEXT('Currency')
     A            L#PCPL        19        15TEXT('Current Principal')
     A            L#INTR        19        36TEXT('Capitalised Interest')
     A            L#TOTD        19        57TEXT('Total Deposits')
     A            L#TOTW        19        78TEXT('Total Withdrawals')
     A            L#STAT         9        99TEXT('Status')
     A            L#DAVD         6  0    110TEXT('Deal Amnd. Value Date')
     A                                      EDTWRD('  /  /  ')
     A            L#DASN         3       120TEXT('Deal Amnd. Sequence')
      *
      *
      **  No Details to Report
     A          R DL0104D1                  SPACEB(4)
     A                                    30'*** No Details To Report ***'
      *
      **  Report Trailer
     A          R DL0104T0                  SPACEB(3)
     A                                    51'***  End of Report  ***'
      *
      **  Database error reporting
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'FILE NAME:'
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    11'KEY:'
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    11'PROGRAM NAME:'
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    11'AT:'
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
      *
