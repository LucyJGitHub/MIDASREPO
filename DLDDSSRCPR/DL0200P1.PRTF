      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas DL Zero Effective Interest report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0200P1 - Midas DL Zero Effective Interest Report      *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL033  *CREATE    Date 23Feb05               *
      *                                                               *
      * --------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL033 - Floating Rate CDs Issued                            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format 1
      *
     A          R DL0200D1                  TEXT('(c) Finastra International +
     A                                      Limited 2005')
     A                                      SKIPB(1)
     A                                     6'Reference DL0200P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    50'LIST OF DEALS WITH +
     A                                       ZERO EFFECTIVE INTE+
     A                                       REST RATE'
     A                                      SPACEA(1)
     A                                    50'-------------------+
     A                                       -------------------+
     A                                       ---------'
     A                                      SPACEA(2)
      *
     A                                     1'BRANCH'
     A            PBRCA          3A        8TEXT('BRANCH')
     A            PBRNM         30A       12TEXT('BRANCH DESCRIPTION')
     A                                      SPACEA(2)
      *
     A                                     1'DEAL  '
     A                                    10'BASE  '
     A                                    30'BASE        '
     A                                    50'SPREAD       '
     A                                    70'SHORT TERM  '
     A                                    90'EFFECTIVE   '
      *
     A                                      SPACEA(1)
      *
     A                                     1'NUMBER'
     A                                    10'RATE  '
     A                                    30'RATE        '
     A                                    50'RATE         '
     A                                    70'RATE        '
     A                                    90'INTEREST    '
      *
     A                                      SPACEA(1)
      *
     A                                    10'CODE  '
     A                                    90'DATE        '
      *
     A                                      SPACEA(2)
      *
      ** Report Detail Record Format 1
      *
     A          R DL0200D2
     A            PDLNO          6A        1TEXT('DEAL NUMBER')
     A            PCCY           3A       10TEXT('CURRENCY CODE')
     A            PBRTT          2A       14TEXT('BASE RATE CODE')
     A            PBRTE         14A       30TEXT('BASE RATE')
     A            PRTSP         14A       50TEXT('SPREAD RATE')
     A            PSTRT         14A       70TEXT('SHORT TERM RATE')
     A            PDLRC          7A       90TEXT('INTEREST EFF DATE')
     A                                      SPACEA(1)
      *
      ** Skip line between deals
      *
     A          R DL0200D3
     A                                      SPACEB(1)
     A                                     1' '
      *
      ** Report Footer Record Format
      *
     A          R DL0200D4
     A                                      SPACEB(3)
      *
     A                                    51'*** END OF REPORT FOR +
     A                                      BRANCH ***'
