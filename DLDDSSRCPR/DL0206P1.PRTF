      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Todays Broken Down Deposits Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0206P1 - Today's Broken Down Deposits - Report        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CDL007  *CREATE    Date 19Oct99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL007 - Deposit Breakdown Penalty                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL0206P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Breakdown Information - Branch '
     A            RRBRCA         3        77TEXT('Branch')
     A            RRINTR         9        85TEXT('Interim')
      *
     A                                      SPACEA(1)
     A                                    46'----------------------------------'
     A                                      SPACEA(5)
     A                                    10'Deal Number'
     A                                    30'Breakdown Penalty'
     A                                    60'Last Update User'
     A                                      SPACEA(1)
     A                                    10'---------------------------------+
     A                                       ---------------------------------'
     A
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A            RRDLNO         6        10
     A            RRPENA        16        30
     A            RRUSRI        10        60
      *
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Record No Detail     *
      *-------------------------*
      *
     A          R NODETS
      *
     A                                      SPACEB(3)
     A                                    48'***  NO DETAIL TO REPORT  ***'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                      SPACEB(3)
     A                                    51'***  END OF REPORT FOR BRANCH '
     A            RRBRCA         3        81TEXT('Branch')
     A                                    85'***'
