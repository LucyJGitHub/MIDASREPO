      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL FX Deals received from TI')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0208P1 - FX deals received from Trade Innovation      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CTI004  *CREATE    Date 12Apr04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CTI004 - MidasPlus-TI Integration Enhancements               *
      *           FX Deals Integration                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL0208P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'FX Deals Received from +
     A                                      Trade Innovation'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'-----------------------+
     A                                      ----------------'
     A                                      SPACEA(2)
      *
      *---------------*
      ** Sub-Headings *
      *---------------*
      *
     A  37 32                             11'ORIG.'
     A  37N32                             11'     '
      *                                              Begin /CDL038/
     A************************************18'DEAL   DEAL    VALUE'                            CDL038
     A                                    18'DEAL   DEAL        VALUE'                        CDL038
      *                                              End   /CDL038/
     A                                    48'CCY'
     A                                    63'PURCHASE/'
     A                                   110'SALE      ACTION'
     A                                      SPACEA(1)
      *
     A                                     2'CUSTOMER'
     A  37                                12'BR.'
     A                                    17'NUMBER'
      *                                              Begin /CDL038/
     A************************************25'TYPE    DATE'                                    CDL038
     A                                    25'TYPE        DATE'                                CDL038
      *                                              End   /CDL038/
     A                                    63'DEP.AMT.'
     A                                    79'RATE   O/R    CCY'
     A                                   110'AMOUNT    STATUS'
     A                                   128'TIME'
     A                                      SPACEA(2)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL
      *
      * Begin /CSD027/
     A**********  CNUM      R              1                                                  CSD027
     A            CNUM           6A        1TEXT('Customer number')
      * End /CSD027/
     A  37 32     ORBR      R             12
     A  37N32     BRCA      R             12
     A            DLNO      R             17
     A            DTYP      R             24
     A                                    26'-'
     A            DLST      R             27
      *                                              Begin /CDL038/
     A            CLAS           4        29                                                  CDL038
     A            VADATE         7        36                                                  CDL038
     A************VADATE         7        33                                                  CDL038
      *                                              End   /CDL038/
     A            KCCY           3        48
     A  35        AMT0          13  0     51EDTWRD(' ,   ,   ,   , 0 ')
     A  46        AMT1          13  1     53EDTWRD('   ,   ,   , 0 . ')
     A  47        AMT2          13  2     54EDTWRD('  ,   ,   , 0 .  ')
     A  48        AMT3          13  3     55EDTWRD(' ,   ,   , 0 .   ')
     A N77        EXRT          13  8     76EDTCDE(L)
     A N77        SLCY      R             93
     A  36        SAMT0         13  0     96EDTWRD(' ,   ,   ,   , 0 ')
     A  53        SAMT1         13  1     98EDTWRD('   ,   ,   , 0 . ')
     A  54        SAMT2         13  2     99EDTWRD('  ,   ,   , 0 .  ')
     A  55        SAMT3         13  3    100EDTWRD(' ,   ,   , 0 .   ')
     A  38                               120'AMEND '
     A  43                               120'INSERT'
     A  44                               120'DELETE'
     A            TIM            4  0    128EDTWRD('  .  ')
     A                                      SPACEA(1)
     A            BBCRNM        20         2
     A            BBCRTN        10        23
     A                                      SPACEA(1)
     A                                     1'Margin'
     A            MGPT           7  2     12EDTCDE(L)
     A            MGBS           1        22
     A                                    25'DL base spot rate'
     A            BDBS          13  8     43EDTCDE(L)
     A                                    61'Buy DL base fwd pts'
     A            BDBF           7  2     81EDTCDE(L)
     A            BDBFS          1        90
     A                                    93'Sell DL base fwd pts'
     A            SDBF           7  2    114EDTCDE(L)
     A            SDBFS          1       123
     A                                      SPACEA(1)
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
