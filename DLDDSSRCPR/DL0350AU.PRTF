      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL Pay/receives netting extract audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module                                       *
     A*                                                               *
     A*     DL0350AU - MIDAS Netting Pay/receive extract audit        *
     A*                  Report                                       *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CDL002             Date 15Apr97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CDL002 - FX Netting                                          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R DL0350AH                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL0350AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    41
     A                                      'Netting Pay/receive Extract file a-
     A                                      udit '
     A                                      SPACEB(002)
     A                                    41
     A                                      '-----------------------------------
     A                                      -----'
     A                                      SPACEB(001)
     A                                    10
     A                                      'S/PREX - Run Controls'
     A                                      SPACEB(002)
     A                                    10
     A                                      'Total records output'
     A                                      SPACEB(001)
     A                                    41
     A                                      'Number'
     A                                   +12
     A                                      'Value'
      *
      *--------------------------------*
      ***  Report Detail Record Format *
      *--------------------------------*
      *
     A          R DL0350AD
     A                                      SPACEB(001)
     A            NORE           5S 0O    41
     A                                      EDTCDE(3)
     A            HRWN          15S 0O    +3
     A                                      EDTCDE(Z)
     A            HRDC           3S 0O    +0
      *
      *----------------------------*
      ***  Report DB Record Format *
      *----------------------------*
      *
     A          R DL0350DB
     A                                      SKIPB(020)
     A                                      SPACEA(001)
     A                                    14
     A                                      DFT('DATABASE IN ERROR')
     A                                    +6
     A                                      DFT('Reference')
     A            DBASE          3A  O    +2
     A                                    +3
     A                                      DFT('File')
     A            DBFILE         8A  O    +2
     A                                    +2
     A                                      DFT('Key')
     A            DBKEY         29A  O    +2
 
