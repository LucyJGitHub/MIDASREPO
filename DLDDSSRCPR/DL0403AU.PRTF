      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL FX Netted deals EM extract audit')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0403   -  Netted Deals EM Extract                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CDL002             Date 16APR97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL002 - Created for FX Netting                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL0403AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'Netted Deals EM Extract +
     A                                                      - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    42'------------------------+
     A                                      -------'
     A                                      SPACEA(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                    10'PF/EMXT2D2'
     A                                      SPACEA(1)
     A                                    10'----------'
     A                                      SPACEA(2)
      *
     A                                    10'No of Records Before +
     A                                       Update'
     A            W#RECBEX  R             60REFFLD(JNLB)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    10'No of Records Added'
     A            W#RECCEX  R             60REFFLD(JNIN)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    10'No of Records After +
     A                                       Update'
     A            W#RECAEX  R             60REFFLD(JNLA)
     A                                      EDTCDE(3)
     A                                      SPACEA(3)
      *
     A                                    10'PF/EMTE2D2'
     A                                      SPACEA(1)
     A                                    10'----------'
     A                                      SPACEA(2)
      *
     A                                    10'No of Records Before +
     A                                       Update'
     A            W#RECBTW  R             60REFFLD(JNLB)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    10'No of Records Added'
     A            W#RECCTW  R             60REFFLD(JNIN)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    10'No of Records After +
     A                                       Update'
     A            W#RECATW  R             60REFFLD(JNLA)
     A                                      EDTCDE(3)
     A                                      SPACEA(3)
      *
     A                                    10'PF/FXNEVTPD'
     A                                      SPACEA(1)
     A                                    10'-----------'
     A                                      SPACEA(2)
      *
     A                                    10'No of Records Before +
     A                                       Update'
     A            W#RECBNE  R             60REFFLD(JNLB)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    10'No of Records Added'
     A            W#RECCNE  R             60REFFLD(JNIN)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                    10'No of Records After +
     A                                       Update'
     A            W#RECANE  R             60REFFLD(JNLA)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
