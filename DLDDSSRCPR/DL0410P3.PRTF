/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Revaluation Method 'F'')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0410P3 - Revaluation of Forward Commitments Report    *
      *                  Method 'F'                                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CDL038             Date 10May05               *
      *                 225831             Date 19Mar04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  225831 - If yield curve rate combination is not found, print *
      *           'Rates Missing' and do not compute for profit/loss. *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP3H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE DL0410P3'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'REVALUATION OF FORWARD COMMITMENTS-
     A                                       METHOD  F    -'
     A            RECCY          3        87
     A                                      SPACEA(1)
      *
     A                                    36'-----------------------------------
     A                                      --------------------'
     A                                      SPACEA(2)
      *
     A N85                                 7'BRANCH'
     A N85        RBRCA     R             14REFFLD(BRCA)
     A N85        RBRNM     R             18REFFLD(BRNM)
     A N85                                  SPACEA(2)
      *
     A  02                                 7'BOOK CODE'
     A  02        RBOKC          2A       17
     A  02        RDBKD         30A       20
     A  02                                  SPACEA(2)
      *
     A  02 32                              7'DEAL TYPE'
     A  02 32     RDTYP1         2A       17
     A  02 32     RDTNAR        35A       20
     A  02 32                               SPACEA(1)
      *
     A  02 32                              7'SUB TYPE'
     A  02 32     RDLST1         2A       17
      *                                              Begin /CDL038/
     A  02 32     RCLAS1         4A       19                                                  CDL038
     A  02 32     RDSNAR        35A       24                                                  CDL038
     A**02*32*****RDSNAR********30A*******20                                                  CDL038
      *                                              End   /CDL038/
     A  02 32                               SPACEA(2)
      *
     A                                     2'VALUE'
     A                                     8'CUSTOMER'
     A                                    17'DEAL'
     A  02N32                             23'DEAL'
     A                                    56'DEALT'
     A                                    68'YIELD'
     A                                    77'DISCOUNT'
     A                                    94'REVALUED'
     A                                   114'PROFIT'
     A                                      SPACEA(1)
      *
     A                                     2'DATE'
     A                                     9'NUMBER'
     A                                    16'NUMBER'
     A  02N32                             23'TYPE'
     A                                    37'AMOUNT'
     A                                    54'EQUIVALENT'
     A                                    68'CURVE'
     A                                    78'FACTOR'
     A                                    93'EQUIVALENT'
     A                                   114'/LOSS'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format   *
      *---------------------------*
      *
     A          R DETLP3D1
      *
     A            RVDAT          7A        1TEXT('Value Date')
      * Begin /CSD027/
     A**02******  RCNUM          6S        9TEXT('Customer Number')                           CSD027
     A  02        RCNUM          6A        9TEXT('Customer Number')
      * End /CSD027/
     A  02        RDLNO          6S       16TEXT('Deal Number')
     A  12        RPREF         15A        9TEXT('Payment Reference')
     A  02N32     RDTYP2         2A       23TEXT('Deal Type')
     A  02N32                             25'-'
     A  02N32     RDLST2         2A       26TEXT('Deal Sub-type')
      *                                              Begin /CDL038/
     A  02N32     RCLAS2         4A       28TEXT('Deal Class')                                CDL038
     A            REAMT         16A       32TEXT('Event Amount')                              CDL038
     A************REAMT         18A       30TEXT('Event Amount')                              CDL038
      *                                              End   /CDL038/
     A            RDBCE         18A       49TEXT('Deal Equivalent')
     A            RYLDC          5A       68TEXT('Yield Curve')
     A            RDSCF         13  9     74TEXT('Discount Factor')
     A                                      EDTWRD('  0 .         ')
     A            RREVA         18A       89TEXT('Revalued Equivalent')
     A            RPLAM         18A      108TEXT('Profit/Loss Amount')
     A                                      SPACEA(1)
     A**Start of change 225831
     A  30                                 1'* RATES MISSING - NOT VALUED *'
     A  30                                  SPACEA(1)
     A**End of change 225831
      *----------------------------*
      ***  Monthly Record Format   *
      *----------------------------*
      *
     A          R DETLP3D2
     A                                      SPACEB(1)
     A                                     4'MONTHLY SUB-TOTAL'
      *
     A            RMEAMT        18A       30TEXT('Monthly Deal Amount')
     A            RMDBCE        18A       49TEXT('Monthly Deal Equivalent')
     A            RMREVA        18A       89TEXT('Monthly Revaluation')
     A            RMPLAM        18A      108TEXT('Monthly Profit/Loss')
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Annual Record Format   *
      *---------------------------*
      *
     A          R DETLP3D3
     A                                     5'ANNUAL SUB-TOTAL'
      *
     A            RYEAMT        18A       30TEXT('Annual Deal Amount')
     A            RYDBCE        18A       49TEXT('Annual Deal Equivalent')
     A            RYREVA        18A       89TEXT('Annual Revaluation')
     A            RYPLAM        18A      108TEXT('Annual Profit/Loss')
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Total Record Format   *
      *--------------------------*
      *
     A          R DETLP3D4
     A                                      SPACEB(1)
      *
     A            RTEAMT        18A       30TEXT('Total Deal Amount')
     A            RTDBCE        18A       49TEXT('Total Deal Equivalent')
      *
     A                                    82'TOTAL'
      *
     A            RTREVA        18A       89TEXT('Total Revaluation')
     A            RTPROF        18A      108TEXT('Total Profit')
     A                                      SPACEA(2)
     A            RTLOSS        18A      108TEXT('Total Loss')
     A                                      SPACEA(2)
      *
     A                                    82'(NET)'
      *
     A            RTPLAM        18A      108TEXT('Total Profit/Loss')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Report Trailer Format *
      *--------------------------*
      *
     A          R TRALP3T1
     A N85                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A N85                                  SPACEB(3)
     A  85                                51'***  END OF REPORT  ***'
     A  85                                  SPACEB(3)
