/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Revaluation Method 'B'')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0410P4 - Revaluation of Forward Commitments Report    *
      *                  Method 'B'                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 225831             Date 19Mar04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  225831 - If yield curve rate combination is not found, print *
      *           'Rates Missing' and do not compute for profit/loss. *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP4H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE DL0410P4'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'REVALUATION OF FORWARD COMMITMENTS-
     A                                       METHOD  B    -'
     A            RECCY          3        87
     A                                      SPACEA(1)
      *
     A                                    36'-----------------------------------
     A                                      --------------------'
     A                                      SPACEA(2)
      *
     A N85                                 7'BRANCH'
     A N85        RBRCA     R             14REFFLD(BRCA)
     A N85        RBRNM     R             18REFFLD(BRNM)
     A N85                                  SPACEA(2)
      *
     A  02                                 7'BOOK CODE'
     A  02        RBOKC          2A       17
     A  02        RDBKD         30A       20
     A  02                                  SPACEA(2)
      *
     A  02 32                              7'DEAL TYPE'
     A  02 32     RDTYP1         2A       17
     A  02 32     RDTNAR        35A       20
     A  02 32                               SPACEA(1)
      *
     A  02 32                              7'SUB TYPE'
     A  02 32     RDLST1         2A       17
      *                                              Begin /CDL038/
     A  02 32     RCLAS1         4A       19                                                  CDL038
     A  02 32     RDSNAR        35A       24                                                  CDL038
     A**02*32*****RDSNAR********30A*******20                                                  CDL038
      *                                              End   /CDL038/
     A  02 32                               SPACEA(2)
      *
     A                                     2'VALUE'
     A                                     8'CUSTOMER'
     A                                    17'DEAL'
     A  02N32                             23'DEAL'
     A                                    54'DEALT'
     A                                    84'DISCOUNT'
     A                                   101'REVALUED'
     A                                   121'PROFIT'
     A                                      SPACEA(1)
      *
     A                                     2'DATE'
     A                                     9'NUMBER'
     A                                    16'NUMBER'
     A  02N32                             23'TYPE'
     A                                    36'AMOUNT'
     A                                    52'EQUIVALENT'
     A                                    70'MID-RATE'
     A                                    85'FACTOR'
     A                                   100'EQUIVALENT'
     A                                   121'/LOSS'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format   *
      *---------------------------*
      *
     A          R DETLP4D1
      *
     A            RVDAT          7A        1TEXT('Value Date')
      * Begin /CSD027/
     A**02******  RCNUM          6S        9TEXT('Customer Number')                           CSD027
     A  02        RCNUM          6A        9TEXT('Customer Number')
      * End /CSD027/
     A  02        RDLNO          6S       16TEXT('Deal Number')
     A  12        RPREF         15A        9TEXT('Payment Reference')
     A  02N32     RDTYP2         2A       23TEXT('Deal Type')
     A  02N32                             25'-'
     A  02N32     RDLST2         2A       26TEXT('Deal Sub-type')
      *                                              Begin /CDL038/
     A  02N32     RCLAS2         4A       28TEXT('Deal Class')                                CDL038
     A            REAMT         16A       32TEXT('Event Amount')                              CDL038
     A************REAMT         18A       29TEXT('Deal Amount')                               CDL038
      *                                              End   /CDL038/
     A            RDBCE         18A       48TEXT('Deal Equivalent')
     A            RMRAT         13  8     67TEXT('Mid Rate')
     A                                      EDTWRD('   0 .        ')
     A            RDSCF         13  9     81TEXT('Discount Factor')
     A                                      EDTWRD('  0 .         ')
     A            RREVA         18A       96TEXT('Revalued Equivalent')
     A            RPLAM         18A      115TEXT('Profit/Loss Amount')
     A                                      SPACEA(1)
     A**Start of change 225831
     A  30                                 1'* RATES MISSING - NOT VALUED *'
     A  30                                  SPACEA(1)
     A**End of change 225831
      *
      *----------------------------*
      ***  Monthly Record Format   *
      *----------------------------*
      *
     A          R DETLP4D2
     A                                      SPACEB(1)
     A                                     4'MONTHLY SUB-TOTAL'
      *
      *                                              Begin /CDL038/
     A************RMEAMT********18A       29TEXT('Monthly Deal Amount')                       CDL038
     A            RMEAMT        18A       30TEXT('Monthly Deal Amount')                       CDL038
      *                                              End   /CDL038/
     A            RMDBCE        18A       48TEXT('Monthly Deal Equivalent')
     A            RMREVA        18A       96TEXT('Monthly Revaluation')
     A            RMPLAM        18A      115TEXT('Monthly Profit/Loss')
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Annual Record Format   *
      *---------------------------*
      *
     A          R DETLP4D3
     A                                     5'ANNUAL SUB-TOTAL'
      *
      *                                              Begin /CDL038/
     A************RYEAMT********18A       29TEXT('Monthly Deal Amount')                       CDL038
     A            RYEAMT        18A       30TEXT('Monthly Deal Amount')                       CDL038
      *                                              End   /CDL038/
     A            RYDBCE        18A       48TEXT('Monthly Deal Equivalent')
     A            RYREVA        18A       96TEXT('Monthly Revaluation')
     A            RYPLAM        18A      115TEXT('Monthly Profit/Loss')
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Total Record Format   *
      *--------------------------*
      *
     A          R DETLP4D4
     A                                      SPACEB(1)
      *
      *                                              Begin /CDL038/
     A************RTEAMT********18A       29TEXT('Total Deal Amount')                         CDL038
     A            RTEAMT        18A       30TEXT('Total Deal Amount')                         CDL038
      *                                              End   /CDL038/
     A            RTDBCE        18A       48TEXT('Total Deal Equivalent')
      *
     A                                    89'TOTAL'
      *
     A            RTREVA        18A       96TEXT('Total Revaluation')
     A            RTPROF        18A      115TEXT('Total Profit')
     A                                      SPACEA(2)
     A            RTLOSS        18A      115TEXT('Total Loss')
     A                                      SPACEA(2)
      *
     A                                    89'(NET)'
      *
     A            RTPLAM        18A      115TEXT('Total Profit/Loss')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Report Trailer Format *
      *--------------------------*
      *
     A          R TRALP4T1
     A N85                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A N85                                  SPACEB(3)
     A  85                                51'***  END OF REPORT  ***'
     A  85                                  SPACEB(3)
