      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Revaluation')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0411P2 - Forward Revaluation - Euro Consolidated Position  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. 155225             Date 31Mar99               *
      *                 CEU011 *Create     Date 11Mar98               *
      *                                                               *
      * --------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type                             l*
      *  155225 - Insert title description for Mid-Rate column as well*
      *           as Profit Centre Header and Detail output field.    *
      *  CEU011 - EMU Position/Risk Reporting.                        *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *-------------------------------*
      ** Report Header Record Format 1*
      *-------------------------------*
     A          R DL0411D1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(1)
     A                                     6'Reference DL0411P2'
     A            RPTTL     R             40REFFLD(URPT)
     A                                   101'DATE'
     A            RMRDT     R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    50'REVALUATION OF FORWARD +
     A                                      COMMITMENTS - CONSOLIDATED +
     A                                      EURO POSITION'
     A                                      SPACEA(1)
     A                                    50'-----------------+
     A                                      -----------------'
     A                                      SPACEA(2)
      *
     A N85                                 1'BRANCH'
     A N85        RBRCA          3A        8TEXT('Branch')
     A N85        RBRNM         30A       12TEXT('Branch Description')
      *
     A                                      SPACEA(2)
      *                                             /* Start 155225
     A N85                                 1'PROFIT CENTRE'
     A N85        PRFC           4A       16TEXT('Profit Centre')
     A N85        DSPCDS        30A       22TEXT('Profit Centre Description')
     A                                      SPACEA(2)
      *                                             /* End  155225
     A                                     1'VALUE'
     A                                     9'CUSTOMER'
     A                                    19'DEAL'
     A                                    26'DEAL'
     A                                    36'IN'
     A                                    50'DEALT EURO'
      *                                               /* Start 15522   5
     A                                    69'IN CURRENCY'
      *                                               /* End  155225
     A                                    90'SPOT EURO'
     A                                   117'EURO'
      *
     A                                      SPACEA(1)
     A                                     1'DATE'
     A                                    10'NUMBER'
     A                                    18'NUMBER'
     A                                    26'TYPE'
     A                                    33'CURRENCY'
     A                                    50'EQUIVALENT'
     A                                    70'MID-RATE'
     A                                    90'EQUIVALENT'
     A                                   113'PROFIT/LOSS'
     A                                      SPACEA(2)
      *-------------------------------*
      ** Report Detail Record Format **
      *-------------------------------*
     A          R DL0411D2
     A            RDATA          7A        1TEXT('Value Date')
     A            RCNUM          6A       10TEXT('Cust No.')
     A  02        RDLNO          6A       18TEXT('Deal No.')
     A  02        RDTYP          2A       26TEXT('Deal Type')
     A  02                                28'-'
     A  02        RDLST          2A       29TEXT('Deal Sub-Type')
      *                                              Begin /CDL038/
     A  02        RCLAS          4A       31TEXT('Deal Class')                                CDL038
      *                                              End   /CDL038/
     A  12        RPREF         15A       18TEXT('Pref. Payment')
      *                                              Begin /CDL038/
     A************RCCY           3A       35TEXT('In Currency')                               CDL038
     A            RCCY           3A       36TEXT('In Currency')                               CDL038
      *                                              End   /CDL038/
      *
     A            REDBCE        21A       43TEXT('Deal Euro Equiv.')
     A            RRATE         14A       68TEXT('Mid-Rate')
     A            REREVA        21A       84TEXT('Spot Euro Equiv')
     A            REPLAM        21A      107TEXT('Euro Profit/Loss')
     A                                      SPACEA(1)
      *-------------------------------*
      ** Report Total Record Format  **
      *-------------------------------*
     A          R DL0411D3
     A                                      SPACEB(1)
     A                                     1'TOTAL'
     A            RTDBCE        21A       43TEXT('Total Deal Euro Eq.')
     A            RTREVA        21A       84TEXT('Total Spot Euro Eq.')
     A            RTPROF        21A      107TEXT('Total Profit')
     A            RTLOSS        21A      107TEXT('Total Loss')
     A                                      SPACEB(2)
     A                                   104'NET'
     A                                      SPACEB(2)
     A            RTOTPL        21A      107TEXT('Net Profit/Loss')
      *-------------------------------*
      ** Report Footer Record Format **
      *-------------------------------*
      *
     A          R DL0411D4
     A                                      SPACEB(5)
      *
     A N85                                51'*** END OF REPORT FOR +
     A                                      BRANCH ***'
     A N85                                  SPACEA(1)
     A  85                                56'*** END OF REPORT ***'
     A  85                                  SPACEA(1)
