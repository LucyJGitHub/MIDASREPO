      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Revaluation')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0410P2 - Forward Revaluation                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last amend No. CDL048 *CREATE     Date 26Jun06               *
      *                                                               *
      * --------------------------------------------------------------*
      *                                                               *
      *  CDL048 - IAS - Forward Revaluation Processing                *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *-------------------------------*
      ** Report Header Record Format 1*
      *-------------------------------*
     A          R DL0412D1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      SKIPB(1)
     A                                     6'Reference DL0410P2'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    50'REVALUATION OF FORWARD +
     A                                      COMMITMENTS - CONSOLIDATED +
     A                                      EURO POSITION'
     A                                      SPACEA(1)
     A                                    50'-----------------+
     A                                      -----------------'
     A                                      SPACEA(2)
      *
     A N85                                 1'BRANCH'
     A N85        PBRNCH         3A        8TEXT('BRANCH')
     A N85        PBRNCD        30A       12TEXT('BRANCH DESCRIPTION')
     A                                      SPACEA(2)
      *
     A                                     1'VALUE'
     A                                     9'CUSTOMER'
     A                                    19'DEAL'
     A                                    26'DEAL'
     A                                    36'IN'
     A                                    49'DEALT EURO'
     A                                    89'SPOT EURO'
     A                                   116'EURO'
      *
     A                                      SPACEA(1)
     A                                     1'DATE'
     A                                    10'NUMBER'
     A                                    18'NUMBER'
     A                                    26'TYPE'
     A                                    33'CURRENCY'
     A                                    49'EQUIVALENT'
     A                                    70'MID-RATE'
     A                                    89'EQUIVALENT'
     A                                   112'PROFIT/LOSS'
     A                                      SPACEA(2)
      *-------------------------------*
      ** Report Detail Record Format 1*
      *-------------------------------*
     A          R DL0412D2
     A            PVDAT          7A        1TEXT('VALUE DATE')
     A            PCNUM          6A       10TEXT('CUST NO.')
     A            PDLNO          6A       18TEXT('DEAL NO.')
     A            PDTYP          2A       26TEXT('Deal Type')
     A  02                                28'-'
     A            PDLST          2A       29TEXT('DEAL SUB-TYPE')
      *                                          Start -158840
     A  12        PPREF         15A       18TEXT('Payment Refer.')
      *                                          End -158840
     A            PCCY           3A       35TEXT('IN CURRENCY')
      *
     A            PDEUEQ        21A       43TEXT('DEAL EURO EQUIV')
     A            PMIDRT        14A       68TEXT('MID-RATE')
     A            PSEUEQ        21A       84TEXT('SPOT EURO EQUIV')
     A            PEURPL        21A      107TEXT('EURO PROFIT/LOSS')
     A                                      SPACEA(1)
      *
     A          R DL0412D3
     A                                      SPACEB(1)
     A                                     1'TOTAL'
     A            PTOTL1        21A       43TEXT('TOTAL DEAL EURO EQ.')
     A            PTOTL2        21A       84TEXT('TOTAL SPOT EURO EQ.')
     A            PTOTL3        21A      107TEXT('TOTAL PROFIT')
     A            PTOTL4        21A      107TEXT('TOTAL LOSS')
     A                                      SPACEB(2)
     A                                   104'NET'
     A                                      SPACEB(2)
     A            PTOTL5        21A      107TEXT('NET PROFIT/LOSS')
      *-------------------------------*
      ** Report Footer Record Format **
      *-------------------------------*
     A          R DL0412D4
     A                                      SPACEB(5)
      *
     A N85                                51'*** END OF REPORT FOR +
     A                                      BRANCH ***'
     A N85                                  SPACEA(1)
     A  85                                56'*** END OF REPORT ***'
     A  85                                  SPACEA(1)
