/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Revaluation Method 'N' or ' '')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0420P1 - Revaluation of Forward Commitments Report    *
      *                  Method 'N' or ' ' (On-Request Version).      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CDL038             Date 10May05               *
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE DL0420P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'REVALUATION OF FORWARD COMMITMENTS-
     A                                        -'
     A            RCYCD          3        81
     A                                      SPACEA(1)
      *
     A                                    42'-----------------------------------
     A                                      --------'
     A                                      SPACEA(2)
      *
     A  02                                 7'BRANCH'
     A  02        RBRCA     R             14REFFLD(BRCA)
     A  02        RBRNM     R             18REFFLD(BRNM)
     A                                      SPACEA(2)
      *
     A                                     7'BOOK CODE'
     A            RBKCD          2A       17
     A            RDBKD         30A       20
     A                                      SPACEA(2)
      *
     A                                     4'VALUE'
     A                                    11'CUSTOMER'
     A                                    21'DEAL'
     A                                    29'DEAL'
     A                                    63'DEALT'
     A                                    98'REVALUED'
     A                                   119'PROFIT'
     A                                      SPACEA(1)
      *
     A                                     4'DATE'
     A                                    12'NUMBER'
     A                                    20'NUMBER'
     A                                    29'TYPE'
     A                                    43'AMOUNT'
     A                                    61'EQUIVALENT'
     A                                    80'MID-RATE'
     A                                    97'EQUIVALENT'
     A                                   119'/LOSS'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format   *
      *---------------------------*
      *
     A          R DETAILP1D1
      *
     A            RVDAT          7A        3TEXT('Value Date')
      * Begin /CSD027/
     A**50******  RCNUM          6S       12TEXT('Customer Number')                           CSD027
     A  50        RCNUM          6A       12TEXT('Customer Number')
      * End /CSD027/
     A  50        RDLNO          6S       20TEXT('Deal Number')
     A  60        RPREF         15A       12TEXT('Payment Reference')
     A  50        RDTYP          2A       29TEXT('Deal Type')
     A  50                                31'-'
     A  50        RDLST          2A       32TEXT('Deal Sub-type')
      *                                              Begin /CDL038/
     A  50        RCLAS          4A       34TEXT('Deal Sub-type')                             CDL038
     A            REAMT         16A       38TEXT('Deal Amount')                               CDL038
     A**********  REAMT         18A       36TEXT('Deal Amount')                               CDL038
      *                                              End   /CDL038/
     A            RDBCE         18A       56TEXT('Deal Equivalent')
     A            RMRAT         13  8     76TEXT('Mid Rate')
     A                                      EDTWRD('   0 .        ')
     A            RREVA         18A       92TEXT('Revaluation Amount')
     A            RPRLA         18A      112TEXT('Profit/Loss Amount')
     A                                      SPACEA(1)
      *
      *----------------------------*
      ***  Monthly Record Format   *
      *----------------------------*
      *
     A          R DETAILP1D2
     A                                      SPACEB(1)
     A                                     9'MONTHLY SUB-TOTAL'
      *
     A            RMEAMT        18A       36TEXT('Monthly Deal Amount')
     A            RMDBCE        18A       56TEXT('Monthly Deal Equivalent')
     A            RMREVA        18A       92TEXT('Monthly Revaluation')
     A            RMPRLA        18A      112TEXT('Monthly Profit/Loss')
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Annual Record Format   *
      *---------------------------*
      *
     A          R DETAILP1D3
     A                                    10'ANNUAL SUB-TOTAL'
      *
     A            RYEAMT        18A       36TEXT('Annual Deal Amount')
     A            RYDBCE        18A       56TEXT('Annual Deal Equivalent')
     A            RYREVA        18A       92TEXT('Annual Revaluation')
     A            RYPRLA        18A      112TEXT('Annual Profit/Loss')
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Total Record Format   *
      *--------------------------*
      *
     A          R DETAILP1D4
     A                                      SPACEB(1)
      *
     A            RTEAMT        18A       36TEXT('Total Deal Amount')
     A            RTDBCE        18A       56TEXT('Total Deal Equivalent')
      *
     A                                    85'TOTAL'
      *
     A            RTREVA        18A       92TEXT('Total Revaluation')
     A            RTPROF        18A      112TEXT('Total Profit')
     A                                      SPACEA(2)
     A            RTLOSS        18A      112TEXT('Total Loss')
     A                                      SPACEA(2)
      *
     A                                    85'(NET)'
      *
     A            RTPRLA        18A      112TEXT('Total Profit/Loss')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Report Trailer Format *
      *--------------------------*
      *
     A          R TRAILP1T1
     A  02                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N02                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
