/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Revaluation Method 'F'')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0420P3 - Revaluation of Forward Commitments Report    *
      *                  Method 'F' (On-Request Version).             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 225831             Date 19Mar04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  225831 - If yield curve rate combination was not found,      *
      *           print 'Rates Missing'.                              *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP3H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE DL0420P3'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'REVALUATION OF FORWARD COMMITMENTS-
     A                                       METHOD  F    -'
     A            RCYCD          3        87
     A                                      SPACEA(1)
      *
     A                                    36'-----------------------------------
     A                                      --------------------'
     A                                      SPACEA(2)
      *
     A  02                                 7'BRANCH'
     A  02        RBRCA     R             14REFFLD(BRCA)
     A  02        RBRNM     R             18REFFLD(BRNM)
     A                                      SPACEA(2)
      *
     A                                     7'BOOK CODE'
     A            RBKCD          2A       17
     A            RDBKD         30A       20
     A                                      SPACEA(2)
      *
     A                                     2'VALUE'
     A                                     8'CUSTOMER'
     A                                    17'DEAL'
     A                                    23'DEAL'
     A                                    56'DEALT'
     A                                    68'YIELD'
     A                                    77'DISCOUNT'
     A                                    94'REVALUED'
     A                                   114'PROFIT'
     A                                      SPACEA(1)
      *
     A                                     2'DATE'
     A                                     9'NUMBER'
     A                                    16'NUMBER'
     A                                    23'TYPE'
     A                                    37'AMOUNT'
     A                                    54'EQUIVALENT'
     A                                    68'CURVE'
     A                                    78'FACTOR'
     A                                    93'EQUIVALENT'
     A                                   114'/LOSS'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format   *
      *---------------------------*
      *
     A          R DETAILP3D1
      *
     A            RVDAT          7A        1TEXT('Value Date')
      * Begin /CSD027/
     A**50******  RCNUM          6S        9TEXT('Customer Number')                           CSD027
     A  50        RCNUM          6A        9TEXT('Customer Number')
      * End /CSD027/
     A  50        RDLNO          6S       16TEXT('Deal Number')
     A  60        RPREF         15A        9TEXT('Payment Reference')
     A  50        RDTYP          2A       23TEXT('Deal Type')
     A  50                                25'-'
     A  50        RDLST          2A       26TEXT('Deal Sub-type')
      *                                              Begin /CDL038/
     A  50        RCLAS          4A       28TEXT('Deal Sub-type')                             CDL038
     A            REAMT         16A       32TEXT('Deal Amount')                               CDL038
     A**********  REAMT         18A       30TEXT('Deal Amount')                               CDL038
      *                                              End   /CDL038/
     A            RDBCE         18A       49TEXT('Deal Equivalent')
     A            RYLDC          5A       68TEXT('Yield Curve')
     A            RDSCF         13  9     74TEXT('Discount Factor')
     A                                      EDTWRD('  0 .         ')
     A            RREVA         18A       89TEXT('Revaluation Amount')
     A            RPRLA         18A      108TEXT('Profit/Loss Amount')
     A                                      SPACEA(1)
     A**Start of change 225831
     A  30                                 1'* RATES MISSING - NOT VALUED *'
     A  30                                  SPACEA(1)
     A**End of change 225831
      *
      *----------------------------*
      ***  Monthly Record Format   *
      *----------------------------*
      *
     A          R DETAILP3D2
     A                                      SPACEB(1)
     A                                     4'MONTHLY SUB-TOTAL'
      *
     A            RMEAMT        18A       30TEXT('Monthly Deal Amount')
     A            RMDBCE        18A       49TEXT('Monthly Deal Equivalent')
     A            RMREVA        18A       89TEXT('Monthly Revaluation')
     A            RMPRLA        18A      108TEXT('Monthly Profit/Loss')
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Annual Record Format   *
      *---------------------------*
      *
     A          R DETAILP3D3
     A                                     5'ANNUAL SUB-TOTAL'
      *
     A            RYEAMT        18A       30TEXT('Annual Deal Amount')
     A            RYDBCE        18A       49TEXT('Annual Deal Equivalent')
     A            RYREVA        18A       89TEXT('Annual Revaluation')
     A            RYPRLA        18A      108TEXT('Annual Profit/Loss')
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Total Record Format   *
      *--------------------------*
      *
     A          R DETAILP3D4
     A                                      SPACEB(1)
      *
     A            RTEAMT        18A       30TEXT('Total Deal Amount')
     A            RTDBCE        18A       49TEXT('Total Deal Equivalent')
      *
     A                                    82'TOTAL'
      *
     A            RTREVA        18A       89TEXT('Total Revaluation')
     A            RTPROF        18A      108TEXT('Total Profit')
     A                                      SPACEA(2)
     A            RTLOSS        18A      108TEXT('Total Loss')
     A                                      SPACEA(2)
      *
     A                                    82'(NET)'
      *
     A            RTPRLA        18A      108TEXT('Total Profit/Loss')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Report Trailer Format *
      *--------------------------*
      *
     A          R TRAILP3T1
     A  02                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N02                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
