/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Revaluation w/ PCA - Method 'B'')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0421P4 - Revaluation of Forward Commitments Report    *
      *                  with PCA - Method 'B' (On-Request Version).  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CDL038             Date 10May05               *
      *                 225831             Date 19Mar04               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  225831 - If yield curve rate combination was not found,      *
      *           print 'Rates Missing'.                              *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP4H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE DL0421P4'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    36'REVALUATION OF FORWARD COMMITMENTS-
     A                                       METHOD  B    -'
     A            RCYCD          3        87
     A                                      SPACEA(1)
      *
     A                                    36'-----------------------------------
     A                                      --------------------'
     A                                      SPACEA(2)
      *
     A  02                                 7'BRANCH'
     A  02        RBRCA     R             14REFFLD(BRCA)
     A  02        RBRNM     R             18REFFLD(BRNM)
     A                                      SPACEA(2)
      *
     A                                     7'PROFIT CENTER'
     A            RPRFC          4A       21
     A            RPCDS         30A       26
     A                                      SPACEA(1)
      *
     A                                     7'BOOK CODE'
     A            FCBKCD         2A       17
     A            BDBKD         30A       20
     A                                      SPACEA(2)
      *
     A                                     2'VALUE'
     A                                     8'CUSTOMER'
     A                                    17'DEAL'
     A                                    23'DEAL'
     A                                    54'DEALT'
     A                                    84'DISCOUNT'
     A                                   101'REVALUED'
     A                                   121'PROFIT'
     A                                      SPACEA(1)
      *
     A                                     2'DATE'
     A                                     9'NUMBER'
     A                                    16'NUMBER'
     A                                    23'TYPE'
     A                                    36'AMOUNT'
     A                                    52'EQUIVALENT'
     A                                    70'MID-RATE'
     A                                    85'FACTOR'
     A                                   100'EQUIVALENT'
     A                                   121'/LOSS'
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Detail Record Format   *
      *---------------------------*
      *
     A          R DETAILP4D1
      *
     A            RVDAT          7A        1TEXT('Value Date')
      * Begin /CSD027/
     A**50******  RCNUM          6S        9TEXT('Customer Number')                           CSD027
     A  50        RCNUM          6A        9TEXT('Customer Number')
      * End /CSD027/
     A  50        RDLNO          6S       16TEXT('Deal Number')
     A  60        RPREF         15A        9TEXT('Payment Reference')
     A  50        RDTYP          2A       23TEXT('Deal Type')
     A  50                                25'-'
     A  50        RDLST          2A       26TEXT('Deal Sub-type')
      *                                              Begin /CDL038/
     A  50        RCLAS          4A       28TEXT('Deal Sub-type')                             CDL038
     A            REAMT         16A       32TEXT('Deal Amount')                               CDL038
     A************REAMT*********18A*******29TEXT('Deal Amount')                               CDL038
      *                                              End   /CDL038/
     A            RDBCE         18A       48TEXT('Deal Equivalent')
     A            RMRAT         13  8     67TEXT('Mid Rate')
     A                                      EDTWRD('   0 .        ')
     A            RDSCF         13  9     81TEXT('Discount Factor')
     A                                      EDTWRD('  0 .         ')
     A            RREVA         18A       96TEXT('Profit/Loss Amount')
     A            RPRLA         18A      115TEXT('Value Date')
     A                                      SPACEA(1)
     A**Start of change 225831
     A  30                                 1'* RATES MISSING - NOT VALUED *'
     A  30                                  SPACEA(1)
     A**End of change 225831
      *
      *----------------------------*
      ***  Monthly Record Format   *
      *----------------------------*
      *
     A          R DETAILP4D2
     A                                      SPACEB(1)
     A                                     4'MONTHLY SUB-TOTAL'
      *
      *                                              Begin /CDL038/
     A************RMEAMT********18A       29TEXT('Monthly Deal Amount')                       CDL038
     A            RMEAMT        18A       30TEXT('Monthly Deal Amount')                       CDL038
      *                                              End   /CDL038/
     A            RMDBCE        18A       48TEXT('Monthly Deal Equivalent')
     A            RMREVA        18A       96TEXT('Monthly Revaluation')
     A            RMPRLA        18A      115TEXT('Monthly Profit/Loss')
     A                                      SPACEA(2)
      *
      *---------------------------*
      ***  Annual Record Format   *
      *---------------------------*
      *
     A          R DETAILP4D3
     A                                     5'ANNUAL SUB-TOTAL'
      *
      *                                              Begin /CDL038/
     A************RYEAMT********18A       29TEXT('Monthly Deal Amount')                       CDL038
     A            RYEAMT        18A       30TEXT('Monthly Deal Amount')                       CDL038
      *                                              End   /CDL038/
     A            RYDBCE        18A       48TEXT('Monthly Deal Equivalent')
     A            RYREVA        18A       96TEXT('Monthly Revaluation')
     A            RYPRLA        18A      115TEXT('Monthly Profit/Loss')
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Total Record Format   *
      *--------------------------*
      *
     A          R DETAILP4D4
     A                                      SPACEB(1)
      *
      *                                              Begin /CDL038/
     A************RTEAMT********18A       29TEXT('Total Deal Amount')                         CDL038
     A            RTEAMT        18A       30TEXT('Total Deal Amount')                         CDL038
      *                                              End   /CDL038/
     A            RTDBCE        18A       48TEXT('Total Deal Equivalent')
      *
     A                                    89'TOTAL'
      *
     A            RTREVA        18A       96TEXT('Total Revaluation')
     A            RTPROF        18A      115TEXT('Total Profit')
     A                                      SPACEA(2)
     A            RTLOSS        18A      115TEXT('Total Loss')
     A                                      SPACEA(2)
      *
     A                                    89'(NET)'
      *
     A            RTPRLA        18A      115TEXT('Total Profit/Loss')
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Report Trailer Format *
      *--------------------------*
      *
     A          R TRAILP4T1
     A  02                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N02                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
