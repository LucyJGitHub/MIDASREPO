      ****************************************************************
/*STD *  PRTFBASE                                                    *
/*EXS *  PR_STD60                                                    *
/*EXI *  TEXT('Midas DL MM Deals due for Automatic Rollover Today')   *
      ****************************************************************
      *                                                              *
      *  Midas - Money Markets (Dealing)                             *
      *                                                              *
      *  PRTF/DL0720P1 - Money Market Deals due for Automatic        *
      *                  Rollover Today.                             *
      *                                                              *
      *  (c) Misys International Banking Systems Ltd. 2004           *
      *                                                              *
      *  Last Amend No. AR747438            Date 11Nov13             *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038              Date 10May05             *
      *                 CDL016 *CREATE      Date 03Feb04             *
      *                                                              *
      ****************************************************************
      *                                                              *
      *  AR747438 - DL0720P1 is reporting the wrong deal, changed    *
      *             'Amount' column to 'Principal Amount'.           *
      *           - Applied for MD023267                             *
      *  CDL038 - Extended Deal Sub Type                             *
      *  CDL016 - Automatic Rollover of Fixed Term Loans/Deposit     *
      *                                                              *
      ****************************************************************
      *
      **  Indicator Function Summary
      *
      * ID F  C  H  L    FUNCTION OF INDICATORS
      * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      **  FORMAT FOR HEADERS
     A          R DL0720R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'DL0720P1'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    36'Midas - MM Deals due for '
     A                                      SPACEB(1)
     A                                    61'Automatic Rollover Today '
     A N40                                86'(All Deals)'
     A  40                                86'Deal Type/Sub-Type : '
     A  40        S0DTYP         2       107TEXT('Deal Type')
     A  40                               109'-'
     A  40        S0DLST         2       110TEXT('Deal Sub-Type')
      *                                              Begin /CDL038/
     A  40        S0CLAS         4       112TEXT('Deal Class')
      *                                              End   /CDL038/
      *
     A                                    36'-------------------------'
     A                                      SPACEB(1)
     A                                    61'-------------------------'
     A N40                                86'----------'
     A  40                                86'--------------------------'
      *
      **  Format for Branch Change:
     A          R DL0720R1                  TEXT('Deal Fields Format')
      *
     A                                    11'Orig.'
     A                                      SPACEB(1)
     A                                    18'Deal'
     A                                    25'Deal'
      *                                              Begin /CDL038/
     A                                    36'Value'                                           CDL038
     A                                    43'Maturity'                                        CDL038
     A                                    57'Principal'                                     AR747438
     A                                    70'Profit'                                          CDL038
     A************************************32'Value'                                           CDL038
     A************************************39'Maturity'                                        CDL038
     A************************************66'Profit'                                          CDL038
      *                                              End   /CDL038/
     A                                     2'Customer'
     A                                      SPACEB(1)
     A                                    12'Br.'
     A                                    17'Number'
     A                                    25'Type'
      *                                              Begin /CDL038/
     A                                    37'Date'                                            CDL038
     A                                    44'Date'                                            CDL038
     A                                    52'Ccy'                                             CDL038
     A                                    58'Amount'                                          CDL038
     A                                    70'Centre'                                          CDL038
     A                                    77'Branch'                                          CDL038
     A************************************33'Date'                                            CDL038
     A************************************40'Date'                                            CDL038
     A************************************48'Ccy'                                             CDL038
     A************************************54'Amount'                                          CDL038
     A************************************66'Centre'                                          CDL038
     A************************************73'Branch'                                          CDL038
      *                                              End   /CDL038/
     A                                      SPACEA(1)
      *
      **  Format for Deal Data:
     A          R DL0720R2                  TEXT('Deal Data Format')
      *
     A            S1CNUM         6         2TEXT('Customer Number')
     A                                      SPACEB(1)
     A            S1ORBR         3        11TEXT('Original Branch')
     A            S1DLNO         6        17TEXT('Deal Number')
     A            S1DTYP         2        25TEXT('Deal Type')
     A                                    27'-'
     A            S1DLST         2        28TEXT('Deal Sub-Type')
      *                                              Begin /CDL038/
     A            S1CLAS         4        30TEXT('Deal Class')                                CDL038
     A            S1VDAT         7        35TEXT('Value Date')                                CDL038
     A            S1MDAT         7        43TEXT('Maturity Date')                             CDL038
     A            S1CCY          3        52TEXT('Currency')                                  CDL038
     A            S1AMNT        13        56TEXT('Amount')                                    CDL038
     A            S1PCEN         4        71TEXT('Profit Centre')                             CDL038
     A            S1BRCA         3        78TEXT('Branch Code')                               CDL038
     A************S1VDAT         7        31TEXT('Value Date')                                CDL038
     A************S1MDAT         7        39TEXT('Maturity Date')                             CDL038
     A************S1CCY          3        48TEXT('Currency')                                  CDL038
     A************S1AMNT        13        52TEXT('Amount')                                    CDL038
     A************S1PCEN         4        67TEXT('Profit Centre')                             CDL038
     A************S1BRCA         3        74TEXT('Branch Code')                               CDL038
      *                                              End   /CDL038/
      *
      **  Format for Deal Data:
     A          R DL0720R3                  TEXT('End  of Report')
      *
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      **  DATABASE ERROR MESSAGE.
     A          R DL0720R4                  TEXT('DATABASE ERROR MSG')
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            DBBASE    R             48REFFLD(DBASE)
     A                                    52'IN PROGRAM'
     A            DBPGM     R             63REFFLD(DBPGM)
     A                                      SPACEA(1)
      *
     A                                    33'FILE'
     A            DBFILE    R             38REFFLD(DBFILE)
     A                                    46', KEY'
     A            DBKEY     R             52REFFLD(DBKEY)
      *
      **  Format for Deal Data:
     A          R DL0720R5                  TEXT('No Details')
      *
     A                                     5'No Details to Report'
     A                                      SPACEB(3)
     A  40                                26'for DealType/Subtype Combination'
     A  40        S0DTYP         2        59TEXT('Deal Type')
     A  40                                61'-'
     A  40        S0DLST         2        62TEXT('Deal Sub-Type')
      *                                              Begin /CDL038/
     A  40        S0CLAS         4        64TEXT('Deal Class')
      *                                              End   /CDL038/
     A                                      SPACEA(1)
