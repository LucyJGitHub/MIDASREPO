      ****************************************************************
/*STD *  PRTFBASE                                                    *
/*EXI *  TEXT('Midas DL Early Maturity of Deposits/Loans Report')
/********PR_STD60*                                                   *                        249458
/*EXS *  PR_STD66                                                    *                        249458
      ****************************************************************
      *                                                              *
      *  Midas - Money Markets (Dealing)                             *
      *                                                              *
      *  PRTF/DL0725P1 - Money Market Deals due for Automatic        *
      *                  Rollover Today.                             *
      *                                                              *
      *  (c) Finastra International Limited 2004                     *
      *                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 249458             Date 31Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL017 *C *CREATE  Date 10Mar04               *
      *                                                              *
      ****************************************************************
      *                                                              *
      *  MD046248 - Finastra Rebranding                               *
      *  249458 - Change creation paramater.                         *
      *  CDL017 - Penalty Fees and Early Maturity of Fixed Term      *
      *           Loans/Deposits                                     *
      *                                                              *
      ****************************************************************
      *
      **  Indicator Function Summary
      *
      * ID F  C  H  L    FUNCTION OF INDICATORS
      * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      **  FORMAT FOR HEADERS
     A          R DL0725R0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'DL0725P1'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    36'Midas - MM Deals for whic'
     A                                      SPACEB(1)
     A                                    61'h Maturity Date amended'
      *
     A                                    36'-------------------------'
     A                                      SPACEB(1)
     A                                    61'-----------------------'
      *
      **  Format for Branch Change:
     A          R DL0725R1                  TEXT('Deal Fields Format')
      *
     A                                     4'Deal'
     A                                      SPACEB(1)
     A                                    11'Value'
     A                                    20'Seq.'
     A                                    56'Amount/'
     A                                    79'Funding'
     A                                    90'Penalty'
     A                                   101'Early'
     A                                   110'Base'
     A                                   116'Input'
     A                                   127'Auth.'
     A                                     3'Number'
     A                                      SPACEB(1)
     A                                    11'Date'
     A                                    20'No.'
     A                                    25'Customer'
     A                                    37'Rate/Spread'
     A                                    51'Ccy'
     A                                    56'Penalty Fee'
     A                                    77'Spread/Rate'
     A                                    90'Pay/Rec.'
     A                                   100'Mat.Date'
     A                                   110'Rate'
     A                                   116'User'
     A                                   127'User'
     A                                      SPACEA(1)
      *
      **  Format for Deal Data:
     A          R DL0725R2                  TEXT('Deal Data Format')
      *
     A            S1DLNO         6  0      3TEXT('Deal Number')
     A                                      SPACEB(1)
     A            S1VDAT         7        11TEXT('Value Date')
     A            S1SEQN         3  0     20TEXT('Sequence Number')
     A            S1CNUM        10        25TEXT('Customer')
     A            S1SPRD        12        37TEXT('Rate/Spread')
     A            S1CCY          3        51TEXT('Currency')
     A            S1AMNP        19        56TEXT('Amount/Penalty Fee')
     A            S1FSPR        12        77TEXT('Funding Spread/Rate')
     A            S1PENP         1        93TEXT('Penalty Pay/Receive')
     A            S1EMDT         7       100TEXT('Early Maturity Date')
     A            S1BSRT         2       111TEXT('Base Rate')
     A            S1INPU        10       116TEXT('Input User')
     A            S1AUTU        10       127TEXT('Auth. User')
      *
      **  Format for Deal Data:
     A          R DL0725R3                  TEXT('End  of Report')
      *
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      **  DATABASE ERROR MESSAGE.
     A          R DL0725R4                  TEXT('DATABASE ERROR MSG')
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            DBBASE    R             48REFFLD(DBASE)
     A                                    52'IN PROGRAM'
     A            DBPGM     R             63REFFLD(DBPGM)
     A                                      SPACEA(1)
      *
     A                                    33'FILE'
     A            DBFILE    R             38REFFLD(DBFILE)
     A                                    46', KEY'
     A            DBKEY     R             52REFFLD(DBKEY)
      *
      **  Format for Deal Data:
     A          R DL0725R5                  TEXT('No Details')
      *
     A                                     5'No Details to Report'
     A                                      SPACEB(3)
