      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Fixed Deposits Register')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0727P1 - DL Fixed Deposits Register                   *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL024  *CREATE    Date 31Mar04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL024 - Fixed Deposits Register                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *
     A          R HEAD                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL0727P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    53'Fixed Deposit Register'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    53'----------------------'
     A                                      SPACEA(2)
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
     A                                      SPACEA(2)
      *
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                     3'Deal Type'
     A            PTYPE          2A       13
     A                                    17'Deal No.'
     A            PDLNO          6A       26
     A                                      SPACEA(1)
     A                                     3'Customer No.'
     A            PCNUM          6A       16
     A            PSNAM         10A       23
     A                                    35'Old Deal No.'
     A            PRODN          6A       48
     A                                    56'New Deal No.'
     A            PRBDN          6A       70
     A                                    78'Source of funds'
     A            PSRFD         20A       95
     A                                      SPACEA(1)
     A                                     3'Currency'
     A            PCCY           3A       12
     A                                    17'Principal'
     A            PPCPL         21A       27
     A                                    50'Value Date'
     A            PVDAT          6A       62
     A                                    71'Maturity Date'
     A            PMDAT          6A       86
     A                                      SPACEA(1)
     A                                     3'Interest Rate'
     A            PINTR         11A       18
     A                                    31'Calc Method'
     A            PICBS          1A       45
     A                                    48'Interest Paid'
     A            PIPRD         21A       62
     A                                    85'Blocked Ind'
     A            PBLKI          1A       97
     A                                      SPACEA(1)
      *
     A          R NODETL
     A N37                                46'***  NO DETAILS TO +
     A                                      REPORT  ***'
     A                                      SPACEB(3)
     A  37                                46'***  NO DETAILS TO +
     A                                      REPORT FOR THIS BRANCH  ***'
     A                                      SPACEB(3)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
