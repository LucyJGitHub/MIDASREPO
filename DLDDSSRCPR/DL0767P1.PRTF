      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Int statement for Call/Time Deposits')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0767P1 - Int. Statements for Call/Deposits            *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL012  *CREATE    Date 13Aug03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL012 - Interest Statement for Call/Time Deposits.          *
      *           PRINTING OF INTEREST IN BASE CCY SUPPRESSED DUE TO  *
      *           BANK'S REQUEST. ALSO SUPPRESSED TOTAL INTEREST.     *
      *                                                               *
     A*****************************************************************
     A                                      REF(*LIBL/DLTCSTPD DLTCSTP0)
      *================================================================
      ** Record format for date and page number
      *================================================================
     A          R DL0767HD
     A                                      SPACEB(013)
     A                                     2
     A                                      'Date'
     A            P#DATE         7A  O     7
     A                                    68
     A                                      'Page'
     A                                    72
     A                                      EDTCDE(Z)
     A                                      PAGNBR
      *================================================================
      ** Record format for Customer name details
      *================================================================
     A          R DL0767CD
      *
     A                                      SPACEB(013)
     A                                     2
     A                                      'Date'
     A            P#DATE         7A  O     7
     A                                    68
     A                                      'Page'
     A                                    72
     A                                      EDTCDE(Z)
     A                                      PAGNBR
      *
     A            PCUSA1        35A  O     2
     A                                      SPACEB(003)
     A            PCUSA2        35A  O     2
     A                                      SPACEB(001)
     A            PCUSA3        35A  O     2
     A                                      SPACEB(001)
     A            PCUSA4        35A  O     2
     A                                      SPACEB(001)
     A                                     2
     A                                      'Dear Sir/Madam'
     A                                      SPACEB(002)
      *================================================================
      ** Record format for showing the period
      *================================================================
     A          R DL0767NM
      *
     A                                     2
     A                                      'Interest Statement for the period'
     A                                      UNDERLINE
     A                                      SPACEB(002)
     A                                    35
     A                                      ' from '
     A                                      UNDERLINE
     A            P#DAT1         7A  O    41
     A                                      UNDERLINE
     A                                    48
     A                                      ' to '
     A                                      UNDERLINE
     A            P#DAT2         7A  O    52
     A                                      UNDERLINE
     A                                     2
     A                                      'We confirm the following interest'
     A                                      SPACEB(002)
     A                                    36
     A                                      'earned on your deposit placed '
     A                                    66
     A                                      'with us'
      *================================================================
      ** Record format for column headings
      *================================================================
     A          R DL0767HR
     A                                     2
     A                                      'Ref No'
     A                                      UNDERLINE
     A                                      SPACEB(002)
     A                                    10
     A                                      'Int Due'
     A                                      UNDERLINE
     A                                    19
     A                                      'Value/'
     A                                      UNDERLINE
     A                                    29
     A                                      'Ccy'
     A                                      UNDERLINE
     A                                    41
     A                                      'Principal'
     A                                      UNDERLINE
     A                                    54
     A                                      'Int. Rate'
     A                                      UNDERLINE
     A                                    71
     A                                      'Interest'
     A                                      UNDERLINE
     A                                    83'Remarks'
     A                                      UNDERLINE
      *
     A                                    10
     A                                      'Date'
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                    19
     A                                      'Maturity'
     A                                      UNDERLINE
     A                                    55
     A                                      '%'
     A                                      UNDERLINE
     A                                    54
     A                                      '('
     A                                    56
     A                                      ')'
     A                                    70
     A                                      '('
     A                                    71
     A                                      'Deal ccy'
     A                                      UNDERLINE
     A                                    79
     A                                      ')'
     A                                    19
     A                                      'Date'
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*================================================================
      ** Record format for printing child name under a parent
      *================================================================
     A          R DL0767CH
     A            PCHLD1        35A  O     2
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A*================================================================
      ** Record format for statement details
      *================================================================
     A          R DL0767DT
     A                                      SPACEB(001)
     A            GDLNO     R        O     2
     A            IDATP          7A  O    10
     A            VDATP          7A  O    19
     A            GDCCY     R        O    29
     A            PPCPL         16A  O    34
     A            GINTR     R        O    51EDTWRD('    .       ')
     A            PDINT         16A  O    64
     A            MDATP          7A  O    19
     A                                      SPACEB(001)
     A************PUGINT        16A  O    64
     A                                      SPACEA(001)
     A
     A*================================================================
      ** Record format for total interest in base currency
      *================================================================
     A          R DL0767TL
     A                                      SPACEB(002)
     A                                    41
     A                                      'Total Int. (Base ccy)'
     A            PTGINT        16A  O    64
     A                                      SPACEA(001)
     A*================================================================
      ** Record format for end of report
      *================================================================
     A          R DL0767EN
     A                                      SPACEB(003)
     A                                    28
     A                                      '***End Of Statements***'
     A                                      SPACEB(001)
     A*================================================================
      ** Record format for no details
      *================================================================
     A          R DL0767ET
     A                                      SPACEB(003)
     A                                    43
     A                                      '*** No Details To Report ***'
     A*================================================================
      ** Record format for skipping to a new page with cont'd sign
      *================================================================
     A          R DL0767CT
     A                                      SPACEB(002)
     A                                      SKIPA(001)
     A            PCONST        10A       64
     A
     A*================================================================
      ** Record format for skipping to a new page
      *================================================================
     A          R DL0767FT
     A                                      SPACEB(002)
     A                                      SKIPA(001)
     A                                    64
     A                                      ' '
