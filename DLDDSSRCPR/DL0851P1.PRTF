     A*****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Deal number input gaps report')               *
     A*****************************************************************
     A*                                                               *
     A*  MIDAS -  Dealing Module                                      *
     A*                                                               *
     A*  PRTF/DL0851P1 - DL0851  Deal Number Input Gaps               *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2002            *
     A*                                                               *
      *  Last Amend No. CDL098             Date 06Oct17               *
      *  Prev Amend No. 231310             Date 19Aug14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *                 205911             Date 23May02               *
      *                 169253 *CREATE     Date 15May02               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CDL098 - Cancelled Deal Numbers                              *
      *  231310 - DL0851P1: Deal Number Input Gaps Report.            *
      *           Added space before keyword on record PREVRP to      *
      *           prevent overlapping of lines printed.               *
      *  205911 - Correct alignment of Next Available Deal Number on  *
      *           DL0851P1.                                           *
      *  169253 - Deal Number Input Gaps Report (Rewrite).            *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
      *
      **  Report header
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL0851P1'
     A            BJURPT    R             37REFFLD(ZZ053)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(ZZ007)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A  10                                42'INTERIM -'
     A                                    52'DEAL NUMBER INPUT GAPS'
     A N10                                  SPACEA(1)
     A  10                                75'-'
     A  10                                77TIME
     A                                      EDTWRD('  .  .  ')
     A                                      SPACEA(1)
     A  10                                42'**********'
     A                                    52'----------------------'
     A N10                                  SPACEA(1)
     A  10                                74'***********'
     A  10                                  SPACEA(1)
      *
      **  COLUMN  heading
      *
     A          R COLHDG                    SPACEB(2)
     A                                   115'NEXT AVAILABLE'
     A                                      SPACEA(1)
     A                                     7'RANGE        NO.  NAME'
     A                                    62'B/F      DEAL NUMBERS'
     A                                    91'CONDITION'
     A                                   118'DEAL NUMBER'
     A                                      SPACEA(2)
     A
      *
      **  Report Details
      *
     A          R RANGE                     SPACEB(1)
      *
     A            FDELR          6  0      2
     A                                    10'-'
     A            LDELR          6  0     12
     A            RNGENO         2A       20
     A            DDELR         30A       25
     A            NADLR          6  0     60
      *
      ** Start of Change: 205911
     A**********R UNALOC                                                                      205911
     A          R UNALOC                    SPACEB(1)
      ** End of Change: 205911
      *
     A            LRLDE1         6  0      2
     A                                    10'-'
     A            FDEL1          6  0     12
     A                                    60'UNALLOCATED RANGE'
      *
      ** Start of Change: 205911
     A**********                             SPACEA(1)                                        205911
      ** End of Change: 205911
      *
     A          R OUTRNG                    SPACEB(1)
      *
     A                                    20'**'
     A            DLNO           6  0     71
     A                                    91'OUT OF RANGE'
     A                                       SPACEA(1)
      *
      **Start of change CDL098
     A  04                               104'- REVERSED'
      **End of change CDL098
     A**Start of Change: 231310
     A**********R PREVRP                                                                      231310
     A          R PREVRP                     SPACEB(1)
     A**End of Change: 231310
      *
     A N28 29     WDLNO          6  0     71
     A  29 50                             78'-'
     A  29 50     WDLPRV         6  0     80
     A N28 29                             91'PREVIOUSLY REPORTED'
      *
      ** Start of Change: 205911
     A**29******                             SPACEA(1)                                        205911
     A  29N60                                SPACEA(1)
      *
     A**********R NOTUSE                                                                      205911
     A          R NOTUSE                     SPACEB(1)
      ** End of Change: 205911
      *
     A            WNADL          6  0     71
     A  31                                78'-'
     A  31        @DLNOB         6  0     80
     A                                    91'DEAL NUMBERS NOT USED'
      ** Start of Change: 205911
     A**********                             SPACEA(1)                                        205911
      *
     A**********R REVERS                                                                      205911
     A          R REVERS                     SPACEB(1)
      ** End of Change: 205911
      *
     A            DLNO           6  0     71
     A                                    91'REVERSED'
      *
      ** Start of Change: 205911
     A**********                             SPACEA(1)                                        205911
      ** End of Change: 205911
      *
     A          R NXTAVL
      *
     A            WNADL          6  0    122
     A                                       SPACEA(2)
      *
     A          R RAGNOT
      *
     A                                    91'RANGE NOT USED TODAY'
      *
      ** Start of Change: 205911
     A**********                             SPACEA(1)                                        205911
      ** End of Change: 205911
      **Start of change CDL098
     A          R GAPS
     A            GAPNO          6  0     71
     A N60                                   SPACEB(1)
     A  73                                91'MISSING'
     A N73                                91'CANCELLED'
      *
     A          R OORCAN                    SPACEB(1)
      *
     A                                    20'**'
     A            CLDLNO         6  0     71
     A                                    91'OUT OF RANGE'
     A                                   104'- CANCELLED'
     A                                       SPACEA(1)
      **End of change CDL098
      *
      **  Database Error Reference
      *
     A          R DBERRF                    SPACEB(3)
     A                                    32'*** REFERENCE DL0851'
     A                                    53'DATABASE ERROR ***'
      *
      **  Report Footer
      *
     A          R TRAILER                   SPACEB(3)
     A                                    51'*** END OF REPORT ***'
