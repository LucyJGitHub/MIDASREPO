      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL SSI warning report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL0950P1 - Midas DL SSI warning report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CDL031   *CREATE   Date 13Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL031 - Effective Date on Extended SSI                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2002')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     6
     A                                      'REFERENCE DL0950P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
      *
     A                                    39
     A                                      'Standard Settlement Instructions C-
     A                                      onflict report'
     A                                      SPACEB(001)
      *
     A                                    39
     A                                      '-----------------------------------
     A                                      --------------'
     A                                      SPACEB(001)
      *
     A                                     1
     A                                      'The following lists SSIs which wer-
     A                                      e AMENDED or DELETED today, togethe-
     A                                      r with Deals affected.'
     A                                      SPACEB(002)
      **************************************************************************
     A          R DETAIL1
     A                                      SPACEB(001)
     A
     A                                     1'SSI:'
     A            P#CUST    R        O     6
     A                                      REFFLD(SDEXSTD0/BYCUST *LIBL/SDEXST-
     A                                      PD)
     A            P#CSSN    R        O    +1
     A                                      REFFLD(SDCUSTD0/BBCSSN *LIBL/SDCUST-
     A                                      PD)
     A            P#CYCD    R        O    24
     A                                      REFFLD(SDEXSTD0/BYCYCD *LIBL/SDEXST-
     A                                      PD)
     A            P#TRTY    R        O    28
     A                                      REFFLD(SDEXSTD0/BYTRTY *LIBL/SDEXST-
     A                                      PD)
     A                                    32
     A                                      'Effective Date'
     A            P#EFFD         6S 0O    47
     A                                      EDTWRD('  /  /  ')
     A            P#DESC        17A  O    +3
      *
     A                                     7
     A                                      'Deal No.  Deal type  Subtype'
     A                                      SPACEB(002)
      **************************************************************************
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            P#DLNO    R        O     7
     A                                      REFFLD(DEALSDBF/DLNO *LIBL/DEALSDB)
     A            P#DTYP    R        O    +7
     A                                      REFFLD(DEALSDBF/DTYP *LIBL/DEALSDB)
     A            P#DLST    R        O    +8
     A                                      REFFLD(DEALSDBF/DLST *LIBL/DEALSDB)
      *                                              Begin /CDL038/
     A            P#CLAS    R        O    +0                                                  CDL038
     A                                      REFFLD(DEALSDBF/CLAS *LIBL/DEALSDB)               CDL038
      *                                              End   /CDL038/
      **************************************************************************
     A          R NODETAIL
     A                                      SPACEB(002)
     A                                    48
     A                                      '***  NO DETAILS TO REPORT ***'
      **************************************************************************
     A          R TRAILER
     A                                      SPACEB(003)
     A                                    51
     A                                      '***  END OF REPORT  ***'
      **************************************************************************
