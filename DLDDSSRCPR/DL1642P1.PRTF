      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL OIS Deal Rate Fixing Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL1642P1 - OIS Deal Rate Fixing Report                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CIR007  *CREATE    Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  STATEMENT HEADER            *
      *--------------------------------*
      *
     A          R PGHEADER                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL1642P1'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    52'OIS DEAL RATE FIXING +
     A                                      REPORT'
      *
     A                                    82'BRANCH -'
     A            RBRCD          3        91
     A            RBRNM         30        95
      *
     A                                   125' '
     A                                      SPACEA(1)
     A                                    52'---------------------+
     A                                      ------'
     A                                      SPACEA(2)
      *
      *------------------------*
      ***  STATEMENT HEADER    *
      *------------------------*
      *
     A          R DTHEADER
     A                                     3'Deal'
     A                                    12'Customer'
     A                                    23'Ccy'
     A                                    32'Principal'
     A                                    51'Our'
     A                                    66'Our'
     A                                    80'Their'
     A                                    95'Their'
     A                                   115'Net'
     A                                   129'P'
     A
     A                                      SPACEA(1)
     A
     A                                     2'Number'
     A                                    11'Short Name'
     A                                    51'Rate'
     A                                    63'Interest'
     A                                    81'Rate'
     A                                    93'Interest'
     A                                   112'Settlement'
     A                                   129'R'
     A
     A                                      SPACEA(1)
     A
     A                                     2'------'
     A                                    11'----------'
     A                                    23'---'
     A                                    28'-----------------'
     A                                    47'----------'
     A                                    59'-----------------'
     A                                    78'----------'
     A                                    90'-----------------'
     A                                   109'-----------------'
     A                                   129'-'
     A
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  STATEMENT DETAIL    *
      *------------------------*
      *
     A          R DLDETAIL
      *
     A            RDLNO          6  0      2
     A            RCSSN         10        11
     A            RCUCY          3        23
     A            RPRIN         18        28
     A            ROINR         11  7     45EDTWRD('  0 .       ')
     A            ROINT         18        59
     A            RTINR         11  7     76EDTWRD('  0 .       ')
     A            RTINT         18        90
     A            RNTSL         18       109
     A            RPYRC          1       129
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  REPORT TRAILER      *
      *------------------------*
      *
     A          R TRAILER
     A  37                                  SPACEB(3)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A  37        RBRCD          3        77
     A  37                                82'***'
     A  37                                  SPACEA(5)
     A N37                                51'***  END OF REPORT  ***'
      *
