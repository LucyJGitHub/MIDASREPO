      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL OIS Deals Bulk Rate Correction Audit Rprt')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL1666AU - OIS Deals Bulk Rate Correction Audit Report  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CIR007  *CREATE    Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R DL1666A0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL1666AU'
     A            RBANK         53        37
     A                                   101'DATE'
     A            RMRDT          7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    52'OIS DEALS BULK RATE +
     A                                       CORRECTION AUDIT REPORT'
     A                                      SPACEA(1)
      *
     A                                    52'------------------------+
     A                                      ---------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R DL1666A1
      *
     A                                    50'Total Number of Deals +
     A                                       Affected'
     A            RDCOUN         4  0     83EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    50'------------------------+
     A                                       ------'
     A                                      SPACEA(2)
     A                                    50'Total Number of Records +
     A                                       Updated'
      *
     A            RRCOUN         4  0     83EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    50'------------------------+
     A                                       -------'
     A                                      SPACEA(3)
      *
     A                                     6'OIS Compounding Interest +
     A                                      Rate file  - PF/DLDOISPD'
     A                                      SPACEA(1)
      *
     A                                     6'----------------------+
     A                                      ---------------'
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R DL1666A2
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DL1666A3
     A                                      SPACEB(3)
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *------------------------*
      ***  AUDIT TRAILER       *
      *------------------------*
      *
     A          R DL1666A4
     A                                      SPACEB(3)
     A                                    51'***  END OF REPORT  ***'
      *
