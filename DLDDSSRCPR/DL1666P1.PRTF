      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL OIS Deals Bulk Rate Correction Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL1666P1 - OIS Deal Bulk Rate Correction - Report       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 226723             Date 12May04               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CIR007  *CREATE    Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  226723 - Allow the insertion of negative base rate for OIS.  *
      *           Applied fix 219286.                                 *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  STATEMENT HEADER            *
      *--------------------------------*
      *
     A          R DL1666R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL1666P1'
     A            RBANK         53        37
     A                                   101'DATE'
     A            RMRDT          7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A                                    37'OIS DEALS BULK RATE +
     A                                      CORRECTION REPORT'
     A                                    82'BRANCH -'
     A            RBRCD          3        91
     A            RBRNM         30        95
     A                                   125' '
     A                                      SPACEA(1)
     A                                    37'------------------------+
     A                                      ----------------'
     A                                      SPACEA(2)
      *
      *------------------------*
      ***  RATE CHANGE DETAIL  *
      *------------------------*
      *
     A          R DL1666R1
      *
     A                                    10'CURRENCY'
     A                                    18':'
     A            RCURR          3        20
     A                                    27'BASE RATE DATE'
     A                                    41':'
     A            RBRDT          6  0     43EDTCDE(Y)
     A                                    53'BASE RATE CODE'
     A                                    67':'
     A            RCODE          2        69
     A                                    75'NEW BASE RATE'
     A                                    88':'
      *
      **Start of change 226723
      *
     A**********  RNEWR         10  7     90EDTWRD(' 0 .       ')                             226723
     A            RNEWR         10  7     90EDTWRD(' 0 .       -')
      *
      **End of change 226723
      *
     A                                      SPACEA(2)
     A                                    10'The following deals were +
     A                                      affected by this update.'
     A                                      SPACEA(2)
      *
      *------------------------*
      ***  STATEMENT HEADER    *
      *------------------------*
      *
     A          R DL1666R2
      *
     A                                     4'Deal'
     A                                    14'Customer Report Name'
     A                                    38'Side'
     A                                    48'Deal'
     A                                    58'Value'
     A                                    68'Maturity'
     A                                    79'Last Interest'
     A                                    95'Next Interest'
     A                                      SPACEA(1)
     A                                     3'Number'
     A                                    48'Date'
     A                                    59'Date'
     A                                    70'Date'
     A                                    79'Payment Date'
     A                                    95'Payment Date'
     A                                      SPACEA(1)
     A                                     3'------'
     A                                    14'--------------------'
     A                                    38'-----'
     A                                    46'--------'
     A                                    57'--------'
     A                                    68'--------'
     A                                    79'-------------'
     A                                    95'-------------'
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  STATEMENT DETAIL    *
      *------------------------*
      *
     A          R DL1666R3
      *
     A            RDLNO          6  0      3
     A            RCRNM         20        14
     A            RSIDE          5        38
     A            RDDAT          6  0     46EDTCDE(Y)
     A            RVDAT          6  0     57EDTCDE(Y)
     A            RMDAT          6  0     68EDTCDE(Y)
     A            RLIDT          6  0     81EDTCDE(Y)
     A            RNIDT          6  0     97EDTCDE(Y)
     A                                      SPACEA(1)
      *
      *------------------------*
      ***  REPORT TRAILER      *
      *------------------------*
      *
     A          R DL1666R4
      *
     A  37                                  SPACEB(3)
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A  37        RBRCD          3        77
     A  37                                82'***'
     A  37                                  SPACEA(5)
     A N37                                51'***  END OF REPORT  ***'
      *
