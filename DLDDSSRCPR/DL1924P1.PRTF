      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL FRA/IRS Fix and Settlement Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL1924P1 - FRA/IRS Rate Fix and Settlement Report       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 226723             Date 12May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW005             Date 01Aug96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  226723 - Correctly print negative base rates.                *
      *           Applied fix 219286.                                 *
      *  CSW005 - MG34n and MG36n message generation                  *
      *                                                               *
      *****************************************************************
      *
      ** Page Headings/New Branch/New Deal Type
      *
     A          R PGHEAD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE DL1924P1'
     A            RTITLE        53A  O    37
     A                                   101
     A                                      'DATE'
     A            RDATE          7A  O   106
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    45
     A                                      'FRA/IRS INPUT CYCLE RATE FIXING AN-
     A                                      D SETTLEMENT REPORT'
     A                                      SPACEB(002)
     A  50                               100
     A                                      '** RE-RUN **'
     A                                    45
     A                                      '-----------------------------------
     A                                      -------------------'
     A                                      SPACEB(001)
     A                                     1
     A                                      'BRANCH'
     A                                      SPACEB(002)
     A                                    12
     A                                      ':'
     A            RBRCA          3A  O    14
     A            RBRNM         30A  O    20
      *
      ** Forward Rate Agreement (FRA) Heading
      *
     A          R FRAHEAD
     A                                      SPACEB(002)
     A                                     1
     A                                      'DEAL TYPE  :'
     A            RDTYP          2A  O    14
     A                                    20
     A                                      'FORWARD RATE AGREEMENTS'
     A                                     1
     A                                      'DEAL'
     A                                      SPACEB(002)
     A                                     9
     A                                      'COUNTERPARTY'
     A                                    43
     A                                      'CCY'
     A                                    47
     A                                      'PRINCIPAL AMOUNT'
     A                                    64
     A                                      'BUY/'
     A                                    70
     A                                      'REFERENCE RATE'
     A                                    86
     A                                      'BASE'
     A                                    92
     A                                      'NEW BASE RATE'
     A                                   107
     A                                      'SETTLEMENT AMOUNT'
     A                                     1
     A                                      'NUMBER'
     A                                      SPACEB(001)
     A                                    64
     A                                      'SELL'
     A                                    86
     A                                      'RATE'
     A                                     1
     A                                      '------'
     A                                      SPACEB(001)
     A                                     9
     A                                      '---------------------------------'
     A                                    43
     A                                      '---'
     A                                    47
     A                                      '----------------'
     A                                    64
     A                                      '----'
     A                                    70
     A                                      '--------------'
     A                                    86
     A                                      '----'
     A                                    92
     A                                      '-------------'
     A                                   107
     A                                      '-----------------'
      *
      ** Forward Rate Agreement (FRA) Detail
      *
     A          R FRADTL
     A                                      SPACEB(001)
     A            RDLNO          6S  O     1
     A            RCPTY         10A  O     9
     A            RCPTYN        20A  O    20
     A            RCCY           3A  O    43
     A            RPAMT         16A  O    47
     A            RBSIN          4A  O    64
     A            RRRTE         10A  O    74
     A            RBRTC          2S  O    87
     A            RBRTE         10A  O    95
      *
      **Start of change 226723
      *
     A            RBSGN          1A  O   105
      *
      **End of change 226723
      *
     A            RSAMT         15A  O   109
      *
      ** Interest Rate Swap (IRS) Heading (Single or Cross Currency)
      *
     A          R IRSHEAD
     A                                      SPACEB(002)
     A                                     1
     A                                      'DEAL TYPE  :'
     A            RDTYP          2A  O    14
     A            RDTYPT        20A  O    20
     A                                     1
     A                                      'DEAL'
     A                                      SPACEB(002)
     A                                     9
     A                                      'COUNTERPARTY'
     A                                    45
     A                                      'CCY'
     A                                    49
     A                                      'PRINCIPAL AMOUNT'
     A                                    66
     A                                      'OURS/'
     A                                    73
     A                                      'BASE'
     A                                    78
     A                                      'SPREAD'
     A                                    90
     A                                      'PREVIOUS'
     A                                   102
     A                                      'BASE RATE'
     A                                   114
     A                                      'NEW EFFECT.'
     A                                     1
     A                                      'NUMBER'
     A                                      SPACEB(001)
     A                                    66
     A                                      'THEIRS'
     A                                    73
     A                                      'RATE'
     A                                    90
     A                                      'EFFEC. RATE'
     A                                   117
     A                                      'RATE'
     A                                     1
     A                                      '------'
     A                                      SPACEB(001)
     A                                     9
     A                                      '---------------------------------'
     A                                    45
     A                                      '---'
     A                                    49
     A                                      '----------------'
     A                                    66
     A                                      '------'
     A                                    73
     A                                      '----'
     A                                    78
     A                                      '-----------'
     A                                    90
     A                                      '-----------'
     A                                   102
     A                                      '-----------'
     A                                   114
     A                                      '-----------'
      *
      ** Interest Rate Swap (IRS) Detail (Single and Cross Currency)
      *
     A          R IRSDTL
     A                                      SPACEB(001)
     A            RDLNO          6S  O     1
     A            RCPTY         10A  O     9
     A            RCPTYN        20A  O    20
     A            RCCY           3A  O    45
     A            RPAMT         16A  O    49
     A            ROT            6A  O    66
     A            RBRTC          2S  O    74
     A            RSPRD         10A  O    78
     A            RSIGN          1A  O    88
     A            RPERTE        10A  O    91
      *
      **Start of change 226723
      *
     A            RPSGN          1A  O   101
      *
      **End of change 226723
      *
     A            RBRTE         10A  O   103
      *
      **Start of change 226723
      *
     A            RBSGN          1A  O   113
      *
      **End of change 226723
      *
     A            RNERTE        10A  O   115
      *
      **Start of change 226723
      *
     A            RNSGN          1A  O   125
      *
      **End of change 226723
      *
     A  77                                 9
     A                                      'INTEREST SETTLEMENT AMOUNT:'
     A                                      SPACEB(001)
     A  77        RSAMT         15A  O    37
      *
      ** End of Report
      *
     A          R ENDRPT
     A                                      SPACEB(002)
     A                                    50
     A                                      '*** END OF REPORT FOR BRANCH ***'
     A                                      SPACEB(002)
