      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL FRA/IRS Rates File List Totals')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS MODULE
     A*                                                               *
     A*  PRTF/DL1932AU - FRA/IRS RATES FILE LIST TOTALS               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01328             Date 08Mar91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01328 - FRA/IRS Revaluations - New Printer File.            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*    20  -  ON = FRA MODE; OFF = IRS MODE                       *
     A*    54  -  AT LEAST ONE SET OF DETAILS OUTPUT - USED FOR       *
     A*           PRINTER FILE CONDITIONING.                          *
     A*    63  -  PROGRAM CALLED AT END OF DAY                        *
     A*    64  -  INPUT AND CALCULATED FILE TOTALS DO NOT AGREE       *
     A*    80  -  DATA BASE ERROR INDICATOR                           *
     A*                                                               *
     A*****************************************************************
     A*
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR WHOLE REPORT
     A*
     A          R DLTOTSR0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL1932AU'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(1)
     A                                      EDTWRD('0 :  :  ')
     A*
     A  20                                49'FRA'
     A N20                                49'IRS'
     A                                    52' RATES FILE LIST TOTALS'
     A                                      SPACEA(1)
     A*
     A                                    49'-------------------'
     A                                    68'-------'
     A                                      SPACEA(3)
     A*
     A  63                                49'FILE CONTROL'
     A                                    65'CALCULATED'
     A                                      SPACEA(1)
     A  63                                49'------------'
     A                                    65'----------'
     A                                      SPACEA(2)
     A*
     A  20                                 6'FILE - FRA RATES TABLE +
     A                                      - LF/DLFRARL4'
     A N20                                 6'FILE - IRS RATES TABLE +
     A                                      - LF/DLIRSRL4'
     A                                     6'-----------------------+
     A                                      -------------'
     A                                      SPACEB(1)
     A                                      SPACEA(3)
     A                                    10'NUMBER OF RECORDS REPORTED:'
     A  63        RRNREC    R             53REFFLD(NREC)
     A                                      EDTWRD('  0 ')
     A            RRCALC         4  0     68TEXT('CALC NO. OF RECORDS')
     A                                      EDTWRD('  0 ')
     A  64                                81'***  DIFFERENCE  ***'
     A*
     A N54N80                             48'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A*
     A**  DATABASE ERROR MESSAGE.
     A*
     A          R DLTOTSR1                  TEXT('DATABASE ERROR MSG')
     A                                      SPACEB(3)
     A                                    48 '***   PRINT TERMINATED   ***'
     A                                      SPACEA(2)
     A                                    45 'DATABASE ERROR'
     A            DBASE          3  0     60
     A                                    65 'FILE'
     A            DBFILE         8        70
     A                                      SPACEA(1)
     A                                    45 'KEY'
     A            DBKEY         29        49
