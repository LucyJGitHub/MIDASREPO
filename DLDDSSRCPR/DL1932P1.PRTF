      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL FRA/IRS Rates File List')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS MODULE
     A*                                                               *
     A*  PRTF/DL1932P1 - FRA/IRS RATES FILE LIST.                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01328             Date 08Mar91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01328 - FRA/IRS Revaluations - New Printer File.            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*    04  -  OUPPUT BASE RATE CODE AND NAME                      *
     A*    20  -  ON = FRA MODE; OFF = IRS MODE                       *
     A*                                                               *
     A*****************************************************************
     A*
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  REPORT HEADER FORMAT
     A*
     A          R DL1932R0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL1932P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A  20                                53'FRA'
     A N20                                53'IRS'
     A                                    57'RATES FILE LIST'
     A                                      SPACEA(1)
     A                                    53'-------------------'
     A                                      SPACEA(3)
     A*
     A                                     5'CURRENCY'
     A            FRCCY          3        15
     A            A6CYNM        14        19
     A                                      SPACEA(2)
     A*
     A                                     5'BASE RATE'
     A                                    20'BASE RATE NAME'
     A                                    56'PERIOD FROM SPOT'
     A  20                                83'VALUE'
     A N20                                83'MATURITY'
     A                                   100'RATE'
     A                                       SPACEA(1)
     A                                     7'CODE'
     A                                    56'YEARS MONTHS DAYS'
     A                                    83'DATE'
     A                                      SPACEA(1)
     A*
     A**  FORMAT TO OUTPUT DETAIL LINES.
     A*
     A          R DL1932R1                  TEXT('DETAIL OUTPUT FMT')
     A*
     A** IF CHANGE OF BASE RATE CODE PRINT OUT CODE AND NAME
     A*
     A**04        FRBRTT         2  0      8SPACEB(1)                                         CSD103
     A  04        FRBRTT         2         8SPACEB(1)                                         CSD103
     A  04        BABSRN        30        20
     A*
     A            FRPDYR         2  0     56
     A            FRPDMN         2  0     62
     A            FRPDDY         2  0     69
     A*
     A            FRVDMY         7        82
     A            RATE          13       100
     A                                      SPACEA(1)
     A**  END OF REPORT FORMAT
     A          R DL1932R2                  TEXT('END OF REPORT FORMAT')
     A*
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
     A*
