      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL FRA/IRS Rates Audit Control Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module.
     A*                                                               *
     A*  DL1933AU - FRA/IRS RATES AUDIT CONTROL REPORT                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01328             Date 01Mar91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01328 - FRA/IRS Revaluations - New Print File.              *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A          R DL1933A0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(2)
     A                                     7'REFERENCE DL1933AU'
     A*
     A            RRTITL        53        36
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                   100'TIME'
     A                                      SPACEB(1)
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(2)
     A*
     A N10                                48'FRA RATES AUDIT REPORT +
     A                                       TOTALS'
     A                                      SPACEA(1)
     A  10                                48'IRS RATES AUDIT REPORT +
     A                                       TOTALS'
     A                                      SPACEA(1)
     A*
     A                                    48'-----------------------+
     A                                       ------'
     A                                      SPACEA(3)
     A*
     A          R DL1933A1                  TEXT('DTL HEADER FORMAT')
     A                                      SPACEB(2)
     A*
     A N10                                10'FILE NAME : DLFRARL3'
     A                                      SPACEA(2)
     A  10                                10'FILE NAME : DLIRSRL3'
     A                                      SPACEA(2)
     A*
     A                                    10'NUMBER OF RECORDS BEFORE-
     A                                       UPDATE:'
     A            RRNREC         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF INSERTIONS:'
     A            RRNINS         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF DELETIONS:'
     A            RRNDEL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF AMENDMENTS:'
     A            RRNAMD         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF RECORDS AFTER -
     A                                      UPDATE:'
     A            RRNCAL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'ACTUAL NUMBER OF RECORDS-
     A                                       ON THE FILE:'
     A            RRNORE         9  0     51EDTCDE(3)
     A  65                                70'***  DIFFERENCE  ***'
     A                                      SPACEA(2)
     A*
     A N62                                48'*** NO DETAILS TO REPORT+
     A                                       ***'
     A N62                                  SPACEB(3)
     A*
     A          R DL1933A2                  TEXT('DATA BASE ERROR FORMAT')
     A                                      SKIPB(20)
     A*
     A                                    48'***   PRINT TERMINATED   ***'
     A                                      SPACEA(1)
     A*
     A                                    45'DATABASE ERROR'
     A            DBASE     R             60REFFLD(DBASE)
     A                                    65'FILE'
     A            DBFILE    R             70REFFLD(DBFILE)
     A                                      SPACEA(1)
     A                                    45'KEY'
     A            DBKEY     R             49REFFLD(DBKEY)
