      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL FRA/IRS Rates Audit Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module
     A*                                                               *
     A*  PRTF/DL1933P1 -  FRA/IRS RATES AUDIT REPORT.                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01328             DATE 08MAR91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01328 - FRA/IRS Revaluation - New Print File.               *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A          R DL1933R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                     7'REFERENCE DL1933P1'
     A*
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A*
     A                                   100'TIME'
     A                                      SPACEB(1)
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A*
     A                                      SPACEA(2)
     A*
     A N10                                51'FRA RATES AUDIT REPORT'
     A                                      SPACEA(1)
     A  10                                51'IRS RATES AUDIT REPORT'
     A                                      SPACEA(1)
     A*
     A                                    51'----------------------'
     A                                      SPACEA(2)
     A*
     A          R DL1933R1                  TEXT('DTL HEADER FORMAT')
     A                                      SKIPB(9)
     A                                      SPACEA(2)
     A                                     1'USER ID/'
     A                                    10'CHANGE TYPE/'
     A                                    42'CURRENCY'
     A                                    54'BASE RATE'
     A                                    68'PERIOD'
     A                                    75'FROM'
     A                                    80'SPOT'
     A N10                                89'VALUE'
     A  10                                89'MATURITY'
     A                                   105'RATE'
     A                                      SPACEA(1)
     A*
     A                                     1'PROGRAM'
     A                                    17'TIME'
     A                                    25'WSID'
     A                                    44'CODE'
     A                                    56'CODE'
     A                                    68'YEARS'
     A                                    74'MONTHS'
     A                                    81'DAYS'
     A                                    89'DATE'
     A*
     A          R DL1933R2                  TEXT('DETAIL OUTPUT FMT')
     A                                      SPACEA(3)
     A*
     A            RRUSER    R              1REFFLD(JOUSER)
     A            RRTYPE         3        14TEXT('CHANGE TYPE')
     A                                    34'BEFORE'
     A            RRCCYB    R             45REFFLD(CCY)
     A            RRBRTB         2        57TEXT('BASE RATE CODE')
     A            RRPDYB         2        68TEXT('PERIOD YEARS')
     A            RRPDMB         2        74TEXT('PERIOD MONTHS')
     A            RRPDDB         2        80TEXT('PERIOD DAYS')
     A            RRVDMB    R             89REFFLD(VDMY)
     A            RRRATB        12       105TEXT('FRA/IRS RATE')
     A                                      SPACEA(1)
     A*
     A            RRPGM     R              1REFFLD(JOPGM)
     A            RRTIME    R             12REFFLD(JOTIME)
     A                                      EDTWRD('  :  : 0')
     A            RRJOB     R             22REFFLD(JOJOB)
     A                                    34'AFTER '
     A            RRCCYA    R             45REFFLD(CCY)
     A            RRBRTA         2        57TEXT('BASE RATE CODE')
     A            RRPDYA         2        68TEXT('PERIOD YEARS')
     A            RRPDMA         2        74TEXT('PERIOD MONTHS')
     A            RRPDDA         2        80TEXT('PERIOD DAYS')
     A            RRVDMA    R             89REFFLD(VDMY)
     A            RRRATA        12       105TEXT('FRA/IRS RATE')
     A*
     A  63
     AO 64                                  SPACEA(1)
     A  64                                12'*****  THIS ACTION IS NOT '
     A  64                                38'REFLECTED IN THE DATABASE DUE '
     A  64                                68'TO RECOVERY PROCESSING  *****'
     A  63                                  SPACEA(1)
     A  63                                 1'*********'
     A*
     A          R DL1933R3                  TEXT('END OF REPORT FMT')
     A                                    52'*** END OF REPORT ***'
