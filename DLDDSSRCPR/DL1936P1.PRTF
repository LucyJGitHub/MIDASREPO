      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL IR Swap Rates List')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS MODULE                                       *
     A*                                                               *
     A*  PRTF/DL1936P1 - SWAP RATES LIST.                             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CIR001             Date 13Jun95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CIR001 - Interest Rate Calendars                             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*    04  -  OUTPUT INT.CAL.MTH, FREQUENCY CODE & NAMES          *
     A*                                                               *
     A*****************************************************************
     A*
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  REPORT HEADER FORMAT
     A*
     A          R DL1936R0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL1936P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    60'SWAP RATES LIST'
     A                                      SPACEA(1)
     A                                    60'---------------'
     A                                      SPACEA(3)
     A*
     A                                     5'CURRENCY'
     A            FRCCY          3        15
     A            A6CYNM        14        19
     A                                      SPACEA(2)
     A*
     A                                     1'INTEREST CALCULATION'
     A                                    26'FREQUENCY'
     A                                    41'PERIOD FROM SPOT'
     A                                    65'MATURITY'
     A                                    82'SWAP'
     A                                   102'DISCOUNT'
     A                                   121'CUMMULATIVE'
     A                                       SPACEA(1)
     A                                     1'       METHOD        '
     A                                    26'CODE'
     A                                    41'YEARS MONTHS DAYS'
     A                                    65'DATE'
     A                                    82'RATE'
     A                                   102'FACTOR'
     A                                   121'DISC. FACT'
     A                                      SPACEA(1)
     A*
     A**  FORMAT TO OUTPUT DETAIL LINES.
     A*
     A          R DL1936R1                  TEXT('DETAIL OUTPUT FMT')
     A*
     A** IF CHANGE OF INTEREST CALCULATION METHOD
     A*
     A  04        IRINCL         1         1SPACEB(1)
     A  04        RINCN         22         3
     A  04        IRFREQ         1        26
     A  04        RFRQN         12        28
     A*
     A            RPDYR          2        41
     A            RPDMN          2        47
     A            RPDDY          2        54
     A*
     A            IRMDMY         7        64
     A            SWRATE        13        79
     A            DSCFCT        13        99
     A            CUMDSF        13       119
     A                                      SPACEA(1)
     A**  END OF REPORT FORMAT
     A          R DL1936R2                  TEXT('END OF REPORT FORMAT')
     A*
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
     A*
