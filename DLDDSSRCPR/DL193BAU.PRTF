      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL IRS Rates Audit Control Report')
      *****************************************************************
      *                                                               *
      *  Midas - IRS Module.                                          *
      *                                                               *
      *  DL1933BAU - IRS RATES AUDIT CONTROL REPORT                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG8891 *CREATE    Date 12Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG8891 - M+ ref DL1956 & DL1950 Single IRS & FRA            *
      *            revaluation reports can not be found in            *
      *            intellistor or AS400 during COB                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R DL1933A0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
      *
     A                                      SKIPB(002)
     A                                     7
     A                                      'REFERENCE DL193BAU'
      *
     A            RRTITL        53        36
     A                                   100
     A                                      'DATE'
     A            RRRUNA    R            105
     A                                      REFFLD(RUNA)
     A                                   118
     A                                      'PAGE'
     A                                   123
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   100
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   105
     A                                      TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(002)
      *
     A                                    48
     A                                      'IRS RATES AUDIT REPORT TOTALS'
     A                                      SPACEA(001)
      *
     A                                    48
     A                                      '-----------------------------'
     A                                      SPACEA(003)
      *
     A          R DL1933A1
     A                                      TEXT('DTL HEADER FORMAT')
     A                                      SPACEB(002)
      *
     A                                    10
     A                                      'FILE NAME : DLSWPRL4'
     A                                      SPACEA(002)
      *
     A                                    10
     A                                      'NUMBER OF RECORDS BEFORE UPDATE:'
     A            RRNREC         9  0     51
     A                                      EDTCDE(3)
     A                                      SPACEA(002)
     A                                    10
     A                                      'NUMBER OF INSERTIONS:'
     A            RRNINS         9  0     51
     A                                      EDTCDE(3)
     A                                      SPACEA(002)
     A                                    10
     A                                      'NUMBER OF DELETIONS:'
     A            RRNDEL         9  0     51
     A                                      EDTCDE(3)
     A                                      SPACEA(002)
     A                                    10
     A                                      'NUMBER OF AMENDMENTS:'
     A            RRNAMD         9  0     51
     A                                      EDTCDE(3)
     A                                      SPACEA(002)
     A                                    10
     A                                      'NUMBER OF RECORDS AFTER UPDATE:'
     A            RRNCAL         9  0     51
     A                                      EDTCDE(3)
     A                                      SPACEA(002)
     A                                    10
     A                                      'ACTUAL NUMBER OF RECORDS ON THE FI-
     A                                      LE:'
     A            RRNORE         9  0     51
     A                                      EDTCDE(3)
     A  65                                70
     A                                      '***  DIFFERENCE  ***'
     A                                      SPACEA(002)
      *
     A N62                                48
     A                                      '*** NO DETAILS TO REPORT***'
     A N62                                  SPACEB(003)
      *
     A          R DL1933A2
     A                                      TEXT('DATA BASE ERROR FORMAT')
     A                                      SKIPB(020)
      *
     A                                    48
     A                                      '***   PRINT TERMINATED   ***'
     A                                      SPACEA(001)
      *
     A                                    45
     A                                      'DATABASE ERROR'
     A            DBASE     R             60
     A                                      REFFLD(DBASE)
     A                                    65
     A                                      'FILE'
     A            DBFILE    R             70
     A                                      REFFLD(DBFILE)
     A                                      SPACEA(001)
     A                                    45
     A                                      'KEY'
     A            DBKEY     R             49
     A                                      REFFLD(DBKEY)
