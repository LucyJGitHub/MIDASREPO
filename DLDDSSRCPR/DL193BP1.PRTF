      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL IRS Rates Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - IRS Module                                           *
      *                                                               *
      *  PRTF/DL193BP1 -  FRA/IRS RATES AUDIT REPORT.                 *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG8891  *CREATE   Date 12Oct05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG8891 - M+ ref DL1956 & DL1950 Single IRS & FRA            *
      *            revaluation reports can not be found in            *
      *            intellistor or AS400 during COB                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R DL1933R0
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      SKIPB(003)
      *
     A                                     7
     A                                      'REFERENCE DL193BP1'
      *
     A            RRTITL    R             36
     A                                      REFFLD(TITL)
     A                                   100
     A                                      'DATE'
     A            RRRUNA    R            105
     A                                      REFFLD(RUNA)
     A                                   118
     A                                      'PAGE'
     A                                   123
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   100
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   105
     A                                      TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A                                      SPACEA(002)
      *
     A                                    51
     A                                      'IRS RATES AUDIT REPORT'
     A                                      SPACEA(001)
      *
     A                                    51
     A                                      '----------------------'
     A                                      SPACEA(002)
      *
     A          R DL1933R1
     A                                      TEXT('DTL HEADER FORMAT')
     A                                      SKIPB(001)
     A                                      SPACEA(002)
     A                                      SPACEB(008)
     A                                     1
     A                                      'USER ID/'
     A                                    10
     A                                      'CHANGE TYPE/'
     A                                    42
     A                                      'CURRENCY'
     A                                    52
     A                                      'INTEREST'
     A                                    64
     A                                      'FREQ'
     A                                    70
     A                                      'PERIOD'
     A                                    77
     A                                      'FROM'
     A                                    83
     A                                      'SPOT'
     A                                    90
     A                                      'MATURITY'
     A                                   102
     A                                      'RATE'
      *
     A                                     1
     A                                      'PROGRAM'
     A                                      SPACEB(001)
     A                                    17
     A                                      'TIME'
     A                                    25
     A                                      'WSID'
     A                                    44
     A                                      'CODE'
     A                                    51
     A                                      'CALC METHOD'
     A                                    64
     A                                      'CODE'
     A                                    70
     A                                      'YEARS'
     A                                    76
     A                                      'MONTHS'
     A                                    83
     A                                      'DAYS'
     A                                    90
     A                                      'DATE'
      *
     A          R DL1933R2
     A                                      TEXT('DETAIL OUTPUT FMT')
     A                                      SPACEA(003)
      *
     A            RRUSER    R              1
     A                                      REFFLD(JOUSER)
     A            RRTYPE         3        14
     A                                      TEXT('CHANGE TYPE')
     A                                    34
     A                                      'BEFORE'
     A            RRCCYB    R             45
     A                                      REFFLD(CCY)
     A            RRINCB         1        57
     A                                      TEXT('INT CALC METHD')
     A            RRFRQB         1        65
     A                                      TEXT('FREQUENCY')
     A            RRPDYB         2        71
     A                                      TEXT('PERIOD YEARS')
     A            RRPDMB         2        77
     A                                      TEXT('PERIOD MONTHS')
     A            RRPDDB         2        83
     A                                      TEXT('PERIOD DAYS')
     A            RRVDMB    R             90
     A                                      REFFLD(VDMY)
     A            RRRATB        12       100
     A                                      TEXT('FRA/IRS RATE')
      *
     A            RRPGM     R              1
     A                                      REFFLD(JOPGM)
     A                                      SPACEB(001)
     A            RRTIME    R             12
     A                                      REFFLD(JOTIME)
     A                                      EDTWRD('  :  : 0')
     A            RRJOB     R             22
     A                                      REFFLD(JOJOB)
     A                                    34
     A                                      'AFTER '
     A            RRCCYA    R             45
     A                                      REFFLD(CCY)
     A            RRINCA         1        57
     A                                      TEXT('INT CALC METHD')
     A            RRFRQA         1        65
     A                                      TEXT('FREQUENCY')
     A            RRPDYA         2        71
     A                                      TEXT('PERIOD YEARS')
     A            RRPDMA         2        77
     A                                      TEXT('PERIOD MONTHS')
     A            RRPDDA         2        83
     A                                      TEXT('PERIOD DAYS')
     A            RRVDMA    R             90
     A                                      REFFLD(VDMY)
     A            RRRATA        12       100
     A                                      TEXT('FRA/IRS RATE')
     A  64                                12
     A                                      '*****  THIS ACTION IS NOT '
     A                                      SPACEB(001)
     A  64                                38
     A                                      'REFLECTED IN THE DATABASE DUE '
     A  64                                68
     A                                      'TO RECOVERY PROCESSING  *****'
     A  63                                 1
     A                                      '*********'
     A                                      SPACEB(001)
      *
     A          R DL1933R3
     A                                      TEXT('END OF REPORT FMT')
     A                                    52
     A                                      '*** END OF REPORT ***'
