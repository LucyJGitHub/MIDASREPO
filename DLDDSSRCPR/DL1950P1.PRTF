      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL IR FRA Revaluation Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module
     A*                                                               *
     A*  DL1950P1 - FRA Revaluation Report                            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS004             Date 26Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 192765             DATE 01May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 E28792             DATE 23SEP91               *
     A*                 S01328             DATE 04FEB91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CAS004 - Hedge Accounting Phase A                            *
     A*  192765 - Remove references not over MFRFL                    *
     A*  E28792 - When Multibranching is OFF then 'BRANCH' should not *
     A*           appear on the report.                               *
     A*  S01328 - FRA/IRS Revaluation - New Print File.               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*    11  - Multi-Branching = Yes                                *
     A*    15  - Print Warning Message                                *
     A*    16  - Print Book Total                                     *
     A*    17  - Print Currency Total                                 *
     A*    18  - Print Branch Total                                   *
     A*    20  - No MM Interest Rates for Currency                    *
     A*    21  - No FRA Rate for Currency                             *
     A**Start of change CAS004
      *    22  - CAS004 is installed                                  *
      *    23  - For Run option 1 or 3                                *
      *    22  - For Run option 2 or 4                                *
     A**End of change CAS004
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      ***  Main Header:
      ***  ------------
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL1950P1'
      *                                               /Start 192765/
     A************BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A            BJURPT    R             37REFFLD(URPT)
      *                                               /End   192765/
     A                                   101'DATE'
      *                                               /Start 192765/
     A************BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A            BJMRDT    R            106REFFLD(MRDT)
      *                                               /End   192765/
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A*/COPY WNCPYSRC,DL1950P101
     A**Start of change CAS004
     A**********                          52'FRA REVALUATION REPORT'                          CAS004
     A N22
     AO 22N23N24                          52'FRA REVALUATION REPORT'
     A  22 24                             40'FRA REVALUATION REPORT - +
     A                                      UP TO'
     A  22 23                             35'FRA REVALUATION REPORT - +
     A                                      UP TO BUT EXCLUDING'
     A  22 23     RDATHE         7        80
     A  22 24     RDATH          7        71
     A                                   132' '
     A**End of change CAS004
     A                                      SPACEA(1)
      *
     A**Start of change CAS004
     A**********                          52'----------------------'                          CAS004
     A N22
     AO 22N23N24                          52'----------------------'
     A  22 23                             35'-------------------------+
     A                                      ---------------------------'
     A  22 24                             40'-------------------------+
     A                                      -------------'
     A                                   132' '
     A**End of change CAS004
     A                                      SPACEA(2)
     A*/COPY WNCPYSRC,DL1950P102
      *
      ***  Warning Message:
      ***  ----------------
      *
     A  15                                21'***  WARNING : RATES MAY -
     A                                      HAVE CHANGED SINCE DISCOUNT -
     A                                      FACTORS LAST RECALCULATED  ***'
     A                                      SPACEA(2)
      *
      ***  Branch/Currency Header:
      ***  -----------------------
      *
     A  11                                 4'BRANCH'
      *                                               /Start 192765/
     A**11********A8BRCD    R             11REFFLD(A8BRCD SDBRCHPD)
     A**11********A8BRNM    R             15REFFLD(A8BRNM SDBRCHPD)
     A  11        A8BRCD    R             11REFFLD(BRCA)
     A  11        A8BRNM    R             15REFFLD(BRNM)
      *                                               /End   192765/
      *
     A                                   101'CURRENCY'
      *                                               /Start 192765/
     A************A6CYCD    R            110REFFLD(A6CYCD SDCURRPD)
     A************A6CYNM    R            114REFFLD(A6CYNM SDCURRPD)
     A            A6CYCD         3       110
     A            A6CYNM        14       114
      *                                               /End   192765/
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     5'VALUE   CUSTOMER   DEAL'
     A                                    29'BOUGHT CONTRACT'
     A                                    52'NOMINAL VALUE'
     A                                    70'REVALUATION'
     A                                    90'PROFIT/LOSS'
     A                                   112'PRESENT VALUE'
     A                                      SPACEA(1)
      *
     A                                     5'DATE     NUMBER'
     A                                    29'/SOLD    RATE'
     A                                    73'RATE'
     A                                   113'PROFIT/LOSS'
     A                                      SPACEA(2)
      *
      *-----------------------*
      ***  Book Header Format *
      *-----------------------*
      *
     A          R BKHEAD
      *
     A                                     4'BOOK'
      *                                               /Start 192765/
     A************BDBKCD    R              9REFFLD(BDBKCD SDBOOKPD)
     A************BDBKD     R             12REFFLD(BDBKD SDBOOKPD)
     A            BDBKCD         2         9
     A            BDBKD         30        12
      *                                               /End   192765/
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RVDAT          7         4
     A            RCNUM          6        14
     A            RDLNO          6        23
     A            RBYSL          1        31
     A            REINR         12        34
     A            RPAMT         17        49
     A N21        RFRART        13        69
     A  21                                73'*.*'
     A N21        RPL           21        84
     A N20N21     RPLNPV        21       108
     A                                      SPACEA(2)
     A  20                               108'* MM Rates Missing *'
     A                                      SPACEA(2)
     A  21                               108'* FRA Rate Missing *'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Totals Record Format *
      *-------------------------*
      *
     A          R TOTALS
      *
      ***  Book Total:
      ***  -----------
      *
     A  16                               108'---------------------'
     A  16                                  SPACEA(1)
     A  16                                91'TOTAL FOR BOOK'
     A  16        RTOTBK        21       108
     A  16                                  SPACEA(1)
     A  16                               108'====================='
     A  16                                  SPACEA(1)
      *
      ***  Currency Total:
      ***  ---------------
      *
     A  17                                87'TOTAL FOR CURRENCY'
     A  17        RTOTCY        21       108
     A  17                                  SPACEA(1)
     A  17                               108'====================='
     A  17                                  SPACEA(1)
      *
      ***  Branch Total:
      ***  -------------
      *
      *                                               /START E28792/
     A**18********************************70'TOTAL FOR BRANCH (IN BASE +
     A************************************  CURRENCY)'
     A  18 11                             70'TOTAL FOR BRANCH (IN BASE +
     A                                      CURRENCY)'
     A  18N11                             81'TOTAL (IN BASE CURRENCY)'
      *                                               /END   E28792/
     A  18        RTOTBR        21       108
     A  18                                  SPACEA(1)
     A  18                               108'====================='
     A  18                                  SPACEA(2)
      *
      ***  End of Report:
      ***  --------------
      *
      *                                               /START E28792/
     A**18********************************46'***  END OF REPORT FOR +
     A**************************************BRANCH  ***'
     A  18 11                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A  18N11                             51'***  END OF REPORT  ***'
      *                                               /END   E28792/
