      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL IRS Revaluation Report AUDIT')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module                                       *
     A*                                                               *
     A*  DL1956AU - IRS Revaluation Report AUDIT                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CIR001             Date 13Jun95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CIR001 - Interest Rate Calendars                             *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *------------------*
      ***  Report Header *
      *------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL1956AU'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    48'IRS REVALUATION REPORT - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    48'------------------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    58'CALCULATED'
     A                                      SPACEA(1)
     A                                    58'----------'
     A                                      SPACEA(3)
      *
     A                                     6'FILE - IRS DEALS FILE +
     A                                      - PF/DEALSDG'
      *
     A                                     6'----------------------+
     A                                      -------------'
     A                                      SPACEB(1)
      *
     A                                    10'NUMBER OF RECORDS INPUT:'
     A                                      SPACEB(3)
     A            RNORE          5  0     59EDTCDE(3)
     A                                      SPACEA(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    47'DATABASE ERROR'
     A                                      SPACEB(2)
     A            DBASE          3  0     62
     A                                    66'IN PROGRAM'
     A            DBPGM         10        77
     A                                      SPACEA(2)
     A                                    47'FILE'
     A            DBFILE         8        52
     A                                    61'KEY'
     A            DBKEY         29        65
