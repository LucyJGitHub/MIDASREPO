      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL IRS Revaluation Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module                                       *
     A*                                                               *
     A*  DL1956P2 - REVALUATION REPORT                                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS004             Date 26Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 137276             DATE 30Apr99               *
      *                 100963             DATE 02APR96               *
     A*                 100284             DATE 05MAR96               *
     A*                 CIR001             DATE 13JUN95               *
     A*                                                               *
     A*---------------------------------------------------------------*
      *  CAS004 - Hedge Accounting Phase A                            *
     A*  137276 - There is no END OF REPORT'.                         *
     A*  100963 - On change of branch and currency, the currency name *
     A*           of the previous currency was reported.              *
     A*  100284 - Add accrued interest adjustment to totals.          *
     A*  CIR001 - Interest Rate Calendars                             *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R PAGHD2                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE DL1956P2'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A**Start of change CAS004
     A**********                          43'IRS REVALUATION REPORT - '                       CAS004
     A*N10******                          68'SINGLE CURRENCY'                                 CAS004
     A N10 40N41
     AAN42
     AON10N40                             43'IRS REVALUATION REPORT - +
     A                                      SINGLE CURRENCY'
     A N10 40 41                          32'IRS REVALUATION REPORT - +
     A                                      SINGLE CURRENCY - PRELIMINARY +
     A                                      REPORT FOR'
     A N10 40 42                          32'IRS REVALUATION REPORT - +
     A                                      SINGLE CURRENCY - UP TO BUT +
                                            EXCLUDING'
     A  10 40N41
     AAN42
     AO 10N40                             43'IRS REVALUATION REPORT - +
     A                                      CROSS CURRENCY'
     A  10 40 41                          32'IRS REVALUATION REPORT - +
     A                                      CROSS CURRENCY - PRELIMINARY +
     A                                      REPORT FOR'
     A  10 40 42                          32'IRS REVALUATION REPORT - +
     A                                      CROSS CURRENCY - UP TO BUT +
                                            EXCLUDING'
     A  40 42     REVCD          7        96
     A  40 41     REVCDE         7        98
     A                                   132' '
     A**End of change CAS004
     A                                      SPACEA(1)
     A**Start of change CAS004
     A**10******                          68'CROSS CURRENCY'                                  CAS004
     A**********                            SPACEA(1)                                         CAS004
     A**End of change CAS004
      *
     A**Start of change CAS004
     A**********                          43'-------------------------'                       CAS004
     A*N10******                          68'---------------'                                 CAS004
     A N10 40N41
     AAN42
     AON10N40                             43'-------------------------+
     A                                      ---------------'
     A N10 40 41                          32'-------------------------+
     A                                      ----------------------------+
     A                                      --------------------'
     A N10 40 42                          32'-------------------------+
     A                                      ------------------------------+
     A                                      ----------------'
     A  10 40N41
     AAN42
     AO 10N40                             43'-------------------------+
     A                                      --------------'
     A  10 40 41                          32'-------------------------+
     A                                      ---------------------------+
     A                                      ---------------------'
     A  10 40 42                          32'-------------------------+
     A                                      ------------------------------+
     A                                      ----------------'
     A                                   132' '
     A**End of start CAS004
     A                                      SPACEA(2)
     A**Start of change CAS004
     A**10******                          68'--------------'                                  CAS004
     A**********                            SPACEA(2)                                         CAS004
     A**End of change CAS004
      *
     A                                     4'BRANCH'
      * Start Changes for 100284.
     A**********  CFBRCA    R             11REFFLD(A8BRCD SDBRCHPD)
     A**********  A8BRNM    R             15REFFLD(A8BRNM SDBRCHPD)
     A            RBRCA     R             11REFFLD(A8BRCD SDBRCHPD)
     A            RBRNM     R             15REFFLD(A8BRNM SDBRCHPD)
      * End Changes for 100284.
      *
     A                                   101'CURRENCY'
      * Start Changes for 100284.
     A**********  CFRCCY    R            110REFFLD(A6CYCD SDCURRPD)
     A**********  A6CYNM    R            114REFFLD(A6CYNM SDCURRPD)
     A            RRCCY     R            110REFFLD(A6CYCD SDCURRPD)
     A            RCYNM     R            114REFFLD(A6CYNM SDCURRPD)
      * End Changes for 100284.
      *
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     3'DEAL'
     A                                    10'OUR/'
     A                                    17'LEG'
     A                                    25'FROM'
     A                                    37'TO'
     A                                    42' VALUE '
     A                                    60'PRINCIPAL'
     A                                    72'DSC/'
     A                                    79'RATE'
     A                                    87'ACT/'
     A                                   100'AMOUNT'
     A                                   117'NPV AMOUNT'
     A                                      SPACEA(1)
     A                                    10'THEIR'
     A                                    42' DATE  '
     A                                    72'YLD'
     A                                    85'FWD/SPR'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DDETL2
      *
     A            RDLNO          6         2
     A            ROTNAR         5        10
     A            RFFNAR         5        16
     A            RSLIP          7        23
     A                                    31'-'
     A            RNIPD          7        33
     A            RPYDT          7        42
     A            RCCY           3        50
     A            RPRIN         19        53
     A            RYLD           1        73
     A            RRATE         12        75
     A            RAFNAR         1        88
     A            RAMNT         19        90
     A            RNPV          19       110
     A                                      SPACEA(1)
      *
      *-----------------------------------*
      ***  Our/Their Totals Record Format *
      *-----------------------------------*
      *
     A          R OTTOT2
      *
     A            ROTTOT        19       110HIGHLIGHT
     A                                      SPACEA(1)
      *
      *-----------------------------------*
      ***  Deal Totals Record Format *
      *-----------------------------------*
      *
     A          R DTOT2
      *
     A                                   110'==================='
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,DL1956P201
      * Start Changes for 100284.
     A**********                           2'--------------------+
     A**********                            --------------------+
     A**********                            --------------------+
     A**********                            --------------------+
     A**********                            -----------------'
      * End Changes for 100284.
     A            RDLNO          6       100HIGHLIGHT
     A            RDTOT         19       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'==================='
     A                                      SPACEA(1)
      * Start Changes for 100284.
     A                                    55'Adjustment for accrued +
     A                                      interest posted this +
     A                                      period'
     A            RDACR         19       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'==================='
     A                                      SPACEA(1)
     A                                     2'--------------------+
     A                                      --------------------+
     A                                      --------------------+
     A                                      --------------------+
     A                                      -------- Adjusted'
     A            RDLNO1         6       100HIGHLIGHT
     A            RDADJ         19       110HIGHLIGHT
     A                                      SPACEA(1)
     A                                   110'==================='
     A                                      SPACEA(1)
      * End Changes for 100284.
     A*/COPY WNCPYSRC,DL1956P202
      * Start 137276
     A          R ENDBRC2                   TEXT('END OF REPORT +
     A                                      FOR BRANCH')
     A                                    43'** END OF REPORT FOR +
     A                                      BRANCH **'
     A*
     A          R ENDRPT2                   TEXT('END OF REPORT')
     A                                    43'** END OF REPORT **'
      * End 137276
