      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL MM Rates and Discount Factors Audit')
     A****************************************************************
     A*                                                              *
     A*  Midas - FRA/IRS Module
     A*                                                              *
     A*   PRTF/DL1961AU - MM Rates and Discount Factors Audit Report *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. S01328               DATE  12FEB91          *
     A*                                                              *
     A*--------------------------------------------------------------*
     A*                                                              *
     A*   S01328 - FRA/IRS Revaluation - New Report File             *
     A*                                                              *
     A****************************************************************
     A*  Index of Indicators
     A*
     A*  15  -  Indicates P1 opened - ie. details to report exist
     A*  85  -  Database error - ie. *INU7 on in DL1961
     A*
     A*****************************************************************
     A                                      INDARA
     A*
     A**  Main Heading Format
     A          R DL1961R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A*
     A                                     7'REFERENCE DL1961AU'
     A            BJURPT        53A       36
     A                                   100'DATE'
     A            BJMRDT         7A      105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    41'MM RATES AND '
     A                                    54'DISCOUNT FACTORS '
     A                                    71'AUDIT REPORT'
     A                                      SPACEA(1)
     A*
     A                                    41'-------------'
     A                                    54'-----------------'
     A                                    71'------------'
     A*
     A                                      SPACEA(3)
     A*
     A                                    65'CALCULATED'
     A                                      SPACEA(1)
     A                                    65'----------'
     A                                      SPACEA(2)
     A*
     A                                     6'FILES - MM INTEREST RATES +
     A                                      TABLE   - LF/DLINTRL1'
     A                                      SPACEA(1)
     A                                     6'      - MM DISCOUNT FACTORS +
     A                                      TABLE - LF/DLMMDFL1'
     A                                      SPACEA(1)
     A                                     6'----------------------------+
     A                                      -------------------'
     A                                      SPACEA(3)
     A                                    10'NUMBER OF RECORDS REPORTED:'
     A            RRREC          5  0     68
     A                                      SPACEA(4)
     A*
     A** No Details to Report format
     A*
     A N15N85                             49 '*** NO DETAILS TO REPORT ***'
     A N15N85                               SPACEA(4)
     A*
     A**  Database error format
     A*
     A  85                                49 '***   PRINT TERMINATED   ***'
     A  85                                  SPACEA(1)
     A  85                                46 'DATABASE ERROR'
     A  85        DBASE          3  0     61
     A  85                                66 'FILE'
     A  85        DBFILE         8        71
     A  85                                  SPACEA(1)
     A  85                                46 'KEY'
     A  85        DBKEY         29        50
