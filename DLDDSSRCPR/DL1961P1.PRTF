      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL MM Rates and Discount Factors Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FRA/IRS Module
     A*                                                               *
     A*  DL1961P1 - MM Rates and Discount Factors                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01328             DATE 12FEB91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01328 - FRA/IRS Revaluation - New Print File.               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*    10  - Indicate Bid (1) or Offer (0) to Dl1961R2            *
     A*                                                               *
     A*****************************************************************
      *
     A                                      INDARA
     A*
     A**  Main Heading Format
     A          R DL1961R1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A*
     A                                     7'REFERENCE DL1961P1'
     A            BJURPT        53A       36
     A                                   100'DATE'
     A            BJMRDT         7A      105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                     5'CURRENCY'
     A            RRCYCD         3A       14
     A            RRCYNM        14A       18
     A*
     A                                    48'MM RATES AND '
     A                                    61'DISCOUNT FACTORS'
     A                                      SPACEA(1)
     A*
     A                                    48'-------------'
     A                                    61'----------------'
     A                                      SPACEA(2)
     A*
     A  35                                21'***  WARNING : RATES'
     A  35                                +1'MAY HAVE CHANGED SINCE'
     A  35                                +1'DISCOUNT FACTORS LAST'
     A  35                                +1'RECALCULATED  ***'
     A  35                                  SPACEA(2)
     A*
     A**  Side Heading Format - BID if *IN10 on else OFFER
     A          R DL1961R2
     A*
     A                                      SPACEB(1)
     A                                     1'YEAR'
     A                                     7'PERIOD'
     A  10                                15'BID RATE'
     A  10                                27'BID'
     A  10                                44'BID DISCOUNT +
     A                                      FACTORS CALCULATIONS'
     A N10                                15'OFFER RATE'
     A N10                                27'OFFER'
     A N10                                44'OFFER DISCOUNT +
     A                                      FACTORS CALCULATIONS'
     A                                   102'INVERSE'
     A                                   116'CUMULATIVE'
     A                                      SPACEA(1)
     A*
     A                                     7'DATE'
     A                                    27'DISC FACTOR'
     A                                   102'DISC FACTOR'
     A                                   116'INV DISC FACT'
     A                                       SPACEA(2)
     A*
     A**  Detail Format
     A          R DL1961R3
     A*
     A            RRYEAR         2A        2TEXT('Period Year')
     A            RRDATE         7A        6TEXT('Period Date')
     A            RRR           12A       14TEXT('Rate')
     A            RRDF          15A       27TEXT('Discount Factor')
     A            RRFC          57A       44TEXT('Discount Factors Calculation')
     A            RRIDF         13A      102TEXT('Inv Disc Fact')
     A            RRCIF         15A      116TEXT('Cumul Inv Disc Fact')
     A                                      SPACEA(2)
     A*
     A**  End of Report Format
     A          R DL1961R4
     A*
     A                                      SPACEB(1)
     A                                    52'*** END OF REPORT ***'
     A*
