      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Maturing fiduciary deposits by a/c off.')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL2101P1 - Maturing Fiduciary Deposits by Account       *
      *                  Officer                                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Header/Detail Record Format
      *
     A          R DETAIL                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL2101P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'MATURING FIDUCIARY DEPOS+
     A                                      ITS BY ACCOUNT OFFICER'
     A                                      SPACEA(1)
     A                                    43'------------------------+
     A                                      ----------------------'
     A                                      SPACEA(2)
      *
     A                                     2'Branch'
     A            PRBRCD    R              9REFFLD(BRCA)
     A            PRBRNM    R             14REFFLD(BRNM)
      *
     A                                    85'A/C Officer'
     A            PRACOC    R             97REFFLD(AZACOC SDACOFPD)
     A            PRACON    R            101REFFLD(AZACON SDACOFPD)
     A                                      SPACEA(2)
      *
     A                                     2'Customer'
     A                                    12'Placed'
     A                                    21'Deal'
     A                                    28'Value'
     A                                    36'Maturity'
     A                                    50'Interest'
     A                                    70'Amount'
     A                                    77'Ccy'
     A                                    90'Interest'
     A                                   104'Commission'
     A                                   127'Total'
     A                                      SPACEA(1)
      *
     A                                     2'Number'
     A                                    12'With'
     A                                    21'Number'
     A                                    28'Date'
     A                                    36'Date'
     A                                    54'Rate'
     A                                      SPACEA(2)
     A            PRCUST         6A        2
     A            PRLNKD         7A       12
     A            PRDLNO         6  0     21
     A            PROTHD         7A       28
     A            PREDAT         7A       36
     A            PREXRT        14A       44
     A            PREAMT        17A       59
     A            PRECCY         3A       77
     A            PROTHA        17A       81
     A            PRCOAM        15A       99
     A            PRTOTA        17A      115
     A                                      SPACEA(5)
      *
     A                                    38'                                  -
     A                                                                         -
     A                                                  '
     A                                      UNDERLINE
     A                                      SPACEA(1)
     A
     A                                    38'I REINVESTMENT                    -
     A                                                                         -
     A                                                 I'
     A                                      SPACEA(1)
     A                                    38'                                  -
     A                                                                         -
     A                                                  '
     A                                      UNDERLINE
     A                                      SPACEA(1)
     A                                    38'I  CCY  I       AMOUNT          I -
     A                                        PERIOD   I    SPECIALS INSTRUCTIO-
     A                                      NS  I POR: I'
     A                                      SPACEA(1)
     A                                    38'                                  -
     A                                                                         -
     A                                                  '
     A                                      UNDERLINE
     A                                      SPACEA(1)
     A                                    38'I       I                       I -
     A                                                 I                       -
     A                                          I      I'
     A                                      SPACEA(1)
     A                                    38'I       I                       I -
     A                                                 I                       -
     A                                          I      I'
     A                                      SPACEA(1)
     A                                    38'I       I                       I -
     A                                                 I                       -
     A                                          I      I'
     A                                      SPACEA(1)
     A                                    38'I       I                       I -
     A                                                 I                       -
     A                                          I      I'
     A                                      SPACEA(1)
     A                                    38'I       I                       I -
     A                                                 I                       -
     A                                          I      I'
     A                                      SPACEA(1)
     A                                    38'I       I                       I -
     A                                                 I                       -
     A                                          I      I'
     A                                      SPACEA(1)
     A                                    38'                                  -
     A                                                                         -
     A                                                  '
     A                                      UNDERLINE
      *
      **  Report Trailer
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
