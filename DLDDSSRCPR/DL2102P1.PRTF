      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Deposits without trust agreem. or taxable')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL2102P1 - Deposits Without Trust Agreement or Taxable  *
      *                  - Audit                                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE DL2102P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'DEPOSITS WITHOUT TRUST AGREEMENT O-
     A                                      R TAXABLE'
     A                                      SPACEA(001)
      *
     A                                    45'-----------------------------------
     A                                      ---------'
     A                                      SPACEA(002)
     A  37                                 4'Branch'
     A  37        PRBRCD    R             11REFFLD(BRCA)
     A  37        PRBRNM    R             16REFFLD(BRNM)
      *
     A                                    74'A/C Officer'
     A            PRACOC    R             86REFFLD(AZACOC SDACOFPD)
     A            PRACON    R             90REFFLD(AZACON SDACOFPD)
     A                                      SPACEA(2)
      *
      ** Sub-Heading
      *
     A                                     4'Customer'
     A                                    25'Trust Agreement Status'
     A                                    +1'/Rectrictions Narrative'
     A                                    74'Deal'
     A                                    81'Value'
     A                                    89'Maturity'
     A                                   101'Interest'
     A                                   121'Amount'
     A                                   129'Ccy'
     A                                      SPACEA(001)
     A                                    74'Number'
     A                                    81'Date'
     A                                    89'Date'
     A                                   101'Rate'
     A                                      SPACEA(001)
      *
      ** Detail Record Format
      *
     A          R DETAIL
     A  65        PRCUST         6A        4
     A                                      SPACEB(001)
     A  65        PRCSSN        10A       +2
     A  65        PRTRAS         1A       35
     A                                      SPACEA(001)
     A  65 41     PRNAR1        60A       12
     A                                      SPACEA(001)
     A  65 42     PRNAR2        60A       12
     A                                      SPACEA(001)
     A  65 43     PRNAR3        60A       12
     A                                      SPACEA(001)
     A  65 44     PRNAR4        60A       12
     A                                      SPACEA(001)
     A  65N41N42
     A    N43N44                          12' '
     A                                      SPACEA(001)
     A            PRDLNO         6  0     74
     A            PRVDAT         7A       81
     A            PRMDAT         7A       89
     A            PRINTR        12A       98
     A            PRPCPL        17A      111
     A            PRCCY          3A      129
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A  37                                46
     A                                      '***  END OF REPORT FOR BRANCH  ***'
     A                                      SPACEB(003)
     A N37                                51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
