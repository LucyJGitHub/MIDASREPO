      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL FI Loans without trust agreem. or taxable')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL2103P1 - FI Loans Without Trust Agreement or Taxable  *
      *                                                               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL2103P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    45'LOANS WITHOUT TRUST AGREEMENT O-
     A                                      R TAXABLE'
     A                                      SPACEA(001)
      *
     A                                    45'--------------------------------
     A                                      ---------'
     A                                      SPACEA(002)
     A  37                                 4'Branch'
     A  37        PRBRCD    R             11REFFLD(BRCA)
     A  37        PRBRNM    R             16REFFLD(BRNM)
      *
     A                                    74'A/C Officer'
     A            PRACOC    R             86REFFLD(AZACOC SDACOFPD)
     A            PRACON    R             90REFFLD(AZACON SDACOFPD)
     A                                      SPACEA(2)
      *
      ** Sub-Heading
      *
     A                                     4'Deal'
     A                                      SPACEB(2)
     A                                    11'Value'
     A                                    19'Maturity'
     A                                    31'Interest'
     A                                    51'Amount'
     A                                    59'Ccy'
     A                                    65'Trust Agreement Status'
     A                                      SPACEA(1)
     A                                     4'Number'
     A                                    11'Date'
     A                                    19'Date'
     A                                    31'Rate'
     A                                    65'Rectrictions Narrative'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETAIL
     A  65                                 4'Customer'
     A                                      SPACEB(1)
     A  65        PRCUST         6A       13
     A  65        PRCSSN        10A       +2
     A            PRDLNO         6  0      4
     A                                      SPACEB(1)
     A            PRVDAT         7A       11
     A            PRMDAT         7A       19
     A            PRINTR        12A       28
     A            PRPCPL        17A       41
     A            PRCCY          3A       59
     A            PRTRAS         1A       65
     A N41N42N43
     A N44N67                             67' '
     A                                      SPACEA(1)
     A N41N42N43
     A N44 67                             67'ONE NARRATIVE MUST BE  ENTERED WIT-
     A                                      H THIS TRUST AGREEMENT'
     A                                      SPACEA(1)
     A  41        PRNAR1        60A       67
     A                                      SPACEA(1)
     A  42        PRNAR2        60A       67
     A                                      SPACEA(1)
     A  43        PRNAR3        60A       67
     A                                      SPACEA(1)
     A  44        PRNAR4        60A       67
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A  37                                46
     A                                      '***  END OF REPORT FOR BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
