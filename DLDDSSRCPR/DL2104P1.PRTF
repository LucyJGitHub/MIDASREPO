      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Mismatched fiduc. deposits & loans report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL2104P1 - Mismatched Fiduciary Deposits & Loans Report *
      *                  - Placing checking (P1)                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL2104P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    45'MISMATCHED FIDUCIATY DEP+
     A                                      OSITS & LOANS REPORT'
     A                                      SPACEA(1)
      *
     A                                    45'------------------------+
     A                                      --------------------'
     A                                      SPACEA(2)
      *
      ** Sub-Heading
      *
     A                                    11'DEAL'
     A                                    19'SUB'
     A                                    49'VALUE'
     A                                    57'MATURITY'
     A                                    69'INTEREST'
     A                                    80'INTEREST'
     A                                    92'INTEREST'
     A                                      SPACEA(1)
     A                                    11'NUMBER'
     A                                    19'TYPE'
     A                                    25'PRINCIPAL AMOUNT'
     A                                    43'CCY'
     A                                    49'DATE'
     A                                    57'DATE'
     A                                    73'RATE'
     A                                    79'CALC.BASIS'
     A                                    92'PAYM.DATE'
      *
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETPLA
     A            PDASH        130A        3
     A                                      SPACEA(1)
     A                                     3'PLACING'
     A            PPDLNO         6  0     11
     A            PPDLST         2A       20
     A            PPPCPL        17A       25
     A            PPCCY          3A       43
     A            PPVDAT         7A       49
     A            PPMDAT         7A       57
     A            PPINTR        12A       66
     A            PPICBS         1A       83
     A            PPNIDT         7A       92
     A N11N12N13
     AAN14                                99' '
     A                                      SPACEA(2)
     A  11                                99'* TOTAL PRINCIPAL DOES NOT MATCH *'
     A                                      SPACEA(2)
     A  12                                99'* ONLY ONE DT FOR ONE DP *'
     A                                      SPACEA(1)
     A  13                                99'* ONLY ONE LT FOR ONE LP *'
     A                                      SPACEA(1)
     A  14                                99'* PLACING WITHOUT TAKING *'
     A                                      SPACEA(2)
     A  14                                 3'TAKING'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETTAK
     A  19                                 3'TAKING'
     A            PTDLNO         6  0     11
     A            PTDLST         2A       20
     A            PTPCPL        17A       25
     A            PTCCY          3A       43
     A            PTVDAT         7A       49
     A            PTMDAT         7A       57
     A            PTINTR        12A       66
     A            PTICBS         1A       83
     A            PTNIDT         7A       92
     A N15N16N17
     AAN18                                99' '
     A                                      SPACEA(1)
     A  15                                99'* VALUE DATE DOES NOT MATCH *'
     A                                      SPACEA(1)
     A  16                                99'* MATURITY DATE DOES NOT MATCH *'
     A                                      SPACEA(1)
     A  17                                99'* INTEREST DETAILS DO NOT MATCH *'
     A                                      SPACEA(1)
     A  18                                99'* SUB TYPE DOES NOT MATCH *'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETDAS
     A            PDASH        130A        3
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
