      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Mismatched fiduc. deposits & loans report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL2104P3 - Mismatched Fiduciary Deposits & Loans Report *
      *                  - Deal amendement checking (P3)              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP3                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL2104P3'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    45'MISMATCHED FIDUCIATY DEP+
     A                                      OSITS & LOANS REPORT'
     A                                      SPACEA(1)
      *
     A                                    45'------------------------+
     A                                      --------------------'
     A                                      SPACEA(3)
      *
      ** Sub-Heading
      *
     A                                     3'DEAL'
     A                                    10'VALUE'
     A                                    17'AMENDMENT'
     A                                    28'AMENDMENT'
     A                                    42'SPREAD'
     A                                    74'ASSOCIATED'
     A                                      SPACEA(1)
     A                                     3'NUMBER'
     A                                    10'DATE'
     A                                    18'SEQ. NO'
     A                                    30'TYPE'
     A                                    43'/RATE'
     A                                    50'CCY'
     A                                    65'AMOUNT'
     A                                    75'DEAL NO'
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETP3
     A            PRDLNO         6  0      3
     A            PRVDAT         7A       10
     A            PRDASN         3A       20
     A            PRAMTP         2A       31
     A            PRRTSP        12A       37
     A            PRCCY          3A       50
     A            PRDAAM        17A       55
     A            PRLNKD         6  0     75
     A                                    83'* NO EQUIVALENT AMENDMENT FOR ASSO-
     A                                      CIATED DEAL *'
     A                                      SPACEA(3)
      *
      ** Report Trailer
      *
     A          R TRAILP3
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
