      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL Fiduciary charges/commissions types rept')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL2115AU - Fiduciary Charges/Commission Types Report    *
      *                  - Audit                                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Last Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Page Headings
      *
     A          R TITLE1AU                  SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE DL2115AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A            PAGE2        004S 0    125EDTCDE(Z) SPACEA(2)
     A                                    27'CHARGE AND COMMISSION CODES +
     A                                      LIST -('
      *
     A          R AUDEXC
     A                                    62'AUDIT - EXCEPTION '
     A                                    80TIME
      *
     A          R AUDIT
     A                                    62'AUDIT'
      *
     A          R TITLE2AU
     A                                    88')- RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'--------------------------'
     A                                      SKIPA(11)
     A                                    47'FILE CONTROL'
     A                                    78'CALCULATED' SKIPA(13)
     A                                    53'NUMBER'
     A                                    74'VALUE   NUMBER'
     A                                   103'VALUE' SKIPA(16)
     A                                     2'FILE CHARGE AND /     '
     A                                    42'INPUT/'
     A                                      SPACEA(1)
     A                                     2'COMMISSION CODES LIST '
     A                                    29'PF/DLCCGDPA  OUTPUT' SKIPA(19)
      *
      ** Control report for 'I' & 'A' mode (common)
      *
     A          R CONTCOM
     A                                      SKIPB(19)
     A                                     2'NO OF LIVE RECORDS BEFORE INPUT'
     A            FDNLRB    R             54REFFLD(NLRB)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
     A                                     2'INSERTIONS'
     A            FDNINS    R             55REFFLD(NINS)
     A                                      EDTCDE(3)
     A            LCTI           5  0     84EDTCDE(3)
     A                                      SPACEA(1)
     A                                     2'AMENDMENTS'
     A            FDNAMD    R             55REFFLD(NAMD)
     A                                      EDTCDE(3)
     A            LCTA           5  0     84EDTCDE(3)
     A                                      SPACEA(1)
     A                                     2'DELETIONS'
     A            FDNDEL    R             55REFFLD(NDEL)
     A                                      EDTCDE(3)
     A            LCTD           5  0     84EDTCDE(3)
     A                                      SPACEA(1)
      *
      ** Control type for 'I' section
      *
     A          R CONTI
     A                                     2'NO OF RECORDS CHANGED TODAY/LISTED'
     A            PNRLT          5  0     84EDTCDE(3)
     A                                      SPACEA(1)
     A                                     2'NO OF LIVE RECORDS ON FILE'
     A            FDNORE    R             54REFFLD(NORE)
     A                                      EDTCDE(3)
     A                                      SPACEA(1)
      *
      ** Control report for 'A' mode section
      *
     A          R CONTA
     A                                     2'NO OF LIVE RECORDS AFTER INPUT'
     A            FDNORE    R             54REFFLD(NORE)
     A                                      EDTCDE(3)
     A            PNLRR          5  0     84EDTCDE(3)
      *
      ** Difference in database and control totals
      *
     A          R DIFF
     A                                   112'*** DIFFERENCE *** '
      *
      ** No records
      *
     A          R NONEAU
     A                                      SKIPB(46)
     A                                    52'*** NO DETAILS TO REPORT ***'
      *
      ** Report database error
      *
     A          R ERROR                     SKIPB(32)
     A                                    26'**** DATABASE ERROR ****' SPACEA(3)
     A                                    12'***************************'
     A                                    39'***************************'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*  FILE NAME'
     A            DBFILE         8        28
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*  KEY'
     A            DBKEY         29        28
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*  PROGRAM NAME'
     A            DBPGM         10A       28
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*  AT'
     A            DBASE          3  0     28
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'*'
     A                                    65'*'
     A                                      SPACEA(1)
     A                                    12'************************'
     A                                    36'************************'
     A                                    60'******'
