      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL IRS Date Schedule Exception Rpt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module                                       *
     A*                                                               *
     A*  PRTF/DL4111P1 - IRS Date Schedule Exception Report           *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. 250619             Date 11Jan22               *
      *  Prev Amend NO. MD046248           DATE 27OCT17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CIR001 *CREATE     DATE 13JUN95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  250619 - Remove *IN20 condition on SPACEA for detail lines   *
      *           Use correct spelling of "PRINCIPAL"                 *
      *  CIR001 - Interest Rate Calendars.                            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL4111P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    49'IRS DATE SCHEDULE EXCEPTION +
     A                                      REPORT'
     A                                      SPACEA(1)
      *
     A                                    49'----------------------------+
     A                                      ------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'DEAL NUMBER'
     A                                    21'SCHEDULE NAME'
     A                                    61'DATE EXCEPTIONS'
     A                                      SPACEA(1)
     A                                     6'-----------'
     A                                    21'-------------'
     A                                    61'---------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A  33        PDLNO          6A        8
     A  10                                21'OUR INTEREST RATE CHANGE SCHEDULE'
     A  11                                21'THEIR INTEREST RATE CHANGE +
     A                                      SCHEDULE'
     A  12                                21'OUR INTEREST PAYMENT SCHEDULE'
     A  13                                21'THEIR INTEREST PAYMENT SCHEDULE'
     A  14                                21'OUR CASH FLOW SCHEDULE'
     A  15                                21'THEIR CASH FLOW SCHEDULE'
      ** Start of change 250619
     A**16******                          21'OUR PRINCIPLE CHANGE SCHEDULE'                   250619
     A**17******                          21'THEIR PRINCIPLE CHANGE SCHEDULE'                 250619
     A  16                                21'OUR PRINCIPAL CHANGE SCHEDULE'
     A  17                                21'THEIR PRINCIPAL CHANGE SCHEDULE'
      ** End of change 250619
     A            DATE1          7A       61
     A            DATE2          7A       71
     A            DATE3          7A       81
     A            DATE4          7A       91
     A            DATE5          7A      101
     A            DATE6          7A      111
      ** Start of change 250619
     A**20******                            SPACEA(1)                                         250619
     A                                      SPACEA(1)
      ** End of change 250619
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
