      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Forward Commitments Revaluation (UP/UL)')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL4410P1 - DL Forward Commitments Revaluation (UP/UL)   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R DL4410D1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL4410P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    30'FORWARD COMMITMENTS REVALUATION +
     A                                      USING'
     A            BNNPV2         1A       68TEXT('NPV Method')
     A                                    70'METHOD REPORT - UP TO'
     A            REVCD          7A       +1TEXT('Event Control Date')
     A                                      SPACEA(1)
      *
     A                                    30'-------------------------------+
     A                                      -------'
     A                                    68'-------------------------------'
     A                                      SPACEA(2)
      *
     A                                    23'BRANCH:'
     A            RBRCA          3A       +1TEXT('Branch')
     A            RBRNM         30A       +2TEXT('Branch Description')
     A                                      SPACEA(2)
      *
     A  02                                23'CUSTOMER'
     A  02                                36'DEAL NO.'
     A                                    49'ACCOUNT KEY'
     A                                    71'AMOUNT'
     A                                    88'CURRENCY'
     A                                   101'REV IND'
      *
     A                                      SPACEA(2)
      *
     A          R DL4410D2
      *
     A  02        RCNUM          6A       24TEXT('Customer Number')
     A  02        RDLNO          6A       37TEXT('Deal Number')
      **Start of change CDL038
     A************RACKY         10A       50TEXT('Account Key')                               CDL038
     A            RACKY         14A       49TEXT('Account Key')                               CDL038
      **End of change CDL038
     A            RYAMT         18A       65TEXT('Deal Amount')
     A            RCYCD          3A       91TEXT('Currency')
     A            RREVI          1A      104TEXT('Reversal Indicator')
     A                                      SPACEA(1)
      *
     A          R DL4410D3
      *
     A                                      SPACEB(1)
      *
     A                                    50'TOTAL ACCOUNT KEYS GENERATED'
     A            RTOTAL         5S 0     82TEXT('Total A/C Keys Generated')
      *
     A                                      SPACEA(2)
      *
     A          R DL4410D4
      *
     A                                      SPACEB(3)
      *
     A                                    46'***  End of Report for +
     A                                      Branch  ***'
      *
