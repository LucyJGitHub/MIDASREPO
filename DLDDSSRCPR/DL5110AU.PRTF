      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas DL Accruals Using N-T-R Extract Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL5110AU - Midas DL Accruals Using N-T-R Extract Audit  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL5110AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    44'INTEREST ACCRUALS +
     A                                      EXTRACT FILE CONTROLS'
     A                                      SPACEA(1)
      *
     A                                    44'------------------+
     A                                      ---------------------'
      *
     A          R CONTROL                   SKIPB(20)
     A                                    10'RUN CONTROLS'
     A                                    54'NUMBER'
     A                                    83'VALUE'
     A                                      SPACEA(2)
     A                                    15'LIVE DEALS - ON FILE'
     A            RNLRA          5S 0     55EDTCDE(3)
     A            RVLAF         15S 0     70EDTCDE(Z)
     A            RVLAL          3S 0     85
     A                                      SPACEA(2)
     A                                    15'LIVE DEALS - CALCULATED'
     A            RRECR          5S 0     55EDTCDE(3)
     A            RINT2         15S 0     70EDTCDE(Z)
     A            RDEC2          3S 0     85
     A N80                                  SPACEA(2)
     A  80                                92'*** DIFFERENCE ***'
     A  80                                  SPACEA(2)
     A                                    15'RECORDS ON EXTRACT FILE'
     A                                    39'CREATED'
     A            RRECT          5S 0     55EDTCDE(3)
     A            RINT1         15S 0     70EDTCDE(Z)
     A            RDEC1          3S 0     85
     A                                      SPACEA(3)
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
