      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Accruals Using Net-Treasury-Rate Report')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL5210P1 - Interest Accruals Using Net-Treasury-Rate    *
      *                  Report                                       *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL5210P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  43                                25'Interest Accruals Using Net +
     A                                      Treasury Rate -'
     A  44                                17'Discount/Premium'
     A  44                                34'Accruals Using Net Treasury +
     A                                      Rate -'
     A  45                                17'Swap Profit/Loss'
     A  45                                34'Accruals Using Net Treasury +
     A                                      Rate -'
     A  46 05                             25'Fee Amortization Using Net +
     A                                      Treasury Rate -'
      *
     A                                    69'Up To But'
     A                                    79'Excluding -'
     A            RDATE          7A       91
     A                                      SPACEA(1)
     A  44
     AO 45                                17'--------'
     A                                    25'---------------------------+
     A                                      -----------------------+
     A                                      -----------------------'
     A                                      SPACEA(3)
      *
      *
     A                                     6'BRANCH '
     A            A8BRCD    R             13REFFLD(BRCA)
     A            A8BRNM    R             18REFFLD(BRNM)
     A                                      SPACEA(2)
      *
     A                                     1'CUST.'
     A                                     8'NAME'
     A                                    30'DEAL'
     A                                    37'ACCRUAL PERIOD'
     A N05
     AON46 05                             58'RATE'
     A                                    70'CCY'
     A                                    81'PRINCIPAL'
     A                                    94'ACCRUED TO DATE'
     A                                   119'TO ACCRUE'
     A                                      SPACEA(1)
      *
     A                                     1'NUMBER'
     A                                     8'TOWN'
     A                                    29'NUMBER'
     A                                    38'FROM'
     A                                    47'TO'
      *
     A          R HEADP2
     A                                      SPACEB(2)
     A  50N19     RNARR1        50A       10
     A  19                                10'INTERNAL CONTRACTS'
      *
     A          R HEADP3
     A                                      SPACEB(2)
     A            RNAR2A         6A       10                                                  CDL038
     A            RNAR2B        35A       17                                                  CDL038
     A************RNARR2        33A        6                                                  CDL038
     A                                      SPACEA(1)
      *
     A          R DETAIL1
     A                                      SPACEB(1)
     A            RCNUM          6A        1
     A            RCNAME        20A        8
     A            RLNRF          6A       29
     A            RACFR          7A       37
     A            RACTD          7A       45
     A  20 35N22                          45'CALL   '
     A  22 35N20                          45'NOTICE '
     A  24        RUBRTT         2A       53
     A N05
     AON46 05     RINTR         13A       56
     A            RCCY           3A       70
     A            RPCPL         19A       74
     A            RAMAD         19A       94
     A            RAMTA         19A      114
     A                                      SPACEA(1)
      *
     A          R DETAIL2
      *
     A            RTOWN         10A        8
     A  58 51                             47'Total Unsettled'
     A  58 51                             63'Interest :'
     A  58 51     ROTUI         19A       94
     A  58 51                               SPACEA(1)
     A  58N64     RTACFR         7A       37
     A  58N64     RTACTD         7A       45
     A  58        RTBRTT         2A       53
     A  58        RTEINR        13A       56
     A  58        RTAMAD        19A       94
     A  58        RTAMTA        19A      114
     A  58 52
     AO 64 71N11                            SPACEA(1)
     A  64 71N11  RFLRAT        13A       56
     A  58 52                             47'Total Unsettled'
     A  58 52                             63'Interest :'
     A  58 52     RTTUI         19A       94
      *
     A          R TOTAL1                    SPACEB(2)
      *
     A                                    50'DEAL SUB-TYPE TOTAL'
     A            RDSPR         19A       74
     A            RDSIAD        19A       94
     A            RDSITA        19A      114
      *
     A          R TOTAL2                    SPACEB(2)
      *
     A                                    54'DEAL TYPE TOTAL'
     A            RDTPR         19A       74
     A            RDTIAD        19A       94
     A            RDTITA        19A      114
      *
     A          R TOTAL3                    SPACEB(2)
      *
     A                                    55'CURRENCY TOTAL '
     A            RCCYT          3A       70
     A            RCYPR         19A       74
     A            RCYIAD        19A       94
     A            RCYITA        19A      114
      *
     A          R TRAILP
     A                                      SPACEB(3)
     A                                    46'***  END OF REPORT FOR +
     A                                      BRANCH '
     A            RBRCA          3A       76
     A                                    80' ***'
      *
