      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Deal Int. and Cost of Funds Accrual Reprt')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL6900P1 - Dealing Interest and Cost of Funds Accruals  *
      *                  Report                                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC001 *C *CREATE  Date 01Feb96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAC001 - Profit Centre Accounting Development:               *
      *           New printer file for DL6900.                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE DL6900P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'DEALING INTEREST AND COST OF +
     A                                      FUNDS ACCRUALS REPORT'
      *
     A  37 38                             96'CONSOLIDATED'
     A  37N38                             93'BY BRANCH'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    42'-----------------------------+
     A                                      ---------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A  37                                 6'BOOKING BRANCH'
     A  37        RBRCA     R             22REFFLD(BRCA)
     A  37        RBRNM     R             26REFFLD(BRNM)
     A                                   132' '
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                    14'DEAL NUMBER'
     A                                    28'CURRENCY'
     A                                    39'DEAL TYPE'
     A                                    54'DEAL'
     A                                    68'AMOUNT'
     A                                    88'ACCRUED'
     A                                   108'ACCRUED COST'
     A                                      SPACEA(1)
     A                                    52'SUB-TYPE'
     A                                    88'INTEREST'
     A                                   110'OF FUNDS'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RDLNO     R             16REFFLD(DLNO)
     A            RCCY      R             31REFFLD(CCY)
     A            RDTYP     R             43REFFLD(DTYP)
     A**Start of change CDL038
     A************RDLST     R             55REFFLD(DLST)                                      CDL038
     A            RDLST     R             54REFFLD(DLST)                                      CDL038
     A            RCLAS          4A       56                                                  CDL038
     A**End of change CDL038
     A  35        RPCPL     R             62REFFLD(ZZ018)
     A  36        RPUPR     R             62REFFLD(ZZ018)
     A            RAIPD     R             83REFFLD(ZZ018)
     A            RACPD     R            105REFFLD(ZZ018)
     A                                      SPACEA(1)
      *
      *-----------------------------*
      ***  Sub-Totals Record Format *
      *-----------------------------*
      *
     A          R SUBTTL
     A                                    83'------------------'
     A                                   105'------------------'
     A                                      SPACEA(1)
     A  31                                39'DEAL SUB-TYPE SUB-TOTAL'
     A  32                                48'DEAL TYPE SUB-TOTAL'
     A  33                                54'CURRENCY SUB-TOTAL'
     A            RTAIPD    R             80REFFLD(ZZ021)
     A            RTACPD    R            102REFFLD(ZZ021)
     A                                      SPACEA(1)
      *
      *-----------------------------*
      ***  Database Error Reference *
      *-----------------------------*
      *
     A          R DBERRF                    SPACEB(3)
     A                                    32'*** REFERENCE DL6900'
     A                                    53'DATABASE ERROR ***'
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37N38                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37
     AO 38                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
