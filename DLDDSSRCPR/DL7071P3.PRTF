      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas DL Outstanding fid deposits by cust - comp')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL7071P3 - Outstanding AUD Fiduciary Deposits By        *
      *                  Customer - Company level                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CDL038             Date 10May05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADR3                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(2) SPACEA(2)
     A                                     7'REFERENCE DL7071P3'
     A            BJURPT        53        41
     A                                    95'START OF DAY'
     A            RPTDAT         7       108
     A                                   118'PAGE'
     A                                   123PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A  88                                 6'COMPANY'
     A  88        CMPYM          3        14
     A  88        CMPTXT        20        19
     A                                    48'OUTSTANDING AUD +
     A                                       FIDUCIARY DEPOSITS BY +
     A                                       CUSTOMER'
     A                                      SPACEA(1)
     A                                    48'----------------+
     A                                       ----------------------+
     A                                       --------'
     A                                      SPACEA(2)
      *
     A                                     2'CUSTOMER  CUSTOMER A/C'
     A                                    25'OFF  DEAL'
     A**Start of change CDL038
     A************************************37'DEAL BROKER    PRINCIPAL'                        CDL038
     A                                    36'DEAL  BROKER      PRINCIPAL'                     CDL038
     A**End of change CDL038
     A                                    67'RATE'
     A                                    91'TERM'
     A                                   101'FREQ LAST TRANS    NET'
     A                                   124'ACCRUED'
     A                                      SPACEA(1)
      *
     A                                     3'NUMBER     NAME'
     A                                    22'CODE   NUMBER'
     A**Start of change CDL038
     A************************************37'S-T   CODE      AMOUNT'                          CDL038
     A                                    36'S-T    CODE        AMOUNT'                       CDL038
     A**End of change CDL038
     A                                   108'VALUE DATE   INTEREST'
     A                                      SPACEA(1)
      *
     A          R DETL3                     SPACEA(1)
      * Begin /CSD027/
     A**10******  CNUM           6S 0      3                                                  CSD027
     A  10        CNUM           6A        3
      * End /CSD027/
     A  10        CSNM          10        11
     A  10        ACOC           2A       23
     A            DLNO           6S 0     29
     A**Start of change CDL038
     A************DLST***********2********38                                                  CDL038
     A            DLST           2        36                                                  CDL038
     A            CLAS           4        38                                                  CDL038
     A**End of change CDL038
     A            BRKC           4        43
     A            XPCPL         13  2     47EDTCDE(J)
     A            RATE           8  4     65EDTCDE(L)
     A            TERMD          8        92
     A            IPFR           1       102
     A            LCDAT          7       107
     A            NAID          13  2    115EDTCDE(J)
      *
     A          R SPACE3                              SPACEA(2)
     A            FILLER         1         2
     A          R CTOT3                               SPACEA(2)
     A                                    47'-----------------'
     A                                    65'---------'
     A                                   115'-----------------'
     A                                      SPACEA(1)
     A            DTPCPL        13  2     47EDTCDE(J)
     A            DTWTAV         8  4     65EDTCDE(L)
     A            DTNAID        13  2    115EDTCDE(J)
      *
     A          R CTOTL3                              SPACEA(2)
     A                                    47'-----------------'
     A                                    65'---------'
     A                                   115'-----------------'
     A                                      SPACEA(1)
     A                                     3'CUSTOMER TOTAL'
     A            DCPCPL        13  2     47EDTCDE(J)
     A            DCWTAV         8  4     65EDTCDE(L)
     A            DCNAID        13  2    115EDTCDE(J)
     A                                      SPACEA(1)
     A                                    47'================='
     A                                    65'========='
     A                                   115'================='
      *
     A          R BTOT3                     SPACEB(1) SPACEA(2)
     A                                     3'COMPANY 11AM TOTAL'
     A            BTRM0         13  2     47EDTCDE(J)
     A            BRATE0         8  4     65EDTCDE(L)
     A                                    81'COMPANY BALANCE < 50,000'
     A            BLTMAA        13  2    110EDTCDE(J)
     A                                      SPACEA(1)
      *
     A                                     3'COMPANY 24HR TOTAL'
     A            BTRM1         13  2     47EDTCDE(J)
     A            BRATE1         8  4     65EDTCDE(L)
     A                                    81'COMPANY BALANCE NOT +
     A                                       < 50,000'
     A            BGEMAA        13  2    110EDTCDE(J)
     A                                      SPACEA(1)
      *
     A                                     3'COMPANY CALL(EX. 11AM +
     A                                      & 24HR) TOTAL'
     A            BTRM2         13  2     47EDTCDE(J)
     A            BRATE2         8  4     65EDTCDE(L)
     A                                      SPACEA(1)
      *
     A                                     3'COMPANY TERM TOTAL'
     A            BTRM3         13  2     47EDTCDE(J)
     A            BRATE3         8  4     65EDTCDE(L)
     A                                      SPACEA(1)
      *
     A                                     3'COMPANY GRAND TOTAL'
     A            BTOTAL        13  2     47EDTCDE(J)
     A            BRTOTL         8  4     65EDTCDE(L)
      *
     A          R NODTL3                    SPACEB(2)
     A                                    56'** NO DETAILS TO REPORT **'
      *
     A          R DBERR3                    SPACEB(2)
     A                                    57'*** DATABASE ERROR ***'
      *
     A          R EORPT3                    SPACEB(2)
     A N88                                58'*** END OF REPORT ***'
     A  88                                58'*** END OF REPORT FOR'
     A  88                                80'COMPANY '
     A  88        CMPYM          3        88
     A  88                                92'***'
