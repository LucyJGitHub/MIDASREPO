      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60J                                                    *
/*EXI *  TEXT('Midas DL Domestic MM Brokerage paid - Company')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing Module                           *
     A*                                                               *
     A*      PRTF/DL8106P1  -  BROKERAGE PAID REPORT.                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. AUS007             Date 13Jul92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*     AUS007 - AUSTRALIAN BROKERAGE                             *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A          R P1HEAD                    SKIPB(02)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     5'REFERENCE DL8106P1'
     A            BJURPT        53        37
     A                                   100'DATE:'
     A            BJMRDT         7       106
     A                                   118'PAGE:'
     A            PAGE           5  0    128EDTCDE(Z)
     A                                      SPACEA(02)
     A                                    44'DOMESTIC MONEY MARKET BROKERAGE '
     A                                    76'PAID REPORT'
     A  88                                91'COMPANY'
     A  88        CMCD      R             99
     A  88        APCMNM        30       103
     A                                      SPACEA(01)
     A                                    44'--------------------------------'
     A                                    76'-----------'
     A                                      SPACEA(02)
     A                                    11'BROKER'
     A            BKNM          30        21
     A                                      SPACEA(01)
     A            BKTN          10        21
     A                                      SPACEA(02)
     A                                    19'DEAL'
     A                                    53'VALUE'
     A                                    63'MATURITY'
     A                                   110'BROKERAGE'
     A                                      SPACEA(01)
     A                                     6'DEAL DATE'
     A                                    18'NUMBER'
     A                                    28'CUSTOMER'
     A                                    53'DATE'
     A                                    64'DATE'
     A                                    78'PRINCIPAL'
     A                                   107'PAID THIS MONTH'
     A                                      SPACEA(02)
     A          R P1DLLN
     A            DDATE          7         7
     A            DLNO           6  0     18
     A            CRNM          20        28
     A            VDATE          7        52
     A            MDATE          7        63
     A            PCPLA         17        74
     A            BPTMA         17       105
     A                                      SPACEA(01)
     A          R P1BKTL                    SPACEB(01)
     A                                    75'BROKER TOTAL'
     A            BBPTMA        17       105
     A                                      SPACEA(01)
     A                                   105'-----------------'
     A                                      SPACEA(01)
     A          R P1GTOT                    SPACEB(01)
     A                                    82'TOTAL'
     A            TBPTMA        17       105
     A                                      SPACEA(01)
     A                                   105'-----------------'
     A                                      SPACEA(01)
     A          R P1ERR                     SPACEB(01)
     A                                    57'*** DATABASE ERROR ***'
     A                                      SPACEA(02)
     A          R P1END                     SPACEB(02)
     A N88                                58'*** END OF REPORT ***'
     A  88                                58'*** END OF REPORT FOR'
     A  88                                80'COMPANY ***'
