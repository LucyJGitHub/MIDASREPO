/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Deal Amendments Consolidation Audit Report')           *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  PRTF/DL9911AU - Deal Amendments Consolidation Audit Report   *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER040  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER040 - Consolidated MM Deal Confirmation Correspondence    *
      *                (upgFGE097)                                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
     A          R AUDHDR
     A                                      TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
      *
     A                                      SKIPB(3)
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            BJURPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            BJMRDT         7       116TEXT('midas rundate')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
      *
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
      *
     A            ##ARTL        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##UNDL        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *
      *================================================================
     A          R BTOTS                     TEXT('Branch Totals')
     A                                      SPACEB(4)
     A                                    24'Branch :'
     A            FBRCA          3        33TEXT('Branch Code')
     A                                    44'Total Number of +
     A                                      Amendments :'
     A            UFTTAM         6  0     74TEXT('Total Amendments')
      *
     A                                    44'Total Number of +
     A                                      Deals      :'
     A                                      SPACEB(2)
     A            UFTTDL         6  0     74TEXT('Total Deals')
      *
      *================================================================
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO +
     A                                      REPORT ***'
     A                                      SPACEB(3)
      *
      *================================================================
      *
     A          R ENDREP
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
      *================================================================
      *
     A          R DBERR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE        10A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEB(1)
      *****************************************************************
