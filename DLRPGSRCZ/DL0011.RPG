     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Citydealer cash flow extract in EVTS fmt')    *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0011 - Midas/Citydealer Cash Flow extract in EVTS format   *
      *                                                               *
      *  Function:  This program will be executed as the 'EXIT' prog- *
      *  ( I/C )    ram from within a RCVJRNE command on journal      *
      *             ICJRN from within DLC0002.  This will monitor for *
      *             any transactions which may affect cashflow and    *
      *             exposure from within LE, RE, FT and NT.  Details  *
      *             of these transactions will be extracted for trans-*
      *             mission to Citydealer.                            *
      *                                                               *
      *  Called By: DLC0002 - LE, FT, RE & NT Transactions Extract    *
      *                       Control                                 *
      *                                                               *
      *  Calls    : BUDEALR0- Background Update of Citydealer Dealing *
      *                       ICD                                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD053295           Date 13Apr19               *
      *  Prev Amend No. MD050139           Date 23Mar18               *      
      *                 CLE071             Date 18Jul18               *
      *                 MD046248           Date 27Oct17               *
      *                 MD047993           Date 23Sep17               *
      *                 MD035545           Date 11Sep15               *
      *                 CGL184             Date 07Dec16               *
      *                 CAP212             Date 15Sep14               *
      *                 AR996895           Date 25Nov12               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CRE075             Date 06Dec10               *
      *                 CLE064             Date 06Dec10               *
      *                 263666             Date 01Feb10               *
      *                 CER059             Date 19Jul10               *
      *                 CER016A            Date 19May08               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 246141             Date 26Mar07               *
      *                 164847             Date 30Nov06               *
      *                 BUG12990           Date 05Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 242286             Date 25Sep06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CIR013             Date 25Apr05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CCM003  *CREATE    Date 31Jan01               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053295 - Lending loan with Interest Calculation Basis      *
      *             method 3 (30/360) has wrong interest payment      *
      *             amount for month of March (Recompile)             *
      *  MD050139- AOSARDR0 called every time causing performance     *
      *            issue. Recompile due to changes in /COPY           *
      *            ZFREQ/ZFRQ*.                                       *      
      *  CLE071 - Value Dating of Rate Changes to Fees (Recompile)    *
      *  MD046248 - Finastra Rebranding                               *
      *  MD047993 - Modified MD035545. Removed dependency on Accrue   *
      *             on Last Day indicator.                            *
      *  MD035545 - LE: Interest Calculation in Leap year. Consider   *
      *             Accrue on Last Day Indicator for method 6.        *
      *  CGL184 - New Frequencies for Account Statements (Recompile)  *
      *  CAP212 - FT Nostro Transfer API (Recompile)                  *
      *  AR996895 - Incorrect accrual interest calculation            *
      *             (Child: AR996897) (Recompile)                     *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CLE148 - Alpha Loan Reference                                *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
      *  263666 - Recompiled due to changes in ZINTDYZ2               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER016A - German Interest Calculation: Upgarde of FGE059     *
      *            to Midas Plus (Recompile)                          *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  246141 - Recompiled due to changes in ZINTDYZ2               *
      *  164847 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  BUG12990 - Recompiled due to changes in ZINTDYZ2/ZINTDYZ2LE  *
      *  242286 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *           Recompile over GLINTC and ZINTDY.                   *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CCM003 - Lending City/Dealer interface.                      *
      *                                                               *
      *****************************************************************
      *
     FACNUMA  IF  E           K        DISK
      ** Define Retail Accounts file
      *
     FDLCDJNPDUF  E                    DISK         KCOMIT
     FDLETNTPDO   E                    DISK         KCOMIT            UC
     FDLETFTPDO   E                    DISK         KCOMIT            UC
     FDL0011AUO   E                    PRINTER                        UC
      *
     F/EJECT
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F*  F U N  C T I O N    O F    I N D I C A T O R S               *
     F*                                                               *
     F*  42         Error on update to PF/DLCDJNPD                    *
     F*  57         End of file on PF/DLCDJNPD                        *
     F*  59         Stop Accruals is not active                       *
     F*  60         Work indicator for COMP op code                   *
     F*  97         SHUT DOWN                                         *
     F*  98         Date Format Indicator                             *
     F*  U7         Database Error                                    *
     F*  U8         Database Error                                    *
     F*  LR         Exit program                                      *
     F*                                                               *
     F*****************************************************************
      /TITLE SUBROUTINE INDEX
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E    I N D E X                             *
     F*                                                               *
     F*  Main routine                                                 *
     F*  #LINIT  -  Initial Screen Processing                         *
     F*  #LPNTD  -  Processing for Nostro Transfer Dealing            *
     F*  #LPNTF  -  Processing for Nostro Transfer FT                 *
     F*  #LPFTP  -  Processing for Funds Transfer Payments            *
     F*  #LCJIP  -  Check if Journal Image to be Processed            *
     F*  #AMNT2  -  Format Amount                                     *
     F*  #FUNDT  -  Find Customer for Funds Transfer Changes          *
     F*  #DATE   -  Convert date DDMMYY to YYYYMMDD format            *
     F*  #NOST   -  Access Nostro customer details                    *
     F*  #CURR   -  Access Currency details (No. of Decimal pos.)     *
     F*  #ADDR   -  Access Customer Details file (Report Name/Town)   *
     F*  #BRCH   -  Access Branch Details file (Internal Customer)    *
     F*  APP6    -  Format for Nostro Transfers Dealing               *
     F*  APP7    -  Format for Nostro Transfers FT                    *
     F*  APP8    -  FT Payments same currency                         *
     F*  APP9    -  FT Payments cross currency                        *
     F*  AP10    -  Format for Nostro Transfers FT      Second Record *
     F*  AP11    -  Format for Nostro Transfers Dealing Second Record *
     F*  GLINTC  -  Calculate Interest Between Two Dates              *
     F*  ZFREQB  -  Calculate Next Payment/Statement Date             *
     F*  ZFWDT   -  Find nth Working Day Forward from a Given Date    *
     F*  *PSSR   -  Error Subroutine                                  *
     F*                                                               *
     F*****************************************************************
     E/EJECT
      *
     E                    AAMT       16  1
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZFREQBZ1
      /COPY ZSRSRC,ZHOLE
      /COPY ZSRSRC,ZDATE9Z1
      /COPY ZSRSRC,ZEDITZ1
      *
      *
     I*-------------------------------------------------------------------------
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IJRNENT      DS                           2000
     I                                        6  150JRNSQ
     I                                       17  18 JOENTT
     I                                       57  66 JOPGM
     I                                       87  96 JFILE
      *
      **      DEALSDB  - DEALS FILE
      *
     IDEALDB      DS                            625
     I                                      126 126 RECIDB
     I                                      127 1320DLNODB
     I                                      133 1330RCDTDB
     I                                      134 1350RCDCDB
     I                                      136 141 CNUMDB
     I                                      142 144 BRCADB
     I                                      145 146 DTYPDB
     I                                      146 147 DLSTDB
     I                                    P 152 1540VDATDB
     I                                      171 173 PUCYDB
     I                                    P 174 1800PUAMDB
     I                                      522 522 CHTPDB
     I                                      534 5350RSTMDB
     I                                      536 547 RONSDB
     I                                      603 6040PSTMDB
     I                                      605 616 PONSDB
      *
      **      NTRANDD  - NOSTRO TRANSFERS FILE
      *
     INTRAND      DS                            625
     I                                      126 126 RECINT
     I                                      127 141 TFRFNT
     I                                      146 1470DESTNT
     I                                    P 148 1500VDATNT
     I                                      167 169 CCYNT
     I                                    P 170 1760AMNTNT
     I                                      185 1860ACBNNT
     I                                      353 353 AUINNT
     I                                      383 383 PRINNT
     I                                      492 494 BRCANT
      *
      **      OTPAYDD  - OUTGOING PAYMENTS FILE
      *
     IOTPAYD      DS                           2000
     I                                      126 126 RECIOP
     I                                      127 141 PREFOP
     I                                      142 143 PYTPOP
     I                                      146 146 DSTTOP
     I                                      147 181 DST1OP
     I                                      287 287 SNCTOP
     I                                      288 2970SNCOOP
     I                                      298 298 ORBTOP
     I                                      299 3100ORBKOP
     I                                      311 311 ORCTOP
     I                                      312 346 ORC1OP
     I                                    P 452 4580SMAMOP
     I                                      467 469 SMCYOP
     I                                    P 471 4778RATEOP
     I                                    P 486 4920PYAMOP
     I                                      501 503 PCCYOP
     I                                    P 504 5060SLDTOP
     I                                    P 525 5270PVDTOP
     I                                     15891589 AUINOP
     I                                     19741976 BRCAOP
      *
      **      INPAYDD  - INCOMING PAYMENTS FILE
      *
     IINPAYD      DS                           2000
     I                                      126 126 RECIIN
     I                                      127 141 PREFIN
     I                                      146 146 SNTPIN
     I                                      147 181 SND1IN
     I                                      287 287 RCRTIN
     I                                      288 2970RCCOIN
     I                                      298 298 ACBTIN
     I                                      299 310 ACBKIN
     I                                      311 311 BNCTIN
     I                                      312 346 BNC1IN
     I                                    P 487 4930SMAMIN
     I                                      502 504 SMCYIN
     I                                    P 506 5128RATEIN
     I                                    P 521 5270PYAMIN
     I                                      536 538 PCCYIN
     I                                    P 539 5410SLDTIN
     I                                    P 560 5620PVDTIN
     I                                     17571757 AUININ
     I                                     18851887 BRCAIN
      *
      ** Data Structures to convert to Date format (YYYYMMDD)
     I            DS
     I                                        1   8 DATF2
     I                                        1   40YEAR
     I                                        5   6 MONTH
     I                                        7   8 DAY
      *
     I            DS
     I                                        1   6 DATF1
     I                                        1   2 DY
     I                                        3   4 MTH
     I                                        5   60YR
      *
      ** Data Structures to hold data to be output to files
     IPFLD        DS                            512
     ITEMP        DS                            512
      *
     II#DTA       DS                            256
      *  Account type N=nostro nbr F=Full nostro G=General Ledger
      *               P=Partial Ledger R= Retail Number
     I                                        1   1 I#ATYP
      *               Full General Ledger - CNUM/CCY/ACOD/ACSQ
     I**********                              2  19 I#ACCT                                    CGL029
     I                                        2  25 I#ACCT                                    CGL029
     I**********                              2   70I#CNU1                                    CSD027
     I                                        2   7 I#CNU1                                    CSD027
     I                                        8  10 I#CCY1
     I**********                             11  140I#ACO1                                    CGL029
     I**********                             15  160I#ACS1                                    CGL029
     I**********                             17  19 I#BRC1                                    CGL029
     I                                       11  200I#ACO1                                    CGL029
     I                                       21  220I#ACS1                                    CGL029
     I                                       23  25 I#BRC1                                    CGL029
      *               Nostro number
     I                                        2   30I#NOSN
     I                                        4   4 I#TEST
      *               Full Nostro - CCY/NOSN
     I                                        2   6 I#NOS
      *               Retail Number
     I                                        2  110I#ACNO
      *               Partial Account - CNUM/ACOD/ACSQ
     I**********                              2   70I#CNUM                                    CSD027
     I                                        2   7 I#CNUM                                    CSD027
     I**********                              8  110I#ACOD                                    CGL029
     I**********                             12  130I#ACSQ                                    CGL029
     I                                        8  170I#ACOD                                    CGL029
     I                                       18  190I#ACSQ                                    CGL029
      *               Branch of account if Partial                      '
     I**********                             14  16 I#BRCA                                    CGL029
     I                                       20  22 I#BRCA                                    CGL029
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISDCUST    E DSSDCUSTPD
      ** Customer Details
      *
     ISDMMOD    E DSSDMMODPD
      ** Module Setting Details
      *
     ISDCLND    E DSSDCLNDPD
      ** Customer Lending ICD Details
      *
     ISDDEAL    E DSSDDEALPD
     I              QQACCD                          QQACC1                                    CGL029
      ** Deal Details
      *
     ISDBSRT    E DSSDBSRTPD
      ** Base Rate Details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency Details
      *
     ISDNOST    E DSSDNOSTPD
     I              QQACCD                          QQACC2                                    CGL029
      ** Nostro Details
      *
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          QQDFA1                                    CGL029
      ** Branch Details
      *
     IDSFDY     E DSDSFDY
      ** Short Data Structure
      *
     ISDACCT    E DSACCNTAB
     I              RECI                            SDRECI
     I              CNUM                            SDCNUM
     I              CCY                             SDCCY
     I              ACOD                            SDACOD
     I              ACSQ                            SDACSQ
     I              PRFC                            SDPRFC
     I              ORED                            SDORED
     I              LCD                             SDLCD
     I              CHTP                            SDCHTP
     I              TNLU                            SDTNLU
      ** Account Details
      *
     I              'FTPAYMENT'           C         FTPAY
     I              'CUSTLEND'            C         CUSTL
     I              ',,,,,,,,,,,,'        C         EMPTY
     I              'LECalcBasis6LeapYear'C         WPARML                                  MD035545
      ** Dealer ID and blank positions for FT Payments
      *
     I/COPY ZSRSRC,ZHOLI
     I/COPY ZSRSRC,ZINTDYZ1
     I/COPY ZSRSRC,ZDATE9Z2
     I/COPY ZSRSRC,ZHOLDS
     I/COPY ZSRSRC,ZAOZ2
     I*--------------------------------------------------------------
     I* THIS SECTION IS ADDED TO THE PGM DUE TO DBA2 PTF06 UPGRADE
     I/COPY ZSRSRC,ZDAT10Z1
     I*--------------------------------------------------------------
      *
      *****************************************************************
      *                    MAIN CYCLE PROCESSING                      *
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Entry parameters from EXITPGM of RCVJRNE command
      *
     C           *ENTRY    PLIST
     C                     PARM           JRNENT
     C                     PARM           CALLCD  1
      *
      ** Initial Processing
      *
     C                     EXSR #LINIT
      *
     C           CALLCD    IFEQ '1'                        B1
      *
     C                     OPEN DLETNTPD
     C                     OPEN DLETFTPD
      *
      ** Perform process for Nostro Transfer Dealing
      *
     C           JFILE     IFEQ 'DEALSDB'
     C                     EXSR #LPNTD
     C                     ENDIF
      *
      ** Perform process for Nostro Transfer FT
      *
     C           JFILE     IFEQ 'NTRANDD'
     C                     EXSR #LPNTF
     C                     ENDIF
      *
      ** Perform process for FT Payments
      *
     C           JFILE     IFEQ 'OTPAYDD'
     C           JFILE     OREQ 'INPAYDD'
      *
     C                     EXSR #LPFTP
     C                     ENDIF
      *
      ** Set on the external User switch U6 and the LR indicator
      *
     C           JOENTT    IFEQ 'XJ'                       B2
     C                     MOVE '1'       *INU6
     C                     MOVE '1'       *INLR
     C                     MOVEL'9'       CALLCD
     C                     END                             E2
      *
      ** Set on the LR indicator
      *
     C           JOENTT    IFEQ 'XE'                       B2
     C                     MOVE '1'       *INLR
     C                     MOVEL'9'       CALLCD
     C                     END                             E2
      *
      ** Read the Citydealer Extracted Journal Info file
      *
     C           1         SETLLDLCDJND0                 57
     C                     READ DLCDJND0                 57
      *
     C           *IN57     IFEQ '1'                        B2
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            *************
     C                     MOVEL'DLCDJNPD'DBFILE           *DB ERROR 04*
     C                     MOVEL'*FIRST'  DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             E2
      *
      ** Update the record on PF/DLCDJNPD
      *
     C                     MOVELJRNSQ     JOSEQN
     C                     UPDATDLCDJND0               42
      *
     C           *IN42     IFEQ '0'                        B2
     C                     COMIT
     C                     END                             E2
      *
      ** Copy details to document DLETVS for transfer to CITYDEALER
      *
     C                     CLOSEDLETNTPD
     C                     CLOSEDLETFTPD
     C                     CALL 'DLC0011'
      *
     C                     END                             E1
     C* Try transmission test even if no new entries
     C           CALLCD    IFEQ '0'                        B1
     C                     CALL 'DLC0011'
     C                     END                             E1
      *
      ** Determine if program is going to shut down
      *
     C                     SHTDN                     97
     C           *IN97     IFEQ '1'                        B1
     C                     MOVE '1'       *INLR
     C                     MOVEL'9'       CALLCD
     C                     END                             E1
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *  #LPNTD - Processing for Nostro Transfers Dealing             *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           #LPNTD    BEGSR
      *
      ** Move the parm-Journal-Entry into a data structure defining all
      ** individual PF/DEALSDB fields
      *
     C                     MOVELJRNENT    DEALDB
      *
      ** Perform check to see whether Journal Image should be processed
      *
     C                     EXSR #LCJIP
      *
      ** Journal entry type is PT (record added to file ) i.e. INSERT
      *
     C           JOENTT    IFEQ 'PT'
      *
     C           DTYPDB    IFEQ 'NX'
     C           VDATDB    ANDGEBJRDNB
      *
     C                     EXSR APP6
     C                     EXSR AP11
     C                     ENDIF
     C                     ENDIF
      *
      ** Journal entry type is UP or UR (after update) and
      ** action is REVERSED.
      *
     C           JOENTT    IFEQ 'UP'
     C           JOENTT    OREQ 'UR'
     C           CHTP      IFEQ 'R'
      *
     C           DTYPDB    IFEQ 'NX'
     C           VDATDB    ANDGEBJRDNB
      *
     C                     EXSR APP6
     C                     EXSR AP11
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #LPNTF - Processing for Nostro Transfers FT                  *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           #LPNTF    BEGSR
      *
      ** Move the parm-Journal-Entry into a data structure defining all
      ** individual PF/NTRANDD fields
      *
     C                     MOVELJRNENT    NTRAND
      *
      ** Only process authorised payments
      *
     C           AUINNT    IFEQ 'Y'
      *
      ** Perform check to see whether Journal Image should be processed
      *
     C                     EXSR #LCJIP
      *
      ** Journal entry type is UP or UR (after update)
      *
     C           JOENTT    IFEQ 'UP'
     C           JOENTT    OREQ 'UR'
      *
     C           VDATNT    IFGE BJRDNB
      *
     C                     EXSR APP7
     C                     EXSR AP10
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #LPFTP - Processing for Funds Transfer Payments              *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           #LPFTP    BEGSR
      *
      ** Clear work fields
     C                     Z-ADD*ZEROS    @VDAT   50
     C                     MOVE *BLANKS   @PCCY   3
     C                     MOVE *BLANKS   @SMCY   3
      *
      ** Move the parm-Journal-Entry into a data structure defining all
      ** individual PF/OTPAYDD and PF/INPAYDD fields
      *
     C           JFILE     IFEQ 'OTPAYDD'
     C                     MOVELJRNENT    OTPAYD
     C                     MOVE 'OP'      TYPE    2
     C                     MOVE AUINOP    AUTH    1
     C                     Z-ADDPVDTOP    @VDAT
     C                     MOVE PCCYOP    @PCCY
     C                     MOVE SMCYOP    @SMCY
     C           PYTPOP    CABEQ'RP'      EXFT
     C                     ENDIF
      *
     C           JFILE     IFEQ 'INPAYDD'
     C                     MOVELJRNENT    INPAYD
     C                     MOVE 'IN'      TYPE
     C                     MOVE AUININ    AUTH
     C                     Z-ADDPVDTIN    @VDAT
     C                     MOVE PCCYIN    @PCCY
     C                     MOVE SMCYIN    @SMCY
     C                     ENDIF
      *
      ** Only process authorised payments
      *
     C           AUTH      IFEQ 'Y'
      *
      ** Perform check to see whether Journal Image should be processed
      *
     C                     EXSR #LCJIP
      *
      ** Journal entry type is UP or UR (after update)
      *
     C           JOENTT    IFEQ 'UP'
     C           JOENTT    OREQ 'UR'
      *
     C           @VDAT     IFGE BJRDNB
      *
     C           @PCCY     IFEQ @SMCY
     C                     EXSR APP8
      *
     C                     ELSE
     C                     EXSR APP9
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           EXFT      TAG
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #LCJIP - Check if Journal Image to be Processed              *
      *                                                               *
      *  Called by  : #LLLAC - Processing for Loan Changes if program *
      *                        is LE0033.                             *
      *               #LALAC - Processing for Loan Changes if program *
      *                        is LE0070.                             *
      *               #LPLAC - Processing for Loan Amendment changes  *
      *               #LPNTD - Processing for Nostro Transfer Deals   *
      *               #LPNTF - Processing for Nostro Transfer FT      *
      *               #LPFTP - Processing for FT Payments             *
      *****************************************************************
     C           #LCJIP    BEGSR
      *
     C                     MOVEL'N'       WINSRT  1
      *
     C           JFILE     IFEQ 'DEALSDB'
     C                     MOVELCHTPDB    CHTP
     C                     ENDIF
      *
     C           JOENTT    IFEQ 'PT'
     C           JOENTT    OREQ 'UR'
     C           JOENTT    OREQ 'UP'
     C                     MOVEL'Y'       WINSRT
     C                     ENDIF
      *
     C           CHTP      IFNE 'A'
     C           CHTP      ANDNE'I'
     C                     MOVEL'N'       WINSRT
     C                     ENDIF
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #AMNT2 - Format Amount                                       *
      *                                                               *
      *  Called by  : APP6, APP7, APP8, APP9.                         *
      *****************************************************************
     C           #AMNT2    BEGSR
      *
     C                     MOVEA*BLANKS   AAMT
     C                     MOVE *BLANKS   PAMT   16
      *
     C                     EXSR ZEDIT
     C                     MOVEAZFIELD    AAMT
     C           ' '       CHECKZFIELD    N       20
     C                     MOVEAAAMT,N    PAMT
      *
     C           PFLD      CAT  PAMT:0    TEMP
     C                     MOVELTEMP      PFLD
      *
     C           WINSRT    IFEQ 'Y'                        Insert Deal
     C           PFLD      CAT  '+,':0    TEMP             ~~~~~~~~~~~
     C                     ELSE                            Reverse Deal
     C           PFLD      CAT  '-,':0    TEMP             ~~~~~~~~~~~~
     C                     ENDIF
     C                     MOVELTEMP      PFLD
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #FUNDT - Find Customer for Funds Transfer Changes            *
      *                                                               *
      *  Called by  : APP8, APP9.                                     *
      *****************************************************************
     C           #FUNDT    BEGSR
      *
     C                     MOVE *BLANKS   @CNUM   6
      *
     C           TYPE      IFEQ 'OP'
     C           I#ATYP    IFEQ 'S'
     C           I#ATYP    OREQ 'A'
     C                     MOVE @PCCY     @CCY
     C                     MOVELSNCOOP    @NOST
     C                     EXSR #NOST
     C                     MOVE A7CUST    @CNUM
     C                     ENDIF
     C                     ELSE
     C           I#ATYP    IFEQ 'S'
     C           I#ATYP    OREQ 'A'
     C                     MOVE @SMCY     @CCY
     C                     MOVELRCCOIN    @NOST
     C                     EXSR #NOST
     C                     MOVE A7CUST    @CNUM
     C                     ENDIF
     C                     ENDIF
      *
      ** If account is Nostro or Full nostro look up on SDNOSTL1
      *
     C           I#ATYP    IFEQ 'N'
     C           I#ATYP    OREQ 'F'
      *
      ** Format key for Access to SDNOSTPD and read file for nostro
      *
     C                     MOVE CNUM      @CNUM
     C           I#ATYP    IFEQ 'N'
     C                     MOVE @SMCY     @CCY
     C                     MOVE I#NOSN    @NOST
     C                     ELSE
     C                     MOVELI#NOS     @CCY
     C                     MOVE I#NOS     @NOST
     C                     ENDIF
      *
     C                     EXSR #NOST
     C                     MOVE A7CUST    @CNUM
     C                     ENDIF
      *
     C           I#ATYP    IFEQ 'P'
     C                     MOVE I#CNUM    @CNUM
     C                     ENDIF
      *
     C           I#ATYP    IFEQ 'G'
     C                     MOVE I#CNU1    @CNUM
     C                     ENDIF
      *
      ** If account is Retail look up on ACNUMA
      *
     C           I#ATYP    IFEQ 'R'
      *
     C           K#RETN    KLIST
     C                     KFLD           ACNO
     C                     MOVELI#ACNO    ACNO
      *
      ** Read retail accounts for details
      *
     C           K#RETN    CHAINACNUMA               90
     C           *IN90     IFEQ '0'
     C                     MOVE CNUM      @CNUM
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #DATE - Convert date DDMMYY to YYYYMMDD format               *
      *                                                               *
      *  Called by  : APP6, APP7, APP8, APP9                          *
      *****************************************************************
     C           #DATE     BEGSR
      *
      ** Convert Day number to date format DDMMYY
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DATF1
      *
      ** Convert Date DDMMYY to format YYYMMDD
     C           ZDAYNO    IFGE 10228
     C           2000      ADD  YR        YEAR
     C                     ELSE
     C           1900      ADD  YR        YEAR
     C                     ENDIF
      *
     C                     MOVE MTH       MONTH
     C                     MOVE DY        DAY
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #NOST - Access Nostro customer details                       *
      *                                                               *
      *  Called by  : APP6, APP7.                                     *
      *****************************************************************
     C           #NOST     BEGSR
      *
      ** Access Nostro details using currency and nostro
     C                     CALL 'AONOSTR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM *BLANKS   @CUST   6
     C                     PARM           @CCY    3
     C**********           PARM           @ACCD   4                                           CGL029
     C                     PARM           @ACCD  10                                           CGL029
     C                     PARM           @ACSN   2
     C                     PARM           @NOST   2
     C                     PARM           @BRCA   3
     C                     PARM *BLANKS   @CSSN  10
     C                     PARM *BLANKS   @PNOI   1
     C           SDNOST    PARM SDNOST    DSFDY
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVELA7CUST    CUSTNO  6
     C                     ELSE
     C                     MOVE *BLANKS   CUSTNO
     C                     ENDIF
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #CURR - Access Currency details (No. of Decimal positions)   *
      *                                                               *
      *  Called by  : APP6, APP7, APP8, APP9                          *
      *****************************************************************
     C           #CURR     BEGSR
      *
      ** Access Currency details
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @AJCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ** Customer record not found - Error
     C           @RTCD     IFNE *BLANKS
     C                     Z-ADD7         DBASE            *************
     C                     MOVEL@AJCD     DBKEY            *DB ERROR 07*
     C                     MOVEL'SDCURRPD'DBFILE           *************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Save No. of Decimal positions to work field
     C                     Z-ADDA6NBDP    @NBDP   10
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #ADDR - Access Customer Details file (Report Name and Town)  *
      *                                                               *
      *  Called by  : APP6, APP7, APP8, APP9                          *
      *****************************************************************
     C           #ADDR     BEGSR
      *
      ** Initialise workfields
     C                     MOVE *BLANKS   @CRTN  10
     C                     MOVE *BLANKS   @CRNM  15
      *
      ** Access SDCUSTPD using customer.
     C                     MOVEL@CNUM     @KEY1
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ** Customer record not found - Error
     C           @RTCD     IFNE *BLANKS
     C                     Z-ADD8         DBASE            *************
     C                     MOVEL@KEY1     DBKEY            *DB ERROR 08*
     C                     MOVEL'SDCUSTPD'DBFILE           *************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Save Customer Report Name and Customer Town to work fields
     C                     MOVELBBCRTN    @CRTN
     C                     MOVELBBCRNM    @CRNM
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  #BRCH - Access Branch Details file (Internal Customer)       *
      *                                                               *
      *  Called by  : APP7, APP8, APP9.                               *
      *****************************************************************
     C           #BRCH     BEGSR
      *
      ** Access SDBRCHPD using branch.
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           @BRCD   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ** Customer record not found - Error
     C           @RTCD     IFNE *BLANKS
     C                     Z-ADD9         DBASE            *************
     C                     MOVEL@BRCD     DBKEY            *DB ERROR 09*
     C                     MOVEL'SDBRCHPD'DBFILE           *************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Save Customer Report Name and Customer Town to work fields
     C                     MOVELA8BICN    @CNUM
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  APP6 - Format for Nostro Transfers Dealing                   *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           APP6      BEGSR
      *
     C                     MOVE *BLANKS   PFLD
     C                     MOVE *BLANKS   TEMP
      *
      ** Record ID.
     C                     MOVEL'MMCASH,' PFLD
      *
      ** Currency
     C           PFLD      CAT  PUCYDB:0  TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty Name (Customer Report Name)
     C                     MOVELCNUMDB    @CNUM
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Amount
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE PUCYDB    @AJCD
     C                     EXSR #CURR
      *
     C                     Z-ADD@NBDP     ZADEC
     C                     MOVE PUAMDB    ZFIELD
     C                     EXSR #AMNT2
      *
      ** Value Date converted to YYYYMMDD
     C                     Z-ADDVDATDB    ZDAYNO
     C                     EXSR #DATE
      *
     C           PFLD      CAT  DATF2:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Broker, Portfolio, Receive to Name and Receive to City are
      ** all blank.
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Pay from Name (Customer Report Name)
      *
     C                     MOVE PUCYDB    @CCY
      *
      ** Pay to Name (Customer Report Name)
     C           WINSRT    IFEQ 'Y'                        Insert Deal
     C                     MOVELPONSDB    @NOST            ~~~~~~~~~~~
     C                     ELSE                            Reverse Deal
     C                     MOVELRONSDB    @NOST            ~~~~~~~~~~~~
     C                     ENDIF
      *
     C                     EXSR #NOST
      *
     C           CUSTNO    IFNE *BLANKS
     C                     MOVELCUSTNO    @CNUM
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      ** Pay to City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF
      *
      ** Via Name, Via Name, In Favour of Name and In Favour of
      ** City are all blank.
     C           PFLD      CAT  ',,,,,,':0TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Flag
     C           PFLD      CAT  'B,':0    TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Reference Number
     C                     MOVEL'D'       BORNO   7
     C                     MOVE DLNODB    BORNO
      *
     C           PFLD      CAT  BORNO:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Dealer ID.
     C           PFLD      CAT  'NOSTRO':0TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Deal No., Cross Reference, Cancel Reference, Flags and
      ** Other PCW are all blank.
     C           PFLD      CAT  ',,,,,':0 TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     MOVELPFLD      EDATA
     C                     WRITEDLETNTD0
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  APP7 - Format for Nostro Transfers FT                        *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           APP7      BEGSR
      *
     C                     MOVE *BLANKS   PFLD
     C                     MOVE *BLANKS   TEMP
      *
      ** Record ID.
     C                     MOVEL'MMCASH,' PFLD
      *
      ** Currency
     C           PFLD      CAT  CCYNT:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty Name (Internal Customer Report Name)
     C                     MOVE BRCANT    @BRCD
     C                     EXSR #BRCH
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty City (Internal Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Amount
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE CCYNT     @AJCD
     C                     EXSR #CURR
      *
     C                     Z-ADD@NBDP     ZADEC
     C                     MOVE AMNTNT    ZFIELD
      *
     C                     MOVE 'Y'       WINSRT
     C                     EXSR #AMNT2
      *
      ** Value Date converted to YYYYMMDD
     C                     Z-ADDVDATNT    ZDAYNO
     C                     EXSR #DATE
      *
     C           PFLD      CAT  DATF2:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Broker, Portfolio, Receive to Name and Receive to City are
      ** all blank.
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Pay from Name (Customer Report Name)
     C                     MOVE CCYNT     @CCY
      *
      ** Pay to Name (Customer Report Name)
     C                     MOVELACBNNT    @NOST
     C                     EXSR #NOST
      *
     C           CUSTNO    IFNE *BLANKS                    B1
     C                     MOVELCUSTNO    @CNUM
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      ***Pay to City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     ELSE                            X1
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF                           E1
      *
      ** Via Name, Via Name, In Favour of Name and In Favour of
      ** City are all blank.
     C           PFLD      CAT  ',,,,,,':0TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Flag
     C           PFLD      CAT  'B,':0    TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Reference Number
     C           PFLD      CAT  TFRFNT:0  TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Dealer ID.
     C           PFLD      CAT  'NOSTRO':0TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Deal No., Cross Reference, Cancel Reference, Flags and
      ** Other PCW are all blank.
     C           PFLD      CAT  ',,,,,':0 TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     MOVELPFLD      EDATA
     C                     WRITEDLETNTD0
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  APP8 - FT Payments same currency                             *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           APP8      BEGSR
      *
     C                     MOVE *BLANKS   PFLD
     C                     MOVE *BLANKS   TEMP
      *
      ** Record ID.
     C                     MOVEL'MMCASH,' PFLD
      *
      ** Currency
     C           PFLD      CAT  @SMCY:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty Name (Internal Customer Report Name)
     C           TYPE      IFEQ 'OP'
     C                     MOVE BRCAOP    @BRCD
     C                     ELSE
     C                     MOVE BRCAIN    @BRCD
     C                     ENDIF
      *
     C                     EXSR #BRCH
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty City (Internal Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Amount
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE @SMCY     @AJCD
     C                     EXSR #CURR
      *
     C                     Z-ADD@NBDP     ZADEC
     C           TYPE      IFEQ 'OP'
     C                     MOVE SMAMOP    ZFIELD
     C                     MOVE 'N'       WINSRT
     C                     ELSE
     C                     MOVE 'Y'       WINSRT
     C                     MOVE SMAMIN    ZFIELD
     C                     ENDIF
      *
     C                     EXSR #AMNT2
      *
      ** Value Date converted to YYYYMMDD
     C           TYPE      IFEQ 'OP'
     C                     Z-ADDSLDTOP    ZDAYNO
     C                     ELSE
     C                     Z-ADDSLDTIN    ZDAYNO
     C                     ENDIF
      *
     C                     EXSR #DATE
      *
     C           PFLD      CAT  DATF2:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Broker, Portfolio, Receive to Name and Receive to City are
      ** all blank.
     C           TYPE      IFEQ 'OP'
     C           PFLD      CAT  ',,,,':0  TEMP
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     END
     C                     MOVELTEMP      PFLD
      *
      ** Pay from Name (Customer Report Name)
     C                     MOVE *BLANKS   I#ACCT
     C           TYPE      IFEQ 'OP'
     C           DSTTOP    IFEQ 'F'
     C                     MOVELDSTTOP    I#ATYP
     C                     MOVELDST1OP    I#ACCT
     C                     ELSE
     C           SNCTOP    IFEQ 'N'
     C                     MOVELDSTTOP    I#ATYP
     C                     MOVELSNCOOP    I#ACCT
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
     C           SNTPIN    IFEQ 'F'
     C                     MOVELSNTPIN    I#ATYP
     C                     MOVELSND1IN    I#ACCT
     C                     ELSE
     C           RCRTIN    IFEQ 'N'
     C                     MOVELSNTPIN    I#ATYP
     C                     MOVELRCCOIN    I#ACCT
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C           I#ACCT    IFNE *BLANKS
     C                     EXSR #FUNDT
     C                     ELSE
     C                     MOVE *BLANKS   @CNUM
     C                     ENDIF
      *
     C           @CNUM     IFNE *BLANKS
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      ** Pay from City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF
     C           TYPE      IFEQ 'IN'
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     END
      *
      ** Pay to Name (Customer Report Name)
     C                     MOVE *BLANKS   I#ACCT
     C           TYPE      IFEQ 'OP'
     C                     MOVEL*BLANKS   I#ACCT
      *
     C                     ELSE
     C                     MOVEL*BLANKS   I#ACCT
     C                     ENDIF
      *
     C           I#ACCT    IFNE *BLANKS
     C                     EXSR #FUNDT
     C                     ELSE
     C                     MOVE *BLANKS   @CNUM
     C                     ENDIF
      *
     C           @CNUM     IFNE *BLANKS
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      ** Pay to City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF
      *
      ** Via Name, Via Name, In Favour of Name and In Favour of
      ** City are all blank.
     C           PFLD      CAT  ',,,,':0  TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Flag
     C           PFLD      CAT  'B,':0    TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Reference Number
     C           TYPE      IFEQ 'OP'
     C           PFLD      CAT  PREFOP:0  TEMP
     C                     ELSE
     C           PFLD      CAT  PREFIN:0  TEMP
     C                     ENDIF
      *
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Dealer ID.
     C           PFLD      CAT  FTPAY:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Deal No., Cross Reference, Cancel Reference, Flags and
      ** Other PCW are all blank.
     C           PFLD      CAT  ',,,,,':0 TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     MOVELPFLD      EDATA
     C                     WRITEDLETFTD0
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  AP49 - FT Payments cross currency                            *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           APP9      BEGSR
      *
     C                     MOVE *BLANKS   PFLD
     C                     MOVE *BLANKS   TEMP
      *
      ** Record ID.
     C                     MOVEL'FXOUTR,' PFLD
      *
      ** Settlement Currency
     C           PFLD      CAT  @SMCY:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Pay Currency
     C           PFLD      CAT  @PCCY:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty Name (Internal Customer Report Name)
     C           TYPE      IFEQ 'OP'
     C                     MOVE BRCAOP    @BRCD
     C                     ELSE
     C                     MOVE BRCAIN    @BRCD
     C                     ENDIF
      *
     C                     EXSR #BRCH
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty City (Internal Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Settlement Amount
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE @SMCY     @AJCD
     C                     EXSR #CURR
      *
     C                     Z-ADD@NBDP     ZADEC
     C                     Z-ADDA6SPRT    @SPRT  135
     C           TYPE      IFEQ 'OP'
     C                     MOVE SMAMOP    ZFIELD
     C                     ELSE
     C                     MOVE SMAMIN    ZFIELD
     C                     ENDIF
      *
     C                     MOVE 'Y'       WINSRT
     C                     EXSR #AMNT2
      *
      ** Pay Amount
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE @PCCY     @AJCD
     C                     EXSR #CURR
      *
     C                     Z-ADD@NBDP     ZADEC
     C           TYPE      IFEQ 'OP'
     C                     MOVE PYAMOP    ZFIELD
     C                     ELSE
     C                     MOVE PYAMIN    ZFIELD
     C                     ENDIF
      *
     C                     MOVE 'N'       WINSRT
     C                     EXSR #AMNT2
      *
      ** Spot Date - Settlement Value Date converted to YYYYMMDD
     C           TYPE      IFEQ 'OP'
     C                     Z-ADDSLDTOP    ZDAYNO
     C                     ELSE
     C                     Z-ADDSLDTIN    ZDAYNO
     C                     ENDIF
      *
     C                     EXSR #DATE
      *
     C           PFLD      CAT  DATF2:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Pay Value Date converted to YYYYMMDD
     C                     Z-ADD@VDAT     ZDAYNO
     C                     EXSR #DATE
      *
     C           PFLD      CAT  DATF2:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Spot Rate
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVEA*BLANKS   AAMT
     C                     MOVE *BLANKS   SRATE  16
     C                     Z-ADD5         ZADEC
     C                     MOVE @SPRT     ZFIELD
      *
     C                     EXSR ZEDIT
     C                     MOVEAZFIELD    AAMT
     C           ' '       CHECKZFIELD    N
     C                     MOVEAAAMT,N    SRATE
      *
     C           PFLD      CAT  SRATE:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Outright Rate - Exchange rate
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVEA*BLANKS   AAMT
     C                     MOVE *BLANKS   ERATE
     C                     Z-ADD5         ZADEC
     C           TYPE      IFEQ 'OP'
     C                     Z-ADDRATEOP    @RATE  135
     C                     MOVE @RATE     ZFIELD
     C                     ELSE
     C                     Z-ADDRATEIN    @RATE
     C                     MOVE @RATE     ZFIELD
     C                     ENDIF
      *
     C                     EXSR ZEDIT
     C                     MOVEAZFIELD    AAMT
     C           ' '       CHECKZFIELD    N
     C                     MOVEAAAMT,N    ERATE  16
      *
     C           PFLD      CAT  ERATE:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Option Date, Broker, Portfolio, Receive to Name and Receive
      ** to City are all blank.
     C           TYPE      IFEQ 'OP'
     C           PFLD      CAT  ',,,,,':0 TEMP
     C                     ELSE
     C           PFLD      CAT  ',,,':0   TEMP
     C                     END
     C                     MOVELTEMP      PFLD
      *
      ** Pay from Name (Customer Report Name) For OP
      ** Receive To Name (Customer Report Name) For IP
     C                     MOVE *BLANKS   I#ACCT
     C           TYPE      IFEQ 'OP'
     C           DSTTOP    IFEQ 'F'
     C                     MOVELDSTTOP    I#ATYP
     C                     MOVELDST1OP    I#ACCT
     C                     ELSE
     C           DSTTOP    IFEQ 'S'
     C                     MOVELDSTTOP    I#ATYP
     C                     MOVELSNCOOP    I#ACCT
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
     C           SNTPIN    IFEQ 'F'
     C                     MOVELSNTPIN    I#ATYP
     C                     MOVELSND1IN    I#ACCT
     C                     ELSE
     C           SNTPIN    IFEQ 'S'
     C                     MOVELSNTPIN    I#ATYP
     C                     MOVELRCCOIN    I#ACCT
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C* Pay From Blank for INPAYDD
     C           I#ACCT    IFNE *BLANKS
     C                     EXSR #FUNDT
     C                     ELSE
     C                     MOVE *BLANKS   @CNUM
     C                     ENDIF
      *
     C           @CNUM     IFNE *BLANKS
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      ** Pay from City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF
     C           TYPE      IFEQ 'IN'
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     END
      *
      ** Pay to Name (Customer Report Name)
     C                     MOVE *BLANKS   I#ACCT
     C           TYPE      IFEQ 'OP'
     C                     MOVEL*BLANKS   I#ACCT
      *
     C                     ELSE
     C                     MOVEL*BLANKS   I#ACCT
     C                     ENDIF
      *
     C           I#ACCT    IFNE *BLANKS
     C                     EXSR #FUNDT
     C                     ELSE
     C                     MOVE *BLANKS   @CNUM
     C                     ENDIF
      *
     C           @CNUM     IFNE *BLANKS
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      ** Pay to City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF
      *
      ** Via Name, Via Name, In Favour of Name and In Favour of
      ** City are all blank.
     C           PFLD      CAT  ',,,,':0  TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Flag
     C           PFLD      CAT  'B,':0    TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Reference Number
     C           TYPE      IFEQ 'OP'
     C           PFLD      CAT  PREFOP:0  TEMP
     C                     ELSE
     C           PFLD      CAT  PREFIN:0  TEMP
     C                     ENDIF
      *
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Dealer ID.
     C           PFLD      CAT  FTPAY:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Update X Position Flag, Near USD/Cur1 Rate, Near USD Equiv,
      ** Far USD/Cur1 Rate, Far USD Equiv, Deal No., Cross Ref.,
      ** Cancel Ref., Flags, Other PCW, Third Currency and Arbitage
      ** Flag are all blank.
     C           PFLD      CAT  EMPTY:0   TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     MOVELPFLD      EDATA
     C                     WRITEDLETFTD0
      *
     C                     ENDSR
     C*****************************************************************
     C*  APP7 - Format for Nostro Transfers FT                        *
     C*                                                               *
     C*  Called by  : MAIN   - Main Processing Routine                *
     C*****************************************************************
     C           AP10      BEGSR
     C*
     C                     MOVE *BLANKS   PFLD
     C                     MOVE *BLANKS   TEMP
     C*
     C           WINSRT    IFEQ 'N'
     C                     MOVEL'Y'       WINSRT
     C                     ELSE
     C                     MOVEL'N'       WINSRT
     C                     ENDIF
     C*
     C** Record ID.
     C                     MOVEL'MMCASH,' PFLD
     C*
     C** Currency
     C           PFLD      CAT  CCYNT:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Counterparty Name (Internal Customer Report Name)
     C                     MOVE BRCANT    @BRCD
     C                     EXSR #BRCH
     C                     EXSR #ADDR
     C*
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Counterparty City (Internal Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Amount
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE CCYNT     @AJCD
     C                     EXSR #CURR
     C*
     C                     Z-ADD@NBDP     ZADEC
     C                     MOVE AMNTNT    ZFIELD
     C*
     C                     EXSR #AMNT2
     C*
     C** Value Date converted to YYYYMMDD
     C                     Z-ADDVDATNT    ZDAYNO
     C                     EXSR #DATE
     C*
     C           PFLD      CAT  DATF2:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Broker, Portfolio, Receive to Name and Receive to City are
     C** all blank.
     C           PFLD      CAT  ',,,,':0  TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Pay from Name (Customer Report Name)
     C                     MOVE CCYNT     @CCY
     C                     MOVELDESTNT    @NOST
     C                     EXSR #NOST
     C*
     C           CUSTNO    IFNE *BLANKS
     C                     MOVELCUSTNO    @CNUM
     C                     EXSR #ADDR
     C*
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C** Pay from City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C*
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF
     C*
     C           PFLD      CAT  ',,,,':0  TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Back Office Flag
     C           PFLD      CAT  'B,':0    TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Back Office Reference Number
     C           PFLD      CAT  TFRFNT:0  TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Dealer ID.
     C           PFLD      CAT  'NOSTRO':0TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
     C*
     C** Deal No., Cross Reference, Cancel Reference, Flags and
     C** Other PCW are all blank.
     C           PFLD      CAT  ',,,,,':0 TEMP
     C                     MOVELTEMP      PFLD
     C*
     C                     MOVELPFLD      EDATA
     C                     WRITEDLETNTD0
     C*
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *  AP11 - Format for Nostro Transfers Dealing                   *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           AP11      BEGSR
      *
     C                     MOVE *BLANKS   PFLD
     C                     MOVE *BLANKS   TEMP
      *
     C           WINSRT    IFEQ 'N'
     C                     MOVEL'Y'       WINSRT
     C                     ELSE
     C                     MOVEL'N'       WINSRT
     C                     ENDIF
      *
      ** Record ID.
     C                     MOVEL'MMCASH,' PFLD
      *
      ** Currency
     C           PFLD      CAT  PUCYDB:0  TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty Name (Customer Report Name)
     C                     MOVELCNUMDB    @CNUM
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Counterparty City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Amount
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE PUCYDB    @AJCD
     C                     EXSR #CURR
      *
     C                     Z-ADD@NBDP     ZADEC
     C                     MOVE PUAMDB    ZFIELD
     C                     EXSR #AMNT2
      *
      ** Value Date converted to YYYYMMDD
     C                     Z-ADDVDATDB    ZDAYNO
     C                     EXSR #DATE
      *
     C           PFLD      CAT  DATF2:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Broker, Portfolio, Receive to Name and Receive to City are
      ** all blank.
     C           PFLD      CAT  ',,,,':0  TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Pay from Name (Customer Report Name)
      *
     C           WINSRT    IFEQ 'Y'                        Insert Deal
     C                     MOVELRONSDB    @NOST            ~~~~~~~~~~~
     C                     ELSE                            Reverse Deal
     C                     MOVELPONSDB    @NOST            ~~~~~~~~~~~~
     C                     ENDIF
      *
     C                     MOVE PUCYDB    @CCY
     C                     EXSR #NOST
      *
     C           CUSTNO    IFNE *BLANKS
     C                     MOVELCUSTNO    @CNUM
     C                     EXSR #ADDR
      *
     C           PFLD      CAT  @CRNM:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *  Pay from City (Customer Town)
     C           PFLD      CAT  @CRTN:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     ELSE
     C           PFLD      CAT  ',,':0    TEMP
     C                     MOVELTEMP      PFLD
     C                     ENDIF
      *
      ** Via Name, Via Name, In Favour of Name and In Favour of
      ** City are all blank.
     C           PFLD      CAT  ',,,,':0  TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Flag
     C           PFLD      CAT  'B,':0    TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Back Office Reference Number
     C                     MOVEL'D'       BORNO
     C                     MOVE DLNODB    BORNO
      *
     C           PFLD      CAT  BORNO:0   TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Dealer ID.
     C           PFLD      CAT  'NOSTRO':0TEMP
     C                     MOVELTEMP      PFLD
     C           PFLD      CAT  ',':0     TEMP
     C                     MOVELTEMP      PFLD
      *
      ** Deal No., Cross Reference, Cancel Reference, Flags and
      ** Other PCW are all blank.
     C           PFLD      CAT  ',,,,,':0 TEMP
     C                     MOVELTEMP      PFLD
      *
     C                     MOVELPFLD      EDATA
     C                     WRITEDLETNTD0
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *  #LINIT - Initial Processing                                  *
      *                                                               *
      *  Called by  : MAIN   - Main Processing Routine                *
      *****************************************************************
     C           #LINIT    BEGSR
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'DL0011'  DBPGM
     C                     OUT  LDA
      *
      ** Key for CHAIN to 'A' or 'B' record from LF/LOAMS
      *
     C           CLONKY    KLIST
     C**********           KFLD           LNRFK   60                                          CLE148
     C                     KFLD           LNRFK   6                                           CLE148
     C                     KFLD           RCDTK   1
      *
      ** Initialise work fields
      *
     C                     MOVEL*BLANKS   WFCYL   1
      *
      ** If First Cycle
      *
     C           WFCYL     IFEQ *BLANKS                    B1
      *
      ** Access the Bank Details.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS                    B2
     C           *LOCK     IN   LDA
     C                     MOVEL'9'       CALLCD
     C                     Z-ADD1         DBASE            *************
     C                     MOVEL'SDBANKPD'DBFILE           *DB ERROR 01*
     C                     MOVEL'*FIRST'  DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             E2
      *
      ** Access Module Setting Details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
     C           @RTCD     IFNE *BLANKS                    B2
     C           *LOCK     IN   LDA
     C                     MOVEL'9'       CALLCD
     C                     Z-ADD2         DBASE            *************
     C                     MOVEL'SDMMODPD'DBFILE           *DB ERROR 02*
     C                     MOVEL'*FIRST'  DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             E2
      *
      ** Check if Customer Lending module used within the system
      *
     C           BGCSLN    IFEQ 'Y'                        B2
      *
      ** Access Customer Lending Details
      *
     C                     CALL 'AOCLNDR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDCLND    PARM SDCLND    DSFDY                                               CLE134
     C           SDCLND    PARM SDCLND    DSSDY                                               CLE134
      *
     C           @RTCD     IFNE *BLANKS                    B3
     C           *LOCK     IN   LDA
     C                     MOVEL'9'       CALLCD
     C                     Z-ADD3         DBASE            *************
     C                     MOVEL'SDCLNDPD'DBFILE           *DB ERROR 03*
     C                     MOVEL'*FIRST'  DBKEY            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             E3
     C                     END                             E2
      *
     C           BJRDNB    ADD  30        WCDAT1  50
     C                     MOVEL'Y'       WFCYL
      *
     C                     END                             E1
      *
      **  Check System Date Format, DDMMYY (*IN98 off)
      **                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVEL'1'       *IN98
     C                     END
      *****************************************************************            MD035545 MD047993
      ***Call*access*program*for*General*Ledger*details.***************            MD035545 MD047993
      *****************************************************************            MD035545 MD047993
     C**********           CALL 'AOGELRR1'                                         MD035545 MD047993
     C**********           PARM '*MSG   ' @RTCD   7                                MD035545 MD047993
     C**********           PARM '*FIRST ' @OPTN   7                                MD035545 MD047993
     C********** SDGELR    PARM SDGELR    DSSDY                                    MD035545 MD047993
      *                                                                                     MD035545
     C********** @RTCD     IFNE *BLANK                                             MD035545 MD047993
     C**********           MOVEL'SDGELRPD'DBFILE                                   MD035545 MD047993
     C**********           MOVEL'062'     DBASE                                    MD035545 MD047993
     C**********           MOVEL@OPTN     DBKEY                                    MD035545 MD047993
     C**********           EXSR *PSSR                                              MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
      *                                                                                     MD035545
      ** Retrieve system value setting                                                      MD035545
      *                                                                                     MD035545
     C                     MOVELWPARML    P@OP01                                            MD035545
     C                     CALL 'AOSVALR0'                                                  MD035545
     C                     PARM *BLANKS   W0RTCD  7                                         MD035545
     C                     PARM           P@OP01 20                                         MD035545
     C                     PARM           P@VL01200                                         MD035545
     C                     PARM           P@OP02 20                                         MD035545
     C                     PARM           P@VL02200                                         MD035545
     C                     PARM           P@OP03 20                                         MD035545
     C                     PARM           P@VL03200                                         MD035545
     C                     PARM           P@OP04 20                                         MD035545
     C                     PARM           P@VL04200                                         MD035545
     C                     PARM           P@OP05 20                                         MD035545
     C                     PARM           P@VL05200                                         MD035545
     C                     PARM           P@OP06 20                                         MD035545
     C                     PARM           P@VL06200                                         MD035545
     C                     PARM           P@OP07 20                                         MD035545
     C                     PARM           P@VL07200                                         MD035545
     C                     PARM           P@OP08 20                                         MD035545
     C                     PARM           P@VL08200                                         MD035545
     C                     PARM           P@OP09 20                                         MD035545
     C                     PARM           P@VL09200                                         MD035545
     C                     PARM           P@OP10 20                                         MD035545
     C                     PARM           P@VL10200                                         MD035545
      *                                                                                     MD035545
      ** If system value is not found then default value to 'N'                             MD035545
      *                                                                                     MD035545
     C           W0RTCD    IFNE '       '                                                   MD035545
     C                     MOVE 'N'       WCALC6                                            MD035545
     C                     ELSE                                                             MD035545
     C                     MOVELP@VL01    WCALC6                                            MD035545
     C********** WCALC6    IFEQ 'Y'                                                MD035545 MD047993
     C********** BKALDI    ANDEQ'Y'                                                MD035545 MD047993
     C**********           MOVE 'Y'       WCALC6                                   MD035545 MD047993
     C**********           ELSE                                                    MD035545 MD047993
     C**********           MOVE 'N'       WCALC6                                   MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
     C                     ENDIF                                                            MD035545
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      * Calls: None                                                   *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     OPEN DL0011AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSEDL0011AU
      *
     C                     DUMP
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
     C/COPY ZSRSRC,ZFWDT
      /EJECT
     C/COPY ZSRSRC,ZACCH
      /EJECT
     C/COPY ZSRSRC,GLINTCZ1
      /EJECT
     C/COPY ZSRSRC,ZSAVEZ1
      /EJECT
     C/COPY ZSRSRC,ZRSTORZ1
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z4
      /EJECT
     C/COPY ZSRSRC,ZFREQBZ2
      /EJECT
     C/COPY ZSRSRC,ZINTDYZ2
      /EJECT
     C/COPY ZSRSRC,ZDATE1Z2
      /EJECT
     C/COPY ZSRSRC,ZCHKH
      /EJECT
     C/COPY ZSRSRC,ZDATE9Z3
      /EJECT
     C/COPY ZSRSRC,ZEDITZ2
      /EJECT
     I*--------------------------------------------------------------
     I* THIS SECTION IS ADDED TO THE PGM DUE TO DBA2 PTF06 UPGRADE
     I/COPY ZSRSRC,ZDAT10Z2
      /EJECT
     I*--------------------------------------------------------------
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZFMD  -  NUMBER OF DAYS IN THE MONTH
312831303130313130313031
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
