     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Calculate default country limits')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Dealing Module
     F*                                                                  *
     F*       DL0132 - CALC DEFAULT CTY  LIMIT                           *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. LN0365             Date 13JUN90               *
     F*                 LN0364             Date 13JUN90               *
     F*                                                                  *
     F*****************************************************************
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*     LN0365 - IF SYSTEM LEVEL REPORT REQUESTED AND NO LIMIT    *   LN0365
     F*              EXISTS, DEFAULT LIMIT NOT BEING CALCULATED FOR   *   LN0365
     F*              FRA/IRS                                          *   LN0365
     F*                                                               *
     F*     LN0364 - DEFAULT COMPANY LIMIT NOT BEING WORKED           *   LN0364
     F*              OUT CORRECTLY FOR FRA/IRS                        *   LN0364
     F*                                                               *
     F********************************************************************
     FSDCOMPL1IF  E           K        DISK
     FSDBRCHL3IF  E           K        DISK
     FLIMITJA IF  E           K        DISK
     E                    CPY@    1   1 80
     I**   DATA STRUCTURES TO LINK FLEP/LLEP, FLPC/LLPC & INPUT FIELDS
     I            DS
     I                                        1  18 FLEP
     I                                        1   3 FLEP1
     I                                        4   60FLEP2
     I                                        7   90FLEP3
     I                                       10  120FLEP4
     I                                       13  150FLEP5
     I                                       16  180FLEP6
     I            DS
     I                                        1  18 LLEP
     I                                        1   3 LLEP1
     I                                        4   60LLEP2
     I                                        7   90LLEP3
     I                                       10  120LLEP4
     I                                       13  150LLEP5
     I                                       16  180LLEP6
     I            DS
     I                                        1  18 FLPC
     I                                        1   30FLPC1
     I                                        4   60FLPC2
     I                                        7   90FLPC3
     I                                       10  120FLPC4
     I                                       13  150FLPC5
     I                                       16  180FLPC6
     I            DS
     I                                        1  18 LLPC
     I                                        1   30LLPC1
     I                                        4   60LLPC2
     I                                        7   90LLPC3
     I                                       10  120LLPC4
     I                                       13  150LLPC5
     I                                       16  180LLPC6
     C*****************************************************************
     C                     MOVEACPY@      BIS@   80
     C           *ENTRY    PLIST
     C                     PARM           CCTZ    2
     C                     PARM           LEVEL   1
     C                     PARM           ENTITY  3
     C                     PARM           FX      1
     C                     PARM           LN      1
     C                     PARM           FRA     1
     C                     PARM           IRS     1
     C                     PARM           FXLIM
     C                     PARM           LNLIM
     C                     PARM           FRLIM
     C                     PARM           IRLIM
     C                     PARM           FXSAME
     C                     PARM           LNSAME
     C                     PARM           FXPER
     C                     PARM           LNPER
     C                     PARM           FXPC
     C                     PARM           LNPC
     C                     PARM           RUND    50
     C** INITIALISE OUTPUT FIELDS
     C                     Z-ADD0         FXLIM   90
     C                     Z-ADD0         LNLIM   90
     C                     Z-ADD0         FRLIM   90
     C                     Z-ADD0         IRLIM   90
     C                     MOVE 'Y'       FXSAME  1
     C                     MOVE 'Y'       LNSAME  1
     C                     MOVE *BLANKS   FXPER  18
     C                     MOVE *BLANKS   LNPER  18
     C                     MOVE *BLANKS   FXPC   18
     C                     MOVE *BLANKS   LNPC   18
     C                     MOVE *BLANKS   FLEPX  18
     C                     MOVE *BLANKS   LLEPX  18
     C                     MOVE *BLANKS   FLPCX  18
     C                     MOVE *BLANKS   LLPCX  18
     C** SET FIRST TIME INDS FX,LN
     C                     SETON                     9091
     C** SET UP KEY FOR LIMITJA
     C           KEY       KLIST
     C                     KFLD           CCTZ    2
     C                     KFLD           LVL     1
     C                     KFLD           ENT     3
     C** IF CO LEVEL
     C           LEVEL     IFEQ 'C'
     C                     MOVE ENTITY    CMPYX   3
     C                     MOVE FX        FX2
     C                     MOVE LN        LN2
     C                     MOVE FRA       FRA2                            LN0365
     C                     MOVE IRS       IRS2                            LN0365
     C                     EXSR DEFC
     C                     END
     C** IF SYS LEVEL
     C           LEVEL     IFEQ 'S'
     C                     EXSR DEFS
     C                     END
     C**
     C** USE LIMIT PERIOD OF LAST RECORD READ IF SAME LIMIT TYPE FOUND
     C           FXSAME    IFEQ 'Y'
     C                     MOVE FLEPX     FXPER
     C                     MOVE FLPCX     FXPC
     C                     END
     C           LNSAME    IFEQ 'Y'
     C                     MOVE LLEPX     LNPER
     C                     MOVE LLPCX     LNPC
     C                     END
     C**
     C** IF NONE FOUND, MIDAS DEFINES DEFAULTS
     C   90                MOVE 'N'       FXSAME
     C   91                MOVE 'N'       LNSAME
     C**
     C                     RETRN
     C**
     C*****************************************************************
     C**  DEFC SR TO CALC DEFAULT COMPANY LIMIT
     C           DEFC      BEGSR                           ** DEFC **
     C** READ BRANCH FILE VIA COMPANY OF BRANCH
     C           CMPYX     SETLLSDBRCHL3
     C** FOR EACH BR IN CO
     C           CMPYX     DOUNEA8CMCD
     C** ACCESS BRANCH
     C                     MOVE '   '     A8CMCD
     C                     READ SDBRCHL3                 99
     C           CMPYX     CABNEA8CMCD    DEFC9
     C** ACCESS BR LIMIT
     C                     MOVE 'B'       LVL
     C                     MOVE A8BRCD    ENT
     C                     SETOF                     99
     C           KEY       CHAINLIMITJA              99
     C** IF FOUND
     C           *IN99     IFNE '1'
     C** IF FX REQD
     C           FX2       IFEQ 'Y'
     C           DXFXLT    IFNE *BLANK
     C** IF LIVE LIMIT
     C           DXFLED    IFGT RUND
     C           DXFLED    OREQ 0
     C** IF FIRST TIME, SAVE LIMIT TYPE FOR COMPARISON
     C           *IN90     IFEQ '1'
     C                     SETOF                     90
     C                     MOVE DXFXLT    SAVF    3
     C                     ELSE
     C** ELSE SEE IF LIMIT TYPE HAS CHANGED
     C                     EXSR SFX
     C                     END
     C** IF FX LIMIT EXISTS, CALC LIMIT
     C           FLPC1     DIV  100       PCENT   32
     C           DXFLAM    MULT PCENT     DXFLAM
     C                     ADD  DXFLAM    FXLIM
     C                     MOVE FLEP      FLEPX
     C                     MOVE FLPC      FLPCX
     C                     END
     C                     END
     C                     END
     C** IF LN REQD
     C           LN2       IFEQ 'Y'
     C           DXLLTY    IFNE *BLANK
     C** IF LIVE LIMIT
     C           DXLLED    IFGT RUND
     C           DXLLED    OREQ 0
     C** IF FIRST TIME, SAVE LIMIT TYPE FOR COMPARISON
     C           *IN91     IFEQ '1'
     C                     SETOF                     91
     C                     MOVE DXLLTY    SAVL    3
     C                     ELSE
     C** ELSE SEE IF LIMIT TYPE HAS CHANGED
     C                     EXSR SLN
     C                     END
     C** IF LN LIMIT EXISTS, CALC LIMIT
     C           LLPC1     DIV  100       PCENT
     C           DXLLAM    MULT PCENT     DXLLAM
     C                     ADD  DXLLAM    LNLIM
     C                     MOVE LLEP      LLEPX
     C                     MOVE LLPC      LLPCX
     C                     END
     C                     END
     C                     END
     C**
     C**
     C** IF FRA REQD
     C           FRA2      IFEQ 'Y'
     C** IF LIVE LIMIT
     C           DXFRED    IFGT RUND
     C           DXFRED    OREQ 0
     C**********           MOVE DXFRLA    FRLIM                           LN0364
     C                     ADD  DXFRLA    FRLIM                           LN0364
     C                     END
     C                     END
     C**
     C**
     C** IF IRA REQD
     C           IRS2      IFEQ 'Y'
     C** IF LIVE LIMIT
     C           DXIRED    IFGT RUND
     C           DXIRED    OREQ 0
     C**********           MOVE DXIRLM    IRLIM                           LN0364
     C                     ADD  DXIRLM    IRLIM                           LN0364
     C                     END
     C                     END
     C**
     C                     END
     C                     END
     C           DEFC9     ENDSR                           ** DEFC9 **
     C*****************************************************************
     C**  DEFS SR TO CALC DEFAULT SYSTEM LIMIT
     C           DEFS      BEGSR                           ** DEFS **
     C**
     C           *LOVAL    SETLLSDCOMPL1
     C**
     C           *IN97     DOUEQ'1'
     C** READ COMPANY FILE
     C                     READ SDCOMPL1                 97
     C           *IN97     CABEQ'1'       DEFS9
     C** IF LIVE CO
     C                     MOVE ' '       FX2     1
     C                     MOVE ' '       LN2     1
     C                     MOVE ' '       FRA2    1                       LN0365
     C                     MOVE ' '       IRS2    1                       LN0365
     C** ACCESS CO LIMIT
     C                     MOVE 'C'       LVL
     C                     MOVE APCMCD    ENT
     C                     SETOF                     99
     C           KEY       CHAINLIMITJA              99
     C** IF FOUND
     C           *IN99     IFNE '1'
     C** IF FX REQD
     C           FX        IFEQ 'Y'
     C           DXFXLT    IFNE *BLANK
     C** IF LIVE LIMIT
     C           DXFLED    IFGT RUND
     C           DXFLED    OREQ 0
     C** IF FIRST TIME, SAVE LIMIT TYPE FOR COMPARISON
     C           *IN90     IFEQ '1'
     C                     SETOF                     90
     C                     MOVE DXFXLT    SAVF
     C                     ELSE
     C** ELSE SEE IF LIMIT TYPE HAS CHANGED
     C                     EXSR SFX
     C                     END
     C** IF FX LIMIT EXISTS, CALC LIMIT
     C           FLPC1     DIV  100       PCENT   32
     C           DXFLAM    MULT PCENT     DXFLAM
     C                     ADD  DXFLAM    FXLIM
     C                     MOVE FLEP      FLEPX
     C                     MOVE FLPC      FLPCX
     C                     END
     C                     ELSE
     C** ELSE  GET CO DEFAULT
     C                     MOVE 'Y'       FX2
     C                     END
     C                     END
     C** IF LN REQD
     C           LN        IFEQ 'Y'
     C           DXLLTY    IFNE *BLANK
     C** IF LIVE LIMIT
     C           DXLLED    IFGT RUND
     C           DXLLED    OREQ 0
     C** IF FIRST TIME, SAVE LIMIT TYPE FOR COMPARISON
     C           *IN91     IFEQ '1'
     C                     SETOF                     91
     C                     MOVE DXLLTY    SAVL
     C                     ELSE
     C** ELSE SEE IF LIMIT TYPE HAS CHANGED
     C                     EXSR SLN
     C                     END
     C** IF LN LIMIT EXISTS, CALC LIMIT
     C           LLPC1     DIV  100       PCENT
     C           DXLLAM    MULT PCENT     DXLLAM
     C                     ADD  DXLLAM    LNLIM
     C                     MOVE LLEP      LLEPX
     C                     MOVE LLPC      LLPCX
     C                     END
     C                     ELSE
     C** ELSE  GET CO DEFAULT
     C                     MOVE 'Y'       LN2
     C                     END
     C**
     C                     END
     C**
     C** IF FRA REQD
     C           FRA       IFEQ 'Y'
     C***IF*LIVE*LIMIT***                                                 LN0365
     C** IF LIMIT EXISTS AND IS LIVE UPDATE LIMIT TOTAL                   LN0365
     C           DXFRLA    IFGT 0                                         LN0365
     C           DXFRED    IFGT RUND
     C           DXFRED    OREQ 0
     C**********           MOVE DXFRLA    FRLIM                           LN0364
     C                     ADD  DXFRLA    FRLIM                           LN0364
     C                     END                                            LN0365
     C** ELSE  GET CO DEFAULT                                             LN0365
     C                     ELSE                                           LN0365
     C                     MOVE 'Y'       FRA2                            LN0365
     C                     END
     C                     END
     C**
     C**
     C** IF IRA REQD
     C           IRS       IFEQ 'Y'
     C***IF*LIVE*LIMIT***                                                 LN0365
     C** IF LIMIT EXISTS AND IS LIVE UPDATE LIMIT TOTAL                   LN0365
     C           DXIRLM    IFGT 0                                         LN0365
     C           DXIRED    IFGT RUND
     C           DXIRED    OREQ 0
     C**********           MOVE DXIRLM    IRLIM                           LN0364
     C                     ADD  DXIRLM    IRLIM                           LN0364
     C                     END                                            LN0365
     C** ELSE  GET CO DEFAULT                                             LN0365
     C                     ELSE                                           LN0365
     C                     MOVE 'Y'       IRS2                            LN0365
     C                     END
     C                     END
     C**
     C                     ELSE
     C                     MOVE FX        FX2
     C                     MOVE LN        LN2
     C                     MOVE FRA       FRA2                            LN0365
     C                     MOVE IRS       IRS2                            LN0365
     C                     END
     C**
     C***IF*NOT*FOUND**OR*FX2*=Y**ON*LN2=Y**GET*DFT*FOR*THIS*CO**         LN0365
     C** IF NOT FOUND, OR FX2 =Y, OR LN2=Y, OR FRA2 =Y, OR IRS2 =Y        LN0365
     C** THEN GET DEFAULT FOR THIS COMPANY                                LN0365
     C           *IN99     IFEQ '1'
     C           FX2       OREQ 'Y'
     C           LN2       OREQ 'Y'
     C           FRA2      OREQ 'Y'                                       LN0365
     C           IRS2      OREQ 'Y'                                       LN0365
     C                     MOVE APCMCD    CMPYX
     C                     EXSR DEFC
     C                     END
     C                     END
     C**
     C           DEFS9     ENDSR                           ** DEFS9 **
     C*****************************************************************
     C**  SFX  SR TO CHECK IF FX LIMIT TYPE HAS CHANGED
     C           SFX       BEGSR                           ** SFX  **
     C**
     C           FXSAME    IFEQ 'Y'
     C           DXFXLT    COMP SAVF                 9999
     C   99                MOVE 'N'       FXSAME
     C                     SETOF                     99
     C                     END
     C                     ENDSR
     C*****************************************************************
     C**  SLN  SR TO CHECK IF LN LIMIT TYPE HAS CHANGED
     C           SLN       BEGSR                           ** SLN  **
     C**
     C           LNSAME    IFEQ 'Y'
     C           DXLLTY    COMP SAVL                 9999
     C   99                MOVE 'N'       LNSAME
     C                     SETOF                     99
     C                     END
     C                     ENDSR
     C*****************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
