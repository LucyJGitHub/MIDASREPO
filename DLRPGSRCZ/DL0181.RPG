     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Deal amendments by deal enquiry')             *
      *===============================================================*
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0181 - DEALS AMENDMENTS BY DEAL ENQUIRY                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL099             Date 06Oct17               *
      *  Prev Amend No. CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 245692             Date 09Dec13               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL033             Date 04Jul05               *
      *                 221673             Date 04Jul05               *
      *                 CSW037A            Date 02May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BG2918             Date 26May04               *
      *                 CLE025             Date 20Oct03               *
      *                 BG2171             Date 01Sep03               *
      *                 CDL022             Date 03Feb04               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 214209             Date 17Feb03               *
      *                 CDL010             Date 02Aug02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 23Aug96               *
      *                 047815             Date 19Jun93               *
      *                 S01311             DATE 01FEB91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhanced Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  245692 - Incorrect validation of rates when the decimal se-  *
      *           parator in the Dealing ICD was defined as "," ins-  *
      *           tead of ".".                                        *
      *         - Applied for MD-23981.                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL033 - Floating Rate CDs Issued.                           *
      *  221673 - Display negative sign for SC amendment.             *
      *           Applied fix 220497.                                 *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BG2918 - Text for deal amendment type EM/BC is incorrect.    *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  BG2171 - Recompilations omitted by BUG1832                   *
      *  CDL022 - Deal Amendment Changes (Recompile)                  *
      *  CDL020 - Apply Base Rate at Value Date (Recompile)           *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *           (Recompile)                                         *
      *  214209 - Not all details are displayed when either Page Up or*
      *           Page Down.                                          *
      *  CDL010 - Prevent last user that actioned the trade from      *
      *           authorising it.Recompile due to changes in MMDEAMPP.*
      *  CDL006 - Dealing Fiduciary.                                  *
      *  S01408 - Core hooks DL0181CCP1 added                         *
      *         - Core hooks DL0181CCP2 added                         *
      *         - Core hooks DL0181CCP3 added                         *
      *         - Core hooks DL0181EEP1 added                         *
      *         - Core hooks DL0181EEP2 added                         *
      *         - Core hooks DL0181FFP1 added                         *
      *         - Core hooks DL0181IIP1 added                         *
      *         - Core hooks DL0181IIP2 added                         *
      *  047815 - Deal amendments entered but not yet authorised are  *
      *           not shown in enquiry.                               *
      *           FIX: Use MMDEAMLL instead of DEAMS                  *
      *           REOPEN DETAILS: Consider deal amendment type for    *
      *           outputting base rate code; Return 'M' to DL0170 if  *
      *           no deal amendments for deal.                        *
      *  S01311 - Program re-written For Exposure Management and      *
      *           Dealing linked enquiries. Replaces Dl0180.          *
      *                                                               *
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  KA -  Return to original calling program                     *
      *  KC -  Exit                                                   *
      *  KL -  Return to previous screen                              *
      *                                                               *
      *  10 -  Error on screen 1                                      *
      *                                                               *
      *  20 -  Error on subfile screen                                *
      *  21 -  Display subfile control record                         *
      *  22 -  Display subfile record                                 *
      *  23 -  End of deal amendments for a deal                      *
      *  24 -  Rollup                                                 *
      *  25 -  Rolldown                                               *
      *                                                               *
      *  34 -  Deal type narrative found in table                     *
      *                                                               *
      *  40 -  Narrative found in table                               *
      *  41 -  Amendment type = 'SC'                                  *
      *                                                               *
      *  50 -  Amendment is reversed                                  *
      *  51 - Dealing Fiduciary, used to hide fields on subfile       *   CDL006
      *  52 -  Deal amendment found for deal                          *
      *  53 -  Chain failure on deals file                            *
      *  54 -  End of DEAMS file                                      *
      *  55 -  Beginning of DEAMS file                                *
      *  56 - Dealing Fiduciary is switched ON                        *   CDL006
      *                                                               *
      *  60 -  Program called as a linked enquiry                     *
      *                                                               *
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
     FDL0181FMCF  E                    WORKSTN      KINFSR *PSSR
     F                                        RCDNBRKSFILE DL0181SF
     FDEALS   IF  E           K        DISK         KINFSR *PSSR
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     F*DEAMS   IF  E           K        DISK         KINFSR *PSSR         047815
     FMMDEAMLLIF  E           K        DISK         KINFSR *PSSR          047815
     F/COPY WNCPYSRC,DL0181FFP1                                           S01408
      *
     E*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *
     E                    DMK        50 14               AMEND TYPE TABLE
      /COPY ZSRSRC,ZSEDITZ1
      /COPY ZSRSRC,ZRATE2Z1
      /COPY ZSRSRC,ZDATE2Z1
     E                    CPY@    1   1 80
     E                    MSSG    1   6 55               MESSAGES ARRAY
     E***********         TABCOD 16  16  2               DEAL TYPE CODES  CDL006
     E***********         TABNAR  2  16 30               DEAL TYPE NARRS. CDL006
     E                    TABCOD 22  22  2               Deal type code   CDL006
     E                    TABNAR  2  22 30               Deal type narr   CDL006
     E/COPY WNCPYSRC,DL0181EEP1                                           S01408
     E***                 TABAMC  1   5  2   TABTYP 23   AMEND TYPE TABLE                     BG2918
     E                    TABAMC  1   7  2   TABTYP 23   AMEND TYPE TABLE                     BG2918
     E/COPY WNCPYSRC,DL0181EEP2                                           S01408
     E                    @YD     4   4  5 0A                             047815
     E*                                                                   047815
     E**  Array for SR. ZA0680 - Cumulative days in year                  047815
     E**  for 4 year period                                               047815
     E*                                                                   047815
     E                    @MD    13  13  5 0A                             047815
     E*                                                                   047815
     E**  Array for SR. ZA0680 - Cumulative days in year per month        047815
     E*                                                                   047815
      *
     I*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
     I/COPY WNCPYSRC,DL0181IIP1                                           S01408
     I*                                                                   047815
     IMMDEAMP0                                                            047815
     I              HNDA38                          DLNO                  047815
     I              HNCCY                           CCY                   047815
     I              HNDS38                          DASN                  047815
     I              HNMTYP                          AMTP                  047815
     I              HNAMNP                          DAAM                  047815
     I              HNINTR                          RTSP                  047815
     I              HNCNUM                          CNUM                  047815
      *                                                                   CDL006
     ISCSARD    E DSSCSARDPD                                              CDL006
      ** External DS for SAR details                                      CDL006
      *                                                                   CDL006
     I/COPY WNCPYSRC,DL0181IIP2                                           S01408
      *
     ISDBANK    E DSSDBANKPD
      * Bank details accessed via access program
      *
     ISDCURR    E DSSDCURRPD
      * Currency details accessed via access program
      *
     ISDCUST    E DSSDCUSTPD
      * Customer details accessed via access program
      *
     ISDDEAL    E DSSDDEALPD                                                                  245692
      ** External DS for DEALING Details                                                      245692
      *                                                                                       245692
     IDSFDY     E DSDSFDY
      * First DS for access programs - Short data structure
      *
     IDSSDY     E DSDSSDY
      * Second DS for access programs - Long data Structure
      *
     IPSDS       SDS
      *  Retrieve WSID from program status data structure.
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     IRUNDAT      DS
      *  Data structure for multibranch indicator using RUNDAT.
     I                                       13  13 @MBIN
     IDLSDS     E DSDLDTA
      *  Data structure for linked enquiry
      *
     IDBERR       DS                            256
      * Data Structure used in Database Error Processing
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     I            DS
     I                                        1  14 SAVKY
     I                                        1   6 SDEAL
     I****************************************7  110VDAT                  047815
     I                                        7  110@VDAT                 047815
     I                                       12  140DASN
     I*                                                                   047815
     I            DS                                                      047815
     I*                                                                   047815
     I**  Data structure for date in, to change format.                   047815
     I*                                                                   047815
     I                                        1   80@@DTIN                047815
     I                                        3   40@@YY                  047815
     I                                        5   60@@MM                  047815
     I                                        7   80@@DD                  047815
     I                                        1   40@@YYYY                047815
     I                                        1   20@@CC                  047815
     I                                        5   60@@MTH                 047815
     I                                        7   80@@DAY                 047815
      *
     I/COPY WNCPYSRC,DL0181I001
      /COPY ZSRSRC,ZSEDITZ2
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
     C                     MOVEACPY@      BIS@   80
      *
     C           DEAMKY    KLIST
     C                     KFLD           NDEAL
     C***********          KFLD           VALK                            047815
     C                     KFLD           VAYY                            047815
     C                     KFLD           VAMM                            047815
     C                     KFLD           VADD                            047815
     C                     KFLD           DANK
      *
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  MAIN     - MAIN CONTROL PROCESSING                           *
      *                                                               *
      *  Calls      INITL    - First Call to Program Processing       *
      *             SCRN1    - Screen 1 processing                    *
      *             SCRN2    - Screen 2 processing                    *
      *             CMD      - Command key processing                 *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C/COPY WNCPYSRC,DL0181C003
     C           FIRST     IFNE 'N'
     C                     EXSR INITL
     C                     MOVE 'N'       FIRST   1
     C                     END
      *
     C           START     TAG
      *
      *  Only display screen 1 if not a linked enquiry
      *
     C           FXMMDL    IFEQ *BLANKS
     C                     EXSR SCRN1
     C                     END
      *
     C                     EXSR SCRN2
     C                     EXSR CMD
      *
     C           *INKL     IFEQ '1'
     C                     GOTO START
     C                     END
      *
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  INITL    - FIRST TIME IN PROCESSING                          *
      *                                                               *
      *  Called by  MAIN     - Main Control Processing                *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           INITL     BEGSR
      *
      *  Get multi branch indicator from data area - RUNDAT
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           *NAMVAR   DEFN           DLSDS
     C                     IN   DLSDS
      *
      *  Retrieve bank details using access program.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      *  Indicator 98 seton if date format indicator = M
      *
     C           BJDFIN    COMP 'M'                      98
      *                                                                                       245692
      *  Retrieve DEALING ICD details                                                         245692
      *                                                                                       245692
     C                     CALL 'AODEALR1'                                                    245692
     C                     PARM '*DBERR ' @RTCD   7                                           245692
     C                     PARM '*FIRST ' @OPTN   7                                           245692
     C           SDDEAL    PARM SDDEAL    DSFDY                                               245692
      *
      * Set up edit code for ZSEDIT
      *
     C                     MOVE 'J'       ZECODE
      *
      * If linked enquiry retrieve deal number from data area
      *
     C                     MOVE RQDTD     FXMMDL  2
     C           FXMMDL    IFNE *BLANKS
     C                     SETON                     60
     C                     MOVELRQDTD     DEAL
     C                     END
      *                                                                   CDL006
      ** Check if Dealing Fiduciary is switched ON                        CDL006
      *                                                                   CDL006
     C                     CALL 'AOSARDR0'                                CDL006
     C                     PARM *BLANKS   WRTCD   7                       CDL006
     C                     PARM '*VERIFY' WOPTN   7                       CDL006
     C                     PARM 'CDL006'  WSARD   6                       CDL006
     C           SCSARD    PARM SCSARD    DSFDY                           CDL006
     C           WRTCD     IFEQ *BLANKS                                   CDL006
     C                     MOVE 'Y'       CDL006  1                       CDL006
     C                     MOVE *ON       *IN56                           CDL006
     C                     ELSE                                           CDL006
     C           WRTCD     IFNE '*NRF   '                                 CDL006
     C                     MOVEL'SCSARDPD'DBFILE           ************   CDL006
     C                     MOVEL'001'     DBASE            * DBERR 01 *   CDL006
     C                     MOVEL'CDL006'  DBKEY            ************   CDL006
     C                     EXSR *PSSR                                     CDL006
     C                     ENDIF                                          CDL006
     C                     MOVE 'N'       CDL006                          CDL006
     C                     MOVE *OFF      *IN56                           CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C/COPY WNCPYSRC,DL0181CCP3                                           S01408
      *
      *                                                                                       CER043
      ** Access AOSARDR0 and check if CER043 is installed                                     CER043
      *                                                                                       CER043
     C                     CALL 'AOSARDR0'                                                    CER043
     C                     PARM *BLANKS   PRTCD   7                                           CER043
     C                     PARM '*VERIFY' POPTN   7                                           CER043
     C                     PARM 'CER043'  PSARD   6                                           CER043
     C           SCSARD    PARM SCSARD    DSFDY                                               CER043
      *                                                                                       CER043
     C           PRTCD     IFEQ *BLANKS                                                       CER043
     C                     MOVEL'Y'       CER043  1                                           CER043
     C                     MOVE *OFF      *IN57                                               CER043
     C                     ELSE                                                               CER043
     C                     MOVEL'N'       CER043                                              CER043
     C                     MOVE *ON       *IN57                                               CER043
     C                     ENDIF                                                              CER043
      *                                                                                       CER043
     C           PRTCD     IFNE *BLANKS                                                       CER043
     C           PRTCD     ANDNE'*NRF    '                                                    CER043
     C           *LOCK     IN   @LDA                                                          CER043
     C                     MOVE 'SCSARDPD'DBFILE                                              CER043
     C                     MOVEL'002'     DBASE                                               CER043
     C                     MOVEL'CER043'  DBKEY                                               CER043
     C                     OUT  @LDA                                                          CER043
     C                     EXSR *PSSR                                                         CER043
     C                     ENDIF                                                              CER043
      *                                                                                       CER043
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  SCRN1    - SCREEN 1 PROCESSING                               *
      *                                                               *
      *  Called by  MAIN     - Main Control Processing                *
      *                                                               *
      *  Calls      DEAL     - Validates Deal                         *
      *             CUST     - Retrieves customer deal details        *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           SCRN1     BEGSR
      *
     C                     SETON                     90
     C                     EXFMTDL0181F1
      *
     C           *INKC     IFEQ '1'
     C                     SETON                     LR
     C                     RETRN
     C                     END
      *
      *  Validate deal entered
      *
     C                     EXSR VALD
     C                     EXSR CUST
      *
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  SCRN2    - SCREEN 2 PROCESSING                               *
      *                                                               *
      *  Calls      LOAD     - Load Subfile                           *
      *             VALD     - Validate deal entered                  *
      *             CUST     - Retrieves customer deal details        *
      *             OPTN     - Checks for entry of option             *
      *             ROLLUP   - Rollup subroutine                      *
      *             ROLLDN   - Rolldown subroutine                    *
      *                                                               *
      *  Called by  MAIN     - Main Control Processing                *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           SCRN2     BEGSR
      *
     C           *INKA     DOWEQ'0'
     C           *INKC     ANDEQ'0'
     C           *INKL     ANDEQ'0'
      *
     C                     SETOF                     10
      *
      *  Validate deal
      *
     C           *IN90     IFEQ '0'
     C                     EXSR VALD
     C                     EXSR CUST
     C                     END
     C                     SETOF                     90
      *
      *  Get deal type narrative and format records for Screen 2 header
      *
     C           DTYP      LOKUPTABCOD    TABNAR         34
     C                     EXSR FORMT1
      *
      *  Retrieve deal amendments
      *
     C                     MOVE DEAL      NDEAL   60
     C***********NDEAL     SETLLDEAMS                    52               047815
     C           NDEAL     SETLLMMDEAMLL                 52               047815
      *
     C           *IN52     IFEQ '1'
     C                     MOVE DEAL      CDEAL   6
     C                     EXSR LOAD
     C                     END
      *
     C           *IN52     IFEQ '0'
     C           RCDNBR    OREQ 0
     C                     SETON                     10
     C*                                                                   047815
     C**  Check if direct call from Midas menu                            047815
     C**  else from DL0170                                                047815
     C*                                                                   047815
     C           FXMMDL    IFNE *BLANKS                                   047815
     C                     MOVE 'M'       RETNWF                          047815
     C                     EXSR RETSR                                     047815
     C                     GOTO SCRN29                                    047815
     C                     ELSE                                           047815
     C                     MOVELMSSG,2    MSG
     C                     EXFMTDL0181F1
     C                     GOTO ENDTAG
     C                     END                                            047815
     C                     END
      *
     C                     WRITEDL0181F2
     C                     EXFMTDL0181SC
      *
     C           *INKA     DOWEQ'0'
     C           *INKC     ANDEQ'0'
     C           *INKL     ANDEQ'0'
      *
     C                     SETOF                     20
     C           DEAL      IFNE CDEAL
     C                     GOTO ENDTAG
     C                     END
      *
     C           *IN24     IFEQ '1'
     C                     EXSR ROLLUP
     C                     ELSE
     C           *IN25     IFEQ '1'
     C                     EXSR ROLLDN
     C                     ELSE
     C                     EXSR OPTN
     C                     END
     C                     END
      *
     C                     EXFMTDL0181SC
     C                     END
      *
     C           ENDTAG    TAG
     C                     END
      *
     C***********          ENDSR                                          047815
     C           SCRN29    ENDSR                                          047815
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  VALD     - VALIDATE DEAL                                     *
      *                                                               *
      *  Called by  SCRN1    - First screen processing                *
      *             SCRN2    - Second screen processing               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           VALD      BEGSR
      *
     C           *IN53     DOUEQ'0'
      *
     C                     SETOF                     10
     C                     MOVE DEAL      NDEAL
     C           NDEAL     CHAINDEALS                53
     C           *IN53     IFEQ '1'
     C                     SETON                     10
     C                     MOVELMSSG,1    MSG    79
     C                     ELSE
      *
      *  Validate booking branch for user
      *
     C           @MBIN     IFEQ 'Y'
     C                     MOVE BRCA      ZBRCDX  3
     C                     CALL 'ZVBBU'
     C                     PARM           ZBRCDX
     C                     PARM           @ERR    10
      *
      *  If branch code not valid for this user send error message
      *
     C           @ERR      IFNE 0
     C                     SETON                     1053
     C                     MOVELMSSG,3    MSG
     C                     END
     C                     END
     C                     END
      *
     C   53                EXFMTDL0181F1
      *
     C                     EXSR CMD
      *
     C                     END
      *
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  CUST     - OBTAIN CUSTOMER DEAL DETAILS                      *
      *                                                               *
      *  Called by  SCRN1    - First screen processing                *
      *             SCRN2    - Second screen processing               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           CUST      BEGSR
      *
      *  Retrieve customer details using access program
      *
     C                     MOVELCNUM      CKEY   10
     C                     CALL 'AOCUSTR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM CKEY      @KEY   10
     C                     PARM           @KSTAT  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      *  Retrieve currency details using access program
      *
     C                     MOVELCCY       @CCY    3
     C                     CALL 'AOCURRR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM @CCY      @KCCY   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  LOAD     - LOAD SUBFILE                                      *
      *                                                               *
      *  Calls      FORMT2   - Format data for subfile                *
      *                                                               *
      *  Called by  SCRN2    - Second screen processing               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           LOAD      BEGSR
      *
      *  Clear Subfile
      *
     C                     MOVEA'000'     *IN,21
     C                     WRITEDL0181SC
     C                     MOVEA'110'     *IN,21
      *
     C                     Z-ADD0         RCDNBR  30
     C**N24******          READ DEAMS                    23               047815
     C  N24                READ MMDEAMLL                 23               047815
      *
     C           RCDNBR    DOUEQ12
     C*                                                                   047815
     C**  Convert value date YYYYMMDD to MIDAS day number                 047815
     C*                                                                   047815
     C                     Z-ADDHNVDYY    @@YYYY                          047815
     C                     Z-ADDHNVDMM    @@MTH                           047815
     C                     Z-ADDHNVDDD    @@DAY                           047815
     C*                                                                   047815
     C           @@DTIN    IFEQ 0                                         047815
     C                     MOVE *ZEROS    @VDATD                          047815
     C                     MOVE *ZEROS    @VDAT                           047815
     C                     ELSE                                           047815
     C                     EXSR ZA0680                                    047815
     C                     Z-ADD@@MDNO    @VDATD  50                      047815
     C                     Z-ADD@@MDNO    @VDAT   50                      047815
     C                     END                                            047815
     C*                                                                   047815
     C***********MOGI      IFEQ 'M'                                       047815
     C                     ADD  1         RCDNBR
      *
      *    Store key
      *
     C                     Z-ADDRCDNBR    X
     C                     MOVEASAVKY     DMK,X
      *
     C                     EXSR FORMT2
     C                     WRITEDL0181SF
      *                                                                   CDL006
      ** Display a second line of information detailing the charge code   CDL006
      ** and charge amount for some of the ficuciary transactions.        CDL006
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C                     MOVE *OFF      *IN51                           CDL006
     C                     MOVE *OFF      *IN50                           CDL006
      *                                                                   CDL006
     C           DTYP      IFEQ 'FT'                                      CDL006
     C           DTYP      OREQ 'DT'                                      CDL006
     C           DTYP      OREQ 'LT'                                      CDL006
      *                                                                   CDL006
     C                     MOVE *ON       *IN51                           CDL006
     C           FCGC      IFNE *BLANK                                    CDL006
     C           FCGA      ORNE 0                                         CDL006
     C                     MOVE *BLANK    REVIND                          CDL006
     C                     MOVE *BLANK    ZADATE                          CDL006
     C                     MOVE *BLANK    BRTTS                           CDL006
     C                     MOVE *BLANK    RTSPS                           CDL006
     C                     MOVE *BLANK    STABTP                          CDL006
     C                     MOVELFCGC      STABTP                          CDL006
     C                     Z-ADD*ZERO     ZFLD15                          CDL006
     C                     MOVE FCGA      ZFLD15                          CDL006
     C                     Z-ADDA6NBDP    ZDECS                           CDL006
     C                     EXSR ZSEDIT                                    CDL006
     C                     MOVE ZOUT21    SDAAM                           CDL006
     C                     ADD  1         RCDNBR                          CDL006
     C                     Z-ADDRCDNBR    X                               CDL006
     C                     MOVEASAVKY     DMK,X                           CDL006
     C                     WRITEDL0181SF                                  CDL006
     C                     ENDIF                                          CDL006
     C                     MOVE *OFF      *IN51                           CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C/COPY WNCPYSRC,DL0181C001
     C***********          END                                            047815
     C                     MOVE DEAL      NDEAL
     C***********NDEAL     READEDEAMS                    23               047815
     C           NDEAL     READEMMDEAMLL                 23               047815
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
     C           RCDNBR    ANDEQ11                                        CDL006
     C                     MOVE *OFF      *IN54                           CDL006
     C                     LEAVE                                          CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C/COPY WNCPYSRC,DL0181C002
     C  N23                END
      *
      *  Reset file cursor for Rolldown
      *
     C           *IN23     IFEQ '1'
     C***********          Z-ADD99999     VALK    50                      047815
     C                     Z-ADD9999      VAYY    40                      047815
     C                     Z-ADD99        VAMM    20                      047815
     C                     Z-ADD99        VADD    20                      047815
     C                     Z-ADD999       DANK    30
     C***********DEAMKY    SETGTDEAMS                54                   047815
     C           DEAMKY    SETGTMMDEAMLL             54                   047815
     C                     END
      *
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  ROLLUP   - ROLLUP SUBROUTINE                                 *
      *                                                               *
      *  Called by  SCRN2    - Second screen processing               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           ROLLUP    BEGSR
      *
      *  If end of file not reached load next page, otherwise
      *  set indicator for error message
      *
     C********** *IN54     IFEQ '0'                                                           214209
     C********** *IN23     ANDEQ'0'                                                           214209
      ** SFLEND indicator is enough to condition the loading of subfile                       214209
     C           *IN23     IFEQ *OFF                                                          214209
     C                     EXSR LOAD
     C                     ELSE
     C                     MOVELMSSG,4    MSG
     C                     MOVEA'1111'    *IN,20
     C                     END
      *
0440 C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  ROLLDN   - ROLLDOWN SUBROUTINE                               *
      *                                                               *
      *  Called by  SCRN2    - Second screen processing               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           ROLLDN    BEGSR
      *
      *  Read back to the top of the current page
      *
     C                     Z-ADD0         Z       20
     C                     MOVE DEAL      NDEAL
     C           Z         DOUEQRCDNBR
     C***********NDEAL     REDPEDEAMS                    55               047815
     C           NDEAL     REDPEMMDEAMLL                 55               047815
      *
     C           *IN55     IFEQ '0'
     C***********MOGI      IFEQ 'M'                                       047815
     C                     ADD  1         Z
     C***********          END                                            047815
     C                     END
      *
     C  N55                END
      *
      *  Read back to the top of the previous page
      *
     C                     Z-ADD0         Y       20
     C                     MOVE DEAL      NDEAL
     C           Y         DOUEQ12
     C***********NDEAL     REDPEDEAMS                    55               047815
     C           NDEAL     REDPEMMDEAMLL                 55               047815
      *
     C           *IN55     IFEQ '0'
     C***********MOGI      IFEQ 'M'                                       047815
     C                     ADD  1         Y
     C***********          END                                            047815
     C                     END
      *
     C  N55                END
      *
      *  If beginning of file, re-position cursor for rollup and set
      *  an indicator for the error message
      *  ELSE
      *  reposition cursor before beginning of last record read
      *
     C                     MOVE DEAL      NDEAL
     C           *IN55     IFEQ '1'
      *
     C***********NDEAL     SETLLDEAMS                    52               047815
     C           NDEAL     SETLLMMDEAMLL                 52               047815
     C                     MOVELMSSG,5    MSG
     C                     SETON                     20
     C                     ELSE
     C***********          Z-ADDVDAT      VALK                            047815
     C                     Z-ADDHNVDYY    VAYY                            047815
     C                     Z-ADDHNVDMM    VAMM                            047815
     C                     Z-ADDHNVDDD    VADD                            047815
     C                     Z-ADDDASN      DANK
     C***********DEAMKY    SETLLDEAMS                    52               047815
     C           DEAMKY    SETLLMMDEAMLL                 52               047815
     C                     END
      *
      *  Load the next page of records
      *
     C                     EXSR LOAD
      *
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  OPTN  - SUBROUTINE TO PROCESS OPTION                         *
      *                                                               *
      *  Called by  SCRN2    - Second screen processing               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           OPTN      BEGSR
      *
      *  Save record number
      *
     C                     Z-ADDRCDNBR    SAVNBR  30
     C                     MOVE ' '       RETND
      *
      *  Check for selected subfile record
      *
     C           *IN97     DOUEQ'1'
     C           RETND     OREQ 'U'
     C           RETND     OREQ 'S'
     C                     READCDL0181SF               9797
      *
     C           *IN97     IFEQ '0'
     C           OPTION    IFNE '1'
     C                     MOVE ' '       OPTION
     C                     UPDATDL0181SF
     C                     SETON                     20
     C                     MOVELMSSG,6    MSG
     C                     ELSE
      *
     C                     MOVE ' '       OPTION
     C                     UPDATDL0181SF
     C                     Z-ADDRCDNBR    X       20
     C           *LOCK     IN   DLSDS
     C                     MOVE ' '       RETND
     C                     MOVE *BLANKS   RQDTW  18
     C                     MOVELRQDTW     RQDTD
     C                     MOVELDMK,X     RQDTD
      *
     C           FXMMDL    IFEQ *BLANKS
     C                     MOVE 'DL'      RQDTD
     C                     END
      *
     C                     OUT  DLSDS
      *
     C                     CALL 'DL0185'
     C                     IN   DLSDS
      *
      *   Test for database error in DL0185
      *
     C           RETND     IFEQ 'E'
     C                     MOVEL'DL0181  'DBPGM
     C                     MOVEL'PROGRAM 'ZDB14  14
     C                     MOVE 'DL0185'  ZDB14
     C                     MOVEL' DEAL '  ZDB12  12
     C                     MOVE DLNO      ZDB12
     C                     MOVELZDB14     DBKEY
     C                     MOVE ZDB12     DBKEY
     C                     RETRN
     C                     END
      *
      *   Test for F1 or F3 pressed in DL0185
      *
     C           RETND     IFEQ 'Q'
     C           RETND     OREQ 'S'
     C           FXMMDL    ANDNE*BLANKS
     C                     SETON                     LR
     C                     RETRN
     C                     END
      *
     C                     END
     C                     END
     C                     END
      *
     C                     Z-ADDSAVNBR    RCDNBR
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  CMD - SUBROUTINE TO VALIDATE COMMAND KEYS                    *
      *                                                               *
      *  Calls      RETSR   - Process command keys                    *
      *                                                               *
      *  Called by  MAIN    - Main processing                         *
      *                                                               *
      *---------------------------------------------------------------*
0741  *
0740 C           CMD       BEGSR
     C*                                                                   047815
     C**  Deal amendments missing & from DL0170                           047815
     C*                                                                   047815
     C           RETNWF    IFEQ 'M'                                       047815
     C                     GOTO CMD9                                      047815
     C                     END                                            047815
      *
      *   Validate Command Keys
      *
     C           *INKA     IFEQ '1'
     C                     MOVE 'S'       RETNWF  1
     C                     EXSR RETSR
     C                     END
      *
     C           *INKC     IFEQ '1'
     C                     MOVE 'Q'       RETNWF
     C                     EXSR RETSR
     C                     END
      *
     C           *INKL     IFEQ '1'
      *
      *  Test whether called direct from Midas menu
      *  ELSE from DL0170
      *
     C           FXMMDL    IFEQ *BLANKS
     C                     MOVE *BLANKS   DEAL
     C                     ELSE
     C                     MOVE 'U'       RETNWF
     C                     EXSR RETSR
     C                     END
     C                     END
      *
     C***********          ENDSR                                          047815
     C           CMD9      ENDSR                                          047815
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  RETSR - SUBROUTINE TO PROCESS COMMAND KEYS                   *
      *                                                               *
      *  Called by  VALD    - Deal validation subroutine              *
      *             CMD     - Command key validation subroutine       *
      *                                                               *
      *---------------------------------------------------------------*
0741  *
0740 C           RETSR     BEGSR
0741  *
0742 C                     SETON                     LR
0392  *  Access DL Data Area
0405 C           *LOCK     IN   DLSDS
0402 C                     MOVE RETNWF    RETND
0405 C                     OUT  DLSDS
0743 C                     RETRN
0744  *
0745 C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  FORMT1 - SUBROUTINE TO FORMAT SCREEN2 HEADER                 *
      *                                                               *
      *  Calls      ZSEDIT   - Format amounts                         *
      *                                                               *
      *  Called by  LOAD    - Load subfile subroutine                 *
      *                                                               *
      *
      *---------------------------------------------------------------*
0741  *
0740 C           FORMT1    BEGSR
      *
      * Initialise screen fields
      *
     C                     MOVE *BLANKS   SDEAL
     C                     MOVE *BLANKS   SNARR
     C                     MOVE *BLANKS   SCNUM
     C                     MOVE *BLANKS   SBCRNM
      *                                                                                       CER043
     C           CER043    IFEQ 'Y'                                                           CER043
     C                     MOVE *BLANKS   SBCSSN                                              CER043
     C                     ENDIF                                                              CER043
      *
     C                     MOVE *BLANKS   SBCRTN
     C                     MOVE *BLANKS   SCCY
     C                     MOVE *BLANKS   SPCPL
     C                     MOVE *BLANKS   SBRTT
     C                     MOVE *BLANKS   SRTSP
      *
      * Edit Current Principal for output to screen
      *
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE PCPL      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    SPCPL
      *
      * Edit Rate for output to screen
      *
     C           RTSP      IFNE 0
     C                     Z-ADDRTSP      @@INPR
     C                     EXSR ZRATE2
     C                     MOVE @@OUTR    SRTSP
     C           RTSP      IFLE 0                                                             221673
     C                     MOVE '-   '    SRTSP                                               221673
     C                     ENDIF                                                              221673
     C                     ELSE
     C                     MOVE *BLANKS   SRTSP
     C                     END
      *
      * Move fields into screen fields
      *
     C                     MOVE DEAL      SDEAL
     C                     MOVE TABNAR    SNARR
     C                     MOVE CNUM      SCNUM
     C                     MOVE BBCRNM    SBCRNM
      *                                                                                       CER043
     C           CER043    IFEQ 'Y'                                                           CER043
     C                     MOVE BBCSSN    SBCSSN                                              CER043
     C                     ENDIF                                                              CER043
      *                                                                                       CER043
     C                     MOVE BBCRTN    SBCRTN
     C                     MOVE CCY       SCCY
     C/COPY WNCPYSRC,DL0181C004
     C           BRTT      IFGT 0
     C                     MOVE BRTT      SBRTT
     C                     ELSE
     C                     MOVE *BLANKS   SBRTT
     C                     END
      *
     C/COPY WNCPYSRC,DL0181C005
     C                     ENDSR
     C*---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  FORMT2 - SUBROUTINE TO FORMAT SUBFILE RECORDS                *
      *                                                               *
      *  Calls      ZDATE2   - Format date                            *
      *                                                               *
      *  Called by  LOAD    - Load subfile subroutine                 *
      *                                                               *
      *
      *---------------------------------------------------------------*
0741  *
0740 C           FORMT2    BEGSR
      *
      *  Recover amendment type narrative
      *
     C           AMTP      LOKUPTABAMC    TABTYP         40
     C           AMTP      COMP 'SC'                     41
     C***********          Z-ADDVDAT      ZDAYNO                          047815
     C                     Z-ADD@VDATD    ZDAYNO                          047815
     C                     EXSR ZDATE2
     C/COPY WNCPYSRC,DL0181CCP1                                           S01408
      *
      *  Check if amendment is reversed
      *
     C                     SETOF                     50
     C                     MOVE ' '       REVIND
     C***********RECI      IFEQ 'R'                                       047815
     C           HNDDLT    IFEQ 'D'                                       047815
     C                     MOVE 'R'       REVIND
     C                     SETON                     50
     C                     END
      *
      * Edit Current Principal for output to screen
      *
     C           DAAM      IFLT 0                                         047815
     C                     Z-SUBDAAM      DAAM                            047815
     C                     END                                            047815
     C*                                                                   047815
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE DAAM      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    SDAAM
      *
      * Edit Rate for output to screen
      *
     C           RTSP      IFNE 0
     C                     Z-ADDRTSP      @@INPR
     C                     EXSR ZRATE2
     C                     MOVE @@OUTR    RTSPS
     C           RTSP      IFLE 0                                                             221673
     C                     MOVE '-   '    RTSPS                                               221673
     C                     ENDIF                                                              221673
     C                     ELSE
     C                     MOVE *BLANKS   RTSPS
     C                     END
      *
      * Move fields into screen fields
      *
     C                     MOVE DASN      SDASN
     C                     MOVE TABTYP    STABTP
     C/COPY WNCPYSRC,DL0181C006
     C           BRTT      IFGT 0
     C           HNTYPE    ANDNE'PI'                                      047815
     C           HNTYPE    ANDNE'PD'                                      047815
     C                     MOVE BRTT      BRTTS
     C                     ELSE
     C                     MOVE *BLANKS   BRTTS
     C                     END
      *
     C/COPY WNCPYSRC,DL0181C007
     C                     ENDSR
      *---------------------------------------------------------------*
      /EJECT
      *---------------------------------------------------------------*
      *                                                               *
      *  *PSSR      -  ERROR PROCESSING                               *
      *                                                               *
      *---------------------------------------------------------------*
      *
     C           *PSSR     BEGSR
      *
      * Update Linked Enquiry Data Area with Return Code set to 'E',
      * update LDA with program name, set on error switch, dump
      * program and return to calling program
      *
     C           ERROR     IFEQ *BLANK
     C                     MOVE 'Y'       ERROR   1
      *
     C           *LOCK     IN   DLSDS
     C                     MOVE 'E'       RETND
     C                     OUT  DLSDS
      *
     C                     MOVEL'DL0181  'DBPGM
     C           *NAMVAR   DEFN LDA       @LDA  256
     C           *LOCK     IN   @LDA
     C                     MOVE DBERR     @LDA
     C                     OUT  @LDA
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     END
      *
     C                     RETRN
      *
     C                     ENDSR
     C/COPY WNCPYSRC,DL0181CCP2                                           S01408
      *===============================================================*
      /EJECT                                                              047815
     C*****************************************************************   047815
     C*                                                               *   047815
     C* ZA0680 - This subroutine converts YYYYMMDD format date to     *   047815
     C*          Midas day number format (no. days from 31/12/71)     *   047815
     C*                                                               *   047815
     C* Called by : LOAD                                              *   047815
     C*                                                               *   047815
     C* Calls : none                                                  *   047815
     C*                                                               *   047815
     C* Input  : @@DTIN DATE (YYYYMMDD) - in data structure (size: 8N)*   047815
     C*                                                               *   047815
     C* Output : @@MDNO Midas day number  (size: 5N)                  *   047815
     C*                                                               *   047815
     C* Uses :   @@CC   number of centuries in date                   *   047815
     C*          @@DAY  day number                                    *   047815
     C*          @@REM  remainder from divide                         *   047815
     C*          @@MTH  month number                                  *   047815
     C*          @MD    compilation time array for cumulative days in *   047815
     C*                 month                                         *   047815
     C*          @@WK2  work field (2,0)                              *   047815
     C*          @@WK5  work field (5,0)                              *   047815
     C*          @@YYYY year (4 character)                            *   047815
     C*          @@YY   year (2 character)                            *   047815
     C*          @YD    compilation time array for cumulative days in *   047815
     C*                 a four year period                            *   047815
     C*                                                               *   047815
     C*****************************************************************   047815
     C*                                                                   047815
     C           ZA0680    BEGSR                                          047815
     C*                                                                   047815
     C**  Clear out any 'OLD' data in field                               047815
     C*                                                                   047815
     C                     Z-ADD0         @@MDNO  50                      047815
     C*                                                                   047815
     C           @@DTIN    CABEQ0         ZA0689                          047815
     C*                                                                   047815
     C**  If date greater than 31/12/2071 need extra processing           047815
     C**  work out the number of centuries passed since 1971,             047815
     C**  by subtracting 1972 from the year field.                        047815
     C*                                                                   047815
     C           @@YYYY    IFGE 2072                       B1             047815
     C           @@YYYY    SUB  1972      @@YYYY           *1             047815
     C*                                                                   047815
     C**  Multiply by number of days in century (since 1971)              047815
     C**  then restore the year which was input back to the               047815
     C**  original value before continuing with normal calculations.      047815
     C**                                                                  047815
     C*                                                                   047815
     C           @@CC      MULT 36524     @@MDNO           *1             047815
     C           @@YYYY    ADD  1972      @@YYYY           *1             047815
     C                     END                             E1             047815
     C*                                                                   047815
     C           @@YYYY    ADD  28        @@WK2   20                      047815
     C*                                                                   047815
     C           @@WK2     DIV  4         @@WK2                           047815
     C                     MVR            @@REM   10                      047815
     C*                                                                   047815
     C**  Multiply by number of days in four year period                  047815
     C*                                                                   047815
     C           @@WK2     MULT 1461      @@WK5   50                      047815
     C                     ADD  @@WK5     @@MDNO                          047815
     C*                                                                   047815
     C**  Check remainder and month number                                047815
     C*                                                                   047815
     C           @@REM     IFEQ 0                          B1             047815
     C           @@MTH     ANDGT2                          B1             047815
     C                     ADD  1         @@MDNO           *1             047815
     C                     END                             E1             047815
     C*                                                                   047815
     C**  Year not leap year,  add cumulative days for year               047815
     C*                                                                   047815
     C           @@REM     IFNE 0                          B1             047815
     C                     ADD  @YD,@@REM @@MDNO           *1             047815
     C                     END                             E1             047815
     C*                                                                   047815
     C**  Add months this year                                            047815
     C*                                                                   047815
     C                     ADD  @MD,@@MTH @@MDNO                          047815
     C*                                                                   047815
     C**  Add days this month                                             047815
     C*                                                                   047815
     C                     ADD  @@DAY     @@MDNO                          047815
     C*                                                                   047815
     C           ZA0689    ENDSR                           **ZA0689 TAG** 047815
      *===============================================================*
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      /COPY ZSRSRC,ZRATE2Z2
      /EJECT
      *COPY*ZSRSRC,ZSEDITZ3*****                                                              245692
      /COPY ZSRSRC,ZSEDITZ4                                                                   245692
      /EJECT
      /COPY ZSRSRC,ZDATE2Z3
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**   MSSG -- MESSAGES OUTPUT TO SCREEN
 INVALID DEAL NUMBER
 NO DEAL AMENDMENTS FOR DEAL
 BOOKING. BR. ON RELATED REC. INVALID FOR USER
 NO MORE DEAL AMENDMENTS TO DISPLAY
 ALREADY AT TOP OF FILE
 ENTER '1' FOR DETAILS OF DEAL AMENDMENT
**   TABCOD -- DEAL TYPE CODES
ITIPCDCLLNTDDLTIBDTBDATABPC1C2CIFLDPLPFTDTLT                              CDL006
**   TABNAR -- DEAL TYPE NARRATIVES
DEPOSIT TAKEN                 DEPOSIT PLACED
CALL/NOTICE DEPOSIT           CALL/NOTICE DEPOSIT PLACED
COMMERCIAL LOAN               TIME DEPOSIT
DEMAND LOAN                   TIME LOAN
BILL DISCOUNTED               TREASURY BILL PURCHASED
ACCEPTANCE DISCOUNTED-BANKS   TRADE ACCEPTANCES DISCOUNTED
BOND PURCHASED                PRIMARY CD PURCHASED
SECONDARY CD PURCHASED        CD ISSUED
FIDUCIARY DEPOSIT TERM PLACED FIDUCIARY DEPOSIT CALL PLACED               CDL006
FIDUCIARY LOAN CALL PLACED    FIDUCIARY DEPOSIT TERM TAKEN                CDL006
FIDUCIARY DEPOSIT CALL TAKEN  FIDUCIARY LOAN CALL TAKEN                   CDL006
**   TABAMC -- AMENDMENT CODES   TABTYP -- AMENDMENT NARRATIVES
PIPRINCIPAL INCREASE
PDPRINCIPAL DECREASE
SCRATE CHANGE
CICAPITALISE INTEREST
SIINTEREST PAYMENT DATE
EMEARLY MATURITY                                                                              BG2918
BCBASE RATE CODE CHANGE                                                                       BG2918
** @YD  USED BY SR. ZA0680 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZA0680 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
