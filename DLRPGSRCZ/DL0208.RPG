     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Todays FX deals received from TI')            *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0208 - Today's FX deals received from TI                   *
      *                                                               *
      *  Function:  This program is based on DL0205 but reports only  *
      *             FX deals coming from TI.                          *
      *  (I/C COB)                                                    *
      *                                                               *
      *  Called By: DLC0218 - Midas DL FX Deals received from TI      *
      *             DLC0601A - Midas DL List of today's deals         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CDL099             Date 06Oct17               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CTI004  *CREATE    Date 12Apr04               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CDL094 - Enhance Receive Settlement Instructions             *
      *           (Recompiled)                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CTI004 - MidasPlus-TI Integration Enhancements               *
      *           FX Deals Integration                                *
      *                                                               *
      *****************************************************************
     FDTRSD1  IF  E           K        DISK         KINFSR *PSSR
     FDLDLDBL1IF  E           K        DISK         KINFSR *PSSR
      *
     FDL0208AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FDL0208P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE1 - Validate Date and Convert into Midas Day Number     *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
      /SPACE 3
     IDTRIXDF
     I              CHTP                            DXCHTP
     I              BRCA                            DXBRCA
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ** File Information Data Structure for MMNNNNPY.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for MMNNNNAU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ** Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDTIIN    E DSSDTIINPD
      ** External data structures for Bank Details
      *
     ISCSARD    E DSSCSARDPD
      ** Data Structure for SAR Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** External DS for BRANCH Details
      *
     ISDCURR    E DSSDCURRPD
      ** EXTERNAL DS FOR CURRENCY DETAILS
      *
     ISDCUST    E DSSDCUSTPD
      ** EXTERNAL DS FOR CUSTOMER DETAILS
     I              QQDFAC                          QQDFAX
      *
     ISDPORT    E DSSDPORTPD
      ** External DS for Portfolio Management ICD.
      *
     ISDMMOD    E DSSDMMODPD
      ** External DS for Module Details.
      *
     ISDGELR    E DSSDGELRPD
      ** External DS for General Ledger Details.
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ** Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  ZDAT   -
      *  ZDAT   -
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
      *
      ** Initialise LDA field.
      *
     C                     MOVEL'DL0208'  DBPGM
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ** Access TI ICD details
      *
     C                     CALL 'AOTIINR0'
     C                     PARM '*MSG'    WRTCD
     C                     PARM '*FIRST'  WOPTN
     C           SDTIIN    PARM SDTIIN    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDTIINPD'DBFILE           ***************
     C                     Z-ADD002       DBASE            * DB ERROR 02 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      **   Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'M'
     C                     SETON                     37
     C                     END
      *
      ** Access GL details to determine if originating branch used
      *
     C                     CALL 'AOGELRR1'
     C                     PARM           WRTCD
     C                     PARM '*FIRST'  WOPTN
     C           SDGELR    PARM *BLANK    DSSDY
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDGELRPD'DBFILE           ***************
     C                     Z-ADD004       DBASE            * DB ERROR 04 *
     C                     EXSR *PSSR                      ***************
     C                     END
     C                     MOVE '0'       *IN32
     C           BKOBRU    IFEQ 'Y'
     C                     MOVE '1'       *IN32
     C                     END
      *
      ** RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ** Read first record from File.
      *
     C                     READ DTRSD1                   89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
     C                     MOVE *BLANKS   WBRCA   3
      *
      ** Do Until End of File.
      *
     C           *IN89     DOUEQ*ON
      *
     C                     SETOF                     353638
     C                     SETOF                     434446
     C                     SETOF                     474853
     C                     SETOF                     5455
      *
     C           DLNO      CHAINDLDLDBL1             42
      *
     C           DXBRCA    IFNE WBRCA
     C           *IN60     ANDEQ*OFF
      *
     C                     CLOSEDL0208P1
      *
     C                     CALL 'AOBRCHR0'
     C                     PARM '*MSG    'WRTCD
     C                     PARM '*KEY    'WOPTN
     C                     PARM           DXBRCA
     C           SDBRCH    PARM SDBRCH    DSFDY
      *
     C                     MOVELA8BRCD    WBRCA
      *
     C                     OPEN DL0208P1
     C                     EXSR RCFP1
     C                     WRITEHEADP1
      *
     C           *IN37     IFEQ *OFF
     C                     MOVE *ON       *IN60
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN42     IFEQ *OFF
     C           TIDLST    ANDEQDLST
      *
      ** Count records read.
      *
     C                     ADD  1         RCOUNT
      *
      ** Process Report Lines.
      *
     C                     EXSR REPORT
      *
     C                     ENDIF
      *
      ** Read next record.
      *
     C                     READ DTRSD1                   89
      *
     C           DXBRCA    IFNE WBRCA
     C           *IN60     ANDEQ*OFF
     C                     EXSR WP1END
     C                     ENDIF
      *
      ** End of Do Until End of Valid Records.
     C                     ENDDO
      *
      *
      ** Write final records and Totals to Report.
      *
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
     C                     MOVE VDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    VADATE
      *
     C                     MOVELPUCY      KCCY
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM           KCCY
     C           SDCURR    PARM SDCURR    DSSDY
     C           WRTCD     IFEQ *BLANK
     C                     SELEC
     C           A6NBDP    WHEQ 0
     C                     MOVE *ON       *IN35
     C                     Z-ADDPUAM      AMT0
     C           A6NBDP    WHEQ 1
     C                     MOVE *ON       *IN46
     C                     Z-ADDPUAM      AMT1
     C           A6NBDP    WHEQ 2
     C                     MOVE *ON       *IN47
     C                     Z-ADDPUAM      AMT2
     C                     OTHER
     C                     MOVE *ON       *IN48
     C                     Z-ADDPUAM      AMT3
     C                     ENDSL
     C                     ENDIF
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM           SLCY
     C           SDCURR    PARM SDCURR    DSSDY
     C           WRTCD     IFEQ *BLANK
     C                     SELEC
     C           A6NBDP    WHEQ 0
     C                     MOVE *ON       *IN36
     C                     Z-ADDSLAM      SAMT0
     C           A6NBDP    WHEQ 1
     C                     MOVE *ON       *IN53
     C                     Z-ADDSLAM      SAMT1
     C           A6NBDP    WHEQ 2
     C                     MOVE *ON       *IN54
     C                     Z-ADDSLAM      SAMT2
     C                     OTHER
     C                     MOVE *ON       *IN55
     C                     Z-ADDSLAM      SAMT3
     C                     ENDSL
     C                     ENDIF
      *
     C                     MOVELCNUM      CKEY    7
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM CKEY      CKEY1  10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           DXCHTP    IFEQ 'A'
     C                     MOVE *ON       *IN38
     C                     ELSE
     C           DXCHTP    IFEQ 'I'
     C                     MOVE *ON       *IN43
     C                     ELSE
     C                     MOVE *ON       *IN44
     C                     ENDIF
     C                     ENDIF
      *
     C           MGPT      IFLT *ZERO
     C                     Z-SUBMGPT      MGPT
     C                     END
      *
     C           BDBF      IFLT *ZERO
     C                     Z-SUBBDBF      BDBF
     C                     MOVE '-'       BDBFS
     C                     ELSE
     C                     MOVE '+'       BDBFS
     C                     END
      *
     C           SDBF      IFLT *ZERO
     C                     Z-SUBSDBF      SDBF
     C                     MOVE '-'       SDBFS
     C                     ELSE
     C                     MOVE '+'       SDBFS
     C                     END
      *
      ** Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      *----------------------------------------------------------------
      *
      *    Perform any formatting before printing via ZDATE2
      *
      *----------------------------------------------------------------
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ** Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELLDA       WLDA
     C                     OUT  WLDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
**  CPY@
(c) Misys International Banking Systems Ltd. 2003
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
