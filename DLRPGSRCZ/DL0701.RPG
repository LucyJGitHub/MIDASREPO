0001 H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL FX Deals by Value Date Report by Branch')     *
0002 F********************************************************************
0004 F*                                                                  *
     F*  Midas - Dealing Module
0004 F*                                                                  *
0005 F*       DL0701  -  FX DEALS BY VALUE DATE REPORT BY BRANCH         *
0006 F*                                                                  *
0006 F*       THIS PROGRAM HAS BEEN GENERATED FROM DL0700                *
0006 F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
0008 F*                                                                  *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 05Aug96               *
      *                 S01393             Date 11Aug92               *
     F*                        BHF348         DATE  13SEP90              *
     F*                        LN0363         DATE  13JUN90              *
     F*                        E296           DATE  28MAR90              *
     F*                        E299           DATE  23MAR90              *  0018
0010 F*                        E257           DATE  08MAR90              *
0010 F*                        E57            DATE  21NOV89              *
0008 F*                                                                  *
0011 F********************************************************************
     F*                                                                  *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance Receive Settlement Instructions             *
      *           (Recompiled)                                        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (recompiled)                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*    S01408 - Core Hook DL0701FFP1 Added                           *
     F*           - Core Hook DL0701EEP1 Added                           *
     F*           - Core Hook DL0701CCP2 Added                           *
     F*           - Core Hook DL0701CCP1 Added                           *
     F*    S01393 - Recompiled for Strategic Risk Management             *
     F*             Book Code added to PF/EVNTXED                        *
     F*    BHF348 - RENAME PRINTER FILES TO USE PROGRAM NAME             *
     F*    LN0363 - 'END OF REPORT' ONLY BEING OUTPUT AT END OF REPORT   *
     F*             FOR LAST BRANCH                                      *
     F*      E296 - PROG SHOULD READ SDBANK WHEN FILES ARE EMPTY         *
     F*      E299 - 'AU' REPORT SHOULD NOT WRITE 'END OF REPORT'         *
     F*      E257 - PRINTER FILE SHOULD BE CHANGED TO SYSTEM REPORT      *
     F*             PRINTER FILE                                         *
     F*                                                                  *
     F*       E57 - CHANGE TO CHECK BRCH REQ. EQUALS ONE OF NEW FIELDS   *
     F*                                                                  *
0010 F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
0011 F********************************************************************
0012 F* ID F  C  H  L    FUNCTION OF INDICATORS
0013 F* 01            SORTED EVENTS FILE - HEADER
0014 F* 02                               - DETAIL
0016 F*    20         FIRST CYCLE PROCESSING DONE
0017 F*    21         EVENT TYPE TO BE PROCESSED
0018 F*    22         NO DETAILS TO REPORT
0019 F*       23      OVERFLOW REACHED WITHIN A CONTROL GROUP
0018 F*    25         MULTIBRANCHING OUTPUT
     F*    26         P1 OPEN                                             LN0363
0018 F*    35         BRANCH HEADER OUTPUT
0018 F*    36         END OF BRANCH OUTPUT
0020 F*    30         PURCHACE CCX HAS 0 D.P.
0021 F*    31                          1 D.P.
0022 F*    32                          2 D.P.
0023 F*    33                          3 D.P.
0024 F*    40         SALE CCY  HAS 0 D.P.
0025 F*    41                       1 D.P.
0026 F*    42                       2 D.P.
0027 F*    43                       3 D.P.
0028 F*    44       EVENT CCY HAS FED. FUNDS INDICATOR
0029 F*             U7DATABASE ERROR
0030 F*             U8FILE CONTROLS OUT OF BALANCE
0011 F********************************************************************
0031 FEVNT2L1 IP  E           K        DISK
     F*                                                                   S01408
     F/COPY WNCPYSRC,DL0701FFP1                                           S01408
     FDEALS   IF  E           K        DISK                               CDL049
     F            DEALSDAF                          KIGNORE               CDL049
     F            DEALSDCF                          KIGNORE               CDL049
     F            DEALSDDF                          KIGNORE               CDL049
     F            DEALSDEF                          KIGNORE               CDL049
     F            DEALSDFF                          KIGNORE               CDL049
     F            DEALSDGF                          KIGNORE               CDL049
     F*                                                                   S01408
     FSDCURRL1IF  E           K        DISK
0015 F**DL0701P1O***F***132*****OF*****PRINTER******KINFDS SPOOL      UC    E257
0015 F**DL0700P1O***F***132*****OF     PRINTER      KINFDS SPOOL   UC E257BHF348
0015 FDL0701P1O   F     132     OF     PRINTER      KINFDS SPOOL      UC  BHF348
0034 F**DL0701AUO***F***132*****OA*****PRINTER******KINFDS SPOOLU           E257
0034 F**DL0700AUO***F***132*****OA     PRINTER      KINFDS SPOOLU     E257BHF348
0034 FDL0701AUO   F     132     OA     PRINTER      KINFDS SPOOLU         BHF348
     F*
     E                    CPY@    1   1 80
0035 E                    CCYA      150  3
0036 E                    DPA       150  1 0
0037 E                    INT         2 15 0
0038 E                    DEC         2  3 0
     E*                                                                   S01408
     E/COPY WNCPYSRC,DL0701EEP1                                           S01408
     E*                                                                   S01408
0039 E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
0040 E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
0041 E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
0042 I*    EVENTS FILE - HEADER RECORD.
0043 I*
0044 IEVNTXECF    01
0047 I*
0048 I*    EVENTS FILE - DETAIL RECORD.
0049 I*
0050 IEVNTXEDF    02
0064 I                                              EDAT  L1
0064 I***************                               EBRC  L2                 E57
0064 I                                              BRCA  L2                 E57
0315 I*
     ISDBANK    E DSSDBANKPD
     I** EXTERNAL DS FOR BANK DETAILS
0121 I*
     ISDBRCH    E DSSDBRCHPD
     I** EXTERNAL DS FOR BRANCH DETAILS
     I*                                                                   S01194
     IDSFDY     E DSDSFDY                                                 S01194
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01194
0121 I*
     ISPOOL       DS
     I** FILE INFORMATION DATA STRUCTURE
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     ISPOOLU      DS
     I** FILE INFORMATION DATA STRUCTURE
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
0142 I**
0925 I**   PROGRAM STATUS DATA STRUCTURE
0925 I**
     IPSDS       SDS
     I                                      244 253 JOBNM
     I                                      254 263 USRID
     I                                      264 2690JOBNO
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
     I*
     C**
     C**   PARAMETERS FOR MULTIBRANCHING
     C**
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C                     PARM           BRNCH   3
     C**
0153 C*    FIRST CYCLE CALCULATIONS.
0154 C   20                GOTO NOTFC
0155 C                     SETON                     20
     C                     MOVEACPY@      BIS@   80
     C**
     C** READ BANK DETAILS VIA ACCESS PROGRAM
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           BJDFIN    COMP 'M'                      98MMDDYY, 98 ON
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE 10        ************
     C                     MOVEL'01'      DBASE   2        * DBERR 01 *
     C                     MOVEL@OPTN     DBKEY            ************
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     END
0159 C*
0160 C*    SET UP ARRAY OF CURRENCY DECIMAL PLACES.
0161 C                     EXSR CCYARR
     C*                                                                   S01408
     C/COPY WNCPYSRC,DL0701CCP1                                           S01408
     C*                                                                   S01408
      *                                                                   CDL049
      * Check if switchable feature CDL049 is present                     CDL049
     C                     CALL 'AOSARDR0'                                CDL049
     C                     PARM '       ' @RTCD   7                       CDL049
     C                     PARM '*VERIFY' @OPTN   7                       CDL049
     C                     PARM 'CDL049'  @SARD   6                       CDL049
      *                                                                   CDL049
     C           @RTCD     IFEQ *BLANK                                    CDL049
     C                     MOVE 'Y'       CDL049  1                       CDL049
     C                     MOVE '1'       *IN11                           CDL049
     C                     ELSE                                           CDL049
     C                     MOVE 'N'       CDL049                          CDL049
     C                     MOVE '0'       *IN11                           CDL049
     C                     ENDIF                                          CDL049
      *                                                                   CDL049
0162 C*
0163 C*    DETAIL CALCULATIONS.
0164 C           NOTFC     TAG                             ** NOTFC TAG **
0165 C   27                SETOF                     2327
0166 C   OF                SETON                     23
0166 C   35                SETOF                     3536
0167 C*
0168 C*    INCREMENT COUNT OF RECORDS READ.
0172 C   02      NREC      ADD  1         NREC    50
     C**
     C**  MULTIBRANCHING OUTPUT CONTROL
     C**
     C           *INL2     IFEQ '1'
0166 C                     SETON                     36
     C                     SETOF                     26                   LN0363
     C*********************CLOSEDL0701P1                                    E257
     C*********************CLOSEDL0700P1                              E257BHF348
     C                     CLOSEDL0701P1                                  BHF348
     C           BRNCH     IFEQ 'ALL'
     C***********BRNCH     OREQ EBRC                                         E57
     C           BRNCH     OREQ BRCA                                         E57
     C**
     C**  Allow output:
     C                     SETON                     25
     C                     Z-ADD0         PAGE
     C**
     C** READ BRANCH DETAILS FOR BRANCH NAME VIA ACCESS PROGRAM
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C****************     PARM EBRC      @DSNB   3                          E57
     C                     PARM BRCA      @DSNB   3                          E57
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBRCHPD'DBFILE 10        ************
     C                     MOVEL'02'      DBASE   2        * DBERR 02 *
     C                     MOVEL@OPTN     DBOPTN  7        ************
     C***************      MOVELEBRC      DBKEY  29                          E57
     C                     MOVELBRCA      DBKEY  29                          E57
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     END
     C**
0261 C**   IF DETAILS TO REPORT EXIST, SET INDICATOR
0262 C***********          SETON                     22                   LN0363
0262 C                     SETON                     2226                 LN0363
0263 C*
     C                     ELSE
     C                     SETOF                     25
     C                     END
     C                     END
0260 C**
0173 C*
0174 C*    PROCESS DETAIL RECORDS ONLY.
0176 C  N02                GOTO END
0177 C*
0178 C*    ADD EVENT AMOUNT TO INPUT HASH TOTAL.
0179 C                     Z-ADD1         S       10
0180 C           EAMT      DIV  1000      ZZAMT
0181 C                     EXSR HASH
     C*                                                                   S01408
     C/COPY WNCPYSRC,DL0701CCP2                                           S01408
     C*                                                                   S01408
0182 C*
0183 C*    CHECK EVENT TYPE.
0184 C  N02                GOTO END
     C*
     C*    SETON L1 IF DATE HAS CHANGED
     C           LSTDAT    COMP EDAT                 2424
     C   24                SETON                     L1
     C                     MOVE EDAT      LSTDAT  50
     C*
0185 C                     SETON                     27
0186 C           FFIN      COMP 'F'                      44
0187 C*
0191 C*    ADD EVENT AMOUNT TO OUTPUT HASH TOTALS.
0192 C                     Z-ADD2         S
0193 C                     EXSR HASH
0194 C*
0195 C*    SET INDICATOR FOR NO. OF D.P. FOR PURCHASE CURRECY.
0196 C                     MOVE ECCY      XCCY
0197 C                     EXSR CCYDEP
0198 C           XDECP     COMP 1                      3031
0199 C           XDECP     COMP 2                    33  32
0200 C*
0201 C*    SET INDICATOR FOR NO. OF D.P. FOR SALE CURRENCY.
0202 C                     MOVE OTHC      XCCY
0203 C                     EXSR CCYDEP
0204 C           XDECP     COMP 1                      4041
0205 C           XDECP     COMP 2                    43  42
0206 C*
0207 C*    CONVERT VALUE DATE TO DDMMYY FORMAT.
0208 C   L1                Z-ADDEDAT      ZDAYNO
0209 C   L1                EXSR ZDATE2
0210 C   L1                MOVE ZADATE    VDATE   7
0211 C*
0212 C*    CONVERT OPTION DATE TO DDMMMYY FORMAT.
0213 C           ETYP      COMP '12V1'                   28
0214 C   28                Z-ADDOTHD      ZDAYNO
0215 C   28                EXSR ZDATE2
0216 C   28                MOVE ZADATE    ODATE   7
      *                                                                   CDL049
      * Retrieve Reference Rate                                           CDL049
     C           CDL049    IFEQ 'Y'                                       CDL049
     C                     Z-ADD0         #RRAT  158                      CDL049
     C           DLNO      CHAINDEALS                12                   CDL049
     C           *IN12     IFEQ '0'                                       CDL049
     C                     Z-ADDFSRRAT    #RRAT                           CDL049
     C                     END                                            CDL049
     C                     END                                            CDL049
      *                                                                   CDL049
0225 C*
0226 C           END       TAG                             ** END TAG **
0226 C   02 25 36          SETON                     35
0226 C***02*25*36**********OPEN DL0701P1                                  E257
0226 C***02*25*36**********OPEN DL0700P1                              E257BHF348
0226 C   02 25 36          OPEN DL0701P1                                  BHF348
     C*
0188 C*    INCREMENT COUNT OF LINES WRITTEN
0189 C   02 25   NRIT      ADD  1         NRIT    50
0190 C*
     C** ENSURE REPORT SPOOL FILE RECORDED BY RCF
     C           *IN02     IFEQ '1'
     C           *IN25     ANDEQ'1'
     C           *IN36     ANDEQ'1'
     C*                                                                   S01117
      ** TRANSFER BINARY DATA INTO NUMERIC FIELD                          S01117
     C                     Z-ADDSFNUM     ZSNUM   60                      S01117
     C***02*25*36***       CALL 'ZSFILE'
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C***************      PARM EBRC      @PARM                              E57
     C                     PARM BRCA      @PARM                              E57
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM *BLANK    ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C                     END                                            S01117
     C*
0227 C*
0228 C*    TOTAL CALCULATIONS.
     C*
     C** ENSURE TOTALS SPOOL FILE RECORDED BY RCF
      ** TRANSFER BINARY DATA INTO NUMERIC FIELD                          S01117
     CLR                   Z-ADDSFNUMU    ZSNUMU  60                      S01117
     CLR                   CALL 'ZSFILE'
     CLR                   PARM           SEQ     5
     CLR                   PARM *BLANKS   @PARM   3
     CLR                   PARM           SFILEU
     CLR                   PARM           ZSNUMU
     CLR                   PARM *BLANK    ZSERR                           S01117
     C*                                                                   S01117
     CLR         ZSERR     IFEQ 'Y'                                       S01117
     CLR                   SETON                     U7U8                 S01117
     CLR                   RETRN                                          S01117
     CLR                   END                                            S01117
     C**
     C** READ BANK DETAILS VIA ACCESS PROGRAM (FOR EMPTY FILES)           E296
     CLRN02                CALL 'AOBANKR0'                                E296
     C                     PARM '*MSG   ' @RTCD   7                       E296
     C                     PARM '*FIRST ' @OPTN   7                       E296
     C           SDBANK    PARM SDBANK    DSFDY                           E296
     CLRN02      BJDFIN    COMP 'M'                      98MMDDYY, 98 ON  E296
     CLRN02      @RTCD     IFNE *BLANK                                    E296
     CLRN02                MOVEL'SDBANKPD'DBFILE 10        ************   E296
     CLRN02                MOVEL'03'      DBASE   2        * DBERR 03 *   E296
     CLRN02                MOVEL@OPTN     DBKEY            ************   E296
     CLRN02                SETON                     U7U8                 E296
     CLRN02                EXSR *PSSR                                     E296
     CLR                   END                                            E296
0159 C*
     C*
0229 C********************************************************************
0230 C**
0231 C**   SR. TO SET UP AN ARRAY OF CURRENCIES AND AN ARRAY OF
0232 C**   DECIMAL PLACES FOR THOSE CURRENCIES.
0233 C**
0234 C           CCYARR    BEGSR                           *** CCYARR  ***
0235 C**
0236 C**   SET ARRAY INDEX TO ZERO.
0237 C                     Z-ADD0         X       30
0238 C**
0243 C**   SET LOWER LIMIT AND READ CURRENCY FILE
0244 C           *LOVAL    SETLLSDCURRL1
0245 C**
0246 C**   LOOP TO READ CURRENCY DETAIL RECORDS.
0247 C           XLOOP     TAG                             ** XLOOP TAG **
0248 C                     READ SDCURRL1                 99
0250 C**
0251 C**   END IF ALL CURRENCY RECORDS PROCESSED
0252 C   99                GOTO XEND
0253 C**
0260 C**   INCREMENT ARRAY INDEX.
0261 C           X         ADD  1         X
0262 C**
0263 C**   MOVE DATA INTO ARRAYS.
0264 C                     MOVE A6CYCD    CCYA,X
0265 C                     MOVE A6NBDP    DPA,X
0266 C**
0267 C**   CHECK FOR FULL ARRAY.96 ON IF FULL.
0268 C           X         COMP 150                      96
0269 C**
0270 C**   GO TO READ NEXT RECORD.
0271 C  N96                GOTO XLOOP
0272 C**
0273 C           XEND      TAG                             ** XEND TAG  **
0274 C                     ENDSR
0275 C**
0276 C********************************************************************
0277 C**
0278 C**   SR. TO OBTAIN NO. OF DECIMAL PLACES FOR A CURRENCY.
0279 C**
0280 C           CCYDEP    BEGSR                           *** CCYDEP  ***
0281 C**
0282 C**   SET ARRAY INDEX TO ONE AND RESET ERROR INDICATOR.
0283 C                     Z-ADD1         X            99
0284 C**
0285 C**   INITIALIZE INPUT-OUTPUT FIELDS.
0286 C                     Z-ADD0         XDECP   10
0287 C                     MOVE XCCY      XCCY    3
0288 C**
0289 C**   LOOK UP NO. OF DECIMAL PLACES FOR CURRENCY.
0290 C**   99 SETON FOR INVALID CURRENCY.
0291 C           XCCY      LOKUPCCYA,X                   95
0292 C   95                MOVE DPA,X     XDECP
0293 C  N95                SETON                     99
0294 C**
0295 C                     ENDSR
0296 C**
0297 C********************************************************************
0298 C*    SR. TO ADD ZZAMT TO HASH TOTALS IN ARRAYS INT & DEC.
0299 C           HASH      BEGSR                           *** HASH ***
0300 C*
0301 C           ZZAMT     COMP 0                      50
0302 C   50                Z-SUBZZAMT     ZZAMT
0303 C                     Z-ADDINT,S     ZZTOTI
0304 C                     Z-ADDDEC,S     ZZTOTD
0305 C                     EXSR GLZADD
0306 C                     Z-ADDZZTOTI    INT,S
0307 C                     Z-ADDZZTOTD    DEC,S
0308 C*
0309 C                     ENDSR
0310 C*
0311 C********************************************************************
0312 C**
0313 C**   ZDATE2 SR. TO CONVERT A DAY NUMBER TO DATE FORMATS.
0314 C**
0315 C**   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
0316 C**
0317 C           ZDATE2    BEGSR                           *** ZDATE2 ***
0318 C**
0319 C**   CLEAR DATE FIELDS.
0320 C                     Z-ADD0         ZDATE   60
0321 C                     MOVEL'       ' ZADATE  7
0322 C**
0323 C**   CALCULATION TO DEFINE INPUT DAY NUMBER.
0324 C                     Z-ADDZDAYNO    ZDAYNO  50
0325 C**
0326 C**   DETERMINE YEAR NUMBER.
0327 C**
0328 C**   ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
0329 C           ZDAYNO    SUB  1         ZDAYN1  50   99
0330 C   99                GOTO ZDEND2
0331 C**
0332 C**   CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
0333 C           ZDAYN1    DIV  1461      ZLYR    20
0334 C                     MVR            ZDAYN1           SAVE REMAINING
0335 C**                                                   DAYS
0336 C**   CALCULATE NUMBER OF REMAINING YEARS.
0337 C                     Z-ADD1         ZWRK2   20
0338 C           ZDTAG1    TAG                             ** ZDTAG1 TAG *
0339 C           ZDAYN1    COMP ZYDY,ZWRK2             90
0340 C  N90      ZWRK2     ADD  1         ZWRK2
0341 C  N90                GOTO ZDTAG1
0342 C           ZWRK2     SUB  1         ZWRK2          91LEAP YR, 91 ON
0343 C**
0344 C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
0345 C  N91      ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
0346 C**
0347 C**   CALCULATE ACTUAL YEAR NUMBER.
0348 C           ZLYR      MULT 4         ZWRK3   30
0349 C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
0350 C                     Z-ADDZWRK3     ZYEAR   20
0351 C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
0352 C**
0353 C**   DETERMINE MONTH NUMBER.
0354 C**
0355 C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
0356 C                     SETOF                     9293
0357 C   91      ZDAYN1    COMP 59                     9293
0358 C   91N92   ZDAYN1    SUB  1         ZDAYN1
0359 C**
0360 C**   CALCULATE MONTH NUMBER.
0361 C                     Z-ADD2         ZWRK2
0362 C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
0363 C           ZDAYN1    COMP ZMDY,ZWRK2             94
0364 C  N94      ZWRK2     ADD  1         ZWRK2
0365 C  N94                GOTO ZDTAG2
0366 C           ZWRK2     SUB  1         ZWRK2
0367 C**
0368 C                     Z-ADDZWRK2     ZMTH    20       MONTH
0369 C**
0370 C**   DETERMINE DAY OF MONTH.
0371 C**
0372 C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
0373 C           ZDAYN1    ADD  1         ZDAYN1
0374 C**
0375 C**   CALCULATE DAY OF MONTH.
0376 C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
0377 C**
0378 C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
0379 C   93      ZDAY      ADD  1         ZDAY
0380 C**
0381 C**   FORMAT DATE, DDMMYY OR MMDDYY.
0382 C  N98                MOVELZDAY      ZWRK4   40
0383 C   98                MOVE ZDAY      ZWRK4
0384 C  N98                MOVE ZMTH      ZWRK4
0385 C   98                MOVELZMTH      ZWRK4
0386 C                     MOVELZWRK4     ZDATE
0387 C                     MOVE ZYEAR     ZDATE
0388 C**
0389 C**   FORMAT ALPHA DATE, DDMMMYY.
0390 C           ZDAY      COMP 10                     95
0391 C                     MOVELZDAY      ZWRK5   5
0392 C   95                MOVEL' '       ZWRK5
0393 C                     MOVE ZMNM,ZMTH ZWRK5
0394 C                     MOVELZWRK5     ZADATE
0395 C                     MOVE ZYEAR     ZADATE
0396 C**
0397 C           ZDEND2    ENDSR                           * ZDEND2 ENDSR*
0398 C**
0260 C********************************************************************
0400 C* SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL (ZZTOTI,ZZTOTD)
0401 C*   IND '99' IS SET BY AN OVERFLOW ERROR
0402 C*
0403 CSR         GLZADD    BEGSR                           *** GLZADD ****
0404 C*
0405 CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
0406 CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
0407 C*
0408 C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
0409 CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
0410 CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
0411 C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
0412 C*
0413 CSR                   EXSR GLZSUM
0414 CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
0415 C*
0416 C********************************************************************
0417 C* SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINES -
0418 C*                        GLZADD, GLZSUB, GLZCMP.
0419 C*          PARAMETERS PASSED -
0420 C*                     I/P ZZAMTI ZZAMTD
0421 C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
0422 C*
0423 CSR         GLZSUM    BEGSR                           *** GLZSUM ****
0424 C*
0425 CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
0426 CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
0427 CSR                   SETOF                     919293
0428 CSR                   SETOF                     949599
0429 C*
0430 C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
0431 CSR         ZZAMTI    COMP 0                      9293
0432 CSR 93      ZZAMTD    COMP 0                      9293
0433 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0434 C*
0435 C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
0436 CSR         ZZTOTI    COMP 0                      9193
0437 CSR 93      ZZTOTD    COMP 0                      9193
0438 C*
0439 C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
0440 CSR 93                Z-ADDZZAMTI    ZZTOTI
0441 CSR 93                Z-ADDZZAMTD    ZZTOTD
0442 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0443 C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
0444 CSR 91N92
0445 CORN91 92             GOTO ZZOFPS
0446 C*
0447 CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
0448 CSR         ZZWK2     COMP 999                  93    '93' = CARRY
0449 CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
0450 C*
0451 CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
0452 CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
0453 CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
0454 CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
0455 C*
0456 C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
0457 CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
0458 CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
0459 CSRN99                Z-ADDZZWK2     ZZTOTD
0460 CSRN99                Z-ADDZZWK3     ZZTOTI
0461 C*
0462 C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
0463 CSR 99                Z-ADD0         ZZAMT  153
0464 CSR                   GOTO ZZSEND
0465 C*
0466 C* THE 'SIGNS' ARE DIFFERENT
0467 CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
0468 C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
0469 C*
0470 CSR 92                Z-SUBZZAMTI    ZZAMTI 150
0471 CSR 92                Z-SUBZZAMTD    ZZAMTD  30
0472 C*
0473 C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
0474 C*
0475 CSR 91                Z-SUBZZTOTI    ZZTOTI
0476 CSR 91                Z-SUBZZTOTD    ZZTOTD
0477 C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
0478 C*
0479 CSR         ZZTOTI    COMP ZZAMTI               93  95
0480 CSR 95      ZZTOTD    COMP ZZAMTD               93  95
0481 C*
0482 C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
0483 CSR 95                Z-ADD0         ZZTOTI
0484 CSR 95                Z-ADD0         ZZTOTD
0485 CSR 95                GOTO ZZSEND
0486 C*
0487 C* IF ZZTOT GT. ZZAMT.
0488 CSR 93      ZZAMTD    COMP ZZTOTD               94
0489 CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
0490 CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
0491 CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
0492 CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
0493 CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
0494 C*
0495 C*
0496 C* IF ZZAMT GT. ZZTOT.
0497 CSRN93      ZZTOTD    COMP ZZAMTD               94
0498 CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
0499 CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
0500 CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
0501 CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
0502 CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
0503 CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
0504 C*
0505 C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
0506 CSR                   SETOF                     94
0507 CSR 93 91
0508 CORN93 92             SETON                     94
0509 CSR 94                Z-SUBZZTOTI    ZZTOTI
0510 CSR 94                Z-SUBZZTOTD    ZZTOTD
0511 C**
0512 C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
0513 CSR 92                Z-SUBZZAMTI    ZZAMTI
0514 CSR 92                Z-SUBZZAMTD    ZZAMTD
0515 CSR         ZZSEND    TAG                             ** ZZSEND TAG *
0516 C**
0517 C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
0518 CSR                   SETOF                     96
0519 CSR         ZZTOTD    COMP 0                        91
0520 CSR 91      ZZTOTI    COMP 0                      96
0521 CSR 96                MOVE '.000-'   ZZNEGD  5
0522 CSR                   ENDSR                             ** ENDSR **
0523 C********************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C*
     C                     ENDSR
0597 C*****************************************************************
0544 O**DL0701P1H**203***02*25*35*******                                    E257
0544 O**DL0700P1H**203***02*25*35*******                              E257BHF348
0544 ODL0701P1H  203   02 25 35                                           BHF348
0545 O       OR  203   02 23 25
     O                                   24 'REFERENCE DL0701P1'
0547 O                         BJURPT    89
0548 O                                  104 'DATE'
0549 O                         BJMRDT   112
0550 O                                  123 'PAGE'
0551 O                         PAGE  Z  127
0552 O        H  1     02 25 35
0553 O       OR  1     02 23 25
0554 O                                   74 'FX DEALS BY VALUE DATE'
0555 O        H  2     02 25 35
0556 O       OR  2     02 23 25
0557 O                                   74 '----------------------'
0555 O        H  2     02 25 35
0556 O       OR  2     02 23 25
     O                                   12 'BRANCH'
     O***************          EBRC      16                                  E57
     O                         BRCA      16                                  E57
     O                         A8BRNM    48
     O/COPY WNCPYSRC,DL0701O001
0558 O        H  1     02 25 35
0559 O       OR  1     02 23 25
0560 O                                   64 'DEAL   DEAL   OPTION  --'
0561 O                                   86 '-------PURCHASE-------'
0562 O                                  101 'EXCHANGE'
0563 O                                  128 '---------SALE---------'
0564 O        H  1     02 25 35
0565 O       OR  1     02 23 25
0566 O                                   12 'CUSTOMER'
0567 O                                   59 'NUMBER  TYPE    FROM'
0568 O                                   86 'F/F CCY           AMOUNT'
0569 O                                  109 'RATE       CCY'
0570 O                                  128 'AMOUNT'
      *                                                                   CDL049
      * Display only if CDL049 is on                                      CDL049
     O        H  1     02 25 35                                           CDL049
     O       AND       11                                                 CDL049
     O       OR  1     02 23 25                                           CDL049
     O       AND       11                                                 CDL049
      *                                                                   CDL049
     O                                  102 'REF. RATE'                   CDL049
      *                                                                   CDL049
0571 O        H 12     02 L1 25
0572 O       OR 12     02 23 25
0573 O                                    4 'DATE'
0574 O                         VDATE     13
0575 O        D  1     02 25
0576 O                         CUNR       6
0577 O                         CRNM      27
0578 O                         CTWN      38
0579 O                         DLNO      45
0580 O                         DTYP      48
0581 O                                   49 '-'
0582 O                         DLST      51
     O                         CLAS      55                               CDL038
     O                         ODATE  B  63                               CDL038
     O                 44      FFIN      65                               CDL038
     O                N44      OCFF      65                               CDL038
0583 O*************************ODATE  B  60                               CDL038
0584 O*****************44******FFIN      63                               CDL038
0585 O****************N44******OCFF      63                               CDL038
0586 O                         ECCY      69
0587 O                 30      EAMT  1   86
0588 O                 31      EAMT      88 '   ,   ,   , 0 . '
0589 O                 32      EAMT      89 '  ,   ,   , 0 .  '
0590 O                 33      EAMT      90 ' ,   ,   , 0 .   '
0591 O                         EXRT     104 '   0 .        '
0592 O                         OTHC     109
0593 O                 40      OTHA  1  126
0594 O                 41      OTHA     128 '   ,   ,   , 0 . '
0596 O                 42      OTHA     129 '  ,   ,   , 0 .  '
0597 O                 43      OTHA     130 ' ,   ,   , 0 .   '
     O/COPY WNCPYSRC,DL0701O002
      *                                                                   CDL049
      * Display only if CDL049 is on                                      CDL049
     O        D  1     02 25 11                                           CDL049
     O                         #RRAT    104 '     0 .        '            CDL049
      *                                                                   CDL049
0317 O******* T 3      L2 25N36                                           LN0363
     O*******AND       22                                                 LN0363
0317 O        T 3      L2 26                                              LN0363
     O                                   66 '*** END OF REPORT FOR'
     O                                   77 ' BRANCH ***'
     O********                           71 '*** END OF BRANCH REPORT'
     O********                           75 ' ***'
0598 O**DL0701AUT**203***LR**************                                   E257
0598 O**DL0700AUT**203***LR**************                             E257BHF348
0598 ODL0701AUT  203   LR                                                 BHF348
0599 O                                   24 'REFERENCE DL0701AU'
0600 O                         BJURPT    89
0601 O                                  104 'DATE'
0602 O                         BJMRDT   112
0603 O                                  123 'PAGE'
0604 O                         PAGE1 Z  127
0605 O        T  1     LR
0606 O                                   74 'FX DEALS BY VALUE DATE'
0607 O        T  1     LR
0608 O                                   74 '----------------------'
0609 O        T  220   LRNU7
0610 O                                   18 'RUN CONTROLS'
0611 O        T  2     LRNU7
0612 O                                   59 'NUMBER'
0613 O                                   84 'VALUE'
0614 O        T  2     LRNU7
0615 O                                   27 'SORTED EVENTS FILE '
0616 O                                   40 'RECORDS INPUT'
0617 O                         NREC  3   59
0618 O                         INT,1 Z   81
0619 O                         DEC,1     84
0628 O        T        LRNU7
0629 O                                   31 'RECORDS LISTED IN THIS '
0630 O                                   37 'REPORT'
0631 O                         NRIT  3   59
0632 O                         INT,2 Z   81
0633 O                         DEC,2     84
0636 O        T 3      LRNU7N22
0637 O                                   59 '***   NO DETAILS TO '
0638 O                                   71 'REPORT   ***'
     O        T 3      LR U7
     O                                   71 '*** PRINT TERMINATED ***'
     O        T 1      LR U7
     O                                   57 'DATABASE ERROR'
     O                         DBASE     61
     O                                   67 'FILE'
     O                         DBFILE    78
     O        T 13     LR U7
     O                                   46 'KEY'
     O                         DBKEY     76
0317 O*****   T 3      LR                                                 E299
0318 O*****                              76 '*** END OF REPORT ***'       E299
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
