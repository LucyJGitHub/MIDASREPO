     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Deposits by maturity date prompt')            *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0710A - Deal Type/Subtype selection prompt                 *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CDL021 *C *CREATE  Date 03Feb04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL021 - Reports by Deal Type                                *
      *                                                               *
      *****************************************************************
      *
     FDL0710DFCF  E                    WORKSTN
     FFDDTSTL0IF  E           K        DISK
      *
      *****************************************************************
     E                    CPY@    1   1 80
     E/COPY WNCPYSRC,DLH00109
     E                    ERRMSG  1   5 50               ERROR MESSAGES                       CDL038
     E/COPY WNCPYSRC,DLH00110
     E********************ERRMSG  1   3 50               ERROR MESSAGES                       CDL038
      *****************************************************************
      /EJECT
      *
     ISCSARD    E DSSCSARDPD                                                                  CDL038
      * EXTERNAL DS FOR SAR DETAILS                                                           CDL038
      *                                                                                       CDL038
     IDSFDY     E DSDSFDY                                                                     CDL038
      * FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                                    CDL038
      *                                                                                       CDL038
     IPSDS       SDS
     I                                      244 253 SWSID
     I                                      254 263 USRID
      *
     I*@PARM******DS******************************4                                           CDL038
      **ONE*PARM*FOR*TYPE,*SUBTYPE*****************                                           CDL038
     I/COPY WNCPYSRC,DLH00117
     I@PARM       DS                              8                                           CDL038
      * ONE PARM FOR TYPE, SUBTYPE, CLASS                                                     CDL038
     I                                        1   2 TYP1
     I                                        3   4 TYP2
     I                                        5   8 TYP3                                      CDL038
     I/COPY WNCPYSRC,DLH00111
      *
     IRUNDT       DS                             13
     I                                        1   7 SRUNA
      /EJECT
      *
      *****************************************************************
      *
     C                     MOVEACPY@      BIS@   80
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
     C*                                                                                       CDL038
     C** DETERMINE IF CDL038 IS ON (EXTENDED DEAL SUB-TYPE)                                   CDL038
     C*                                                                                       CDL038
     C                     CALL 'AOSARDR0'                                                    CDL038
     C                     PARM *BLANKS   @RTCD   7                                           CDL038
     C                     PARM '*VERIFY' @OPTN   7                                           CDL038
     C                     PARM 'CDL038'  @SARD   6                                           CDL038
     C           SCSARD    PARM SCSARD    DSFDY                                               CDL038
     C*                                                                                       CDL038
     C           @RTCD     IFEQ *BLANKS                                                       CDL038
     C                     MOVE 'Y'       CDL038  1                                           CDL038
     C                     MOVE *ON       *IN65                                               CDL038
     C                     ELSE                                                               CDL038
     C                     MOVE 'N'       CDL038                                              CDL038
     C                     MOVE *OFF      *IN65                                               CDL038
     C                     END                                                                CDL038
     C/COPY WNCPYSRC,DLH00112
      *
      **   RECEIVE PRODUCTION OPTION, SELECTED VALUE AND BRANCH TYPE
      **   PARAMETERS FROM CALLING PROGRAM
      **
     C           *ENTRY    PLIST
     C                     PARM           @RSEQ   5
     C                     PARM           @RLEV   1
     C                     PARM           @RENT   3
     C                     PARM           @PARM
     C                     PARM           @RACT   1
     C                     PARM           @RCMD   1
      *
     C                     MOVE *BLANK    TYP1
     C                     MOVE *BLANK    TYP2
     C                     MOVE *BLANK    TYP3                                                CDL038
     C                     MOVE *BLANK    FLD2B
     C/COPY WNCPYSRC,DLH00113
      *
      **
      **
      **   (LOOP UNTIL EITHER A COMMAND KEY HIT OR VALID SCREEN FIELDS
      **    ENTERED)
      **
     C           *IN12     DOUEQ'1'
     C           *IN03     OREQ '1'
      **
     C                     EXFMTDL0710D0
     C                     MOVE '0'       *IN20
      **
      **   IF CMD/3 IS PRESSED, RETURN TO CALLING PROGRAM
      **
     C           *IN03     IFEQ '1'
     C                     MOVE 'E'       @RCMD
     C                     SETON                     LR
     C                     RETRN
     C                     END
      **
     C           *IN12     IFEQ '1'
     C                     MOVE 'F'       @RCMD
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C                     CALL 'ZA0250'
      **
     C                     WRITEDL0710D0
      **
     C                     EXSR #VDTYP
     C/COPY WNCPYSRC,DLH00114
      *
      ** IF NO ERRORS ON INPUT THEN COME OUT OF PROGRAM ELSE LOOP BACK
     C           *IN20     IFEQ '0'
     C/COPY WNCPYSRC,DLH00119
     C                     SETON                     LR
     C                     RETRN
     C                     END
      *
     C                     END
      /EJECT
      ********************************************************************
      **
      **   #VDTYP SUBROUTINE TO VALIDATE DEAL TYPE /SUBTYPE
      **
      ********************************************************************
     C           #VDTYP    BEGSR                           ** #VDTYP SR **
      **
     C           DTYPE     KLIST
     C                     KFLD           TYP1
     C                     KFLD           TYP2
     C                     KFLD           TYP3                                                CDL038
      **
     C           TYP1      IFNE *BLANKS
     C           TYP2      ANDNE*BLANKS
     C           CDL038    ANDEQ'N'                                                           CDL038
     C           TYP1      ORNE *BLANKS                                                       CDL038
     C           TYP2      ANDNE*BLANKS                                                       CDL038
     C           TYP3      ANDNE*BLANKS                                                       CDL038
     C           CDL038    ANDEQ'Y'                                                           CDL038
     C           DTYPE     CHAINFDDTSTL0             21
     C           *IN21     IFEQ '1'
     C           CDL038    IFEQ 'Y'                                                           CDL038
     C                     MOVELERRMSG,4  FLD2B                                               CDL038
     C                     ELSE                                                               CDL038
     C                     MOVELERRMSG,1  FLD2B
     C                     ENDIF                                                              CDL038
     C                     SETON                     20
     C                     END
     C                     END
      *
     C           TYP1      IFEQ *BLANKS
     C           TYP2      ANDNE*BLANKS
     C           TYP1      OREQ *BLANKS                                                       CDL038
     C           TYP3      ANDNE*BLANKS                                                       CDL038
     C           TYP2      OREQ *BLANKS                                                       CDL038
     C           TYP3      ANDNE*BLANKS                                                       CDL038
     C           TYP2      ORNE *BLANKS                                                       CDL038
     C           TYP3      ANDEQ*BLANKS                                                       CDL038
     C           CDL038    ANDEQ'Y'                                                           CDL038
     C           CDL038    IFEQ 'Y'                                                           CDL038
     C                     MOVELERRMSG,5  FLD2B                                               CDL038
     C                     ELSE                                                               CDL038
     C                     MOVELERRMSG,3  FLD2B
     C                     ENDIF                                                              CDL038
     C                     SETON                     20
     C                     END
      *
     C           TYP1      IFNE *BLANKS
     C           TYP2      ANDEQ*BLANK
     C           TYP3      ANDEQ*BLANKS                                                       CDL038
     C           DTYPE     SETLLFDDTSTL0               2223
     C           *IN22     IFEQ '0'
     C           *IN23     ANDEQ'0'
     C                     READ FDDTSTPD                 21
     C           *IN21     IFEQ '0'
     C           DTPE11    ANDEQTYP1
     C                     MOVE '**'      TYP2
     C                     ELSE
     C                     MOVELERRMSG,2  FLD2B
     C                     SETON                     20
     C                     END
     C                     ELSE
     C                     MOVELERRMSG,2  FLD2B
     C                     SETON                     20
     C                     END
     C                     END
      *
     C           TYP1      IFEQ *BLANKS
     C           TYP2      ANDEQ*BLANKS
     C           TYP3      ANDEQ*BLANKS                                                       CDL038
     C                     MOVE '**'      TYP1
     C                     MOVE '**'      TYP2
     C                     ENDIF
      **
     C                     ENDSR
      /EJECT
     C/COPY WNCPYSRC,DLH00115
      ********************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2004
**   ERRMSG - ERROR MESSAGES
INVALID DEAL TYPE/SUBTYPE
INVALID DEAL TYPE
DEAL TYPE MUST BE ENTERED
INVALID DEAL TYPE/SUBTYPE/CLASS                                                               CDL038
JUST DEAL TYPE MUST BE ENTERED OR ALL FIELDS                                                  CDL038
     X/COPY WNCPYSRC,DLH00116
