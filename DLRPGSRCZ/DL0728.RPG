     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Fixed Deposits Rollover History')             *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0728 - Fixed Deposits Rollover History                     *
      *                                                               *
      *  Function:  This program produces a report showing details    *
      *             of fixed deposits                                 *
      *                                                               *
      *  Called By: DLCnnn1 - DL Fixed Deposits Register              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 BUG7411            Date 01Jun05               *
      *                 CSW037A            Date 02May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CDL024  *CREATE    Date 31Mar04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance Receive Settlement Instructions             *
      *           (Recompiled)                                        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  BUG7411- CDL028 fields missing from DLGHISPD (Recompile)     *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CDL024 - Fixed Deposits Register                             *
      *                                                               *
      *****************************************************************
     FDEALSALLIF  E           K        DISK
     F            DEALSDBF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     F            MMDELDP0                          KIGNORE
     F            MMDENBP0                          KIGNORE
     F            MMDENSP0                          KIGNORE
     F            FXDEALP0                          KIGNORE
     F            DLBHISD0                          KIGNORE
     F            DLDHISD0                          KIGNORE
     F            DLEHISD0                          KIGNORE
     F            DLGHISD0                          KIGNORE
      * Fixed Deposits Including History File
      *
     FDL0728P1O   E                    PRINTER
     F                                              KINFDS SPOOL1
     FDL0728AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *                                                               *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for RED435P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for RED435AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IPPARM       DS                            100
      ***  Parameters passed into program
     I                                        1   60DLNUM
     I                                        7   7 TYPE
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      *
     ISDCUST    E DSSDCUSTPD
      ***  External data structures for Customer Details
      *
     IDSSDY     E DSDSSDY
     I** Second DS for access programs, Long data structure
     I*
     IDSFDY     E DSDSFDY
     I** First DS for access programs, Short data structure
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  ZDAT   -
      *  ZDAT   -
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLEV    1
     C                     PARM           PENT    3
     C                     PARM           PPARM 100
      *
      ***  Initialise LDA field.
     C                     MOVEL'DL0728'  DBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ***  RCF Processing for File.
     C                     EXSR RCFP1
     C                     EXSR RCFAU
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
     C           PROCES    BEGSR                           *** PROCES ***
      *
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN71
      * Process according to whether previous, future or all rollovers
      * are requested
      *
B1   C           TYPE      IFEQ 'P'
     C           TYPE      OREQ 'A'
      *
     C                     MOVE *ON       *IN70
      * Access deals file and print details, then if rollover of
      * deal number not blank, access deals file and get details
      * of this deal. Continue until no more.
     C           DLNUM     CHAINDEALSALL             80
B2   C           *IN80     IFEQ *ON
     C                     MOVE 'DEALSALL'DBFILE
     C                     MOVE DLNUM     DBKEY
     C                     MOVE '001'     DBASE
     C                     EXSR *PSSR
X2   C                     ELSE
     C                     WRITEHEAD
     C                     EXSR REPORT
      *
     C           RODN      DOWNE0
     C           RODN      CHAINDEALSALL             80
B3   C           *IN80     IFEQ *ON
     C                     MOVE 'DEALSALL'DBFILE
     C                     MOVE RODN      DBKEY
     C                     MOVE '002'     DBASE
     C                     EXSR *PSSR
E3   C                     ENDIF
     C                     EXSR REPORT
     C           RODN      CHAINDEALSALL             80
      *
     C                     ENDDO
      *
E2   C                     ENDIF
E1   C                     ENDIF
      *
B1   C           TYPE      IFEQ 'F'
     C           TYPE      OREQ 'A'
      *
     C                     MOVE *ON       *IN71
      * Access deals file and print details, then if rolled over by
      * deal number not blank, access deals file and get details
      * of this deal. Continue until no more.
     C           DLNUM     CHAINDEALSALL             80
B2   C           *IN80     IFEQ *ON
     C                     MOVE 'DEALSALL'DBFILE
     C                     MOVE DLNUM     DBKEY
     C                     MOVE '003'     DBASE
     C                     EXSR *PSSR
X2   C                     ELSE
     C                     WRITEHEAD
     C                     EXSR REPORT
      *
     C           RBDN      DOWNE0
     C           RBDN      CHAINDEALSALL             80
B3   C           *IN80     IFEQ *ON
     C                     MOVE 'DEALSALL'DBFILE
     C                     MOVE RBDN      DBKEY
     C                     MOVE '004'     DBASE
     C                     EXSR *PSSR
E3   C                     ENDIF
     C                     EXSR REPORT
     C           RBDN      CHAINDEALSALL             80
      *
     C                     ENDDO
      *
E2   C                     ENDIF
E1   C                     ENDIF
      *
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
     C           REPORT    BEGSR                           *** REPORT ***
      *
      * Move Details to report fields
     C                     MOVE DLNO      PDLNO
     C                     MOVE CNUM      PCNUM
      *
      *  Get customer shortname
     C                     MOVELCNUM      @KEY1
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE BBCSSN    PSNAM
     C                     ELSE
     C                     MOVE *BLANKS   PSNAM
     C                     ENDIF
      *
     C           *IN70     IFEQ *ON
     C                     MOVE RODN      PRODN
     C                     ENDIF
     C           *IN71     IFEQ *ON
     C                     MOVE RBDN      PRBDN
     C                     ENDIF
      *
     C                     MOVE FSSOFD    PSRFD
     C                     MOVE CCY       PCCY
      *
     C                     Z-ADDPCPL      ZFLD
     C                     CALL 'ZSEDIT'
     C                     PARM           ZFLD   150
     C                     PARM           ZDECS   10
     C                     PARM 'J'       ZECODE  1
     C                     PARM           ZOUT21 21
     C                     MOVE ZOUT21    PPCPL
      *
     C                     CALL 'ZDATE2'
     C                     PARM           VDAT
     C                     PARM           BJDFIN
     C                     PARM           ZDATE   60
     C                     PARM           ZADATE  7
     C                     MOVE ZDATE     PVDAT
      *
     C                     CALL 'ZDATE2'
     C                     PARM           MDAT
     C                     PARM           BJDFIN
     C                     PARM           ZDATE   60
     C                     PARM           ZADATE  7
     C                     MOVE ZDATE     PMDAT
      *
      * Edit interest rate
     C                     MOVE INTR      ZFIELD
     C                     Z-ADD7         ZDECS
     C                     CALL 'ZEDIT'
     C                     PARM           ZFIELD 16
     C                     PARM           ZDECS   10
     C                     MOVELZFIELD    PINTR
      *
     C                     MOVE ICBS      PICBS
      *
      * Edit interest paid to date
     C                     Z-ADDIPRD      ZFLD15
     C                     Z-ADD0         ZDECS
     C                     CALL 'ZSEDIT'
     C                     PARM           ZFLD15 150
     C                     PARM           ZDECS   10
     C                     PARM 'J'       ZECODE  1
     C                     PARM           ZOUT21 21
     C                     MOVELZOUT21    PIPRD
      *
     C                     MOVE FSBLKA    PBLKI
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEAD
     C                     ENDIF
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEAD
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILPY
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C                     WRITEDBERROR
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      *  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      *  If Error occurs during ZSFILE processing, then return to the
      *  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      *  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      *  If Error occurs during ZSFILE processing, then return to the
      *  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2004
