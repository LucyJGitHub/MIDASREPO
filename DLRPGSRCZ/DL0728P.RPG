     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Fixed Deposits Rollover History Prompt')      *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL0728P - Fixed Deposits Rollover History Prompt             *
      *                                                               *
      *  Function:  This will be the prompt program for DL0726        *
      *             Fixed Deposit Rollover History                    *
      *                                                               *
      *  Called by:                                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 MD023613           Date 22Nov13               *
      *                 MD023407           Date 12Nov13               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 BUG7411            Date 01Jun05               *
      *                 CDL028             Date 07Feb05               *
      *                 CDL024  *CREATE    Date 31Mar04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance Receive Settlement Instructions             *
      *           (Recompiled)                                        *
      *  MD023613 - Incorrect field is highlighted for the error      *
      *             message                                           *
      *  MD023407 - Error message is not showing in Fixed deposit     *
      *             register history menu                             *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  BUG7411- CDL028 fields missing from DLGHISPD (Recompile)     *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CDL024 - Fixed Deposit Register                              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *    F U N C T I O N   O F   I N D I C A T O R S                *
      *                                                               *
      *                                                               *
      *  41 - Error indicator for Deal Number                         *
      *  42 - Error indicator for Rollover type reported              *
      *                                                               *
      *---------------------------------------------------------------*
      * U7 U8 LR                                                      *
      *****************************************************************
     FDL0728DFCF  E                    WORKSTN
     FDEALSALLIF  E           K        DISK
     F            DEALSDBF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     F            MMDELDP0                          KIGNORE
     F            MMDENBP0                          KIGNORE
     F            MMDENSP0                          KIGNORE
     F            FXDEALP0                          KIGNORE
     F            DLBHISD0                          KIGNORE
     F            DLDHISD0                          KIGNORE
     F            DLEHISD0                          KIGNORE
     F            DLGHISD0                          KIGNORE
      *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
     ILDA       E DS
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 SWSID
     I                                      254 263 SUSER
      *
     IPPARM       DS                            100
      ***  Parameters passed into program
     I                                        1   6 #DLNO
     I                                        7   7 #TYPE
      ** External DS for bank details
     ISDBANK    E DSSDBANKPD
      *
      *  First DS for access program, short Data Structure
     IDSFDY     E DSDSFDY
      *
      * Second DS for access program, long data structure
     IDSSDY     E DSDSSDY
      *
      /EJECT
      *****************************************************************
      *  MAIN
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLEV    1
     C                     PARM           PENT    3
     C                     PARM           PPARM 100
     C                     PARM           PACT    1
     C                     PARM           PCMD    1
      ** Perform initial processing
      *
     C                     EXSR INIT
      *
      ** Display Prompt Screen
      *
     C                     EXSR DISPLY
      *
     C           *IN03     IFEQ '1'
     C                     MOVE 'E'       PCMD
     C                     ENDIF
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *  INIT - Initialisation
      *****************************************************************
      *
     C           INIT      BEGSR
      *
     C                     MOVEACPY@      CPY2@  80
      *
      **   Define the LDA for error handling
     C           *NAMVAR   DEFN           LDA
      *
     C                     SETOF                     4142
      *
      * Set up message file name for error message execution, and
      * rundate.
      *
     C                     MOVEL'DRSMM'   ZAMSGF
     C                     MOVEL'DL0728P' DBPGM
      *
      *    Access Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG'    @RTCD   7
     C                     PARM '*FIRST'  @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           *************
     C                     MOVEL'*FIRST'  DBKEY            * DBERR 001 *
     C                     Z-ADD1         DBASE            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE 'Y'       W#ERR1  1
      *
      * Clear message queue for screen.
      *
     C                     EXSR CLEARM
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  DISPLY - Display Prompt screen
      *****************************************************************
      *
     C           DISPLY    BEGSR
      *
     C                     WRITEDL0728F0                   Header
     C                     EXSR SCREEN
      *
     C           *IN03     DOWEQ*OFF
     C           W#ERR1    ANDEQ'Y'
      *
     C                     MOVE 'N'       W#ERR1
     C                     MOVE *OFF      *IN41                                             MD023613
     C                     MOVE *OFF      *IN42                                             MD023613
      *
      * Validate screen fields.
      *
     C                     EXSR VALID
      *
      * Display error message.
      *
     C           W#ERR1    IFEQ 'Y'
     C                     EXSR SCREEN
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  VALID - Validate Screen input
      *****************************************************************
      *
     C           VALID     BEGSR
      *
      *   Validate Deal Number
     C                     MOVE #DLNO     NDLNO   60
      *                                                                                     MD023407
     C           NDLNO     IFEQ *ZEROS                                                      MD023407
     C                     MOVEL'MMM0111' ZAMSID                                            MD023407
     C                     EXSR ZASNMS                                                      MD023407
     C                     MOVE *ON       *IN41                                             MD023407
     C                     MOVE 'Y'       W#ERR1                                            MD023407
     C                     ENDIF                                                            MD023407
      *                                                                                     MD023407
     C           NDLNO     CHAINDEALSALL             80
     C           *IN80     IFEQ *ON
     C**********           MOVEL'XXX0103' ZAMSID                                            MD023407
     C                     MOVEL'MMM0167' ZAMSID                                            MD023407
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN41
     C                     MOVE 'Y'       W#ERR1
     C                     ENDIF
      *
      *   Validate Rollover type field
     C           #TYPE     IFNE 'A'
     C           #TYPE     ANDNE'P'
     C           #TYPE     ANDNE'F'
     C**********           MOVEL'XXX0104' ZAMSID                                            MD023407
     C                     MOVEL'MMM0359' ZAMSID                                            MD023407
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN42
     C                     MOVE 'Y'       W#ERR1
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * SCREEN  - Display Screen
      *****************************************************************
     C           SCREEN    BEGSR
      *
     C                     SETON                     45    Msg SFLEND
     C                     WRITE#MSGCTL                    Messages
     C                     EXFMTDL0728F1                   Main Screen
     C                     EXSR CLEARM
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  ZASNMS - Sends messages to the program's message queue       *
      *****************************************************************
     C           ZASNMS    BEGSR
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     ENDIF
      *
     C                     MOVE *BLANKS   ZAMSDA
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * CLEARM  -  Clears the message queue                           *
      *****************************************************************
     C           CLEARM    BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *  *PSSR - Exception error processing
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** PSSR **
      *
     C           @DUMP     IFEQ *BLANK
     C                     DUMP
     C                     MOVE 'Y'       @DUMP   1
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
** CPY@
(c) Misys International Banking Systems Ltd. 2001
