     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL daily purge of time/call statements')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  RPG/DL0768 - Purge records from DLTCSTPD                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL012  *CREATE    Date 12Aug03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CDL012 - Interest Statement for Call/Time Deposits.          *
      *                                                               *
      *****************************************************************
      *
     FDLTCSTPDUF  E                    DISK
      ** SD file containing int. statement retention period
      *
      *================================================================
      *
      ** Data structure to retrieve bank details
      *
     ISDBANK    E DSSDBANKPD
      *
      ** First DS for Access Programs, Short Data Structure
      *
     IDSFDY     E DSDSFDY
      *
      ** Program status data structure
      *
     IPGMDS     ESDSY2PGDSP
      *
     ILDA         DS                            256
      * Local Data Area
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     ISVAL1       DS                            200
     I                                        1   1 SVAL11
      ** DS for AOSVALR0
      *
     I              'DepIntRetenPerd'     C         SVALKK
      *
      *================================================================
      *
      * Use access program AOBANKR0 to retrieve run date
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** If error in access program, exit via *PSSR subroutine
      *
     C           @RTCD     IFNE *BLANKS                    B001
     C                     MOVEL##PGM     AUDBP  10           1
     C                     MOVE *BLANKS   AUDBK   5           1
     C                     MOVEL'SDBANKPD'AUDBS  10        ***************
     C                     Z-ADD001       AUDBL   30       * DBERROR 001 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF                           E001
      *
      ** Get the Deposit Interest Retention Period
      *
      ** Check if 'DepIntRetenPerd' is On
     C                     CALL 'AOSVALR0'
     C                     PARM           @RTCD
     C                     PARM SVALKK    SVALK1 20
     C                     PARM           SVAL1 200
     C                     PARM *BLANK    SVALK2 20
     C                     PARM           SVAL2 200
     C                     PARM *BLANK    SVALK3 20
     C                     PARM           SVAL3 200
     C                     PARM *BLANK    SVALK4 20
     C                     PARM           SVAL4 200
     C                     PARM *BLANK    SVALK5 20
     C                     PARM           SVAL5 200
     C                     PARM *BLANK    SVALK6 20
     C                     PARM           SVAL6 200
     C                     PARM *BLANK    SVALK7 20
     C                     PARM           SVAL7 200
     C                     PARM *BLANK    SVALK8 20
     C                     PARM           SVAL8 200
     C                     PARM *BLANK    SVALK9 20
     C                     PARM           SVAL9 200
     C                     PARM *BLANK    SVALK0 20
     C                     PARM           SVAL10200
     C           @RTCD     IFNE *BLANK
     C                     MOVEL##PGM     AUDBP  10           1
     C                     MOVELSVALKK    AUDBK   5           1
     C                     MOVEL'SDSVALPD'AUDBS  10        ***************
     C                     Z-ADD002       AUDBL   30       * DBERROR 002 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
     C                     MOVE SVAL11    GDIRP   10
     C           GDIRP     IFGT 0
      *
     C           GDIRP     MULT 366       UGRTND  50
     C           BJRDNB    SUB  UGRTND    UGCTOF  50
      *
     C           1         SETLLDLTCSTPD
     C                     READ DLTCSTP0                 55
      *
     C           *IN55     DOWEQ*OFF
      *
     C           GMDAT     IFLE UGCTOF
     C                     DELETDLTCSTP0
     C                     ENDIF
      *
     C                     READ DLTCSTP0                 55
      *
     C                     ENDDO
      *
     C                     ENDIF
      *
     C           END       TAG
     C                     SETON                     LR
      *================================================================
      * ERROR PROCESSING
      *================================================================
     C           *PSSR     BEGSR
      *
     C                     DUMP
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM
     C                     MOVELAUDBK     DBKEY
     C                     MOVELAUDBS     DBFILE
     C                     Z-ADDAUDBL     DBASE
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
