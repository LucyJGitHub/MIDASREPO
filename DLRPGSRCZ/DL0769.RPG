     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL I/C prompt for Time/Call statements')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  RPG/DL0769 - Prompt Control for Time/Call Deposits Report    *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 BUG1988            Date 28Apr04               *
      *                 CDL012  *CREATE    Date 12Aug03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  BUG1988- Replace message IDs with existing ones from core    *
      *  CDL012 - Interest Statements for Time/Call Deposits          *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicator Function Summary                                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *       03         Exit program.                                *
      *       05         Refresh screen fields                        *
      *                                                               *
      *****************************************************************
     FDL0769DFCF  E                    WORKSTN
      ** Display file for prompt
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     ISDCUST    E DSSDCUSTPD
      ** Data structure for accessing Customer details
      *
     ISDCOMP    E DSSDCOMPPD
      ** Data structure for accessing Company details
      *
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          XXDFAC
      ** Data structure for accessing Branch details
      *
     IDSSDY     E DSDSSDY
      ** Data structure for passing data
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 ##WSID
     I                                      254 263 ##USER
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     I            DS
     I                                        1  18 @DATA
     I                                        1   6 @FRM
     I                                        7  12 @TO
     I                                       13  18 @CUST
      *
      ** Loop until F3 is taken or Inputs are valid
      *
     C           *IN03     DOWEQ*OFF
      *
      ** Display program messages
     C                     WRITEDL0769C1
      *
      ** Display main screen for input
     C                     EXFMTDL0769F0
      *
      ** Clear messages from program message queue
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
      ** Process according user action
     C                     SELEC
      *
      ** If F3 pressed, exit.
      *
     C           *IN03     WHEQ *ON
     C                     MOVE 'E'       CMD
      *
      ** If F5 pressed, initialise screen fields
      *
     C           *IN05     WHEQ *ON
     C                     MOVE *OFF      *IN61
     C                     MOVE *OFF      *IN62
     C                     MOVE *OFF      *IN63
     C                     MOVE *BLANK    SPRDFR
     C                     MOVE *BLANK    SPRDTO
     C                     MOVE *BLANK    SCUSNO
      *
      ** If Enter pressed, valid inputs
      *
     C                     OTHER
     C                     EXSR SRVALD
     C           FLDSOK    IFEQ 'Y'
     C                     LEAVE
     C                     ENDIF
      *
     C                     ENDSL
     C                     ENDDO
      *
      ** Terminate and return to calling pgm
     C                     MOVE *ON       *INLR
     C                     RETRN
      *================================================================
      ** Validated inputs.
      *================================================================
     C           SRVALD    BEGSR
      *
      ** Input fields are valid
     C                     MOVE 'Y'       FLDSOK  1
      *
      ** Set off the error indicators
     C                     MOVE *OFF      *IN61
     C                     MOVE *OFF      *IN62
     C                     MOVE *OFF      *IN63
      *
      ** Initialise work variables
     C                     Z-ADD0         UGFRM   50
     C                     Z-ADD0         UGTO    50
      *
      ** Verify mandatory field from date
      *
     C           SPRDFR    IFEQ *BLANKS
     C                     MOVE 'N'       FLDSOK
     C                     MOVE *ON       *IN61
     C                     MOVEL'From'    ZAMSDA
     C**********           MOVEL'DLM1016' ZAMSID                                             BUG1988
     C                     MOVEL'DLM0141' ZAMSID                                             BUG1988
     C                     MOVEL'GBDRSMM' ZAMSGF
     C                     EXSR ZASNMS
      *
     C                     ELSE
     C                     MOVE SPRDFR    ZDATE
     C                     CALL 'ZDATE1'
     C                     PARM           ZERR    7
     C                     PARM           ZDATE   60
     C                     PARM AGDFF     ZDATF   1
     C                     PARM           ZDAYNO  50
      *
      **  Error returned from ZDATE1
      *
     C           ZERR      IFNE *BLANKS
     C                     MOVE 'N'       FLDSOK
     C                     MOVE *ON       *IN61
     C                     MOVEL'From'    ZAMSDA
     C**********           MOVEL'DLM1017' ZAMSID                                             BUG1988
     C                     MOVEL'DLM0026' ZAMSID                                             BUG1988
     C                     MOVEL'GBDRSMM' ZAMSGF
     C                     EXSR ZASNMS
     C                     ELSE
     C                     Z-ADDZDAYNO    UGFRM
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Verify mandatory field To date.
      *
     C           SPRDTO    IFEQ *BLANKS
     C                     MOVE 'N'       FLDSOK
     C                     MOVE *ON       *IN62
     C                     MOVEL'To'      ZAMSDA
     C**********           MOVEL'DLM1018' ZAMSID                                             BUG1988
     C                     MOVEL'DLM0141' ZAMSID                                             BUG1988
     C                     MOVEL'GBDRSMM' ZAMSGF
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE SPRDTO    ZDATE
     C                     CALL 'ZDATE1'
     C                     PARM           ZERR    7
     C                     PARM           ZDATE   60
     C                     PARM AGDFF     ZDATF   1
     C                     PARM           ZDAYNO  50
      *
      **  Error returned from ZDATE1
      *
     C           ZERR      IFNE *BLANKS
     C                     MOVE 'N'       FLDSOK
     C                     MOVE *ON       *IN62
     C                     MOVEL'To'      ZAMSDA
     C**********           MOVEL'DLM1019' ZAMSID                                             BUG1988
     C                     MOVEL'DLM0028' ZAMSID                                             BUG1988
     C                     MOVEL'GBDRSMM' ZAMSGF
     C                     EXSR ZASNMS
     C                     ELSE
     C                     Z-ADDZDAYNO    UGTO
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** FROM DATE cannot be after TO DATE.
      *
     C           UGTO      IFNE 0
     C           UGFRM     ANDNE0
     C           UGTO      ANDLTUGFRM
     C                     MOVE 'N'       FLDSOK
     C                     MOVE *ON       *IN61
     C                     MOVE *ON       *IN62
     C**********           MOVEL'DLM1020' ZAMSID                                             BUG1988
     C                     MOVEL'DLM0030' ZAMSID                                             BUG1988
     C                     MOVEL'GBDRSMM' ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** Verify optional field Customer number.
      *
     C           SCUSNO    IFNE *BLANKS
      *
     C                     MOVELSCUSNO    WCUST
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM           WCUST  10
     C                     PARM           WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'N'       FLDSOK
     C                     MOVE *ON       *IN63
     C**********           MOVEL'DLM1021' ZAMSID                                             BUG1988
     C                     MOVEL'DLM0007' ZAMSID                                             BUG1988
     C                     MOVEL'GBDRSMM' ZAMSGF
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVELBBCUST    SCUSNO
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Move input to outgoing parameters
     C           FLDSOK    IFEQ 'Y'
     C                     MOVE SPRDFR    @FRM
     C                     MOVE SPRDTO    @TO
     C                     MOVE SCUSNO    @CUST
     C                     MOVEL@DATA     RDATA
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      * Initial subroutine.                                           *
      *****************************************************************
     C           *INZSR    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C                     PARM           LEV     1
     C                     PARM           ENT     3
     C                     PARM           RDATA 100
     C                     PARM           ACT     1
     C                     PARM           CMD     1
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read dtaara/RUNDAT
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           AGDFF     IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ELSE
     C                     MOVE *OFF      *IN98
     C                     ENDIF
      *
      ** setup the screen fields
     C                     MOVE ##USER    SUSER
     C                     MOVE AGMRDT    SRUNA
     C                     MOVE ##WSID    SWSID
      *
     C                     ENDSR
      *
      *================================================================
      * Send message to program's message queue
      *================================================================
     CSR         ZASNMS    BEGSR
     C           ZAPGM     IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGM
     C                     END
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message ID
     C                     PARM           ZAMSGF 10        Message file
     C                     PARM           ZAMSDA132        Message data
     C                     PARM           ZAMSTP  7        Message type
      *
      ** Clear all fields for default mechanism next time
     C                     MOVEL*BLANK    ZAPGM
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSGF
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
      *
     C                     ENDSR
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2003
