     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL take-on proc. for time/call stmts.')          *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  RPG/DL0770 - ZAP DLTCSTPD WITH EXISTING INTEREST DETAILS     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CDL096             Date 22Sep14               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL016             Date 13May04               *
      *                 BUG2636            Date 13May04               *
      *                 CDL012  *Create    Date 13Aug03               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL016 - Autumatic Rollover of Fixed Term Loan/Deposit       *
      *  BUG2636 - Add entry parameters required for initialisation   *
      *            programs.                                          *
      *  CDL012 - Interest Statements for Time/Call Deposits          *
      *         - EXTRA CONDITIONS FOR BACK-VALUED DEALS              *
      *         - INTRODUCE EXTRA FIELDS IN DLTCSTPD.                 *
      *                                                               *
      *****************************************************************
      *
     FDLTCSTPDO   E                    DISK
     FDEALSDC IF  E                    DISK
     FDEALDCV0IF  E           K        DISK
     F            DEALSDCF                          KRENAMEDLDCSTMT
      *
      *================================================================
     IDEALSDCF                                                                                CDL016
     I              FSROLI                          XROLI                                     CDL016
      *                                                                                       CDL016
     ISDCURR    E DSSDCURRPD
     ISDBRCH    E DSSDBRCHPD
      *
      * First DS for Access Programs, Short Data Structure
     IDSFDY     E DSDSFDY
      *
     IPGMDS     ESDSY2PGDSP
      *
     ILDA         DS                            256
      * Local Data Area
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *================================================================
     C           *LIKE     DEFN RECI      FSROLI
      *
     C           *ENTRY    PLIST                                                             BUG2636
     C                     PARM           @RTCD   7                                          BUG2636
     C                     PARM           IPMODE  1                                          BUG2636
      *                                                                                      BUG2636
     C                     MOVE *BLANKS   @RTCD                                              BUG2636
     C                     MOVE *BLANK    IPMODE                                             BUG2636
      *                                                                                      BUG2636
     C           1         SETLLDEALSDC
     C                     READ DEALSDCF                 55
      *
     C           *IN55     DOWEQ*OFF
      *
     C           DTYP      IFEQ 'TD'
     C           DTYP      OREQ 'CD'
      *
     C           SLID      IFNE VDAT
     C           IPFR      ANDNE' '
     C           RECI      ANDNE'R'
      *
     C           IPFR      OREQ ' '
     C           RECI      ANDNE'D'
     C           RECI      ANDNE'R'
      *
     C                     Z-ADDSLID      GNIDT
      *
     C           NIDT      IFEQ 0
     C           IPFR      ANDEQ' '
     C                     Z-ADDMDAT      GNIDT
     C                     ENDIF
      *
     C           SLID      IFEQ 0
     C           MDAT      ANDNE0
     C                     Z-ADDMDAT      GNIDT
     C                     ENDIF
      *
     C                     Z-ADDVDAT      GVDAT
     C                     Z-ADDMDAT      GMDAT
     C                     Z-ADDIPRD      GDINT
     C           RECI      IFEQ 'M'
     C           RECI      OREQ 'N'
     C           DTYP      IFEQ 'TD'
     C                     Z-ADDTOTI      GDINT
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDDLNO      GDLNO
     C                     MOVE CNUM      GCUST
     C                     Z-ADDINTR      GINTR
     C                     Z-ADDPCPL      GPCPL
     C                     MOVE CCY       GDCCY
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM GDCCY     @AJCD   3
     C           SDCURR    PARM SDCURR    DSFDY
      *
     C           @RTCD     IFEQ *BLANK
     C                     Z-ADDA6PRRT    GEXCR
     C                     ELSE
     C                     MOVEL##PGM     AUDBP  10           1
     C                     MOVE *BLANKS   AUDBK   5           1
     C                     MOVEL'SDCURRPD'AUDBS  10        ***************
     C                     Z-ADD002       AUDBL   30       * DBERROR 002 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
     C                     MOVE BRCA      GBRCD
      *
     C                     CALL 'AOBRCHR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM BRCA      @DSNB   3
     C           SDBRCH    PARM SDBRCH    DSFDY
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE A8CMCD    GCMCD
     C                     ELSE
     C                     MOVEL##PGM     AUDBP  10           1
     C                     MOVE *BLANKS   AUDBK   5           1
     C                     MOVEL'SDBRCHPD'AUDBS  10        ***************
     C                     Z-ADD003       AUDBL   30       * DBERROR 003 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
     C                     MOVE DTYP      GDTYP
     C                     Z-ADDORED      GORED
     C           DTYP      IFEQ 'TD'
     C           RBDN      ANDNE0
     C                     Z-ADDRBDN      GRBDN
     C           RBDN      CHAINDEALDCV0             88
     C           *IN88     IFEQ *OFF
     C                     Z-ADDORED2     GRBOR
     C                     ELSE
     C           RECI      IFNE 'D'
     C                     Z-ADD0         GRBDN
     C                     Z-ADD0         GRBOR
     C                     ELSE
     C                     MOVEL##PGM     AUDBP  10           1
     C                     MOVE *BLANKS   AUDBK   5           1
     C                     MOVEL'DEALDCV0'AUDBS  10        ***************
     C                     Z-ADD012       AUDBL   30       * DBERROR 012 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
     C                     ENDIF
      *
     C                     ELSE
     C                     Z-ADD0         GRBDN
     C                     Z-ADD0         GRBOR
     C                     ENDIF
      *
     C                     MOVE FSROLI    GROLI
      *
      *
     C                     WRITEDLTCSTP0
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ DEALSDCF                 55
      *
     C                     ENDDO
      *
     C           END       TAG
     C                     SETON                     LR
      *================================================================
      * ERROR PROCESSING
      *================================================================
     C           *PSSR     BEGSR
      *
     C                     DUMP
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM
     C                     MOVELAUDBK     DBKEY
     C                     MOVELAUDBS     DBFILE
     C                     Z-ADDAUDBL     DBASE
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
