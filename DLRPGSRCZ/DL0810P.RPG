     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Parent/Nonparent FX by maty Pmpt')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Dealing Module
     F*                                                               *
     F*   DL0810P - PARENT/NONPARENT FX MATURITY DATE REPORT          *
     F*              (PRODUCTION OPTION PROMPT)                       *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E36223             Date 30Apr92               *
     F*                                                               *
     F*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
     F*   E36223 - PARAMETER SHOULD BE CORRECTLY DISPLAYED            *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FDL0810DFCF  E                    WORKSTN
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*       03         F3 TAKEN ON DISPLAY FILE
     F*       10         F10 TAKEN ON DISPLAY FILE
     F*       12         F12 TAKEN ON DISPLAY FILE
     F*       20         SETS ON ERROR MESSAGE
     F*       30         DELETE, ENABLE F10
     F*       40         PREVENT INPUT INTO FIELD ON DISPLAY FILE
     F*
     F*****************************************************************
     E                    CPY@    1   1 80
     F*****************************************************************
     E/EJECT
     I*
     IPSDS       SDS
     I*    HOLDS WORKSTATION AND USER ID
     I                                      244 246 WSID
     I                                      254 263 USER
     I*
     I@PARM       DS                            100
     I* PARAMETER FOR FULL OR EXCEPTION REPORTING
     I                                        1   1 @OPTN
     IRUNDT       DS                             13                       E36223
     I                                        1   7 SRUNA                 E36223
     I/EJECT
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           E36223
     C                     IN   RUNDT                                     E36223
     C                     MOVEACPY@      BIS@   80
     C**
     C** RECEIVE STANDARD PARAMETERS
     C**
     C           *ENTRY    PLIST
     C                     PARM           @RSEQ   5
     C                     PARM           @RLEV   1
     C                     PARM           @RENT   3
     C                     PARM           @PARM
     C                     PARM           @RACT   1
     C                     PARM           @RCMD   1
     C**
     C**   INITIALISE SCREEN FIELDS
     C*
     C**  ACTION CODES E,D DO NOT ALLOW INPUT TO SCREEN FIELDS
     C**  ACTION CODES D ALLOW F10
     C           @RACT     IFEQ 'D'
     C           @RACT     OREQ 'E'
     C           @RACT     OREQ 'A'                                       E36223
     C                     MOVEL@PARM     @OPTN
     C           @OPTN     IFEQ 'P'                                       E36223
     C                     MOVE '1'       @PRNT                           E36223
     C                     ELSE                                           E36223
     C                     MOVE '2'       @PRNT                           E36223
     C                     END                                            E36223
     C********** @RACT     COMP 'D'                      30               E36223
     C           @RACT     IFEQ 'D'                                       E36223
     C           @RACT     OREQ 'E'                                       E36223
     C                     SETON                         40
     C                     END
     C                     END                                            E36223
     C**
     C**   DISPLAY FOOTER
     C                     WRITEDL810PD0
     C**
     C**   (LOOP UNTIL EITHER A COMMAND KEY HIT OR VALID SCREEN FIELDS
     C**    ENTERED)
     C**
     C           *IN12     DOUEQ'1'
     C           *IN03     OREQ '1'
     C           *IN10     OREQ '1'
     C**
     C                     EXFMTDL810PS0
     C**
     C**   IF CMD/3 IS PRESSED, RETURN TO CALLING PROGRAM
     C**
     C           *IN03     IFEQ '1'
     C                     MOVE 'E'       @RCMD
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C**
     C           *IN12     IFEQ '1'
     C                     MOVE 'F'       @RCMD
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C*
     C**   IF F10 IS PRESSED, RETURN TO CALLING PROGRAM
     C           *IN10     IFEQ '1'
     C                     MOVE 'D'       @RCMD
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C**
     C**   RESET INDICATORS
     C**
     C                     MOVE '0'       *IN20
     C**
     C                     WRITEDL810PS0
     C*
     C**
     C** DEPENDING ON LEVEL OF REPORTING REQUIRED CHANGE PARAMETER
     C**
     C*    Parent only Reporting
     C           @PRNT     IFEQ '1'
     C                     MOVE 'P'       @OPTN
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C**
     c*    Non Parent Reporting only
     C           @PRNT     IFEQ '2'
     C                     MOVE 'N'       @OPTN
     C                     SETON                     LR
     C                     RETRN
     C                     END
     C*
     C** ERROR IF SELECTED OPTION IS NOT 1 OR 2
     C*
     C                     MOVE '1'       *IN20
     C**
     C                     END
     C**
     C********************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
