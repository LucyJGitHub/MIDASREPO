     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Future Deal Amendments by Brnch Rept')        *
     F*****************************************************************
     F*                                                               *
     F*  Midas - DEALING MODULE
     F*                                                               *
     F*  DL0831  -  FUTURE DEAL AMENDMENTS REPORT BY BRANCH.          *
     F*             THIS PROGRAM IS BASED ON DL0830                   *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 27Aug96               *
      *                 051158             Date 08Dec93               *
     F*                 LN0662             DATE 01FEB91               *
     F*                 LN0746             DATE 18SEP90               *
     F*                 LN0246             DATE 23MAY90               *
     F*                 E333               DATE 23APR90               *
     F*                 E299               DATE 23MAR90               *
     F*                 E257               DATE 08MAR90               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (recompiled)                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
0010 F*  S01408 - Hook DL0831CCP1 added                                  *
0010 F*         - Hook DL0831CCP2 added                                  *
0010 F*         - Hook DL0831FFP1 added                                  *
0010 F*         - Hook DL0831FFP2 added                                  *
0010 F*         - Hook DL0831IIP1 added                                  *
0010 F*         - Hook DL0831OOP1 added                                  *
0010 F*         - Hook DL0831OOP2 added                                  *
0010 F*         - Hook DL0831OOP3 added                                  *
     F*  051158 - Level break should be on Booking Branch not on      *
     F*           Settlement Branch                                   *
     F*  LN0662 - CHANGES TO SDCURRL0 AND L1                          *
     F*  LN0746 - P1 & AU FILES CHANGED TO SAME NAME AS PROGRAM       *
     F*           DUMMY FILES CREATED                                 *
     F*  LN0246 - DUE TO SPACING 'NO DETAILS TO REPORT' WAS PRINTED   *
     F*           ON SECOND PAGE                                      *
     F*  E333   - CHAIN TO BRANCH FILE ONLY IF BRANCH IS NON-BLANK    *
     F*           SETT METH.'00'                                      *
     F*  E299   - 'AU' REPORT SHOULD NOT WRITE 'END OF REPORT'        *
     F*  E257   - CHANGE SPOOL FILE NAME TO DL0830P1/AU               *
     F*                                                               *
0011 F*****************************************************************
     F/SPACE 3
0026 FSDEAML1 IP  E           K        DISK
     F/COPY WNCPYSRC,DL0831FFP1                                           S01408
0012 F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*SDCURRL0IF  E           K        DISK                              LN0662
     FSDCURRL1IF  E           K        DISK                               LN0662
0028 F***DL0831P1O   F     132     OF     PRINTER      KINFDS SPOOL     UCE257
0028 F***830P1O   F     132     OF     PRINTER      KINFDS SPOOL UC E257  LN0746
0028 FDL0831P1O   F     132     OF     PRINTER      KINFDS SPOOL      UC  LN0746
0028 F***DL0831AUO   F     132     OA     PRINTER      KINFDS SPOOLU      E257
0028 F***830AUO   F     132     OA     PRINTER      KINFDS SPOOLU   E257  LN0746
0028 FDL0831AUO   F     132     OA     PRINTER      KINFDS SPOOLU         LN0746
     F/SPACE 3
0012 F* ID F  C  H  L    FUNCTION OF INDICATORS
0013 F* 01            SDEAML1 - HEADER
0014 F* 02                    - DETAIL
0015 F* 03                    - TRAILER
0017 F*       20      FIRST CYCLE CALCULATIONS DONE.
0018 F*       21      DEAL AMENDMENT AMOUNT POSITIVE.
0019 F*       22      SEQ. NO. LESS THAN 998.
0021 F*       30      CURRENCY HAS O D.P.
0022 F*       31                   1 D.P.
0023 F*       32                   2 D.P.
0024 F*       33                   3 D.P.
     F*       34      DETAILS TO REPORT.
0025 F*       40      EOF OF READ.
     F/COPY WNCPYSRC,DL0831FFP2                                           S01408
0025 F*       55      PRTF DL0831P1 OPEN FOR OUTPUT.
0025 F*       56      CHAIN FAIL.
     E                    CPY@    1   1 80
0029 E                    CCYA      150  3
0030 E                    DPA       150  1 0
0031 E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
0032 E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
0033 E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
0034 I*
0036 I*
0037 IDEAMSDHF    01
0035 I*    DEAL AMENDMENTS FILE - HEADER RECORD.
0048 I*
0051 IDEAMSDIF    02
0049 I*    DEALS AMENDMENT FILE - DETAIL RECORD.
     I**************OSBR**********************      BRCA  L1              051158
     I                                              BRCA  L1              051158
0078 I*
0082 IDEAMSDJF    03
0080 I*    DEALS AMENDMENT FILE - TRAILER RECORD.
0113 I*
     I/COPY WNCPYSRC,DL0831IIP1                                           S01408
     I**
     ISPOOL       DS
     I**   FILE INFORMATION DATA STRUCTURE
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I**
     ISPOOLU      DS
     I**   FILE INFORMATION DATA STRUCTURE
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     I*
     I@BANK     E DSSDBANKPD
     I** EXTERNAL DS FOR BANK DETAILS
     I*
     I@BRCH     E DSSDBRCHPD
     I** EXTERNAL DS FOR BRANCH DETAILS
     I*                                                                   S01117
     IDSFDY     E DSDSFDY                                                 S01194
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01194
     I*
     I/EJECT
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      *
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C                     PARM           BRNCH   3         BRANCH OR ALL
0146 C*    FIRST CYCLE CALCULATIONS.
0147 C   20                GOTO NOTFC
0148 C                     SETON                     20
     C                     MOVEACPY@      BIS@   80
     C*
     C** READ BANK DETAILS VIA ACCESS PROGRAM
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           @BANK     PARM @BANK     DSFDY
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE 10        ************
     C                     MOVEL'01'      DBASE   3        * DBERR 01 *
     C                     MOVEL@OPTN     DBOPTN  7        ************
     C                     MOVEL@OPTN     DBKEY  29
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     GOTO END
     C                     END
     C*
0640 C*    CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
     C           BJDFIN    COMP 'M'                      98MMDDYY, 98 ON
0157 C*
0158 C*    SET UP ARRAYS OF CCY/DP.
0159 C                     EXSR CCYARR
0160 C*
0161 C*    DETAIL CALCULATIONS.
0162 C           NOTFC     TAG                             ** NOTFC TAG **
0163 C*
0164 C*    PROCESS DETAIL RECORDS ONLY.
     C   02      RECI      COMP 'D'                      02
0165 C  N02                GOTO END
     C**
     C** IF A REQUIRED BRANCH AND A LEVEL BREAK, OPEN DL0831P1
     C           *INL1     IFEQ '1'
     C*******              CLOSEDL0831P1                                  E257
     C***********          CLOSEDL0830P1                             E257 LN0746
     C                     CLOSEDL0831P1                                  LN0746
     C           BRNCH     IFEQ 'ALL'
     C           BRNCH     OREQ BRCA
     C                     SETON                     55
     C*******              OPEN DL0831P1                                  E257
     C***********          OPEN DL0830P1                             E257 LN0746
     C                     OPEN DL0831P1                                  LN0746
     C*
     C* REPORT SPOOL FILE GENERATION
     C*                                                                   S01117
      ** TRANSFER BINARY DATA INTO NUMERIC FIELD                          S01117
     C                     Z-ADDSFNUM     ZSNUM   60                      S01117
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM BRCA      @PARM   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM *BLANK    ZSERR   1                       S01117
     C*                                                                   S01117
     C           ZSERR     IFEQ 'Y'                                       S01117
     C                     SETON                     U7U8LR               S01117
     C                     RETRN                                          S01117
     C                     END                                            S01117
     C*
     C                     Z-ADD0         PAGE
     C**
     C** GET BRANCH NAME VIA ACCESS PGM AOBRCHR0
     C**
     C           BRCA      IFNE *BLANKS                                   E333
      *                                                                   E333
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*KEY    '@OPTN   7
     C                     PARM BRCA      @DSNB   3
     C********** @BRCH     PARM @BRCH     DSFDY                                               CGL029
     C           @BRCH     PARM @BRCH     DSSDY                                               CGL029
     C**
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SDBRCHPD'DBFILE 10
     C                     MOVEL'02'      DBASE   3        **************
     C                     MOVEL@OPTN     DBOPTN  7        *DBERROR 002 *
     C                     MOVELBRCA      DBKEY  29        **************
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     GOTO END
     C                     END
      *                                                                   E333
     C                     END                                            E333
      *                                                                   E333
     C**
     C                     ELSE
     C                     SETOF                     55
     C                     END
     C                     END
     C/COPY WNCPYSRC,DL0831CCP1                                           S01408
0166 C*
0167 C*    INCREMENT COUNT OF RECORDS INPUT.
0168 C           NREC      ADD  1         NREC    50
0169 C*
0170 C*    ADD DEAL AMENDMENT AMOUNT TO HASH TOTAL.
0171 C           DAAM      DIV  1000      ZZAMT      21
0172 C   21                EXSR GLZADD
0173 C  N21                EXSR GLZSUB
0174 C*
0175 C*    DO NOT REPORT RECORDS WITH SEQUENCE NO. GREATER THAN
0176 C*    OR EQUAL TO 998
0177 C           DASN      COMP 998                    22
0178 C  N22                GOTO END
0179 C*
0180 C*    CONVERT INPUT DATE TO DDMMMYY FORMAT
0181 C                     Z-ADDORED      ZDAYNO
0182 C                     EXSR ZDATE2
0183 C                     MOVE ZADATE    IDATE   7
0184 C*
0185 C*    CONVERT VALUE DATE TO DDMMMYY FORMAT
0186 C                     Z-ADDVDAT      ZDAYNO
0187 C                     EXSR ZDATE2
0188 C                     MOVE ZADATE    VDATE   7
0189 C*
0190 C*    SET INDICATOR FOR NO. OF D.P. FOR CCY.
0191 C                     MOVE CCY       XCCY
0192 C                     EXSR CCYDEP
0193 C           XDECP     COMP 1                      3031
0194 C           XDECP     COMP 2                    33  32
0195 C*
0196 C*    INCREMENT COUNT OF RECORDS REPORTED.
0197 C           NRIT      ADD  1         NRIT    50
     C                     SETON                     34
0198 C                     GOTO END
0199 C*
0204 C           END       TAG                             ** END TAG **
0205 C*
0206 C*    TOTAL CALCULATIONS.
      *
     C* TOTALS SPOOL FILE RECORDED BY RCF
     C*                                                                   S01117
      ** TRANSFER BINARY DATA INTO NUMERIC FIELD                          S01117
     CLR                   Z-ADDSFNUM     ZSNUMU  60                      S01117
     CLR                   CALL 'ZSFILE'
     CLR                   PARM           SEQ
     CLR                   PARM *BLANKS   @PARM   3
     CLR                   PARM           SFILEU
     CLR                   PARM           ZSNUMU
     CLR                   PARM *BLANK    ZSERR                           S01117
     C*                                                                   S01117
     CLR         ZSERR     IFEQ 'Y'                                       S01117
     CLR                   SETON                     U7U8                 S01117
     CLR                   RETRN                                          S01117
     CLR                   END                                            S01117
      *
0208 CLR U7                GOTO ENDLR
0209 C*
0210 C*    COMPARE INPUT AND CALCULATED TOTALS
0211 CLR         NLRA      COMP NREC                 U8U8
0212 CLRNU8      VLAF      COMP ZZTOTI               U8U8
0213 CLRNU8      VLAL      COMP ZZTOTD               U8U8
     C*
0215 CLR         ENDLR     TAG                             ** ENDLR TAG **
0216 C********************************************************************
0217 C**
0218 C**   SR. TO SET UP AN ARRAY OF CURRENCIES AND AN ARRAY OF
0219 C**   DECIMAL PLACES FOR THOSE CURRENCIES.
0220 C**
0221 C           CCYARR    BEGSR                           *** CCYARR  ***
0222 C**
0223 C**   SET ARRAY INDEX TO ZERO.
0224 C                     Z-ADD0         X       30
0225 C**
0347 C**   SET LOWER LIMIT AND READ CURRENCY FILE
0421 C           *LOVAL    SETLL@A6REA3
0229 C**
0233 C**   LOOP TO READ CURRENCY DETAIL RECORDS.
0234 C           XLOOP     TAG                             ** XLOOP TAG **
     C                     READ @A6REA3                  99
0237 C**
0238 C**   END IF ALL CURRENCY RECORDS PROCESSED
0239 C   99                GOTO XEND
0240 C**
0247 C**   INCREMENT ARRAY INDEX.
0248 C           X         ADD  1         X
0249 C**
0250 C**   MOVE DATA INTO ARRAYS.
0251 C                     MOVE A6CYCD    CCYA,X
0252 C                     MOVE A6NBDP    DPA,X
0253 C**
0254 C**   CHECK FOR FULL ARRAY.96 ON IF FULL.
0255 C           X         COMP 150                      96
0256 C**
0257 C**   GO TO READ NEXT RECORD.
0258 C  N96                GOTO XLOOP
0259 C**
0260 C           XEND      TAG                             ** XEND TAG  **
0261 C                     ENDSR
0262 C**
0263 C********************************************************************
0264 C**
0265 C**   SR. TO OBTAIN NO. OF DECIMAL PLACES FOR A CURRENCY.
0266 C**
0267 C           CCYDEP    BEGSR                           *** CCYDEP  ***
0268 C**
0269 C**   SET ARRAY INDEX TO ONE AND RESET ERROR INDICATOR.
0270 C                     Z-ADD1         X            99
0271 C**
0272 C**   INITIALIZE INPUT-OUTPUT FIELDS.
0273 C                     Z-ADD0         XDECP   10
0274 C                     MOVE XCCY      XCCY    3
0275 C**
0276 C**   LOOK UP NO. OF DECIMAL PLACES FOR CURRENCY.
0277 C**   99 SETON FOR INVALID CURRENCY.
0278 C           XCCY      LOKUPCCYA,X                   95
0279 C   95                MOVE DPA,X     XDECP
0280 C  N95                SETON                     99
0281 C**
0282 C                     ENDSR
0283 C**
0284 C********************************************************************
0285 C* SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL (ZZTOTI,ZZTOTD)
0286 C*   IND '99' IS SET BY AN OVERFLOW ERROR
0287 C*
0288 CSR         GLZADD    BEGSR                           *** GLZADD ****
0289 C*
0290 CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
0291 CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
0292 C*
0293 C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
0294 CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
0295 CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
0296 C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
0297 C*
0298 CSR                   EXSR GLZSUM
0299 CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
0300 C*
0301 C********************************************************************
0302 C* SUBROUTINE TO SUBTRACT AN AMOUNT (ZZAMT) FROM THE TOTAL
0303 C*                                                   (ZZTOTI,ZZTOTD)
0304 C*  IND '99' IS SET BY AN OVERFLOW ERROR
0305 CSR         GLZSUB    BEGSR                           *** GLZSUB ****
0306 C* PROCESSING. JUST REVERSE THE 'SIGN' AND ADD
0307 CSR                   Z-SUBZZAMT     ZZAMT  153       REVERSE 'SIGN'
0308 CSR                   EXSR GLZADD                       AND 'ADD'
0309 CSR                   Z-SUBZZAMT     ZZAMT            RESTORE 'SIGN'
0310 CSR                   ENDSR
0311 C********************************************************************
0312 C* SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINES -
0313 C*                        GLZADD, GLZSUB, GLZCMP.
0314 C*          PARAMETERS PASSED -
0315 C*                     I/P ZZAMTI ZZAMTD
0316 C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
0317 C*
0318 CSR         GLZSUM    BEGSR                           *** GLZSUM ****
0319 C*
0320 CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
0321 CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
0322 CSR                   SETOF                     919293
0323 CSR                   SETOF                     949599
0324 C*
0325 C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
0326 CSR         ZZAMTI    COMP 0                      9293
0327 CSR 93      ZZAMTD    COMP 0                      9293
0328 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0329 C*
0330 C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
0331 CSR         ZZTOTI    COMP 0                      9193
0332 CSR 93      ZZTOTD    COMP 0                      9193
0333 C*
0334 C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
0335 CSR 93                Z-ADDZZAMTI    ZZTOTI
0336 CSR 93                Z-ADDZZAMTD    ZZTOTD
0337 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0338 C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
0339 CSR 91N92
0340 CORN91 92             GOTO ZZOFPS
0341 C*
0342 CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
0343 CSR         ZZWK2     COMP 999                  93    '93' = CARRY
0344 CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
0345 C*
0346 CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
0347 CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
0348 CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
0349 CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
0350 C*
0351 C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
0352 CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
0353 CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
0354 CSRN99                Z-ADDZZWK2     ZZTOTD
0355 CSRN99                Z-ADDZZWK3     ZZTOTI
0356 C*
0357 C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
0358 CSR 99                Z-ADD0         ZZAMT  153
0359 CSR                   GOTO ZZSEND
0360 C*
0361 C* THE 'SIGNS' ARE DIFFERENT
0362 CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
0363 C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
0364 C*
0365 CSR 92                Z-SUBZZAMTI    ZZAMTI 150
0366 CSR 92                Z-SUBZZAMTD    ZZAMTD  30
0367 C*
0368 C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
0369 C*
0370 CSR 91                Z-SUBZZTOTI    ZZTOTI
0371 CSR 91                Z-SUBZZTOTD    ZZTOTD
0372 C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
0373 C*
0374 CSR         ZZTOTI    COMP ZZAMTI               93  95
0375 CSR 95      ZZTOTD    COMP ZZAMTD               93  95
0376 C*
0377 C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
0378 CSR 95                Z-ADD0         ZZTOTI
0379 CSR 95                Z-ADD0         ZZTOTD
0380 CSR 95                GOTO ZZSEND
0381 C*
0382 C* IF ZZTOT GT. ZZAMT.
0383 CSR 93      ZZAMTD    COMP ZZTOTD               94
0384 CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
0385 CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
0386 CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
0387 CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
0388 CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
0389 C*
0390 C*
0391 C* IF ZZAMT GT. ZZTOT.
0392 CSRN93      ZZTOTD    COMP ZZAMTD               94
0393 CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
0394 CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
0395 CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
0396 CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
0397 CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
0398 CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
0399 C*
0400 C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
0401 CSR                   SETOF                     94
0402 CSR 93 91
0403 CORN93 92             SETON                     94
0404 CSR 94                Z-SUBZZTOTI    ZZTOTI
0405 CSR 94                Z-SUBZZTOTD    ZZTOTD
0406 C**
0407 C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
0408 CSR 92                Z-SUBZZAMTI    ZZAMTI
0409 CSR 92                Z-SUBZZAMTD    ZZAMTD
0410 CSR         ZZSEND    TAG                             ** ZZSEND TAG *
0411 C**
0412 C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
0413 CSR                   SETOF                     96
0414 CSR         ZZTOTD    COMP 0                        91
0415 CSR 91      ZZTOTI    COMP 0                      96
0416 CSR 96                MOVE '.000-'   ZZNEGD  5
0417 CSR                   ENDSR                             ** ENDSR **
0418 C********************************************************************
0419 C**   ZDATE2 SR. TO CONVERT A DAY NUMBER TO DATE FORMATS.
0420 C**
0421 C**   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
0422 C**
0423 C           ZDATE2    BEGSR                           *** ZDATE2 ***
0424 C**
0425 C**   CLEAR DATE FIELDS.
0426 C                     Z-ADD0         ZDATE   60
0427 C                     MOVEL'       ' ZADATE  7
0428 C**
0429 C**   CALCULATION TO DEFINE INPUT DAY NUMBER.
0430 C                     Z-ADDZDAYNO    ZDAYNO  50
0431 C**
0432 C**   DETERMINE YEAR NUMBER.
0433 C**
0434 C**   ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
0435 C           ZDAYNO    SUB  1         ZDAYN1  50   99
0436 C   99                GOTO ZDEND2
0437 C**
0438 C**   CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
0439 C           ZDAYN1    DIV  1461      ZLYR    20
0440 C                     MVR            ZDAYN1           SAVE REMAINING
0441 C**                                                   DAYS
0442 C**   CALCULATE NUMBER OF REMAINING YEARS.
0443 C                     Z-ADD1         ZWRK2   20
0444 C           ZDTAG1    TAG                             ** ZDTAG1 TAG *
0445 C           ZDAYN1    COMP ZYDY,ZWRK2             90
0446 C  N90      ZWRK2     ADD  1         ZWRK2
0447 C  N90                GOTO ZDTAG1
0448 C           ZWRK2     SUB  1         ZWRK2          91LEAP YR, 91 ON
0449 C**
0450 C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
0451 C  N91      ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
0452 C**
0453 C**   CALCULATE ACTUAL YEAR NUMBER.
0454 C           ZLYR      MULT 4         ZWRK3   30
0455 C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
0456 C                     Z-ADDZWRK3     ZYEAR   20
0457 C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
0458 C**
0459 C**   DETERMINE MONTH NUMBER.
0460 C**
0461 C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
0462 C                     SETOF                     9293
0463 C   91      ZDAYN1    COMP 59                     9293
0464 C   91N92   ZDAYN1    SUB  1         ZDAYN1
0465 C**
0466 C**   CALCULATE MONTH NUMBER.
0467 C                     Z-ADD2         ZWRK2
0468 C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
0469 C           ZDAYN1    COMP ZMDY,ZWRK2             94
0470 C  N94      ZWRK2     ADD  1         ZWRK2
0471 C  N94                GOTO ZDTAG2
0472 C           ZWRK2     SUB  1         ZWRK2
0473 C**
0474 C                     Z-ADDZWRK2     ZMTH    20       MONTH
0475 C**
0476 C**   DETERMINE DAY OF MONTH.
0477 C**
0478 C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
0479 C           ZDAYN1    ADD  1         ZDAYN1
0480 C**
0481 C**   CALCULATE DAY OF MONTH.
0482 C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
0483 C**
0484 C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
0485 C   93      ZDAY      ADD  1         ZDAY
0486 C**
0487 C**   FORMAT DATE, DDMMYY OR MMDDYY.
0488 C  N98                MOVELZDAY      ZWRK4   40
0489 C   98                MOVE ZDAY      ZWRK4
0490 C  N98                MOVE ZMTH      ZWRK4
0491 C   98                MOVELZMTH      ZWRK4
0492 C                     MOVELZWRK4     ZDATE
0493 C                     MOVE ZYEAR     ZDATE
0494 C**
0495 C**   FORMAT ALPHA DATE, DDMMMYY.
0496 C           ZDAY      COMP 10                     95
0497 C                     MOVELZDAY      ZWRK5   5
0498 C   95                MOVEL' '       ZWRK5
0499 C                     MOVE ZMNM,ZMTH ZWRK5
0500 C                     MOVELZWRK5     ZADATE
0501 C                     MOVE ZYEAR     ZADATE
0502 C**
0503 C           ZDEND2    ENDSR                           * ZDEND2 ENDSR*
0504 C**
     C/COPY WNCPYSRC,DL0831CCP2                                           S01408
0505 C**
0273 C********************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C*
     C                     ENDSR
0597 C*****************************************************************
0507 O****DL0831P1H  203   L1 55                                          E257
0507 O****DL0830P1H  203   L1 55                                     E257 LN0746
0507 ODL0831P1H  203   L1 55                                              LN0746
0508 O       OR  203   OF 55
0509 O                                   24 'REFERENCE DL0831P1'
0510 O                         BJURPT    89
0511 O                                  104 'DATE'
0512 O                         BJMRDT   112
0513 O                                  122 'PAGE'
0514 O                         PAGE  Z  127
0515 O        H  1     L1 55
0516 O       OR  1     OF 55
0517 O                                   57 'OUTSTANDING DEAL '
0518 O                                   79 'AMENDMENTS BY CUSTOMER'
0519 O        H  2     L1 55
0520 O       OR  2     OF 55
0521 O                                   64 '------------------------'
0522 O                                   79 '---------------'
0519 O        H  2     L1 55
0520 O       OR  2     OF 55
     O                                   12 'BRANCH'
     O                         BRCA      16
     O                         A8BRNM    48
0523 O        H  1     L1 55
0524 O       OR  1     OFNLR 55
0525 O                                   24 'CUSTOMER DEAL      INPUT'
0526 O                                   50 'VALUE   SEQ  AMENDMENT'
0527 O                                   69 'SPREAD/     CCY'
0528 O                                   86 'AMOUNT'
     O/COPY WNCPYSRC,DL0831OOP1                                           S01408
0529 O        H  2     L1 55
0530 O       OR  2     OFNLR 55
0531 O                                   24 'NUMBER    NO.      DATE'
0532 O                                   48 'DATE   NO.     TYPE'
0533 O                                   59 'RATE'
     O/COPY WNCPYSRC,DL0831OOP2                                           S01408
0534 O        D  1     02 22 55
0535 O                         CNUM       7
0536 O                         DLNO      15
0537 O                         IDATE     25
0538 O                         VDATE     34
0539 O                         DASN      39
0540 O                         AMTP      47
0541 O                         RTSP  4   63
0542 O                         CCY       69
0543 O                 30      DAAM  2   86
0544 O                 31      DAAM      88 '   ,   ,   ,   .0'
0545 O                 32      DAAM      89 '  ,   ,   ,   . 0'
0546 O                 33      DAAM      90 ' ,   ,   ,   .  0'
     O/COPY WNCPYSRC,DL0831OOP3                                           S01408
     O        T 3      L1 55
     O*********                          71 '*** END OF BRANCH REPORT'
     O*********                          75 ' ***'
     O                                   66 '*** END OF REPORT FOR'
     O                                   77 ' BRANCH ***'
0507 O****DL0831AUT  203   LR                                             E257
0507 O****DL0830AUT  203   LR                                        E257 LN0746
0507 ODL0831AUT  203   LR                                                 LN0746
0509 O                                   24 'REFERENCE DL0831AU'
0510 O                         BJURPT    89
0511 O                                  104 'DATE'
0512 O                         BJMRDT   112
0513 O                                  122 'PAGE'
0514 O                         PAGE1 Z  127
0515 O        T  1     LR
0517 O                                   57 'OUTSTANDING DEAL '
0518 O                                   79 'AMENDMENTS BY CUSTOMER'
0519 O        T  2     LR
0521 O                                   64 '------------------------'
0522 O                                   79 '---------------'
0548 O        T  220   LRNU7
0549 O                                   18 'RUN CONTROL'
0550 O        T  2     LRNU7
0551 O                                   59 'NUMBER'
0552 O                                   84 'VALUE'
0553 O        T  2     LRNU7
0554 O                                   25 'RECORDS PROCESSED'
0555 O                         NREC  3   59
0556 O                         ZZTOTIZ   81
0557 O                         ZZTOTD    84
0558 O        T  2     LRNU7
0559 O                                   29 'DEAL AMENDMENTS FILE '
0560 O                                   43 'CONTROL TOTALS'
0561 O                         NLRA  3   59
0562 O                         VLAF  Z   81
0563 O                         VLAL      84
0564 O        T 3      LR U8NU7
0565 O                                   72 '***  DIFFERENCE   ***'
     O******* T   12  N34 LR                                              LN0232
     O        T 3     N34 LR                                              LN0232
0567 O                                   60 '***   NO DETAILS TO '
0568 O                                   72 'REPORT   ***'
0569 O        T 3      LR U7 U8
     O                                   71 '*** PRINT TERMINATED ***'
     O        T 2      LR U7 U8
     O                                   57 'DATABASE ERROR'
     O                         DBASE     61
     O                                   67 'FILE'
     O                         DBFILE    78
     O        T 13     LR U7 U8
     O                                   49 'OPTION'
     O                         DBOPTN    57
     O                                   66 'KEY'
     O                         DBKEY     96
     O******  T 3      LRNU7                                              E299
     O******                             72 '***  END OF REPORT  ***'     E299
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
