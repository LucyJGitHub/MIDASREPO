     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL OIS Settlement Rerun File Maintenance')       *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL1648 - OIS Settlement Rerun File Maintenance               *
      *                                                               *
      *  Function:  This program will remove records that have matured*
      *             more than 3 months ago from the settlement rerun  *
      *             file DLOISRPD                                     *
      *                                                               *
      *  Called By: DLC1648 - OIS Settlement Rerun File Maintenance   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL099             Date 06Oct17               *
      *  Prev Amend No. CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 BUG7411            Date 01Jun05               *
      *                 CDL028             Date 07Feb05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007  *CREATE    Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  BUG7411- CDL028 fields missing from DLGHISPD (Recompile)     *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *   SRINIT - Initialisation of Program                          *
      *   SRPROC - Main Processing                                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   80  - READ DLOISRPD                                         *
      *   85  - CHAIN DEALS                                           *
      *   86  - CHAIN DEALSH                                          *
      * 90-99 - Used by Standard Subroutines                          *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      *   99  - Date Format not valid.                                *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     FDLOISRPDUF  E           K        DISK
      *
      ** Logical file built over DEALSDG
     FDEALS   IF  E           K        DISK
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDCF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDFF                          KIGNORE
      *
      ** Logical file built over DLGHISPD
     FDEALSH  IF  E           K        DISK
     F            DEALSDBF                          KIGNORE
     F            DEALSDCF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     F            DLBHISD0                          KIGNORE
     F            DLCHISD0                          KIGNORE
     F            DLDHISD0                          KIGNORE
     F            DLEHISD0                          KIGNORE
      *
      *****************************************************************
      /EJECT
      *
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      *****************************************************************
      /SPACE 3
      *
     IDSFDY     E DSDSFDY
      *
      ** SAR descriptions
     ISCSARD    E DSSCSARDPD
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
     ILDA       E DSLDA                         256
      *
      ** Data structure for Bank details
     ISDBANK    E DSSDBANKPD
      *
      *****************************************************************
      *                                                               *
      **  M A I N    P R O C E S S I N G                              *
      *                                                               *
      *****************************************************************
      *
      ** Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ** Perform Main Processing
      *
     C                     EXSR SRPROC
      *
     C                     SETON                         LR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT -   Initial Processing                                *
      *                                                               *
      *  Calls      - AOBANKR0                                        *
      *                                                               *
      *  Called By  - Main Processing                                 *
      *                                                               *
      *****************************************************************
      *
      ** Perform initial processing
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Initialise LDA field.
      *
     C                     MOVEL'DL1648'  DBPGM
     C           *NAMVAR   DEFN           LDA
      *
      ** Obtain Midas Run Date from SDBANKPD
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE
     C                     Z-ADD1         DBASE
     C                     MOVEL'*FIRST'  DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Establish control date a month before Run Date
      *
     C           BJRDNB    SUB  90        WCLDT   50
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on)
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPROC -   Perform Main processing                           *
      *                                                               *
      *  Calls      - NONE                                            *
      *                                                               *
      *  Called By  - Main Processing                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR
      *
      ** Read through the settlement rerun file DLOISRPD
      *
     C                     READ DLOISRPD                 80
      *
     C           *IN80     DOWEQ'0'
     C           L2DLNO    CHAINDEALS                85
      *
     C           *IN85     IFEQ '1'
     C           L2DLNO    CHAINDEALSH               86
      *
     C           *IN86     IFEQ '0'
      *
      ** Remove deal record from the settlement file DLOISRPD
      ** if it has matured more than 90 days before the run date
      *
     C           MDAT      IFLE WCLDT
     C                     DELETDLOISRD0
     C                     ENDIF
      *
     C                     ELSE
     C                     DELETDLOISRD0
     C                     ENDIF
      *
     C                     ELSE
      *
     C           RECI      IFEQ 'M'
     C           MDAT      ANDLEWCLDT
     C                     DELETDLOISRD0
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ DLOISRPD                 80
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WERROR    IFEQ *BLANK
     C                     MOVE 'Y'       WERROR  1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
