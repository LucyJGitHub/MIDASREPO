     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL OIS input cycle settlement')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL1650 - OIS Input Cycle Settlement                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD053295           Date 13Apr19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CDL099             Date 06Oct17               *
      *                 MD047993           Date 23Sep17               *
      *                 MD035545           Date 11Sep15               *      
      *                 CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 AR996895           Date 25Nov12               *
      *                 CRE073             Date 06Dec10               *
      *                 263666             Date 01Feb10               *
      *                 CER059             Date 19Jul10               *
      *                 CER054             Date 07Apr09               *
      *                 CER016A            Date 19May08               *
      *                 CER048             Date 19May08               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CAP205             Date 08Mar10               *
      *                 CAP204             Date 09Feb10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 246141             Date 26Mar07               *
      *                 164847             Date 30Nov06               *
      *                 BUG12990           Date 05Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 242286             Date 25Sep06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CIR013             Date 21Apr05               *
      *                 CAS008             Date 16Jun04               *
      *                 CDL028             Date 07Feb05               *
      *                 CGL014             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 217684             Date 12May03               *
      *                 CRE009             Date 03Sep02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 209171             Date 13Mar02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007 *CREATE     Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD053295 - Lending loan with Interest Calculation Basis      *
      *             method 3 (30/360) has wrong interest payment      *
      *             amount for month of March (Recompile)             *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  MD047993 - Modified MD035545. Removed dependency on Accrue   *
      *             on Last Day indicator.                            *
      *  MD035545 - LE: Interest Calculation in Leap year. Consider   *
      *             Accrue on Last Day Indicator for method 6.        *      
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  AR996895 - Incorrect acrrual interest calculation            *
      *             (Child: AR996897) (Recompile)                     *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  263666 - Recompiled due to changes in ZINTDYZ2               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax (Recompile)            *
      *  CER016A - German Interest Calculation: Upgarde of FGE059     *
      *            to Midas Plus (Recompile)                          *
      *  CER048 - German Features - Taxes (Recompile)                 *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *  CAP204 - Teller Related APIs - Account Transfer (Recompile)  *
      *  246141 - Recompiled due to changes in ZINTDYZ2               *
      *  164847 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  BUG12990 - Recompiled due to changes in ZINTDYZ2/ZINTDYZ2LE  *
      *  242286 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CGL014 - Collaterals Processing (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CRE009 - Projected Account Movements for RE accounts         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  209171 - Error fix for 204923                                *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
      *
     FDL1650AUO   E                    PRINTER
      *
     FDLOISSL2UF  E           K        DISK
     F                                              KCOMIT
      *
     FDEALS   UF  E           K        DISK
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDCF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     F                                              KCOMIT
      *
     FDLCFSCPDIF  E           K        DISK
     FDLDTSCPDIF  E           K        DISK
     FDLINDHH UF  E                    DISK                      A
     FDLINDDM UF  E                    DISK                      A
     FDLINDZX UF  E                    DISK                      A
     FACCNT   IF  E           K        DISK
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
     FACNUM   IF  E           K        DISK
     F            ACCNTABF                          KRENAMEACNUMABF
     FOLPOS   UF  E           K        DISK
     F            OLPOSHHF                          KIGNORE
     F            OLPOSOBF                          KIGNORE
     F                                              KCOMIT
     FPRONO   UF  E           K        DISK
     F            PRONOHHF                          KIGNORE
     F                                              KCOMIT
     FDLOEVTL2UF  E           K        DISK         KCOMIT
     FDLOISRL0UF  E           K        DISK                      A
     F                                              KCOMIT
     FRSACMTPDO   E                    DISK
     F                                              KCOMIT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N  O F  I N D I C A T O R S                    *
      *                                                               *
      *   14  - Telex already produced (TLXI bit 3 = ON)              *
      *   16  - Initial Rate Set (DNSI bit 0 = ON)                    *
      *   25  - Retail module present on system                       *
      *   33  - Maturity confirmation required                        *
      *   34  - Interest confirmation required                        *
      *   55  - Work indicator for field UDNSI in PF/DEALSDG          *
      *   56  - Work indicator for field TDNSI in PF/DEALSDG          *
      *   60  - Chain fail for PF/DEALSDG                             *
      *   66  - Deals file update indicator                           *
      *   70  - Chain fail for PF/MEMOS                               *
      *   71  - Chain fail for LF/DLOISSL2                            *
      *   72  - Chain fail for LF/DEALS                               *
      *   73  - End of file for LF/DLOISSL2                           *
      *   74  - Chain fail for LF/DLOEVTL2                            *
      *   75  - Interest confirmation indicator                       *
      *   76  - Interest amount not equal to zero                     *
      *   77  - Chain fail for LF/DLOISRL0                            *
      *   78  - Chain fail for LF/DLOISRL0                            *
      *   79  - Floating rate already set if on                       *
      *   84  - Chain fail for LF/ACNUM                               *
      *   86  - Confirmation production indicator                     *
      *   87  - Interest receipt confirmations                        *
      *   88  - Chain fail for LF/ACCNT                               *
      *                                                               *
      * 90-99 - Used by Standard Subroutines                          *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      *   99  - Date Format not valid.                                *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRUTYP - Process FRA deal types (Our side)                   *
      *  SRTTYP - Process FRA deal types (Their side)                 *
      *  SRAB   - FRA Pay/Receive instructions                        *
      *  SRBC   - Interest Payment confirmations                      *
      *  SRBD   - Interest fix confirmations                          *
      *  SRUPD  - Update DEALSDG                                      *
      *  SROLP  - Access PF/OLPOS                                     *
      *  SRPRO  - Access PF/PRONO                                     *
      *  SRIRS  - Calculate interest payment amounts                  *
      *  SRDTP2 - Process FRA deal types                              *
      *  SRDETC - Determine next cash flow                            *
      *  SRDETR - Determine next Rate Change/Payment Date             *
      *  SRCAL1 - Calculate interest up front                         *
      *  SRDETR - Calculate interest if yield (method is 'Y')         *
      *  SRTSET - Setup 'their' fields                                *
      *  SRUSET - Setup 'our' fields                                  *
      *  SRMEMO - Update PF/MEMOS                                     *
      *  SROLPO - Update PF/OLPOS                                     *
      *  SRACCO - Access PF/OLPOS                                     *
      *  SRPRON - Update PF/PRONO                                     *
      *  SRACMT - Output posting details to RSACMTPD                  *
      *  SRACCA - Access ACCNT                                        *
      *  SRCONV - Convert amount to settlement currency               *
      *  SRINIT - Initialisation subroutine                           *
      *  SRWRDM - Writes record to PF/DLINDDM                         *
      *                                                               *
      *  *PSSR    - Error Handling                                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     E/COPY ZSRSRC,ZDATE9Z1
     E                    CPY@    1   1 80               COPYRIGHT ARRAY
      *
      ** Projected nostros Balances
     E                    PBAL        5 15 0
      *
      ** Projected nostros Dates
     E                    PDAT        5  5 0A
      *
      ** On line position Incoming Balances
     E                    OBLI       16 15 0
      *
      ** On line position Outgoing Balances
     E                    OBLO       16 15 0
      *
      ** On line position Dates
     E                    ODAT       16  5 0A
      *
     E/COPY ZSRSRC,ZHOLE
      *
      /EJECT
      *
      ** Rename fields of PF/DLINDHH
     IDTRIEHHF
     I              RECI                            HHRECI
     I              RCDT                            HHRCDT
     I              DLNO                            HHDLNO
     I              NORE                            HHNORE
     I              FLSZ                            HHFLSZ
     I              RRLC                            HHRRLC
     I              RRNA                            HHRRNA
     I              RRLX                            HHRRLX
     I              RRXP                            HHRRXP
     I              NDLR                            HHNDLR
     I              NDAR                            HHNDAR
     I              STPI                            HHSTPI
     I              ZZ004                           HZZ004
     I              TNLU                            HHTNLU
      *
      ** Rename fields of PF/DLINDDM
     IDTRIEPHF
     I              RECI                            DMRECI
     I              RCDT                            DMRCDT
     I              DLNO                            DMDLNO
     I              VDAT                            DMVDAT
     I              DASN                            DMDASN
     I              CHTP                            DMCHTP
     I              CNRQ                            DMCNRQ
     I              CNPR                            DMCNPR
     I              REPR                            DMREPR
     I              RRLA                            DMRRLA
     I              DADI                            DMDADI
     I              PRI1                            DMPRI1
     I              PRI2                            DMPRI2
     I              PRI3                            DMPRI3
     I              ZZ004                           DMZ004
     I              ICIN                            DMICIN
     I              BRCA                            DMBRCA
     I              RRNT                            DMRRNT
     I              TNLU                            DMTNLU
     I              PIAMT                           DMPIAM
     I              RIAMT                           DMRIAM
     I              UFIR                            DMUFIR
     I              TFIR                            DMTFIR
     I              EVDAT                           DMEVDT
     I              TAXPY                           DMTXPY
     I              ZZ022                           DMZ022
      *
      ** Rename fields of PF/DLINDZX
     IDTRIEZZF
     I              RECI                            ZXRECI
     I              NORE                            ZXNORE
     I              SPARE                           ZXPARE
      *
      ** Data structure to define retail account
     I            DS
     I**********                              1  12 RACNO                                     CGL029
     I                                        1  18 RACNO                                     CGL029
     I                                        1   6 RCNUM
     I**********                              7  10 RPCAC                                     CGL029
     I**********                             11  12 RSEQ                                      CGL029
     I                                        7  16 RPCAC                                     CGL029
     I                                       17  18 RSEQ                                      CGL029
      *
      ** Work fields DS for subroutine SRACCA
     I            DS
     I**********                              1  18 ERRKEY                                    CGL029
     I                                        1  24 ERRKEY                                    CGL029
     I**********                              1   60WCNUM                                     CSD027
     I                                        1   6 WCNUM                                     CSD027
     I                                        7   9 WCCY
     I**********                             10  130WACOD                                     CGL029
     I**********                             14  150WACSQ                                     CGL029
     I                                       10  190WACOD                                     CGL029
     I                                       20  210WACSQ                                     CGL029
      *
      ** Include Booking Branch Code in ACCNT key
     I**********                             16  18 WWBRCA                                    CGL029
     I                                       22  24 WWBRCA                                    CGL029
      *
      ** Fields renamed to be more meaningful
     I**********                             19  30 DBNKAC                                    CGL029
     I**********                             19  240W@CNUM                                    CGL029
     I**********                             25  280W@ACOD                                    CGL029
     I**********                             29  30 W@ACSQ                                    CGL029
     I                                       25  42 DBNKAC                                    CGL029
     I**********                             25  300W@CNUM                             CGL029 CSD027
     I                                       25  30 W@CNUM                                    CSD027
     I                                       31  400W@ACOD                                    CGL029
     I                                       41  42 W@ACSQ                                    CGL029
      *
      ** Data structure for PRONO updating to redefine array PBAL as
      ** file field PNB  ( Prono balances )
     I            DS
     I                                    P   1  400PBAL
     I                                        1  40 PNB
      *
      ** Data structure for PRONO updating to redefine array PDAT as
      ** file field PND  ( Prono dates )
     I            DS
     I                                    P   1  150PDAT
     I                                        1  15 PND
      *
      ** Data structure for OLPOS updating to redefine array OBLI as
      ** file field OFI  ( Olpos incoming balances )
     I            DS
     I                                    P   1 1280OBLI
     I                                        1 128 OFI
      *
      ** Data structure for OLPOS updating to redefine array OBLO as
      ** file field OFO  ( Olpos outgoing balances )
     I            DS
     I                                    P   1 1280OBLO
     I                                        1 128 OFO
      *
      **  Data structure for OLPOS updating to redefine array ODAT as
      **  file field ODT  ( Olpos dates )
     I            DS
     I                                    P   1  480ODAT
     I                                        1  48 ODT
      *
      **  Program status data structure for workstation ID &  user ID
     IPSDS       SDS
     I                                      244 246 WSID
     I                                      245 246 WSID2
     I                                      254 263 USRID
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
     ILDA       E DSLDA                         256
      *
      **  Data structure for commitment text.
     ICMTTXT      DS
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTCDX
     I                                       26  35 USRIDX
     I                                       51  70 BTEXT
      *
     IL4EREC      DS
     I                                    P  21  270EAMT
     I                                       28  30 ECCY
     I                                    P  57  630OTHA
     I                                       64  66 OTHC
      *
      ** Bank details accessed via access program
     ISDBANK    E DSSDBANKPD
      *
      ** Base Rate code details accessed via access program
     ISDBSRT    E DSSDBSRTPD
      *
      ** Nostro code details accessed via access program
     ISDNOST    E DSSDNOSTPD
      *
      ** Retail details accessed via access program
     ISDRETL    E DSSDRETLPD
     I              QQACCD                          QQACC1                                    CGL029
      *
      ** Module details accessed via access program
     ISDMMOD    E DSSDMMODPD
      *
      ** Dealing details accessed via access program
     ISDDEAL    E DSSDDEALPD
     I              QQACCD                          QQACC2                                    CGL029
      *
      ** Data structure for SAR details
     ISCSARD    E DSSCSARDPD
      *
      ** Data structure for Currencies file
     ISDCURR    E DSSDCURRPD
      *
     I***SDGELR*   E DSSDGELRPD                                                    MD035545 MD047993
     I**GENERAL*LEDGER*DETAILS*ACCESSED*VIA*ACCESS*PROGRAM*************            MD035545 MD047993
     I**********    QQACCD                          ACCDQQ                         MD035545 MD047993
      * Dummy data structure used by access programs
     IDSFDY     E DSDSFDY
      *
     I/COPY ZSRSRC,ZHOLI
     I/COPY ZSRSRC,ZHOLDS
     I/COPY ZSRSRC,ZINTDYZ1
     I/COPY ZSRSRC,ZDATE9Z2
     I/COPY ZSRSRC,ZDAT10Z1
     I              'LECalcBasis6LeapYear'C         WPARML                                  MD035545
      *
      *****************************************************************
      *                                                               *
      *  M   A   I   N      P   R   O   C   E   S   S   I   N   G     *
      *                                                               *
      *****************************************************************
      *
     C                     EXSR SRINIT
      *
      ** Read first record from OIS Settlement Control File, LF/DLOISSL2.
      ** Records are sequenced Branch/Currency/Deal, and this LF only
      ** selects records where the 'Select For Settlement' field (L5SFST)
      ** is set to 'Y'. Read has no lock.
      *
     C                     READ DLOISSL2            N    73
      *
      ** Continue while there are still records in LF/DLOISSL2.
      *
     C           *IN73     DOWEQ'0'
      *
      ** Chain to DEALS for the deal record.
      *
     C           L5DLNO    CHAINDEALS               N72
      *
      ** If record found on DEALS.
      *
     C           *IN72     IFEQ '0'
      *
      ** Find previous settlement amounts for this deal.
      ** Note that this may be from a previous settlement run for this
      ** deal today, or if this is the first settlement run for this
      ** deal today, these amounts will represent the latest known
      ** amounts, as calculated in the previous Close Of Business run.
      ** These amounts need to be found so that the appropriate
      ** corrections can be made to PRONO, OLPOS, RSACMTPD & MEMOS.
      *
     C                     MOVEA'00'      *IN,33
     C                     Z-ADD0         WUPSET 130
     C                     Z-ADD0         WTPSET 130
      *
     C           DLNO      CHAINDLOISRL0             78
      *
     C           *IN78     IFEQ '0'
     C                     Z-ADDL2OSET    WUPSET
     C                     Z-ADDL2TSET    WTPSET
     C                     ENDIF
      *
      ** If this is a RERUN or if this is not a RERUN and the rate has
      ** not yet been fixed then do processing (else exit program).
      *
     C           CIR001    IFNE 'Y'
     C           CIR001    OREQ 'Y'
     C           DTYP      ANDNE'RS'
      *
     C           UBRTT     IFNE 0
     C                     TESTB'0'       UDNSI      80
     C                     ELSE
     C                     TESTB'0'       TDNSI      80
     C                     ENDIF
      *
     C                     ELSE
     C                     TESTB'34'      TLXI       80
     C                     ENDIF
      *
     C           PRERUN    IFEQ 'Y'
     C           PRERUN    OREQ 'N'
     C           *IN80     ANDEQ'1'
      *
     C                     Z-ADD0         WZERO1 130
      *
      ** Perform IRS's processing
      *
     C                     EXSR SRIRS
      *
      ** Perform update of files if necessary.
      *
     C           *IN66     IFEQ '1'
      *
      ** Perform update of PF/DEALSDG.
      *
     C                     EXSR SRUPD
      *
      ** Perform update of PF/MEMOS - Shadow postings accounts.
      *
     C                     EXSR SRMEMO
      *
      ** Perform update of LF/PRONO - Projected Nostro Accounts.
      *
     C                     EXSR SRPRON
      *
      ** Perform update of LF/OLPOS - On Line Positions file.
      *
     C                     EXSR SROLPO
      *
     C           DLNO      CHAINDLOISRL0             77
      *
      ** If a record is found, change Our Previous Settlement Amount
      ** and Their Previous Settlement Amount. If not, then create
      ** and write one.
      *
     C                     Z-ADDWUINOT    L2OSET
     C                     Z-ADDWTINOT    L2TSET
     C           *IN77     IFEQ '0'
     C                     UPDATDLOISRD0
     C                     ELSE
     C                     MOVELDLNO      L2DLNO
     C                     WRITEDLOISRD0
     C                     ENDIF
      *
      ** Update the record from the OIS Deal Settlement Control File.
      *
     C           KYOISC    CHAINDLOISSL2             71
     C                     MOVEL'N'       L5SFST
     C                     ADD  1         L5SRUN
     C                     UPDATDLOISSD0
      *
      ** Commit details of changes to relevant files for each record.
      *
     C           CMTTXT    COMIT
      *
      ** Increment record count of PF/DEALSDG selected.
      *
     C           RRECT     ADD  1         RRECT
      *
      ** End of perform update of files if necessary.
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Read the next record from LF/DLOISSL2 (no lock).
      *
     C                     READ DLOISSL2            N    73
      *
     C                     ENDDO
      *
      ** Write record to PF/DLINDZX
      *
     C           *HIVAL    SETGTDLINDZX
     C                     MOVE 'T'       ZXRECI
     C                     Z-ADDRDLCT     ZXNORE
     C                     WRITEDTRIEZZF
      *
      ** Generate audit report
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ** If Databse error occured
      *
     C           *INU7     IFEQ '1'
     C                     WRITEDBERROR
      *
      ** If no records were processed
      *
     C                     ELSE
     C           RRECT     IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     WRITEAUTRAIL
      *
     C                     SETON                     LR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZACCH
     C/COPY ZSRSRC,ZFWDT
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRUTYP - Process FRA Deal Types (Our Side)                   *
      *                                                               *
      *  Called by: SRBD, SRIRS                                       *
      *                                                               *
      *  Calls    : AOBSRTR0                                          *
      *                                                               *
      *****************************************************************
      *
     C           SRUTYP    BEGSR
      *
      ** Define fields for subroutine
      *
     C                     Z-ADD0         WBRDAT  50
     C                     Z-ADD0         WRATE  117
     C                     Z-ADD0         WPCNT  137
     C                     MOVE *BLANKS   WKEY2   5
      *
      ** KEY2 only for DB error to set up LDA
      *
     C                     MOVE UBRTT     WKEY2
     C                     MOVELUCUCY     WKEY2
      *
      ** Since UBRTT is a 2N field, move it to a 2A work field
      ** for call to access object
      *
     C                     MOVE UBRTT     PUBRTT  2
      *
      ** Get details for Our Base Rate Type/Currency using call
      ** to access object
      *
     C                     CALL 'AOBSRTR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM UCUCY     PUCUCY  3
     C                     PARM           PUBRTT
     C           SDBSRT    PARM SDBSRT    DSSDY
      *
      ** Handle database error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVE 'SDBSRTPD'DBFILE
     C                     MOVELWKEY2     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Obtain current base rate unless new value date of rate change
      ** is less than or equal to the fix date in which case obtain
      ** new base rate
      *
     C           BAVDNR    IFLE UINFD
     C                     Z-ADDBANBRT    WRATE
     C                     Z-ADDBAVDNR    WBRDAT
     C                     ENDIF
     C           WRATE     IFEQ 0
     C                     Z-ADDBACBSR    WRATE
     C                     Z-ADDBAVDRC    WBRDAT
     C                     ENDIF
      *
     C                     Z-ADDWRATE     WBBRTE 117
      *
      ** Calculate the new effective interest rate
      *
     C           USPIN     IFEQ 'A'
     C           WRATE     ADD  URTSP     WRATE
     C                     ENDIF
     C           USPIN     IFEQ 'S'
     C           WRATE     SUB  URTSP     WRATE
     C                     ENDIF
     C           USPIN     IFEQ 'P'
     C           WRATE     MULT URTSP     WPCNT
     C           WPCNT     DIV  100       WPCNT
     C           WRATE     ADD  WPCNT     WRATE
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRTTYP - Process FRA Deal Types (Their Side)                 *
      *                                                               *
      *  Called by: SRBD, SRIRS                                       *
      *                                                               *
      *  Calls    : AOBSRTR0                                          *
      *                                                               *
      *****************************************************************
      *
     C           SRTTYP    BEGSR
      *
      ** Define fields for subroutine
      *
     C                     Z-ADD0         WBRDAT  50
     C                     Z-ADD0         WRATE  117
     C                     Z-ADD0         WPCNT  137
     C                     MOVE *BLANKS   WKEY2   5
      *
      ** KEY2 only for DB error to set up LDA
      *
     C                     MOVE TBRTT     WKEY2
     C                     MOVELTCUCY     WKEY2
      *
      ** Since TBRTT is a 2N field, move it to a 2A work field
      ** for call to access object
      *
     C                     MOVE TBRTT     PTBRTT  2
      *
      ** Get details for Their Base Rate Type/Currency using call
      ** to access object
      *
     C                     CALL 'AOBSRTR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM TCUCY     PTCUCY  3
     C                     PARM           PTBRTT
     C           SDBSRT    PARM SDBSRT    DSSDY
      *
      ** Handle database error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVE 'SDBSRTPD'DBFILE
     C                     MOVELWKEY2     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Obtain current base rate unless new value date of rate change
      ** is less than or equal to the fix date in which case obtain
      ** new base rate
      *
     C           BAVDNR    IFLE TINFD
     C                     Z-ADDBANBRT    WRATE
     C                     Z-ADDBAVDNR    WBRDAT
     C                     ENDIF
     C           WRATE     IFEQ 0
     C                     Z-ADDBACBSR    WRATE
     C                     Z-ADDBAVDRC    WBRDAT
     C                     ENDIF
      *
     C                     Z-ADDWRATE     WBBRTE 117
      *
      ** Calculate the new effective interest rate
      *
     C           TSPIN     IFEQ 'A'
     C           WRATE     ADD  TRTSP     WRATE
     C                     ENDIF
     C           TSPIN     IFEQ 'S'
     C           WRATE     SUB  TRTSP     WRATE
     C                     ENDIF
     C           TSPIN     IFEQ 'P'
     C           WRATE     MULT TRTSP     WPCNT
     C           WPCNT     DIV  100       WPCNT
     C           WRATE     ADD  WPCNT     WRATE
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRAB - FRA Pay/Receive instructions                          *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : *PSSR, ZFWDT, SRWRDM                              *
      *                                                               *
      *****************************************************************
      *
     C           SRAB      BEGSR
      *
      ** Define fields for subroutine
      *
     C                     MOVEA'00'      *IN,75           *IN 75-76
     C                     MOVE *BLANKS   WSETM   2
     C                     Z-ADD0         WPIAMT 130
     C                     Z-ADD0         WRIAMT 130
     C                     Z-ADD0         WPRI1
     C                     Z-ADD0         WPRI2
     C                     Z-ADD0         WPRI3
     C                     Z-ADD0         WEVDAT
     C                     Z-ADD0         WTAXPY
     C                     Z-ADD0         WUFIR
     C                     Z-ADD0         WTFIR
      *
      ** Define fields for subroutine
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANK
     C                     MOVE PSCY      PCURR
     C                     ELSE
     C                     MOVE UCUCY     PCURR
     C                     ENDIF
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVE 'SDCURRPD'DBFILE
     C                     MOVELPCURR     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDWUEDAT    WOIUVD  50
     C                     Z-ADDWTEDAT    WOITVD  50
      *
      ** If Settlement Days is not zero, WOIUVD & WOITVD must be
      ** increased by a number of working days in the Deal CCY
      *
     C           A6SETD    IFNE 0
      *
     C                     MOVELWOIUVD    ZDAYNO
     C                     MOVELPCURR     ZCCY
     C                     Z-ADDA6SETD    ZNRDYS
     C                     EXSR ZFWDT
     C                     MOVELZDYNBR    WOIUVD
      *
     C                     MOVELWOITVD    ZDAYNO
     C                     MOVELPCURR     ZCCY
     C                     Z-ADDA6SETD    ZNRDYS
     C                     EXSR ZFWDT
     C                     MOVELZDYNBR    WOITVD
      *
     C                     ENDIF
      *
      ** A payment event is due to be reported if
      ** 1) the associated telex indicator is off
      *
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
     C                     Z-ADDWUEDAT    WPAYD
     C                     TESTB'3'       TLXI       75
     C                     MOVE PSETM     WSETM
     C                     ENDIF
      *
     C           WSETL     IFEQ 'R'
     C                     Z-ADDWTEDAT    WPAYD
     C                     TESTB'4'       TLXI       75
     C                     MOVE RSETM     WSETM
     C                     ENDIF
      *
      ** If the program is being RERUN, produce Pay Receive Telexes
      *
     C           PRERUN    IFEQ 'Y'
     C                     MOVE '1'       *IN75
     C                     ENDIF
      *
      ** 2) a payment amount exists (NET FOR 'FR')
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
     C           WNET      ANDNE0
     C                     MOVE '1'       *IN76
     C                     ENDIF
     C*
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDNEWTEDAT                                                        209171
     C           WEDATU    ANDNEWEDATT                                                        209171
     C           WUINOT    IFNE 0
     C           WTINOT    ORNE 0
     C                     MOVE '1'       *IN76
     C                     ENDIF
     C                     ENDIF
      *
      ** 3) the event date is before or the same as date of next
      **    working day
      *
     C           *IN75     IFEQ '1'
     C           *IN76     ANDEQ'1'
      *
      ** If deal is an IRS deal, update telex indicators accordingly.
      *
     C           DTYP      IFEQ 'RS'
      *
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
     C                     BITON'3'       TLXI
     C                     ENDIF
      *
     C           WSETL     IFEQ 'R'
     C           WSETL     OREQ 'B'
     C                     BITON'4'       TLXI
     C                     ENDIF
      *
     C********** WUEDAT    IFEQ WTEDAT                                                        209171
     C           WEDATU    IFEQ WEDATT                                                        209171
     C                     BITON'3'       TLXI
     C                     BITON'4'       TLXI
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Set up fields for creation of record in PF/DLINDDM
      *
     C                     MOVE 'I'       WACTC   1
     C                     MOVE 'I'       ACTCDX
      *
      ** Tax Payable
      *
     C           DTYP      IFEQ 'RS'
     C                     Z-ADDWTAXC     WTAXPY
     C                     ENDIF
      *
      ** Set up 'RS' matched deal record
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
     C           WSETL     IFEQ 'P'
     C                     Z-ADDWNET      WPIAMT
     C           RDLCT     ADD  1         RDLCT
     C                     Z-ADD1         WPRI3
     C                     Z-ADDWOIUVD    WEVDAT
     C                     MOVE 'O'       WTYPE   1
     C                     EXSR SRWRDM
     C                     ELSE
     C                     Z-ADDWNET      WRIAMT
     C           RDLCT     ADD  1         RDLCT
     C                     Z-ADD1         WPRI1
     C                     Z-ADDWOITVD    WEVDAT
     C                     MOVE 'T'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
     C                     ENDIF
      *
      ** Set up 'RS' unmatched deal records
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDNEWTEDAT                                                        209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
     C           RDLCT     ADD  1         RDLCT
     C                     Z-ADDWUINOT    WPIAMT
     C                     Z-ADD1         WPRI3
     C                     Z-ADDWOIUVD    WEVDAT
     C                     MOVE 'O'       WTYPE
     C                     EXSR SRWRDM
     C                     Z-ADD0         WPIAMT
     C                     Z-ADD0         WUFIR
     C                     ENDIF
      *
     C           WSETL     IFEQ 'R'
     C           RDLCT     ADD  1         RDLCT
     C                     Z-ADDWTINOT    WRIAMT
     C                     Z-ADD1         WPRI1
     C                     Z-ADDWOITVD    WEVDAT
     C                     MOVE 'T'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Set on deals file update indicator.
      *
     C           RECI      IFNE 'R'
     C                     MOVE '1'       *IN66
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBC - Interest payment confirmations                        *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : SRWRDM                                            *
      *                                                               *
      *****************************************************************
      *
     C           SRBC      BEGSR
      *
      ** Define fields for subroutine
      *
     C                     MOVEA'00'      *IN,75
     C                     MOVE *BLANKS   WCNRQ   1
     C                     Z-ADD0         WUFIR  117
     C                     Z-ADD0         WEVDAT  50
     C                     Z-ADD0         WTFIR  117
      *
      ** Payment confirmations
      *
     C           WSETL     IFEQ 'P'
      *
     C                     TESTB'2'       CNFI       75
      *
      ** If the program is being RERUN, produce Pay Receive Confirmations
      *
     C           PRERUN    IFEQ 'Y'
     C                     MOVE '1'       *IN75
     C                     ENDIF
      *
      ** The interest amount calculated must be non-zero
      *
     C           CIR001    IFEQ 'Y'
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
     C           WUINTR    IFNE 0
     C           WTINTR    ORNE 0
     C                     MOVE '1'       *IN76
     C                     ENDIF
     C                     ENDIF
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDNEWTEDAT                                                        209171
     C           WEDATU    ANDNEWEDATT                                                        209171
     C           WUINTR    ANDNE0
     C                     MOVE '1'       *IN76
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If confirmation is to be produced,
      ** set on appropriate confirmation indicator
      *
     C           *IN75     IFEQ '1'
     C           *IN76     ANDEQ'1'
     C                     MOVE '1'       *IN87
     C                     ENDIF
      *
     C                     ENDIF
      /SPACE 2
      *
      ** Interest Receipt Confirmations
      *
     C           WSETL     IFEQ 'R'
      *
     C                     TESTB'3'       CNFI       75
      *
      ** If the program is being RERUN, produce Pay Receive Confirmation
     C           PRERUN    IFEQ 'Y'
     C                     MOVE '1'       *IN75
     C                     ENDIF
      *
      ** The interest amount calculated must be non-zero
      *
     C           CIR001    IFEQ 'Y'
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
     C           WUINTR    IFNE 0
     C           WTINTR    ORNE 0
     C                     MOVE '1'       *IN76
     C                     ENDIF
     C                     ENDIF
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDNEWTEDAT                                                        209171
     C           WEDATU    ANDNEWEDATT                                                        209171
     C           WTINTR    ANDNE0
     C                     MOVE '1'       *IN76
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If confirmation is to be produced
      ** set on appropriate confirmation indicator
      *
     C           *IN75     IFEQ '1'
     C           *IN76     ANDEQ'1'
     C                     MOVE '1'       *IN87
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If both sides are being processed, also set on *IN87.
      *
     C           WSETL     IFEQ 'B'
     C                     MOVE '1'       *IN87
     C                     ENDIF
      *
      ** If Pay or Receive Confirmations are to be produced.
     C           *IN87     IFEQ '1'
      *
     C           UBRTT     IFNE 0
     C                     TESTB'3'       UDNSI          86
     C                     ELSE
     C                     TESTB'3'       TDNSI          86
     C                     ENDIF
      *
     C           *IN86     IFEQ '1'
     C                     MOVE 'A'       WACTC
     C                     MOVE 'A'       ACTCDX
     C                     ELSE
     C                     MOVE 'I'       WACTC
     C                     MOVE 'I'       ACTCDX
     C                     ENDIF
      *
      ** Set up Tax Payable field and Interest Rates
      *
     C           CIR001    IFEQ 'Y'
     C           DTYP      ANDEQ'RS'
     C                     Z-ADDWTAXC     WTAXPY
     C                     Z-ADDWUEINS    WUFIR
     C                     Z-ADDWTEINS    WTFIR
     C                     ENDIF
      *
     C           CIR001    IFEQ 'Y'
      *
      ** Set up 'RS' matched record
      ** (output only if event date *GE run date)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
     C                     Z-ADDWUINOT    WPIAMT
     C                     Z-ADDWTINOT    WRIAMT
      *
     C           WSETL     IFEQ 'P'
     C                     MOVE 'E'       WCNRQ
     C                     Z-ADDWUEDAT    WEVDAT
     C                     BITON'23'      CNFI
     C                     BITOF'3'       UDNSI
     C                     BITOF'3'       TDNSI
     C********** WEVDAT    IFGE BJRDNB                                                        209171
     C           WEDATU    IFGE BJRDNB                                                        209171
     C           RDLCT     ADD  1         RDLCT
     C                     MOVE 'B'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
     C                     ENDIF
      *
     C           WSETL     IFEQ 'R'
     C                     MOVE 'G'       WCNRQ
     C                     Z-ADDWTEDAT    WEVDAT
     C                     BITON'23'      CNFI
     C                     BITOF'3'       UDNSI
     C                     BITOF'3'       TDNSI
     C********** WEVDAT    IFGE BJRDNB                                                        209171
     C           WEDATT    IFGE BJRDNB                                                        209171
     C           RDLCT     ADD  1         RDLCT
     C                     MOVE 'B'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Set up 'RS' unmatched deal records
      ** (output only if event date *GE run date)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDNEWTEDAT                                                        209171
     C           WEDATU    ANDNEWEDATT                                                        209171
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
     C                     MOVE 'E'       WCNRQ
     C                     Z-ADDWUINOT    WPIAMT
     C                     Z-ADDWUEDAT    WEVDAT
     C                     BITON'2'       CNFI
     C                     BITOF'3'       UDNSI
      *
     C********** WEVDAT    IFGE BJRDNB                                                        209171
     C           WEDATU    IFGE BJRDNB                                                        209171
     C           RDLCT     ADD  1         RDLCT
     C                     MOVE 'O'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
     C                     ENDIF
      *
     C           WSETL     IFEQ 'R'
     C                     MOVE 'G'       WCNRQ
     C                     Z-ADDWTINOT    WRIAMT
     C                     Z-ADDWTEDAT    WEVDAT
     C                     BITON'3'       CNFI
     C                     BITOF'3'       TDNSI
     C********** WEVDAT    IFGE BJRDNB                                                        209171
     C           WEDATT    IFGE BJRDNB                                                        209171
     C           RDLCT     ADD  1         RDLCT
     C                     MOVE 'T'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Set on deals file update indicator.
      *
     C           RECI      IFNE 'R'
     C                     MOVE '1'       *IN66
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBD - Interest fix confirmations                            *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : SRUTYP, SRTTYP, SRWRDM                            *
      *                                                               *
      *****************************************************************
      *
     C           SRBD      BEGSR
      *
     C                     Z-ADD0         WTAXPY
     C                     Z-ADD0         WPIAMT
     C                     Z-ADD0         WRIAMT
     C                     Z-ADD0         WUFIR
     C                     Z-ADD0         WTFIR
     C                     BITOF'45'      CNFI
     C                     MOVE ' '       PRTIN   1
     C                     Z-ADD0         WEVDAT
      *
      ** A rate fix confirmation is due if the rate fix date is
      ** before the date of next working day then obtain new fixed
      ** rate via SRUTYP/SRTTYP and set on relevant rate fix conf. ind
      *
     C           UBRTT     IFNE 0
     C                     EXSR SRUTYP
     C                     Z-ADDWRATE     WUFIR
     C                     BITON'4'       CNFI
     C                     MOVEL'U'       PRTIN
     C                     ENDIF
      *
     C           TBRTT     IFNE 0
     C                     EXSR SRTTYP
     C                     Z-ADDWRATE     WTFIR
     C                     BITON'5'       CNFI
     C           PRTIN     IFEQ ' '
     C                     MOVE 'T'       PRTIN
     C                     ELSE
     C                     MOVE 'B'       PRTIN
     C                     ENDIF
     C                     ENDIF
      *
      ** Output details to PF/DLINDDM
      *
     C                     MOVE 'I'       WACTC
     C                     MOVE 'I'       ACTCDX
      *
      ** Set up 'FR' deal records
      ** produce FR only if not previously produced
      *
     C                     MOVE '0'       *IN87
      *
     C           UBRTT     IFNE 0
     C                     TESTB'0'       UDNSI      87
     C                     ELSE
     C                     TESTB'0'       TDNSI      87
     C                     ENDIF
      *
      ** If the programme is being RERUN produce Rate Fix Confirmations.
      *
     C           PRERUN    IFEQ 'Y'
     C                     MOVE '1'       *IN87
     C                     ENDIF
      *
      ** Set up 'RS' matched deal records
      *
     C           CIR001    IFEQ 'Y'
     C           *IN87     ANDEQ'0'
      *
     C           PRTIN     IFEQ 'B'                         OURS & THEIRS
     C                     MOVE 'H'       WCNRQ
     C           RDLCT     ADD  1         RDLCT
     C                     Z-ADDUNICD     WEVDAT
     C                     MOVE 'B'       WTYPE
     C                     EXSR SRWRDM
      *
     C                     ELSE
      *
      ** Set up 'RS' unmatched deal records
      *
     C           PRTIN     IFEQ 'U'                         OUR
     C                     MOVE 'D'       WCNRQ
     C           RDLCT     ADD  1         RDLCT
     C                     Z-ADDUNICD     WEVDAT
     C                     MOVE 'O'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
      *
     C           PRTIN     IFEQ 'T'                        THEIR
     C                     MOVE 'F'       WCNRQ
     C           RDLCT     ADD  1         RDLCT
     C                     Z-ADDTNICD     WEVDAT
     C                     MOVE 'T'       WTYPE
     C                     EXSR SRWRDM
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Set on deals file update indicator.
      *
     C           RECI      IFNE 'R'
     C                     MOVE '1'       *IN66
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRUPD - Update PF/DEALSDG                                    *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  Calls    : *PSSR                                             *
      *                                                               *
      *****************************************************************
      *
     C           SRUPD     BEGSR
      *
      ** Save current value of fields to be updated.
      *
     C           *IN55     IFEQ '1'
     C                     BITON'0'       UDNSI
     C                     ENDIF
      *
     C           *IN56     IFEQ '1'
     C                     BITON'0'       TDNSI
     C                     ENDIF
      *
     C                     MOVE UDNSI     WUDNSI  1
     C                     MOVE TDNSI     WTDNSI  1
     C                     MOVE UEINR     WUEINR 117
     C                     MOVE TEINR     WTEINR 117
     C                     MOVELTLXI      WTLXI   1
     C                     MOVELCNFI      WCNFI   1
      *
      ** Access deals file for update.
      *
     C           DLNO      CHAINDEALS                60
      *
     C           *IN60     IFEQ '1'
     C           RECI      ORNE 'D'
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE
     C                     MOVE 'DEALS'   DBFILE
     C                     MOVELDLNO      DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Move in values from calculations.
      *
     C                     MOVE WUDNSI    UDNSI
     C                     MOVE WTDNSI    TDNSI
      *
     C           CIR001    IFEQ 'Y'
     C           DTYP      ANDEQ'RS'
      *
     C           UINPM     IFEQ 'Y'
     C           UINPM     OREQ 'D'
     C                     MOVE WUBRTE    UBRTE
     C                     MOVE WUEINR    UEINR
     C                     ENDIF
      *
     C           TINPM     IFEQ 'Y'
     C           TINPM     OREQ 'D'
     C                     MOVE WTBRTE    TBRTE
     C                     MOVE WTEINR    TEINR
     C                     ENDIF
      *
     C                     MOVELWTLXI     TLXI
     C                     MOVELWCNFI     CNFI
      *
     C                     ENDIF
      *
      ** Update file.
      *
     C                     UPDATDEALSDGF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SROLP - Access PF/OLPOS                                      *
      *                                                               *
      *  Called by: SROLPO                                            *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SROLP     BEGSR
      *
     C                     MOVE UCUCY     KOLCCY
     C                     MOVE '02'      KOLPNO
      *
     C           OLKEY     KLIST
      *
      ** Booking Branch included in key for OLPOS
      *
     C                     KFLD           BRCA
     C                     KFLD           KOLCCY  3
     C                     KFLD           KOLPNO  20
      *
     C           OLKEY     CHAINOLPOS                86
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPRO - Access PF/PRONO                                      *
      *                                                               *
      *  Called by: SRPRON                                            *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRPRO     BEGSR
      *
     C           PRKEY     KLIST
     C                     KFLD           PRCCY   3
     C                     KFLD           PRNNO   20
     C           PRKEY     CHAINPRONO                86
      *
      ** If record not found, use 'unallocated' code
      *
     C           *IN86     IFEQ '1'
     C           RECI      ORNE 'D'
      *
      ** Release PF/PRONOPN record if record found, but not live
      *
     C           *IN86     IFEQ '0'
     C                     UNLCKPRONO
     C                     ENDIF
      *
     C                     Z-ADD0         PRNNO
     C           PRKEY     CHAINPRONO                86
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRWRDM - Writes record to PF/DLINDDM                         *
      *                                                               *
      *  Called by: SRAB, SRBC, SRBD                                  *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRWRDM    BEGSR
      *
     C           *HIVAL    SETGTDLINDDM
      *
     C                     MOVE 'D'       DMRECI
     C                     MOVE 'D'       DMRCDT
     C                     Z-ADDDLNO      DMDLNO
     C                     MOVE WACTC     DMCHTP
     C                     MOVE WCNRQ     DMCNRQ
     C                     Z-ADDWUFIR     DMUFIR
     C                     Z-ADDWTFIR     DMTFIR
     C                     Z-ADDWEVDAT    DMEVDT
      *
      ** OUR
      *
     C           WTYPE     IFEQ 'O'
     C                     Z-ADDWOIUVD    DMVDAT
     C                     Z-ADD0         DMPRI1
     C                     Z-ADDWPRI3     DMPRI3
     C                     MOVE BRCA      DMBRCA
     C                     Z-ADDWPIAMT    DMPIAM
     C                     Z-ADDWZERO1    DMRIAM
     C                     Z-ADDWTAXPY    DMTXPY
     C                     ENDIF
      *
      ** THEIR
      *
     C           WTYPE     IFEQ 'T'
     C                     Z-ADDWOITVD    DMVDAT
     C                     Z-ADDWPRI1     DMPRI1
     C                     Z-ADD0         DMPRI3
     C                     MOVE BRCA      DMBRCA
     C                     Z-ADDWZERO1    DMPIAM
     C                     Z-ADDWRIAMT    DMRIAM
     C                     Z-ADDWZERO1    DMTXPY
     C                     ENDIF
      *
      ** BOTH
      *
     C           WTYPE     IFEQ 'B'
     C                     Z-ADDWOITVD    DMVDAT
     C                     Z-ADDWPRI1     DMPRI1
     C                     Z-ADD0         DMPRI3
     C                     MOVE BRCA      DMBRCA
     C                     Z-ADDWZERO1    DMPIAM
     C                     Z-ADDWRIAMT    DMRIAM
     C                     Z-ADDWZERO1    DMTXPY
     C                     ENDIF
      *
     C                     WRITEDTRIEPHF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRIRS - Calculates interest payment amounts if applicable,   *
      *          for IRS.                                             *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  Calls    : SRUTYP, SRTTYP, SRDTP2, SRDETR, GLINTC, SRUSET    *
      *             SRTSET, SRCAL1, SRAB, SRBC, SRBD, *PSSR           *
      *                                                               *
      *****************************************************************
      *
     C           SRIRS     BEGSR
      *
      ** Define fields for subroutine
      *
     C                     MOVE 'N'       WJUMP   1
     C                     MOVE '0'       *IN55
     C                     MOVE '0'       *IN56
     C                     MOVE '0'       *IN79
     C                     MOVE '0'       *IN87
     C                     Z-ADD0         WUEDAT  50
     C                     Z-ADD0         WTEDAT  50
     C                     MOVE *BLANKS   WPRINT  1
     C                     MOVE ' '       WUPRT   1
     C                     MOVE ' '       WTPRT   1
     C                     Z-ADD0         WSZPPL 130
     C                     Z-ADD0         WSZIAN 130
     C                     Z-ADD0         WEINTR 152
     C                     Z-ADD0         WENDDT  50
     C                     Z-ADD0         WZTAXC 150
     C                     Z-ADD0         WTAXC  130
     C                     Z-ADD0         WUINOT 130
     C                     Z-ADD0         WTINOT 130
     C                     Z-ADD0         WNET   130
     C                     Z-ADD0         WPAYD   50
     C                     Z-ADD0         WUINTR 130
     C                     Z-ADD0         WTINTR 130
     C                     Z-ADD0         WTAXPY 130
     C                     Z-ADD0         WUBRTE 117
     C                     Z-ADD0         WTBRTE 117
     C                     Z-ADDUNICD     UINFD
     C                     Z-ADDTNICD     TINFD
     C                     MOVE *BLANKS   WSETL   1
     C                     MOVE '0'       *IN14
     C                     MOVE '0'       *IN20
      *
      ** For 'RS' deals, identify OUR and THEIR event dates
      *
     C           DTYP      IFEQ 'RS'
      *
      ** Set Event Date to Next Interest Payment Date (if present)
      ** Else, set to Next Interest Change Date (if present)
      ** Else, set to Maturity Date
      *
     C           UNIPD     IFNE 0
     C                     Z-ADDUNIPD     WUEDAT
      *
     C                     ELSE
     C           UNICD     IFNE 0
     C                     Z-ADDUNICD     WUEDAT
     C                     ELSE
     C                     Z-ADDMDAT      WUEDAT
     C                     ENDIF
     C                     ENDIF
      *
     C           TNIPD     IFNE 0
     C                     Z-ADDTNIPD     WTEDAT
      *
     C                     ELSE
     C           TNICD     IFNE 0
     C                     Z-ADDTNICD     WTEDAT
     C                     ELSE
     C                     Z-ADDMDAT      WTEDAT
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE *BLANK    WUBACK  1
      *
      ** If OUR Interest is up front (discount or yield)
      *
     C           UINPM     IFEQ 'D'
     C           UINPM     OREQ 'Y'
      *
     C                     TESTB'1'       ACEI       86
      *
      ** If Value Date of deal has not yet arrived
      *
     C           *IN86     IFEQ '1'
      *
      ** Our First Interest Payment Date is Value Date and
      ** OUR Interest End Date is the Next Interest Payment Date
      *
     C                     Z-ADDVDAT      WUEDAT
     C                     ENDIF
      *
      ** OUR Interest End Date is the Following Interest Payment Date
      *
     C                     MOVEL'U'       OTIN
     C                     MOVEL'P'       RCIP
     C                     Z-ADDWUEDAT    ZDAYNO
     C                     EXSR SRDETR
     C                     Z-ADDZDAYNO    WUNEDT  50
      *
      ** If interest is up front, any interest payments from previous
      ** periods will already have been made therefore pay interest
      ** for current period only.
      *
     C           WUNEDT    DOWLEBJRDNB
     C                     Z-ADDWUNEDT    WUEDAT
     C                     Z-ADDWUEDAT    ZDAYNO
     C                     EXSR SRDETR
     C                     Z-ADDZDAYNO    WUNEDT
     C                     MOVE 'Y'       WUBACK
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     MOVE *BLANK    WTBACK  1
      *
      ** IF THEIR Interest is up front (discount or yield)
      *
     C           TINPM     IFEQ 'D'
     C           TINPM     OREQ 'Y'
      *
     C                     TESTB'1'       ACEI       86
      *
      ** If Value Date of deal has not yet arrived
      *
     C           *IN86     IFEQ '1'
      *
      ** THEIR First Interest Payment Date is Value Date and
      ** THEIR Interest End Date is the Next Interest Payment Date
      *
     C                     Z-ADDVDAT      WTEDAT
     C                     ENDIF
      *
      ** THEIR Interest End Date is the following Interest Payment Date
      *
     C                     MOVEL'T'       OTIN
     C                     MOVEL'P'       RCIP
     C                     Z-ADDWTEDAT    ZDAYNO
     C                     EXSR SRDETR
     C                     Z-ADDZDAYNO    WTNEDT  50
      *
      ** If interest is up front, any interest payments from previous
      ** periods will already have been made therefore pay interest
      ** for current period only.
      *
     C           WTNEDT    DOWLEBJRDNB
     C                     Z-ADDWTNEDT    WTEDAT
     C                     Z-ADDWTEDAT    ZDAYNO
     C                     EXSR SRDETR
     C                     Z-ADDZDAYNO    WTNEDT
     C                     MOVE 'Y'       WTBACK
     C                     ENDDO
      *
     C                     ENDIF
      *                                                                                       209171
     C                     Z-ADDWUEDAT    WEDATU  50                                          209171
     C                     Z-ADDWTEDAT    WEDATT  50                                          209171
      *                                                                                       209171
     C                     CALL 'AOCURRR0'                                                    209171
     C                     PARM *BLANKS   PRTCD                                               209171
     C                     PARM '*KEY   ' POPTN                                               209171
     C                     PARM UCUCY     PCURR                                               209171
     C           SDCURR    PARM SDCURR    DSSDY                                               209171
      *                                                                                       209171
     C           PRTCD     IFNE *BLANKS                                                       209171
     C           *LOCK     IN   LDA                                                           209171
     C                     Z-ADD3         DBASE                                               209171
     C                     MOVE 'SDCURRPD'DBFILE                                              209171
     C                     MOVELPCURR     DBKEY                                               209171
     C                     OUT  LDA                                                           209171
     C                     EXSR *PSSR                                                         209171
     C                     ELSE                                                               209171
     C                     Z-ADDA6FXDY    WFXDY   10                                          209171
     C                     ENDIF                                                              209171
      *                                                                                       209171
      ** If not 0, adjust the Event dates by the Rate Fix Days                                209171
      ** Adjustment for sides which are OIS.                                                  209171
      *                                                                                       209171
     C           WFXDY     IFNE 0                                                             209171
      *                                                                                       209171
      ** OURS                                                                                 209171
      *                                                                                       209171
     C           UICFR     IFEQ 'O'                                                           209171
     C                     Z-ADDWUEDAT    ZDAYNO                                              209171
     C                     Z-ADD1         ZNRDYS                                              209171
     C                     MOVE BJLCCY    ZCCY                                                209171
     C                     EXSR ZFWDT                                                         209171
     C                     MOVE ZDYNBR    WEDATU                                              209171
     C                     ENDIF                                                              209171
      *                                                                                       209171
      ** THEIRS                                                                               209171
      *                                                                                       209171
     C           TICFR     IFEQ 'O'                                                           209171
     C                     Z-ADDWTEDAT    ZDAYNO                                              209171
     C                     Z-ADD1         ZNRDYS                                              209171
     C                     MOVE BJLCCY    ZCCY                                                209171
     C                     EXSR ZFWDT                                                         209171
     C                     MOVE ZDYNBR    WEDATT                                              209171
     C                     ENDIF                                                              209171
      *                                                                                       209171
     C                     ENDIF                                                              209171
      *
      ** Identify all deals for which OUR or THEIR Pay/Receive
      ** reporting or Interest Payment confirmations are required
      *
      ** For 'RS' deals
      *
     C********** WUEDAT    IFEQ BJRDNB                                                        209171
     C           WEDATU    IFEQ BJRDNB                                                        209171
     C                     MOVE 'Y'       WUPRT               PRINT OUR
     C                     MOVE 'Y'       WPRINT
     C                     ENDIF
      *
     C********** WTEDAT    IFEQ BJRDNB                                                        209171
     C           WEDATT    IFEQ BJRDNB                                                        209171
     C                     MOVE 'Y'       WTPRT               PRINT THEIR
     C                     MOVE 'Y'       WPRINT
     C                     ENDIF
      *
      ** Payment confirmations and telexes can only be done for
      ** known details, or if they are still outstanding,
      *
      ** Check to see if telex or confirmations have been produced
      *
     C           UBRTT     IFNE 0
     C                     TESTB'3'       TLXI           14
     C                     TESTB'2'       CNFI           16
      *
     C           *IN14     IFEQ '1'
     C           *IN16     ANDEQ'1'
     C           PRERUN    ANDEQ'N'
     C                     MOVE *BLANKS   WUPRT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           TBRTT     IFNE 0
     C                     TESTB'4'       TLXI           14
     C                     TESTB'3'       CNFI           16
      *
     C           *IN14     IFEQ '1'
     C           *IN16     ANDEQ'1'
     C           PRERUN    ANDEQ'N'
     C                     MOVE *BLANKS   WTPRT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           WUPRT     IFNE 'Y'
     C           WTPRT     ANDNE'Y'
     C                     MOVE *BLANKS   WPRINT
     C                     ENDIF
      *
      ** Calculate amounts of interest payable for those deals
      ** identified above
      *
     C           WPRINT    IFEQ 'Y'
      *
      ** /////////////
      ** RS DEAL TYPES
      ** /////////////
      *
     C           DTYP      IFEQ 'RS'
      *
      ** Process OUR details if OUR Event Date due and
      ** if Print for OUR details requested
      *
      ** If our base rate code is not zero and the floating rate has
      ** not been set,or if our base rate code is not equal to zero,
      ** and the input parameter PRERUN is equal to  'Y' then:
      ** Access floating rate for base rate using SR:SRUTYP
      *
     C           WUPRT     IFEQ 'Y'
      *
      ** If details relate to a non-zero Base Rate Type and the Event
      ** Date is greater than the Rate Change Date, calculate Interest
      ** Rate for that period via SRUTYP
      *
     C           UBRTT     IFNE 0
      *
      ** Bit '2' Of UDNSI signifies Short Term Rate Set. Only payments
      ** due today selected so if UEINR not equal to '0' and USTRT equal
      ** to '0'. Floating Rate was set on previous run and should be reversed.
      *
     C           UEINR     IFNE 0
     C           USTRT     ANDEQ0
     C                     MOVE '1'       *IN79
     C                     ELSE
     C                     MOVE '0'       *IN79
     C                     ENDIF
      *
     C                     TESTB'2'       UDNSI          20
      *
      ** If PRERUN and *IN79 is off, then rate has not been previously
      ** set and hence deal shouldn't be reversed before re-fixing rate
      *
     C           PRERUN    IFEQ 'Y'
     C           *IN79     IFEQ '0'
     C                     MOVE 'N'       WPRVST  1
     C                     ELSE
     C                     MOVE 'Y'       WPRVST
     C                     ENDIF
     C                     ENDIF
      *
      ** Set on deals file update indicator.
      *
     C                     MOVE '1'       *IN66
      *
      ** Increment count of deals with rate fixed.
      *
     C                     ADD  1         RRTFIX
      *
     C                     EXSR SRUTYP
     C                     MOVE '1'       *IN55
      *
     C           UINPM     IFEQ 'D'
     C           UINPM     OREQ 'Y'
     C                     Z-ADDWRATE     UEINR
     C                     Z-ADDWBBRTE    WUBRTE
     C                     ENDIF
      *
      ** Use SR/GLINTC to calculate the end interest from the Interest
      ** Change Date up to but excluding the Event Date
      *
     C           UNICD     IFNE 0
     C********** WUEDAT    ANDGTUNICD                                                         209171
     C           WEDATU    ANDGTUNICD                                                         209171
     C                     Z-ADDWRATE     ZIRATE
     C           *IN20     IFEQ '1'
     C                     Z-SUBWRATE     ZIRATE
     C                     ENDIF
     C                     Z-ADDUICBS     ZICALC
     C                     Z-ADDUNICD     ZIBEG
     C                     Z-ADDWUEDAT    ZIEND
     C                     Z-ADDUPAMT     ZIAMT
     C                     EXSR GLINTC
      *
     C                     Z-ADDZINTR     WEINTR
      *
     C                     ELSE
      *
      ** Otherwise, zeroise End Interest
      *
     C                     Z-ADD0         WEINTR
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Set End Date to the Event Date or the Rate Change Date if
      ** this is before the Event Date
      *
     C           UNICD     IFNE 0
     C********** WUEDAT    ANDGTUNICD                                                         209171
     C           WUEDAT    ANDGTUNICD                                                         209171
     C                     Z-ADDUNICD     WENDDT
     C                     ELSE
     C                     Z-ADDWUEDAT    WENDDT
     C                     ENDIF
      *
      ** If interest is up front, calculate interest via SRCAL1,
      ** else, calculate interest due SRDTP2
      *
     C                     EXSR SRUSET
      *
     C           UINPM     IFEQ 'D'
     C           UINPM     OREQ 'Y'
      *
     C                     Z-ADDWUNEDT    WEDAT
     C                     EXSR SRCAL1
     C                     ELSE
     C                     EXSR SRDTP2
     C                     ENDIF
      *
      ** Add the End Interest to the Interest due
      *
     C           WINTD     ADD  WEINTR    WEINTR
     C                     Z-ADDWEINTR    WUINTR    H
      *
      ** If the deal is tax payable, calculate the tax on the interest due
      *
     C           TAXI      IFEQ 'Y'
     C           WEINTR    MULT BNHKTR    WZTAXC    H
     C           WZTAXC    DIV  100       WTAXC
     C                     ENDIF
      *
      ** Calculate the Interest Net of Tax (OUR Interest Amount)
      *
     C           WEINTR    SUB  WTAXC     WUINOT    H
      *
      ** Save the Effective Interest Rate
      *
     C                     Z-ADDUEINR     WUEINS 117
      *
      ** If our side of the deal is an OIS, call DL1646 to retrieve
      ** the settlement amount (net of tax).
      *
     C           UICFR     IFEQ 'O'
     C                     CALL 'DL1646'
     C                     PARM DLNO      PDLNO   60
     C                     PARM 'O'       PSIDE   1
     C           PAMNT     PARM *ZERO     PAMNT  130
     C                     PARM *BLANKS   PRTRN   1
      *
      ** If call ends in error:
      *
     C           PRTRN     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE
     C                     MOVEL'*NONE   'DBFILE
     C                     MOVELDLNO      DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ELSE
     C                     Z-ADDPAMNT     WUINOT    H
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If their base rate code is not zero and the floating rate has
      ** not been set,or if their base rate code is not equal to zero,
      ** and the input parameter PRERUN is equal to  'Y' then
      ** Access floating rate for base rate using SR:SRTTYP
      *
      ** Process THEIR details if THEIR Event Date due and
      ** Print for THEIR details requested
      *
     C           WTPRT     IFEQ 'Y'
     C                     Z-ADD0         WEINTR
      *
      ** If details relate to a non-zero Base Rate Type and the Event
      ** Date is greater than the Rate Change Date, calculate interest
      ** rate for that period via SRTTYP
      *
     C           TBRTT     IFNE 0
      *
      ** Bit '2' Of TDNSI signifies Short Term Rate Set. Only payments
      ** due today selected so if TEINR not equal to 0 and TSTRT equal to 0,
      ** floating rate was set on previous run and should be reversed.
      *
     C           TEINR     IFNE 0
     C           TSTRT     ANDEQ0
     C                     MOVE '1'       *IN79
     C                     ELSE
     C                     MOVE '0'       *IN79
     C                     ENDIF
      *
     C                     TESTB'2'       TDNSI          20
      *
      **If PRERUN and *IN79 is off, then rate has not been previously
      **set and hence deal shouldn't be reversed before re-fixing rate
      *
     C           PRERUN    IFEQ 'Y'
     C           *IN79     IFEQ '0'
     C                     MOVE 'N'       WPRVST  1
     C                     ELSE
     C                     MOVE 'Y'       WPRVST
     C                     ENDIF
     C                     ENDIF
      *
      ** Set on deals file update indicator.
      *
     C                     MOVE '1'       *IN66
      *
      ** Increment count of deals with rate fixed.
      *
     C                     ADD  1         RRTFIX
      *
     C                     EXSR SRTTYP
     C                     MOVE '1'       *IN56
      *
     C           TINPM     IFEQ 'D'
     C           TINPM     OREQ 'Y'
     C                     Z-ADDWRATE     TEINR
     C                     Z-ADDWBBRTE    WTBRTE
     C                     ENDIF
      *
     C           TNICD     IFNE 0
     C********** WTEDAT    ANDGTTNICD                                                         209171
     C           WEDATT    ANDGTTNICD                                                         209171
      *
      ** Use SR/GLINTC to calculate the End Interest from the Interest
      ** Change Date up to but excluding the Event Date
      *
     C                     Z-ADDWRATE     ZIRATE
     C           *IN20     IFEQ '1'
     C                     Z-SUBWRATE     ZIRATE
     C                     ENDIF
     C                     Z-ADDTICBS     ZICALC
     C                     Z-ADDTNICD     ZIBEG
     C                     Z-ADDWTEDAT    ZIEND
     C                     Z-ADDTPAMT     ZIAMT
     C                     EXSR GLINTC
      *
     C                     Z-ADDZINTR     WEINTR
      *
     C                     ELSE
      *
      ** Otherwise, zeroise End Interest
      *
     C                     Z-ADD0         WEINTR
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Set End Date to the Event Date or the Rate Change Date IF
      ** if this before the Event Date
      *
     C           TNICD     IFNE 0
     C********** WTEDAT    ANDGTTNICD                                                         209171
     C           WEDATT    ANDGTTNICD                                                         209171
     C                     Z-ADDTNICD     WENDDT
     C                     ELSE
     C                     Z-ADDWTEDAT    WENDDT
     C                     ENDIF
      *
      ** If interest is up front, calculate interest via SRCAL1,
      ** else, calculate interest due via SRDTP2
      *
     C                     EXSR SRTSET
      *
     C           TINPM     IFEQ 'D'
     C           TINPM     OREQ 'Y'
     C                     Z-ADDWTNEDT    WEDAT
     C                     EXSR SRCAL1
     C                     ELSE
     C                     EXSR SRDTP2
     C                     ENDIF
      *
      ** Add the End Interest to the Interest Due
      *
     C           WINTD     ADD  WEINTR    WEINTR
     C                     Z-ADDWEINTR    WTINTR    H
      *
      ** Interest is receivable, therefore no tax
      *
     C                     Z-ADDWEINTR    WTINOT    H
      *
      ** If Their side of the deal is an OIS, call DL1646 to retrieve
      ** the settlement amount.
      *
     C           TICFR     IFEQ 'O'
     C                     CALL 'DL1646'
     C                     PARM DLNO      PDLNO
     C                     PARM 'T'       PSIDE
     C           PAMNT     PARM *ZERO     PAMNT
     C                     PARM *BLANKS   PRTRN
      *
      ** If call ends in error:
      *
     C           PRTRN     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE
     C                     MOVEL'*NONE   'DBFILE
     C                     MOVELDLNO      DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ELSE
     C                     Z-ADDPAMNT     WTINOT    H
     C                     ENDIF
     C                     ENDIF
      *
      ** Save the Effective Interest Rate
      *
     C                     Z-ADDTEINR     WTEINS 117
      *
     C                     ENDIF
      *
      ** For matched 'RS' deals, calculate the Net Amount (Payment Amount)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
      ** If OUR Net Int greater than THEIR Net Int, pay settlement
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
      *
     C           WUINOT    IFGE WTINOT
     C                     MOVE 'P'       WSETL
     C           WUINOT    SUB  WTINOT    WNET
     C                     ELSE
      *
      ** If THEIR Net Int greater than  OUR Net Ine, receive settlement
      *
     C                     MOVE 'R'       WSETL
     C           WTINOT    SUB  WUINOT    WNET
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Unmatched 'RS'
      *
     C           DTYP      IFEQ 'RS'                          RS
     C********** WUEDAT    ANDNEWTEDAT                         NOT MATCH.                     209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WUINOT    IFNE 0
     C                     MOVE 'P'       WSETL
     C                     ENDIF
     C           WTINOT    IFNE 0
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
     C                     MOVE 'B'       WSETL                BOTH
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Set up Pay/Receive and Confirmations
      *
      ** If WSETL = 'B', execute SRAB,
      ** If WSETL = 'P', execute SRBC,
      ** If WSETL = 'R', run twice through the routines
      *
     C                     Z-ADD1         X
      *
     C           WSETL     IFEQ 'B'
     C                     ADD  1         X
     C                     ENDIF
      *
     C           X         DOUEQ0
      *
      ** Set up Pay/Receive instructions
      *
     C                     MOVE ' '       WCNRQ
      *
     C                     EXSR SRAB
      *
     C                     Z-ADD0         WPRI1   10
     C                     Z-ADD0         WPRI2   10
     C                     Z-ADD0         WPRI3   10
      *
      ** Set up Interest Payment confirmations
      *
     C                     EXSR SRBC
      *
     C                     SUB  1         X
      *
     C           WSETL     IFEQ 'B'
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** Set up interest fix confirmations
      *
     C                     EXSR SRBD
      *
      ** Set indicator for Deals file update
      *
     C                     MOVE '1'       *IN66
      *
      ** Update PF/DLOEVTPD (OIS Held Events).
      *
      ** If the dates have not been advanced, there will be no DLOEVTPD
      ** record to update, so bypass this code.
      *
     C           WOIUVD    IFEQ WUEDAT
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
      ** Key list for DLOEVTPD.
      *
     C           DLOEVT    KLIST
     C                     KFLD           KDEAL   6
     C                     KFLD           L4VDAT
      *
     C           WJUMP     IFEQ 'N'
      *
      ** Build the key.
      *
     C                     MOVE DLNO      KDEAL
     C           WSETL     IFEQ 'P'
     C                     Z-ADDWOIUVD    L4VDAT
     C                     ELSE
     C                     Z-ADDWOITVD    L4VDAT
     C                     ENDIF
      *
      ** Chain to the Held Event record.
      *
     C           DLOEVT    CHAINDLOEVTL2             74
      *
      ** If a record was found, it will have been created by DL0401
      ** (FRA/IRS Events) in the previous COB. The amount on this
      ** record, however, will only be an approximation, as the final
      ** rate has only just become available. Using this rate, we have
      ** just calculated the exact settlement amounts. We update the
      ** PF/DLOEVTPD record, as it will be mergerd back into the main
      ** events file during each COB, until the (advanced) value date
      ** is reached.
      *
     C           *IN74     IFEQ '0'
      *
      ** If a matched settlement
      *
     C********** WUEDAT    IFEQ WTEDAT                                                        209171
     C           WEDATU    IFEQ WEDATT                                                        209171
     C                     Z-ADDWNET      EAMT
     C                     ENDIF
      *
      ** If a non-matched settlement and our side is settling
      *
     C********** WUEDAT    IFNE WTEDAT                                                        209171
     C           WEDATU    IFNE WEDATT                                                        209171
     C           WSETL     IFEQ 'P'
     C                     Z-ADDWUINOT    EAMT
     C                     ENDIF
      *
      ** If a non-matched settlement and their side is settling
      *
     C           WSETL     IFEQ 'R'
     C                     Z-ADDWTINOT    EAMT
     C                     ENDIF
     C                     ENDIF
      *
     C           PSCY      IFEQ *BLANKS
      *
      ** Update the record
      *
     C                     UPDATDLOEVTD0
      *
     C                     ELSE
      *
     C                     Z-ADDEAMT      WEAMT  130
     C                     MOVE UCUCY     P@FRCY
     C                     MOVE PSCY      P@TOCY
     C                     Z-ADDWEAMT     P@FRAM
      *
     C                     EXSR SRCONV
     C                     Z-ADDP@OUTA    WOTHA  130
      *
     C           *IN74     DOWEQ'0'
      *
     C           ECCY      IFEQ UCUCY
     C                     Z-ADDWEAMT     EAMT
     C                     ENDIF
      *
     C           ECCY      IFEQ PSCY
     C                     Z-ADDWOTHA     EAMT
     C                     ENDIF
      *
     C           OTHC      IFEQ UCUCY
     C                     Z-ADDWEAMT     OTHA
     C                     ENDIF
      *
     C           OTHC      IFEQ PSCY
     C                     Z-ADDWOTHA     OTHA
     C                     ENDIF
      *
      ** Update the record
      *
     C                     UPDATDLOEVTD0
      *
     C           DLOEVT    READEDLOEVTL2                 74
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF                           (WPRINT=Y)
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDTP2 - Process FRA/IRS Deal Types                          *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : GLINTC, SRDETC                                    *
      *                                                               *
      *****************************************************************
      *
     C           SRDTP2    BEGSR
      *
      ** Define fields for subroutine
      *
     C                     Z-ADD0         WSZAIT 130
     C                     Z-ADD0         WINTD  130
      *
     C                     Z-ADDWEDAT     ZIEND
     C                     Z-ADDWSZPPL    ZIAMT
     C                     Z-ADDWSZINR    ZIRATE
     C                     Z-ADDWSZCBS    ZICALC
      *
      ** If Deal Type ='RS', calculate interest from and including
      ** Interest Accrual Control Date to but excluding End Date
      *
     C           DTYP      IFEQ 'RS'
     C           WAVRM     ANDEQ'N'
      *
     C                     Z-ADDWSZAIC    ZIBEG
      *
      ** If frequency is not 'C' for Cash Flow Schedule,
      ** use GLINTC to calculate interest
      *
     C           WIPFC     IFNE 'C'
     C                     EXSR GLINTC
     C                     ELSE
      *
      ** If freq. is 'C', determine payment due on next date
      ** from Cash Flow Schedule
      *
     C           WDIR      IFEQ 'P'
     C                     MOVEL'U'       OTIN             * OURS
     C                     ELSE
     C                     MOVEL'T'       OTIN             * THEIRS
     C                     ENDIF
     C                     EXSR SRDETC
     C                     Z-ADDCFLA      ZINTR
     C                     ENDIF
     C                     ENDIF
      *
      ** Add interest accrued to Control Date, Accrued Interest Adj.
      ** not posted and Accrued Interest Adj. posted to interest calc
      ** to give Accrued Interest to Date
      *
     C           WSZIAC    ADD  WSZIAN    WSZAIT
     C           WSZAIT    ADD  WSZAIA    WSZAIT
     C           WSZAIT    ADD  ZINTR     WSZAIT    H
      *
      ** Subtract Interest Paid/Received to Date from Accrued Interest
      ** To Date to give interest due
      *
     C           WSZAIT    SUB  WSZIPR    WINTD
      *
      ** If interest is to be capitalized and End Date is Maturity,
      ** add interest paid/received to date for total amount due
      ** ADD INTEREST PAID/RECEIVED TO DATE FOR TOTAL AMOUNT DUE
      *
     C           WINPM     IFEQ 'C'
     C           WEDAT     IFEQ MDAT
     C                     ADD  WSZIPR    WINTD
     C                     ELSE
     C                     Z-ADD*ZERO     WINTD
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDETC - Determine next cash flow from the Cash Flow         *
      *           Schedule File                                       *
      *                                                               *
      *  Called by: SRDTP2                                            *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           SRDETC    BEGSR
      *
     C                     Z-ADDZIEND     PRDT
     C           KCFL      CHAINDLCFSCPD             99    *
     C           *IN99     IFEQ '1'
     C                     Z-ADD*ZERO     CFLA
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDETR - Determine Next Rate Change/Payment Date from Date   *
      *           Schedule File                                       *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRDETR    BEGSR
      *
     C                     Z-ADDZDAYNO    PRDT
      *
     C           KDTL      SETGTDLDTSCPD
     C           KDTP      READEDLDTSCPD                 99
      *
     C           *IN99     IFEQ '0'
     C                     Z-ADDPRDT      ZDAYNO
     C                     ELSE
     C                     Z-ADDMDAT      ZDAYNO
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCAL1 - Calculate interest up front                         *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : GLINTC, SRCAL2                                    *
      *                                                               *
      *****************************************************************
      *
     C           SRCAL1    BEGSR
      *
      ** Calculate interest from interest accrual control date
      ** to end date (This Payment Date to Next Payment Date)
      *
     C                     Z-ADDWSZAIC    ZIBEG
     C                     Z-ADDWEDAT     ZIEND
     C                     Z-ADDWSZPPL    ZIAMT
     C                     Z-ADDWSZINR    ZIRATE
     C                     Z-ADDWSZCBS    ZICALC
     C                     EXSR GLINTC
      *
      ** If interest is yield based (method is 'Y'), calculate it
      ** using information provided by GLINTC
      *
     C           WINPM     IFEQ 'Y'
     C                     EXSR SRCAL2
     C                     ENDIF
      *
      ** Add interest accrued to Control Date, accrued interest
      ** adjustments not posted, accrued interest adjustments posted
      ** and this interest amount to obtain the accrued interest to date
      *
     C           WSZIAC    ADD  WSZIAN    WSZAIT
     C                     ADD  WSZAIA    WSZAIT
     C                     ADD  ZINTR     WSZAIT    H
      *
      ** Subtract Interest Paid/Received To Date from Accrued Interest
      ** To Date to give interest due
      *
     C           WSZAIT    SUB  WSZIPR    WINTD
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCAL2 - Calculate interest up front                         *
      *                                                               *
      *  Called by: SRCAL1                                            *
      *                                                               *
      *  Calls    : GLINTC, SRCAL2                                    *
      *                                                               *
      *****************************************************************
      *
     C           SRCAL2    BEGSR
      *
      ** Interest = Principal ( 1 - 1 / ( 1 + Rate X Number of days/360))
      **       I.E. Principal ( 1 - 360 / ( 360 + Rate X Number of days))
      *
     C           ZICALC    IFEQ 2
     C           ZICALC    OREQ 3
     C           ZICALC    OREQ 5
     C           ZICALC    OREQ 7
     C           CIR004    ANDEQ'Y'
     C                     Z-ADD36000     WRKZI3  50
     C                     ELSE
     C           ZICALC    IFEQ 1
     C           ZICALC    OREQ 4
     C           ZICALC    OREQ 9                                                             CIR013
     C           #29FEB    ANDEQ'N'                                                           CIR013
     C                     Z-ADD36500     WRKZI3
     C                     ELSE
     C                     Z-ADD36600     WRKZI3
     C                     ENDIF
     C                     ENDIF
      *
     C           ZICALC    IFEQ 6                                                             CIR013
     C           ZICALC    OREQ 9                                                             CIR013
     C           #29FEB    ANDEQ'Y'                                                           CIR013
     C           INT6DY    MULT ZIRATE    WRKZI4 157                                          CIR013
     C                     ELSE                                                               CIR013
     C           ZZINDY    MULT ZIRATE    WRKZI4 157
     C                     ENDIF                                                              CIR013
     C                     ADD  WRKZI3    WRKZI4
      *
     C           WRKZI3    DIV  WRKZI4    WRKZI5 159H
     C           1         SUB  WRKZI5    WRKZI5
     C           ZIAMT     MULT WRKZI5    ZINTR     H
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRTSET - Set up 'THEIR' fields                               *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRTSET    BEGSR
      *
     C                     Z-ADDTPAMT     WSZPPL
     C                     Z-ADDTEINR     WSZINR
     C                     Z-ADDTICBS     WSZCBS
     C                     Z-ADDTIALC     WSZLIC
     C                     Z-ADDTAITC     WSZIAC
     C                     Z-ADDTAIAN     WSZIAN
     C                     Z-ADDTAIAP     WSZAIA
     C                     Z-ADDTAIPD     WSZIPT
     C                     Z-ADDTIPRD     WSZIPR
     C                     Z-ADDTIACD     WSZAIC
     C           WTBACK    IFEQ 'Y'
     C                     Z-ADDWTEDAT    WSZAIC
     C                     ENDIF
     C                     TESTB'3'       TDSTI          81
     C           *IN81     IFEQ '1'
     C                     MOVE 'Y'       WAVRM
     C                     ELSE
     C                     MOVE 'N'       WAVRM
     C                     ENDIF
      *
     C                     Z-ADDWENDDT    WEDAT
     C                     Z-ADDSDCAR     WSDCR
     C                     MOVEL'R'       WDIR
     C                     MOVELTIPFR     WIPFC
     C                     MOVELTINPM     WINPM
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRUSET - Set up 'OUR' fields                                 *
      *                                                               *
      *  Called by: SRIRS                                             *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRUSET    BEGSR
      *
     C                     Z-ADDUPAMT     WSZPPL 130
     C                     Z-ADDUEINR     WSZINR 117
     C                     Z-ADDUICBS     WSZCBS  10
     C                     Z-ADDUIALC     WSZLIC 130
     C                     Z-ADDUAITC     WSZIAC 130
     C                     Z-ADDUAIAN     WSZIAN 130
     C                     Z-ADDUAIAP     WSZAIA 130
     C                     Z-ADDUAIPD     WSZIPT 130
     C                     Z-ADDUIPRD     WSZIPR 130
     C                     Z-ADDUIACD     WSZAIC  50
     C           WUBACK    IFEQ 'Y'
     C                     Z-ADDWUEDAT    WSZAIC
     C                     ENDIF
     C                     TESTB'3'       UDSTI          81
     C           *IN81     IFEQ '1'
     C                     MOVE 'Y'       WAVRM   1
     C                     ELSE
     C                     MOVE 'N'       WAVRM
     C                     ENDIF
      *
     C                     Z-ADDWENDDT    WEDAT   50
     C                     Z-ADDSDCAR     WSDCR   50
     C                     MOVEL'P'       WDIR    1
     C                     MOVELUIPFR     WIPFC   1
     C                     MOVELUINPM     WINPM   1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRMEMO - Update PF/MEMOS shadow postings file for an amended *
      *           deal                                                *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  Calls    : AOCURRR0, AOMEMSU0, *PSSR, SRACCA, SRCONV, SRACMT *
      *                                                               *
      *****************************************************************
      *
     C           SRMEMO    BEGSR
      *
     C                     MOVE 'N'       WJUMP
      *
      ** Save new values of Net Totals
      *
     C                     Z-ADDWUINOT    WUINO1 130
     C                     Z-ADDWTINOT    WTINO1 130
     C                     Z-ADDWUPSET    WUINOT
     C                     Z-ADDWTPSET    WTINOT
     C                     MOVE 'N'       WURET
     C                     MOVE 'N'       WTRET
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANK
     C                     MOVE PSCY      PCURR
     C                     ELSE
     C                     MOVE UCUCY     PCURR
     C                     ENDIF
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD7         DBASE
     C                     MOVE 'SDCURRPD'DBFILE
     C                     MOVELPCURR     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *LIKE     DEFN VUDT      WMEMDT
     C           *LIKE     DEFN VDAT      WRSADT
     C                     Z-ADDVUDT      WMEMDT
     C                     Z-ADDVDAT      WRSADT
      *
      ** If we are advancing the date, we must not update PF/MEMOS,
      ** as this file holds details of settlements due today only.
      *
     C           A6SETD    IFNE 0
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
     C           WJUMP     IFEQ 'N'
      *
      ** Determine whether PF/MEMOS requires reversing before updating
      *
      ** Since PF/DEALSDG has been updated, the current
      ** file field values are the 'new' versions (the '@_____' fields)
      ** and the 'old' values from the file at beginning of
      ** the programme are in the '#_____' fields.
      *
      ** Process if programme is being re-run, the base rate code is not
      ** zero and bit '0' UDNSI = '1' and rate has been previously set.
      ** Otherwise no Memos reversal is required.
      *
      ** Only process Details if instructions settled through
      ** a retail account.
      *
     C                     MOVE PSETM     WDSETM
     C**********           Z-ADDCNUM      WDCNUM                                              CSD027
     C                     MOVE CNUM      WDCNUM                                              CSD027
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      WDDCCY
     C                     ELSE
     C                     MOVE UCUCY     WDDCCY
     C                     ENDIF
     C                     MOVE UPACC     DBNKAC
     C                     MOVELPOBR      WWBRCA
      *
     C                     EXSR SRACCA
      *
     C           WRETL     IFEQ 'N'
     C                     MOVE 'N'       WURET   1
     C                     MOVE 'Y'       WJUMP
     C                     ELSE
     C**********           Z-ADDWCNUM     WUCNUM  60                                          CSD027
     C                     MOVE WCNUM     WUCNUM  6                                           CSD027
     C                     MOVE WCCY      WUCCY   3
     C**********           Z-ADDWACOD     WUACOD  40                                          CGL029
     C                     Z-ADDWACOD     WUACOD 100                                          CGL029
     C                     Z-ADDWACSQ     WUACSQ  20
     C                     MOVE 'Y'       WURET
     C                     ENDIF
      *
     C           WJUMP     IFEQ 'N'
     C**********           MOVELURACC     WACC   12                                           CGL029
     C                     MOVELURACC     WACC   18                                           CGL029
     C                     ENDIF
      *
      ** Only process Details if instructions settled through
      ** a retail account.
      *
     C                     MOVE RSETM     WDSETM
     C**********           Z-ADDCNUM      WDCNUM                                              CSD027
     C                     MOVE CNUM      WDCNUM                                              CSD027
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE RSCY      WDDCCY
     C                     ELSE
     C                     MOVE TCUCY     WDDCCY
     C                     ENDIF
     C                     MOVE URACC     DBNKAC
     C                     MOVELROBR      WWBRCA
      *
     C                     EXSR SRACCA
      *
     C           WRETL     IFEQ 'N'
     C                     MOVE 'N'       WTRET   1
     C                     ELSE
     C**********           Z-ADDWCNUM     WTCNUM  60                                          CSD027
     C                     MOVE WCNUM     WTCNUM  6                                           CSD027
     C                     MOVE WCCY      WTCCY   3
     C**********           Z-ADDWACOD     WTACOD  40                                          CGL029
     C                     Z-ADDWACOD     WTACOD 100                                          CGL029
     C                     Z-ADDWACSQ     WTACSQ  20
     C                     MOVE 'Y'       WTRET
     C                     ENDIF
      *
      ** ( Fixed side has Base rate type EQ 0,
      ** Rate/Spread is the Effective Interest Rate)
      *
      ** Access PF/MEMOS record
      *
      ** For RS Deals, determine if matched, if so, calculate the Net
      ** Amount (Payment Amount)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
      ** If OUR Net Int *GT THEIR Net Int, pay settlement
      *
     C           WUINOT    IFGE WTINOT
     C                     MOVE 'P'       WSETL
     C           WUINOT    SUB  WTINOT    WUINOT
     C                     ELSE
      *
      ** If THEIR Net Int *GT OUR Net Int, receive settlement
      *
     C                     MOVE 'R'       WSETL
     C           WTINOT    SUB  WUINOT    WTINOT
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Unmatched 'RS'
      *
     C           DTYP      IFEQ 'RS'                          RS
     C********** WUEDAT    ANDNEWTEDAT                         NOT MATCH.                     209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WUINOT    IFNE 0
     C                     MOVE 'P'       WSETL
     C                     ENDIF
     C           WTINOT    IFNE 0
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
     C                     MOVE 'B'       WSETL                BOTH
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Adjust Shadow Balances
      ** (Pay)
      *
     C           WURET     IFEQ 'Y'
     C           WUINOT    ANDNE0
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
      *
     C                     MOVE PSETM     WDSETM
     C**********           Z-ADDCNUM      WDCNUM                                              CSD027
     C                     MOVE CNUM      WDCNUM                                              CSD027
     C                     MOVE UCUCY     WDDCCY
     C                     MOVE UPACC     DBNKAC
      *
      ** Convert WUINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE UCUCY     P@FRCY
     C                     MOVE PSCY      P@TOCY
     C                     Z-ADDWUINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WUINOT
      *
     C                     ENDIF
      *
      ** Update PF/MEMOS file
      *
     C                     Z-ADDWUINOT    WPAMT  150
      *
      ** Call access object AOMEMSU0 to update the MEMOS file
      *
     C                     CALL 'AOMEMSU0'
     C                     PARM *BLANKS   P0RTCD  7        I:Return code
     C                     PARM *ZEROS    P0ACNO 100       O:Retail A/C No
     C**********           PARM WUCNUM    P0CNUM  60       O:Customer No.                     CSD027
     C                     PARM WUCNUM    P0CNUM  6        O:Customer No.                     CSD027
     C                     PARM WUCCY     P0CCY   3        O:Currency
     C**********           PARM WUACOD    P0ACOD  40       O:Account Code                     CGL029
     C                     PARM WUACOD    P0ACOD 100                                          CGL029
     C                     PARM WUACSQ    P0ACSQ  20       O:Account Seq.
     C                     PARM BRCA      P0BRCA  3        O:Branch
     C                     PARM WMEMDT    P0PSTD  50       O:Posting date
     C                     PARM WMEMDT    P0VDAT  50       O:Value date
     C                     PARM WPAMT     P0AMT  150       O:Movement amt
     C                     PARM 'N'       P0FTOV  1        O:FT override
      *
     C           P0RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD8         DBASE
     C                     MOVEL'AOMEMSU0'DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If not a nostro account then output posting details to RSACMTPD
      *
     C           WNOST     IFEQ 'N'
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      W#CCY
     C                     ELSE
     C                     MOVELUCUCY     W#CCY   3
     C                     ENDIF
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDBJRDNB    VUDT
     C                     Z-ADDWUINOT    WNETA  150
     C                     Z-ADD0         DORC
     C**********           Z-ADDWUCNUM    CUSN                                                CSD027
     C                     MOVE WUCNUM    CUSN                                                CSD027
     C                     MOVE WUCCY     CCYD
     C                     Z-ADDWUACOD    ACDE
     C                     Z-ADDWUACSQ    ASNC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** ( Receive )
      *
     C           WTRET     IFEQ 'Y'
     C           WTINOT    ANDNE0
     C           WSETL     IFEQ 'R'
     C           WSETL     OREQ 'B'
      *
     C                     MOVE RSETM     WDSETM
     C**********           Z-ADDCNUM      WDCNUM                                              CSD027
     C                     MOVE CNUM      WDCNUM                                              CSD027
     C                     MOVE TCUCY     WDDCCY
     C                     MOVE URACC     DBNKAC
      *
      ** Convert WTINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE TCUCY     P@FRCY
     C                     MOVE RSCY      P@TOCY
     C                     Z-ADDWTINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WTINOT
      *
     C                     ENDIF
      *
      ** Update PF/MEMOS file
      *
     C                     Z-SUBWTINOT    WPAMT
      *
      ** Call access object AOMEMSU0 to update the MEMOS file
      *
     C                     CALL 'AOMEMSU0'
     C                     PARM *BLANKS   P0RTCD           I:Return code
     C                     PARM *ZEROS    P0ACNO           O:Retail A/C No
     C                     PARM WTCNUM    P0CNUM           O:Customer No.
     C                     PARM WTCCY     P0CCY            O:Currency
     C                     PARM WTACOD    P0ACOD           O:Account Code
     C                     PARM WTACSQ    P0ACSQ           O:Account Seq.
     C                     PARM BRCA      P0BRCA           O:Branch
     C                     PARM WMEMDT    P0PSTD           O:Posting date
     C                     PARM WMEMDT    P0VDAT           O:Value date
     C                     PARM WPAMT     P0AMT            O:Movement amt
     C                     PARM 'N'       P0FTOV           O:FT override
      *
     C           P0RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD9         DBASE
     C                     MOVEL'AOMEMSU0'DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If not a nostro account then output posting details to RSACMTPD
      *
     C           WNOST     IFEQ 'N'
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE RSCY      W#CCY
     C                     ELSE
     C                     MOVELTCUCY     W#CCY
     C                     ENDIF
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDBJRDNB    VUDT
     C                     Z-ADDWTINOT    WNETA
     C                     Z-ADD1         DORC
     C**********           Z-ADDWTCNUM    CUSN                                                CSD027
     C                     MOVE WTCNUM    CUSN                                                CSD027
     C                     MOVE WTCCY     CCYD
     C                     Z-ADDWTACOD    ACDE
     C                     Z-ADDWTACSQ    ASNC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Restore saved values and update Memos balances with new values
      *
     C                     Z-ADDWUINO1    WUINOT
     C                     Z-ADDWTINO1    WTINOT
      *
      ** For RS deals, determine if matched, if so, calculate the Net
      ** Amount (Payment Amount)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
      ** If OUR Net Int *GT THEIR Net Int, pay settlement
      *
     C           WUINOT    IFGE WTINOT
     C                     MOVE 'P'       WSETL
     C           WUINOT    SUB  WTINOT    WUINOT
     C                     ELSE
      *
      ** If THEIR Net Int *GT OUR Net Int - receive settlement
      *
     C                     MOVE 'R'       WSETL
     C           WTINOT    SUB  WUINOT    WTINOT
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Unmatched 'RS'
      *
     C           DTYP      IFEQ 'RS'                          RS
     C********** WUEDAT    ANDNEWTEDAT                         NOT MATCH.                     209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WUINOT    IFNE 0
     C                     MOVE 'P'       WSETL
     C                     ENDIF
     C           WTINOT    IFNE 0
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
     C                     MOVE 'B'       WSETL                BOTH
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Adjust Shadow Balances
      ** ( Pay )
      *
     C           WURET     IFEQ 'Y'
     C           WUINOT    ANDNE0
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
      *
     C                     MOVE PSETM     WDSETM
     C**********           Z-ADDCNUM      WDCNUM                                              CSD027
     C                     MOVE CNUM      WDCNUM                                              CSD027
     C                     MOVE UCUCY     WDDCCY
     C                     MOVE UPACC     DBNKAC
      *
      ** Convert WUINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE UCUCY     P@FRCY
     C                     MOVE PSCY      P@TOCY
      *
     C                     Z-ADDWUINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WUINOT
      *
     C                     ENDIF
      *
      ** Update PF/MEMOS file
      *
     C                     Z-SUBWUINOT    WPAMT
      *
      ** Call access object AOMEMSU0 to update the MEMOS file
      *
     C                     CALL 'AOMEMSU0'
     C                     PARM *BLANKS   P0RTCD           I:Return code
     C                     PARM *ZEROS    P0ACNO           O:Retail A/C No
     C                     PARM WUCNUM    P0CNUM           O:Customer No.
     C                     PARM WUCCY     P0CCY            O:Currency
     C                     PARM WUACOD    P0ACOD           O:Account Code
     C                     PARM WUACSQ    P0ACSQ           O:Account Seq.
     C                     PARM BRCA      P0BRCA           O:Branch
     C                     PARM WMEMDT    P0PSTD           O:Posting date
     C                     PARM WMEMDT    P0VDAT           O:Value date
     C                     PARM WPAMT     P0AMT            O:Movement amt
     C                     PARM 'N'       P0FTOV           O:FT override
      *
     C           P0RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD10        DBASE
     C                     MOVEL'AOMEMSU0'DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If not a nostro account then output posting details to RSACMTPD
      *
     C           WNOST     IFEQ 'N'
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      W#CCY
     C                     ELSE
     C                     MOVELUCUCY     W#CCY
     C                     ENDIF
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDBJRDNB    VUDT
     C                     Z-ADDWUINOT    WNETA
     C                     Z-ADD1         DORC
     C**********           Z-ADDWUCNUM    CUSN                                                CSD027
     C                     MOVE WUCNUM    CUSN                                                CSD027
     C                     MOVE WUCCY     CCYD
     C                     Z-ADDWUACOD    ACDE
     C                     Z-ADDWUACSQ    ASNC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** ( Receive )
      *
     C           WTRET     IFEQ 'Y'
     C           WTINOT    ANDNE0
     C           WSETL     IFEQ 'R'
     C           WSETL     OREQ 'B'
      *
     C                     MOVE RSETM     WDSETM
     C**********           Z-ADDCNUM      WDCNUM                                              CSD027
     C                     MOVE CNUM      WDCNUM                                              CSD027
     C                     MOVE TCUCY     WDDCCY
     C                     MOVE URACC     DBNKAC
      *
      ** Convert WTINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE TCUCY     P@FRCY
     C                     MOVE RSCY      P@TOCY
     C                     Z-ADDWTINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WTINOT
      *
     C                     ENDIF
      *
      ** Update PF/MEMOS file
      *
     C                     Z-ADDWTINOT    WPAMT
      *
      ** Call access object AOMEMSU0 to update the MEMOS file
      *
     C                     CALL 'AOMEMSU0'
     C                     PARM *BLANKS   P0RTCD           I:Return code
     C                     PARM *ZEROS    P0ACNO           O:Retail A/C No
     C                     PARM WTCNUM    P0CNUM           O:Customer No.
     C                     PARM WTCCY     P0CCY            O:Currency
     C                     PARM WTACOD    P0ACOD           O:Account Code
     C                     PARM WTACSQ    P0ACSQ           O:Account Seq.
     C                     PARM BRCA      P0BRCA           O:Branch
     C                     PARM WMEMDT    P0PSTD           O:Posting date
     C                     PARM WMEMDT    P0VDAT           O:Value date
     C                     PARM WPAMT     P0AMT            O:Movement amt
     C                     PARM 'N'       P0FTOV           O:FT override
      *
     C           P0RTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD11        DBASE
     C                     MOVEL'AOMEMSU0'DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If not a nostro account then output posting details to RSACMTPD
      *
     C           WNOST     IFEQ 'N'
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE RSCY      W#CCY
     C                     ELSE
     C                     MOVELTCUCY     W#CCY
     C                     ENDIF
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDBJRDNB    VUDT
     C                     Z-ADDWTINOT    WNETA
     C                     Z-ADD0         DORC
     C**********           Z-ADDWTCNUM    CUSN                                                CSD027
     C                     MOVE WTCNUM    CUSN                                                CSD027
     C                     MOVE WTCCY     CCYD
     C                     Z-ADDWTACOD    ACDE
     C                     Z-ADDWTACSQ    ASNC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Restore saved values for PRONO update.
      *
     C                     Z-ADDWUINO1    WUINOT
     C                     Z-ADDWTINO1    WTINOT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SROLPO - Updates PF/OLPOS on Line Positions file for an      *
      *           amended deal                                        *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  Calls    : AOCURRR0, *PSSR, ZFWDT, SROLP, SRACCO             *
      *                                                               *
      *****************************************************************
      *
     C           SROLPO    BEGSR
      *
     C                     MOVE 'N'       WJUMP
      *
      ** Save new values of Net Totals
      *
     C                     Z-ADDWUINOT    WUINO1 130
     C                     Z-ADDWTINOT    WTINO1 130
      *
      ** Settlement amounts from previous run, for reversals.
      *
     C                     MOVELWUPSET    WUINOT
     C                     Z-ADDWTPSET    WTINOT
      *
      ** Reset record fields for both sides.
      *
     C                     MOVE 'N'       WUOLP
     C                     MOVE 'N'       WTOLP
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANK
     C                     MOVE PSCY      PCURR
     C                     ELSE
     C                     MOVE UCUCY     PCURR
     C                     ENDIF
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD12        DBASE
     C                     MOVE 'SDCURRPD'DBFILE
     C                     MOVELPCURR     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDWUEDAT    WUDAT
     C                     Z-ADDWTEDAT    WTDAT
      *
      ** If Settlement Days is not zero WUDAT & WTDAT must be increased
      ** by a number of working days in the Deal CCY
      *
     C           A6SETD    IFNE 0
      *
     C                     MOVELWUDAT     ZDAYNO
     C                     MOVELPCURR     ZCCY
     C                     Z-ADDA6SETD    ZNRDYS
     C                     EXSR ZFWDT
     C                     MOVELZDYNBR    WUDAT
      *
     C                     MOVELWTDAT     ZDAYNO
     C                     MOVELPCURR     ZCCY
     C                     Z-ADDA6SETD    ZNRDYS
     C                     EXSR ZFWDT
     C                     MOVELZDYNBR    WTDAT
      *
     C                     ENDIF
      *
      ** Determine whether PF/OLPOS requires updating
      *
      ** Since PF/DEALSDG has been updated, the current
      ** file field values are the 'new' versions (the '@_____' fields)
      ** and the 'old' values from the file at beginning of
      ** the programme are in the '#_____' fields.
      *
      ** Set Date on PF/OLPOSOA to be amended ( 'P' )
      *
      ** Process if programme is being re-run, the base rate code is not
      ** zero and bit '0' UDNSI = '1'(ie. *IN55 or 56 = '1') and rate
      ** has been previously set.
      ** Otherwise no Olpos reversal is required.
      *
      ** Access Olpos record
      *
     C                     EXSR SROLP
      *
      ** Only process Details if a record is found
      ** ( Otherwise End )
      *
     C           *IN86     IFEQ '1'
     C           RECI      ORNE 'D'
      *
      ** Release PF/OLPOSOA record if record found, but not live
      *
     C           *IN86     IFEQ '0'
     C                     UNLCKOLPOS
     C                     ENDIF
      *
     C                     MOVE 'N'       WUOLP   1
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
     C           WJUMP     IFEQ 'N'
      *
     C                     MOVE 'Y'       WUOLP
     C                     UNLCKOLPOS
      *
      ** Net Settlement Amounts are applied to the on line position
      ** balance whose occurrence is the greatest of the 16 dates
      ** which is less than, or equal to, the event date
      *
      ** If event is earlier than the first of the 16th dates,
      ** use the first date
      ** If event is on or after the 16th date,
      ** use the 16th date ( Catchall )
      *
     C                     Z-ADD1         P       20
      *
     C           WUDAT     IFGT ODAT,1
      *
     C           WUDAT     IFGE ODAT,16
     C                     Z-ADD16        P
     C                     ELSE
     C           WUDAT     LOKUPODAT,P                 8585
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVE 'N'       WJUMP
      *
      ** Access Olpos record
      *
     C                     EXSR SRACCO
      *
      ** Only process Details if a record is found
      ** ( Otherwise End )
      *
     C           *IN86     IFEQ '1'
     C           RECI      ORNE 'D'
      *
      ** Release PF/OLPOSOA record if record found, but not live
      *
     C           *IN86     IFEQ '0'
     C                     UNLCKOLPOS
     C                     ENDIF
      *
     C                     MOVE 'N'       WTOLP   1
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
     C           WJUMP     IFEQ 'N'
      *
     C                     MOVE 'Y'       WTOLP
     C                     UNLCKOLPOS
      *
      ** ( Fixed side has Base rate type EQ 0,
      ** Rate/Spread is the Effective Interest Rate)
      *
      ** Process if programme is being re-run, the base rate code is not
      ** zero and bit '0' UDNSI = '1' and rate has been previously set.
      ** Otherwise no OLPOS reversal is required.
      *
      ** Net Settlement Amounts are applied to the on line position
      ** balance whose occurrence is the greatest of the 16 dates
      ** which is less than, or equal to, the event date
      *
      ** If event is earlier than the first of the 16th dates,
      ** use the first date
      ** If event is on or after the 16th date,
      ** use the 16th date ( Catchall )
      *
     C                     Z-ADD1         R       20
      *
     C           WTDAT     IFGT ODAT,1
      *
     C           WTDAT     IFGE ODAT,16
     C                     Z-ADD16        R
     C                     ELSE
     C           WTDAT     LOKUPODAT,R                 8585
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** For RS deals determine if matched, if so, calculate the Net
      ** Amount (Payment Amount)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
      ** If OUR Net Int *GT THEIR Net Int, pay settlement
      *
     C           WUINOT    IFGE WTINOT
     C                     MOVE 'P'       WSETL
     C           WUINOT    SUB  WTINOT    WUINOT
     C                     ELSE
      *
      ** If THEIR Net Int *GT OUR Net Int, receive settlement
      *
     C                     MOVE 'R'       WSETL
     C           WTINOT    SUB  WUINOT    WTINOT
     C                     ENDIF
      *
     C                     ENDIF
      *
      * Unmatched 'RS'
      *
     C           DTYP      IFEQ 'RS'                          RS
     C********** WUEDAT    ANDNEWTEDAT                         NOT MATCH.                     209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WUINOT    IFNE 0
     C                     MOVE 'P'       WSETL
     C                     ENDIF
     C           WTINOT    IFNE 0
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
     C                     MOVE 'B'       WSETL                BOTH
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Adjust Shadow Balances
      ** ( Pay )
      *
     C           WUOLP     IFEQ 'Y'
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
      *
     C                     EXSR SROLP
      *
      ** Update PF/OLPOS file
      *
     C                     SUB  WUINOT    OBLO,P
     C                     UPDATOLPOSOAF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** ( Receive )
      *
     C           WTOLP     IFEQ 'Y'
     C           WSETL     IFEQ 'R'
     C           WSETL     OREQ 'B'
      *
     C                     EXSR SRACCO
      *
      ** Update PF/MEMOS file
      *
     C                     SUB  WTINOT    OBLI,R
     C                     UPDATOLPOSOAF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** New deal details (the '@_____' fields)
      **
      ** Restore saved values and update Memos balances with new values
      **
      *
     C                     Z-ADDWUINO1    WUINOT
     C                     Z-ADDWTINO1    WTINOT
      *
      ** For RS deals determine if matched, if so, calculate the Net
      ** Amount (Payment Amount)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
      ** If OUR Net Int *GT THEIR Net Int, pay settlement
      *
     C           WUINOT    IFGE WTINOT
     C                     MOVE 'P'       WSETL
     C           WUINOT    SUB  WTINOT    WUINOT
     C                     ELSE
      *
      ** If THEIR Net Int *GT OUR Net Int, receive settlement
      *
     C                     MOVE 'R'       WSETL
     C           WTINOT    SUB  WUINOT    WTINOT
     C                     END
      *
     C                     ENDIF
      *
      ** Unmatched 'RS'
      *
     C           DTYP      IFEQ 'RS'                          RS
     C********** WUEDAT    ANDNEWTEDAT                         NOT MATCH.                     209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WUINOT    IFNE 0
     C                     MOVE 'P'       WSETL
     C                     ENDIF
     C           WTINOT    IFNE 0
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
     C                     MOVE 'B'       WSETL                BOTH
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Adjust Shadow Balances
      *    ( Pay )
     C           WUOLP     IFEQ 'Y'
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
      *
     C                     EXSR SROLP
      *
      ** Update PF/OLPOS file
      *
     C                     ADD  WUINOT    OBLO,P
     C                     UPDATOLPOSOAF
      *
     C                     ENDIF
     C                     ENDIF
      *
      *    ( Receive )
     C           WTOLP     IFEQ 'Y'
     C           WSETL     IFEQ 'R'
     C           WSETL     OREQ 'B'
      *
     C                     MOVE RSETM     WDSETM
      *
      ** Update PF/MEMOS file
      *
     C                     EXSR SRACCO
      *
      ** Update PF/OLPOS file
      *
     C                     ADD  WTINOT    OBLI,R
     C                     UPDATOLPOSOAF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     Z-ADDWUINO1    WUINOT
     C                     Z-ADDWTINO1    WTINOT
      *
     C                     ENDSR
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  SRACCO - Access PF/OLPOS                                     *
      *                                                               *
      *  Called by: SROLPO                                            *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRACCO    BEGSR
      *
     C                     MOVE TCUCY     KOLCCY
     C                     MOVE '02'      KOLPNO
      *
     C           OLKEY     CHAINOLPOS                86
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  SRPRON - Updates PF/PRONO Projected Nostros file for an      *
      *           amended deal                                        *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  Calls    : AOCURRR0, AONOST, *PSSR, ZFWDT, SRPRO, SRCONV,    *
      *             SRACMT                                            *
      *                                                               *
      *****************************************************************
      *
     C           SRPRON    BEGSR
      *
     C                     MOVE 'N'       WJUMP
     C                     MOVE 'N'       WJUMP2  1
      *
      ** Save new values of net totals
      *
     C                     Z-ADDWUINOT    WUINO1 130
     C                     Z-ADDWTINOT    WTINO1 130
      *
      ** Reset Nostro settlement fields
      *
     C                     MOVE 'N'       WUPRO
     C                     MOVE 'N'       WTPRO
      *
      ** Initialise fields used for reversal.
      *
     C                     Z-ADDWUPSET    WUINOT
     C                     Z-ADDWTPSET    WTINOT
     C                     MOVE *BLANKS   WSETL
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANK
     C                     MOVE PSCY      PCURR
     C                     ELSE
     C                     MOVE UCUCY     PCURR
     C                     ENDIF
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD13        DBASE
     C                     MOVE 'SDCURRPD'DBFILE
     C                     MOVELPCURR     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           *LIKE     DEFN WUEDAT    WUDAT
     C           *LIKE     DEFN WTEDAT    WTDAT
     C                     Z-ADDWUEDAT    WUDAT
     C                     Z-ADDWTEDAT    WTDAT
      *
      ** If Settlement Days is not zero, WUDAT & WTDAT must be
      ** increased by a number of working days in the Deal CCY
      *
     C           A6SETD    IFNE 0
      *
     C                     MOVELWUDAT     ZDAYNO
     C                     MOVELPCURR     ZCCY
     C                     Z-ADDA6SETD    ZNRDYS
     C                     EXSR ZFWDT
     C                     MOVELZDYNBR    WUDAT
      *
     C                     MOVELWTDAT     ZDAYNO
     C                     MOVELPCURR     ZCCY
     C                     Z-ADDA6SETD    ZNRDYS
     C                     EXSR ZFWDT
     C                     MOVELZDYNBR    WTDAT
      *
     C                     ENDIF
      *
      ** Determine whether PF/PRONO requires updating
      *
      ** Since PF/DEALSDG has been updated, the current
      ** file field values are the 'new' versions (the '@_____' fields)
      ** and the 'old' values from the file at beginning of
      ** the programme are in the '#_____' fields.
      *
      ** Process if programme is being re-run, the base rate code is not
      ** zero and bit '0' UDNSI = '1' and rate has been previously set.
      ** Otherwise no PRONO reversal is required.
      *
      ** Only process details if settled through nostro
      ** ( Otherwise process New details )
      *
     C           PSETM     IFNE 01
     C           PSETM     ANDNE02
     C           PSETM     ANDNE07
     C           PSETM     ANDNE08
     C           PSETM     ANDNE12
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
     C           WJUMP     IFEQ 'N'
      *
      ** Access Prono record
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      PRCCY
     C                     ELSE
     C                     MOVE UCUCY     PRCCY
     C                     ENDIF
     C                     MOVELUPACC     PRNNO
      *
     C                     EXSR SRPRO
      *
      ** Only process Details if a record is found
      ** ( Otherwise process New details )
      *
     C           *IN86     IFEQ '1'
     C           RECI      ORNE 'D'
      *
      ** Release PF/PRONOPN record if record found, but not live
      *
     C           *IN86     IFEQ '0'
     C                     UNLCKPRONO
     C                     MOVE 'N'       WUPRO   1
     C                     ENDIF
      *
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
     C           WJUMP     IFEQ 'N'
      *
     C                     MOVE 'Y'       WUPRO
     C                     UNLCKPRONO
      *
      ** ( PAY )
      *
     C                     MOVE '1'       *IN74
      *
      ** If Event Date is beyond Range End Date for nostro, release
      ** record, then no further processing for original file details
      ** Otherwise, calculate discuounted amount & update record.
      *
      ** NB Range End Date is up to the 6th working day
      *
     C           PDAT,5    ADD  1         ZDAYNO
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      ZCCY
     C                     ELSE
     C                     MOVE UCUCY     ZCCY
     C                     ENDIF
      *
      ** Set up nostro location code (ZLOC) for input to ZFWDT.SR
      *
      ** Move nostro no. into 2A work field for call to access prog.
      *
     C                     MOVELUPACC     WRK2    2
      *
      ** Set up work field containing file key, in case of db error
      *
     C                     MOVELZCCY      WRK5    5
     C                     MOVE WRK2      WRK5
      *
      ** If nostro no. blanks, set ZLOC to blanks, otherwise call
      ** access program to get nostro location
      *
     C           WRK2      IFEQ *BLANKS
     C                     MOVE *BLANKS   ZLOC
     C                     ELSE
     C                     CALL 'AONOSTR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM *BLANK    PCUST   6        Customer No.
     C                     PARM ZCCY      PCCY    3        Currency
     C**********           PARM *BLANKS   PACCD   4        A/C Code                           CGL029
     C                     PARM *BLANKS   PACCD  10                                           CGL029
     C                     PARM *BLANKS   PACSN   2        A/C Seq
     C                     PARM WRK2      PNOST   2        Nostro No.
     C                     PARM *BLANKS   PBRCD   3        Branch Code
     C                     PARM *BLANKS   PCSSN  10        Cust. Shortname
     C                     PARM *BLANKS   PPNOI   1        Prime Nost. Ind
     C           SDNOST    PARM SDNOST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD14        DBASE
     C                     MOVE 'SDNOSTPD'DBFILE
     C                     MOVE WRK5      DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE A7NOSN    ZLOC
      * (Last 3 chars. of customer shortname contains nostro loc.)
     C                     ENDIF
     C                     ENDIF
      *
      ** Check that value of ZDAYNO is a working day, if not get
      ** the next working day after this date
      *
     C                     Z-ADD0         ZNRDYS  20
     C                     EXSR ZFWDT
      *
     C           VDAT      IFGE ZDYNBR
     C                     MOVE 'Y'       WJUMP2
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C           WJUMP2    IFEQ 'N'
      *
      ** Only process details if settled through nostro
      ** ( Otherwise process New details )
      *
     C           RSETM     IFNE 01
     C           RSETM     ANDNE02
     C           RSETM     ANDNE07
     C           RSETM     ANDNE08
     C           RSETM     ANDNE12
     C                     MOVE 'Y'       WJUMP2
     C                     ENDIF
      *
     C           WJUMP2    IFEQ 'N'
      *
      ** Access Prono record
      *
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE RSCY      PRCCY
     C                     ELSE
     C                     MOVE TCUCY     PRCCY
     C                     ENDIF
     C                     MOVELURACC     PRNNO
      *
     C                     EXSR SRPRO
      *
      ** Only process Details if a record is found
      ** ( Otherwise process New details )
      *
     C           *IN86     IFEQ '1'
     C           RECI      ORNE 'D'
      *
      ** Release PF/PRONOPN record if record found, but not live
      *
     C           *IN86     IFEQ '0'
     C                     UNLCKPRONO
     C                     MOVE 'N'       WTPRO   1
     C                     ENDIF
      *
     C                     MOVE 'Y'       WJUMP2
     C                     ENDIF
      *
     C           WJUMP2    IFEQ 'N'
      *
     C                     MOVE 'Y'       WTPRO
     C                     UNLCKPRONO
      *
      ** Process if programme is being re-run, the base rate code is not
      ** zero and bit '0' UDNSI = '1' and rate has been previously set.
      ** Otherwise no Memos reversal is required.
      *
      ** ( RECEIVE )
      *
     C                     MOVE '0'       *IN74
      *
      ** If Event Date is beyond Range End Date for nostro, release
      ** record, then no further processing for original file details
      ** Otherwise, calculate discuounted amount & update record.
      *
      ** NB Range End Date is up to the 6th working day
      *
     C           PDAT,5    ADD  1         ZDAYNO
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE RSCY      ZCCY
     C                     ELSE
     C                     MOVE TCUCY     ZCCY
     C                     ENDIF
      *
      ** Set up nostro location code (ZLOC) for input to ZFWDT.SR
      *
      ** Move nostro no. into 2A work field for call to access prog.
      *
     C                     MOVELURACC     WRK2
      *
      ** If the nostro no. from last call to AONOSTR0 is same as
      ** value in WRK2, no need for another call to access program
      ** since currency key is the same as well, therefore use
      ** same value of ZLOC
      *
     C           WRK2      IFNE A7NONB
     C           ZCCY      ORNE A7CYCD
      *
      ** Set up work field containing file key, in case of db error
      *
     C                     MOVELZCCY      WRK5
     C                     MOVE WRK2      WRK5
      *
      ** If nostro no. blanks, set ZLOC to blanks, otherwise call
      ** access program to get nostro location
      *
     C           WRK2      IFEQ *BLANKS
     C                     MOVE *BLANKS   ZLOC
     C                     ELSE
     C                     CALL 'AONOSTR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM *BLANK    PCUST   6        Customer No.
     C                     PARM ZCCY      PCCY    3        Currency
     C**********           PARM *BLANKS   PACCD   4        A/C Code                           CGL029
     C                     PARM *BLANKS   PACCD  10                                           CGL029
     C                     PARM *BLANKS   PACSN   2        A/C Seq
     C                     PARM WRK2      PNOST   2        Nostro No.
     C                     PARM *BLANKS   PBRCD   3        Branch Code
     C                     PARM *BLANKS   PCSSN  10        Cust. Shortname
     C                     PARM *BLANKS   PPNOI   1        Prime Nost. Ind
     C           SDNOST    PARM SDNOST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD15        DBASE
     C                     MOVE 'SDNOSTPD'DBFILE
     C                     MOVE WRK5      DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE A7NOSN    ZLOC
      * (Last 3 chars. of customer shortname contains nostro loc.)
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Check that value of ZDAYNO is a working day, if not get
      ** the next working day after this date
      *
     C                     Z-ADD0         ZNRDYS  20
     C                     EXSR ZFWDT
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** For RS deals, determine if matched, if so, calculate the Net
      ** Amount (Payment Amount)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
      ** If OUR Net Int *GT THEIR Net Int, pay settlement
      *
     C           WUINOT    IFGE WTINOT
     C                     MOVE 'P'       WSETL
     C           WUINOT    SUB  WTINOT    WUINOT
     C                     ELSE
     C                     MOVE 'R'       WSETL
     C           WTINOT    SUB  WUINOT    WTINOT
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Unmatched 'RS'
      *
     C           DTYP      IFEQ 'RS'                          RS
     C********** WUEDAT    ANDNEWTEDAT                         NOT MATCH.                     209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WUINOT    IFNE 0
     C                     MOVE 'P'       WSETL
     C                     ENDIF
     C           WTINOT    IFNE 0
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
     C                     MOVE 'B'       WSETL                BOTH
     C                     END
      *
     C                     ENDIF
      *
      ** Adjust Projected Nostro balances
      *
      ** Net Discounted Amounts are applied to the projected nostro
      ** balances whose occurrence matches the projected nostro dates
      ** which are GE the Event Date.
      *
      ** If event is earlier than the first of the 5 dates,
      ** use from the first date
      ** If event is after the 5th date, but before the 6th
      ** use the 5th date
      ** ( NB  Dates GE 6th date excluded in above validation )
      *
      ** ( Pay )
      *
     C           WUPRO     IFEQ 'Y'
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      PRCCY
     C                     ELSE
     C                     MOVE UCUCY     PRCCY
     C                     ENDIF
      *
     C                     MOVELUPACC     PRNNO
     C                     EXSR SRPRO
      *
     C                     Z-ADD1         X       10
      *
     C           WUDAT     IFGT PDAT,1
      *
     C           WUDAT     IFGE PDAT,5
     C                     Z-ADD5         X
     C                     ELSE
     C           WUDAT     LOKUPPDAT,X                 8585
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Convert WUINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE UCUCY     P@FRCY
     C                     MOVE PSCY      P@TOCY
     C                     Z-ADDWUINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WUINOT
      *
     C                     ENDIF
      *
      ** Adjust Balances for Entries X to 5
      *
     C           X         DOWLE5
      *
      ** Update PF/PRONO file
      *
     C                     ADD  WUINOT    PBAL,X
     C                     ADD  1         X
     C                     ENDDO
     C                     UPDATPRONOPNF
      *
      ** Output posting details to RSACMTPD
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      W#CCY
     C                     ELSE
     C                     MOVELUCUCY     W#CCY
     C                     ENDIF
     C                     MOVEL'Y'       WNOST
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDWUDAT     VUDT
     C                     Z-ADDWUINOT    WNETA
     C                     Z-ADD0         DORC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** ( Receive )
      *
     C           WTPRO     IFEQ 'Y'
     C           WSETL     IFEQ 'R'
     C           WSETL     OREQ 'B'
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      PRCCY
     C                     ELSE
     C                     MOVE TCUCY     PRCCY
     C                     ENDIF
      *
     C                     MOVELURACC     PRNNO
     C                     EXSR SRPRO
      *
     C                     Z-ADD1         X       10
      *
     C           WTDAT     IFGT PDAT,1
      *
     C           WTDAT     IFGE PDAT,5
     C                     Z-ADD5         X
     C                     ELSE
     C           WTDAT     LOKUPPDAT,X                 8585
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Convert WTINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE TCUCY     P@FRCY
     C                     MOVE RSCY      P@TOCY
     C                     Z-ADDWTINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WTINOT
      *
     C                     ENDIF
      *
      ** Adjust Balances for Entries X to 5
      *
     C           X         DOWLE5
      *
      ** Update PF/PRONO file
      *
     C                     SUB  WTINOT    PBAL,X
     C                     ADD  1         X
     C                     ENDDO
     C                     UPDATPRONOPNF
      *
      ** Output posting details to RSACMTPD
      *
     C                     MOVEL'Y'       WNOST
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE RSCY      W#CCY
     C                     ELSE
     C                     MOVELTCUCY     W#CCY
     C                     ENDIF
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDWTDAT     VUDT
     C                     Z-ADDWTINOT    WNETA
     C                     Z-ADD1         DORC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Restore saved values and update Memos balances with new values
      *
     C                     Z-ADDWUINO1    WUINOT
     C                     Z-ADDWTINO1    WTINOT
      *
      ** For RS deals, determine if matched, if so, calculate the Net
      ** Amount (Payment Amount)
      *
     C           DTYP      IFEQ 'RS'
     C********** WUEDAT    ANDEQWTEDAT                                                        209171
     C           WEDATU    ANDEQWEDATT                                                        209171
      *
      ** If OUR Net Int *GT THEIR Net Int, pay settlement
      *
     C           WUINOT    IFGE WTINOT
     C                     MOVE 'P'       WSETL
     C           WUINOT    SUB  WTINOT    WUINOT
     C                     ELSE
      *
      ** If THEIR Net Int *GT OUR Net Int, receive settlement
      *
     C                     MOVE 'R'       WSETL
     C           WTINOT    SUB  WUINOT    WTINOT
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Unmatched 'RS'
      *
     C           DTYP      IFEQ 'RS'                          RS
     C********** WUEDAT    ANDNEWTEDAT                         NOT MATCH.                     209171
     C           WEDATU    ANDNEWEDATT                                                        209171
      *
     C           WUINOT    IFNE 0
     C                     MOVE 'P'       WSETL
     C                     ENDIF
     C           WTINOT    IFNE 0
     C                     MOVE 'R'       WSETL
     C                     ENDIF
      *
     C           WUINOT    IFNE 0
     C           WTINOT    ANDNE0
     C                     MOVE 'B'       WSETL                BOTH
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Adjust Projected Nostro balances
      *
      ** Net Discounted Amounts are applied to the projected nostro
      ** balances whose occurrence matches the projected nostro dates
      ** which are GE the Event Date.
      *
      ** If event is earlier than the first of the 5 dates,
      ** use from the first date
      ** If event is after the 5th date, but before the 6th
      ** use the 5th date
      ** ( NB  Dates GE 6th date excluded in above validation )
      *
      ** Only process details if settled through nostro
      ** ( Otherwise process Their details )
      *
     C                     MOVE 'N'       WJUMP
      *
     C           PSETM     IFNE 01
     C           PSETM     ANDNE02
     C           PSETM     ANDNE07
     C           PSETM     ANDNE08
     C           PSETM     ANDNE12
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
     C           WJUMP     IFEQ 'N'
      *
      ** ( Pay )
      *
     C           WUPRO     IFEQ 'Y'
     C           WSETL     IFEQ 'P'
     C           WSETL     OREQ 'B'
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      PRCCY
     C                     ELSE
     C                     MOVE UCUCY     PRCCY
     C                     ENDIF
      *
     C                     MOVELUPACC     PRNNO
     C                     EXSR SRPRO
      *
     C                     Z-ADD1         X       10
      *
     C           WUDAT     IFGT PDAT,1
      *
     C           WUDAT     IFGE PDAT,5
     C                     Z-ADD5         X
     C                     ELSE
     C           WUDAT     LOKUPPDAT,X                 8585
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Convert WUINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE UCUCY     P@FRCY
     C                     MOVE PSCY      P@TOCY
     C                     Z-ADDWUINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WUINOT
      *
     C                     ENDIF
      *
      ** Adjust Balances for Entries X to 5
      *
     C           X         DOWLE5
      *
      ** Update PF/PRONO file
      *
     C                     SUB  WUINOT    PBAL,X
     C                     ADD  1         X
     C                     ENDDO
     C                     UPDATPRONOPNF
      *
      ** Output posting details to RSACMTPD
      *
     C                     MOVEL'Y'       WNOST
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      W#CCY
     C                     ELSE
     C                     MOVELUCUCY     W#CCY
     C                     ENDIF
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDWUDAT     VUDT
     C                     Z-ADDWUINOT    WNETA
     C                     Z-ADD1         DORC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Only process details if settled through nostro
      ** ( Otherwise End )
      *
     C                     MOVE 'N'       WJUMP
      *
     C           RSETM     IFNE 01
     C           RSETM     ANDNE02
     C           RSETM     ANDNE07
     C           RSETM     ANDNE08
     C           RSETM     ANDNE12
     C                     MOVE 'Y'       WJUMP
     C                     ENDIF
      *
      ** ( Receive )
      *
     C           WJUMP     IFEQ 'N'
      *
     C           WTPRO     IFEQ 'Y'
     C           WSETL     IFEQ 'R'
     C           WSETL     OREQ 'B'
      *
     C           CEU003    IFEQ 'Y'
     C           PSCY      ANDNE*BLANKS
     C                     MOVE PSCY      PRCCY
     C                     ELSE
     C                     MOVE TCUCY     PRCCY
     C                     ENDIF
      *
     C                     MOVELURACC     PRNNO
     C                     EXSR SRPRO
      *
     C                     Z-ADD1         X       10
      *
     C           WTDAT     IFGT PDAT,1
      *
     C           WTDAT     IFGE PDAT,5
     C                     Z-ADD5         X
     C                     ELSE
     C           WTDAT     LOKUPPDAT,X                 8585
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Convert WTINOT to settlement currency if required
      *
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE TCUCY     P@FRCY
     C                     MOVE RSCY      P@TOCY
     C                     Z-ADDWTINOT    P@FRAM           TRANS AMT
      *
     C                     EXSR SRCONV
      *
     C                     Z-ADDP@OUTA    WTINOT
      *
     C                     ENDIF
      *
      ** Adjust Balances for Entries X to 5
      *
     C           X         DOWLE5
      *
      ** Update PF/PRONO file
      *
     C                     ADD  WTINOT    PBAL,X
     C                     ADD  1         X
     C                     ENDDO
     C                     UPDATPRONOPNF
      *
      ** Output posting details to RSACMTPD
      *
     C                     MOVEL'Y'       WNOST
     C           CEU003    IFEQ 'Y'
     C           RSCY      ANDNE*BLANKS
     C                     MOVE RSCY      W#CCY
     C                     ELSE
     C                     MOVELTCUCY     W#CCY
     C                     ENDIF
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADDWTDAT     VUDT
     C                     Z-ADDWTINOT    WNETA
     C                     Z-ADD0         DORC
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'F'       W#MTYP                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     EXSR SRACMT
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Restore saved values for OLPOS update
      *
     C                     Z-ADDWUINO1    WUINOT
     C                     Z-ADDWTINO1    WTINOT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRACMT - Output posting details to PF/RSACMTPD               *
      *                                                               *
      *  Called by: SRMEMO                                            *
      *                                                               *
      *  Calls    : AONOSTR0                                          *
      *                                                               *
      *****************************************************************
      *
     C           SRACMT    BEGSR
      *
      ** Only write a record if the amount is
      ** not zero
      *
     C           WNETA     IFNE *ZEROS
      *
      ** For settlement through a nostro;
      *
     C           WNOST     IFNE 'N'
     C                     MOVELPRNNO     WRK2
      *
      ** Call Access Program to retrieve currency nostro record
      *
     C                     CALL 'AONOSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM *BLANK    PCUST   6        Customer No.
     C                     PARM W#CCY     PCCY    3        Currency
     C**********           PARM *BLANKS   PACCD   4        A/C Code                           CGL029
     C                     PARM *BLANKS   PACCD  10                                           CGL029
     C                     PARM *BLANKS   PACSN   2        A/C Seq
     C                     PARM WRK2      PNOST   2        Nostro No.
     C                     PARM *BLANKS   PBRCD   3        Branch Code
     C                     PARM *BLANKS   PCSSN  10        Cust. Shortname
     C                     PARM *BLANKS   PPNOI   1        Prime Nost. Ind
     C           SDNOST    PARM SDNOST    DSSDY
      *
     C           PRTCD     IFEQ *BLANKS
      *
      ** Use fields from nostros file to shadow post
      *
     C                     MOVE A7CUST    CUSN
     C                     MOVE A7CYCD    CCYD
     C                     MOVE A7ACCD    ACDE
     C                     MOVE A7ACSN    ASNC
     C                     MOVE A7BRCD    BRCA
      *
     C                     ELSE
      *
      ** Else initialise account keys of unallocated nostros.
      *
     C**********           Z-ADD*ZEROS    CUSN                                                CSD027
     C                     MOVE *BLANKS   CUSN                                                CSD027
     C                     MOVELW#CCY     CCYD
     C                     Z-ADD*ZEROS    ACDE
     C                     Z-ADD*ZEROS    ASNC
     C                     MOVE *BLANKS   BRCA
      *
     C                     ENDIF
      *                                                                                  CRE009
      ** If CRE009 is ON and account is RE, set the posting date to be the same          CRE009
      ** as the value date when the latter is not less than the run date.                CRE009
      *                                                                                  CRE009
     C           CRE009    IFEQ 'Y'                                                      CRE009
      *                                                                                  CRE009
     C                     MOVE CUSN      WCNUM                                          CRE009
     C                     MOVE CCYD      WCCY                                           CRE009
     C                     MOVE ACDE      WACOD                                          CRE009
     C                     MOVE ASNC      WACSQ                                          CRE009
     C                     MOVE BRCA      WWBRCA                                         CRE009
     C           ACCKEY    CHAINACCNT                88                                  CRE009
     C           *IN88     IFEQ *OFF                                                     CRE009
     C           ATYP      ANDEQ'R'                                                      CRE009
     C           VUDT      IFGE BJRDNB                                                   CRE009
     C                     Z-ADDVUDT      PTDT                                           CRE009
     C                     ENDIF                                                         CRE009
     C                     ENDIF                                                         CRE009
      *                                                                                  CRE009
     C                     ENDIF                                                         CRE009
      *
     C                     MOVE DLNO      TNMR
     C                     MOVE DTYP      TRYP
      *
     C                     ENDIF
      *
     C                     Z-ADDWNETA     MVAM
      *
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'DL'      CMOD                                                CAP205
     C                     ELSE                                                               CAP205
     C                     MOVEL*BLANKS   CMOD                                                CAP205
     C                     ENDIF                                                              CAP205
     C                     MOVELW#MTYP    MTYP                                                CAP205
      *                                                                                       CAP205
     C                     WRITERSACMTPO
     C                     Z-ADD*ZEROS    MVAM
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,GLINTCZ1
     C/COPY ZSRSRC,ZDAT10Z2
     C/EJECT
     C/COPY ZSRSRC,ZINTDYZ2
     C/COPY ZSRSRC,ZDATE9Z3
      *****************************************************************
      *                                                               *
      *  SRACCA - Process account records                             *
      *                                                               *
      *  Called by: SRMEMO                                            *
      *                                                               *
      *  Calls    : AONOSTR0, *PSSR                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRACCA    BEGSR
      *
      ** Set routine fields
      *
     C**********           Z-ADDWDCNUM    WDCNUM  60                                          CSD027
     C                     MOVE WDCNUM    WDCNUM  6                                           CSD027
     C                     MOVE WDDCCY    WDDCCY  3
      *
     C                     MOVE '01'      WDACSQ  2
     C                     MOVE 'Y'       WRETL   1
      *
      ** Initialise shadow postings work indicator
      *
     C                     MOVE 'N'       WNOST   1
      *
      ** Settlement Types 01 02 07 08 & 12
      *
     C           WDSETM    IFEQ '01'
     C           WDSETM    OREQ '02'
     C           WDSETM    OREQ '07'
     C           WDSETM    OREQ '08'
     C           WDSETM    OREQ '12'
      *
      ** Settlement method is via a nostro set
      *
     C                     MOVE 'Y'       WNOST
      *
      ** Not retail, if no data entered
      *
     C                     MOVELDBNKAC    WRK2
     C           WRK2      IFNE '  '
      *
     C                     MOVELWDDCCY    WRK5
     C                     MOVE WRK2      WRK5
      *
      ** (WRK5 used to set up LDA in event of db error)
      *
      ** Call access program to get currency nostro record
      *
     C                     CALL 'AONOSTR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM *BLANK    PCUST   6        Customer No.
     C                     PARM WDDCCY    PCCY    3        Currency
     C**********           PARM *BLANKS   PACCD   4        A/C Code                           CGL029
     C                     PARM *BLANKS   PACCD  10                                           CGL029
     C                     PARM *BLANKS   PACSN   2        A/C Seq
     C                     PARM WRK2      PNOST   2        Nostro No.
     C                     PARM *BLANKS   PBRCD   3        Branch Code
     C                     PARM *BLANKS   PCSSN  10        Cust. Shortname
     C                     PARM *BLANKS   PPNOI   1        Prime Nost. Ind
     C           SDNOST    PARM SDNOST    DSSDY
      *
      ** Handle database error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD16        DBASE
     C                     MOVE 'SDNOSTPD'DBFILE
     C                     MOVELWRK5      DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      ** Use fields from nostros file & default ccy to get ACCNT record
      *
     C                     MOVE A7CUST    WCNUM
     C                     MOVE WDDCCY    WCCY
     C                     MOVE A7ACCD    WACOD
     C                     MOVE A7ACSN    WACSQ
     C                     MOVE A7BRCD    WWBRCA
      *
     C           ACCKEY    CHAINACCNT                88
      *
      ** If record not found or not active, database error
      *
     C           *IN88     IFEQ '1'
     C           RECI      ORNE 'D'
      *
      ** Move key fields into DS fields for DB error key
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD17        DBASE
     C                     MOVE 'ACCNT'   DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      ** If record active & retail, return Memo run else zero
      *
     C           ATYP      IFNE 'R'
     C                     MOVE 'N'       WRETL
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Settlement Type 14
      *
     C           WDSETM    IFEQ '14'
     C                     MOVELDBNKAC    ACNO
     C           ACNO      CHAINACNUM                84
      *
      ** If ACNUM record found and not active, database error
      *
     C           *IN84     IFEQ '0'                        RECORD FOUND
     C           RECI      IFNE 'D'                        NOT ACTIVE
     C           *LOCK     IN   LDA
     C                     Z-ADD18        DBASE
     C                     MOVE 'ACNUM'   DBFILE
     C                     MOVELACNO      DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      ** If ACNUM record found & active, use data to access ACCNT
      *
     C                     MOVE CNUM      WCNUM
     C                     MOVE CCY       WCCY
     C                     MOVE ACOD      WACOD
     C                     MOVE ACSQ      WACSQ
     C                     MOVE BRCA      WWBRCA
      *
      ** Branch key (WWBRCA) already set up before routine called
      *
     C           ACCKEY    CHAINACCNT                88
      *
      ** If record not found or not active, database error
      *
     C           *IN88     IFEQ '1'                        NOT FOUND
     C           RECI      ORNE 'D'                        OR NOT ACTIVE
      *
      ** Move key fields into DS fields for DB Error key
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD19        DBASE
     C                     MOVE 'ACCNT'   DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      ** Move account details to shadow post later
      *
     C                     MOVE DLNO      TNMR
     C                     MOVE DTYP      TRYP
     C**********           Z-ADDCNUM      CUSN                                                CSD027
     C                     MOVE CNUM      CUSN                                                CSD027
     C                     MOVE CCY       CCYD
     C                     Z-ADDACOD      ACDE
     C                     Z-ADDACSQ      ASNC
      *
      ** If record active & retail, return Memo RRN,  else zero
      *
     C           ATYP      IFNE 'R'
     C                     MOVE 'N'       WRETL
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
     C                     ELSE
     C                     MOVE 'N'       WRETL
     C                     ENDIF
     C                     ENDIF
      *
      ** Settlement Type 04
      *
     C           WDSETM    IFEQ '04'
     C                     MOVE WDCNUM    WCNUM
     C                     MOVE WDDCCY    WCCY
     C                     MOVE BMACCD    WACOD
     C                     MOVE WDACSQ    WACSQ
      *
      ** Branch key (WWBRCA) already set up before routine called
      *
     C           ACCKEY    CHAINACCNT                88
      *
      ** If record not found or not active, database error
      *
     C           *IN88     IFEQ '1'
     C           RECI      ORNE 'D'
     C           *LOCK     IN   LDA
     C                     Z-ADD20        DBASE
     C                     MOVE 'ACCNT'   DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      ** Move account details to shadow post later
      *
     C                     MOVE DLNO      TNMR
     C                     MOVE DTYP      TRYP
     C**********           Z-ADDCNUM      CUSN                                                CSD027
     C                     MOVE CNUM      CUSN                                                CSD027
     C                     MOVE CCY       CCYD
     C                     Z-ADDACOD      ACDE
     C                     Z-ADDACSQ      ASNC
      *
      ** If record active & retail, return Memo RRN, else zero
      *
     C           ATYP      IFNE 'R'
     C                     MOVE 'N'       WRETL
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Settlement Type 05
      *
     C           WDSETM    IFEQ '05'
      *
      ** Use bank A/C split fields to access 'ACCNT'
      *
     C                     MOVE W@CNUM    WCNUM
     C                     MOVE WDDCCY    WCCY
     C                     MOVE W@ACOD    WACOD
      *
      ** If account sequence is zero or blank, use default sequence
      *
     C           W@ACSQ    IFEQ *BLANK
     C           W@ACSQ    OREQ *ZERO
     C                     MOVE WDACSQ    WACSQ
     C                     ELSE
     C                     MOVE W@ACSQ    WACSQ
     C                     ENDIF
      *
      ** WWBRCA (Branch Code) key already set up before call to SRACCA
      *
     C           ACCKEY    CHAINACCNT                88
      *
      ** If record not found or not active, database error
      *
     C           *IN88     IFEQ '1'
     C           RECI      ORNE 'D'
      *
      ** Move key fields into DS fields for DB Error key
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD21        DBASE
     C                     MOVE 'ACCNT'   DBFILE
     C                     MOVELERRKEY    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      ** Move account details to shadow post later
      *
     C                     MOVE DLNO      TNMR
     C                     MOVE DTYP      TRYP
     C**********           Z-ADDCNUM      CUSN                                                CSD027
     C                     MOVE CNUM      CUSN                                                CSD027
     C                     MOVE CCY       CCYD
     C                     Z-ADDACOD      ACDE
     C                     Z-ADDACSQ      ASNC
      *
      ** If record active & retail, return Memo RRN, else zero
      *
     C           ATYP      IFNE 'R'
     C                     MOVE 'N'       WRETL
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Clear passed fields
      *
     C                     MOVE *BLANK    WDDCCY
     C                     MOVE *BLANK    WDSETM  2
     C                     MOVE *BLANK    DBNKAC
     C**********           Z-ADDWDCNUM    CNUM                                                CSD027
     C**********           Z-ADD*ZERO     WDCNUM                                              CSD027
     C                     MOVE WDCNUM    CNUM                                                CSD027
     C                     MOVE *BLANKS   WDCNUM                                              CSD027
     C                     MOVE *BLANKS   WWBRCA
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCONV - Converts amount to Settlement Currency              *
      *                                                               *
      *  Called by: SRMEMO, SRPRON                                    *
      *                                                               *
      *  Calls    : EU0200, *PSSR                                     *
      *                                                               *
      *****************************************************************
      *
     C           SRCONV    BEGSR
      *
      ** Call external program
      *
     C                     CALL 'EU0200'
     C                     PARM *BLANKS   P@RTCD  7        RETURN CODE
     C                     PARM           P@FRAM 183       TRANS AMT
     C                     PARM           P@FRCY  3        TRANS CCY
     C                     PARM           P@TOCY  3        SETTL CCY
     C                     PARM           P@OUTA 150       SETTL AMT
     C                     PARM           P@INTA 183       INT SET AMT
     C                     PARM           P@EXRT 159       EXCH RATE
     C                     PARM           P@MDIN  1        M/D IND
      *
      ** Error routine
      *
     C           P@RTCD    IFEQ '*ERROR*'
     C           *LOCK     IN   LDA
     C                     Z-ADD22        DBASE
     C                     MOVELP@FRCY    DBKEY
     C                     MOVEL'EU0200'  DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialisation subroutine                           *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  Calls    : AOBANKR0, AOMMODR0, AORETL0, AODEALR0, AOSARDR0   *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C                     MOVEACPY@      MKI@   80
      *
     C           *ENTRY    PLIST
     C                     PARM           PRERUN  1
      *
      ** Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'DL1650'  DBPGM
     C                     OUT  LDA
      *
     C           KCFL      KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C                     KFLD           PRDT
      *
     C           KDTL      KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C                     KFLD           RCIP
     C                     KFLD           PRDT
      *
     C           KDTP      KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C                     KFLD           RCIP
      *
     C           KYOISC    KLIST
     C                     KFLD           L5BRCA
     C                     KFLD           L5CURR
     C                     KFLD           L5DLNO
      *
      ** Call Access prog. for Bank Details.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle database error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD23        DBASE
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST  'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Call Access prog. for Module Details.
      *
     C                     CALL 'AOMMODR0'
     C                     PARM '*DBERR ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
      ** Handle database error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD24        DBASE
     C                     MOVE 'SDMMODPD'DBFILE
     C                     MOVEL'*FIRST  'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Call Access prog. for Retail Details.
      *
     C           BGRTBN    IFEQ 'Y'
     C           BGIOAC    OREQ 'Y'
     C                     CALL 'AORETLR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDRETL    PARM SDRETL    DSFDY
      *
     C                     ENDIF
      *
      ** Handle database error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD25        DBASE
     C                     MOVE 'SDRETLPD'DBFILE
     C                     MOVEL'*FIRST  'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Call Access prog. for Dealing Details.
      *
     C**********           CALL 'AODEALR0'                                                    CGL029
     C                     CALL 'AODEALR1'                                                    CGL029
     C                     PARM '*DBERR ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C********** SDDEAL    PARM SDDEAL    DSFDY                                               CGL029
     C           SDDEAL    PARM SDDEAL    DSSDY                                               CGL029
      *
      ** Handle database error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD26        DBASE
     C                     MOVE 'SDDEALPD'DBFILE
     C                     MOVEL'*FIRST  'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check system date format, DDMMYY or MMDDYY
      ** *IN98 on if MMDDYY
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ENDIF
      *
      ** Access SAR details file to determine if Australian
      ** Modifications is switched on.
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*VERIFY' POPTN
     C                     PARM 'CIR001'  PSARD   6
     C           SCSARD    PARM SCSARD    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C           PRTCD     ANDNE'*NRF   '
     C           *LOCK     IN   LDA
     C                     Z-ADD27        DBASE
     C                     MOVE 'SCSARDPD'DBFILE
     C                     MOVEL'CIR001'  DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVEL'Y'       CIR001  1
     C                     ELSE
     C                     MOVEL'N'       CIR001
     C                     ENDIF
      *
      ** Access AOSARDR0 to check if CEU003 is installed
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*VERIFY' POPTN   7
     C                     PARM 'CEU003'  PSARD   6
     C           SCSARD    PARM SCSARD    DSFDY
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       CEU003  1
     C                     ELSE
     C                     MOVE 'N'       CEU003
     C                     ENDIF
      *
      ** Access AOSARDR0 to check if CIR004 is installed
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*VERIFY' POPTN
     C                     PARM 'CIR004'  PSARD
     C           SCSARD    PARM SCSARD    DSFDY
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       CIR004  1
     C                     ELSE
     C                     MOVE 'N'       CIR004
     C                     ENDIF
      *                                                                             CRE009
      ** Access AOSARDR0 to check if CRE009 is installed                            CRE009
      *                                                                             CRE009
     C                     CALL 'AOSARDR0'                                          CRE009
     C                     PARM *BLANKS   PRTCD                                     CRE009
     C                     PARM '*VERIFY' POPTN                                     CRE009
     C                     PARM 'CRE009'  PSARD                                     CRE009
     C           SCSARD    PARM SCSARD    DSFDY                                     CRE009
      *                                                                             CRE009
     C           PRTCD     IFEQ *BLANKS                                             CRE009
     C                     MOVE 'Y'       CRE009  1                                 CRE009
     C                     ELSE                                                     CRE009
     C                     MOVE 'N'       CRE009                                    CRE009
     C                     ENDIF                                                    CRE009
      *                                                                                       CAP205
      ** Determine if SAR CAP205 Teller Related APIs - Account Balance Check                  CAP205
      ** is installed.                                                                        CAP205
      *                                                                                       CAP205
     C                     MOVEL'N'       CAP205  1                                           CAP205
     C                     CALL 'AOSARDR0'                                                    CAP205
     C                     PARM *BLANKS   @RTCD                                               CAP205
     C                     PARM '*VERIFY '@OPTN                                               CAP205
     C                     PARM 'CAP205'  @SARD                                               CAP205
     C           SCSARD    PARM SCSARD    DSFDY                                               CAP205
      *                                                                                       CAP205
     C           @RTCD     IFEQ *BLANKS                                                       CAP205
     C                     MOVEL'Y'       CAP205                                              CAP205
     C                     ELSE                                                               CAP205
     C           @RTCD     IFNE *BLANKS                                                       CAP205
     C           @RTCD     ANDNE'*NRF   '                                                     CAP205
     C                     MOVEL'SCSARDPD'DBFILE                                              CAP205
     C                     Z-ADD38        DBASE                                               CAP205
     C                     MOVEL'CAP205'  DBKEY                                               CAP205
     C                     EXSR *PSSR                                                         CAP205
     C                     ENDIF                                                              CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C                     MOVEL*BLANK    W#MTYP  1                                           CAP205
      *****************************************************************            MD035545 MD047993
      ***Call*access*program*for*General*Ledger*details.***************            MD035545 MD047993
      *****************************************************************            MD035545 MD047993
     C**********           CALL 'AOGELRR1'                                         MD035545 MD047993
     C**********           PARM '*MSG   ' @RTCD   7                                MD035545 MD047993
     C**********           PARM '*FIRST ' @OPTN   7                                MD035545 MD047993
     C********** SDGELR    PARM SDGELR    DSSDY                                    MD035545 MD047993
      *                                                                                     MD035545
     C********** @RTCD     IFNE *BLANK                                             MD035545 MD047993
     C**********           MOVEL'SDGELRPD'DBFILE                                   MD035545 MD047993
     C**********           MOVEL'062'     DBASE                                    MD035545 MD047993
     C**********           MOVEL@OPTN     DBKEY                                    MD035545 MD047993
     C**********           EXSR *PSSR                                              MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
      *                                                                                     MD035545
      ** Retrieve system value setting                                                      MD035545
      *                                                                                     MD035545
     C                     MOVELWPARML    P@OP01                                            MD035545
     C                     CALL 'AOSVALR0'                                                  MD035545
     C                     PARM *BLANKS   W0RTCD  7                                         MD035545
     C                     PARM           P@OP01 20                                         MD035545
     C                     PARM           P@VL01200                                         MD035545
     C                     PARM           P@OP02 20                                         MD035545
     C                     PARM           P@VL02200                                         MD035545
     C                     PARM           P@OP03 20                                         MD035545
     C                     PARM           P@VL03200                                         MD035545
     C                     PARM           P@OP04 20                                         MD035545
     C                     PARM           P@VL04200                                         MD035545
     C                     PARM           P@OP05 20                                         MD035545
     C                     PARM           P@VL05200                                         MD035545
     C                     PARM           P@OP06 20                                         MD035545
     C                     PARM           P@VL06200                                         MD035545
     C                     PARM           P@OP07 20                                         MD035545
     C                     PARM           P@VL07200                                         MD035545
     C                     PARM           P@OP08 20                                         MD035545
     C                     PARM           P@VL08200                                         MD035545
     C                     PARM           P@OP09 20                                         MD035545
     C                     PARM           P@VL09200                                         MD035545
     C                     PARM           P@OP10 20                                         MD035545
     C                     PARM           P@VL10200                                         MD035545
      *                                                                                     MD035545
      ** If system value is not found then default value to 'N'                             MD035545
      *                                                                                     MD035545
     C           W0RTCD    IFNE '       '                                                   MD035545
     C                     MOVE 'N'       WCALC6                                            MD035545
     C                     ELSE                                                             MD035545
     C                     MOVELP@VL01    WCALC6                                            MD035545
     C********** WCALC6    IFEQ 'Y'                                                MD035545 MD047993
     C********** BKALDI    ANDEQ'Y'                                                MD035545 MD047993
     C**********           MOVE 'Y'       WCALC6                                   MD035545 MD047993
     C**********           ELSE                                                    MD035545 MD047993
     C**********           MOVE 'N'       WCALC6                                   MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
     C                     ENDIF                                                            MD035545
      *
      ** Set up fields for commitment text data structure.
      *
     C                     MOVE 'DL'      MDID
     C                     MOVE 'DL1650'  PGMN
     C                     MOVE WSID      WSIDX
     C                     MOVE USRID     USRIDX
     C                     MOVEL' SUBMITT'BTEXT
     C                     MOVE 'ED TO BA'WBTXT1  8
     C                     MOVE 'TCH '    WBTXT2  4
     C                     MOVELWBTXT1    WBTXT3 12
     C                     MOVE WBTXT2    WBTXT3
     C                     MOVE WBTXT3    BTEXT
      *
     C                     TIME           MTIME
      *
      ** Set up ACCKEY key list for ACCNT file.
      *
     C           ACCKEY    KLIST
     C**********           KFLD           WCNUM   60                                          CSD027
     C                     KFLD           WCNUM   6                                           CSD027
     C                     KFLD           WCCY    3
     C**********           KFLD           WACOD   40                                          CGL029
     C                     KFLD           WACOD  100                                          CGL029
     C                     KFLD           WACSQ   20
     C                     KFLD           WWBRCA  3
      *
      ** Set up record count for DLIND output file.
      *
     C                     Z-ADD2         RDLCT
      *
      ** Set up count for records read from DEALS file.
      *
     C                     Z-ADD0         RRECT
      *
      ** Set up count of deals with rates fixed.
      *
     C                     Z-ADD0         RRTFIX
      *
      ** Set up and output DLINDHH record.
      *
     C           *HIVAL    SETGTDLINDHH
     C                     MOVE 'H'       HHRECI
     C                     WRITEDTRIEHHF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
**  CPY@
(c) Finastra International Limited 2001
