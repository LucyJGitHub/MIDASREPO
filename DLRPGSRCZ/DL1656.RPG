     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL OIS Events Merge')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL1656 - OIS Events Merge                                    *
      *                                                               *
      *  Function:  This program merges event records for OIS deals   *
      *  (CoB)      into the Events files.                            *
      *                                                               *
      *  Called By: DLC1656 - OIS Events Merge Control                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 240320             Date 19Jun06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 BUG6490            Date 04Apr05               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007  *CREATE    Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  240320 - Initialise other lending related fields             *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  BUG6490- Initialise lending related fields                   *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
     FDLOEVTL0UF  E           K        DISK         KINFSR *PSSR
     FEVNTXZZ UF  E                    DISK         KINFSR *PSSR
     FEVNTXED O   E                    DISK         KINFSR *PSSR
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      *    01         File-access indicator for the primary file      *
      *    50         General file-access indicator                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRZADD - Adds amount (WZAMT) to total (WZTOTI,WZTOTD)        *
      *  SRZSUM - Carry out addition for SR/SRZADD                    *
      *  *PSSR  - Error processing                                    *
      *  *INZSR - Initialise                                          *
      *                                                               *
      *    The *INZSR subroutine will only get called automatically   *
      *    on entry to the module the first time it is run            *
      *    (unless you end the program with LR on).                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      ** Rename same field names
     IEVNTXZZF
     I              RECI                            EZRECI
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
      ** First data structure for access objects
     IDSFDY     E DSDSFDY
      *
      ** External data structure for bank details
     ISDBANK    E DSSDBANKPD
      *
      ** External data structure for events details
     IDSEVNT    E DSEVNTXED
      *
     I*L4EREC*****DS****************************363                                           CDL038
     IL4EREC      DS                            405                                           CDL038
     I                                    P  21  270WEEAMT
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Read records elegible for merging into the events file
      *
     C                     READ DLOEVTL0                 01
      *
     C           *IN01     DOWEQ*OFF
      *
      ** If Creation Date is before today merge the record
     C           L4CDAT    IFLT BJRDNB
     C           WEEAMT    ANDNE0
      *
      ** Create and Write a record to EVNTXED
     C                     MOVELL4EREC    DSEVNT    P
     C                     Z-ADD0         MCAS                                               BUG6490
     C                     Z-ADD0         MCAT                                               BUG6490
     C**********           Z-ADD0         MCAC                                        BUG6490 CSD027
     C                     MOVE *BLANKS   MCAC                                                CSD027
     C                     Z-ADD0         CGAMT                                               240320
     C                     Z-ADD0         CMAMT                                               240320
     C                     Z-ADD0         ETAX                                                240320
     C                     WRITEEVNTXEDF
      *
      ** For live deals, count the number of records read
     C                     ADD  1         NORE
     C           WEEAMT    DIV  1000      WZAMT
     C                     Z-ADDHRWN      WZTOTI
     C                     Z-ADDHRDC      WZTOTD
     C                     EXSR SRZADD
     C                     Z-ADDWZTOTI    HRWN
     C                     Z-ADDWZTOTD    HRDC
      *
     C                     ENDIF
      *
      ** If Value Date is today merge the record for the last time
     C           L4VDAT    IFEQ BJRDNB
     C                     MOVE 'Y'       L4MIND
     C                     UPDATDLOEVTD0
     C                     ENDIF
      *
      ** Read the next record
     C                     READ DLOEVTL0                 01
      *
     C                     ENDDO
      *
      ** Update Events Trailer File.
      *
     C                     UPDATEVNTXZZF
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRZADD     - Adds amount (WZAMT) to total (WZTOTI,WZTOTD)    *
      *                                                               *
      *  Called by: Main processing                                   *
      *                                                               *
      *  Calls:     SRZSUM                                            *
      *                                                               *
      *****************************************************************
     C           SRZADD    BEGSR
      *
     C                     Z-ADDWZAMT     WZAMT  153
     C           WZAMT     IFNE 0
      ** Split WZAMT into Integer and Decimal fields
     C                     Z-ADDWZAMT     WZAMTI 150
     C                     MOVE WZAMT     WZAMTD  30
      ** Both WZAMTI and WZAMTD comtain a 'sign' zone now
     C                     EXSR SRZSUM
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRZSUM     - Carry out addition for SR/SRZADD                *
      *                                                               *
      *          Parameters passed -                                  *
      *                     I/P WZAMTI WZAMTD                         *
      *                     O/P WZTOTI WZTOTD WZNEGD                  *
      *                                                               *
      *  Called by: SRZADD                                            *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
     C           SRZSUM    BEGSR
      *
     C                     Z-ADDWZTOTI    WZTOTI 150
     C                     Z-ADDWZTOTD    WZTOTD  30
     C                     MOVE 'N'       WZNEGA  1
     C                     MOVE 'N'       WZNEGT  1
      *
     C           WZAMTI    IFNE 0
     C           WZAMTD    ORNE 0
      *
      ** Determine sign of WZAMTI & D
     C           WZAMTI    IFLT 0
     C           WZAMTI    OREQ 0
     C           WZAMTD    ANDLT0
     C                     MOVE 'Y'       WZNEGA
     C                     ENDIF
      *
      ** Determine sign of WZTOTI & D
     C           WZTOTI    IFLT 0
     C           WZTOTI    OREQ 0
     C           WZTOTD    ANDLT0
     C                     MOVE 'Y'       WZNEGT
     C                     ENDIF
      *
     C           WZTOTI    IFEQ 0
     C           WZTOTD    ANDEQ0
      *
      ** If WZTOTAL = 0, return WZAMOUNT.
     C                     Z-ADDWZAMTI    WZTOTI
     C                     Z-ADDWZAMTD    WZTOTD
      *
     C                     ELSE
      *
      ** If signs differ bypass overflow checks
     C           WZNEGA    IFEQ WZNEGT
      *
     C           WZAMTD    ADD  WZTOTD    WZWK2   40
     C           WZWK2     IFGT 999
     C           WZWK2     ORLT -999
     C           WZNEGA    IFEQ 'N'
     C           WZAMTI    ADD  1         WZWK3  150
     C                     ELSE
     C           WZAMTI    SUB  1         WZWK3
     C                     ENDIF
     C           WZTOTI    ADD  WZWK3     WZWK3
     C                     ELSE
     C           WZTOTI    ADD  WZAMTI    WZWK3
     C                     ENDIF
      *
      ** If Modulus of WZWK3 < Modulus of WZTOTI then O/F has occured
     C           WZNEGA    IFEQ 'N'
     C           WZWK3     ANDLTWZTOTI
     C           WZNEGA    OREQ 'Y'
     C           WZWK3     ANDGTWZTOTI
      ** If O/F, zeroize WZAMT and WZTOT fields left intact
     C                     Z-ADD0         WZAMT  153
     C                     ELSE
     C                     Z-ADDWZWK2     WZTOTD
     C                     Z-ADDWZWK3     WZTOTI
     C                     ENDIF
      *
      ** The signs are different
     C                     ELSE
      *
      ** IF WZAMT was negative, make it positive to comp with WZTOT
     C           WZNEGA    IFEQ 'Y'
     C                     Z-SUBWZAMTI    WZAMTI 150
     C                     Z-SUBWZAMTD    WZAMTD  30
     C                     ENDIF
      *
      ** IF WZTOT was negative, make it positive to comp with WZAMT
     C           WZNEGT    IFEQ 'Y'
     C                     Z-SUBWZTOTI    WZTOTI
     C                     Z-SUBWZTOTD    WZTOTD
     C                     ENDIF
      *
      ** Both WZAMT and WZTOT are now positive
      ** If WZTOT = WZAMT, return zero
     C           WZTOTI    IFEQ WZAMTI
     C           WZTOTD    ANDEQWZAMTD
      *
     C                     Z-ADD0         WZTOTI
     C                     Z-ADD0         WZTOTD
      *
     C                     ELSE
      *
      ** IF WZTOT > WZAMT
     C           WZTOTI    IFGT WZAMTI
     C           WZTOTI    OREQ WZAMTI
     C           WZTOTD    ANDGTWZAMTD
      *
     C           WZAMTD    IFGT WZTOTD
     C           WZTOTI    SUB  1         WZTOTI
     C           WZTOTD    ADD  1000      WZWK2
     C                     ENDIF
     C           WZTOTI    SUB  WZAMTI    WZTOTI
     C           WZAMTD    IFGT WZTOTD
     C           WZWK2     SUB  WZAMTD    WZTOTD
     C                     ELSE
     C           WZTOTD    SUB  WZAMTD    WZTOTD
     C                     ENDIF
      *
     C                     ELSE
      *
      ** If WZAMT > WZTOT
     C           WZTOTD    IFGT WZAMTD
     C           WZAMTI    SUB  1         WZWK3  150
     C           WZAMTD    ADD  1000      WZWK2
     C           WZWK3     SUB  WZTOTI    WZTOTI
     C           WZWK2     SUB  WZTOTD    WZTOTD
     C                     ELSE
     C           WZAMTI    SUB  WZTOTI    WZTOTI
     C           WZAMTD    SUB  WZTOTD    WZTOTD
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Reverse sign of WZTOT if larger I/P fields were negative
     C           WZTOTI    IFGT WZAMTI
     C           WZNEGT    ANDEQ'Y'
     C           WZTOTI    OREQ WZAMTI
     C           WZTOTD    ANDGTWZAMTD
     C           WZNEGT    ANDEQ'Y'
     C           WZTOTI    ORLE WZAMTI
     C           WZNEGA    ANDEQ'Y'
     C           WZTOTI    OREQ WZAMTI
     C           WZTOTD    ANDLEWZAMTD
     C           WZNEGA    ANDEQ'Y'
     C                     Z-SUBWZTOTI    WZTOTI
     C                     Z-SUBWZTOTD    WZTOTD
     C                     ENDIF
      *
      ** Restore sign of WZAMTI & WZAMTD if it was reversed
     C           WZNEGA    IFEQ 'Y'
     C                     Z-SUBWZAMTI    WZAMTI
     C                     Z-SUBWZAMTD    WZAMTD
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If WZTOTD is zero and WZTOTI is negative, set up WZNEGD
     C           WZTOTD    IFEQ 0
     C           WZTOTI    ANDLT0
     C                     MOVE '.000-'   WZNEGD  5
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR    - Program Initialisation routine                   *
      *                                                               *
      *  Called by: Implicitly on program activation                  *
      *                                                               *
      *  Calls:     AOBANKR0                                          *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Initialize LDA fields
     C           *LOCK     IN   LDA
     C                     MOVEL'DL1656'  DBPGM     P
     C                     Z-ADD0         DBASE
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     OUT  LDA
      *
      ** Call Access program for bank details
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVELPOPTN     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     READ EVNTXZZ                  50
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL'EVNTXZZ 'DBFILE
     C                     MOVEL'*FIRST ' DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called by: Error processing routines                         *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
