     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL OIS Call to DEALSDG')                         *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL1660 - OIS: Call to DEALSDG                                *
      *                                                               *
      *  Function:  This program is used to obtain deal information   *
      *             from deals file DEALSDG.                          *
      *                                                               *
      *  Called By: GL0550 -  GL Daily Events By Ccy - Consolidated   *
      *             GL0551 -  GL Daily Events By Ccy - By Branch      *
      *             GL0552 -  GL Daily Events By Ccy - By Company     *
      *             GL0580 -  GL Generate Projected Nostro Events     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL099             Date 06Oct17               *
      *  Prev Amend No. CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 207102             Date 12Sep06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CIR007  *CREATE    Date 11May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  207102 - Use logical file DEALSALL instead of DEALS to also  *
      *           cater for historic deals in DLGHISPD.               *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CIR007 - Overnight Index Swaps                               *
      *                                                               *
      *****************************************************************
     F***DEALS   IF  E           K        DISK         KINFSR *PSSR                           207102
     FDEALSALLIF  E           K        DISK         KINFSR *PSSR                              207102
      ** Logical file built over DEALSDG
      *
     F**********  DEALSDAF                          KIGNORE                                   207102
     F            DEALSDBF                          KIGNORE
     F            DEALSDCF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F**********  DEALSDFF                          KIGNORE                                   207102
     F            MMDELDP0                          KIGNORE                                   207102
     F            MMDENBP0                          KIGNORE                                   207102
     F            MMDENSP0                          KIGNORE                                   207102
     F            FXDEALP0                          KIGNORE                                   207102
     F            DLBHISD0                          KIGNORE                                   207102
     F            DLCHISD0                          KIGNORE                                   207102
     F            DLDHISD0                          KIGNORE                                   207102
     F            DLEHISD0                          KIGNORE                                   207102
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   50  - Chain Failed Indicator DEALS                          *
      *   98  - Date Format is MMDDYY                                 *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRMAIN - Perform Main processing                             *
      *  SRENDP - Terminate the Program                               *
      *  *INZSR - Initial processing                                  *
      *  *PSSR  - Program exception error routine                     *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      /EJECT
      ****************************************************************
      *                M A I N   P R O C E S S I N G                 *
      ****************************************************************
      *
      ** Perform Main Processing
      *
     C                     EXSR SRMAIN
      *
      ** Terminate Program
      *
     C                     EXSR SRENDP
      *
      *****************************************************************
      *         E N D   O F   M A I N   P R O C E S S I N G           *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRMAIN - Perform Main processing                             *
      *                                                               *
      *  Called By : Main Processing                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRMAIN    BEGSR
      *
     C********** WDLNO     CHAINDEALS                50                                       207102
     C           WDLNO     CHAINDEALSALL             50                                       207102
      *
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE
     C                     MOVEL'DEALSDG' DBFILE    P
     C                     MOVELWDLNO     DBKEY     P
     C                     MOVEL'DL1660'  DBPGM     P
     C                     OUT  LDA
     C                     MOVE 'Y'       PERR
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check if THEIR or OUR side of the deal is an OIS or not.
      *
     C           TICFR     IFEQ 'O'
     C           UICFR     OREQ 'O'
     C                     MOVE 'Y'       POIS
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRENDP - Terminate the Program                               *
      *                                                               *
      *  Called By : Main Processing                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRENDP    BEGSR
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR    - Initial Processing                               *
      *                                                               *
      *  Called By : Main Processing                                  *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ** Receive Parameter List
      *
     C           *ENTRY    PLIST
     C                     PARM           PDLNO   60
     C                     PARM           POIS    1
     C                     PARM           PERR    1
      *
     C                     Z-ADDPDLNO     WDLNO   60
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** LDA DEFINITION.
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: SRMAIN                                             *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      *
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
