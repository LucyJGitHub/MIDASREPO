     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL IRS revaluation extract')                     *
     F*****************************************************************
     F*                                                               *
     F*  Midas - FRA/IRS Module                                       *
     F*                                                               *
     F*  DL1955 - IRS REVALUATION EXTRACT                             *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CGL184             Date 07Dec16               *
      *  Prev Amend No. CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CIR018             Date 12Aug14               *
      *                 CDL094             Date 11Jun14               *
      *                 CIR017             Date 10Apr14               *
      *                 CRE073             Date 06Dec10               *
      *                 CLE064             Date 06Dec10               *
      *                 245132             Date 31Jan07               *
      *                 CER059             Date 19Jul10               *
      *                 CER054             Date 07Apr09               *
      *                 CER048             Date 19May08               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 05Jul06               *
      *                 CIR014             Date 11May06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 228922             Date 17Aug04               *
      *                 CAS008             Date 16Jun04               *
      *                 CDL028             Date 07Feb05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 217684             Date 12May03               *
      *                 CIR012             Date 02Apr03               *
      *                 207270             Date 26Jun02               *
      *                 207269             Date 26Jun02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 211032             Date 13Jan03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 187326             Date 26Dec00               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 180466             Date 27Jun00               *
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW097             Date 02Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 152016             Date 07Jan99               *
     F*                 146477             DATE 11Nov98               *
     F*                 127751             DATE 17DEC97               *
     F*                 127748             DATE 17DEC97               *
     F*                 110055             DATE 17DEC97               *
     F*                 100317             DATE 13MAR97               *
     F*                 100953             DATE 15MAR97               *
     F*                 100298             DATE 05MAR97               *
     F*                 100288             DATE 04MAR96               *
     F*                 CIR001 *CREATE     DATE 13JUN95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CGL184 - New Frequencies for Account Statements (Recompile)  *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CIR018 - Daily Rate Change Frequency on SIRS                 *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CIR017 - SIRS Business Day Conventions for both legs         *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
      *  245132 - Call DLZFWR2 with additional parameter, created     *
      *           from DLZFWR.                                        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax (Recompile)            *
      *  CER048 - German Features - Taxes (Recompile)                 *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CIR014 - Mark to Market Cross-currency Interest Rate Swaps   *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  228922 - DLC1950 failed component.                           *
      *  CAS008 - IAS 39 Enhancements                                 *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CIR012 - Allow report selection by deal number.              *
      *  207270 - Error occurred on forward rate calculation          *
      *  207269 - Recalculates spread twice in DL1956P1 and DL1956P2  *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  211032 - Defined Payment Date Convention in OUR side not     *
      *           applied in the revaluation report.  Move Payment    *
      *           Convention field 1st Leg in OUR subroutine.         *
      *  187326 - Interest period with end date equal to Rundate is   *
      *           still being printed in the FRA/IRS Revaluation      *
      *           Report.                                             *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  180466 - Change in program DLZFRQ. Additional parameter      *
      *           ZFRZBD inserted for DLZFRQ.                         *
      *  CIR005 - FRA/IRS Business Day Conventions                    *
      *  CSW097 - MT340s and MT360s changes for SWIFT97               *
      *           Removed 146447 as it does not cater for settlement  *
      *           currencies which may be defined differently for     *
      *           RXs.  The call to DLZFRQ wil be changed to          *
      *           add two new parameters to specify settlement ccy    *
      *           and whether Rate Change Date or Interest Payment    *
      *           Date is looked for as these two have different      *
      *           holiday considerations.                             *
     F*  152016 - Previous fix #146477 incorrectly incorporated.      *
     F*  146477 - If the Settlement currency has been entered, use    *
     F*           the value to calculate the Forward Rate.            *
     F*  127751 - Where a rate for a new interest period has fixed but*
     F*           has not yet taken effect (between fix date and rate *
     F*           change date), the system attempts to recalculate the*
     F*           rate for the next period. As the rate is known, it  *
     F*           should be taken from the file.                      *
     F*  127748 - Spread is still being output when before rate change*
     F*           date - extend fix 100953.                           *
     F*  110055 - When revaluing SWAPS with an interest frequency of  *
     F*           'Z' on the floating side, the estimated forward rate*
     F*           for every fixing period after the first is identical*
     F*           and too high. DL1955 is not outputting records for  *
     F*           subsequent fixing periods.                          *
     F*  100317 - The straight-line method of interpolating discount  *
     F*           factors using SR/ZINTPL did not provide satisfatory *
     F*           results. As the discount factors curve is           *
     F*           exponential, an PL1 program (ZEXPIN) is used to     *
     F*           perform an exponential interpolation. This program  *
     F*           amended to pass spot date to PGM/DLZFWR.            *
     F*  100953 - Do not produce a cash flow for Spread if it is      *
     F*           before the rate change date.                        *
     F*  100298 - Do not output any cash-flows dated prior to the     *
     F*           run date.                                           *
     F*  100288 - When building up the deal activity for rate changes *
     F*           from either a schedule (SR/ACRATS) or a frequency   *
     F*           code (SR/ACRATF), the access to the principal       *
     F*           schedule did not work for single-currency swaps. In *
     F*           this case, the Our/Their Indicator should be blank. *
     F*  CIR001 - Interest Rate Calendars                             *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,DL1955F001
     FDEALSDG IF  E           K        DISK         KINFSR *PSSR
     FDEALALL UF  E           K        DISK         KINFSR *PSSR
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDCF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     F            DEALSDGF                          KRENAMEDEALSUF
     FDLDTSCPDIF  E           K        DISK         KINFSR *PSSR
     FDLPCSCPDIF  E           K        DISK         KINFSR *PSSR
     FDLCFSCPDIF  E           K        DISK         KINFSR *PSSR
     FDLDEALL5IF  E           K        DISK         KINFSR *PSSR                              CIR014
     FDLACTVPDIF  E           K        DISK         KINFSR *PSSR A
     FDLCFEVPDO   E           K        DISK         KINFSR *PSSR
      *                                                                                       CAS008
     FSDYLDRL1IF  E           K        DISK         KINFSR *PSSR                              CAS008
      ** Yield Rates File                                                                     CAS008
      *                                                                                       CAS008
     FDL1955AUO   E                    PRINTER      KINFDS SPOOLU
     F                                              KINFSR *PSSR
      /EJECT
      *----------------------------------------------------------------
      * ID  F  C  H  L    FUNCTION OF INDICATORS
      *       11          INITIAL RATE SET IND ON
      *       12          CHAIN (SDYLDRL1)                                                    CAS008
      *       13          CHAIN (DLDEALL5)                                                    CIR014
      *       90-99       STANDARD SUBROUTINES
      *       U7          DATABASE ERROR
      *----------------------------------------------------------------
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZHOLE                                                  100317
     E/COPY ZSRSRC,ZFRQB2Z1                                               CIR005
     E/COPY ZSRSRC,ZDATE2Z1                                               CIR005
     ISPOOLU      DS
      ***  File Information Data Structure for DL1955AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     ISDBANK    E DSSDBANKPD
     I** External DS for BANK details
     ISDGELR    E DSSDGELRPD
     I** External DS for GENERAL LEDGER details
     ISDCURR    E DSSDCURRPD                                              100317
     I** External DS for CURRENCIES details                               100317
     ISCSARD    E DSSCSARDPD                                              CIR005
      ** External DS for SAR details                                      CIR005
      *                                                                   CIR005
     I/COPY ZSRSRC,ZHOLI                                                  100317
     I/COPY ZSRSRC,ZHOLDS                                                 100317
     IDSFDY     E DSDSFDY
     I*
     I**   Local data area to hold database error information
     I*
     IDBERR       DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     IPCURR       DS                             30                       CIR005
      ** Data Structure for Payment Convention Currencies                 CIR005
      *                                                                   CIR005
     I                                        1   3 WCYP1                 CIR005
     I                                        4   6 WCYP2                 CIR005
     I                                        7   9 WCYP3                 CIR005
     I                                       10  12 WCYP4                 CIR005
     I                                       13  15 WCYP5                 CIR005
     I                                       16  18 WCYP6                 CIR005
     I                                       19  21 WCYP7                 CIR005
     I                                       22  24 WCYP8                 CIR005
     I                                       25  27 WCYP9                 CIR005
     I                                       28  30 WCYP0                 CIR005
     I**************************************************************************
      /SPACE 5
     C**************************************************************************
     C                     MOVEACPY@      BIS@   80
     C*
     C** INITIAL PROCESSING
     C*
     C                     EXSR INIT
     C*
     C** MAIN PROCESSING
     C*
     C                     READ DEALSDGF                 99*
     C           *IN99     DOWEQ'0'
      *                                                                                       CIR014
      ** Access the extension file                                                            CIR014
      *                                                                                       CIR014
     C           CIR014    IFEQ 'Y'                                                           CIR014
      *                                                                                       CIR014
     C                     MOVE DLNO      KDLNOX                                              CIR014
     C           KDLNOX    CHAINDLDEALL5             13                                       CIR014
      *                                                                                       CIR014
     C                     ENDIF                                                              CIR014
     C*
     C** DETERMINE IF DEAL TO BE EXTRACTED
     C*
     C                     MOVEL*BLANK    ##EXT   2
     C                     EXSR DETIFX
     C*
     C** IF DEAL TO BE EXTRACTED, EXTRACT IT
     C*
     C           ##EXT     IFNE 'NO'
     C                     EXSR EXTRAC
     C                     END
     C                     READ DEALSDGF                 99*
     C                     END
     C*
     C** AUDIT
     C*
     C                     EXSR AUDIT
     C**************************************************************************
      /EJECT
     C********************************************************************
     C** EXTRACT LIVE SINGLE AND CROSS CURRENCY SWAPS
     C********************************************************************
     C           EXTRAC    BEGSR
     C*
     C*--------------------------------------------------------------------
     C*
     C** D E A L    A C T I V I T Y   U P D A T E
     C*
     C** MOVE 'OUR' DETAILS TO WORK FIELDS
     C*
     C                     EXSR OUR
     C*
     C** DEAL ACTIVITY UPDATE FOR 'OUR' DETAILS
     C*
     C                     EXSR DEALAC
     C*
     C** MOVE 'THEIR' DETAILS TO WORK FIELDS
     C*
     C                     EXSR THEIR
     C*
     C** DEAL ACTIVITY UPDATE FOR 'THEIR' DETAILS
     C*
     C                     EXSR DEALAC
     C*
     C*--------------------------------------------------------------------
     C*
     C** C A S H    F L O W   E V E N T S    U P D A T E
     C** -   I N T E R E S T   &  C A S H   F L O W S
     C*
     C** MOVE 'OUR' DETAILS TO WORK FIELDS
     C*
     C                     EXSR OUR
     C*
     C** UPDATE CASH FLOW EVENTS FOR INTEREST & CASH FLOWS - 'OUR' DETAILS
     C*
     C                     EXSR CFEVIC
     C*
     C** MOVE 'THEIR' DETAILS TO WORK FIELDS
     C*
     C                     EXSR THEIR
     C*
     C** UPDATE CASH FLOW EVENTS FOR INTEREST & CASH FLOWS - 'THEIR' DETAILS
     C*
     C                     EXSR CFEVIC
     C*
     C*--------------------------------------------------------------------
     C*
     C** C A S H    F L O W   E V E N T S    U P D A T E
     C** -   S P R E A D    P A Y M E N T S
     C*
     C** MOVE 'OUR' DETAILS TO WORK FIELDS
     C*
     C                     EXSR OUR
     C*
     C** UPDATE CASH FLOW EVENTS FOR SPREAD PAYMENTS - 'OUR' DETAILS
     C*
     C                     EXSR CFEVSP
     C*
     C** MOVE 'THEIR' DETAILS TO WORK FIELDS
     C*
     C                     EXSR THEIR
     C*
     C** UPDATE CASH FLOW EVENTS FOR SPREAD PAYMENTS - 'THEIR' DETAILS
     C*
     C                     EXSR CFEVSP
     C*
     C/COPY WNCPYSRC,DL1955C003
     C                     ENDSR
     C********************************************************************
      /SPACE 5
     C********************************************************************
     C** DEAL ACTIVITY UPDATE
     C********************************************************************
     C           DEALAC    BEGSR
     C*
     C** SET UP VARIABLES USED IN FORWARD RATE CALCULATION
     C*
     C                     MOVE WCUCY     ZFWCCY
     C                     MOVE WSTCY     ZSTCY                           CSW097
     C                     MOVE WICBS     ZFWINC
     C*
     C** SET UP VARIABLES USED IN FREQUENCY CALCULATION
     C*
     C                     MOVELWICFR     ZFRFRQ
     C                     Z-ADDWICFD     ZFRBAS
     C                     Z-ADDWSETM     ZFRSET
     C                     MOVELWCUCY     ZFRCCY
     C                     MOVELWSACC     ZFRNOS
     C                     Z-ADDMDAT      ZFRMDT
      *                                                                                       CIR014
      ** For RX deals with a revalued exchange rate, use the revalued                         CIR014
      ** principal starting from the next int pay date.                                       CIR014
      *                                                                                       CIR014
     C           CIR014    IFEQ 'Y'                                                           CIR014
     C           DTYP      ANDEQ'RX'                                                          CIR014
     C           F1PRVR    ANDNE0                                                             CIR014
     C           F1PRVR    ANDNE0                                                             CIR014
     C           WNIPD     ANDLEWIACD                                                         CIR014
      *                                                                                       CIR014
     C           WOTIN     IFEQ 'U'                                                           CIR014
     C           F1PRVI    ANDEQ'O'                                                           CIR014
      *                                                                                       CIR014
     C           WOTIN     OREQ 'T'                                                           CIR014
     C           F1PRVI    ANDEQ'T'                                                           CIR014
      *                                                                                       CIR014
     C                     Z-ADDF1NPRI    WPAMT                                               CIR014
      *                                                                                       CIR014
     C                     ENDIF                                                              CIR014
      *                                                                                       CIR014
     C                     ENDIF                                                              CIR014
     C*
     C** WRITE RECORD TO DEAL ACTIVITY FILE DATED INTEREST ACCRUAL CONTROL
     C** DATE WITH PRINCIPAL = CURRENT PRINCIPAL & RATE = CURRENT RATE
     C*
     C                     Z-ADDDLNO      ACDLNO
     C                     MOVELWOTIN     ACOTIN
     C                     Z-ADDWIACD     ACPRDT
     C                     Z-ADDWPAMT     ACPCPL
     C*
     C/COPY WNCPYSRC,DL1955C001
     C** CHECK IF INITIAL RATE SET (EFFECTIVE INTEREST RATE SET
     C** FOR FIRST PERIOD OF THE IRS; I.E. STARTING FROM VALUE DATE)
     C*
     C           WBRTT     COMP *ZERO                    11*
     C  N11                TESTB'0'       WDNSI          11*INITIAL RATE SET IND
     C*
     C** IF INITIAL RATE SET USE EFFECTIVE INTEREST RATE
     C*
     C           *IN11     IFEQ '1'
     C                     Z-ADDWEINR     ACEINR
     C                     MOVEL'A'       ACRTTP           * ACTUAL RATE
     C*
     C** IF INITIAL RATE NOT SET, DETERMINE FORWARD RATE
     C** (FOR VALUE DATE TO NEXT RATE CHANGE DATE)
     C*
     C                     ELSE
     C                     Z-ADDVDAT      ZFWNDT           * NEAR DATE
     C                     Z-ADDWNICD     ZFWFDT         99* FAR DATE
     C   99                Z-ADDMDAT      ZFWFDT           * FAR DATE
     C                     EXSR CALFWR
     C                     Z-ADDZFWOUT    ACEINR
     C                     MOVEL'F'       ACRTTP           * FORWARD RATE
     C                     END
     C/COPY WNCPYSRC,DL1955C006
     C*
     C                     WRITEDLACTVD0
     C*
     C** WRITE RATE CHANGES TO DEAL ACTIVITY FILE
     C*
     C           WBRTT     IFNE *ZERO
     C           WNICD     ANDNE*ZERO
     C           WNICD     ANDLTMDAT
     C*
     C* WRITE RATE CHANGES TO DEAL ACTIVITY FILE - FROM SCHEDULE
     C*
     C           WICFR     IFEQ 'Z'
     C                     EXSR ACRATS
     C                     ELSE
     C*
     C* WRITE RATE CHANGES TO DEAL ACTIVITY FILE - FROM FREQUENCY CODE
     C*
     C                     EXSR ACRATF
     C                     END
     C*
     C                     END
     C/COPY WNCPYSRC,DL1955C012
     C*
     C** WRITE PRINCIPAL CHANGES TO DEAL ACTIVITY FILE
     C*
     C           WNPCD     IFNE *ZERO
     C                     EXSR ACPRIN
     C                     END
     C/COPY WNCPYSRC,DL1955C013
     C*
     C                     ENDSR
     C**************************************************************************
     C/SPACE 5
     C********************************************************************
     C* WRITE RATE CHANGES TO DEAL ACTIVITY FILE - FROM SCHEDULE
     C********************************************************************
     C           ACRATS    BEGSR
     C*
     C** SET FILE POINTER TO NEXT RATE CHANGE DATE
     C*
     C                     MOVELWOTIN     OTIN
     C                     MOVEL'R'       RCIP
     C                     Z-ADDWNICD     PRDT
     C           DLDTSK    CHAINDLDTSCPD             99    *
     C*
     C** BYPASS IF NEXT RATE CHANGE DATE NOT FOUND
     C*
     C           *IN99     CABEQ'1'       EACRTS
     C*
     C** UPDATE LAST INTEREST RATE CHANGE DATE
     C** TO THIS CURRENT RATE CHANGE DATE
     C*
     C                     Z-ADDWNICD     WLICD   50
     C*
     C** GET FOLLOWING RATE CHANGE DATE
     C*
     C           DLDTSP    READEDLDTSCPD                 99*
     C*
     C** DO WHILE RATE CHANGES ARE FOUND
     C*
     C           *IN99     DOWEQ'0'
     C*
     C** CALCULATE FORWARD RATE FROM LAST RATE CHANGE DATE
     C** TO THIS CURRENT RATE CHANGE DATE
     C*
     C                     Z-ADDWLICD     ZFWNDT           * NEAR DATE
     C                     Z-ADDPRDT      ZFWFDT           * FAR DATE
     C                     EXSR CALFWR
     C*
     C** DETERMINE PRINCIPAL
     C** AS AT THE LAST CURRENT RATE CHANGE DATE
     C*
      *                                                                   100288
      ** If swap is single currency, the Our/Their Indicator on the       100288
      **   principal change records will be blank.                        100288
      *                                                                   100288
     C           DTYP      IFEQ 'RX'                                      100288
     C                     MOVELWOTIN     OTIN                            100288
     C                     ELSE                                           100288
     C                     MOVEL*BLANK    OTIN                            100288
     C                     END                                            100288
      *                                                                   100288
     C                     Z-ADDWLICD     PRDT
     C           DLPCSK    SETGTDLPCSCPD
     C           DLPCSP    REDPEDLPCSCPD                 99*
     C  N99                Z-ADDPAMT      WPAMT
      *                                                                                       CIR014
      ** For RX deals with a revalued exchange rate, use the revalued                         CIR014
      ** principal starting from the next int pay date.                                       CIR014
      *                                                                                       CIR014
     C           CIR014    IFEQ 'Y'                                                           CIR014
     C           DTYP      ANDEQ'RX'                                                          CIR014
     C           F1PRVR    ANDNE0                                                             CIR014
     C           F1PRVR    ANDNE0                                                             CIR014
     C           WNIPD     ANDLEWLICD                                                         CIR014
      *                                                                                       CIR014
     C           WOTIN     IFEQ 'U'                                                           CIR014
     C           F1PRVI    ANDEQ'O'                                                           CIR014
      *                                                                                       CIR014
     C           WOTIN     OREQ 'T'                                                           CIR014
     C           F1PRVI    ANDEQ'T'                                                           CIR014
      *                                                                                       CIR014
     C                     Z-ADDF1NPRI    WPAMT                                               CIR014
      *                                                                                       CIR014
     C                     ENDIF                                                              CIR014
      *                                                                                       CIR014
     C                     ENDIF                                                              CIR014
     C*
     C** WRITE RECORD TO DEAL ACTIVITY FILE DATED LAST RATE CHANGE DATE
     C** WITH PRINCIPAL = PRINCIPAL AS AT THIS DATE
     C** & RATE = FORWARD RATE TO NEXT RATE CHANGE DATE
     C*
     C                     Z-ADDDLNO      ACDLNO
     C                     MOVELWOTIN     ACOTIN
     C                     Z-ADDWLICD     ACPRDT
     C                     Z-ADDWPAMT     ACPCPL
     C                     Z-ADDZFWOUT    ACEINR
     C                     MOVEL'F'       ACRTTP           * FORWARD RATE
     C           WINFD     IFGT WLICD                                     127751
     C           WINFD     OREQ *ZERO                                                         207270
     C           WICFR     ANDNE*BLANKS                                                       207270
     C                     Z-ADDWBRTE     ACEINR           *NEXT BASERATE 127751
     C                     MOVEL'A'       ACRTTP                          127751
     C********** WSPIN     IFEQ 'A'                                                    127751 207269
     C********** ACEINR    ADD  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C********** WSPIN     IFEQ 'S'                                                    127751 207269
     C********** ACEINR    SUB  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C                     END                                            127751
     C           ZFRRTC    IFEQ '*NRCD'
     C                     MOVEL'?'       ACRTTP
     C                     END
     C/COPY WNCPYSRC,DL1955C007
     C                     WRITEDLACTVD0
     C*
     C** UPDATE LAST INTEREST RATE CHANGE DATE
     C** TO THIS CURRENT RATE CHANGE DATE
     C*
     C                     Z-ADDZFWFDT    WLICD
     C*
     C** GET NEXT RATE CHANGE DATE
     C*
     C                     SETOF                     99                   110055
     C                     MOVELWOTIN     OTIN                            110055
     C           DLDTSP    READEDLDTSCPD                 99*
     C                     END
     C*
     C** CALCULATE FORWARD RATE FROM LAST RATE CHANGE DATE
     C** TO MATURITY DATE
     C*
     C                     Z-ADDWLICD     ZFWNDT           * NEAR DATE
     C                     Z-ADDMDAT      ZFWFDT           * FAR DATE
     C                     EXSR CALFWR
     C*
     C** DETERMINE PRINCIPAL
     C** AS AT THE LAST CURRENT RATE CHANGE DATE
     C*
      *                                                                   100288
      ** If swap is single currency, the Our/Their Indicator on the       100288
      **   principal change records will be blank.                        100288
      *                                                                   100288
     C           DTYP      IFEQ 'RX'                                      100288
     C                     MOVELWOTIN     OTIN                            100288
     C                     ELSE                                           100288
     C                     MOVEL*BLANK    OTIN                            100288
     C                     END                                            100288
      *                                                                   100288
     C                     Z-ADDWLICD     PRDT
     C           DLPCSK    SETGTDLPCSCPD
     C           DLPCSP    REDPEDLPCSCPD                 99*
     C  N99                Z-ADDPAMT      WPAMT
     C*
     C** WRITE RECORD TO DEAL ACTIVITY FILE DATED LAST RATE CHANGE DATE
     C** WITH PRINCIPAL = PRINCIPAL AS AT THIS DATE
     C** & RATE = FORWARD RATE TO MATURITY DATE
     C*
     C                     Z-ADDDLNO      ACDLNO
     C                     MOVELWOTIN     ACOTIN
     C                     Z-ADDWLICD     ACPRDT
     C                     Z-ADDWPAMT     ACPCPL
     C                     Z-ADDZFWOUT    ACEINR
     C                     MOVEL'F'       ACRTTP           * FORWARD RATE
     C           WINFD     IFGT WLICD                                     127751
     C           WINFD     OREQ *ZERO                                                         207270
     C           WICFR     ANDNE*BLANKS                                                       207270
     C                     Z-ADDWBRTE     ACEINR           *NEXT BASERATE 127751
     C                     MOVEL'A'       ACRTTP                          127751
     C********** WSPIN     IFEQ 'A'                                                    127751 207269
     C********** ACEINR    ADD  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C********** WSPIN     IFEQ 'S'                                                    127751 207269
     C********** ACEINR    SUB  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C                     END                                            127751
     C           ZFRRTC    IFEQ '*NRCD'
     C                     MOVEL'?'       ACRTTP
     C                     END
     C/COPY WNCPYSRC,DL1955C008
     C                     WRITEDLACTVD0
     C*
     C           EACRTS    ENDSR
     C**************************************************************************
     C/SPACE 5
     C********************************************************************
     C* WRITE RATE CHANGES TO DEAL ACTIVITY FILE - FROM FREQUENCY CODE
     C********************************************************************
     C           ACRATF    BEGSR
     C*
     C** UPDATE LAST INTEREST RATE CHANGE DATE
     C** TO THIS CURRENT RATE CHANGE DATE
     C*
     C                     Z-ADDWNICD     WLICD
     C*
     C** DETERMINE FOLLOWING RATE CHANGE DATE
     C*
     C                     Z-ADDWLICD     ZFRDAT
     C                     MOVEL'R'       ZRCIP                           CSW097
     C                     EXSR DETNXD
     C*
     C** DO WHILE NEXT RATE CHANGE DATE < MATURITY DATE
     C*
     C           ZFRNXD    DOWLTMDAT
     C*
     C** CALCULATE FORWARD RATE FROM LAST RATE CHANGE DATE
     C** TO THIS CURRENT RATE CHANGE DATE
     C*
     C                     Z-ADDWLICD     ZFWNDT           * NEAR DATE
     C                     Z-ADDZFRNXD    ZFWFDT           * FAR DATE
     C                     EXSR CALFWR
     C*
     C** DETERMINE PRINCIPAL
     C** AS AT THE LAST CURRENT RATE CHANGE DATE
     C*
      *                                                                   100288
      ** If swap is single currency, the Our/Their Indicator on the       100288
      **   principal change records will be blank.                        100288
      *                                                                   100288
     C           DTYP      IFEQ 'RX'                                      100288
     C                     MOVELWOTIN     OTIN                            100288
     C                     ELSE                                           100288
     C                     MOVEL*BLANK    OTIN                            100288
     C                     END                                            100288
      *                                                                   100288
     C                     Z-ADDWLICD     PRDT
     C           DLPCSK    SETGTDLPCSCPD
     C           DLPCSP    REDPEDLPCSCPD                 99*
     C  N99                Z-ADDPAMT      WPAMT
      *                                                                                       CIR014
      ** For RX deals with a revalued exchange rate, use the revalued                         CIR014
      ** principal starting from the next int pay date.                                       CIR014
      *                                                                                       CIR014
     C           CIR014    IFEQ 'Y'                                                           CIR014
     C           DTYP      ANDEQ'RX'                                                          CIR014
     C           F1PRVR    ANDNE0                                                             CIR014
     C           F1PRVR    ANDNE0                                                             CIR014
     C           WNIPD     ANDLEWLICD                                                         CIR014
      *                                                                                       CIR014
     C           WOTIN     IFEQ 'U'                                                           CIR014
     C           F1PRVI    ANDEQ'O'                                                           CIR014
      *                                                                                       CIR014
     C           WOTIN     OREQ 'T'                                                           CIR014
     C           F1PRVI    ANDEQ'T'                                                           CIR014
      *                                                                                       CIR014
     C                     Z-ADDF1NPRI    WPAMT                                               CIR014
      *                                                                                       CIR014
     C                     ENDIF                                                              CIR014
      *                                                                                       CIR014
     C                     ENDIF                                                              CIR014
     C*
     C** WRITE RECORD TO DEAL ACTIVITY FILE DATED LAST RATE CHANGE DATE
     C** WITH PRINCIPAL = PRINCIPAL AS AT THIS DATE
     C** & RATE = FORWARD RATE TO NEXT RATE CHANGE DATE
     C*
     C                     Z-ADDDLNO      ACDLNO
     C                     MOVELWOTIN     ACOTIN
     C                     Z-ADDWLICD     ACPRDT
     C                     Z-ADDWPAMT     ACPCPL
     C                     Z-ADDZFWOUT    ACEINR
     C                     MOVEL'F'       ACRTTP           * FORWARD RATE
     C           WINFD     IFGT WLICD                                     127751
     C                     Z-ADDWBRTE     ACEINR           *NEXT BASERATE 127751
     C                     MOVEL'A'       ACRTTP                          127751
     C********** WSPIN     IFEQ 'A'                                                    127751 207269
     C********** ACEINR    ADD  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C********** WSPIN     IFEQ 'S'                                                    127751 207269
     C********** ACEINR    SUB  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C                     END                                            127751
     C           ZFRRTC    IFEQ '*NRCD'
     C                     MOVEL'?'       ACRTTP
     C                     END
     C/COPY WNCPYSRC,DL1955C009
     C                     WRITEDLACTVD0
     C*
     C** UPDATE LAST INTEREST RATE CHANGE DATE
     C** TO THIS CURRENT RATE CHANGE DATE
     C*
     C                     Z-ADDZFRNXD    WLICD
     C*
     C** DETERMINE NEXT RATE CHANGE DATE
     C*
     C                     Z-ADDWLICD     ZFRDAT
     C                     MOVEL'R'       ZRCIP                           CSW097
     C                     EXSR DETNXD
     C*
     C                     END
     C*
     C** CALCULATE FORWARD RATE FROM LAST RATE CHANGE DATE
     C** TO MATURITY DATE
     C*
     C                     Z-ADDWLICD     ZFWNDT           * NEAR DATE
     C                     Z-ADDMDAT      ZFWFDT           * FAR DATE
     C                     EXSR CALFWR
     C*
     C** DETERMINE PRINCIPAL
     C** AS AT THE LAST CURRENT RATE CHANGE DATE
     C*
      *                                                                   100288
      ** If swap is single currency, the Our/Their Indicator on the       100288
      **   principal change records will be blank.                        100288
      *                                                                   100288
     C           DTYP      IFEQ 'RX'                                      100288
     C                     MOVELWOTIN     OTIN                            100288
     C                     ELSE                                           100288
     C                     MOVEL*BLANK    OTIN                            100288
     C                     END                                            100288
      *                                                                   100288
     C                     Z-ADDWLICD     PRDT
     C           DLPCSK    SETGTDLPCSCPD
     C           DLPCSP    REDPEDLPCSCPD                 99*
     C  N99                Z-ADDPAMT      WPAMT
     C*
     C** WRITE RECORD TO DEAL ACTIVITY FILE DATED LAST RATE CHANGE DATE
     C** WITH PRINCIPAL = PRINCIPAL AS AT THIS DATE
     C** & RATE = FORWARD RATE TO MATURITY DATE
     C*
     C                     Z-ADDDLNO      ACDLNO
     C                     MOVELWOTIN     ACOTIN
     C                     Z-ADDWLICD     ACPRDT
     C                     Z-ADDWPAMT     ACPCPL
     C                     Z-ADDZFWOUT    ACEINR
     C                     MOVEL'F'       ACRTTP           * FORWARD RATE
     C           WINFD     IFGT WLICD                                     127751
     C                     Z-ADDWBRTE     ACEINR           *NEXT BASERATE 127751
     C                     MOVEL'A'       ACRTTP                          127751
     C********** WSPIN     IFEQ 'A'                                                    127751 207269
     C********** ACEINR    ADD  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C********** WSPIN     IFEQ 'S'                                                    127751 207269
     C********** ACEINR    SUB  WRTSP     ACEINR                                       127751 207269
     C**********           END                                                         127751 207269
     C                     END                                            127751
     C           ZFRRTC    IFEQ '*NRCD'
     C                     MOVEL'?'       ACRTTP
     C                     END
     C/COPY WNCPYSRC,DL1955C010
     C                     WRITEDLACTVD0
     C*
     C                     ENDSR
     C**************************************************************************
     C/SPACE 5
     C********************************************************************
     C* WRITE PRINCIPAL CHANGES TO DEAL ACTIVITY FILE
     C********************************************************************
     C           ACPRIN    BEGSR
     C*
     C** GET NEXT PRINCIPAL CHANGE DATE
     C*
     C           DTYP      IFEQ 'RX'
     C                     MOVELWOTIN     OTIN
     C                     ELSE
     C                     MOVEL*BLANK    OTIN
     C                     END
     C                     Z-ADDWNPCD     PRDT
     C           DLPCSK    CHAINDLPCSCPD             99    *
     C*
     C** DO WHILE PRINCIPAL CHANGES FOUND
     C*
     C           *IN99     DOWEQ'0'
     C*
     C** ONLY ADD TO DEAL ACTIVITY FILE IF DATE NOT REPRESENTED
     C*
     C                     Z-ADDDLNO      ACDLNO
     C                     MOVE WOTIN     ACOTIN
     C                     Z-ADDPRDT      ACPRDT
     C           DLACTK    CHAINDLACTVD0             99    *
     C           *IN99     IFEQ '1'
     C*
     C** DETERMINE RATE AS AT PRINCIPAL CHANGE DATE FROM
     C** DEAL ACTIVITY FILE
     C*
     C           DLACTK    SETLLDLACTVD0
     C           DLACTP    REDPEDLACTVD0                 99*
     C*
     C** WRITE RECORD TO DEAL ACTIVITY FILE DATED PRINCIPAL CHANGE DATE
     C** WITH PRINCIPAL = PRINCIPAL AS AT THIS DATE
     C** & RATE = RATE TAKEN FROM DEAL ACTIVITY FILE
     C*
     C                     Z-ADDPRDT      ACPRDT
     C                     Z-ADDPAMT      ACPCPL
     C/COPY WNCPYSRC,DL1955C011
     C                     WRITEDLACTVD0
     C                     END
     C*
     C** GET NEXT PRINCIPAL CHANGE DATE
     C*
     C           DLPCSP    READEDLPCSCPD                 99*
     C                     END
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C**************************************************************************
     C* UPDATE CASH FLOW EVENTS FOR INTEREST & CASH FLOWS
     C**************************************************************************
     C           CFEVIC    BEGSR
     C*
     C**  CASHFLOWS DEFINED AS A SCHEDULE
     C*
     C           WIPFR     IFEQ 'C'
     C                     EXSR CSHFLW
     C                     GOTO ECFEVI
     C                     END
     C*
     C** IF NEXT INTEREST PAY DATE NOT PRESENT,
     C** DEFAULT IT TO MATURITY DATE
     C*
     C           WNIPD     IFEQ *ZERO
     C                     Z-ADDMDAT      WNIPD
     C                     END
     C*
     C** INTEREST PAYABLE UP FRONT (MUST BE PART OF A SCHEDULE)
     C*
     C           WINPM     IFEQ 'D'
     C           WINPM     OREQ 'Y'
     C                     EXSR INPYUF
     C                     GOTO ECFEVI
     C                     END
     C*
     C** INTEREST NOT PAYABLE UP FRONT (SCHEDULE OR FREQUENCY)
     C*
     C*
     C**  INTEREST PAYMENTS - FROM INTEREST PAYMENT SCHEDULE
     C*
     C           WIPFR     IFEQ 'Z'
     C                     EXSR INPYS
     C*
     C**  INTEREST PAYMENTS - FROM INTEREST PAYMENT FREQUENCY CODE
     C*
     C                     ELSE
     C                     EXSR INPYF
     C                     END
     C           ECFEVI    ENDSR
     C**************************************************************************
      /SPACE 5
     C**************************************************************************
     C* UPDATE CASH FLOW EVENTS FOR SPREAD PAYMENTS
     C**************************************************************************
     C           CFEVSP    BEGSR
     C*
     C** IF A SPREAD ON THE FLOATING SIDE, THE SPREAD CAN BE
     C** REPRESENTED AS A CASH FLOW PAID ON INTEREST PAYMENT DATES
     C*
     C           WSPIN     IFEQ 'S'                        * SUBTRACT
     C           WSPIN     OREQ 'A'                        * ADD
     C*
     C** ONLY IF NEXT RATE CHANGE DATE PRESENT AND PRIOR TO MATURITY DATE
     C*
     C           WNICD     IFNE *ZERO
     C           WNICD     ANDLTMDAT
     C*
     C** IF NEXT INTEREST PAY DATE NOT PRESENT,
     C** DEFAULT IT TO MATURITY DATE
     C*
     C           WNIPD     IFEQ *ZERO
     C                     Z-ADDMDAT      WNIPD
     C                     END
     C*
     C**  SPREAD PAYMENTS - FROM INTEREST PAYMENT SCHEDULE
     C*
     C           WIPFR     IFEQ 'Z'
     C                     EXSR SPPYS
     C*
     C**  SPREAD PAYMENTS - FROM INTEREST PAYMENT FREQUENCY CODE
     C*
     C                     ELSE
     C                     EXSR SPPYF
     C                     END
     C                     END
     C                     END
     C           ECFEVS    ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* PROCESS CASHFLOWS
     C********************************************************************
     C           CSHFLW    BEGSR
     C*
     C** GET NEXT CASH FLOW
     C*
     C                     MOVELWOTIN     OTIN
     C                     Z-ADDWNIPD     PRDT
     C           DLCFSK    CHAINDLCFSCPD             99    *
     C*
     C           *IN99     DOWEQ'0'
     C*
     C** WRITE OUT CASH FLOW EVENT FOR DEFINED CASHFLOW
     C*
     C                     Z-ADDPRDT      CFPYDT
     C                     Z-ADDPRDT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADDCFLA      CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C           CFAMNT    IFNE *ZERO
     C                     EXSR WRTCFE
     C                     END
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C*
     C                     Z-ADDPRDT      WSLIP
     C*
     C** GET NEXT CASH FLOW
     C*
     C           DLCFSP    READEDLCFSCPD                 99*
     C                     END
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* PROCESS INTEREST PAYMENTS - UP FRONT
     C********************************************************************
     C           INPYUF    BEGSR
     C*
     C** WRITE OUT CASH FLOW EVENT FOR VALUE DATE IF NOT YET REACHED
     C*
     C           VDAT      IFGE BJDNWD
     C                     Z-ADDVDAT      CFPYDT
     C                     Z-ADDWNIPD     CFNIPD
     C                     Z-ADDVDAT      CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C                     END
     C*
     C** SET FILE POINTER TO NEXT INTEREST PAYMENT DATE
     C*
     C                     MOVELWOTIN     OTIN
     C                     MOVEL'P'       RCIP
     C                     Z-ADDWNIPD     PRDT
     C           DLDTSK    CHAINDLDTSCPD             99    *
     C*
     C** BYPASS IF NEXT INTEREST PAYMENT DATE NOT FOUND
     C*
     C           *IN99     CABEQ'1'       EINPYU
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C** TO THIS CURRENT PAYMENT DATE
     C*
     C                     Z-ADDWNIPD     WSLIP
     C*
     C** GET FOLLOWING INTEREST PAYMENT DATE
     C*
     C           DLDTSP    READEDLDTSCPD                 99*
     C*
     C** DO WHILE INTEREST PAYMENTS ARE FOUND
     C*
     C           *IN99     DOWEQ'0'
     C*
     C** WRITE OUT CASH FLOW EVENT FOR LAST INTEREST PAYMENT DATE
     C*
     C                     Z-ADDWSLIP     CFPYDT
     C                     Z-ADDPRDT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C** TO THIS CURRENT PAYMENT DATE
     C*
     C                     Z-ADDPRDT      WSLIP
     C*
     C** GET NEXT RATE CHANGE DATE
     C*
     C           DLDTSP    READEDLDTSCPD                 99*
     C                     END
     C*
     C** WRITE OUT CASH FLOW EVENT FOR LAST INTEREST PAYMENT DATE
     C*
     C           WSLIP     IFGT VDAT
     C           WSLIP     ANDLTMDAT
     C                     Z-ADDWSLIP     CFPYDT
     C                     Z-ADDMDAT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C                     END
     C*
     C           EINPYU    ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* PROCESS INTEREST PAYMENTS - FROM INTEREST PAYMENT SCHEDULE
     C********************************************************************
     C           INPYS     BEGSR
     C*
     C** GET NEXT DATE
     C*
     C                     MOVELWOTIN     OTIN
     C                     MOVEL'P'       RCIP
     C                     Z-ADDWNIPD     PRDT
     C           DLDTSK    CHAINDLDTSCPD             99    *
     C*
     C           *IN99     DOWEQ'0'
     C*
     C** WRITE OUT CASH FLOW EVENT FOR NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDPRDT      CFPYDT
     C                     Z-ADDPRDT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C*
     C                     Z-ADDPRDT      WSLIP
     C*
     C** GET NEXT DATE
     C*
     C           DLDTSP    READEDLDTSCPD                 99*
     C                     END
     C*
     C** WRITE OUT CASH FLOW EVENT FOR MATURITY DATE
     C*
     C           WSLIP     IFLT MDAT
     C                     Z-ADDMDAT      CFPYDT
     C                     Z-ADDMDAT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C                     END
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* PROCESS INTEREST PAYMENTS - FROM INTEREST PAYMENT FREQUENCY CODE
     C********************************************************************
     C           INPYF     BEGSR
     C*
     C** SET UP VARIABLES USED IN FREQUENCY CALCULATION
     C*
     C                     MOVELWIPFR     ZFRFRQ
     C                     Z-ADDWIPFD     ZFRBAS
     C                     Z-ADDWSETM     ZFRSET
     C                     MOVELWCUCY     ZFRCCY
     C                     MOVELWSACC     ZFRNOS
     C                     Z-ADDMDAT      ZFRMDT
     C*
     C** DO WHILE MATURITY DATE NOT REACHED
     C*
     C           WNIPD     DOWLTMDAT
     C*
     C** WRITE OUT CASH FLOW EVENT FOR NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDWNIPD     CFPYDT
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WAMCN     ANDEQ'A'                                       CIR005
     C           WCYP1     ANDNE*BLANK                                    CIR005
     C                     Z-ADDWIPBD     CFNIPD                          CIR005
     C                     ELSE                                           CIR005
     C                     Z-ADDWNIPD     CFNIPD
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C*
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WAMCN     ANDEQ'A'                                       CIR005
     C           WCYP1     ANDNE*BLANK                                    CIR005
     C                     Z-ADDWIPBD     WSLIP                           CIR005
     C                     ELSE                                           CIR005
     C                     Z-ADDWNIPD     WSLIP
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C*
     C** DETERMINE NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDWNIPD     ZFRDAT
     C                     MOVEL'P'       ZRCIP                           CSW097
     C                     EXSR DETNXD
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WCYP1     ANDNE*BLANKS                                   CIR005
     C           WIPBD     ANDGTZFRMDT                                    CIR005
     C                     Z-ADDZFRMDT    ZFRNXD                          CIR005
     C                     Z-ADDZFRMDT    WIPBD                           CIR005
     C                     ENDIF                                          CIR005
     C*
     C** UPDATE NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDZFRNXD    WNIPD
     C                     END
     C*
     C** WRITE OUT CASH FLOW EVENT FOR MATURITY DATE
     C*
     C                     Z-ADDMDAT      CFPYDT
     C                     Z-ADDMDAT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADD*ZERO     CFRTSP           * SPREAD
     C                     MOVEL*BLANK    CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* PROCESS SPREAD PAYMENTS - FROM INTEREST PAYMENT SCHEDULE
     C********************************************************************
     C           SPPYS     BEGSR
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C** WITH NEXT RATE CHANGE DATE
     C*
     C                     Z-ADDWNICD     WSLIP
     C*
     C** GET NEXT INTEREST PAYMENT DATE AFTER THE NEXT RATE CHANGE DATE
     C*
     C                     MOVELWOTIN     OTIN
     C                     MOVEL'P'       RCIP
     C                     Z-ADDWNICD     PRDT
     C           DLDTSK    SETGTDLDTSCPD
     C           DLDTSP    READEDLDTSCPD                 99*
     C*
     C           *IN99     DOWEQ'0'
     C*
     C** WRITE OUT CASH FLOW EVENT FOR NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDPRDT      CFPYDT
     C                     Z-ADDPRDT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADDWRTSP     CFRTSP           * SPREAD
     C                     MOVELWSPIN     CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C*
     C                     Z-ADDPRDT      WSLIP
     C*
     C** GET NEXT DATE
     C*
     C           DLDTSP    READEDLDTSCPD                 99*
     C                     END
     C*
     C** WRITE OUT CASH FLOW EVENT FOR MATURITY DATE
     C*
     C           WSLIP     IFLT MDAT
     C                     Z-ADDMDAT      CFPYDT
     C                     Z-ADDMDAT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADDWRTSP     CFRTSP           * SPREAD
     C                     MOVELWSPIN     CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C                     END
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* PROCESS SPREAD PAYMENTS - FROM INTEREST PAYMENT FREQUENCY CODE
     C********************************************************************
     C           SPPYF     BEGSR
     C*
     C** SET UP VARIABLES USED IN FREQUENCY CALCULATION
     C*
     C                     MOVELWIPFR     ZFRFRQ
     C                     Z-ADDWIPFD     ZFRBAS
     C                     Z-ADDWSETM     ZFRSET
     C                     MOVELWCUCY     ZFRCCY
     C                     MOVELWSACC     ZFRNOS
     C                     Z-ADDMDAT      ZFRMDT
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C** WITH NEXT RATE CHANGE DATE
     C*
     C                     Z-ADDWNICD     WSLIP
     C*
     C** IF NEXT RATE CHANGE DATE IS > OR EQUAL TO NEXT INTEREST PAYMENT
     C** DATE, CALCULATE THE FIRST INTEREST PAYMENT DATE FOLLOWING IT
     C*
     C********** WNICD     IFGE WNIPD                                     100953
     C           WNICD     DOWGEWNIPD                                     100953
     C           WNIPD     ORGT MDAT                                      100953
     C                     Z-ADDWNIPD     ZFRDAT
     C                     EXSR DETNXD
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WCYP1     ANDNE*BLANKS                                   CIR005
     C           WIPBD     ANDGTZFRMDT                                    CIR005
     C                     Z-ADDZFRMDT    ZFRNXD                          CIR005
     C                     Z-ADDZFRMDT    WIPBD                           CIR005
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C                     Z-ADDZFRNXD    WNIPD
     C                     END
     C*
     C** DO WHILE MATURITY DATE NOT REACHED
     C*
     C           WNIPD     DOWLTMDAT
     C*
     C** WRITE OUT CASH FLOW EVENT FOR NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDWNIPD     CFPYDT
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WAMCN     ANDEQ'A'                                       CIR005
     C           WCYP1     ANDNE*BLANK                                    CIR005
     C                     Z-ADDWIPBD     CFNIPD                          CIR005
     C                     ELSE                                           CIR005
     C                     Z-ADDWNIPD     CFNIPD
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADDWRTSP     CFRTSP           * SPREAD
     C                     MOVELWSPIN     CFSPIN           * SPREAD IND
     C*                                                                   127748
     C** Only if the start/last int. date is after the fix date           127748
     C*                                                                   127748
     C           CFSLIP    IFGE WINFD                                     127748
     C                     EXSR WRTCFE
     C                     END                                            127748
     C*
     C** UPDATE LAST INTEREST PAYMENT DATE
     C*
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WAMCN     ANDEQ'A'                                       CIR005
     C           WCYP1     ANDNE*BLANK                                    CIR005
     C                     Z-ADDWIPBD     WSLIP                           CIR005
     C                     ELSE                                           CIR005
     C                     Z-ADDWNIPD     WSLIP
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C*
     C** DETERMINE NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDWNIPD     ZFRDAT
     C                     EXSR DETNXD
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WCYP1     ANDNE*BLANKS                                   CIR005
     C           WIPBD     ANDGTZFRMDT                                    CIR005
     C                     Z-ADDZFRMDT    ZFRNXD                          CIR005
     C                     Z-ADDZFRMDT    WIPBD                           CIR005
     C                     ENDIF                                          CIR005
     C*
     C** UPDATE NEXT INTEREST PAYMENT DATE
     C*
     C                     Z-ADDZFRNXD    WNIPD
     C                     END
     C*
     C** WRITE OUT CASH FLOW EVENT FOR MATURITY DATE
     C*
     C                     Z-ADDMDAT      CFPYDT
     C                     Z-ADDMDAT      CFNIPD
     C                     Z-ADDWSLIP     CFSLIP
     C                     Z-ADD0         CFAMNT           * CASH FLOW AMT
     C                     Z-ADDWRTSP     CFRTSP           * SPREAD
     C                     MOVELWSPIN     CFSPIN           * SPREAD IND
     C                     EXSR WRTCFE
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* WRITE OUT CASH FLOW EVENTS
     C********************************************************************
     C           WRTCFE    BEGSR
     C*
      ** Do not output any cash-flows dated prior to the run date.        100298
      *                                                                   100298
     C********** CFPYDT    IFGE BJRDNB                             100298 187326
     C           CFPYDT    IFGT BJRDNB                                    187326
      *                                                                   100298
     C                     Z-ADDDLNO      CFDLNO           * DEAL NUMBER
     C                     MOVELWOTIN     CFOTIN           * OUR/THEIR
     C           CFSPIN    IFEQ 'S'
     C                     Z-SUBCFRTSP    CFRTSP           * SPREAD
     C                     END
     C                     MOVELWCUCY     CFCCY            * DEAL CCY
     C                     MOVELWBRTT     CFBRTT           * BASE RATE
     C                     MOVELWICBS     CFINCL           * INT.CAL.BASIS
     C                     MOVELWINPM     CFINPM           * INT PROC.METH.
     C           DTYP      IFEQ 'RS'
     C                     MOVELWCUCY     CFRCCY           * REPORT CCY
     C                     ELSE
     C                     MOVELBJCYCD    CFRCCY           * BASE CURRENCY
     C                     END
     C                     MOVELBOKC      CFBOKC           * BOOK
     C                     MOVELBRCA      CFBRCA           * BRANCH
      *                                                                                       CAS001
     C           CAS001    IFEQ 'Y'                                                           CAS001
     C           CAS005    IFEQ 'Y'                                                           CAS005
     C                     MOVELWRYLD     CFYLDC                                              CAS005
     C           WYLDC     IFEQ *BLANKS                                                       CAS005
     C           CFBRTT    ANDNE*ZERO                                                         CAS005
     C                     MOVE *BLANKS   CFYLDC                                              CAS005
     C                     ENDIF                                                              CAS005
     C                     ELSE                                                               CAS005
     C                     MOVELWYLDC     CFYLDC                                              CAS001
     C                     ENDIF                                                              CAS005
     C                     ENDIF                                                              CAS001
      *                                                                                       CAS001
     C                     WRITEDLCFEVD0
      *                                                                   100298
     C                     ENDIF                                          100298
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* MOVE 'OUR' DETAILS TO WORK FIELDS
     C********************************************************************
     C           OUR       BEGSR
     C*
     C                     MOVE UCUCY     WCUCY   3
     C                     MOVE PSCY      WSTCY   3                       CSW097
     C                     Z-ADDUPAMT     WPAMT  130
     C                     Z-ADDUBRTT     WBRTT   20
     C                     Z-ADDUBRTE     WBRTE  117
     C                     Z-ADDURTSP     WRTSP  117
     C                     Z-ADDUEINR     WEINR  117
     C                     MOVE USPIN     WSPIN   1
     C                     Z-ADDUICBS     WICBS   10
     C                     Z-ADDUINFD     WINFD   50
     C                     Z-ADDUNICD     WNICD   50
     C                     Z-ADDUICBD     WICBD   50
     C                     MOVE UICFR     WICFR   1
     C                     Z-ADDUICFD     WICFD   20
     C                     Z-ADDUNIPD     WNIPD   50
     C                     Z-ADDUIPBD     WIPBD   50
     C                     Z-ADDUSLIP     WSLIP   50
     C                     MOVE UIPFR     WIPFR   1
     C                     Z-ADDUIPFD     WIPFD   20
     C                     Z-ADDUIACD     WIACD   50
     C                     Z-ADDUAITC     WAITC  130
     C                     Z-ADDUAIAN     WAIAN  130
     C                     Z-ADDUAIAP     WAIAP  130
     C                     Z-ADDUIPRD     WIPRD  130
     C                     MOVE UDNSI     WDNSI   1
     C                     MOVE UDSTI     WDSTI   1
     C                     MOVE PSETM     WSETM   20
     C**********           MOVE UPACC     WSACC  12                                           CGL029
     C                     MOVE UPACC     WSACC  18                                           CGL029
     C                     MOVE UINPM     WINPM   1
     C                     MOVE UNPCD     WNPCD   50
     C                     MOVE 'U'       WOTIN   1
      *                                                                   CIR005
      ** Save Business Day Convention fields                              CIR005
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
     C                     MOVE L1PDCN    WPDCN                                               211032
     C                     MOVE L1AMCN    WAMCN                           CIR005
     C                     MOVE L1CYP1    WCYP1                           CIR005
     C                     MOVE L1CYP2    WCYP2                           CIR005
     C                     MOVE L1CYP3    WCYP3                           CIR005
     C                     MOVE L1CYP4    WCYP4                           CIR005
     C                     MOVE L1CYP5    WCYP5                           CIR005
     C                     MOVE L1CYP6    WCYP6                           CIR005
     C                     MOVE L1CYP7    WCYP7                           CIR005
     C                     MOVE L1CYP8    WCYP8                           CIR005
     C                     MOVE L1CYP9    WCYP9                           CIR005
     C                     MOVE L1CYP0    WCYP0                           CIR005
     C                     ENDIF                                          CIR005
      *                                                                   100317
      ** Access the currency details and calculate the spot date.         100317
      *                                                                   100317
     C                     CALL 'AOCURRR0'                                100317
     C                     PARM '*MSG    '@RTCD                           100317
     C                     PARM '*KEY    '@OPTN                           100317
     C                     PARM           WCUCY                           100317
     C           SDCURR    PARM SDCURR    DSSDY                           100317
     C           @RTCD     IFNE *BLANK                                    100317
     C                     MOVE 'SDCURRPD'DBFILE           ***************100317
     C                     Z-ADD011       DBASE            * DB ERROR 11 *100317
     C                     MOVELWCUCY     DBKEY            ***************100317
     C                     EXSR *PSSR                                     100317
     C                     END                                            100317
      *                                                                   100317
      ** Calculate the currency spot date.                                100317
      *                                                                   100317
     C           CAS008    IFNE 'Y'                                                           CAS008
     C                     MOVE WCUCY     ZCCY                            100317
     C                     MOVE *BLANKS   ZLOC                            100317
     C                     Z-ADDBJRDNB    ZDAYNO                          100317
     C                     Z-ADDA6SPDY    ZNRDYS                          100317
     C                     EXSR ZFWDT                                     100317
     C                     Z-ADDZDYNBR    WSPDT   50                      100317
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS001
     C           CAS001    IFEQ 'Y'                                                           CAS001
      *                                                                                       CAS005
      ** If Enhancements to CAS001, CAS002 and CAS004 is installed,                           CAS005
      ** use forward yield curve.                                                             CAS005
      *                                                                                       CAS005
     C           CAS005    IFEQ 'Y'                                                           CAS005
     C                     MOVELFSOFYC    WYLDC                                               CAS005
     C                     MOVELFSOYLC    WRYLD   5                                           CAS005
     C                     ELSE                                                               CAS005
     C                     MOVELFSOYLC    WYLDC                                               CAS001
     C                     ENDIF                                                              CAS005
      *                                                                                       CAS005
     C                     ENDIF                                                              CAS001
      *                                                                                       CAS008
     C           CAS008    IFEQ 'Y'                                                           CAS008
      *                                                                                       CAS008
      ** Access Yield Rate file                                                               CAS008
      *                                                                                       CAS008
     C           WYLDC     IFNE *BLANKS                                                       CAS008
     C                     MOVE WCUCY     KCCY                                                CAS008
     C                     MOVE WICBS     KICM                                                CAS008
     C           KYLDR     CHAINSDYLDRL1             12                                       CAS008
      *                                                                                       CAS008
     C           *IN12     IFEQ *ON                                                           CAS008
      *                                                                                       CAS008
      ** If Yield Curve is applicable to all currencies                                       CAS008
      *                                                                                       CAS008
     C                     MOVE *BLANKS   KCCY                                                CAS008
     C           KYLDR     CHAINSDYLDRL1             12                                       CAS008
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS008
     C           *IN12     IFEQ *ON                                                           CAS008
     C                     Z-ADDA6SPDY    ZNRDYS                                              CAS008
     C                     ELSE                                                               CAS008
     C                     Z-ADDFSSPDY    ZNRDYS                                              CAS008
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS008
     C                     ELSE                                                               CAS008
     C                     Z-ADDA6SPDY    ZNRDYS                                              CAS008
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS008
     C                     MOVE WCUCY     ZCCY                                                CAS008
     C                     MOVE *BLANKS   ZLOC                                                CAS008
     C                     Z-ADDBJRDNB    ZDAYNO                                              CAS008
     C                     EXSR ZFWDT                                                         CAS008
     C                     Z-ADDZDYNBR    WSPDT                                               CAS008
      *                                                                                       CAS008
     C                     ENDIF                                                              CAS008
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* MOVE 'THEIR' DETAILS TO WORK FIELDS
     C********************************************************************
     C           THEIR     BEGSR
     C*
     C                     MOVE TCUCY     WCUCY   3
     C                     MOVE RSCY      WSTCY   3                       CSW097
     C                     Z-ADDTPAMT     WPAMT  130
     C                     Z-ADDTBRTT     WBRTT   20
     C                     Z-ADDTBRTE     WBRTE  117
     C                     Z-ADDTRTSP     WRTSP  117
     C                     Z-ADDTEINR     WEINR  117
     C                     MOVE TSPIN     WSPIN   1
     C                     Z-ADDTICBS     WICBS   10
     C                     Z-ADDTINFD     WINFD   50
     C                     Z-ADDTNICD     WNICD   50
     C                     Z-ADDTICBD     WICBD   50
     C                     MOVE TICFR     WICFR   1
     C                     Z-ADDTICFD     WICFD   20
     C                     Z-ADDTNIPD     WNIPD   50
     C                     Z-ADDTIPBD     WIPBD   50
     C                     Z-ADDTSLIP     WSLIP   50
     C                     MOVE TIPFR     WIPFR   1
     C                     Z-ADDTIPFD     WIPFD   20
     C                     Z-ADDTIACD     WIACD   50
     C                     Z-ADDTAITC     WAITC  130
     C                     Z-ADDTAIAN     WAIAN  130
     C                     Z-ADDTAIAP     WAIAP  130
     C                     Z-ADDTIPRD     WIPRD  130
     C                     MOVE TDNSI     WDNSI   1
     C                     MOVE TDSTI     WDSTI   1
     C                     MOVE RSETM     WSETM   20
     C**********           MOVE URACC     WSACC  12                                           CGL029
     C                     MOVE URACC     WSACC                                               CGL029
     C                     MOVE TINPM     WINPM   1
     C           DTYP      IFEQ 'RX'
     C                     MOVE TNPCD     WNPCD   50
     C                     ELSE
     C                     MOVE UNPCD     WNPCD   50
     C                     END
     C                     MOVE 'T'       WOTIN   1
      *                                                                   CIR005
      ** Save Business Day Convention fields                              CIR005
      *                                                                   CIR005
     C           CIR005    IFEQ 'Y'                                       CIR005
      *                                                                   CIR005
     C           DTYP      IFEQ 'RX'                                      CIR005
     C                     MOVE L2PDCN    WPDCN   2                       CIR005
     C                     MOVE L2AMCN    WAMCN   2                       CIR005
     C                     MOVE L2CYP1    WCYP1   3                       CIR005
     C                     MOVE L2CYP2    WCYP2   3                       CIR005
     C                     MOVE L2CYP3    WCYP3   3                       CIR005
     C                     MOVE L2CYP4    WCYP4   3                       CIR005
     C                     MOVE L2CYP5    WCYP5   3                       CIR005
     C                     MOVE L2CYP6    WCYP6   3                       CIR005
     C                     MOVE L2CYP7    WCYP7   3                       CIR005
     C                     MOVE L2CYP8    WCYP8   3                       CIR005
     C                     MOVE L2CYP9    WCYP9   3                       CIR005
     C                     MOVE L2CYP0    WCYP0   3                       CIR005
     C                     ELSE                                           CIR005
     C                     MOVE L1PDCN    WPDCN                           CIR005
     C                     MOVE L1AMCN    WAMCN                           CIR005
     C                     MOVE L1CYP1    WCYP1                           CIR005
     C                     MOVE L1CYP2    WCYP2                           CIR005
     C                     MOVE L1CYP3    WCYP3                           CIR005
     C                     MOVE L1CYP4    WCYP4                           CIR005
     C                     MOVE L1CYP5    WCYP5                           CIR005
     C                     MOVE L1CYP6    WCYP6                           CIR005
     C                     MOVE L1CYP7    WCYP7                           CIR005
     C                     MOVE L1CYP8    WCYP8                           CIR005
     C                     MOVE L1CYP9    WCYP9                           CIR005
     C                     MOVE L1CYP0    WCYP0                           CIR005
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C           CIR017    IFEQ 'Y'                                                           CIR017
     C           DTYP      ANDEQ'RS'                                                          CIR017
     C                     MOVE L2PDCN    WPDCN                                               CIR017
     C                     MOVE L2AMCN    WAMCN                                               CIR017
     C                     ENDIF                                                              CIR017
      *                                                                   CIR005
     C                     ENDIF                                          CIR005
      *                                                                   100317
      ** Access the currency details and calculate the spot date.         100317
      *                                                                   100317
     C                     CALL 'AOCURRR0'                                100317
     C                     PARM '*MSG    '@RTCD                           100317
     C                     PARM '*KEY    '@OPTN                           100317
     C                     PARM           WCUCY                           100317
     C           SDCURR    PARM SDCURR    DSSDY                           100317
     C           @RTCD     IFNE *BLANK                                    100317
     C                     MOVE 'SDCURRPD'DBFILE           ***************100317
     C                     Z-ADD011       DBASE            * DB ERROR 11 *100317
     C                     MOVELWCUCY     DBKEY            ***************100317
     C                     EXSR *PSSR                                     100317
     C                     END                                            100317
      *                                                                   100317
      ** Calculate the currency spot date.                                100317
      *                                                                   100317
     C           CAS008    IFNE 'Y'                                                           CAS008
     C                     MOVE WCUCY     ZCCY                            100317
     C                     MOVE *BLANKS   ZLOC                            100317
     C                     Z-ADDBJRDNB    ZDAYNO                          100317
     C                     Z-ADDA6SPDY    ZNRDYS                          100317
     C                     EXSR ZFWDT                                     100317
     C                     Z-ADDZDYNBR    WSPDT                           100317
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS001
     C           CAS001    IFEQ 'Y'                                                           CAS001
      *                                                                                       CAS005
     C           CAS005    IFNE 'Y'                                                           CAS005
      *                                                                                       CAS005
     C           DTYP      IFEQ 'RX'                                                          CAS001
     C                     MOVELFSTYLC    WYLDC                                               CAS001
     C                     ELSE                                                               CAS001
     C                     MOVELFSOYLC    WYLDC                                               CAS001
     C                     ENDIF                                                              CAS001
      *                                                                                       CAS005
     C                     ELSE                                                               CAS005
     C                     MOVELFSTFYC    WYLDC                                               CAS005
     C                     MOVELFSTYLC    WRYLD                                               CAS005
     C                     ENDIF                                                              CAS005
      *                                                                                       CAS005
     C                     ENDIF                                                              CAS001
      *                                                                                       CAS008
     C           CAS008    IFEQ 'Y'                                                           CAS008
      *                                                                                       CAS008
      ** Access Yield Rate file                                                               CAS008
      *                                                                                       CAS008
     C           WYLDC     IFNE *BLANKS                                                       CAS008
     C                     MOVE WCUCY     KCCY                                                CAS008
     C                     MOVE WICBS     KICM                                                CAS008
     C           KYLDR     CHAINSDYLDRL1             12                                       CAS008
      *                                                                                       CAS008
     C           *IN12     IFEQ *ON                                                           CAS008
      *                                                                                       CAS008
      ** If Yield Curve is applicable to all currencies                                       CAS008
      *                                                                                       CAS008
     C                     MOVE *BLANKS   KCCY                                                CAS008
     C           KYLDR     CHAINSDYLDRL1             12                                       CAS008
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS008
     C           *IN12     IFEQ *ON                                                           CAS008
     C                     Z-ADDA6SPDY    ZNRDYS                                              CAS008
     C                     ELSE                                                               CAS008
     C                     Z-ADDFSSPDY    ZNRDYS                                              CAS008
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS008
     C                     ELSE                                                               CAS008
     C                     Z-ADDA6SPDY    ZNRDYS                                              CAS008
     C                     ENDIF                                                              CAS008
      *                                                                                       CAS008
     C                     MOVE WCUCY     ZCCY                                                CAS008
     C                     MOVE *BLANKS   ZLOC                                                CAS008
     C                     Z-ADDBJRDNB    ZDAYNO                                              CAS008
     C                     EXSR ZFWDT                                                         CAS008
     C                     Z-ADDZDYNBR    WSPDT                                               CAS008
      *                                                                                       CAS008
     C                     ENDIF                                                              CAS008
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* CALCULATE FORWARD RATE
     C********************************************************************
     C           CALFWR    BEGSR
     C*
     C** Use the Settlement currency if it has been entered.                                  146477
     C*                                                                                       146477
     C           RSCY      IFNE *BLANKS                                                       146477
     C           PSCY      ANDNE*BLANKS                                                       146477
     C                     MOVELRSCY      ZFWCCY                                              146477
     C                     ENDIF                                                              146477
     C**********           CALL 'DLZFWR'                                                      245132
     C                     CALL 'DLZFWR2'                                                     245132
     C                     PARM *BLANK    ZFWRTC  5
     C                     PARM           ZFWCCY  3        * CURRENCY
     C                     PARM           ZFWINC  1        * INT.CALC.METH.
     C                     PARM WSPDT     ZFWSDT  50       * SPOT DATE    100317
     C                     PARM           ZFWNDT  50       * NEAR DATE
     C                     PARM           ZFWFDT  50       * FAR DATE
     C                     PARM           ZFWOUT 117       * FORWARD RATE
     C                     PARM           WYLDC   5                                           CAS001
     C                     PARM           AS001C  1                                           245132
     C                     PARM           CAS008                                              245132
     C                     PARM           CIR004  1                                           245132
     C                     PARM           ZFWCC2  3                                           245132
     C                     PARM           ZFWIN2  1                                           245132
     C                     PARM           ZFYLD2  5                                           245132
     C                     PARM           ZFWSD2  50                                          245132
     C                     PARM           ZFWND2  50                                          245132
     C                     PARM           ZFWNE2 119                                          245132
     C*
     C/COPY WNCPYSRC,DL1955C002
     C           ZFWRTC    IFEQ '*ERR*'
     C                     EXSR *PSSR
     C                     END
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* DETERMINE NEXT DAY
     C********************************************************************
     C           DETNXD    BEGSR
     C*                                                                   146477
     C** Use the Settlement currency if it has been entered.              146477
     C*                                                                   146477
     C           RSCY      IFNE *BLANKS                                   146477
     C           PSCY      ANDNE*BLANKS                                   146477
     C***********          MOVELRSCY      ZFWCCY                   146477 152016
     C                     MOVELRSCY      ZFRCCY                          152016
     C                     ENDIF                                          146477
      *                                                                   CIR005
      ** Determine Next Rate Change Date                                  CIR005
      *                                                                   CIR005
      ** If Daily Rate Change Frequency then simply add 1 to next                             CIR018
      ** change date                                                                          CIR018
     C           CIR018    IFEQ 'Y'                                                           CIR018
     C           ZFREQ     ANDEQ'D'                                                           CIR018
     C           ZFRDAT    ADD  1         ZFRNXD                                              CIR018
     C                     ELSE                                                               CIR018
      *                                                                                       CIR018
     C           CIR005    IFEQ 'Y'                                       CIR005
     C           WCYP1     ANDNE*BLANK                                    CIR005
      *                                                                   CIR005
     C           ZFRFRQ    IFEQ *BLANK                                    CIR005
     C                     Z-ADDZFRMDT    ZFRNXD                          CIR005
     C                     Z-ADDZFRMDT    WIPBD                           CIR005
      *                                                                   CIR005
     C                     ELSE                                           CIR005
      *                                                                   CIR005
     C                     MOVELZFRFRQ    ZFREQ                           CIR005
     C                     Z-ADDZFRDAT    ZDAYNO                          CIR005
     C                     Z-ADDZFRBAS    ZMDAY                           CIR005
     C                     MOVE BJSLCD    ZLOC                            CIR005
     C                     EXSR ZFRQB3                                    CIR005
      *                                                                   CIR005
      ** Adjust date according to payment convention currencies           CIR005
      *                                                                   CIR005
     C                     CALL 'DLZIRP'                                  CIR005
     C                     PARM           ZDAYNO                          CIR005
     C                     PARM           PCURR                           CIR005
     C                     PARM WPDCN     PPDCN   2                       CIR005
     C                     PARM           ZLOC                            CIR005
     C                     PARM           BJDFIN                          CIR005
     C                     PARM           PAJDT   50                      CIR005
     C                     PARM           PBSDT   50                      CIR005
      *                                                                   CIR005
     C           PAJDT     IFGE ZFRMDT                                    CIR005
     C                     Z-ADDZFRMDT    ZFRNXD                          CIR005
     C                     Z-ADDZFRMDT    WIPBD                           CIR005
     C                     ELSE                                           CIR005
     C                     Z-ADDPAJDT     ZFRNXD                          CIR005
     C                     Z-ADDPBSDT     WIPBD                           CIR005
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
      ** Do not return adjusted date if it is equal to or earlier than    CIR005
      ** previous date (only happens for 'previous' date calculations);   CIR005
      ** instead use the base date                                        CIR005
      *                                                                   CIR005
     C           ZFRDAT    IFGE ZFRNXD                                    CIR005
     C                     Z-ADDPBSDT     ZFRNXD                          CIR005
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C                     ELSE                                           CIR005
     C*
     C                     CALL 'DLZFRQ'
     C                     PARM *BLANK    ZFRRTC  5
     C                     PARM           ZFRDAT  50       * DATE
     C                     PARM           ZFRFRQ  1        * FREQUENCY
     C                     PARM           ZFRBAS  20       * BASE DAY
     C                     PARM           ZFRSET  20       * SETT.METH.
     C                     PARM           ZFRCCY  3        * CURRENCY
     C                     PARM           ZSTCY   3        * Settl Ccy    CSW097
     C                     PARM           ZRCIP   1        * 'R' or 'P'   CSW097
     C                     PARM           ZFRNOS  2        * NOSTRO
     C                     PARM           ZFRMDT  50       * MAT.DATE
     C                     PARM           BKAPDT  50
     C                     PARM           BJSLCD  3
     C                     PARM           BJLCCY  3
     C                     PARM *ZERO     ZFRZBD  50       * BASE DATE    180466
     C                     PARM *ZERO     ZFRNXD  50       * NEXT DATE
     C           ZFRRTC    IFEQ '*ERR*'
     C                     EXSR *PSSR
     C                     END
      *                                                                   CIR005
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C                     ENDIF                                                              CIR018
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C** DETERMINE IF DEAL TO BE EXTRACTED
     C********************************************************************
     C           DETIFX    BEGSR
     C*
     C** LIVE DEALS ONLY MAY BE EXTRACTED
     C*
     C           RECI      IFNE 'D'
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
     C*
     C** SINGLE OR CROSS CURRENCY DEALS ONLY MAY BE EXTRACTED
     C*
     C           DTYP      IFNE 'RS'
     C           DTYP      ANDNE'RX'
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
     C*
     C** IGNORE IF BOUGHT OUT
     C*
     C           BOAM      IFNE *ZERO
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
     C*
     C** SINGLE OR CROSS CURRENCY SELECTED
     C*
     C           PSXCY     IFEQ 'S'                        * SINGLE
     C           DTYP      ANDEQ'RX'
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
     C           PSXCY     IFNE 'S'                        * CROSS
     C           DTYP      ANDEQ'RS'
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
      *                                                                                       CIR012
      ** Deal Selected                                                                        CIR012
      *                                                                                       CIR012
     C           PDEAL     IFNE *BLANK                                                        CIR012
     C           WDEAL     ANDNEDLNO                                                          CIR012
     C                     MOVEL'NO'      ##EXT                                               CIR012
     C                     GOTO EDETIF                                                        CIR012
     C                     END                                                                CIR012
      *                                                                                       CIR012
      ** If selection is by deal number, branch does not matter.                              CIR012
      *                                                                                       CIR012
     C           PDEAL     IFNE *BLANKS                                                       CIR012
     C                     MOVE *BLANKS   PBRCA                                               CIR012
     C                     END                                                                CIR012
     C*
     C** BRANCH SELECTED
     C*
     C           PBRCA     IFNE *BLANK
     C           PBRCA     ANDNEBRCA
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
     C*
     C** CURRENCY SELECTED - ONLY APPLICABLE IF SINGLE CURRENCY SELECTED
     C*
     C           PSXCY     IFEQ 'S'                        * SINGLE
     C           PCUCY     ANDNE*BLANK
     C           PCUCY     ANDNEUCUCY
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
     C*
     C** BOOK SELECTED
     C*
     C           PBOKC     IFNE *BLANK
     C           PBOKC     ANDNEBOKC
     C                     MOVEL'NO'      ##EXT
     C                     GOTO EDETIF
     C                     END
     C*
     C** COUNT OF DEALS EXTRACTED
     C*
     C                     ADD  1         RNORE
     C*
     C** SET UNREALIZED P/L ON DEAL TO ZERO IF PROGRAM RUN IN COB
     C*
     C********** PICCB     IFEQ 'C'                                                           228922
     C           PICCB     IFEQ '1'                                                           228922
     C           CAS004    ANDEQ'N'                                                           CAS004
     C           PICCB     ORNE 'I'                                                           CAS004
     C           CAS004    ANDEQ'Y'                                                           CAS004
     C           DLNO      CHAINDEALSUF              99    *
     C           *IN99     IFEQ '1'
     C                     MOVE 'DEALSDG 'DBFILE           ***************
     C                     MOVEL'010'     DBASE            * DB ERROR 10 *
     C                     MOVELDLNO      DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
     C                     Z-ADD*ZERO     UNRL
     C                     EXCPTUDEAL
     C                     END
     C*
     C           EDETIF    ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* AUDIT
     C********************************************************************
     C           AUDIT     BEGSR
      *
     C                     Z-ADDSFNUMU    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   WPARM   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ '1'
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no valid records read, Output "No Details" message.
      *
     C           RNORE     IFEQ *ZERO
     C                     WRITENODTLS
     C                     END
     C                     END
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C********************************************************************
     C* INITIAL PROCESSING
     C********************************************************************
     C           INIT      BEGSR
      *
      ***  Receive Parameter List:
      ***   PSXCY - ('S' or 'X'), Single or Cross Currency.
      ***   PICCB - ('I' or 'C'), Input Cycle or Close of Business.
      ***   PBRCA - Branch Code selected. Blank if no Branch selected.
      ***   PCUCY - Currency selected. Blank if no Currency selected.
      ***   PBOKC - Book Code selected. Blank if no Book selected.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSXCY   1
     C                     PARM           PICCB   1
     C                     PARM           PBRCA   3
     C                     PARM           PCUCY   3
     C                     PARM           PBOKC   2
     C                     PARM           SEQ     5
     C                     PARM           PDEAL   6                                           CIR012
      *                                                                                       CIR012
     C           *LIKE     DEFN DLNO      WDEAL                                               CIR012
     C                     MOVE PDEAL     WDEAL                                               CIR012
      *
      ** CALL ACCESS PROGRAM FOR BANK DETAILS
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR  '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'DBFILE            *************
     C                     MOVEL'001'     DBASE             *DBERROR 001*
     C                     MOVEL@OPTN     DBKEY             *************
     C                     EXSR *PSSR
     C                     END
      *
      **  Set Date Format Indicator
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
      *                                                                   CIR005
      ** Check if switchable feature CIR005 is switched on                CIR005
      *                                                                   CIR005
     C                     CALL 'AOSARDR0'                                CIR005
     C                     PARM *BLANKS   @RTCD                           CIR005
     C                     PARM '*VERIFY' @OPTN                           CIR005
     C                     PARM 'CIR005'  @SARD   6                       CIR005
     C           SCSARD    PARM SCSARD    DSFDY                           CIR005
      *                                                                   CIR005
     C           @RTCD     IFNE *BLANK                                    CIR005
     C           @RTCD     ANDNE'*NRF   '                                 CIR005
     C                     MOVEL'SCSARDPD'DBFILE           ************   CIR005
     C                     Z-ADD04        DBASE            * DBERR 04 *   CIR005
     C                     MOVEL'CIR005'  DBKEY            ************   CIR005
     C                     EXSR *PSSR                                     CIR005
     C                     ENDIF                                          CIR005
      *                                                                   CIR005
     C           @RTCD     IFEQ *BLANKS                                   CIR005
     C                     MOVE 'Y'       CIR005  1                       CIR005
     C                     ELSE                                           CIR005
     C                     MOVE 'N'       CIR005                          CIR005
     C                     ENDIF                                          CIR005
      *                                                                                       CIR017
     C                     CALL 'AOSARDR0'                                                    CIR017
     C                     PARM           @RTCD                                               CIR017
     C                     PARM '*VERIFY' @OPTN                                               CIR017
     C                     PARM 'CIR017'  @SARD                                               CIR017
      *                                                                                       CIR017
     C           @RTCD     IFEQ *BLANKS                                                       CIR017
     C                     MOVE 'Y'       CIR017  1                                           CIR017
     C                     ELSE                                                               CIR017
     C                     MOVE 'N'       CIR017                                              CIR017
     C                     ENDIF                                                              CIR017
      *                                                                                       CIR018
     C                     CALL 'AOSARDR0'                                                    CIR018
     C                     PARM           @RTCD                                               CIR018
     C                     PARM '*VERIFY' @OPTN                                               CIR018
     C                     PARM 'CIR018'  @SARD                                               CIR018
      *                                                                                       CIR018
     C           @RTCD     IFEQ *BLANKS                                                       CIR018
     C                     MOVE 'Y'       CIR018  1                                           CIR018
     C                     ELSE                                                               CIR018
     C                     MOVE 'N'       CIR018                                              CIR018
     C                     ENDIF                                                              CIR018
      *                                                                                       CAS001
      ** Check if switchable feature CAS001 is switched on                                    CAS001
      *                                                                                       CAS001
     C                     CALL 'AOSARDR0'                                                    CAS001
     C                     PARM *BLANKS   PRTCD   7                                           CAS001
     C                     PARM '*VERIFY' POPTN   7                                           CAS001
     C                     PARM 'CAS001'  PSARD   6                                           CAS001
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS001
      *                                                                                       CAS001
     C           PRTCD     IFNE *BLANK                                                        CAS001
     C           PRTCD     ANDNE'*NRF   '                                                     CAS001
     C                     MOVEL'SCSARDPD'DBFILE                                              CAS001
     C                     Z-ADD20        DBASE                                               CAS001
     C                     MOVEL'CAS001'  DBKEY                                               CAS001
     C                     EXSR *PSSR                                                         CAS001
     C                     ENDIF                                                              CAS001
      *                                                                                       CAS001
     C           PRTCD     IFEQ *BLANKS                                                       CAS001
     C                     MOVE 'Y'       CAS001  1                                           CAS001
     C                     ELSE                                                               CAS001
     C                     MOVE 'N'       CAS001                                              CAS001
     C                     ENDIF                                                              CAS001
      *                                                                                       CAS004
      ** Check if Hedge Accounting is installed                                               CAS004
      *                                                                                       CAS004
     C                     CALL 'AOSARDR0'                                                    CAS004
     C                     PARM *BLANKS   PRTCD   7                                           CAS004
     C                     PARM '*VERIFY' POPTN   7                                           CAS004
     C                     PARM 'CAS004'  PSARD                                               CAS004
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS004
      *                                                                                       CAS004
     C           PRTCD     IFEQ *BLANKS                                                       CAS004
     C                     MOVE 'Y'       CAS004  1                                           CAS004
     C                     ELSE                                                               CAS004
     C                     MOVE 'N'       CAS004                                              CAS004
     C           PRTCD     IFNE '*NRF   '                                                     CAS004
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS004
     C                     Z-ADD21        DBASE                                               CAS004
     C                     MOVEL'CAS004'  DBKEY                                               CAS004
     C                     EXSR *PSSR                                                         CAS004
     C                     ENDIF                                                              CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS005
      ** Check if Enhancements to CAS001, CAS002 and CAS004 is                                CAS005
      ** installed.                                                                           CAS005
      *                                                                                       CAS005
     C                     CALL 'AOSARDR0'                                                    CAS005
     C                     PARM *BLANKS   PRTCD                                               CAS005
     C                     PARM '*VERIFY' POPTN                                               CAS005
     C                     PARM 'CAS005'  PSARD                                               CAS005
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS005
      *                                                                                       CAS005
     C           PRTCD     IFEQ *BLANKS                                                       CAS005
     C                     MOVE 'Y'       CAS005  1                                           CAS005
     C                     ELSE                                                               CAS005
     C                     MOVE 'N'       CAS005                                              CAS005
     C           PRTCD     IFNE '*NRF   '                                                     CAS005
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS005
     C                     Z-ADD22        DBASE                                               CAS005
     C                     MOVEL'CAS005'  DBKEY                                               CAS005
     C                     EXSR *PSSR                                                         CAS005
     C                     ENDIF                                                              CAS005
     C                     ENDIF                                                              CAS005
      *                                                                                       CAS008
      ** Check if IAS39 Enhancement(CAS008) is installed.                                     CAS008
      *                                                                                       CAS008
     C                     CALL 'AOSARDR0'                                                    CAS008
     C                     PARM *BLANKS   PRTCD                                               CAS008
     C                     PARM '*VERIFY' POPTN                                               CAS008
     C                     PARM 'CAS008'  PSARD                                               CAS008
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS008
      *                                                                                       CAS008
     C           PRTCD     IFEQ *BLANKS                                                       CAS008
     C                     MOVE 'Y'       CAS008  1                                           CAS008
     C                     ELSE                                                               CAS008
     C                     MOVE 'N'       CAS008                                              CAS008
     C           PRTCD     IFNE '*NRF   '                                                     CAS008
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS008
     C                     Z-ADD23        DBASE                                               CAS008
     C                     MOVEL'CAS008'  DBKEY                                               CAS008
     C                     EXSR *PSSR                                                         CAS008
     C                     ENDIF                                                              CAS008
     C                     ENDIF                                                              CAS008
      *                                                                                       CIR014
      ** Check if Mark to Market Cross-Currency IRS is on                                     CIR014
      *                                                                                       CIR014
     C                     CALL 'AOSARDR0'                                                    CIR014
     C                     PARM *BLANKS   PRTCD                                               CIR014
     C                     PARM '*VERIFY' POPTN                                               CIR014
     C                     PARM 'CIR014'  PSARD                                               CIR014
     C           SCSARD    PARM SCSARD    DSFDY                                               CIR014
      *                                                                                       CIR014
     C           PRTCD     IFEQ *BLANKS                                                       CIR014
     C                     MOVE 'Y'       CIR014  1                                           CIR014
     C                     ELSE                                                               CIR014
     C                     MOVE 'N'       CIR014                                              CIR014
     C           PRTCD     IFNE '*NRF   '                                                     CIR014
     C                     MOVE 'SCSARDPD'DBFILE                                              CIR014
     C                     Z-ADD24        DBASE                                               CIR014
     C                     MOVELPSARD     DBKEY                                               CIR014
     C                     EXSR *PSSR                                                         CIR014
     C                     ENDIF                                                              CIR014
     C                     ENDIF                                                              CIR014
      *                                                                                       CIR014
     C           *LIKE     DEFN F1DLNO    KDLNOX                                              CIR014
      *
      ** CALL ACCESS PROGRAM FOR GENERAL LEDGER DETAILS
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR  '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDGELRPD'DBFILE            *************
     C                     MOVEL'002'     DBASE             *DBERROR 002*
     C                     MOVEL@OPTN     DBKEY             *************
     C                     EXSR *PSSR
     C                     END
     C*
     C** KEY LISTS FOR ACCESS OF DATE SCHEDULE
     C*
     C           DLDTSK    KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C                     KFLD           RCIP
     C                     KFLD           PRDT
     C           DLDTSP    KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C                     KFLD           RCIP
     C*
     C** KEY LISTS FOR ACCESS OF CASHFLOWS
     C*
     C           DLCFSK    KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C                     KFLD           PRDT
     C           DLCFSP    KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C*
     C** KEY LISTS FOR ACCESS OF PRINCIPAL CHANGES
     C*
     C           DLPCSK    KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C                     KFLD           PRDT
     C           DLPCSP    KLIST
     C                     KFLD           DLNO
     C                     KFLD           OTIN
     C*
     C** KEY LISTS FOR ACCESS OF DEAL ACTIVITY
     C*
     C           DLACTK    KLIST
     C                     KFLD           ACDLNO
     C                     KFLD           ACOTIN
     C                     KFLD           ACPRDT
     C*
     C           DLACTP    KLIST
     C                     KFLD           ACDLNO
     C                     KFLD           ACOTIN
      *                                                                                       CAS008
      ** Key lists for Yield Rate file                                                        CAS008
      *                                                                                       CAS008
     C           KYLDR     KLIST                                                              CAS008
     C                     KFLD           WYLDC                                               CAS008
     C                     KFLD           KCCY    3                                           CAS008
     C                     KFLD           KICM    1                                           CAS008
     C*
     C** INITIALIZE
     C*
     C                     Z-ADD*ZERO     RNORE   50
     C/COPY WNCPYSRC,DL1955C004
     C*
     C                     ENDSR
     C**************************************************************************
      /SPACE 5
     C/COPY ZSRSRC,ZFWDT                                                  100317
      /SPACE 5                                                            100317
     C/COPY ZSRSRC,ZACCH                                                  100317
     C/COPY WNCPYSRC,DL1955C005
      /SPACE 5                                                            100317
     C**************************************************************************
     C* *PSSR      - Error Subroutine                                 *
     C*****************************************************************
     C           *PSSR     BEGSR
     C*
     C** If not yet run, then do error processing
     C*
     C           WWRUN     IFNE 'Y'
     C                     MOVEL'Y'       WWRUN   1
     C                     MOVEL'DL1955'  DBPGM
     C           *NAMVAR   DEFN LDA       @LDA  256
     C           *LOCK     IN   @LDA
     C                     MOVELDBERR     @LDA
     C                     OUT  @LDA
     C                     SETON                     U7U8
     C                     DUMP
     C                     EXSR AUDIT
     C                     END
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C           PSSR9     ENDSR
     C*****************************************************************
     C/EJECT                                                              CIR005
     C/COPY ZSRSRC,ZFRQB3                                                 CIR005
     C/EJECT                                                              CIR005
     C/COPY ZSRSRC,ZCHKH                                                  CIR005
     C/EJECT                                                              CIR005
     C/COPY ZSRSRC,ZDATE1Z2                                               CIR005
     C/EJECT                                                              CIR005
     C/COPY ZSRSRC,ZDATE2Z2                                               CIR005
      /SPACE 5
     ODEALSUF E                UDEAL
     O                         UNRL
      /SPACE 5
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZFMD  -  NUMBER OF DAYS IN THE MONTH
312831303130313130313031
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
