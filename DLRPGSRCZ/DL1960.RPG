     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL MM Discount Factors Rebuild')
     F*****************************************************************
     F*                                                               *
     F*  Midas - FRA/IRS Module
     F*                                                               *
     F*  DL1960 - MM Discount Factors Rebuild                         *
     F*                                                               *
     F*  This program is called by DLC1960.  DLC1960 is called by     *
     F*  FDC0010 whenever the MM Interest Rates are maintained.       *
     F*  DL1960 reads through the periods for 1 to 10 years for each  *
     F*  currency on the MM Interest Rates file, calculates a         *
     F*  Discount Factor and writes this Discount Factor to DLMMDFPP. *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CPK014             Date 04Oct01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  Prev Amend No.  E28440             DATE  17SEP91             *
     F*                  S01328             DATE  08FEB91             *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CPK014 - Release 4 packaging.  Rename of field which is too  *
      *           long for OPM program.                               *
     F*  E28440 - Clear down of physical file, DLMMDFPP, will be      *
     F*           done in DL1960 to avoid error if file locked by     *
     F*           the FRA Book positions program (DL1905).            *
     F*  S01328 - FRA/IRS Revaluation - New Program                   *
     F*                                                               *
     F*****************************************************************
     F* INDEX OF INDICATORS
     F*
     F*   70    - End of file for DLINTRL1
     F*   71    - Error on file DLINTRL1
     F*   73    - Error/End of file on file DLMMDFL1                      E28440
     F*   U7/U8 - Database error
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     FDLINTRL1IF  E           K        DISK         KINFSR *PSSR
     F*DLMMDFL1O***E********************DISK         KINFSR *PSSR         E28440
     FDLMMDFL1UF  E           K        DISK         KINFSR *PSSR A        E28440
     E*
     E                    CPY@    1   1 80
     E** Array for Copyright Statement
     E*
     E                    PRDN       10  5 0
     E** Array for Period Dates (Midas Day Number) from DLINTRL1
     E*
     E                    PRD        10  2 0
     E** Array for Periods (1 to 10) from DLINTRL1
     E*
     E                    BR         10 11 7
     E** Array for Bid Rates from DLINTRL1
     E*
     E                    OR         10 11 7
     E** Array for Offer Rates from DLINTRL1
     E*
     E                    BRD        10 11 9
     E** Array for Bid Rates from DLINTRL1 divided by 100
     E*
     E                    ORD        10 11 9
     E** Array for Offer Rates from DLINTRL1 divided by 100
     E*
     E                    BDF        10 13 9
     E** Array for Bid Discount Factor
     E*
     E                    ODF        10 13 9
     E** Array for Offer Discount Factor
     E*
     E                    BIDF       10 11 9
     E** Array for Bid Inverse Discount Factor
     E*
     E                    OIDF       10 11 9
     E** Array for Offer Inverse Discount Factor
     E*
     E                    BCIF       10 13 9
     E** Array for Bid Cumulative Inverse Discount Factor
     E*
     E                    OCIF       10 13 9
     E** Array for Offer Cumulative Inverse Discount Factor
     E*
     I*****************************************************************
     I/EJECT
     I*****************************************************************
      *                                                                                       CPK014
     IFDINTRP0                                                                                CPK014
     I              HWTMESTMP                       HWTMST                                    CPK014
     IDBERR       DS                            256
     I** LOCAL DATA AREA FOR DATABASE ERROR
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C* SUBROUTINES USED                                              *
     C*                                                               *
     C*  LOAD  - Load Rates Arrays                                    *
     C*  CALC  - Calculate Discount Factors                           *
     C*  WRITE - Set up file fields and write record to DLMMDFL1      *
     C*  DELET - Delete all records from DLMMDFPP before Rebuilding   *   E28440
     C*  *PSSR  - Error Subroutine                                    *
     C*                                                               *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*            - Main Line Processing                             *
     C*                                                               *
     C* CALLS      -  LOAD    - Load Rates Arrays                     *
     C*               CALC    - Calculate Discount Factors            *
     C*                                                               *
     C* CALLED BY                                                     *
     C*                                                               *
     C* FLDS USED    *IN70 - End of file for DLINTRL1                 *
     C*              *IN71 - Error on DLINTRL1                        *
     C*                                                               *
     C*****************************************************************
     C                     MOVEACPY@      BIS@   80
     C*                                                                   E28440
     C** Delete all records from DLMMDFL1.  This is instead of a CLRPFM   E28440
     C** in the controlling CL because, if DLMMDFPP is in use (Book       E28440
     C** Positions Enquiry), the file cannot be accessed for CLRPFM.      E28440
     C                     EXSR DELET                                     E28440
     C*
     C** Read each record for each currency on the MM Interest Rates
     C** file, load the rates into the arrays, and calculate the
     C** Discount Factor.
     C                     READ DLINTRL1               7170
     C*
     C** if error on read then process an error
     C           *IN71     IFEQ '1'
     C                     MOVEL'001'     DBASE
     C                     MOVEL'DLINTRL1'DBFILE           *************
     C                     MOVEL*BLANKS   DBKEY            * DBERR 001 *
     C                     EXSR *PSSR                      *************
     C                     END
     C*
     C           *IN70     DOWEQ'0'
     C                     EXSR LOAD
     C                     EXSR CALC
     C**
     C** (this logical selects for Period = 'Y' (year) only)
     C                     READ DLINTRL1               7170
     C*
     C** if error on read then process an error
     C           *IN71     IFEQ '1'
     C                     MOVEL'002'     DBASE
     C                     MOVEL'DLINTRL1'DBFILE           *************
     C                     MOVEL*BLANKS   DBKEY            * DBERR 002 *
     C                     EXSR *PSSR                      *************
     C                     END
     C**
     C                     END
     C**
     C                     MOVEL'1'       *INLR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* LOAD      - Load Rates Arrays                                 *
     C*                                                               *
     C* CALLS      -                                                  *
     C*                                                               *
     C* CALLED BY    Main Line Processing                             *
     C*                                                               *
     C* FLDS USED    X     - Array Index                              *
     C*              PRDN  - Period Date (Midas Day Number)           *
     C*              PRD   - Period (1 to 10)                         *
     C*              BR    - Array for Bid Rate                       *
     C*              OR    - Array for Offer Rate                     *
     C*              BRD   - Array for Bid Rate divided by 100        *
     C*              ORD   - Array for Offer Rate divided by 100      *
     C*                                                               *
     C*****************************************************************
     C           LOAD      BEGSR
     C*
     C** Set up Currency file field - also used later for comparison
     C                     MOVELHWCCY     DFCCY
     C*
     C** set array index to zero
     C                     Z-ADD*ZERO     X       20
     C*
     C** do the following for each of the year periods (1 to 10) for
     C** this currency on DLINTRL1 :
     C           X         DOUEQ10
     C                     ADD  1         X
     C*
     C** move the Period Date and Period into arrays
     C                     MOVELHWPRDN    PRDN,X
     C                     MOVELHWPRD     PRD,X
     C*
     C** move the bid and offer rates from file into arrays
     C                     MOVELHWBRRT    BR,X
     C                     MOVELHWLDRT    OR,X
     C*
     C** divide each rate by 100 and move into appropriate arrays
     C           HWBRRT    DIV  100       BRD,X
     C           HWLDRT    DIV  100       ORD,X
     C*
     C** if all 10 of the year periods for this currency have not yet
     C** been read then read the MM Interest Rates file again.
     C           X         IFLT 10
     C                     READ DLINTRL1               7170
     C*
     C** if error on read, or end of file, or not the same currency
     C** as before (now saved in file field) then process an error
     C           HWCCY     IFNE DFCCY
     C           *IN70     OREQ '1'
     C           *IN71     OREQ '1'
     C                     MOVEL'003'     DBASE
     C                     MOVEL'DLINTRL1'DBFILE           *************
     C                     MOVELDFCCY     DBKEY            * DBERR 003 *
     C                     EXSR *PSSR                      *************
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C           LOAD9     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* CALC       - Calculate Discount Factors                       *
     C*                                                               *
     C* CALLS        WRITE - Set up file fields                       *
     C*                                 and write record to DLMMDFL1  *
     C*                                                               *
     C* CALLED BY    Main Line Processing                             *
     C*                                                               *
     C* FLDS USED    X     - Array Index                              *
     C*              Y     - Second Array Index (one less than X)     *
     C*              PRDN  - Period Date (Midas Day Number)           *
     C*              PRD   - Period (1 to 10)                         *
     C*              BR    - Array for Bid Rate                       *
     C*              OR    - Array for Offer Rate                     *
     C*              BRD   - Array for Bid Rate divided by 100        *
     C*              ORD   - Array for Offer Rate divided by 100      *
     C*              BDF   - Array for Bid Discount Factor            *
     C*              ODF   - Array for Offer Discount Factor          *
     C*              BIDF  - Array for Bid Inverse Discount Factor    *
     C*              OIDF  - Array for Offer Inverse Discount Factor  *
     C*              BCIF  - Array for Bid Cumulative Inv Disc Fact   *
     C*              OIDF  - Array for Offer Cumulative Inv Disc Fact *
     C*                                                               *
     C* CALCULATIONS :                                                *
     C*                                                               *
     C* a)  Bid Discount Factor for Year 1 :                          *
     C*                                                               *
     C*     BDF,(yr 1) = 1 + BRD,(yr 1)                               *
     C*                                                               *
     C* b)  Bid Discount Factor for Years 2 to 10 :                   *
     C*                                                               *
     C*                            1 + BRD,(yr X)                     *
     C*     BDF,(yr X) = ----------------------------------           *
     C*                   1 - {BRD,(yr X) x OCIF,(yr X-1)}            *
     C*                                                               *
     C*                          1       1       1           1        *
     C*  where OCIF,(yr X-1) = ----- + ----- + ----- + ------------   *
     C*                        ODF,1   ODF,2   .....   ODF,(yr X-1)   *
     C*                                                               *
     C* c)  Offer Discount Factor for Year 1 :                        *
     C*                                                               *
     C*     ODF,(yr 1) = 1 + ORD,(yr 1)                               *
     C*                                                               *
     C* d)  Offer Discount Factor for Years 2 to 10 :                 *
     C*                                                               *
     C*                            1 + ORD,(yr X)                     *
     C*     ODF,(yr X) = ----------------------------------           *
     C*                   1 - {ORD,(yr X) x BCIF,(yr X-1)}            *
     C*                                                               *
     C*                          1       1       1           1        *
     C*  where BCIF,(yr X-1) = ----- + ----- + ----- + ------------   *
     C*                        BDF,1   BDF,2   .....   BDF,(yr X-1)   *
     C*                                                               *
     C*****************************************************************
     C           CALC      BEGSR
     C*
     C** set array index to 1
     C                     Z-ADD1         X
     C*
     C** process for X = 1
     C** -----------------
     C*
     C** Bid Discount Factor = (Bid Rate/100) + 1
     C           BRD,1     ADD  1         BDF,1
     C*
     C** Offer Discount Factor = (Offer Rate/100) + 1
     C           ORD,1     ADD  1         ODF,1
     C*
     C** Bid Inverse Discount Factor = (1/Bid Discount Factor)
     C           1         DIV  BDF,1     BIDF,1
     C*
     C** Offer Inverse Discount Factor = (1/Offer Discount Factor)
     C           1         DIV  ODF,1     OIDF,1
     C*
     C** Bid Cumulative Inverse Discount Factor = Bid Inv Disc Fact
     C                     Z-ADDBIDF,1    BCIF,1
     C*
     C** Offer Cumulative Inverse Discount Factor = Offer Inv Disc Fact
     C                     Z-ADDOIDF,1    OCIF,1
     C*
     C** set up file fields and write record to Discount Factors file
     C                     EXSR WRITE
     C*
     C** Process for X = 2 to 10
     C** -----------------------
     C*
     C           X         DOUEQ10
     C*
     C** increment array index by 1; ensure second counter is one less
     C                     ADD  1         X
     C           X         SUB  1         Y       20
     C*
     C** Bid Discount Factor
     C**  BDF,X = (1 + BRD,X)/(1 - (BRD,X x OCIF,{X-1}))
     C*
     C           BRD,X     ADD  1         WWFLD1 119
     C           BRD,X     MULT OCIF,Y    WWFLD2 139
     C           1         SUB  WWFLD2    WWFLD3 119
     C           WWFLD1    DIV  WWFLD3    BDF,X
     C*
     C** Offer Discount Factor
     C**  ODF,X = (1 + ORD,X)/(1 - (ORD,X x BCIF,{X-1}))
     C*
     C           ORD,X     ADD  1         WWFLD1
     C           ORD,X     MULT BCIF,Y    WWFLD2
     C           1         SUB  WWFLD2    WWFLD3
     C           WWFLD1    DIV  WWFLD3    ODF,X
     C*
     C** Bid Inverse Discount Factor
     C**  BIDF,X = 1/BDF,X
     C*
     C           1         DIV  BDF,X     BIDF,X
     C*
     C** Offer Inverse Discount Factor
     C**  OIDF,X = 1/ODF,X
     C*
     C           1         DIV  ODF,X     OIDF,X
     C*
     C** Bid Cumulative Inverse Discount Factor
     C**  BCIF,X = BCIF,{X-1} + BIDF,X
     C*
     C           BCIF,Y    ADD  BIDF,X    BCIF,X
     C*
     C** Offer Cumulative Inverse Discount Factor
     C**  OCIF,X = OCIF,{X-1} + OIDF,X
     C*
     C           OCIF,Y    ADD  OIDF,X    OCIF,X
     C*
     C** set up file fields and write record to Discount Factors file
     C                     EXSR WRITE
     C*
     C                     END
     C*
     C           CALC9     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* WRITE      - Set up file fields and write record to DLMMDFL1  *
     C*                                                               *
     C* CALLS      -                                                  *
     C*                                                               *
     C* CALLED BY    CALC  - Calculate Discount Factors               *
     C*                                                               *
     C* FLDS USED    X     - Array Index                              *
     C*              PRDN  - Period Date (Midas Day Number)           *
     C*              PRD   - Period (1 to 10)                         *
     C*              BDF   - Array for Bid Discount Factor            *
     C*              ODF   - Array for Offer Discount Factor          *
     C*              BIDF  - Array for Bid Inverse Discount Factor    *
     C*              OIDF  - Array for Offer Inverse Discount Factor  *
     C*              BCIF  - Array for Bid Cumulative Inv Disc Fact   *
     C*              OIDF  - Array for Offer Cumulative Inv Disc Fact *
     C*                                                               *
     C*****************************************************************
     C           WRITE     BEGSR
     C*
     C*  Period Date (Midas Day Number)
     C                     Z-ADDPRDN,X    DFPRDN
     C*
     C*  Period (1 to 10)
     C                     Z-ADDPRD,X     DFPRD
     C*
     C*  Discount Factor
     C                     Z-ADDBDF,X     DFBDF
     C                     Z-ADDODF,X     DFODF
     C*
     C*  Inverse Discount Factor
     C                     Z-ADDBIDF,X    DFBIDF
     C                     Z-ADDOIDF,X    DFOIDF
     C*
     C*  Cumulative Inverse Discount Factor
     C                     Z-ADDBCIF,X    DFBCIF
     C                     Z-ADDOCIF,X    DFOCIF
     C*
     C                     WRITEDLMMDFP0
     C*
     C           WRIT9     ENDSR
     C*****************************************************************
     C/TITLE DELET                                                        E28440
     C*****************************************************************   E28440
     C*                                                               *   E28440
     C* DELET      - Delete all records from Discount Factors file    *   E28440
     C*                                                               *   E28440
     C* CALLS      -                                                  *   E28440
     C*                                                               *   E28440
     C* CALLED BY    Main Line Processing                             *   E28440
     C*                                                               *   E28440
     C* FLDS USED                                                     *   E28440
     C*                                                               *   E28440
     C*****************************************************************   E28440
     C           DELET     BEGSR                                          E28440
     C*                                                                   E28440
     C*  Set lower limits on the file, DLMMDFPP (Discount Factors)        E28440
     C           *LOVAL    SETLLDLMMDFL1                                  E28440
     C*                                                                   E28440
     C*  Read the file, delete if record found and repeat until EOF.      E28440
     C                     READ DLMMDFL1               7373               E28440
     C           *IN73     DOWEQ'0'                                       E28440
     C                     DELETDLMMDFL1                                  E28440
     C                     READ DLMMDFL1               7373               E28440
     C                     END                                            E28440
     C*                                                                   E28440
     C*  Reposition on file                                               E28440
     C           *LOVAL    SETLLDLMMDFL1                                  E28440
     C*                                                                   E28440
     C           DELE9     ENDSR                                          E28440
     C*****************************************************************   E28440
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* *PSSR      - Error Subroutine                                 *
     C*                                                               *
     C* FLDS USED    WWRUN - *PSSR already called - do not re-call    *
     C*                                                               *
     C*****************************************************************
     C           *PSSR     BEGSR
     C*
     C** if not yet processed then process for a dump
     C           WWRUN     IFNE 'Y'
     C                     MOVEL'Y'       WWRUN   1
     C*
     C                     MOVEL'DL1960'  DBPGM
     C*
     C           *NAMVAR   DEFN LDA       @LDA  256
     C           *LOCK     IN   @LDA
     C                     MOVELDBERR     @LDA
     C                     OUT  @LDA
     C*
     C                     MOVEL'1'       *INU7
     C                     MOVEL'1'       *INU8
     C                     MOVEL'1'       *INLR
     C                     DUMP
     C*
     C                     END
     C*
     C                     RETRN
     C*
     C           PSSR9     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
