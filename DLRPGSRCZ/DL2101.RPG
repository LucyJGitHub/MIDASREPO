     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Maturing fiduciary deposits by a/c off.')     *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL2101 - Maturing Fiduciary Deposits by Account Officer      *
      *                                                               *
      *  Function:  This program print the Maturing Fiduciary Deposits*
      *             by Account Officer                                *
      *                                                               *
      *  (phase(s)) This new report will be produced by Midas during  *
      *             the close of business                             *
      *                                                               *
      *  Called By: DLC2101                                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL096             Date 22Sep14               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     FDLDTEVPDIF  E           K        DISK         KINFSR *PSSR
      ** Takings events
      *
     FDEALS   IF  E           K        DISK         KINFSR *PSSR
      ** Deals
      *
     FDL2101AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      ** Maturing Fiduciary Deposits by Account Officer Audit
      *
     FDL2101P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      ** Maturing Fiduciary Deposits by Account Officer Report
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *                                                               *
      *   89  - End of File for PF/DLDTEVPD                           *
      *   90  - End of File for LF/DEALS                              *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Program Initialisation                              *
      *  SRPROC - Detail Processing                                   *
      *  SRRPT  - Process Report Lines                                *
      *                                                               *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFWDT  - Find next working day forward from a given date for *
      *           any particular currency                             *
      *  ZSEDIT - Edit an Amount                                      *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZHOLE
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
      /SPACE 3
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ** File Information Data Structure for DL2101P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for DL2101AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     I/COPY ZSRSRC,ZHOLI
     I/COPY ZSRSRC,ZHOLDS
     I/COPY ZSRSRC,ZSEDITZ2
      *
     IDSFDY     E DSDSFDY
      ** Dummy Data Structures used by Access Programs.
      *
     ISDDEAL    E DSSDDEALPD
      ** External data structures for Dealing ICD
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** External data structures for Branch Details
      *
     ISDACOF    E DSSDACOFPD
      ** External data structures for Account Officer Details
      *
     ISDCURR    E DSSDCURRPD
      ** External data structures for Currency Details
      *
     I            DS
      *
     I                                        1  21 ZOUT21
     I                                        1  20 ZOUT20
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      ** Output Audit Report and End Program.
      *
     C                     EXSR SRAUDT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AOBANKR0 Access Program                                      *
      *  ZFWDT  - Find next working day forward from a given date for *
      *           any particular currency                             *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           *** SRINIT ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PLEV    1
     C                     PARM           PENT    3
      *
      ** LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVELWOPTN     DBKEY            *************
     C                     Z-ADD001       DBASE            * DBERR 001 *
     C                     MOVEL'DL2101'  DBPGM            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Access Dealing ICD to retrieve the field 'Maturity Days to
      ** go for A/C Officer report (BNMDAO)
      *
     C**********           CALL 'AODEALR0'                                                    CGL029
     C                     CALL 'AODEALR1'                                                    CGL029
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*FIRST ' WOPTN
     C********** SDDEAL    PARM SDDEAL    DSFDY                                               CGL029
     C           SDDEAL    PARM SDDEAL    DSSDY                                               CGL029
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDDEALPD'DBFILE
     C                     MOVELWOPTN     DBKEY            *************
     C                     Z-ADD002       DBASE            * DBERR 002 *
     C                     MOVEL'DL2101'  DBPGM            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Determine period of reporting  WBEGDT , WENDDT
      ** Begin of period (WBEGDT)
      *
     C           BJRDNB    ADD  1         WBEGDT  50
      *
      ** End  of period  ( WENDDT )
      ** = Run date + 'Maturity Days to go for A/C officer'
      ** .. find next working day using number of days forward
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     Z-ADDBNMDAO    ZNRDYS
     C                     MOVE BJLCCY    ZCCY
     C                     MOVE *BLANKS   ZLOC
     C                     EXSR ZFWDT
     C                     Z-ADDZDYNBR    WENDDT  50
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         WCOUNT  50
      *
     C                     MOVE 'Y'       WFIRST
     C                     MOVE 'N'       WOPNP1
     C           *LIKE     DEFN BBACOC    WWACOC
     C                     MOVEL*HIVAL    WWACOC
     C           *LIKE     DEFN BBBRCD    WWBRCD
     C                     MOVEL*HIVAL    WWBRCD
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPROC
      *****************************************************************
      *                                                               *
      *  SRPROC - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  SRRPT  - Process Report Lines                                *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR                           *** SRPROC ***
      *
      ** Read first record from File.
      *
     C                     READ DLDTEVP0                 89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** Process records until end of file
      *
     C           *IN89     DOUEQ*ON
      *
      ** Selection of record
      *
     C           DTYP      IFEQ 'DT'
     C           DTYP      OREQ 'LT'
      *
      ** EDAT contains notice day, Maturity date must be calculated
      ** (WWEDAT)
      *
     C           EDAT      IFLT BJRDNB
     C           BJRDNB    ADD  EDAT      WWEDAT  50
     C                     ELSE
     C                     Z-ADDEDAT      WWEDAT
     C                     ENDIF
     C                     ENDIF
      *
     C           DTYP      IFEQ 'FT'
     C           EDAT      ANDGEWBEGDT
     C           EDAT      ANDLEWENDDT
     C           DTYP      OREQ 'DT'
     C           WWEDAT    ANDGEWBEGDT
     C           WWEDAT    ANDLEWENDDT
     C           DTYP      OREQ 'LT'
     C           WWEDAT    ANDGEWBEGDT
     C           WWEDAT    ANDLEWENDDT
      *
      ** RCF Processing for P1 File.
      *
     C           WFIRST    IFEQ 'Y'
     C           BBACOC    ORNE WWACOC
      *
     C           WFIRST    IFEQ 'Y'
     C                     MOVE 'N'       WFIRST  1
     C                     ELSE
     C                     CLOSEDL2101P1
     C                     ENDIF
      *
     C                     EXSR RCFP1
      *
      ** Account officer code, Account officer name
      *
     C           BBACOC    IFNE *BLANK
      *
     C                     CALL 'AOACOFR0'
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM BBACOC    WAFNB   4
     C           SDACOF    PARM SDACOF    DSFDY
      *
     C           WRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD003       DBASE            *************
     C                     MOVELBBACOC    DBKEY            * DBERR 003 *
     C                     MOVEL'SDACOFPD'DBFILE           *************
     C                     MOVEL'DL2101'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELAZACOC    PRACOC
     C                     MOVELAZACON    PRACON
      *
     C                     ELSE
      *
     C                     MOVEL*BLANK    PRACOC
     C                     MOVEL*BLANK    PRACON
     C                     ENDIF
      *
     C                     MOVELBBACOC    WWACOC
     C                     ENDIF
      *
      ** Process Report Lines.
      *
     C                     EXSR SRRPT
      *
     C                     ENDIF
      *
      ** Read next record.
      *
     C                     READ DLDTEVP0                 89
     C                     ENDDO
      *
      ** Write trailer (End of Report)
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRRPT
      *****************************************************************
      *                                                               *
      *  SRRPT  - Process Report Lines.                               *
      *                                                               *
      *  Called By: SRPROC subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  AOBRCHR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           SRRPT     BEGSR                           *** SRRPT  ***
      *
      ** Count records process
      *
     C                     ADD  1         WCOUNT
      *
      ** Branch code , Branch name
      *
     C           BBBRCD    IFNE WWBRCD
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM BBBRCD    WDSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           WRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD004       DBASE            *************
     C                     MOVELBBBRCD    DBKEY            * DBERR 004 *
     C                     MOVEL'SDBRCHPD'DBFILE           *************
     C                     MOVEL'DL2101'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     MOVELBBBRCD    WWBRCD
     C                     ENDIF
      *
     C                     MOVELA8BRCD    PRBRCD
     C                     MOVELA8BRNM    PRBRNM
      *
      ** Customer number
      *
     C                     MOVELBBCUST    PRCUST
      *
      ** Placed with
      *
     C           DLNO      CHAINDEALS                90
      *
     C           *IN90     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD005       DBASE            *************
     C                     MOVELDLNO      DBKEY            * DBERR 005 *
     C                     MOVEL'DEALS   'DBFILE           *************
     C                     MOVEL'DL2101'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           LNKDN     IFEQ 0
     C                     MOVEL'*ERROR*' PRLNKD
     C                     ELSE
     C           LNKDN     CHAINDEALS                90
     C           *IN90     IFEQ '1'
     C                     MOVEL'*ERROR*' PRLNKD
     C                     ELSE
     C                     MOVE *BLANK    PRLNKD
     C                     MOVELCNUM      PRLNKD
     C                     ENDIF
     C                     ENDIF
      *
      ** Deal number
      *
     C                     Z-ADDDLNO      PRDLNO
      *
      ** Value date
      *
     C                     Z-ADDOTHD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PROTHD
      *
      ** Maturity date
      *
     C           DTYP      IFEQ 'FT'
     C                     Z-ADDEDAT      ZDAYNO
     C                     ELSE
     C                     Z-ADDWWEDAT    ZDAYNO
     C                     ENDIF
      *
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PREDAT
      *
      ** Interest rate
      *
     C                     MOVE EXRT      ZFLD15 150
     C                     Z-ADD8         ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT20    PREXRT
      *
      ** Number of decimal place
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM ECCY      WKEY1   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           WRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE            *************
     C                     MOVELECCY      DBKEY            * DBERR 006 *
     C                     MOVEL'SDCURRPD'DBFILE           *************
     C                     MOVEL'DL2101'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Principal Amount
      *
     C                     MOVE EAMT      ZFLD15 150
     C                     Z-ADDA6NBDP    ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT20    PREAMT
      *
      ** Currency
      *
     C                     MOVELECCY      PRECCY
      *
      ** Interest Amount
      *
     C                     MOVE OTHA      ZFLD15 150
     C                     Z-ADDA6NBDP    ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT20    PROTHA
      *
      ** Commission Amount
      *
     C                     MOVE CMAMT     ZFLD15 150
     C                     Z-ADDA6NBDP    ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT20    PRCOAM
      *
      ** Total Amount
      *
     C           EAMT      ADD  OTHA      WWTOAM 150
     C                     SUB  CMAMT     WWTOAM 150
     C                     MOVE WWTOAM    ZFLD15 150
     C                     Z-ADDA6NBDP    ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT20    PRTOTA
      *
      ** Write out the record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, SRPROC, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR                           *** SRAUDT ***
      *
      ** RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           WCOUNT    IFEQ 0
     C                     WRITEHEADAU
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR ***
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     DUMP
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     OPEN DL2101P1
     C                     Z-ADDSFNUM1    ZSNUM   60
     C                     MOVE 'Y'       WOPNP1  1
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM *BLANK    SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZSEDITZ3
     C/COPY ZSRSRC,ZACCH
     C/COPY ZSRSRC,ZFWDT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
