     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL FI Loan  without Trust Agreem. or Taxable')   *
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DL2103 - Fiduciary Loans Without Trust Agreement or          *
      *           Taxable                                             *
      *                                                               *
      *  (phase(s)) This new report will be produced by Midas during  *
      *             the close of business (PMOD = C) and              *
      *             I/C on request (PMOD = I)                         *
      *             based on DL2102 (fiduciary deposit)               *
      *                                                               *
      *  Called By: DLC2103                                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL096             Date 22Sep14               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL019             Date 03Feb04               *
      *                 CDL018             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 CDL016             Date 03Feb04               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 CDL010             Date 02Aug02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL020 - Apply Base Rate at Value Date (Recompile)           *
      *  CDL019 - Allow Base Rate Changes on Fixed Deposits/Loans     *
      *           (Recompile)                                         *
      *  CDL018 - Multiple Settlement Accounts on Loans/Deposits      *
      *           (Recompile)                                         *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *           (Recompile)                                         *
      *  CDL016 - Automatic rollover of Fixed Term Loans/Deposits     *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CDL010 - Prevent last user that actioned the trade from      *
      *           authorising it.Recompile due to changes in MMDELDPP.*
      *  CDL006 - Dealing Fiduciary.                                  *
      *                                                               *
      *****************************************************************
     FDLDTKGPDIF  E           K        DISK         KINFSR *PSSR
      ** Takings events
      *
     FMMDEALLLIF  E           K        DISK         KINFSR *PSSR
      ** Loans/Deposits file
      *
     F            MMDENSP0                          KIGNORE
     F            MMDENBP0                          KIGNORE
     FDL2103AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      ** Fiduciary Loans without Trust Agreement or taxable.
      *
     FDL2103P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      ** Fiduciary Loans without Trust Agreement or taxable.
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *   41  - narrative 1 isn't blank  and must be print            *
      *   42  - narrative 2 isn't blank  and must be print            *
      *   43  - narrative 3 isn't blank  and must be print            *
      *   44  - narrative 4 isn't blank  and must be print            *
      *   65  - Break on customer                                     *
      *   67  - No narrative with trust agreement '4'                 *
      *   68  - Chain to MMDEALLL                                     *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Program Initialisation                              *
      *  SRPROC - Detail Processing                                   *
      *  SRREPT - Process Report Lines                                *
      *  SRDATE - Process Report Lines                                *
      *                                                               *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFWDT  - Find next working day forward from a given date for *
      *           any particular currency                             *
      *  ZSEDIT - Edit an Amount                                      *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZHOLE
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ** File Information Data Structure for DL2103P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for DL2103AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     I/COPY ZSRSRC,ZHOLI
     I/COPY ZSRSRC,ZHOLDS
     I/COPY ZSRSRC,ZSEDITZ2
      *
     IDSFDY     E DSDSFDY
      ** Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** External data structures for Branch Details
      *
     ISDACOF    E DSSDACOFPD
      ** External data structures for Account Officer Details
      *
     ISDCURR    E DSSDCURRPD
      ** External data structures for Currency Details
      *
     ISDDEAL    E DSSDDEALPD
      ** External data structures for Currency Details
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      ** Output Audit Report and End Program.
      *
     C                     EXSR SRAUDT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *  AODEALR0 Access Program                                      *
      *  ZFWDT  - Find next working day forward from a given date for *
      *           any particular currency                             *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           *** SRINIT ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PMOD    1
     C                     PARM           PSEQ    5
     C                     PARM           PLEV    1
     C                     PARM           PENT    3
      *
      ** Setup LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVELWOPTN     DBKEY            *************
     C                     Z-ADD001       DBASE            * DBERR 001 *
     C                     MOVEL'DL2103'  DBPGM            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'Y'
     C                     MOVE *ON       *IN37
     C                     ENDIF
      *
      ** Access Dealing ICD to retrieve the field 'Maturity Days to
      ** go for A/C Officer report (BNMDAO)
      *
     C**********           CALL 'AODEALR0'                                                    CGL029
     C                     CALL 'AODEALR1'                                                    CGL029
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*FIRST ' WOPTN
     C********** SDDEAL    PARM SDDEAL    DSFDY                                               CGL029
     C           SDDEAL    PARM SDDEAL    DSSDY                                               CGL029
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDDEALPD'DBFILE
     C                     MOVELWOPTN     DBKEY            *************
     C                     Z-ADD002       DBASE            * DBERR 002 *
     C                     MOVEL'DL2103'  DBPGM            *************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Determine period of reporting  WBEGDT , WENDDT
      ** Begin of period ( WBEGDT )
      *
     C           BJRDNB    ADD  1         WBEGDT  50
      *
      ** End  of period  ( ENNDAT )
      ** = Run date + 'Maturity Days to go for A/C officer'
      ** .. find next working day using number of days forward
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     Z-ADDBNMDAO    ZNRDYS
     C                     MOVE BJLCCY    ZCCY
     C                     MOVE *BLANKS   ZLOC
     C                     EXSR ZFWDT
     C                     Z-ADDZDYNBR    WENDDT  50
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RCOUNT  50
     C                     Z-ADD0         RBRANC  50
      *
     C                     MOVE 'Y'       WFIRST
     C           *LIKE     DEFN BBACOC    WWACOC
     C                     MOVEL*HIVAL    WWACOC
     C           *LIKE     DEFN BBBRCD    WWBRCD
     C                     MOVEL*HIVAL    WWBRCD
     C           *LIKE     DEFN BBCUST    WWCUST
     C                     MOVEL*HIVAL    WWCUST
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPROC
      *****************************************************************
      *                                                               *
      *  SRPROC - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:                                                       *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  SRREPT - Process Report Lines                                *
      *  SRDATE - Process Report Lines                                *
      *  SRREND - Write End of Report                                 *
      *  RCFP1  - Subroutine to register P1 printer file via RCF.     *
      *  AOACOFR0 Access Program                                      *
      *  AOBRCHR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR                           *** SRPROC ***
      *
      ** Read first record from File.
      *
     C                     READ DLDTKGP0                 89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** Do Until End of File.
      *
     C           *IN89     DOUEQ*ON
      *
     C           WWCUST    IFNE BBCUST
      *
      ** Customer's break
      *
     C                     MOVE *ON       *IN65
     C                     MOVELBBCUST    WWCUST
     C                     MOVELBBCUST    PRCUST
     C                     MOVELBBCSSN    PRCSSN
     C                     ENDIF
      *
      ** Retrieve Trust Agreement Status (HKFTST) and restriction
      ** narrative HKFRN1,HKFRN2,HKFRN3,HKFRN4
      *
     C           DLNO      CHAINMMDEALLL             68
      *
     C           *IN68     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD003       DBASE            *************
     C                     MOVELDLNO      DBKEY            * DBERR 003 *
     C                     MOVEL'MMDELDPP'DBFILE           *************
     C                     MOVEL'DL2103'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Rectriction narrative must be written only if it isn't blank
      *
     C           HKFRN1    COMP *BLANK               4141
     C           HKFRN2    COMP *BLANK               4242
     C           HKFRN3    COMP *BLANK               4343
     C           HKFRN4    COMP *BLANK               4444
      *
     C                     MOVE *OFF      *IN67
      *
     C           HKFTST    IFEQ '4'
     C           *IN41     ANDEQ*OFF
     C           *IN42     ANDEQ*OFF
     C           *IN43     ANDEQ*OFF
     C           *IN44     ANDEQ*OFF
      *
      ** For trust agreement '4' must at least have one narrative,
      ** else *IN67 ON. The message 'ONE NARRATIVE MUST BE ENTERED
      ** WITH THIS TRUST' is written.
      *
     C                     MOVE *ON       *IN67
     C                     ENDIF
      *
      ** If Trust Agreement isn't 3 or 4(No trust agrement):all deals
      ** in margin's date must be selected
      ** Else only taxable deals (TAXI=Y) in margin's date must be
      ** selected.
      *
     C           HKFTST    IFEQ '3'
     C           HKFTST    OREQ '4'
      *
     C           *IN67     IFEQ *OFF
      *
      ** Deal with trust agreement
      *
     C           TAXI      IFEQ 'N'
     C                     READ DLDTKGP0                 89
     C                     ITER
     C                     ENDIF
      *
      ** Rectriction narrative mustn't  be written for taxable deals
      *
     C                     MOVE *OFF      *IN41
     C                     MOVE *OFF      *IN42
     C                     MOVE *OFF      *IN43
     C                     MOVE *OFF      *IN44
     C                     ENDIF
     C                     ENDIF
      *
      ** Selection on date
      *
     C           PMOD      IFEQ 'C'
     C                     EXSR SRDATE
     C           WWSEL     IFEQ 'N'
     C                     READ DLDTKGP0                 89
     C                     ITER
     C                     ENDIF
     C                     ENDIF
      *
      ** Record is selected
      *
     C                     ADD  1         RCOUNT
      *
      ** Branch's break
      *
     C           WFIRST    IFEQ 'Y'
     C           BBBRCD    ORNE WWBRCD
      *
     C           WFIRST    IFEQ 'Y'
     C                     MOVE 'N'       WFIRST  1
     C                     ELSE
     C                     EXSR SRREND
     C                     Z-ADD0         RBRANC
     C                     ENDIF
      *
     C                     ADD  1         RBRANC
      *
      ** Change of page
      *
     C                     MOVE 'Y'       WRTHEA  1
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM BBBRCD    WDSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           WRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD004       DBASE            *************
     C                     MOVELBBBRCD    DBKEY            * DBERR 004 *
     C                     MOVEL'SDBRCHPD'DBFILE           *************
     C                     MOVEL'DL2103'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBBRCD    WWBRCD
     C                     MOVELA8BRCD    PRBRCD
     C                     MOVELA8BRNM    PRBRNM
      *
     C                     EXSR RCFP1
      *
     C                     ENDIF
      *
      ** Account officer's break
      *
     C           BBACOC    IFNE WWACOC
     C           BBACOC    IFNE *BLANK
     C                     CALL 'AOACOFR0'
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM BBACOC    WAFNB   4
     C           SDACOF    PARM SDACOF    DSFDY
      *
     C           WRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD005       DBASE            *************
     C                     MOVELBBACOC    DBKEY            * DBERR 005 *
     C                     MOVEL'SDACOFPD'DBFILE           *************
     C                     MOVEL'DL2103'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELAZACOC    PRACOC
     C                     MOVELAZACON    PRACON
      *
     C                     ELSE
      *
     C                     MOVEL*BLANK    PRACOC
     C                     MOVEL*BLANK    PRACON
     C                     ENDIF
      *
      ** Change of page
      *
     C                     MOVE 'Y'       WRTHEA
     C                     MOVE BBACOC    WWACOC
     C                     ENDIF
      *
     C                     Z-ADD1         RQDLN1
      *
     C           *IN65     IFEQ *ON
      *
      ** Customer's break
      *
     C                     ADD  2         RQDLN1
     C                     ENDIF
      *
      ** Special calculation due to the fact that the restriction
      ** narrative are optional.Nb: the fisrt narrative is write on
      ** the deal's line
      *
     C                     Z-ADD0         WWDLN1  10
      *
     C           *IN41     IFEQ *ON
     C                     ADD  1         WWDLN1
     C                     ENDIF
      *
     C           *IN42     IFEQ *ON
     C                     ADD  1         WWDLN1
     C                     ENDIF
      *
     C           *IN43     IFEQ *ON
     C                     ADD  1         WWDLN1
     C                     ENDIF
      *
     C           *IN44     IFEQ *ON
     C                     ADD  1         WWDLN1
     C                     ENDIF
      *
     C           WWDLN1    IFNE 0
     C                     SUB  1         WWDLN1
     C                     ADD  WWDLN1    RQDLN1
     C                     ENDIF
      *
     C           OFLLN1    SUB  PRTLN1    DIFLN1
      *
     C           WRTHEA    IFEQ 'Y'
     C           DIFLN1    ORLE RQDLN1
      *
      ** Repeat of customer information
      *
     C                     MOVE *ON       *IN65
     C                     WRITEHEADP1
     C                     MOVE ' '       WRTHEA
     C                     ENDIF
      *
      ** Process Report Lines.
      *
     C                     EXSR SRREPT
      *
     C           *IN65     IFEQ *ON
     C                     MOVE *OFF      *IN65
     C                     ENDIF
      *
      ** Read next record.
      *
     C                     READ DLDTKGP0                 89
      *
      ** End of Valid Records selection
      *
     C                     ENDDO
      *
      ** End of report for branch ?
      *
     C           RBRANC    IFNE 0
     C                     EXSR SRREND
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRREPT
      *****************************************************************
      *                                                               *
      *  SRREPT - Process Report Lines.                               *
      *                                                               *
      *  Called By: SRPROC Subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  ZDATE2  - Date Conversion                                    *
      *  ZSEDIT  - Edit Numeric fields                                *
      *  AOCURRR0 Access Program                                      *
      *  AOBRCHR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           SRREPT    BEGSR                           *** SRREPT ***
      *
      ** Customer number
      *
     C                     MOVELBBCUST    PRCUST
      *
      ** Customer shortname
      *
     C                     MOVELBBCSSN    PRCSSN
      *
      ** Deal number
      *
     C                     Z-ADDDLNO      PRDLNO
      *
      ** Value date
      *
     C                     Z-ADDVDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PRVDAT
      *
      ** Maturity date
      *
     C           DTYP      IFEQ 'LT'
     C           MDAT      ANDNE0
     C                     Z-ADDMDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PRMDAT
     C                     ELSE
     C                     MOVEL*BLANKS   PRMDAT
     C                     MOVELNOTD      PRMDAT
     C                     ENDIF
      *
      ** Interest rate
      *
     C                     MOVE INTR      ZFLD15 150
     C                     Z-ADD7         ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PRINTR
      *
      ** Number of decimal place
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM CCY       WKEY1   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           WRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE            *************
     C                     MOVELCCY       DBKEY            * DBERR 006 *
     C                     MOVEL'SDCURRPD'DBFILE           *************
     C                     MOVEL'DL2103'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Principal Amount
      *
     C                     MOVE PCPL      ZFLD15 150
     C                     Z-ADDA6NBDP    ZDECS   10
     C                     MOVE 'J'       ZECODE  1
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    PRPCPL
      *
      ** Currency
      *
     C                     MOVELCCY       PRCCY
      *
      ** Trust agreement status
      *
     C                     MOVELHKFTST    PRTRAS
      *
      ** Restriction narrative
      *
     C           *IN41     IFEQ *ON
     C                     MOVELHKFRN1    PRNAR1
     C                     ENDIF
      *
     C           *IN42     IFEQ *ON
     C                     MOVELHKFRN2    PRNAR2
     C                     ENDIF
      *
     C           *IN43     IFEQ *ON
     C                     MOVELHKFRN3    PRNAR3
     C                     ENDIF
      *
     C           *IN44     IFEQ *ON
     C                     MOVELHKFRN4    PRNAR4
     C                     ENDIF
      *
      ** Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRDATE
      *****************************************************************
      *                                                               *
      *  SRDATE - Process Report Lines.                               *
      *                                                               *
      *  Called By: SRPROC Subroutine                                 *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRDATE    BEGSR                           *** SRDATE ***
      *
     C                     MOVE 'N'       WWSEL   1
      *
     C           DTYP      IFEQ 'LT'
     C           MDAT      ANDEQ0
      *
      ** Maturity date must be calculated (WWMDAT)
      *
     C           BJRDNB    ADD  NOTD      WWMDAT  50
     C           WWMDAT    IFGE WBEGDT
     C           WWMDAT    ANDLEWENDDT
     C                     MOVE 'Y'       WWSEL
     C                     ENDIF
     C                     ENDIF
      *
     C           DTYP      IFEQ 'LT'
     C           MDAT      ANDNE0
     C           MDAT      IFGE WBEGDT
     C           MDAT      ANDLEWENDDT
     C                     MOVE 'Y'       WWSEL
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRREND
      *****************************************************************
      *                                                               *
      *  SRREND - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: SRPROC Subroutine                                 *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRREND    BEGSR                           *** SRREND ***
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Total Format.
      *
     C                     WRITETRAILP1
     C                     CLOSEDL2103P1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, SRPROC, *PSSR                    *
      *                                                               *
      *  Calls:                                                       *
      *  RCFAU   - Subroutine to register AU printer file via RCF     *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR                           *** SRAUDT ***
      *
      ** RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITEHEADAU
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     OPEN DL2103P1
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      WSEQ    5
     C                     PARM BBBRCD    WSENTY  3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      WSEQ
     C                     PARM *BLANKS   WSENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZSEDITZ3
     C/COPY ZSRSRC,ZACCH
     C/COPY ZSRSRC,ZFWDT
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
