     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Outstanding Deals by Customer Prompt')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Dealing Module
     F*                                                               *
     F*   DL2485  - OUTSTANDING DEALS BY CUSTOMER PROMPT PROGRAM      *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 BUG11439           Date 04Jun06               *
      *                 CDL038             Date 10May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CDL021             Date 005feB4               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSD007             Date 28Aug00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                  DT0204            Date 10Jun91               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  BUG11439 - Incorrect processing if shortname entered.        *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *   CDL021 - Reports by Deal Type                               *
     F*   CSD007 - Customer Closing.                                  *
     F*   DT0204 - New program produced to standardise RCF processing *
     F*            for report DL2480                                  *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FDL2485DFCF  E                    WORKSTN
     FFDDTSTL0IF  E           K        DISK                                                   CDL021
     F*
     F*****************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F*       03      COMMAND KEY 3 TAKEN                             *
     F*       10      COMMAND KEY 10 TAKEN                            *
     F*       12      COMMAND KEY 12 TAKEN                            *
     F*       30      INPUT VALUE FOR CUSTOMER INVALID                *
     F*       31      INPUT VALUE FOR LIVE DEALS INDICATOR INVALID    *
     F*       32      INPUT VALUE FOR MATURE DEALS INDICATOR INVALID  *
     F*       33      INPUT VALUE FOR DEAL TYPE / SUBTYPE             *
     F*       40      GENERAL ERROR FIELD INDICATOR                   *
     F*       45      PROTECT SCREEN FIELDS TO PREVENT INSERT/AMEND   *
     F*       46      ACTION CODE DELETE, ALLOW F10                   *
     F*       50      DISPLAY ERROR MESSAGE SUBFILE END               *
     F*       65      EXTENDED DEAL SUB TYPES SWITCHED ON             *                       CDL038
     F*       70      SUBFILE DISPLAY CONTROL INDICATOR               *
     F*       71      SUBFILE END INDICATOR                           *
     F*     U7+U8     DATABASE ERROR                                  *
     F*                                                               *
     F*     STANDARD PROGRAMS USED                                    *
     F*                                                               *
     F*     ZA0240  - SEND MESSAGE TO PROGRAM MESSAGE QUEUE           *
     F*     ZA0250  - CLEAR PROGRAM MESSAGE QUEUE                     *
     F*     ZA0440  - SEND MESSAGE TO PROGRAM MESSAGE QUEUE WITH DATA *
     F*****************************************************************
     E                    CPY@    1   1 80
     E/EJECT
     E/COPY WNCPYSRC,DL2485E001
     I*
     ISCSARD    E DSSCSARDPD                                              CSD007
      ** External Data Structure for Switchable Features                  CSD007
      *                                                                   CSD007
     ISDCUST    E DSSDCUSTPD
     I** EXTERNAL DS FOR CUSTOMER DETAILS
     I*
     IDSFDY     E DSDSFDY                                                 CSD007
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                CSD007
     I*                                                                   CSD007
     IDSSDY     E DSDSSDY
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     I*
     IDSLDY     E DSDSLDY                                                 CSD007
      ** Third DS for access programs, very long data structure           CSD007
      *                                                                   CSD007
     IPSDS       SDS
     I*    PROGRAM STATUS DATA STRUCTURE
     I                                     *PROGRAM PGMNM
     I                                      244 253 WSID
     I                                      254 263 USRID
     I*
     ILDA        UDS                            256                       CSD007
     I                                      134 141 DBFILE                CSD007
     I                                      142 170 DBKEY                 CSD007
     I                                      171 180 DBPGM                 CSD007
     I                                      181 1830DBASE                 CSD007
      *                                                                   CSD007
     I@PARM       DS                            100
     I* ONE PARM FOR CUSTOMER,LIVE AND MATURE DEALS INDICATOR
     I                                        1   6 OCUST
     I                                        7   7 OLIV
     I                                        8   8 OMAT
     I                                        9  10 TYP1                                      CDL021
     I                                       11  12 TYP2                                      CDL021
     I                                       13  16 TYP3                                      CDL038
     I*
     IRUNDT       DS                             13
     I                                        1   7 SRUNA
     I/EJECT
     C*
     C**   SET UP COPYRIGHT PARAMETER
     C*
     C                     MOVE *BLANK    TYP1                                                CDL021
     C                     MOVE *BLANK    TYP2                                                CDL021
     C                     MOVE *BLANK    TYP3                                                CDL038
     C                     MOVEACPY@      BIS@   80
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
     C/COPY WNCPYSRC,DL2485C001
     C*
     C**   RECEIVE PARAMETERS FROM CALLING PROGRAM
     C*
     C           *ENTRY    PLIST
     C                     PARM           @RSEQ   5
     C                     PARM           @RLEV   1
     C                     PARM           @RENT   3
     C                     PARM           @PARM
     C                     PARM           @RACT   1
     C                     PARM           @RCMD   1
     C*
     C**   INITIALISE SCREEN MESSAGE QUEUE AND SET ON SUBFILE END IND.
     C*
     C                     SETON                     50
     C                     MOVEL'*'       PGMQ
      *                                                                   CSD007
      ** Access Switchable Features file and check if CSD007              CSD007
      ** is switched on or not.                                           CSD007
      *                                                                   CSD007
     C                     CALL 'AOSARDR0'                                CSD007
     C                     PARM *BLANKS   @RTCD   7                       CSD007
     C                     PARM '*VERIFY' @OPTN   7                       CSD007
     C                     PARM 'CSD007'  @SARD   6                       CSD007
     C           SCSARD    PARM SCSARD    DSFDY                           CSD007
      *                                                                   CSD007
     C           @RTCD     IFEQ *BLANKS                                   CSD007
     C                     MOVE 'Y'       CSD007  1                       CSD007
     C                     ELSE                                           CSD007
     C                     MOVE 'N'       CSD007                          CSD007
      *                                                                   CSD007
      ** Database Error, if Return Code is not blanks or *NRF             CSD007
      *                                                                   CSD007
     C           @RTCD     IFNE '*NRF   '                                 CSD007
     C                     MOVEL'DL2485  'DBPGM                           CSD007
     C                     MOVEL'SCSARDPD'DBFILE                          CSD007
     C                     Z-ADD01        DBASE                           CSD007
     C                     MOVEL'CSD007  'DBKEY                           CSD007
     C                     MOVE *ON       *INU7                           CSD007
     C                     MOVE *ON       *INU8                           CSD007
     C                     MOVE *ON       *INLR                           CSD007
     C                     EXSR *PSSR                                     CSD007
     C                     ENDIF                                          CSD007
     C                     ENDIF                                          CSD007
     C*                                                                   CDL038
     C** DETERMINE IF CDL038 IS ON (EXTENDED DEAL SUB-TYPE)               CDL038
     C*                                                                   CDL038
     C                     CALL 'AOSARDR0'                                CDL038
     C                     PARM *BLANKS   @RTCD   7                       CDL038
     C                     PARM '*VERIFY' @OPTN   7                       CDL038
     C                     PARM 'CDL038'  @SARD   6                       CDL038
     C           SCSARD    PARM SCSARD    DSFDY                           CDL038
     C*                                                                   CDL038
     C           @RTCD     IFEQ *BLANKS                                   CDL038
     C                     MOVE 'Y'       CDL038  1                       CDL038
     C                     MOVE *ON       *IN65                           CDL038
     C                     ELSE                                           CDL038
     C                     MOVE 'N'       CDL038                          CDL038
     C                     MOVE *OFF      *IN65                           CDL038
     C                     END                                            CDL038
     C*
     C**   INITIALISE SCREEN FIELDS
     C**   FOR ACTION CODES I,A ALLOW INPUT TO SCREEN FIELDS
     C*
     C           @RACT     IFEQ 'I'
     C                     MOVE *BLANK    SCUST
     C                     MOVE *BLANK    SLIV
     C                     MOVE *BLANK    SMAT
     C                     MOVE *BLANK    TYP1                                                CDL021
     C                     MOVE *BLANK    TYP2                                                CDL021
     C                     MOVE *BLANK    TYP3                                                CDL038
     C                     ELSE
     C*
     C**   FOR ACTION CODES E,D DO NOT ALLOW INPUT TO SCREEN FIELDS
     C**   FOR ACTION CODES D ALLOW F10
     C*
     C                     MOVELOCUST     SCUST
     C                     MOVELOLIV      SLIV
     C                     MOVELOMAT      SMAT
     C           @RACT     COMP 'D'                      46
     C           @RACT     COMP 'D'                      45
     C  N45      @RACT     COMP 'E'                      45
     C                     END
     C*
     C**   LOOP UNTIL EITHER A COMMAND KEY HIT OR VALID SCREEN FIELDS
     C**   ENTERED
     C*
     C           *IN12     DOUEQ'1'
     C           *IN03     OREQ '1'
     C           *IN10     OREQ '1'
     C           *IN40     OREQ '0'
     C*
     C** OUTPUT SCREEN
     C*
     C                     EXFMTDL2485F0
     C*
     C**   IF F3 IS PRESSED, RETURN TO CALLING PROGRAM
     C*
     C           *IN03     IFEQ '1'
     C                     MOVE 'E'       @RCMD
     C                     ELSE
     C*
     C**   IF F12 IS PRESSED, RETURN TO CALLING PROGRAM
     C*
     C           *IN12     IFEQ '1'
     C                     MOVE 'F'       @RCMD
     C                     ELSE
     C*
     C**   IF F10 IS PRESSED, RETURN TO CALLING PROGRAM
     C*
     C           *IN10     IFEQ '1'
     C                     MOVE 'D'       @RCMD
     C                     ELSE
     C*
     C**   RESET INDICATORS AND ERROR MESSAGE QUEUE
     C*
     C           *IN40     IFEQ '1'
     C                     CALL 'ZA0250'
     C                     WRITEDL2485C0
     C                     END
     C*
     C                     SETOF                     303132
     C                     SETOF                     40
     C*
     C**   VALIDATION OF INPUT FIELDS
     C*
     C                     EXSR VALID
     C*
     C**   IF NO ERRORS ON INPUT THEN EXIT PROGRAM ELSE LOOP BACK
     C*
     C           *IN40     IFEQ '0'
     C                     MOVELSCUST     OCUST
     C                     MOVELSLIV      OLIV
     C                     MOVELSMAT      OMAT
     C                     ELSE
     C                     WRITEDL2485C0
     C*
     C                     END
     C*
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**   VALID SUBROUTINE TO VALIDATE INPUT FIELDS
     C*
     C           VALID     BEGSR                           ** VALID SR **
     C*
     C**   VALIDATE CUSTOMER NUMBER/SHORTNAME
     C*
     C                     MOVELSCUST     @CKEY
     C                     CLEARSDCUST                                    CDL038
     C***********          CALL 'AOCUSTR0'                                CSD007
     C                     CALL 'AOCUSTR2'                                CSD007
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @CKEY  10
     C                     PARM *BLANKS   @KYST   7
     C***********SDCUST    PARM SDCUST    DSSDY                           CSD007
     C           SDCUST    PARM SDCUST    DSLDY                           CSD007
     C*
      ** If CSD007 is on, allow processing of a closed record (return     CSD007
      ** code '*NRF' and date deleted equal to zero).                     CSD007
      *                                                                   CSD007
     C           CSD007    IFEQ 'Y'                                       CSD007
     C           @RTCD     ANDEQ'*NRF'                                    CSD007
     C           BBCUST    ANDNE*BLANK                                    CDL038
     C           BBDTDL    ANDEQ*ZERO                                     CSD007
     C           @KYST     ANDEQ'*CNUM  '                                                   BUG11439
     C           CSD007    OREQ 'Y'                                                         BUG11439
     C           @RTCD     ANDEQ'*NRF'                                                      BUG11439
     C           BBDTDL    ANDEQ*ZERO                                                       BUG11439
     C           @KYST     ANDEQ'*CSHT  '                                                   BUG11439
      *                                                                   CSD007
     C                     ELSE                                           CSD007
      *                                                                   CSD007
     C           @RTCD     IFNE *BLANKS
     C*
     C**   SELECTED VALUE MUST EXIST ON CUSTOMERS FILE
     C*
     C                     SETON                     3040
     C                     MOVEL'DLM0007' @MSGID
     C                     CALL 'ZA0240'
     C                     PARM           @MSGID 10
     C*
     C**   ELSE NO ERROR - ENSURE SCREEN FIELD IS NUMBER NOT SHORTNAME
     C*
     C                     ELSE
     C                     MOVE *BLANKS   SCUST                                             BUG11439
     C                     MOVELBBCUST    SCUST
     C                     END
      *                                                                   CSD007
     C                     ENDIF                                          CSD007
      *                                                                   CSD007
     C*
     C**   VALIDATE LIVE DEALS INDICATOR
     C*
     C           SLIV      IFNE 'Y'
     C           SLIV      ANDNE*BLANKS
     C                     SETON                     3140
     C                     MOVEL'DLM0011' @MSGID
     C                     MOVEL'live'    @DATA
     C                     CALL 'ZA0440'
     C                     PARM           @MSGID
     C                     PARM           @DATA  45
     C                     END
     C*
     C**   VALIDATE MATURE DEALS INDICATOR
     C*
     C           SMAT      IFNE 'Y'
     C           SMAT      ANDNE*BLANKS
     C                     SETON                     3240
     C                     MOVEL'DLM0011' @MSGID
     C                     MOVEL'mature'  @DATA
     C                     CALL 'ZA0440'
     C                     PARM           @MSGID
     C                     PARM           @DATA
     C                     END
     C*
     C**   ENSURE LIVE AND MATURE DEALS INDICATORS ARE NOT BOTH BLANK
     C*
     C           SLIV      IFEQ *BLANKS
     C           SMAT      ANDEQ*BLANKS
     C                     SETON                     313240
     C                     MOVEL'DLM0012' @MSGID
     C                     CALL 'ZA0240'
     C                     PARM           @MSGID
     C                     END
     C*                                                                                       CDL021
     C           DTYPE     KLIST                                                              CDL021
     C                     KFLD           TYP1                                                CDL021
     C                     KFLD           TYP2                                                CDL021
     C                     KFLD           TYP3                                                CDL038
     C*                                                                                       CDL021
     C           TYP1      IFNE *BLANKS                                                       CDL021
     C           TYP2      ANDNE*BLANKS                                                       CDL021
     C           CDL038    ANDEQ'N'                                                           CDL038
     C           TYP1      ORNE *BLANKS                                                       CDL038
     C           TYP2      ANDNE*BLANKS                                                       CDL038
     C           TYP3      ANDNE*BLANKS                                                       CDL038
     C           CDL038    ANDEQ'Y'                                                           CDL038
     C           DTYPE     CHAINFDDTSTL0             21                                       CDL021
     C           *IN21     IFEQ '1'                                                           CDL021
     C                     SETON                     3340                                     CDL021
     C           CDL038    IFEQ 'Y'                                                           CDL038
     C                     MOVEL'MMM1070' @MSGID                                              CDL038
     C                     ELSE                                                               CDL038
     C                     MOVEL'MMM1051' @MSGID                                              CDL021
     C                     ENDIF                                                              CDL038
     C                     CALL 'ZA0240'                                                      CDL021
     C                     PARM           @MSGID                                              CDL021
     C                     END                                                                CDL021
     C                     END                                                                CDL021
      *                                                                                       CDL021
     C           TYP1      IFEQ *BLANKS                                                       CDL021
     C           TYP2      ANDNE*BLANKS                                                       CDL021
     C           TYP1      OREQ *BLANKS                                                       CDL038
     C           TYP3      ANDNE*BLANKS                                                       CDL038
     C           TYP2      OREQ *BLANKS                                                       CDL038
     C           TYP3      ANDNE*BLANKS                                                       CDL038
     C           TYP2      ORNE *BLANKS                                                       CDL038
     C           TYP3      ANDEQ*BLANKS                                                       CDL038
     C           CDL038    ANDEQ'Y'                                                           CDL038
     C                     SETON                     3340                                     CDL021
     C           CDL038    IFEQ 'Y'                                                           CDL038
     C                     MOVEL'MMM1070' @MSGID                                              CDL038
     C                     ELSE                                                               CDL038
     C                     MOVEL'MMM1051' @MSGID                                              CDL021
     C                     ENDIF                                                              CDL038
     C                     CALL 'ZA0240'                                                      CDL021
     C                     PARM           @MSGID                                              CDL021
     C                     END                                                                CDL021
      *                                                                                       CDL021
     C           TYP1      IFNE *BLANKS                                                       CDL021
     C           TYP2      ANDEQ*BLANK                                                        CDL021
     C           TYP3      ANDEQ*BLANKS                                                       CDL038
     C           *IN40     ANDEQ*OFF                                                          CDL038
     C           DTYPE     SETLLFDDTSTL0               2223                                   CDL021
     C           *IN22     IFEQ '0'                                                           CDL021
     C           *IN23     ANDEQ'0'                                                           CDL021
     C                     READ FDDTSTPD                 21                                   CDL021
     C           *IN21     IFEQ '0'                                                           CDL021
     C           DTPE11    ANDEQTYP1                                                          CDL021
     C                     MOVE '**'      TYP2                                                CDL021
     C                     ELSE                                                               CDL021
     C                     SETON                     3340                                     CDL021
     C           CDL038    IFEQ 'Y'                                                           CDL038
     C                     MOVEL'MMM1070' @MSGID                                              CDL038
     C                     ELSE                                                               CDL038
     C                     MOVEL'MMM1051' @MSGID                                              CDL021
     C                     ENDIF                                                              CDL038
     C                     CALL 'ZA0240'                                                      CDL021
     C                     PARM           @MSGID                                              CDL021
     C                     END                                                                CDL021
     C                     ELSE                                                               CDL021
     C                     SETON                     3340                                     CDL021
     C           CDL038    IFEQ 'Y'                                                           CDL038
     C                     MOVEL'MMM1070' @MSGID                                              CDL038
     C                     ELSE                                                               CDL038
     C                     MOVEL'MMM1051' @MSGID                                              CDL021
     C                     ENDIF                                                              CDL038
     C                     CALL 'ZA0240'                                                      CDL021
     C                     PARM           @MSGID                                              CDL021
     C                     END                                                                CDL021
     C                     END                                                                CDL021
      *                                                                                       CDL021
     C           TYP1      IFEQ *BLANKS                                                       CDL021
     C           TYP2      ANDEQ*BLANKS                                                       CDL021
     C           TYP3      ANDEQ*BLANKS                                                       CDL038
     C           *IN40     ANDEQ*OFF                                                          CDL038
     C                     MOVE '**'      TYP1                                                CDL021
     C                     MOVE '**'      TYP2                                                CDL021
     C                     ENDIF                                                              CDL021
     C**                                                                                      CDL021
     C                     ENDSR
     C*
     C********************************************************************
     C*                                                                   CSD007
     C* *PSSR    -   ROUTINE CALLED ON FILE / PROGRAM ERROR               CSD007
     C*                                                                   CSD007
     C* CALLS           -                                                 CSD007
     C*                                                                   CSD007
     C* CALLED BY       -                                                 CSD007
     C*                                                                   CSD007
     C* FLDS USED       -                                                 CSD007
     C*                                                                   CSD007
     C*****************************************************************   CSD007
     C           *PSSR     BEGSR                                          CSD007
      *                                                                   CSD007
     C           @RUN      IFEQ *BLANK                                    CSD007
     C                     MOVE 'Y'       @RUN    1                       CSD007
     C                     SETON                     U7U8LR               CSD007
     C                     DUMP                                           CSD007
     C                     END                                            CSD007
      *                                                                   CSD007
     C                     RETRN                                          CSD007
      *                                                                   CSD007
     C                     ENDSR                                          CSD007
     C********************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
