     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL IRS Date Schedule Exception Report')          *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Dealing Module                                       *
     F*                                                               *
     F*  DL4111 - IRS Date Schedule Exception Report                  *
     F*                                                               *
     F*  Function:  This program is designed to report any holiday    *
     F*             errors that have occurred on schedules            *
     F*             previously set up before the holidays were        *
     F*             entered.                                          *
     F*                                                               *
     F*  Called By: DLC4110       - IRS Date Schedule Extract/Report  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 05Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CIR001 *CREATE     Date 13Jun95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
     F*  CIR001 - Interest Rate Calendars.                            *
     F*                                                               *
     F*****************************************************************
     FDEALS   IF  E           K        DISK
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDCF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     FDLSDEEL0IF  E           K        DISK
     FDL4111P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
     FDL4111AUO   E                    PRINTER                        UC
     F                                              KINFDS SPOOLU
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   10    'OUR INTEREST RATE CHANGE SCHEDULE' print indicator   *
      *   11    'THEIR INTEREST RATE CHANGE SCHEDULE' print indicator *
      *   12    'OUR INTEREST PAYMENT SCHEDULE' print indicator       *
      *   13    'THEIR INTEREST PAYMENT SCHEDULE' print indicator     *
      *   14    'OUR CASH FLOW SCHEDULE' print indicator              *
      *   15    'THEIR CASH FLOW SCHEDULE' print indicator            *
      *   16    'OUR PRINCIPAL CHANGE SCHEDULE' print indicator       *
      *   17    'THEIR PRINCIPAL CHANGE SCHEDULE' print indicator     *
      *   20    Print spacing indicator                               *
      *   21    End of File DLSDEEL0                                  *
      *   33    Print deal number indicator                           *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *PSSR        Program exception error routine                 *
      *  #LINIT       Initial Processing                              *
      *  #LLOOP       Loop Processing                                 *
      *  #LNEWD       Check for new deal number                       *
      *  #LNEWS       Check for new schedule type                     *
      *  #LVALD       Validate date for exception                     *
      *  #LWRT        Write detail lines to printer                   *
      *  #LREDL       Read related deal record                        *
      *  #LRCFP       Subroutine to register the P1 Printer File      *
      *  #LRCFA       Subroutine to register the AU Printer File      *
      *  #LAUD        Output Audit report and end program             *
      *                                                               *
      *  Standard routines:                                           *
      *  ZDATE2       To convert a day number to date format          *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E                    HOL        50  3
     E                    DAT         6  7
     E/COPY ZSRSRC,ZDATE2Z1
      /SPACE 3
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     I            DS
     I                                        1  42 DAT
     I                                        1   7 DATE1
     I                                        8  14 DATE2
     I                                       15  21 DATE3
     I                                       22  28 DATE4
     I                                       29  35 DATE5
     I                                       36  42 DATE6
      *
     ISPOOL1      DS
      ** File Information Data Structure for DL4111P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for DL4111AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISDBANK    E DSSDBANKPD
      ** Bank details
      *
     IDSFDY     E DSDSFDY
      ** First DS for Access Programs, short data structure
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *                M A I N  P R O C E S S I N G                   *
      *                                                               *
      *****************************************************************
      *
      ** Perform initialisation
      *
     C                     EXSR #LINIT
      *
      ** Process schedules
      *
     C                     EXSR #LLOOP
     C                     EXSR #LWRT
      *
      ** Perform end processing
      *
     C                     WRITETRAILP1
     C                     EXSR #LAUD
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LINIT - Initial Processing                              *
      *                                                               *
      *****************************************************************
      *
     C           #LINIT    BEGSR
      *
      ** Copyright Statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      ** Define the LDA for error handling
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Initialise constants
      *
     C                     Z-ADD11        LINE    20
     C                     Z-ADD1         N       20
     C                     Z-ADD0         NREC
      *
      ** Access bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'DL4111'  DBPGM
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'SDBANKPD'DBFILE
     C                     Z-ADD1         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Check system date format, DDMMYY or MMDDYY.
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     ELSE
     C                     MOVE '0'       *IN98
     C                     END
      *
      ** Read first schedule record
      *
     C                     READ DLSDEEL0                 21
      *
      ** If no records, output audit report
      *
     C           *IN21     IFEQ '1'
     C                     EXSR #LAUD
     C                     END
      *
      ** Output first page heading
      *
     C                     OPEN DL4111P1
     C                     EXSR #LRCFP
     C                     WRITEHEADP1
      *
      ** Set first page details
      *
     C                     MOVE '1'       *IN33
     C                     MOVE DLNO      PDLNO   6
     C                     MOVE SCHT      PSCHT   1
      *
      ** Read first deal record
      *
     C                     EXSR #LREDL
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LLOOP - Loop Processing                                 *
      *                                                               *
      *****************************************************************
      *
     C           #LLOOP    BEGSR
      *
     C           *IN21     DOWEQ'0'
      *
      ** Check for new deal number
      *
     C           DLNO      IFNE LASTDN
     C                     EXSR #LNEWD
     C                     ELSE
      *
      ** Check for new schedule type
      *
     C                     EXSR #LNEWS
     C                     END
      *
      ** Validate date and write if more than six
      *
     C                     EXSR #LVALD
      *
      ** Read next record
      *
     C                     READ DLSDEEL0                 21
      *
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LNEWD - Check for new deal number                       *
      *                                                               *
      *****************************************************************
      *
     C           #LNEWD    BEGSR
      *
     C           DLNO      IFNE LASTDN
     C           N         ANDNE0
     C                     MOVE '1'       *IN20
     C                     EXSR #LWRT
     C                     MOVE '0'       *IN20
     C                     MOVE '1'       *IN33
     C                     MOVE DLNO      PDLNO   6
     C                     MOVE SCHT      PSCHT   1
     C                     EXSR #LREDL
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LNEWS - Check for new schedule type                     *
      *                                                               *
      *****************************************************************
      *
     C           #LNEWS    BEGSR
      *
     C           SCHT      IFNE PSCHT
     C           N         ANDNE0
     C                     MOVE '1'       *IN20
     C                     EXSR #LWRT
     C                     MOVE '0'       *IN20
     C                     MOVE SCHT      PSCHT
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LVALD - Validate date for exception                     *
      *                                                               *
      *****************************************************************
      *
     C           #LVALD    BEGSR
      *
     C           N         IFGT 6
     C                     EXSR #LWRT
     C                     END
      *
      ** Convert date
      *
     C                     Z-ADDPRCDT     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    DAT,N
     C                     ADD  1         N
     C                     ADD  1         NREC
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LWRT  - Write detail lines to printer                   *
      *                                                               *
      *****************************************************************
      *
     C           #LWRT     BEGSR
      *
     C                     MOVEA'00000000'*IN,10
     C                     MOVE '0'       *IN21
      *
     C           LINE      IFGT 54
     C                     ADD  1         PAGE
     C                     WRITEHEADP1
     C                     Z-ADD11        LINE
     C                     MOVE LASTDN    PDLNO
     C                     END
      *
     C           PSCHT     IFEQ '1'
     C                     MOVE '1'       *IN10
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C           PSCHT     IFEQ '2'
     C                     MOVE '1'       *IN11
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C           PSCHT     IFEQ '3'
     C                     MOVE '1'       *IN12
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C           PSCHT     IFEQ '4'
     C                     MOVE '1'       *IN13
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C           PSCHT     IFEQ '5'
     C                     MOVE '1'       *IN14
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C           PSCHT     IFEQ '6'
     C                     MOVE '1'       *IN15
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C           PSCHT     IFEQ '7'
     C                     MOVE '1'       *IN16
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C           PSCHT     IFEQ '8'
     C                     MOVE '1'       *IN17
     C                     MOVE ' '       PSCHT
     C                     END
      *
     C                     WRITEDETAIL
     C                     MOVE '1'       *IN33
     C                     ADD  1         LINE
     C                     MOVE *BLANKS   DAT
     C                     Z-ADD1         N
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LREDL - Read related deal record                        *
      *                                                               *
      *****************************************************************
      *
     C           #LREDL    BEGSR
      *
     C           DLNO      CHAINDEALS                40
      *
     C           *IN40     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'DL4111'  DBPGM
     C                     MOVELDLNO      DBKEY
     C                     MOVEL'DEALS   'DBFILE
     C                     Z-ADD2         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVE DLNO      LASTDN  60
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R *PSSR  - Error control subroutine                        *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     EXSR #LAUD
     C                     END
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LRCFP - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           #LRCFP    BEGSR
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANK    ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LRCFA - Register the AU Printer File via RCF            *
      *                                                               *
      *****************************************************************
      *
     C           #LRCFA    BEGSR
      *
      ** Ensure audit spool file recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR   1
      *
      ** If error occurs during ZSFILE processing, then return to the
      ** calling program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S/R #LAUD  - Output Audit report and End Program             *
      *                                                               *
      *****************************************************************
      *
     C           #LAUD     BEGSR
      *
     C                     OPEN DL4111AU
     C                     EXSR #LRCFA
      *
      ** Output report header and database error details.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ '1'
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           NREC      IFEQ 0
     C                     WRITENODTLS
     C                     END
     C                     END
      *
      ** End program and return.
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
     C/COPY ZSRSRC,ZDATE2Z2
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
