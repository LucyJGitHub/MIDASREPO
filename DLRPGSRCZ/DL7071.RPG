     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Outstanding loan/deposits cust comp')
     F*****************************************************************
     F*  Midas - Dealing Module
     F*                                                               *
     F*  DL7071  -  OUTSTANDING LOAN/DEPOSITS FILE REPORT             *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 049997             Date 30Jan93               *
      *                 046940           Date  14Jan93                *
     F*                 048550           DATE  17DEC92                *
     F*                 S01390           DATE  30JUN92                *
     F*                 AUE148           DATE  09JUL91                *
     F*                 039044           DATE  16/10/90               *
     F*                 AUS029           DATE  07/06/88               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CDL006 - Dealing Fiduciary.                                  *
     F*  049997 - DL7071 BY COMPANY DOES NOT CONSOLIDATE AT COMPANY   *
     F*           COMPANY LEVEL.                                      *
     F*  046940 - FIELD TERMD IS NOT INITIALISED. '10APR92' IS        *
     F*           DISPLAYED AS '010APR92' AND '5MAY92' IS DISPLAYED   *
     F*           AS '0 5MAY92'. FIX DONE IS TO BLANK OUT TERMD       *
     F*           BEFORE MOVING DATE TO TERMD.                        *
     F*  048550 - Company name not printed and text 'End of Report    *
     F*           for a Company' was printed twice                    *
     F*  S01390 - Release 10 Changes for Australianisation            *
     F*  AUE148 - COMPANY NAME NOT PRINTED                            *
     F*  AUS029 - AUSTRALIAN PRODUCT - OUTSTANDING AUD DEPOSITS       *
     F*           AND LOANS BY CUSTOMER                               *
     F*           PRIOR TO R22 ALL FID FIELDS WERE DEFINED WITH       *  *
     F*           DECIMAL POINT ON FILE. FROM R22 THESE FIELDS        *  *
     F*           WILL BE REDEFINED WITH ZERO DECIMAL POINT (EXCEPT   *  *
     F*           RATE FIELDS) TO CONFORM WITH MIDAS STANDTARD.       *  *
     F*  039044 - FIELD LENGTHS ARE CHANGED TO MAKE VALUES            *
     F*           IN DL7071P2 ACCURATE UP TO FOURTH DECIMAL           *
     F*           POSITION.                                           *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*            L4 Change in Company Code.
     F*            L3 Change in Deposit/Loan Indicator.
     F*            L2 Change in Customer Short Name.
     F*            L1 Change in the Term of Deals.
     F*
     F*       02      No Record Found for Company Code.
     F*       03      No Record Founs for FID Rates.
     F*       07      Chain fail for Customer Short Name.
     F*       08      No records on the ODEAL file.
     F*       10      Print Customer No. & Short Name.
     F*       11      First Cycle Indicator.
     F*       50      Work Indicator.
     F*       51      DL7071P1 is open
     F*       52      DL7071P2 is open
     F*       88      Multibranching Indicator
     F*     90-99     Standard Subroutine Indicators.
     F*     U7-U8     DATABASE ERROR
     F*
     F/EJECT
     F*     Files used.
     FDL7071AUO   E                    PRINTER      KINFDS PRTU
     FDL7071P1O   E                    PRINTER                        UC
     F                                              KINFDS PRTA
     FDL7071P2O   E                    PRINTER                        UC
     F                                              KINFDS PRTB
     FDL7071P3O   E                    PRINTER      KINFDS SPOOL3     UC  CDL006
     FDL7071P4O   E                    PRINTER      KINFDS SPOOL4     UC  CDL006
     FFIDRT   IF  E           K        DISK                           UC
     F*DEAL   IP  E           K        DISK                               049997
     FODEALL1 IP  E           K        DISK                               049997
     F*     Arrays Used.
     F/EJECT
     E*
     E                    TRM     1   2  8
     E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
     E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
     E                    CPY@    1   1 80
     E*
     E/EJECT
     I*
     IODEALAAF
     I                                              CMPYM L4              048550
     I                                              DLNI  L3
     I                                              CSNM  L2
     I                                              TERM  L1
     I              LCD                             LCD1
     I*
     I*     Data Structures.
     I*    File Information Data Structure for Printer File DL7071P1.
     I*    EFICLA is the Current Line Number.
     IPRTA        DS
     I                                       83  92 SFILEA
     I                                    B 123 1240SFNUMA
     I                                    B 367 3680EFICLA
     I*    File Information Data Structure for Printer File DL7071P2.
     I*    EFICLB is the Current Line Number.
     IPRTB        DS
     I                                       83  92 SFILEB
     I                                    B 123 1240SFNUMB
     I                                    B 367 3680EFICLB
     I*
     IPRTU        DS
     I**    SPOOL FILE INFORMATION DATA STRUCTURE
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *                                                                   CDL006
     ISPOOL3      DS                                                      CDL006
      ** File Information Data Structure for DL7071P3                     CDL006
      *                                                                   CDL006
     I                                       83  92 SFILE3                CDL006
     I                                    B 123 1240SFNUM3                CDL006
     I                                    B 188 1890OFLLN3                CDL006
     I                                    B 367 3680PRTLN3                CDL006
      *                                                                   CDL006
     ISPOOL4      DS                                                      CDL006
      ** File Information Data Structure for DL7071P4                     CDL006
      *                                                                   CDL006
     I                                       83  92 SFILE4                CDL006
     I                                    B 123 1240SFNUM4                CDL006
     I                                    B 188 1890OFLLN4                CDL006
     I                                    B 367 3680PRTLN4                CDL006
     I*
     ISDBANK    E DSSDBANKPD
     I* Bank Details Accesses Via Access Program
     I*
     ISDCOMP    E DSSDCOMPPD
     I* Company Details Accessed Via Access Program
     I*
     ISDCUST    E DSSDCUSTPD
     I* Customer Details Accessed Via Access Program
     I*                                                                   S01390
     ISCSARD    E DSSCSARDPD                                              S01390
     I** EXTERNAL DS FOR SAR DETAILS                                      S01390
     I*
     IDSFDY     E DSDSFDY
     I* First DS For Access Programs, Short Data Structure
     I*
     IDSSDY     E DSDSSDY
     I* Second DS For Access Programs, Long Data Structure
     ILDA         DS                            256
     I*
     I** Local Data Area To Identify Database Errors
     I*
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     IRUNDT       DS
     I                                       13  13 MBIN
     I*
     C*
     C*     Definitions of Like Fields.
     C           *LIKE     DEFN TOT0      TOTC
     C           *LIKE     DEFN TOT0      TOT1
     C           *LIKE     DEFN TOT0      TOT2
     C           *LIKE     DEFN TOT0      TOT3
     C           *LIKE     DEFN TOT0      TOT0B0
     C           *LIKE     DEFN TOT0      TOT1B1
     C           *LIKE     DEFN TOT0      TOT2B2
     C           *LIKE     DEFN TOT0      TOT3B3
     C           *LIKE     DEFN PCPL      PCPL0
     C           *LIKE     DEFN PCPL      PCPL1
     C           *LIKE     DEFN PCPL      PCPL2
     C           *LIKE     DEFN PCPL      PCPL3
     C           *LIKE     DEFN PCPL      PCPLB
     C           *LIKE     DEFN PCPL      PCPLB0
     C           *LIKE     DEFN PCPL      PCPLB1
     C           *LIKE     DEFN PCPL      PCPLB2
     C           *LIKE     DEFN PCPL      PCPLB3
     C           *LIKE     DEFN PCPL      PCPLC
     C           *LIKE     DEFN NAID      NAID0
     C           *LIKE     DEFN NAID      NAID1
     C           *LIKE     DEFN NAID      NAID2
     C           *LIKE     DEFN NAID      NAID3
     C           *LIKE     DEFN NAID      NAIDC
     C           *LIKE     DEFN TERM      XERM
     C/EJECT
     C***********************************************
     C*                                             *
     C*            MAIN-LINE PROCESSING.            *
     C*                                             *
     C***********************************************
     C*
     C*   Perform Initialisation.
     C*
     C           *IN11     IFEQ '0'
     C                     EXSR SR0000
     C                     END
     C*
     C*   Proccess For Empty File.
     C*
     C           *IN08     IFEQ '0'
     C           *INLR     CABEQ'1'       FINN
     C                     MOVE '1'       *IN08
     C                     END
     C*
     C** Only process if Company was requested.
     C*
     C           ENT       IFNE 'ALL'
     C           ENT       ANDNE'   '
     C           ENT       ANDNECMPYM
     C                     SETON                     LR
     C                     GOTO FINN
     C                     END
     C*
     C*   Change of Deposit Indicator.
     C*
     C   L3                EXSR SRL3D
     C*
     C*   Change of Company Code.
     C*
     C   L3
     COR L4                EXSR SRL4D
     C*
     C*   Change of Customer Short Name.
     C*
     C   L2                EXSR SRL2D
     C*
     C*   Change of Term Indicator.
     C*
     C   L1                EXSR SRL1D
     C*
     C*   Perform Detail Time Processing.
     C*
     C                     EXSR SR6000
     C*
     C           FINN      TAG
     C*
     C*   TOTAL TIME PROCESSING.
     C*
     C*   Perform Term Indicator Change.
     C*
     CL1 11                EXSR SRL1T
     C*
     C*   Perform Customer Short Name Change.
     C*
     CL2 11                EXSR SRL2T
     C*
     C*   Perform Company Code Change.
     C*
     CL3 11NL4             EXSR SRL4T
     CL4 11                EXSR SRL4T
     C*
     C*   Perform Deposit Loan Indicator Change.
     C*
     CL3 11                EXSR SRL3T
     C*
     C*   Perform LR Processing.
     C*
     CLR                   EXSR SR9999
     C*
     C*****************************************************
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*          SR0000 - Initialisation.                 *
     C*                                                   *
     C*****************************************************
     C*
     C           SR0000    BEGSR
     C*
     C*   Initialise ALL Indicators.
     C*
     C                     MOVE '1'       *IN11
     C                     Z-ADD0         OUTA
     C                     Z-ADD0         OUTB
     C*
     C** READ BANK DETAILS VIA ACCESS PROGRAM
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' RTCD    7
     C                     PARM '*FIRST ' OPTN    7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C*    - Set date indicator.
     C*
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
     C*
     C*   Set Report Header Date at Next Working Day.
     C*
     C                     Z-ADDBJDNWD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RPTDAT
     C*
     C                     MOVEACPY@      BIS@   80
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C                     PARM           ENT     3
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN88
     C                     END
     C*                                                                   S01390
     C** Access SAR details file to determine if FID processing           S01390
     C** is switched on                                                   S01390
     C*                                                                   S01390
     C                     CALL 'AOSARDR0'                                S01390
     C                     PARM *BLANKS   RTCD    7                       S01390
     C                     PARM '*VERIFY' OPTN    7                       S01390
     C                     PARM 'AUS005'  @SARD   6                       S01390
     C           SCSARD    PARM SCSARD    DSFDY                           S01390
     C           RTCD      IFEQ *BLANK                                    S01390
     C                     MOVEL'Y'       AUS005  1                       S01390
     C                     END                                            S01390
      *                                                                   CDL006
      ** Check if Dealing Fiduciary is switched ON                        CDL006
      *                                                                   CDL006
     C                     CALL 'AOSARDR0'                                CDL006
     C                     PARM *BLANKS   WRTCD   7                       CDL006
     C                     PARM '*VERIFY' WOPTN   7                       CDL006
     C                     PARM 'CDL006'  WSARD   6                       CDL006
     C           SCSARD    PARM SCSARD    DSFDY                           CDL006
     C           WRTCD     IFEQ *BLANKS                                   CDL006
     C                     MOVE 'Y'       CDL006  1                       CDL006
     C                     ELSE                                           CDL006
     C           WRTCD     IFNE '*NRF   '                                 CDL006
     C                     MOVEL'SCSARDPD'DBFILE           ************   CDL006
     C                     Z-ADD1         DBASE            * DBERR 01 *   CDL006
     C                     MOVEL'CDL006'  DBKEY            ************   CDL006
     C                     EXSR *PSSR                                     CDL006
     C                     ENDIF                                          CDL006
     C                     MOVE 'N'       CDL006                          CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
     C                     MOVE 'N'       FLAGC   1                       CDL006
     C                     MOVE 'N'       FLAGD   1                       CDL006
     C                     Z-ADD0         OUTD    50                      CDL006
     C                     Z-ADD0         OUTE    50                      CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*     SRL3D - Deposit Loan Indicator Change.(Detail)*
     C*                                                   *
     C*****************************************************
     C*
     C           SRL3D     BEGSR
     C*
     C*   Deals Record so Open DL7071P1.
     C*
     C           DLNI      IFEQ 'D'
     C                     MOVE 'Y'       FLAGA   1
     C                     ELSE
     C*
     C*   Loans Record so Open DL7071P2.
     C*
     C           DLNI      IFEQ 'L'
     C                     MOVE 'Y'       FLAGB   1
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           DLNI      IFEQ 'T'                                       CDL006
     C                     MOVE 'Y'       FLAGC                           CDL006
     C                     ENDIF                                          CDL006
     C           DLNI      IFEQ 'P'                                       CDL006
     C                     MOVE 'Y'       FLAGD                           CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*          SRL4D  - Company Code Change.(Detail)    *
     C*                                                   *
     C*****************************************************
     C*
     C           SRL4D     BEGSR
     C*
     C*   Initialise the Company Accumulator Fields.
     C*
     C                     Z-ADD*ZEROS    TOT0B0
     C                     Z-ADD*ZEROS    TOT1B1
     C                     Z-ADD*ZEROS    TOT2B2
     C                     Z-ADD*ZEROS    TOT3B3
     C*
     C                     Z-ADD*ZEROS    PCPLB0
     C                     Z-ADD*ZEROS    PCPLB1
     C                     Z-ADD*ZEROS    PCPLB2
     C                     Z-ADD*ZEROS    PCPLB3
     C*
     C*   Initialise the Company Output Fields.
     C*
     C                     Z-ADD*ZEROS    BGEMAA
     C                     Z-ADD*ZEROS    BLTMAA
     C*
     C                     Z-ADD0         BRATE0
     C                     Z-ADD0         BRATE1
     C                     Z-ADD0         BRATE2
     C                     Z-ADD0         BRATE3
     C************INL4     IFEQ 'Y'                                       048550
     C           *INL4     IFEQ '1'                                       048550
     C*
     C*   Set key to Access new Company Code.
     C*
     C                     SETOF                     0203
     C*
     C           MBIN      IFEQ 'Y'
     C                     CALL 'AOCOMPR0'
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' OPTN
     C                     PARM CMPYM     KYCM    3
     C           SDCOMP    PARM SDCOMP    DSFDY
     C*   Set Company Code Text or Exit if Error.
     C*
     C           RTCD      IFEQ *BLANKS
     C                     MOVELAPCMNM    CMPTXT
     C                     ELSE
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCOMPPD'DBFILE
     C                     MOVELCMPYM     DBKEY
     C                     Z-ADD2         DBASE
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     END
     C                     END
     C*
     C*   Try to Access F.I.D. Rates File &store F.I.D. rates.
     C*
     C           AUS005    IFEQ 'Y'                                       S01390
     C           FILOPF    IFEQ 'Y'
     C                     OPEN FIDRT
     C                     MOVEL'Y'       FILOPF  1
     C                     END
     C           LOCC      CHAINFIDRT                03
     C           *IN03     IFEQ '1'
     C                     Z-ADD*ZEROS    MAAL
     C                     END
     C                     ELSE                                           S01390
     C                     Z-ADD*ZEROS    MAAL                            S01390
     C                     END                                            S01390
     C                     END                                            S01390
     C*
     C*   Output new headings for new Company
     C*
     C           FLAGA     IFEQ 'Y'
     C           *INLR     IFEQ '0'
     C           FILOPA    IFEQ 'Y'
     C                     CLOSEDL7071P1
     C                     END
     C                     OPEN DL7071P1
     C                     MOVE '1'       *IN51                           048550
     C                     Z-ADD0         PAGE
     C                     MOVEL'Y'       FILOPA  1
     C*
     C** Report printer file to RCF via ZSFILE
     C*
     C                     Z-ADDSFNUMA    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM CMPYM     @ENT    3
     C                     PARM           SFILEA
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
     C*
     C** Error during ZSFILE processing, return to calling program
     C*
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     GOTO FINN
     C                     END
     C*
     C                     WRITEHEADERA
     C                     END
     C*
     C                     ELSE
     C           FLAGB     IFEQ 'Y'
     C           *INLR     IFEQ '0'
     C           FILOPB    IFEQ 'Y'
     C                     CLOSEDL7071P2
     C                     END
     C                     OPEN DL7071P2
     C                     MOVE '1'       *IN52                           048550
     C                     Z-ADD0         PAGE
     C                     MOVEL'Y'       FILOPB  1
     C*
     C** Report printer file to RCF via ZSFILE
     C*
     C                     Z-ADDSFNUMB    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM CMPYM     @ENT    3
     C                     PARM           SFILEB
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
     C*
     C** Error during ZSFILE processing, return to calling program
     C*
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     GOTO FINN
     C                     END
     C*
     C                     WRITEHEADERB
     C                     END
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C           *INLR     ANDEQ'0'                                       CDL006
     C           WOPN3     IFEQ 'Y'                                       CDL006
     C                     CLOSEDL7071P3                                  CDL006
     C                     ENDIF                                          CDL006
     C                     OPEN DL7071P3                                  CDL006
     C                     Z-ADD0         PAGE                            CDL006
     C                     MOVE 'Y'       WOPN3   1                       CDL006
      *                                                                   CDL006
      ** Report printer file to RCF via ZSFILE                            CDL006
      *                                                                   CDL006
     C                     Z-ADDSFNUM3    ZSNUM                           CDL006
     C                     CALL 'ZSFILE'                                  CDL006
     C                     PARM           SEQ                             CDL006
     C                     PARM CMPYM     PENT    3                       CDL006
     C                     PARM           SFILE3                          CDL006
     C                     PARM           ZSNUM                           CDL006
     C                     PARM *BLANKS   ZSERR   1                       CDL006
      *                                                                   CDL006
      ** Error during ZSFILE processing, return to calling program        CDL006
      *                                                                   CDL006
     C           ZSERR     IFEQ 'Y'                                       CDL006
     C                     SETON                     U7U8LR               CDL006
     C                     GOTO FINN                                      CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     WRITEHEADR3                                    CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C           *INLR     ANDEQ'0'                                       CDL006
     C           WOPN4     IFEQ 'Y'                                       CDL006
     C                     CLOSEDL7071P4                                  CDL006
     C                     ENDIF                                          CDL006
     C                     OPEN DL7071P4                                  CDL006
     C                     Z-ADD0         PAGE                            CDL006
     C                     MOVE 'Y'       WOPN4   1                       CDL006
      *                                                                   CDL006
      ** Report printer file to RCF via ZSFILE                            CDL006
      *                                                                   CDL006
     C                     Z-ADDSFNUM4    ZSNUM                           CDL006
     C                     CALL 'ZSFILE'                                  CDL006
     C                     PARM           SEQ                             CDL006
     C                     PARM CMPYM     PENT    3                       CDL006
     C                     PARM           SFILE4                          CDL006
     C                     PARM           ZSNUM                           CDL006
     C                     PARM *BLANKS   ZSERR   1                       CDL006
      *                                                                   CDL006
      ** Error during ZSFILE processing, return to calling program        CDL006
      *                                                                   CDL006
     C           ZSERR     IFEQ 'Y'                                       CDL006
     C                     SETON                     U7U8LR               CDL006
     C                     GOTO FINN                                      CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     WRITEHEADR4                                    CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*        SRL2D - Customer Short Name Change.(Detail)*
     C*                                                   *
     C*****************************************************
     C*
     C           SRL2D     BEGSR
     C*
     C*   Initilize Customer Totals.
     C*
     C                     Z-ADD*ZEROS    TOTC
     C                     Z-ADD*ZEROS    PCPLC
     C                     Z-ADD*ZEROS    NAIDC
     C*
     C*   Chain to client file for A/C Officer field.
     C*
     C                     MOVE CSNM      KYCN   10
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   RTCD
     C                     PARM '*KEY   ' OPTN
     C                     PARM           KYCN
     C                     PARM *BLANKS   KYST    7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C           RTCD      IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCUSTPD'DBFILE
     C                     MOVELCSNM      DBKEY
     C                     Z-ADD3         DBASE
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVE BBACOC    ACOC
     C                     END
     C*
     C*   Set Print Indicator for Customer No. & Short Name.
     C*
     C                     SETON                     10
     C                     Z-ADD0         B
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*            SRL1D  - Term Indicator Change.(Detail)*
     C*                                                   *
     C*****************************************************
     C*
     C           SRL1D     BEGSR
     C*
     C*   Store term.
     C*
     C                     Z-ADDTERM      XERM
     C*
     C*   Initilize 11hr Term Totals.
     C*
     C           TERM      IFEQ 0
     C                     Z-ADD*ZEROS    TOT0
     C                     Z-ADD*ZEROS    PCPL0
     C                     Z-ADD*ZEROS    NAID0
     C                     ELSE
     C*
     C*   Initilize 24hr Term Totals.
     C*
     C           TERM      IFEQ 1
     C                     Z-ADD*ZEROS    TOT1
     C                     Z-ADD*ZEROS    PCPL1
     C                     Z-ADD*ZEROS    NAID1
     C                     ELSE
     C*
     C*   Initilize Call Money Deal (Excluding 11am & 24hr money) Totals.
     C*
     C           TERM      IFGT 1
     C           TERM      IFLT 9999
     C                     Z-ADD*ZEROS    TOT2
     C                     Z-ADD*ZEROS    PCPL2
     C                     Z-ADD*ZEROS    NAID2
     C                     ELSE
     C*
     C*   Initilize Term Deal Term Totals.
     C*
     C                     Z-ADD*ZEROS    TOT3
     C                     Z-ADD*ZEROS    PCPL3
     C                     Z-ADD*ZEROS    NAID3
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C*   Initilize Output Fields.
     C*
     C                     Z-ADD0         A
     C                     Z-ADD0         DTPCPL
     C                     Z-ADD0         DTWTAV
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*          SR6000 - Detail Time Processing.         *
     C*                                                   *
     C*****************************************************
     C*
     C           SR6000    BEGSR
     C*
     C*   Accumulate Report Total.
     C*
     C           PCPL      IFGE MAAL
     C           PCPL      DIV  100       BGEMWF 132H
     C                     ADD  BGEMWF    BGEMAA
     C                     ELSE
     C           PCPL      DIV  100       BLTMWF 132H
     C                     ADD  BLTMWF    BLTMAA
     C                     END
     C*
     C*   Calculate Output Fields.
     C*
     C           BRKC      COMP 'PHON'                   50
     C  N50      BRKC      COMP 'MAIL'                   50
     C  N50      BRKC      COMP 'TELX'                   50
     C   50                MOVE *BLANK    BRKC
     C*
     C                     MOVELRTSP      RATE
     C*
     C           PCPL      DIV  100       XPCPL     H
     C*
     C           BAGE      DIV  100       XBAGE  132H
     C*
     C                     MOVE *ALL'?'   LCDAT
     C                     Z-ADDLCD1      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    LCDAT
     C*
     C*   11am Term Indicator.
     C*
     C           TERM      IFEQ 0
     C           RTSP      DIV  100       XTSPWF 119H
     C           PCPL      MULT XTSPWF    TOTWF  154H
     C                     ADD  TOTWF     TOT0   154
     C                     ADD  PCPL      PCPL0
     C                     ADD  NAID      NAID0
     C                     MOVE TRM,1     TERMD
     C                     ELSE
     C*
     C*   24hr Term Indicator.
     C*
     C           TERM      IFEQ 1
     C           RTSP      DIV  100       XTSPWF
     C           PCPL      MULT XTSPWF    TOTWF
     C                     ADD  TOTWF     TOT1
     C                     ADD  PCPL      PCPL1
     C                     ADD  NAID      NAID1
     C                     MOVE TRM,2     TERMD
     C                     ELSE
     C*
     C*   Call Deal Money (Excluding 11am & 24hr money) Indicator.
     C*
     C           TERM      IFGT 1
     C           TERM      IFLT 9999
     C           RTSP      DIV  100       XTSPWF
     C           PCPL      MULT XTSPWF    TOTWF
     C                     ADD  TOTWF     TOT2
     C                     ADD  PCPL      PCPL2
     C                     ADD  NAID      NAID2
     C                     Z-ADDTERM      TEMP3   30
     C                     MOVELTEMP3     TERMD
     C                     MOVE ' DAYS'   TERMD
     C                     ELSE
     C*
     C*   Term Deal Term Indicator.
     C*
     C           RTSP      DIV  100       XTSPWF
     C           PCPL      MULT XTSPWF    TOTWF
     C                     ADD  TOTWF     TOT3
     C                     ADD  PCPL      PCPL3
     C                     ADD  NAID      NAID3
     C                     Z-ADDMDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE *BLANKS   TERMD                          046940
     C                     MOVE ZADATE    TERMD
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C*   Overflow.
     C*
     C                     EXSR SR9001
     C*
     C*   Output Detail line for DEAL.
     C*
     C           FLAGA     IFEQ 'Y'
     C                     WRITEDCDET
     C                     ADD  1         OUTA
     C                     ELSE
     C*
     C*   Output Detail line for LOAN.
     C*
     C           FLAGB     IFEQ 'Y'
     C                     WRITEBCDET
     C                     ADD  1         OUTB
     C                     END
     C                     END
      *                                                                   CDL006
      ** Output detail line for fiduciary deals.                          CDL006
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C                     WRITEDETL3                                     CDL006
     C                     ADD  1         OUTD                            CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C                     WRITEDETL4                                     CDL006
     C                     ADD  1         OUTE                            CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     SETOF                     10
     C                     ADD  1         A       20
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*            SRL1T  - Term Indicator Change.        *
     C*                                                   *
     C*****************************************************
     C*
     C           SRL1T     BEGSR
     C*
     C*   11am Term Indicator.
     C*
     C           XERM      IFEQ 0
     C           PCPL0     IFNE *ZEROS
     C           TOT0      DIV  PCPL0     DTWTAV    H
     C           DTWTAV    MULT 100       DTWTAV
     C           PCPL0     DIV  100       DTPCPL    H
     C                     END
     C                     Z-ADDNAID0     DTNAID
     C*
     C                     ADD  TOT0      TOTC
     C                     ADD  PCPL0     PCPLC
     C                     ADD  NAID0     NAIDC
     C                     ADD  TOT0      TOT0B0
     C                     ADD  PCPL0     PCPLB0
     C                     ELSE
     C*
     C*   24hr Term Indicator.
     C*
     C           XERM      IFEQ 1
     C           PCPL1     IFNE *ZEROS
     C           TOT1      DIV  PCPL1     DTWTAV    H
     C           DTWTAV    MULT 100       DTWTAV
     C           PCPL1     DIV  100       DTPCPL    H
     C                     END
     C                     Z-ADDNAID1     DTNAID
     C*
     C                     ADD  TOT1      TOTC
     C                     ADD  PCPL1     PCPLC
     C                     ADD  NAID1     NAIDC
     C                     ADD  TOT1      TOT1B1
     C                     ADD  PCPL1     PCPLB1
     C                     ELSE
     C*
     C*   Call Deal (Excluding 11am & 24hr money) Indicator.
     C*
     C           XERM      IFGT 1
     C           XERM      IFLT 9999
     C           PCPL2     IFNE *ZEROS
     C           TOT2      DIV  PCPL2     DTWTAV    H
     C           DTWTAV    MULT 100       DTWTAV
     C           PCPL2     DIV  100       DTPCPL    H
     C                     END
     C                     Z-ADDNAID2     DTNAID
     C*
     C                     ADD  TOT2      TOTC
     C                     ADD  PCPL2     PCPLC
     C                     ADD  NAID2     NAIDC
     C                     ADD  TOT2      TOT2B2
     C                     ADD  PCPL2     PCPLB2
     C                     ELSE
     C*
     C*   Term Deal Term Indicator.
     C*
     C           PCPL3     IFNE *ZEROS
     C           TOT3      DIV  PCPL3     DTWTAV    H
     C           DTWTAV    MULT 100       DTWTAV
     C           PCPL3     DIV  100       DTPCPL    H
     C                     END
     C                     Z-ADDNAID3     DTNAID
     C*
     C                     ADD  TOT3      TOTC
     C                     ADD  PCPL3     PCPLC
     C                     ADD  NAID3     NAIDC
     C                     ADD  TOT3      TOT3B3
     C                     ADD  PCPL3     PCPLB3
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C*   Overflow.
     C*
     C                     EXSR SR9000
     C*
     C*   Output Totals for Term Change, DEAL.
     C*
     C           FLAGA     IFEQ 'Y'
     C           A         IFGT 1
     C                     WRITEDCTOT
     C                     ELSE
     C                     WRITESPACED
     C                     END
     C                     ELSE
     C*
     C*   Output Totals for Term Change, LOAN.
     C*
     C           FLAGB     IFEQ 'Y'
     C           A         IFGT 1
     C                     WRITEBCTOT
     C                     ELSE
     C                     WRITESPACEL
     C                     END
     C                     END
     C                     END
      *                                                                   CDL006
      ** Output Totals for fiduciary deals.                               CDL006
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C           A         IFGT 1                                         CDL006
     C                     WRITECTOT3                                     CDL006
     C                     ELSE                                           CDL006
     C                     WRITESPACE3                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C           A         IFGT 1                                         CDL006
     C                     WRITECTOT4                                     CDL006
     C                     ELSE                                           CDL006
     C                     WRITESPACE4                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     ADD  A         B       20
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*         SRL2T  - Customer Short Name Change.      *
     C*                                                   *
     C*****************************************************
     C*
     C           SRL2T     BEGSR
     C*
     C*   Calculate output fields for Customer Totals.
     C*
     C           PCPLC     IFNE 0
     C           TOTC      DIV  PCPLC     DCWTAV    H
     C           DCWTAV    MULT 100       DCWTAV
     C                     ELSE
     C                     Z-ADD0         DCWTAV
     C                     END
     C                     Z-ADDNAIDC     DCNAID
     C           PCPLC     DIV  100       DCPCPL    H
     C*
     C*   Overflow.
     C*
     C           FLAGA     IFEQ 'Y'
     C           EFICLA    IFGE 56
     C           B         IFGT 1
     C                     WRITEHEADERA
     C                     END
     C                     END
     C                     ELSE
     C           FLAGB     IFEQ 'Y'
     C           EFICLB    IFGE 56
     C           B         IFGT 1
     C                     WRITEHEADERB
     C                     END
     C                     END
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C           PRTLN3    IFGE 56                                        CDL006
     C           B         ANDGT1                                         CDL006
     C                     WRITEHEADR3                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C           PRTLN4    IFGE 56                                        CDL006
     C           B         ANDGT1                                         CDL006
     C                     WRITEHEADR4                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C*   Deposit Type is DEAL.
     C*
     C           FLAGA     IFEQ 'Y'
     C           B         IFGT 1
     C                     WRITEDCTOTL
     C                     ELSE
     C                     WRITESPACED
     C                     END
     C                     ELSE
     C*
     C*   Deposit Type is LOAN.
     C*
     C           FLAGB     IFEQ 'Y'
     C           B         IFGT 1
     C                     WRITEBCTOTL
     C                     ELSE
     C                     WRITESPACEL
     C                     END
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C           B         IFGT 1                                         CDL006
     C                     WRITECTOTL3                                    CDL006
     C                     ELSE                                           CDL006
     C                     WRITESPACE3                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C           B         IFGT 1                                         CDL006
     C                     WRITECTOTL4                                    CDL006
     C                     ELSE                                           CDL006
     C                     WRITESPACE4                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*          SRL4T  - Company Code Change.            *
     C*                                                   *
     C*****************************************************
     C*
     C           SRL4T     BEGSR
     C*
     C*   Accumulate Principal Amounts for Company Totals.
     C*
     C           PCPLB0    DIV  100       BTRM0     H
     C           PCPLB1    DIV  100       BTRM1     H
     C           PCPLB2    DIV  100       BTRM2     H
     C           PCPLB3    DIV  100       BTRM3     H
     C*
     C*   11am Princ. Total is Not 0.
     C*
     C           PCPLB0    IFNE *ZEROS
     C           TOT0B0    DIV  PCPLB0    @BRAT0 106H
     C           @BRAT0    MULT 100       BRATE0
     C                     END
     C*
     C*   24hr Princ. Total is Not 0.
     C*
     C           PCPLB1    IFNE *ZEROS
     C           TOT1B1    DIV  PCPLB1    @BRAT1 106H
     C           @BRAT1    MULT 100       BRATE1
     C                     END
     C*
     C*   Call Deal (Excluding 11am & 24hr money) Princ. Total is Not 0.
     C*
     C           PCPLB2    IFNE *ZEROS
     C           TOT2B2    DIV  PCPLB2    @BRAT2 106H
     C           @BRAT2    MULT 100       BRATE2
     C                     END
     C*
     C*   Term Deal Princ. Total is Not 0.
     C*
     C           PCPLB3    IFNE *ZEROS
     C           TOT3B3    DIV  PCPLB3    @BRAT3 106H
     C           @BRAT3    MULT 100       BRATE3
     C                     END
     C*
     C*   Accumulate All Term Totals for Company.
     C*
     C           BTRM0     ADD  BTRM1     WORKB3 132
     C           WORKB3    ADD  BTRM2     WORKB3
     C           WORKB3    ADD  BTRM3     BTOTAL
     C*
     C           TOT0B0    ADD  TOT1B1    WORKB4 132
     C           WORKB4    ADD  TOT2B2    WORKB4
     C           WORKB4    ADD  TOT3B3    WORKB5 132
     C*
     C           BTOTAL    IFNE 0
     C           WORKB5    DIV  BTOTAL    BRTOTL    H
     C                     ELSE
     C                     Z-ADD0         BRTOTL
     C                     END
     C*
     C*   Overflow.
     C*
     C           FLAGA     IFEQ 'Y'
     C           EFICLA    IFGE 54
     C                     WRITEHEADERA
     C                     END
     C                     ELSE
     C           FLAGB     IFEQ 'Y'
     C           EFICLB    IFGE 54
     C                     WRITEHEADERB
     C                     END
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C           PRTLN3    ANDGE54                                        CDL006
     C                     WRITEHEADR3                                    CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C           PRTLN4    ANDGE54                                        CDL006
     C                     WRITEHEADR4                                    CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C*   Deposit Type is DEAL.
     C*
     C           FLAGA     IFEQ 'Y'
     C                     WRITEDBTOT                  20
     C                     ELSE
     C*
     C*   Deposit Type is LOAN.
     C*
     C           FLAGB     IFEQ 'Y'
     C                     WRITEBBTOT                  20
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C                     WRITEBTOT3                  20                 CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C                     WRITEBTOT4                  20                 CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*       SRL3T  - Deposit Loan Indicator Change.     *
     C*                                                   *
     C*****************************************************
     C*
     C           SRL3T     BEGSR
     C*
     C*   Close Appropriate Printer File.
     C*
     C           FLAGA     IFEQ 'Y'
     C                     WRITEFINALA
     C                     MOVE 'N'       FLAGA
     C                     ELSE
     C           FLAGB     IFEQ 'Y'
     C                     WRITEFINALB
     C                     MOVE 'N'       FLAGB
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C                     EXSR SR9001                                    CDL006
     C                     WRITEEORPT3                                    CDL006
     C                     MOVE 'N'       FLAGC                           CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C                     EXSR SR9001                                    CDL006
     C                     WRITEEORPT4                                    CDL006
     C                     MOVE 'N'       FLAGD                           CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDSR
     C*
     C/EJECT
     C*
     C*****************************************************
     C*                                                   *
     C*            SR9000 - Overflow Check.               *
     C*                                                   *
     C*****************************************************
     C*
     C           SR9000    BEGSR
     C*
     C*   Active Printer File is DL7071P1.
     C*
     C           FLAGA     IFEQ 'Y'
     C*
     C*   Force New Page if Required.
     C*
     C           EFICLA    IFGE 53
     C                     WRITEHEADERA
     C                     END
     C                     ELSE
     C*
     C*   Active Printer File is DL7071P2.
     C*
     C           FLAGB     IFEQ 'Y'
     C*
     C*   Force New Page if Required.
     C*
     C           EFICLB    IFGE 53
     C                     WRITEHEADERB
     C                     END
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C           PRTLN3    ANDGE53                                        CDL006
     C                     WRITEHEADR3                                    CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C           PRTLN4    ANDGE53                                        CDL006
     C                     WRITEHEADR4                                    CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************
     C*                                                   *
     C*       SR9001 - Overflow Check for Detail Time.    *
     C*                                                   *
     C*****************************************************
     C*
     C*
     C           SR9001    BEGSR
     C*
     C*   Active Printer File is DL7071P1.
     C*
     C           FLAGA     IFEQ 'Y'
     C*
     C*   Force New Page if Required.
     C*
     C           EFICLA    IFGE 58
     C*                                                                   048550
     C**  Check if DL7071P1 is open                                       048550
     C*                                                                   048550
     C           *IN51     IFEQ '0'                                       048550
     C                     OPEN DL7071P1                                  048550
     C                     MOVE '1'       *IN51                           048550
     C                     END                                            048550
     C*                                                                   048550
     C                     WRITEHEADERA
     C                     END
     C                     ELSE
     C*
     C*   Active Printer File is DL7071P2.
     C*
     C           FLAGB     IFEQ 'Y'
     C*
     C*   Force New Page if Required.
     C*
     C           EFICLB    IFGE 58
     C*                                                                   048550
     C**  Check if DL7071P2 is open                                       048550
     C*                                                                   048550
     C           *IN52     IFEQ '0'                                       048550
     C                     OPEN DL7071P2                                  048550
     C                     MOVE '1'       *IN52                           048550
     C                     END                                            048550
     C*                                                                   048550
     C                     WRITEHEADERB
     C                     END
     C                     END
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           FLAGC     IFEQ 'Y'                                       CDL006
     C           PRTLN3    ANDGE58                                        CDL006
     C           WOPN3     IFEQ 'N'                                       CDL006
     C                     OPEN DL7071P3                                  CDL006
     C                     MOVE 'Y'       WOPN3                           CDL006
     C                     ENDIF                                          CDL006
     C                     WRITEHEADR3                                    CDL006
     C                     ENDIF                                          CDL006
     C           FLAGD     IFEQ 'Y'                                       CDL006
     C           PRTLN4    ANDGE58                                        CDL006
     C           WOPN4     IFEQ 'N'                                       CDL006
     C                     OPEN DL7071P4                                  CDL006
     C                     MOVE 'Y'       WOPN4                           CDL006
     C                     ENDIF                                          CDL006
     C                     WRITEHEADR4                                    CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*************************************************************
     C*     SR9999 - LR PROCESSING                                *
     C*************************************************************
     C*
     C           SR9999    BEGSR
     C*
     C*    If no records on DEALS file output reports with no details.
     C*
     C           *IN11     IFEQ '0'
     C                     EXSR SR0000
     C*
     C                     END
     C           FILOPA    IFEQ 'Y'
     C*
     C*   If no details reported on deposits report then ouput headings.
     C*   If no Database Error and no details printed,                    048550
     C*   print 'No Details To Report'                                    048550
     C*
     C           OUTA      IFEQ 0
     C           *INU7     ANDEQ'0'                                       048550
     C                     WRITENDTRA
     C                     WRITEFINALA                                    048550
     C                     END
     C           *INU7     IFEQ '1'
     C                     WRITEDBERRA
     C                     WRITEFINALA                                    048550
     C                     END
     C*
     C*   End of deposits report.
     C*
     C***********          WRITEFINALA                                    048550
     C                     END
     C*
     C           FILOPB    IFEQ 'Y'
     C*
     C*   If no details reported on loans report then ouput headings.
     C*
     C           OUTB      IFEQ 0
     C           *INU7     ANDEQ'0'                                       048550
     C                     WRITENDTRB
     C                     WRITEFINALB                                    048550
     C                     END
     C           *INU7     IFEQ '1'
     C                     WRITEDBERRB
     C                     WRITEFINALB                                    048550
     C                     END
     C*
     C*   End of loans report.
     C*
     C***********          WRITEFINALB                                    048550
     C                     END
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
      *                                                                   CDL006
     C           WOPN3     IFEQ 'Y'                                       CDL006
     C           OUTD      IFEQ 0                                         CDL006
     C           *INU7     ANDEQ'0'                                       CDL006
     C                     WRITENODTL3                                    CDL006
     C                     WRITEEORPT3                                    CDL006
     C                     ENDIF                                          CDL006
     C           *INU7     IFEQ '1'                                       CDL006
     C                     WRITEDBERR3                                    CDL006
     C                     WRITEEORPT3                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
     C           WOPN4     IFEQ 'Y'                                       CDL006
     C           OUTE      IFEQ 0                                         CDL006
     C           *INU7     ANDEQ'0'                                       CDL006
     C                     WRITENODTL4                                    CDL006
     C                     WRITEEORPT4                                    CDL006
     C                     ENDIF                                          CDL006
     C           *INU7     IFEQ '1'                                       CDL006
     C                     WRITEDBERR4                                    CDL006
     C                     WRITEEORPT4                                    CDL006
     C                     ENDIF                                          CDL006
     C                     ENDIF                                          CDL006
      *                                                                   CDL006
     C                     ENDIF                                          CDL006
     C*
     C*    Calculate Total Records Output.
     C*
     C           OUTA      ADD  OUTB      OUTC
      *                                                                   CDL006
     C           CDL006    IFEQ 'Y'                                       CDL006
     C                     ADD  OUTD      OUTC                            CDL006
     C                     ADD  OUTE      OUTC                            CDL006
     C                     ENDIF                                          CDL006
     C*
     C** Report printer file to RCF via ZSFILE
     C*
     C                     Z-ADDSFNUMU    ZSNUM   60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM *BLANKS   @ENT    3
     C                     PARM           SFILEU
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
     C*
     C** Error during ZSFILE processing, return to calling program
     C*
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     GOTO FINN
     C                     END
     C*
     C*    Output Control Report
     C*
     C                     Z-ADD1         PAGE
     C                     WRITEH70713
     C*
     C           *INU7     IFNE '0'
     C                     WRITED70713
     C                     END
     C           OUTC      IFEQ 0
     C                     WRITENODETL
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C*
     C*  ERROR DUMP SUB-ROUTINE
     C           *PSSR     BEGSR                           *** *PSSR  ***
     C*
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     OUT  LDA
     C                     END
     C                     ENDSR                           * *PSSR  ENDSR*
     C*
     C********************************************************************
     C**
     C/EJECT
     C/COPY ZSRSRC,ZDATE2Z2
     C/SPACE 3
** TRM **
 11 AM
 24 HR
**   ZYDY - YEARS IN DAYS CUMULATIVE IN FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
