     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('MIDAS - Spot date acnt key generation.**new*')         *
      *****************************************************************
      *                                                               *
      *  MIDAS - Dealing Module                                       *
      *                                                               *
      *  DL8601 - Spot Date Account Key Generation.                   *
      *                                                               *
      *  Function: This program reads all live and reversed FX deals  *
      *            and generates an account key for each record with  *
      *            a value date<the spot event date. Spot event date  *
      *            is calculated as run date plus 3 working days in   *
      *            local currency. A new file PF/DEALSDX0 will be used*
      *            as the 'account keys already generated' file.      *
      *            This will prevent keys being generated twice.      *
      *                                                               *
      *  Called By: DLC8601 - Spot Date Account Keys Generation       *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CDL086             Date 30Oct12               *
      *  Prev Amend No. AR1014698 *CREATE  Date 27Oct17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL086 - Portuguese FX Revaluation Extensions                *
      *           Upgrade to FM 2.1 2021.A with fix AR1014698.        *
      *    AR1014698 - Reconciliation issues with Midas R4 system     *
      *                (Child: AR1014700). Upgrade LBM001 as part of  *
      *                CDL086.                                        *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX                                         *
      *****************************************************************
      *
      *  S U B R O U T I N E    I N D E X
      *  MAIN   - Main processing
      *  #LP01  - Initial processing
      *  #LP02  - Detail processing
      *  #LP021 - Generate account keys for deal
      *  #LP022 - Generate account keys for 'OP' deal reversal
      *  #LP03  - Final processing
      *           Write a/c keys trailer record
      *  #LP999 - Database error processing
      *  #LW01  - Write a/c keys header record
      *  #LW02  - Write a/c keys detail record - purchase
      *  #LW021 - Format a/c key for - purchase
      *  #LW022 - Write record to PF/DLSPKYPP & update hash totals
      *  #LW03  - Write a/c keys detail record - sale
      *  #LW031 - Format a/c keys for - sale
      *  #LW04  - Write a/c keys detail record - base ccy equiv
      *                                               purchase
      *  #LW041 - Format a/c key for - base ccy equiv purchase
      *  #LW05  - Write a/c keys detail record - base ccy equiv sale
      *  #LW051 - Format a/c key for base ccy equiv sale
      *  #LW06  - Write a/c keys detail record - total P/L
      *  #LW061   Format a/c key for - total P/L
      *  #LW08  - Write a/c keys detail record - deposit amt (1st le   g of PS)
      *  #LW081 - Format a/c key for - deposit amt (1st leg of PS)
      *  #LW09  - Write a/c keys detail record - loan amt (1st leg o   f PS)
      *  #LW091 - Format a/c key for - loan amt (1st leg of PS)
      *  #LW10  - Write a/c keys detail record - deposit amt (2nd le   g of PS)
      *  #LW101 - Format a/c key for - deposit amt (2nd leg of PS)
      *  #LW11  - Write a/c keys detail record - deposit amt (2nd le   g of PS)
      *  #LW111 - Format a/c key for - loan amt (2nd leg of swap)
      *  #LW12  - Write a/c keys detail record - deposit int. due at    mat
      *  #LW121 - Format a/c key for - deposit int. due at maturity
      *  #LW13  - Write a/c keys detail record - loan int. due at ma   turity
      *  #LW131 - Format a/c key for - loan int. due at maturity
      *  #LW14  - Write a/c keys detail record - total deposit
      *  #LW141 - Format a/c key for - total deposit
      *  #LW15  - Write a/c keys detail record - total loan
      *  #LW151 - Format a/c key for - total loan
      *  #LW16  - Write record to PF/DEALSDX0
      *  ZFWDT  - Calculate nos of days fwd in any ccy
      *  ZACCH  - Sets Up holiday array
      *  ZHASH  - Calculate hash totals for integers and decimals
      *  *PSSR  - Program status sub-routine
      *
      ********************************************************************
      /EJECT
      * Live FX deals. Prime input.
     FDEALSDV0IF  E           K        DISK         KINFSR *PSSR
      * ICD 1 file
     FSDBANKPDIF  E                    DISK         KINFSR *PSSR
      * Spot date accounting entries already generated
     FDEALSDX0UF  E           K        DISK         KINFSR *PSSR A
      * Spot date generated a/c keys header
     FDLSPKYPHO   E                    DISK         KINFSR *PSSR
      * Spot date generated a/c keys detail
     FDLSPKYPPO   E                    DISK         KINFSR *PSSR
      * Spot date generated a/c keys trailer
     FDLSPKYPTO   E                    DISK         KINFSR *PSSR
     F*
      /TITLE FUNCTION OF INDICATORS
     F*****************************************************************
     F*                                                               *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*       10         End of file                                  *
     F*       11         Accounting entry generated/Reversal          *
     F*       55         Chain fail                                   *
     F*       90 - 97    Used in standard sub-routines                *
     F*                                                               *
     F*****************************************************************
      /EJECT
      *
      * Tables/Arrays
      *
     E                    CPY@    1   1 80
      *** Array for Object Statement
      *
      * Calculate hash totals for integers and decimals.
     E                    INT         1 15 0             ZHASH integers
     E                    DEC         1  3 0             ZHASH decimals
     E/COPY ZSRSRC,ZHOLE
      *** Array to hold individual days in a year
      *
      /EJECT
      *
      **  Data structures :
      *
      * Calculate hash totals for integers and decimals.
      /COPY ZSRSRC,ZHASHZ2
      *
     I/COPY ZSRSRC,ZHOLI
      *** Data structure to define holiday file fields
     I/COPY ZSRSRC,ZHOLDS
      *** Data structure required for the call of the access program
      *
      **   Format spot date account key
      *
     I            DS
     I                                        1   2 DTYP
     I                                        3   3 @ETYP
     I                                        4   5 DLST
     I                                        6   8 @CCY
     I                                        9   9 @FBSE
     I                                       10  10 @PURC
     I                                        1  10 @AKEY
      *
      ** Local data area
      *
     ILDA       E DSLDA
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  MAIN PROCESSING                                              *
      *                                                               *
      *  Called by   *none                                            *
      *                                                               *
      *  Calls       #LP01 - Initial processing                       *
      *              #LP02 - Detail processing                        *
      *              #LP03- Final processing                          *
      *****************************************************************
      *
      * Execute initial procesing.
     C                     EXSR #LP01
      * Execute detail procesing.
     C                     EXSR #LP02
      * Execute final procesing.
     C                     EXSR #LP03
      * End program.
     C                     SETON                     LR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LP02  SR.    Detail processing.                             *
      *                                                               *
      *  Called by   Main - Main processing                           *
      *                                                               *
      *  Calls       #LP021 - Generate account keys for deal          *
      *              #LP022 Generate account keys for OP deal reversal*
      *******************************************************************
      *
     C           #LP02     BEGSR                           ***  #LP02 ****
      *
      * Read a LIVE FX deal from file
     C                     READ DEALSDV0                 10
      * Do while not EOF
     C           *IN10     DOWEQ'0'                        B001
      * If deal type is NE 'OP' and
      * value date less then spot date event date
      * or Deal type = 'OP' and the option to date is less then
      * the spot event date.
     C           VDAT      IFLT @SEVD                      B002
     C           DTYP      ANDNE'OP'                        002
     C           DTYP      OREQ 'OP'                        002
     C           OTDT      ANDLT@SEVD                       002
      *
      * Check if an accounting entry has already been generated
     C           DLNO      CHAINDEALSDX0             11     002
      * No and a live deal then do ...
     C           *IN11     IFEQ '1'                        B003
     C           RECI      ANDEQ'D'                         003
      * #LP021 Generate account keys for deal
     C                     EXSR #LP021                      003
      * If an option take-down account key produced during
      * spot event date period then check if an option purchase
      * reversal account key needs to be produced ?
     C           DTYP      IFEQ 'OT'                       B004
      * #LP022 Generate account keys for 'OP' reversal OF 'OT' amount
     C                     EXSR #LP022                      004
     C                     END                             E004
      *
     C                     ELSE                            X003
      * Produce a reversal account key if one not already produced
      * for a deal entered already
     C           RECI      IFEQ 'R'                        B004
     C           *IN11     ANDEQ'0'                         004
      * #LP021 Generate account keys for deal
     C                     EXSR #LP021                      004
      * If an option take-down reversal account key produced during
      * spot event date period then check if an option purchase
      * account key needs to be produced ?
     C           DTYP      IFEQ 'OT'                       B005
      * P023 - Generate account keys for 'OP' deal
     C                     EXSR P023                        005
     C                     END                             E005
     C                     END                             E004
     C                     END                             E003
     C                     END                             E002
      * Read a LIVE FX deal from file
     C                     READ DEALSDV0                 10 001
     C                     END                             E001
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LP021 SR.    Generate account keys for deal                 *
      *                                                               *
      *  Called by   #LP02 - Detail  processing                       *
      *              #LP022 Generate account keys for OP deal reversal*
      *              P023 - Generate account keys for OP deal         *
      *                                                               *
      *  Calls       #LW02- Write a/c keys detail record - purchase   *
      *              #LW03- Write a/c keys detail record - sale       *
      *              #LW04- Write a/c keys detail record - base ccy   *
      *                                              equiv purchase   *
      *              #LW05- Write a/c keys detail record - base ccy   *
      *                                                  equiv sale   *
      *              #LW06- Write a/c keys detail record - total P/L  * *
      *              #LW08- Write a/c keys detail record - deposit amt*
      *                                             (1st leg of PS)   *
      *              #LW09- Write a/c keys detail record - loan amt   *
      *                                             (1st leg of PS)   *
      *              #LW10- Write a/c keys detail record - deposit amt*
      *                                             (2nd leg)         *
      *              #LW11- Write a/c keys detail record - deposit amt*
      *                                             (2nd leg of PS)   *
      *              #LW12- Write a/c keys detail record -            *
      *                                deposit int. due at maturity   *
      *              #LW13- Write a/c keys detail record -            *
      *                                loan int. due at maturity      *
      *              #LW14- Write a/c keys detail record - total dep  *
      *              #LW15- Write a/c keys detail record - total loan *
      *              #LW16- Write record to PF/DEALSDX0               *
      *****************************************************************
      *
     C           #LP021    BEGSR                           ***  #LP021****
      *
      * #LW02- Write a/c keys detail record - purchase
     C                     EXSR #LW02
      * #LW03- Write a/c keys detail record - sale
     C                     EXSR #LW03
      * #LW04- Write a/c keys detail record - base ccy
     C                     EXSR #LW04
      * #LW05- Write a/c keys detail record - base ccy
      *        equivilant sale
     C                     EXSR #LW05
      * Second leg of swap
     C           FSLI      IFEQ 2                          B001
     C           DTYP      ANDEQ'SW'                        001
      * #LW06- Write a/c keys detail record - total P/L
     C                     EXSR #LW06                       001
     C                     END                             E001
      * First leg of position swap deal
     C           FSLI      IFEQ 1                          B001
     C           DTYP      ANDEQ'PS'                        001
      * #LW08- Write a/c keys detail record - deposit amt
     C                     EXSR #LW08                       001
      * #LW09- Write a/c keys detail record - loan amount
     C                     EXSR #LW09                       001
     C                     END                             E001
      * Second leg of position swap deal
     C           FSLI      IFEQ 2                          B001
     C           DTYP      ANDEQ'PS'                        001
      * #LW10- Write a/c keys detail record - deposit amt
     C                     EXSR #LW10                       001
      * #LW11- Write a/c keys detail record - loan amount
     C                     EXSR #LW11                       001
      * #LW12- Write a/c keys detail record - deposit interest due
      *                                        at maturity
     C                     EXSR #LW12                       001
      * #LW13- Write a/c keys detail record - deposit loan interest
      *                                        due at maturity
     C                     EXSR #LW13                       001
      * #LW14- Write a/c keys detail record - total deposit
     C                     EXSR #LW14                       001
      * #LW15- Write a/c keys detail record - total loan
     C                     EXSR #LW15                       001
     C                     END                             E001
      * #LW16- Write record to PF/DEALSDX0
     C                     EXSR #LW16
      *
     C                     ENDSR                           ***  #LP021****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LP022 SR.    Generate account keys for OP deal              *
      *                (reverse amount of live OT)                    *
      *                                                               *
      *  Called by   #LP02 - Detail  processing                       *
      *                                                               *
      *  Calls       #LP021 Generate account keys for deal            *
      *****************************************************************
      *
     C           #LP022    BEGSR                           ***  #LP022****
      *
      * Save original deal number
     C                     Z-ADDDLNO      @DLNO
      * Check if an accounting entry has already been generated for
      * 'OP' ?
     C           SODN      CHAINDEALSDX0             11
      * If a live deal then do ...
     C           *IN11     IFEQ '0'                        B001
     C           LRUND     ANDLTBJRDNB                      001
      * Save contents of 'OT' deal amount for later use
      * Purchase amt
     C                     Z-ADDPUAM      @WRKP  130        001
      * Sale amt
     C                     Z-ADDSLAM      @WRKS  130        001
      * Base currency equivalent
     C                     Z-ADDBCEQ      @WRKB  130        001
      * Get details of the 'OP' deal from DEALSDB
     C                     MOVE 'OP'      #DTYP             001
     C                     Z-ADDSODN      #DLNO             001
     C           DKEY      CHAINDEALSDBF             11     001
      * Database error ..
     C           *IN11     IFEQ '1'                        B002
     C                     MOVEL'DEALSDB' DBFILE           ***************
     C                     MOVELSODN      DBKEY            ** DBERR 002 **
     C                     Z-ADD2         DBASE            ***************
     C                     EXSR #LP999                      002
     C                     END                             E002
      * Restore contents of 'OT' deal amount into 'OP' deal fields
     C                     Z-ADD@WRKP     PUAM              001
     C                     Z-ADD@WRKS     SLAM              001
     C                     Z-ADD@WRKB     BCEQ              001
      *
      * #LP021 Generate account keys for deal
     C                     EXSR #LP021                      001
      * Restore file pointer to the correct position on file before
      * next read.
     C                     MOVE 'OT'      #DTYP             001
     C                     Z-ADD@DLNO     #DLNO             001
     C           DKEY      CHAINDEALSDBF             55     001
     C                     END                             E001
      *
     C                     ENDSR                           ***  #LP022****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  P023   SR.    Generate account keys for OP deal              *
      *                (add back amount of OT reversed)               *
      *                                                               *
      *  Called by   #LP02 - Detail  processing                       *
      *                                                               *
      *  Calls       #LP021 Generate account keys for deal            *
      *****************************************************************
      *
     C           P023      BEGSR                           ***  P023  ****
      * Save original deal number
     C                     Z-ADDDLNO      @DLNO   60
      *
      * Check if an accounting entry has already been generated for
      * 'OP' ?
     C           SODN      CHAINDEALSDX0             11
      * If a live deal then do ...
     C           *IN11     IFEQ '0'                        B001
      * Save contents of 'OT' deal amount for later use
      * Purchase amt
     C                     Z-ADDPUAM      @WRKP             001
      * Sale amt
     C                     Z-ADDSLAM      @WRKS             001
      * Base currency equivalent
     C                     Z-ADDBCEQ      @WRKB             001
      * Value date
     C                     Z-ADDVDAT      @WRKV   50        001
      * Get details of the 'OP' deal from DEALSDB
     C                     MOVE 'OP'      #DTYP             001
     C                     Z-ADDSODN      #DLNO             001
     C           DKEY      CHAINDEALSDBF             11     001
      * Database error ..
     C           *IN11     IFEQ '1'                        B002
     C                     MOVEL'DEALSDB' DBFILE           ***************
     C                     MOVELSODN      DBKEY            ** DBERR 003 **
     C                     Z-ADD3         DBASE            ***************
     C                     EXSR #LP999                      002
     C                     END                             E002
      * Deal not already reversed
      * Restore contents of 'OT' deal amount into 'OP' deal fields
     C                     Z-ADD@WRKP     PUAM              001
     C                     Z-ADD@WRKS     SLAM              001
     C                     Z-ADD@WRKB     BCEQ              001
     C                     Z-ADD@WRKV     VDAT              001
     C                     SETON                     11     001
      * #LP021 Generate account keys for deal
     C                     EXSR #LP021                      001
      * Restore file pointer to the correct position on file before
      * next read.
     C                     MOVE 'OT'      #DTYP             001
     C                     Z-ADD@DLNO     #DLNO             001
     C           DKEY      CHAINDEALSDBF             55     001
     C                     END                             E001
      *
     C                     ENDSR                           ***  P023  ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW01  SR.    Add header record to PF/DLSPKYPH.              *
      *                                                               *
      *  Called by   #LP01 - Initial processing                       *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
     C           #LW01     BEGSR                           ***  #LW01 ****
      *
     C                     MOVEL'H'       RECI
      * Insert spot date a/c keys generated header record.
     C                     WRITEDKEYSHHF
      *
     C                     ENDSR                           ***  #LW01 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW02  SR.    Add detail record to PF/DLSPKYPP.              *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW021 Generate spot date a/c key - purchase     *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW02     BEGSR                           ***  #LW02 ****
      *
     C                     MOVEL'D'       RECI
      * Format account key
     C                     EXSR #LW021
      * Deal number
      *
      * Customer number
      *
      * Branch code
      *
      * Account sequence number
     C           ZZ002     IFEQ '00'                       B001
     C           ZZ002     OREQ *BLANKS                     001
     C                     Z-ADD1         ACSQ              001
     C                     ELSE                            X001
     C                     MOVELZZ002     ACSQ              001
     C                     END                             E001
      * Event date = value date.
      *  *OR option-to-date if 'OP' deal
     C           DTYP      IFEQ 'OP'                       B001
     C                     Z-ADDOTDT      EDAT              001
     C                     ELSE                            X001
     C                     Z-ADDVDAT      EDAT              001
     C                     END                             E001
      * Event amount =  purchase amt
     C                     Z-ADDPUAM      EAMT
      * Event currrency = purchase ccy
     C                     MOVELPUCY      ECCY
      * Settle currency = receipt settle currency                                             IPL026
     C                     MOVELRSCY      STCY                                                IPL026
      * Settlement type = purchase settlement type
     C                     MOVELPCST      SETP
      * Our settlement account number = our purchase a/c number
     C                     MOVELOPCA      OSAC
      * Reversal indicator
      * Live deal read in
     C           *IN11     IFEQ '1'                        B001
     C                     Z-ADD0         REVI              001
      * Reversal deal read in
     C                     ELSE                            X001
     C                     Z-ADD1         REVI              001
     C                     END                             E001
      * Other amount = sale amount
     C                     Z-ADDSLAM      OTHA
      * Other currency = sell currency
     C                     MOVELSLCY      OTHC
      * Exchange rate
      *
      * Value date
      *
      * Start date or last interest date
     C                     Z-ADD0         SLID
      * Maturity date
     C                     Z-ADD0         MDAT
      * Base rate codes
     C                     Z-ADD0         BRTT
      * Current base rate
     C                     Z-ADD0         BRTE
      * Rate/Spread
     C                     Z-ADD0         RTSP
      * Interest rate
     C                     Z-ADD0         INTR
      * Federal funds value date
     C                     Z-ADD0         FFVD
      * Yield rate
     C                     Z-ADD0         YRAT
      * Federal funds indicator
     C                     MOVE *BLANKS   FFIN
      * Current holder of N.A.
     C*******************  Z-ADD0         CHNACH
     C                     MOVE *BLANKS   CHNACH
      * For account of
      *
      * Profit centre
     C                     MOVE *BLANKS   PRFC
      * Our settlement branch
     C                     MOVE *BLANKS   OSBR
      * Book code
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW02 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW03  SR.    Add detail record to PF/DLSPKYPP.              *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW031 Generate spot date a/c key - sale         *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW03     BEGSR                           ***  #LW03 ****
      * Format account key
     C                     EXSR #LW031
      * Event amount = sale amount
     C                     Z-ADDSLAM      EAMT
      * Event currrency = sell ccy
     C                     MOVELSLCY      ECCY
      * Settle currency = payment settle currency                                             IPL026
     C                     MOVELPSCY      STCY                                                IPL026
      * Settlement type = sell settlement type
     C                     MOVELSCST      SETP
      * Our settlement account number = our sell a/c number
     C                     MOVELOSCA      OSAC
      * Other amount = purchase amount
     C                     Z-ADDPUAM      OTHA
      * Other currency = purchase currency
     C                     MOVELPUCY      OTHC
      * Write record and update hash totals
     C                     EXSR #LW022
      *
     C                     ENDSR                           ***  #LW03 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW04  SR.    Add detail record to PF/DLSPKYPP.              *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW041 Generate spot date a/c key -              *
      *                     Purchase base ccy equivalent              *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW04     BEGSR                           ***  #LW04 ****
      * Format account key
     C                     EXSR #LW041
      * Event amount = base ccy equivalent
     C                     Z-ADDBCEQ      EAMT
      * Event currrency = Base ccy from SDBANKPD.
     C                     MOVELBJCYCD    ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Settlement type = purchase ccy settlement type
     C                     MOVELPCST      SETP
      * Our settlement account number = our purchase a/c number
     C                     MOVE OPCA      OSAC
      * Other amount = sale amount
     C                     Z-ADDSLAM      OTHA
      * Other currency = sell currency
     C                     MOVELSLCY      OTHC
      * Write record and update hash totals
     C                     EXSR #LW022
      *
     C                     ENDSR                           ***  #LW04 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW05  SR.    Add detail record to PF/DLSPKYPP.             *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW051 Generate spot date a/c key -              *
      *                     Sale base ccy equivalent                  *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW05     BEGSR                           ***  #LW05 ****
      * Format account key
     C                     EXSR #LW051
      * Event amount = base ccy equivalent
     C                     Z-ADDBCEQ      EAMT
      * Event currrency = Base ccy from SDBANKPD.
     C                     MOVELBJCYCD    ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Settlement type = sell settlement type
     C                     MOVELSCST      SETP
      * Our settlement account number = our sell a/c number
     C                     MOVELOSCA      OSAC
      * Other amount = purchase amount
     C                     Z-ADDPUAM      OTHA
      * Other currency = purchase currency
     C                     MOVELPUCY      OTHC
      * Write record and update hash totals
     C                     EXSR #LW022
      *
     C                     ENDSR                           ***  #LW05 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW06  SR.    Add detail record to PF/DLSPKYPP               *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW061 Generate spot date a/c key -              *
      *                     Total profit/loss                         *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW06     BEGSR                           ***  #LW06 ****
      * Format account key
     C                     EXSR #LW061
      * Event amount = Swap P & L ccy amount
      * Ensure that the value is +ve
     C           SPLA      IFLT 0                          B001
     C           -1        MULT SPLA      EAMT              001
     C                     ELSE                            X001
     C                     Z-ADDSPLA      EAMT              001
     C                     END                             E001
      * Only generate account key if event amt *NE 0
     C           EAMT      IFNE 0                          B001
      *
      * Event currrency = Swap P & L ccy
     C                     MOVELSPLC      ECCY              001
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Settlement type
      * If Swap p/l ccy *NE purchase ccy then
      * settlement type *EQ sell ccy settlement type
      * Else
      * settlement type *EQ purchase ccy settlement type
     C           SPLC      IFNE PUCY                       B002
     C                     MOVELSCST      SETP              002
     C                     ELSE                            X002
     C                     MOVELPCST      SETP              002
     C                     END                             E002
      * Our settlement account number = our sell a/c number
      * If Swap p/l ccy *NE purchase ccy then
      * Our settlement a/c no *EQ sell ccy settlement a/c no
      * Else
      * Our settlement a/c no *EQ purchase ccy settlement a/c no
     C           SPLC      IFNE PUCY                       B002
     C                     MOVELOSCA      OSAC              002
     C                     ELSE                            X002
     C                     MOVELOPCA      OSAC              002
     C                     END                             E002
      * Other amount = *ZEROS
     C                     Z-ADD0         OTHA              001
      * Other currency = *BLANKS
     C                     MOVEL*BLANKS   OTHC              001
      * Write record and update hash totals
     C                     EXSR #LW022                      001
     C                     END                             E001
      *
     C                     ENDSR                           ***  #LW06 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW08  SR.    Add detail record to PF/DLSPKYPP               *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW081 Generate spot date a/c key - deposit amt  *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW08     BEGSR                           ***  #LW08 ****
      * Format account key
     C                     EXSR #LW081
      * Event date = value date.
     C                     Z-ADDVDAT      EDAT
      * Event amount = sale amount
     C                     Z-ADDSLAM      EAMT
      * Event currrency = sale ccy
     C                     MOVELSLCY      ECCY
      * Settle currency = payment     currency                                                IPL026
     C                     MOVELPSCY      STCY                                                IPL026
      * Settlement type = sell settlement type
     C                     MOVELSCST      SETP
      * Our settlement account number = our sell a/c number
     C                     MOVELOSCA      OSAC
      * Other amount = purchase amount
     C                     Z-ADDPUAM      OTHA
      * Other currency = purchase currency
     C                     MOVELPUCY      OTHC
      * Store the exchange rate then initialise
     C                     Z-ADDEXRT      @EXRT  138
     C                     Z-ADD0         EXRT
      *
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW08 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW09  SR.    Add detail record to PF/DLSPKYPP.              *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW091 Generate spot date a/c key - loan amount  *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW09     BEGSR                           ***  #LW09 ****
      * Format account key
     C                     EXSR #LW091
      * Event amount = purchase  amount
     C                     Z-ADDPUAM      EAMT
      * Event currrency = purchase ccy
     C                     MOVELPUCY      ECCY
      * Settle currency = receipt     currency                                                IPL026
     C                     MOVELRSCY      STCY                                                IPL026
      * Write record and update hash totals
     C                     EXSR #LW022
      * Restore the original exchange rate
     C                     Z-ADD@EXRT     EXRT
     C                     ENDSR                           ***  #LW09 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW10  SR.    Add detail record to PF/DLSPKYPP.              *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW101 Generate spot date a/c key - deposit amt  *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW10     BEGSR                           ***  #LW10 ****
      * Format account key
     C                     EXSR #LW101
      * Event amount = internal deposit amt
     C                     Z-ADDIDAM      EAMT
      * Event currrency = internal deposit ccy
     C                     MOVELIDCY      ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Settlement type = sell settlement type
     C                     MOVELSCST      SETP
      * Our settlement account number = our sell a/c number
     C                     MOVELOSCA      OSAC
      * Other amount = purchase amount
     C                     Z-ADDPUAM      OTHA
      * Other currency = purchase currency
     C                     MOVELPUCY      OTHC
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW10 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW11  SR.    Add detail record to PF/DLSPKYPP.              *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW111 Generate spot date a/c key - loan amount  *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW11     BEGSR                           ***  #LW11 ****
      * Format account key
     C                     EXSR #LW111
      * Event amount = internal loan amt
     C                     Z-ADDILAM      EAMT
      * Event currrency = internal loan ccy
     C                     MOVELILCY      ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW11 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW12  SR.    Add detail record to PF/DLSPKYPP               *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW121 Generate spot date a/c key - deposit int  *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW12     BEGSR                           ***  #LW12 ****
      * Format account key
     C                     EXSR #LW121
      * Event amount = internal deposit interest amt
     C                     Z-ADDIDIA      EAMT
      * Event currrency = internal deposit ccy
     C                     MOVELIDCY      ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW12 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW13  SR.    Add detail record to PF/DLSPKYPP.              *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW131 Generate spot date a/c key - loan interest*
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP                            *
      *****************************************************************
      *
     C           #LW13     BEGSR                           ***  #LW13 ****
      * Format account key
     C                     EXSR #LW131
      * Event amount = internal loan interest amt
     C                     Z-ADDILIA      EAMT
      * Event currrency = internal loan ccy
     C                     MOVELILCY      ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW13 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW14  SR.    Add detail record to PF/DLSPKYPP               *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW141 Generate spot date a/c key - total deposit*
      *              #LW022 Write PF/DLSPKYPP and update hash total   *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW14     BEGSR                           ***  #LW14 ****
      * Format account key
     C                     EXSR #LW141
      * Event amount = internal deposit interest amt
      *                   + internal deposit amt
     C           IDAM      ADD  IDIA      EAMT
      * Event currrency = internal deposit ccy
     C                     MOVELIDCY      ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW14 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW15  SR.    Add detail record to PF/DLSPKYPP               *
      *                                                               *
      *  Called by   #LP021 Generate account keys for deal            *
      *                                                               *
      *  Calls       #LW151 Generate spot date a/c key - total loan   *
      *              #LW022 Write PF/DLSPKYPP and update hash totals  *
      *                                                               *
      *  Unles otherwise stated there is a straight transfer of       *
      *  data between DEALSDB and DLSPKYPP.                           *
      *****************************************************************
      *
     C           #LW15     BEGSR                           ***  #LW15 ****
      * Format account key
     C                     EXSR #LW151
      * Event amount = internal loan interest amt
      *                   + internal loan amt
     C           ILAM      ADD  ILIA      EAMT
      * Event currrency = internal loan ccy
     C                     MOVELILCY      ECCY
      * Settle currency = transaction currency                                                IPL026
     C                     MOVEL*BLANKS   STCY                                                IPL026
      * Write record and update hash totals
     C                     EXSR #LW022
     C                     ENDSR                           ***  #LW15 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW021 SR.  Generate spot rate a/c key - purchase            *
      *                                                               *
      *  Called by   #LW021 Generate spot rate a/c key - purchase     *
      *                                                               *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW021    BEGSR                           ***  #LW021****
      *
      * Set event type = '3' if 1st/2nd leg Swap ind. = 2
      * else, set event type = 2
     C           FSLI      IFEQ 2                          B001
     C                     MOVEL'3'       @ETYP             001
     C                     ELSE                            X001
     C                     MOVEL'2'       @ETYP             001
     C                     END                             E001
      * Blank out ccy.
     C                     MOVE *BLANKS   @CCY
      * If purchase ccy = base ccy then set foreign/base = 'B'
      * else set foreign/base = 'F'
     C           PUCY      IFEQ BJCYCD                     B001
     C                     MOVE 'B'       @FBSE             001
     C                     ELSE                            X001
     C                     MOVE 'F'       @FBSE             001
     C                     END                             E001
      * Indicate that this is a purchase deal
     C                     MOVE 'B'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW021****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW022 SR.    To write record to PF/DLSPKYPP and update hash *
      *                totals.                                        *
      *                                                               *
      *  Called by   #LW02- Write a/c keys detail record - purchase   *
      *              #LW03- Write a/c keys detail record - sale       *
      *              #LW04- Write a/c keys detail record - base ccy   *
      *                                              equiv purchase   *
      *              #LW05- Write a/c keys detail record - base ccy   *
      *                                                  equiv sale   *
      *              #LW06- Write a/c keys detail record - total P/L  * *
      *              #LW08- Write a/c keys detail record - deposit amt*
      *                                             (1st leg of PS)   *
      *              #LW09- Write a/c keys detail record - loan amt   *
      *                                             (1st leg of PS)   *
      *              #LW10- Write a/c keys detail record - deposit amt*
      *                                             (2nd leg)         *
      *              #LW11- Write a/c keys detail record - deposit amt*
      *                                             (2nd leg of PS)   *
      *              #LW12- Write a/c keys detail record -            *
      *                                deposit int. due at maturity   *
      *              #LW13- Write a/c keys detail record -            *
      *                                loan int. due at maturity      *
      *              #LW14- Write a/c keys detail record - total dep  *
      *              #LW15- Write a/c keys detail record - total loan *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
     C           #LW022    BEGSR                           ***  #LW022****
      * Insert spot date a/c keys generated detail record.
     C                     WRITEDKEYSDPF
      * Add one to total records output.
     C                     ADD  1         @TREC
      * Update hash totals
     C                     MOVE EAMT      ZZAMT
     C                     EXSR ZHASH
     C                     ENDSR                           ***  #LW022****
      *
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW031 SR.  Generate spot rate a/c key - sale                *
      *                                                               *
      *  Called by   #LW03- Generate spot rate a/c key - sale         *
      *                                                               *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW031    BEGSR                           ***  #LW031****
      *
      * Set event type = '3' if 1st/2nd leg Swap ind. = 2
      * else, set event type = 2
     C           FSLI      IFEQ 2                          B001
     C                     MOVEL'3'       @ETYP             001
     C                     ELSE                            X001
     C                     MOVEL'2'       @ETYP             001
     C                     END                             E001
      * Blank out ccy.
     C                     MOVE *BLANKS   @CCY
      * If sale  ccy = base ccy then set foreign/base = 'B'
      * else set foreign/base = 'F'
     C           SLCY      IFEQ BJCYCD                     B001
     C                     MOVE 'B'       @FBSE             001
     C                     ELSE                            X001
     C                     MOVE 'F'       @FBSE             001
     C                     END                             E001
      * Indicate that this is a sale deal
     C                     MOVE 'C'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW031****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW041 SR.  Generate spot rate a/c key -                     *
      *                purchase base ccy equivalent                   *
      *  Called by #LW04  - Generate spot rate a/c key -              *
      *                purchase base ccy equivalent                   *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW041    BEGSR                           ***  #LW041****
      *
      * Set event type = '3' if 1st/2nd leg Swap ind. = 2
      * else, set event type = 2
     C           FSLI      IFEQ 2                          B001
     C                     MOVEL'3'       @ETYP             001
     C                     ELSE                            X001
     C                     MOVEL'2'       @ETYP             001
     C                     END                             E001
      * Set ccy = purchase ccy
     C                     MOVE PUCY      @CCY
      * Blank out foreign/base indicator
     C                     MOVE ' '       @FBSE
      * Indicate that this is a purchase
     C                     MOVE 'P'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW041****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW051 SR.  Generate spot rate a/c key -                     *
      *                sale base ccy equivalent                       *
      *  Called by   #LW05- Generate spot rate a/c key -              *
      *                sale base ccy equivalent                       *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW051    BEGSR                           ***  #LW051****
      *
      * Set event type = '3' if 1st/2nd leg Swap ind. = 2
      * else, set event type = 2
     C           FSLI      IFEQ 2                          B001
     C                     MOVEL'3'       @ETYP             001
     C                     ELSE                            X001
     C                     MOVEL'2'       @ETYP             001
     C                     END                             E001
      * Set ccy = sale ccy
     C                     MOVE SLCY      @CCY
      * Blank out foreign/base indicator
     C                     MOVE ' '       @FBSE
      * Indicate that this is a sale
     C                     MOVE 'S'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW051****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW061 SR.  Generate spot rate a/c key -                     *
      *                total profil/loss                              *
      *  Called by   #LW06- Generate spot rate a/c key -              *
      *                      total profit/loss                        *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW061    BEGSR                           ***  #LW061****
      *
      * Set event type
     C                     MOVEL'3'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Blank out foreign/base
     C                     MOVE *BLANKS   @FBSE
      * Set up total profit/loss
     C           SPLA      IFGT 0                          B001
     C                     MOVEL'H'       @PURC             001
     C                     ELSE                            X001
     C                     MOVEL'L'       @PURC             001
     C                     END                             E001
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW061****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW081 SR.  Generate spot rate a/c key -                     *
      *                deposit amount                                 *
      *  Called by   #LW08- Generate spot rate a/c key -              *
      *                      deposit amount                           *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW081    BEGSR                           ***  #LW081****
      *
      * Set event type
     C                     MOVEL'2'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'D'       @FBSE
      * Set position 10
     C                     MOVEL'F'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW081****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW091 SR.  Generate spot rate a/c key -                     *
      *                loan amount                                    *
      *  Called by   #LW09- Generate spot rate a/c key -              *
      *                      loan amount                              *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW091    BEGSR                           ***  #LW091****
      *
      * Set event type
     C                     MOVEL'2'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'L'       @FBSE
      * Set position 10
     C                     MOVEL'G'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW091****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW101 SR.  Generate spot rate a/c key -                     *
      *                deposit  amount                                *
      *  Called by   #LW10- Generate spot rate a/c key -              *
      *                      deposit amount                           *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW101    BEGSR                           ***  #LW101****
      *
      * Set event type
     C                     MOVEL'3'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'D'       @FBSE
      * Set position 10
     C                     MOVEL'F'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW101****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW111 SR.  Generate spot rate a/c key -                     *
      *                loan amount                                    *
      *  Called by   #LW11- Generate spot rate a/c key -              *
      *                      loan amount                              *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW111    BEGSR                           ***  #LW111****
      *
      * Set event type
     C                     MOVEL'3'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'L'       @FBSE
      * Set position 10
     C                     MOVEL'G'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW111****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW121 SR.  Generate spot rate a/c key -                     *
      *                deposit interest                               *
      *  Called by   #LW12- Generate spot rate a/c key -              *
      *                      deposit interest                         *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW121    BEGSR                           ***  #LW121****
      *
      * Set event type
     C                     MOVEL'3'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'D'       @FBSE
      * Set position 10
     C                     MOVEL'I'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW121****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW131 SR.  Generate spot rate a/c key -                     *
      *                loan interest                                  *
      *  Called by   #LW13- Generate spot rate a/c key -              *
      *                      loan interest                            *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW131    BEGSR                           ***  #LW131****
      *
      * Set event type
     C                     MOVEL'3'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'L'       @FBSE
      * Set position 10
     C                     MOVEL'I'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW131****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW141 SR.  Generate spot rate a/c key -                     *
      *                total deposit                                  *
      *  Called by   #LW14- Generate spot rate a/c key -              *
      *                      total deposit                            *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW141    BEGSR                           ***  #LW141****
      *
      * Set event type
     C                     MOVEL'3'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'D'       @FBSE
      * Set position 10
     C                     MOVEL'B'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW141****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW151 SR.  Generate spot rate a/c key -                     *
      *                total loan                                     *
      *  Called by   #LW15- Generate spot rate a/c key -              *
      *                      total loan                               *
      *  Calls       *none                                            *
      *                                                               *
      *****************************************************************
      *
     C           #LW151    BEGSR                           ***  #LW151****
      *
      * Set event type
     C                     MOVEL'3'       @ETYP
      * Blank out currency
     C                     MOVE *BLANKS   @CCY
      * Set position 9
     C                     MOVE 'L'       @FBSE
      * Set position 10
     C                     MOVEL'B'       @PURC
      * Update account key field.
     C                     MOVEL@AKEY     AKEY
      *
     C                     ENDSR                           ***  #LW151****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LW16  SR.    Write record to PF/DEALSDX0                    *
      *                                                               *
      *  Called by   #LP02 - Detail processing                        *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
     C           #LW16     BEGSR                           ***  #LW16 ****
      *
      * Write new record to file to avoid later duplication.
      * if the deal is a LIVE one
     C           *IN11     IFEQ '1'                        B001
     C                     Z-ADDDLNO      LDLNO             001
     C                     Z-ADDBJRDNB    LRUND             001
     C                     WRITEDEALSD1                     001
     C                     END                             E001
     C                     ENDSR                           ***  #LW16 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LP03  SR.    Add a/c keys trailer record PF/DLSPKYPT.       *
      *                                                               *
      *  Called by   Main - Main processing                           *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
     C           #LP03     BEGSR                           ***  #LP03 ****
      *
     C                     MOVEL'Z'       RECI
      * Number of records written
     C                     Z-ADD@TREC     NORE
      * Hash total integers
     C                     Z-ADDINT,1     HRWN
      * Hash total decimal
     C                     Z-ADDDEC,1     HRDC
      * Insert spot date a/c keys generated trailer record.
     C                     WRITEDKEYSZZF
      *
     C                     ENDSR                           ***  #LP03 ****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  SR.    Program processing error                       *
      *                & exit program                                 *
      *                                                               *
      *  Called by   Program execution or file error                  *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR*
      *
     C           ULRUN     IFEQ *BLANK                      B1
     C           *LOCK     IN   LDA                          1
     C                     MOVE 'Y'       ULRUN   1          1
     C                     MOVEL'*PSSR   'DBFILE             1
     C                     MOVEL'*PSSR'   DBKEY              1
     C                     Z-ADD4         DBASE              1
     C                     MOVEL'DL8601  'DBPGM              1
     C                     MOVE *ON       *INU7              1
     C                     DUMP                              1
     C                     OUT  LDA                          1
     C                     ENDIF                            E1
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LP999 SR.    To update LDA with database error information  *
      *                & exit program                                 *
      *                                                               *
      *  Called by   #LP01 - Initial processing                       *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
     C           #LP999    BEGSR                           ***  #LP999****
      *
     C           *LOCK     IN   LDA
      *
      ** Set up external d/base handling requirements.
      *
     C                     MOVEL'DL8601  'DBPGM
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     OUT  LDA
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR                           ***  #LP999****
      ********************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZFWDT  SR.    To calculate number of days forward in any     *
      *                currency.                                      *
      *                                                               *
      *  Called by   #LP01 - Initial processi n                       *
      *                                                               *
      *  Calls       ZACCH - Set up holidays array                    *
      *****************************************************************
      *
      /COPY ZSRSRC,ZFWDT
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZACCH  SR.    Set up holidays array                          *
      *                                                               *
      *  Called by   ZFWDT  - To calculate nos of days fwd in any ccy *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
      /COPY ZSRSRC,ZACCH
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZHASH  SR.    To calculate hash totals for integers and      *
      *                decimals.                                      *
      *                                                               *
      *  Called by                                                    *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
      /COPY ZSRSRC,ZHASHZ3
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  #LP01  SR.    Initial processing.                            *
      *                                                               *
      *  Called by   Main - Main processing                           *
      *                                                               *
      *  Calls       ZFWDT  - Calculate nos of days fwd in any ccy    *
      *              #LW01  - Write account keys header record        *
      *****************************************************************
      *
     C           #LP01     BEGSR                           ***  #LP01 ****
      *
      *** Copyright Statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      *** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      * Get run date from SDBANKPD.
      * Data base error if record not found.
     C                     READ SDBANKPD                 10
      *
     C           *IN10     IFEQ '1'                        B001
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     MOVEL*BLANKS   DBKEY            ** DBERR 001 **
     C                     Z-ADD1         DBASE            ***************
     C                     EXSR #LP999                      001
     C                     END                             E001
      *
      * Calculate spot event date with the following parms
      *  ZCCY = local currency
      *  ZDAYNO = run date
      *  ZNRDYS = 4
     C                     MOVELBJLCCY    ZCCY
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     Z-ADD4         ZNRDYS
     C                     EXSR ZFWDT
      * Store spot event date
     C                     Z-ADDZDYNBR    @SEVD   50
      *
      * Initialise total records = 2.
     C                     Z-ADD2         @TREC   50
      * Initialise a/c key header record.
     C                     EXSR #LW01
      * Initialise SR/ZHASH variables.
     C                     Z-ADD1         Z
     C                     Z-ADD1         S
      * Search key for LF/DEALSDV0
     C           DKEY      KLIST
     C                     KFLD           #DTYP   2
     C                     KFLD           #DLNO   60
      *
     C                     ENDSR                           ***  #LP01 ****
      ********************************************************************
      /EJECT
** CPY@ - OBJECT COPYRIGHT
(c) Finastra International Limited 2021
