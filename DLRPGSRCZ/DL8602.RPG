     H        1
     F*****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('MIDAS - Spot date extension file drop.**new*')         *
      *****************************************************************
      *                                                               *
      *  MIDAS - Dealing Module                                       *
      *                                                               *
      *  DL8602 - Spot Date Extension File Drop.                      *
      *                                                               *
      *  Function: This program reads all through the new extension   *
      *            file DEALSDX0, and checks whether the corresponding*
      *            deal is still being held on Pf/DEALSDB. If it has  *
      *            already been dropped from DEALSDB then it drops the*
      *            record from the deals extension file.              *
      *                                                               *
      *  Called By: DLC1201 - Create fresh deals file monthly         *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CDL086             Date 30Oct12               *
      *  Prev Amend No. AR101698 *CREATE   Date 27Oct17               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL086 - Portuguese FX Revaluation Extensions                *
      *           Upgrade to FM 2.1 2021.A with fix AR1014698.        *
      *    AR1014698 - Reconciliation issues with Midas R4 system     *
      *                (Child: AR1014700). Upgrade LBM001 as part of  *
      *                CDL086.                                        *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      /TITLE SUBROUTINE INDEX                                         *
      *****************************************************************
      *
      *  S U B R O U T I N E    I N D E X
      *  *PSSR  - Program Status Subroutine
      *
      ********************************************************************
      /EJECT
      * All FX deals.
     FDEALSDV1IF  E           K        DISK         KINFSR *PSSR
      * Spot date accounting entries already generated
     FDEALSDX0UF  E           K        DISK         KINFSR *PSSR
      *
      /EJECT
     F* ID F  C  H  L    FUNCTION OF INDICATORS
      *       10         End of file DEALSDX0
      *       11         Deal not found on PF/DEALSDB
      /EJECT
      *
     E                    CPY@    1   1 80
      *** Array for Object Statement
      /EJECT
      *
      ** Local data area
      *
     ILDA       E DSLDA
      /EJECT
      *****************************************************************
      *                                                               *
      *  MAIN PROCESSING                                              *
      *                                                               *
      *  Called by   *none                                            *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
      *** Copyright Statement
      *
     C                     MOVEACPY@      ACT@   80
      *
      *** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
      * Initial read
     C                     READ DEALSDX0                 10
      * Do until EOF DEALSDX0
     C           *IN10     DOWEQ'0'                        B001
      * Get deal details
     C           LDLNO     CHAINDEALSDBF             11     001
      * If deal has been dropped then....
     C           *IN11     IFEQ '1'                        B002
      * Delete from file
     C                     DELETDEALSD1                     002
     C                     END                             E002
      * Get next deal number
     C                     READ DEALSDX0                 10 001
     C                     END                             E001
      * End program.
     C                     SETON                     LR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  SR.    Program processing error                       *
      *                & exit program                                 *
      *                                                               *
      *  Called by   Program execution or file error                  *
      *                                                               *
      *  Calls       *none                                            *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR*
      *
     C           ULRUN     IFEQ *BLANK                      B1
     C           *LOCK     IN   LDA                          1
     C                     MOVE 'Y'       ULRUN   1          1
     C                     MOVEL'*PSSR   'DBFILE             1
     C                     MOVEL'*PSSR'   DBKEY              1
     C                     Z-ADD1         DBASE              1
     C                     MOVEL'DL8602  'DBPGM              1
     C                     OUT  LDA                          1
     C                     MOVE *ON       *INU7              1
     C                     MOVE *ON       *INU8              1
     C                     DUMP                              1
     C                     ENDIF                            E1
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
** CPY@ - OBJECT COPYRIGHT
(c) Finastra International Limited 2021
