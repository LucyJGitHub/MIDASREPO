     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL Cheque and statement reprint Prompt')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Dealing Module
     F*                                                               *
     F*  DL9036 - DEPOSIT STATEMENT WITH CHEQUES REPRINT PROMPT       *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 051380             Date 07Jan94               *
      *                 048547             Date 18Mar93               *
     F*                 AUS006             DATE 10AUG92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  051380 - Wrong customer numbers being passed to DL9035.  Use *
     F*           the value of BBCUST for processing.                 *
     F*  048547 - Recompiled to reflect changes made in DL9036FM.     *
     F*  AUS006 - AUSTRALIAN PRODUCT - INTEREST HISTORY               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FDL9036FMCF  E                    WORKSTN
     F*
     F/EJECT
     F********************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F********************************************************************
     F*
     F*       50         WORK INDICATOR.
     F*       51         WORK INDICATOR.
     F*
     E*
     E                    CPY@    1   1 80                                S01390
     I*                                                                   S01390
     ISDBANK    E DSSDBANKPD                                              S01390
     I* Bank Details Accesses Via Access Program                          S01390
     I*                                                                   S01390
     ISDCUST    E DSSDCUSTPD                                              S01390
     I* Customer Details Accessed Via Access Program                      S01390
     I*                                                                   S01390
     IDSFDY     E DSDSFDY                                                 S01390
     I* First DS For Access Programs, Short Data Structure                S01390
     I*                                                                   S01390
     IDSSDY     E DSDSSDY                                                 S01390
     I* Second DS For Access Programs, Long Data Structure                S01390
     I*
     ILDA         DS                            256                       S01390
     I*                                                                   S01390
     I** Local Data Area To Identify Database Errors                      S01390
     I*                                                                   S01390
     I                                      134 141 DBFILE                S01390
     I                                      142 170 DBKEY                 S01390
     I                                      171 180 DBPGM                 S01390
     I                                      181 1830DBASE                 S01390
     IRPTPM       DS                            100                       S01390
     I                                        1   6 SCUS                  S01390
     I                                        7  12 ECUS                  S01390
     I*                                                                   S01390
     IPSDS       SDS                                                      S01390
     I                                      244 253 WKSTN                 S01390
     I                                      254 263 USER                  S01390
     I*
     I/EJECT
     C********************************************************************
     C*
     C*    M A I N L I N E.
     C*
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5                       S01390
     C                     PARM           LEV     1                       S01390
     C                     PARM           ENT     3                       S01390
     C                     PARM           RPTPM                           S01390
     C                     PARM           ACT     1                       S01390
     C                     PARM           CMD     1                       S01390
     C*
     C*    FIRST CYCLE PROCESSING
     C*
     C                     EXSR FIRSTC
     C*
     C*    READ AND VALIDATE INPUT SCREEN
     C*
     C                     EXSR SCREEN
     C                     MOVE '1'       *INLR
     C*
     C/EJECT
     C********************************************************************
     C*
     C*      FIRST IS A SUBROUTINE TO PERFORM ALL FIRST CYCLE
     C*      CALCULATIONS.
     C*
     C           FIRSTC    BEGSR                           *** FIRSTC ***
     C*
     C*    INITILIZE STANDARD VARIABLES & FIRST CYCLE AMOUNT
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'DL9026'  DBPGM
     C                     OUT  LDA
     C                     MOVEACPY@      BIS@   80                       S01390
     C*
     C** ACCESS BANK DETAILS
     C*
     C                     CALL 'AOBANKR0'                                S01390
     C                     PARM '*DBERR ' RTCD    7                       S01390
     C                     PARM '*FIRST ' OPTN    7                       S01390
     C           SDBANK    PARM SDBANK    DSFDY                           S01390
     C*                                                                   E245
     C**   INITIALISE SCREEN FIELDS                                       E245
     C**  ACTION CODES I,A ALLOW INPUT TO SCREEN FIELDS                   E245
     C*                                                                   E245
     C           ACT       IFEQ 'I'                                       E245
     C                     MOVE *BLANKS   SCUSA
     C                     MOVE *BLANKS   ECUSA
     C                     ELSE                                           E245
     C*                                                                   E245
     C**  ACTION CODES E,D DO NOT ALLOW INPUT TO SCREEN FIELDS            E245
     C**  ACTION CODES D ALLOW F10                                        E245
     C*                                                                   E245
     C                     MOVELSCUS      SCUSA                           E245
     C                     MOVELECUS      ECUSA                           E245
     C           ACT       COMP 'D'                      46               E245
     C           ACT       COMP 'D'                      45               E245
     C  N45      ACT       COMP 'E'                      45               E245
     C                     END                                            E245
     C*
     C           ENDFC     ENDSR                           *** ENDFC  ***
     C*
     C/EJECT
     C********************************************************************
     C*
     C*      SCREEN SR  SCREEN VALIDATION.
     C*
     C           SCREEN    BEGSR                           *** SCREEN ***
     C*
     C           *IN02     DOWEQ'0'
     C*
     C*    WRITE AND READ SCREEN.
     C*
     C                     EXFMTSCRN01
     C*                                                                   S01390
     C**   IF CMD/3 IS PRESSED, RETURN TO CALLING PROGRAM                 S01390
     C*                                                                   S01390
     C           *IN03     IFEQ '1'                                       S01390
     C                     MOVE 'E'       CMD                             S01390
     C                     MOVE '1'       *INLR
     C                     MOVE '1'       *IN02
     C                     ELSE
     C**                                                                  S01390
     C**   IF CMD/12 IS PRESSED, RETURN TO CALLING PROGRAM                S01390
     C**                                                                  S01390
     C           *IN12     IFEQ '1'                                       S01390
     C                     MOVE 'F'       CMD                             S01390
     C                     MOVE '1'       *INLR
     C                     MOVE '1'       *IN02
     C                     ELSE
     C**                                                                  S01390
     C**   IF CMD/10 IS PRESSED, RETURN TO CALLING PROGRAM                S01390
     C**                                                                  S01390
     C           *IN10     IFEQ '1'                                       S01390
     C                     MOVE 'D'       CMD                             S01390
     C                     MOVE '1'       *INLR
     C                     MOVE '1'       *IN02
     C                     ELSE
     C*
     C*    VALIDATE SCREEN.
     C*
     C                     MOVE '0'       *IN61
     C                     MOVE '0'       *IN66
     C                     MOVE '0'       *IN67
     C*
     C*    VALIDATE START CUSTOMER.
     C*
     C           SCUSA     IFEQ '      '
     C**********           Z-ADD0         SCUST   60                                          CSD027
     C                     MOVE *BLANKS   SCUST   6                                           CSD027
     C                     Z-ADD0         BRCCS   30
     C                     MOVE *BLANKS   CASKS   8
     C                     ELSE
     C                     MOVE SCUSA     KYCN   10                       S01390
     C                     CALL 'AOCUSTR0'                                S01390
     C                     PARM *BLANKS   RTCD                            S01390
     C                     PARM '*KEY   ' OPTN                            S01390
     C                     PARM           KYCN                            S01390
     C                     PARM *BLANKS   KYST    7                       S01390
     C           SDCUST    PARM SDCUST    DSSDY                           S01390
     C           RTCD      IFEQ *BLANKS
     C                     MOVELKYCN      SCUSA                           S01390
     C                     MOVELBBBRCD    BRCCS
     C                     MOVE BBCASC    CASKS
     C***********          MOVE SCUSA     SCUST                           051380
     C                     MOVE BBCUST    SCUST                           051380
     C                     ELSE
     C                     SETON                     61
     C                     END
     C                     END
     C*
     C*    VALIDATE END CUSTOMER.
     C*
     C           *IN61     IFEQ '0'
     C           ECUSA     IFEQ '      '
     C**********           Z-ADD999999    ECUST   60                                          CSD027
     C                     MOVE '999999'  ECUST   6                                           CSD027
     C                     Z-ADD999       BRCCE   30
     C                     MOVE 'ZZZZZZZZ'CASKE   8
     C                     ELSE
     C                     MOVE ECUSA     KYCN                            S01390
     C                     CALL 'AOCUSTR0'                                S01390
     C                     PARM *BLANKS   RTCD                            S01390
     C                     PARM '*KEY   ' OPTN                            S01390
     C                     PARM           KYCN                            S01390
     C                     PARM *BLANKS   KYST                            S01390
     C           SDCUST    PARM SDCUST    DSSDY                           S01390
     C           RTCD      IFEQ *BLANKS
     C                     MOVELKYCN      ECUSA                           S01390
     C                     MOVELBBBRCD    BRCCE
     C                     MOVE BBCASC    CASKE
     C***********          MOVE ECUSA     ECUST                           051380
     C                     MOVE BBCUST    ECUST                           051380
     C                     ELSE
     C                     SETON                     67
     C                     END
     C                     END
     C*
     C*    CHECK THAT START CUSTOMER NOT AFTER END CUSTOMER.
     C*
     C           *IN67     IFNE '1'
     C           BRCCS     IFGT BRCCE
     C                     MOVE '1'       *IN66
     C                     ELSE
     C           BRCCS     IFEQ BRCCE
     C           CASKS     IFGT CASKE
     C                     MOVE '1'       *IN66
     C                     ELSE
     C                     MOVE '1'       *IN02
     C                     END
     C                     ELSE
     C                     MOVE '1'       *IN02
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C*    OUTPUT START AND END CUSTOMER NUMBERS TO DATA STRUCTURE
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C           *IN10     ANDEQ'0'
     C                     MOVELSCUST     SCUS
     C                     MOVELECUST     ECUS
     C                     END
     C*
     C                     ENDSR
     C*
** CPY@   **      OBJECT COPYRIGHT                                        S01390
(c) Finastra International Limited 2001
