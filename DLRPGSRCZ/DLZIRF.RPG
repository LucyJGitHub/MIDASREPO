     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas DL IRS schedule date generation')
      *****************************************************************
      *                                                               *
      *  Midas - Dealing Module                                       *
      *                                                               *
      *  DLZIRF - IRS Schedule Date Generation Formula using Payment  *
      *           Date Conventions                                    *
      *                                                               *
      *  Function: This program will ba a copy of standard subroutine *
      *  (I/C)     FFDATE. Wherever FFDATE performs holiday checks    *
      *            using single currency calendars, this program will *
      *            use the calendars of all the payment convention    *
      *            currencies.                                        *
      *                                                               *
      *  Called By: DL4000 - IRC Schedules Maintenance                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005  *CREATE    Date 21Jan00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CIR005 - FRA/IRS Business Day Conventions                    *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E                    TABDY   7   7  1   TABNO   1 0
      *****************************************************************
      /EJECT
      *****************************************************************
     IPDATC       DS
     I                                        1   1 WAA
     I                                        1   2 WAB
     I                                        2   2 WB
     I                                        3   3 WC
     I                                        4   5 WDEA
     I                                        4   6 WDEF
     I                                        6   6 WF
     I                                        7   7 WG
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           PDATC   7
     C                     PARM           PMTH    20
     C                     PARM           PYR     20
     C                     PARM           PCURR  30
     C                     PARM           PLOCN   3
     C                     PARM           PDFIN   1
     C                     PARM           PDAY    50
     C                     PARM           PBDAY   50
      *
     C                     MOVE PLOCN     PLOCN
     C                     Z-ADD0         PDAY
     C                     Z-ADD0         PBDAY
      *
     C           WAB       IFEQ '31'
     C                     EXSR SRDE31
     C                     ELSE
     C                     EXSR SRDN31
     C                     ENDIF
      *
     C           WAB       IFNE '31'
     C           WAB       OREQ '31'
     C           WDEF      ANDNE*BLANKS
     C                     Z-ADD0         WDE     20
     C           WDEF      IFNE *BLANK
     C                     MOVE WDEA      WDE
     C                     ENDIF
     C                     ENDIF
      *
      ** Adjust date by determining code (WF)
      *
     C           WF        CASEQ'A'       SRDAAD
     C           WF        CASEQ'B'       SRDABD
     C           WF        CASEQ'C'       SRDABD
     C           WF        CASEQ*BLANK    SRDAWD
     C                     END
      *
     C                     MOVE *ON       *INLR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDE31 - Calculates Last Working Day in Month                *
      *                                                               *
      *  Called by: Main Routine                                      *
      *                                                               *
      *  Calls: DLZIRP, ZDATE1                                        *
      *                                                               *
      *****************************************************************
      *
     C           SRDE31    BEGSR
      *
      ** Determine month number for next month
      *
     C           PMTH      IFNE 12
     C           PMTH      ADD  1         WMTH    20
     C                     Z-ADDPYR       WYR     20
     C                     ELSE
     C                     Z-ADD1         WMTH
     C           PYR       ADD  1         WYR
     C                     ENDIF
      *
      ** Determine Midas day number for 1st day of next month
      *
     C                     MOVE WMTH      WMTH2   2
     C                     MOVE WYR       WYR2    2
      *
     C           PDFIN     IFEQ 'M'
     C           WMTH2     CAT  '01'      WRK4    4
     C                     ELSE
     C           '01'      CAT  WMTH2     WRK4
     C                     ENDIF
      *
     C           WRK4      CAT  WYR2      WZDATE  6
     C                     MOVE WZDATE    ZDATE   60
      *
     C                     CALL 'ZDATE1'
     C                     PARM           ZERR    7
     C                     PARM           ZDATE
     C                     PARM           PDFIN
     C                     PARM           ZDAYNO  50
      *
      ** Get last day of the month
      *
     C           ZDAYNO    SUB  1         PDAY
      *
      ** Determine last working day in month in all the 10 currencies
      ** for payment convention
      *
     C                     CALL 'DLZIRP'
     C                     PARM           PDAY
     C                     PARM           PCURR
     C                     PARM 'P'       PPAYC   2
     C                     PARM           PLOCN
     C                     PARM           PDFIN
     C                     PARM           PAJDT   50
     C                     PARM           PBSDT   50
      *
     C                     Z-ADDPAJDT     PDAY
     C                     Z-ADDPBSDT     PBDAY
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDN31 - Calculates Specified Day in Month                   *
      *                                                               *
      *  Called by: Main Routine                                      *
      *                                                               *
      *  Calls: ZDATE1                                                *
      *                                                               *
      *****************************************************************
      *
     C           SRDN31    BEGSR
      *
      ** Determine first day of the month
      *
     C                     MOVE PMTH      PMTH2   2
     C                     MOVE PYR       PYR2    2
      *
     C           PDFIN     IFEQ 'M'
     C           PMTH2     CAT  '01'      WRK4
     C                     ELSE
     C           '01'      CAT  PMTH2     WRK4
     C                     ENDIF
      *
     C           WRK4      CAT  PYR2      WZDATE
     C                     MOVE WZDATE    ZDATE
      *
     C                     CALL 'ZDATE1'
     C                     PARM           ZERR
     C                     PARM           ZDATE
     C                     PARM           PDFIN
     C                     PARM           ZDAYNO
      *
     C                     Z-ADDZDAYNO    PDAY
      *
      ** Determine whether 1st two characters are day no. or day code
      *
     C           WB        LOKUPTABDY     TABNO          21
      *
      ** Determine number of working days after the 1st month
      *
     C           *IN21     IFEQ *ON
      *
     C           PDAY      DIV  7         WPDAY   50
     C                     MVR            WDAYY   10
      *
     C           TABNO     SUB  WDAYY     WDAYZ   10
      *
     C           WDAYZ     IFLT 0
     C                     ADD  7         WDAYZ
     C                     ENDIF
      *
     C                     ADD  WDAYZ     PDAY
     C                     MOVE WAA       WA      10
      *
     C           2         DO   WA
     C                     ADD  7         PDAY
     C                     ENDDO
      *
     C                     ELSE
      *
     C                     MOVE WAB       WABN    20
     C                     SUB  1         WABN
     C                     ADD  WABN      PDAY
      *
     C                     ENDIF
      *
     C                     Z-ADDPDAY      PBDAY
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDAAD - Adjusts Date with Specified No. of Actual Days      *
      *                                                               *
      *  Called by: Main Routine                                      *
      *                                                               *
      *  Calls: DLZIRP                                                *
      *                                                               *
      *****************************************************************
      *
     C           SRDAAD    BEGSR
      *
     C           WC        IFEQ '+'
     C                     ADD  WDE       PDAY
     C                     ELSE
     C                     SUB  WDE       PDAY
     C                     ENDIF
      *
     C           WG        IFEQ '+'
     C                     MOVEL'F'       PPAYC
     C                     ELSE
     C                     MOVEL'P'       PPAYC
     C                     ENDIF
      *
      ** Determine next/previous working day using all the currencies
      ** for Payment Date Convention
      *
     C                     CALL 'DLZIRP'
     C                     PARM           PDAY
     C                     PARM           PCURR
     C                     PARM           PPAYC
     C                     PARM           PLOCN
     C                     PARM           PDFIN
     C                     PARM           PAJDT
     C                     PARM           PBSDT
      *
     C                     Z-ADDPAJDT     PDAY
     C                     Z-ADDPBSDT     PBDAY
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDABD - Adjusts Date with Specified No. of Business Days    *
      *                                                               *
      *  Called by: Main Routine                                      *
      *                                                               *
      *  Calls: DLZIRP                                                *
      *                                                               *
      *****************************************************************
      *
     C           SRDABD    BEGSR
      *
     C           WC        IFEQ '+'
     C                     MOVEL'F'       PPAYC
     C                     ELSE
     C                     MOVEL'P'       PPAYC
     C                     ENDIF
      *
      ** Adjust date with no. of business days for all currencies
      *
     C                     ADD  1         WDE
     C                     Z-ADD1         X       20
      *
     C           X         DOWLEWDE
      *
     C                     CALL 'DLZIRP'
     C                     PARM           PDAY
     C                     PARM           PCURR
     C                     PARM           PPAYC
     C                     PARM           PLOCN
     C                     PARM           PDFIN
     C                     PARM           PAJDT
     C                     PARM           PBSDT
      *
     C           X         IFLT WDE
      *
     C           PPAYC     IFEQ 'F'
     C           PAJDT     ADD  1         PDAY
     C           PBSDT     ADD  1         PBDAY
     C                     ELSE
     C           PAJDT     SUB  1         PDAY
     C           PBSDT     SUB  1         PBDAY
     C                     ENDIF
      *
     C                     ELSE
      *
     C                     Z-ADDPAJDT     PDAY
     C                     Z-ADDPBSDT     PDAY
      *
     C                     ENDIF
      *
     C                     ADD  1         X
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDAWD - Get Adjusted Date as Working Day                    *
      *                                                               *
      *  Called by: Main Routine                                      *
      *                                                               *
      *  Calls: DLZIRP                                                *
      *                                                               *
      *****************************************************************
      *
     C           SRDAWD    BEGSR
      *
     C           WC        IFEQ '+'
     C                     MOVEL'F'       PPAYC
     C                     ELSE
     C                     MOVEL'P'       PPAYC
     C                     ENDIF
      *
      ** Determine next/previous working day using all the currencies
      ** for Payment Date Convention
      *
     C                     CALL 'DLZIRP'
     C                     PARM           PDAY
     C                     PARM           PCURR
     C                     PARM           PPAYC
     C                     PARM           PLOCN
     C                     PARM           PDFIN
     C                     PARM           PAJDT
     C                     PARM           PBSDT
      *
     C                     Z-ADDPAJDT     PDAY
     C                     Z-ADDPBSDT     PBDAY
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
**  TABDY/TABNO - Shortnames & Day Numbers
F0S1X2M3U4W5T6
