      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ED Bulk Transfer Audit Report')
      *****************************************************************
      *  Midas - Midas Export Data Module                             *
      *                                                               *
      *  ED0035P1 - BULK TRANSFER AUDIT REPORT                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. S01493 *CREATE     DATE 16MAY94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * S01493 - Midas Export Data Module                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      ** Main Header
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ED0035P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    38'MIDAS EXPORT DATA - BULK T+
     A                                      RANSFER AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    38'--------------------------+
     A                                      --------------------'
     A                                      SPACEA(2)
      *
     A                                    11'BATCH NO.'
     A                                    31'BATCH FILE'
     A                                    51'BATCH MEMBER'
     A                                    71'NUMBER OF RCDS'
      *
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETAIL1
      *
     A            EABTCH        10        10
     A            EAFNME        10        30
     A            EAMNME        10        50
     A            EARECT         9  0     74EDTCDE(Z)
     A                                      SPACEA(1)
      *
      ** Trailer Record Format
      *
     A          R TRAILP1
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(2)
