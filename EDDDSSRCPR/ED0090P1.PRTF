      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ED Drip Feed Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Export Data Module                             *
      *                                                               *
      *  ED0090P1 - DRIP FEED AUDIT REPORT                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. S01493 *CREATE     DATE 16MAY94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * S01493 - Midas Export Data Module                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      ** Main Header
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ED0090P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    38'MIDAS EXPORT DATA - DRIP F+
     A                                       EED AUDIT REPORT'
     A  40                                83'- INTERIM'
     A                                      SPACEA(1)
     A N40                                84' '
     A                                      SPACEA(1)
     A                                    38'--------------------------+
     A                                      ----------------'
     A                                      SPACEA(2)
      *
     A                                    11'BATCH NO.'
     A                                    32'STATUS'
     A                                    45'CREATION TIME'
     A                                    65'CREATION DATE'
     A                                    85'DELETION TIME'
     A                                   105'DELETION DATE'
      *
     A                                      SPACEA(2)
      *
      ** Detail Record Format
      *
     A          R DETAIL1
      *
     A            EEBTCH        10        10
     A                                      SPACEB(1)
     A  51                                31'SENT     '
     A  52                                31'COMPLETED'
     A  53                                31'FAILED   '
     A            EECRET         6  0     50EDTWRD('  :  :  ')
     A            RRCRED         7        70
     A  54        EEDELT         6  0     90EDTWRD('  :  :  ')
     A  55        RRDELD         7       110
      *
      ** Trailer Record Format
      *
     A          R TRAILP1
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(2)
