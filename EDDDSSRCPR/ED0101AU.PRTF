      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ED Drip feed housekeeping audit')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Export Data Module                             *
      *                                                               *
      *  ED0101AU - DRIP FEED HOUSEKEEPING AUDIT REPORT               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  PREV AMEND NO. 101971 *CREATE     DATE 17JUL95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * MD046248 - Finastra Rebranding                                *
      * 101971 - Phase II Development                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ED0101AU'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRDATE    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    26'MIDAS EXPORT DATA - DRIP +
     A                                      FEED - HOUSEKEEPING+
     A                                       - AUDIT EXCEPTION'
     A                                      SPACEA(1)
     A                                    26'-----------------------+
     A                                      -------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
      *
      ** Run Controls
      *
     A          R DETAILAU
     A                                 12  8'TOTAL NUMBER OF LIVE RECORDS     '
     A            RRLIV          7  0  12 49EDTCDE(1)
      *
     A                                 14  8'TOTAL NUMBER OF DELETED RECORDS  '
     A            RRDEL          7  0  14 49EDTCDE(1)
      *
      ** NULL1
      *
     A          R NULLAU
     A                                    51'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
      *
      ** Database Error
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
