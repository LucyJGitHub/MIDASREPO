      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ED Daily reconciliation report audit')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Export Data Module                             *
      *                                                               *
      *  ED0200AU - ED DAILY TRANSACTION RECONCILIATION REPORT AUDIT  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  PREV AMEND NO. 101973 *CREATE     DATE 12FEB96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * MD046248 - Finastra Rebranding                                *
      * 101973 - Midas Export Data Module - Phase 5.                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ED0200AU'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE1        004S 0    124EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    26'MIDAS EXPORT DATA - '
     A                                    46'DAILY TRANSACTION RE'
     A                                    66'CONCILIATION REPORT '
     A                                    86'- RUN CONTROL'
     A                                      SPACEA(1)
     A                                    26'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       -------------'
     A                                      SPACEA(3)
      *
      ** Run Control Formats
      *
     A          R DETAILAU
     A                                     8'TOTAL NUMBER OF RECORDS PROCESSED'
     A            RRPROG         7  0     49EDTCDE(1)
      *
     C          R NULLAU
     A                                    48'NO RECONCILIATION DATA TO REPORT'
      *
      ** Database Error
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
