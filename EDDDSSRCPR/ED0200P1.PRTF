      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ED Daily reconciliation report')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Export Data Module                             *
      *                                                               *
      *  ED0200P1 - BULK TRANSFER AUDIT REPORT                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 106562             Date 20Aug96               *
      *                 101973 *C *CREATE  Date 12Feb96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      * 106562 - Improve readability of report by formatting amounts  *
      *          with commas.                                         *
      * 101973 - Midas Export Data Module - Phase 5.                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ** Main Header
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ED0200P1'
     A            BJURPT    R             39REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    40'MIDAS EXPORT DATA MO'
     A                                    60'DULE - DAILY TRANSAC'
     A                                    80'TION RECONCILIATION '
     A                                   100'REPORT'
     A                                      SPACEA(1)
     A                                    40'--------------------'
     A                                    60'--------------------'
     A                                    80'--------------------'
     A                                   100'------'
      *
     A          R HEADP2                    SPACEB(2)
     A                                    60'No. of Transactions'
     A                                    88'Total Value'
      *
      ** Branch Header Format
      *
     A          R BRANCHH
      *
     A                                      SPACEB(3)
     A                                    20'Booking Branch - '
     A            RRBRNM    R             37REFFLD(BRNM)
      *
      ** Transaction Type Record Format
      *
     A          R TRANTY
     A                                      SPACEB(2)
     A  11                                20'Securities Trades'
     A  12                                20'FX Deals'
     A  13                                20'FRAs, IRSs, CAPS/COLLARS/FLOORS'
     A  14                                20'Futures and Options Trades'
     A  15                                20'Facilities, Loans and Deposits'
      *
      ** Detail Record Format
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
     A            EJCCYC    R             22REFFLD(CCY)
     A            EJTCOT    R             57REFFLD(EDREC1D0/EJTCOT EDREC1PD)
     A                                      EDTCDE(J)
      *                                     Start of change /106562/
     A************RRVAL4        30  4     70EDTCDE(3)
     A            RRVAL4        30  4     70EDTCDE(J)
      *                                       End of change /106562/
      *
      ** Trailer Record Format
      *
     A          R TRAILP1
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(2)
