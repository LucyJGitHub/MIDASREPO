     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED Create a special case batch reference')
      *****************************************************************
      *                                                               *
      *  Midas - Export Data Module                                   *
      *                                                               *
      *  ED0005 - Create a Special Case Batch Reference Record        *
      *                                                               *
      *  Function:  This program writes the reference record for      *
      *             a special case batch to EDSAVEPD (I/C) or         *
      *             EDSAVXPD (COB).                                   *
      *                                                               *
      *  Called By: EDC0005 - Create a Special Case Batch             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 117467 *CREATE     Date 30APR97               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  117467 - New program to write the EDSAVEPD/EDSAVXPD record   *
      *           for a special case batch.                           *
      *                                                               *
      *****************************************************************
     FEDSAVEPDO   E                    DISK         KINFSR *PSSR
      **  Batches ready for DTA (Input Cycle)
      *
     FEDSAVXPDO   E                    DISK         KINFSR *PSSR
      **  Batches ready for DTA (Close of Business)
      *
     FEDFBUTPDUF  E           K        DISK         KINFSR *PSSR
      **  File containing the COB sequence value for the batches
      *
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
     IEDSAVX    E DSEDSAVXPD
      ** Shared data structure for use in EDSAVEPD and EDSAVXPD writes
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      ** Data area giving Installation Control Details
      *
     IPSDS       SDS
      ** Program status data structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      ** Entry parameter list
      *
     C           *ENTRY    PLIST
     C                     PARM           STRING 32        Log string
     C                     PARM           TARGET 10        Target file
     C                     PARM           COBKEY 20        EDSAVxPD key
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Move log string to data structure ready to be written
      *
     C                     MOVELSTRING    EDSAVX
      *
      ** EDSAVEPD write processing
      *
     C           TARGET    IFEQ 'EDSAVEPD'
     C                     WRITEEDSAVED0
     C                     ENDIF
      *
      ** EDSAVXPD write processing
      *
     C           TARGET    IFEQ 'EDSAVXPD'
      *
      ** Access EDFBUTPD for a sequence code
      *
     C           COBKEY    CHAINEDFBUTPD             99
     C           *IN99     IFEQ '0'
     C                     MOVELEDSQCD    EFSQCD
     C                     ENDIF
      *
      ** If no record is found then default to '999'
      *
     C           *IN99     IFEQ '1'
     C                     MOVEL'999'     EFSQCD
     C                     ENDIF
     C                     WRITEEDSAVXD0
     C           *IN99     IFEQ '0'
     C                     DELETEDBUTFD0
     C                     ENDIF
     C                     ENDIF
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
