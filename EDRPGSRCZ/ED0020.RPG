     H        1
      *****************************************************************
/*OVRF*  OVRDBF FILE(EDOUTFPD) TOFILE(QAFDMBR)                        *
/*STD *  RPGBASE                                                      *
/*EXI *  GENOPT(*OPTIMIZE)                                            *
/*EXI *  TEXT('Midas ED Bulk transfer')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Export Data Interface Module                   *
      *                                                               *
      *  ED0020 - BULK TRANSFER                                       *
      *                                                               *
      *  Function: Program Creates the header record for each member  *
      *            of PF/EDBULKPD.                                    *
      *                                                               *
      *  Called By: EDC0020 - Bulk Transfer Component                 *
      *                                                               *
      *  Calls:     AOBANKR0 - Access Object for Bank Details ICD     *
      *             AOMEXPR0 - Access Object for ED ICD               *
      *             EDC0024  - Bulk Transfer Member Creation          *
      *             EDC0027  - Bulk Transfer Copy File                *
      *                                                               *
      *  Notes:  i) Created with override for EDOUTFPD to the IBM     *
      *             file layout for the output of DSPFD command.      *
      *             Also creation parameters contain *OPTIMIZE value. *
      *             Efficiency is of paramount importance in this     *
      *             program.                                          *
      *         ii) ED0020AU is only printed for Database Errors, not *
      *             each time this program runs.  This is because     *
      *             the program could run a great many times during   *
      *             one COB.                                          *
      *        iii) When called, PF/EDSAVEPD will be overridden to    *
      *             PF/EDSAVxPD.  PF/EDBUTFPD will be overridden to   *
      *             PF/EDBUTxPD.  x = Stream number.                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. 121889             DATE 15AUG97               *
      *  PREV AMEND NO. CED002             DATE 13MAY97               *
      *                 CED001             DATE 27AUG96               *
      *                 103143             DATE 05JUN96               *
      *                 S01493 *CREATE     DATE 31MAY94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  121889  - Add extra (blank) parameter to EDC0027 call.       *
      *  CED002  - Midas ED Enhancements                              *
      *            Recompile only for changes to EDFBUTPD/EDSAVXPD    *
      *  CED001  - Midas Export Data Phases III & IV                  *
      *  103143  - BULK TRANSFER COPY PROBLEM + AUDIT REPORT          *
      *  S01493  - Midas Export Data Interface Module.                *
      *                                                               *
      *****************************************************************
     FEDOUTFPDIF  E                    DISK         KINFSR *PSSR
      *   File Member Description
     F*EDBUTFPDUF**E*************       DISK         KINFSR *PSSR         CED001
     FEDFBUTPDUF  E           K        DISK         KINFSR *PSSR          CED001
      *   List of files to transfer to DTA                                S01493
     FEDBULKPDO   F    4096            DISK         KINFSR *PSSR      UC
      *   Bulk Transfer data
     F*EDSAVEPDO***E*************       DISK         KINFSR *PSSR         CED001
     FEDSAVXPDO   E           K        DISK         KINFSR *PSSR A        CED001
     F                                              KCOMIT                CED001
      *   Batches to send to DTA                                          S01493
     FEDBUAUPDO   E                    DISK         KINFSR *PSSR
     F                                              KCOMIT                CED001
      *   Audit File
     FED0020AUO   E                    PRINTER                        UC
      *   Audit Report
      *
      /TITLE FUNCTION OF INCICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    01  - READ OF EDOUTFPD                                     *
      *****02**- READ*OF*EDBUTFPD                                     *   CED001
      *    02  - NO EDFBUTPD RECORD FOUND TO DELETE                   *   CED001
      *    03  - OUTPUT ALL MEMBERS OF FILE TO COPY                   *
      *                                                               *
      *    98  - Date Format: DDMMYY (off); MMDDYY (on)               *
      *  90-99 - Used by Standard Subroutines                         *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #DETL  - Detail Processing                                   *
      *  #MBRRC - Data File Record Processing                         *
      *  #BATCH - Create batch                                        *
      *  #INIT  - Initial Processing                                  *
      *  #LAST  - Termination processing                              *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     I/TITLE I-SPECIFICATIONS
      *
      *-----------------------------------------------------------
     IDSFDY     E DSDSFDY
      ** First Data Structure for Access Objects
      *
      *-----------------------------------------------------------
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
      *-----------------------------------------------------------
     ISDMEXP    E DSSDMEXPPD
      ** ED ICD Details
      *
      *-----------------------------------------------------------
     IEDBULK      DS
      ** ED ICD Details
     I                                        1   60UBTCH0
     I                                        7  120UBTCH1
     I                                       13  180UBTCH2
     I                                       19  240UBTCH3
     I                                       25  300UBTCH4
     I                                       31  360UBTCH5
     I                                       37  420UBTCH6
     I                                       43  480UBTCH7
     I                                       49  540UBTCH8
     I                                       55  600UBTCH9
      *
      *-----------------------------------------------------------
     IWBATCH      DS                             10
      ** Batch Name field
     I                                        1   3 WEDDTX
     I                                        4   40WSTREM
     I                                        5  100WBATNN
      *
      *-----------------------------------------------------------
     I            DS
      * Zoned field for Output Spec.
     I                                        1  100EBNUMR
      *
      *-----------------------------------------------------------
     IWOVR        DS                             54
      *  DS for QCMDEXC
     I                                       44  53 WDSBAT
      *
      *-----------------------------------------------------------
      *  Constant for QCMDEXC
     I              'OVRDBF FILE(EDBULKPD-C         QOVR
     I              ') TOFILE(EDBULKPD) M-
     I              'BR(XXXXXXXXXX)'
      *
      *-----------------------------------------------------------
     ITFROK       DS                                                      CED001
      * Setup Data structure for Deletion from Failed Bulk Transfers      CED001
     I                                        1  10 XFILEN                CED001
     I                                       11  20 XMEMBN                CED001
      *                                                                   CED001
      *-----------------------------------------------------------        CED001
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      *-----------------------------------------------------------
     I           SDS
      ** PROGRAM STATUS DATA STRUCTURE
     I                                      244 253 WSID
     I                                      254 263 USID
      *-----------------------------------------------------------
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR #INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR #DETL
      *
      ***  Perform Final Processing.
      *
     C                     EXSR #LAST
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                     LR
     C                     RETRN
      *                                                               *
      *================================================================
      /EJECT
      /TITLE SR/#DETL
      *****************************************************************
      *                                                               *
      *  #DETL    - Subroutine to do the Detail Processing.           *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : #MBRRC                                            *
      *                                                               *
      *****************************************************************
      *
     C           #DETL     BEGSR
      *
     C                     READ EDOUTFPD                 01
      *
      *** If no record found then set up fields for PF/EDBUAUPD and WRITE
      *
     C           *IN01     IFEQ *ON                        B1
     C                     MOVEL'No Mbrs 'EABTCH
     C                     Z-ADD*ZERO     EARECT
      *
     C                     WRITEEDBUAUD0
     C                     ENDIF                           E1
      *
      ***  Process WHILE there are valid records.
      *
     C           *IN01     DOWEQ*OFF                       B1
      *
      *** Match the member on the file list PF/EDBUTFPD (passed in as a
      *** parameter from EDC0020) to a member in the file
      *
     C           *IN03     IFEQ *ON                        B2
     C           UMEMBN    OREQ MBNAME
      *
      ***  If the member name field passed in was blank then use
      ***  the member name from PF/EDOUTFPD
      *
     C           *IN03     IFEQ *ON                        B3
     C***********          MOVELMBNAME    UMEMBN                          CED001
     C                     MOVELMBNAME    EAMNME                          CED001
     C                     ENDIF                           E3
      *
      ***  If the member is empty then write an audit record and
      ***  continue
      *
     C           MBNRCD    IFEQ *ZERO                      B3
     C                     MOVEL*BLANKS   EABTCH
     C                     MOVEL'No Data 'EABTCH
     C                     Z-ADD*ZERO     EARECT
      *
     C                     WRITEEDBUAUD0
      *
      ***  Otherwise process the records
      ***  continue
      *
     C                     ELSE                            E3
      *
     C                     EXSR #MBRRC
      *
     C                     ENDIF                           E3
      *
     C                     ENDIF                           E2
      *
      *** Read next record
      *
     C                     READ EDOUTFPD                 01
      *
     C                     ENDDO                           E1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#MBRRC
      *****************************************************************
      *                                                               *
      *  #MBRRC - Subroutine to process data file records.            *
      *                                                               *
      *  Called By: #DETL                                             *
      *  Calls    : #BATCH                                            *
      *                                                               *
      *****************************************************************
      *
     C           #MBRRC    BEGSR
      *
      ***       Start and end of copy range for EDC0027
      *
     C                     Z-ADD1         WSTRR   90
     C                     Z-ADDE7BUMR    WENDR   90
      *
      ***   Determine number of whole batches for this member of the
      ***   Data file which is being copied into EDBULKPD for DTA         S01493
      *
      *** Deleted records have to be added first                          103143
      *                                                                   103143
     C                     ADD  MBNDTR    MBNRCD                          103143
     C           MBNRCD    DIV  E7BUMR    WNOWB   90
     C                     MVR            WNOER   90
      *
      ***   Create whole batches
      *
     C                     DO   WNOWB                      B1
      *
     C**********           Z-ADDE7BUMR    URECT   90                      103143
     C                     EXSR #BATCH
      *
     C                     ENDDO                           E1
      *
      *** Final batch unless all records fitted into an exact number of
      *** whole batches
      *
     C           WNOER     IFNE *ZERO
      *
     C**********           Z-ADDWNOER     URECT                           103143
      *
      *** Correct value for last record of final batch
      *
     C           WSTRR     ADD  WNOER     WENDR
     C                     SUB  1         WENDR
     C                     EXSR #BATCH
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#BATCH
      *****************************************************************
      *                                                               *
      *  #BATCH - Subroutine to create a batch                        *
      *                                                               *
      *  Called By: #MBRRC                                            *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           #BATCH    BEGSR
      *
      *** Increment batch name
      *
     C                     ADD  1         WBATNN
      *
      *** Call EDC0024 to create new member of PF/EDBULKPD
      *
     C                     CALL 'EDC0024'
     C                     PARM           WBATCH
     C                     PARM           WRTCD1  1
      *
     C           WRTCD1    IFNE *BLANKS
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'EDC0024 'DBFILE           * DB ERROR 01 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      *** Override to new member of PF/EDBULKPD - Do this in RPG
      *** rather than in EDC0024 as the override is needed at
      *** this level.
      *
     C                     MOVELQOVR      WOVR
     C                     MOVELWBATCH    WDSBAT
     C                     CALL 'QCMDEXC'
     C                     PARM           WOVR
     C                     PARM 54        QLEN   155
      *
      *** Open PF/EDBULKPD now the correct member exists
      *** Write header record of PF/EDBULKPD
      *
     C                     OPEN EDBULKPD
     C                     MOVELUFILEN    EBENTI 10
     C                     EXCPTHDR
      *
      *** Call EDC0027 to copy out the data for PF/EDBULKPD
      *
     C                     CALL 'EDC0027'
     C                     PARM           UFILEN
     C***********          PARM           UMEMBN                          CED001
     C                     PARM           EAMNME                          CED001
     C                     PARM           WBATCH
     C                     PARM           WSTRR
     C                     PARM           WENDR
     C                     PARM           WRTCD1
     C                     PARM           WCOUNT 100                      103143
     C                     PARM           SPP     1                       121889
      *
     C           WRTCD1    IFNE *BLANKS
     C                     Z-ADD002       DBASE            ***************
     C                     MOVEL'EDC0027 'DBFILE           * DB ERROR 02 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      *** Close PF/EDBULKPD now the correct member has been filled
      *
     C                     CLOSEEDBULKPD
      *
      *** Update start and end copy points for next batch
      *
     C                     ADD  E7BUMR    WSTRR
     C                     ADD  E7BUMR    WENDR
      *
      *** Set up data for BATCH LOCATION DETAILS
      *
     C                     MOVELUSVLIB    EFLIBR
     C                     MOVEL'/'       EFSEP1
     C                     MOVEL'EDBULKPD'EFFILE
     C                     MOVEL'.'       EFSEP2
     C                     MOVELWBATCH    EFMEMB
      *
     C*********            WRITEEDSAVED0                                  CED001
     C                     WRITEEDSAVXD0                                  CED001
      *
      *** Set up data for Audit file and write to it
      *
     C                     MOVELWBATCH    EABTCH
     C                     Z-ADDWCOUNT    URECT   90                      103143
     C                     Z-ADDURECT     EARECT
     C                     WRITEEDBUAUD0
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  #INIT  - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           #INIT     BEGSR
      *
      ***  Program entry
      *
     C           *ENTRY    PLIST
     C                     PARM           USVLIB  7
     C***********          PARM           UFILEN 10                       CED001
     C***********          PARM           UMEMBN 10                       CED001
     C           XFILEN    PARM           UFILEN 10                       CED001
     C           XMEMBN    PARM           UMEMBN 10                       CED001
     C                     PARM           USQCD   3                       CED001
     C                     PARM           USTREM  1
      *
      ***  Copyright Statement.
      *
     C                     MOVEACPY@      ACT@   80
      *
      *** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD7  7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD7    IFNE *BLANKS
     C                     Z-ADD003       DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 03 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ELSE
     C                     SETOF                     98
     C                     END
      *
      *** Access ED ICD Details
      *
     C                     CALL 'AOMEXPR0'
     C                     PARM '*MSG   ' WRTCD7
     C                     PARM '*FIRST ' WOPTN
     C           SDMEXP    PARM SDMEXP    DSFDY
      *
     C           WRTCD7    IFNE *BLANKS
     C                     Z-ADD004       DBASE            ***************
     C                     MOVEL'SDMEXPPD'DBFILE           * DB ERROR 04 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      ***  Definitions
      *
     C           *LIKE     DEFN EABTCH    WBCNNM
      *
      ***  Initialisations
      *
      ***       Name of program
      *
     C                     MOVEL'ED0020'  DBPGM
      *
      ***       Blank member name - means all members
      *
     C           UMEMBN    IFEQ *BLANKS
     C                     SETON                     03
     C                     ELSE
     C                     SETOF                     03
     C                     ENDIF
      *
      ***       File and member names for PF/EDBUAUPD Audit file
      *
     C                     MOVELUFILEN    EAFNME
     C           UMEMBN    IFEQ *BLANKS
     C                     MOVELUFILEN    EAMNME
     C                     ELSE
     C                     MOVELUMEMBN    EAMNME
     C                     ENDIF
      *
      ***       Components of batch name and batch location fields
      *
     C                     MOVE *BLANKS   WBCNNM
     C                     MOVEL'EDB'     WEDDTX
     C                     MOVELUSTREM    WSTREM
     C                     MOVELUSQCD     EFSQCD                          CED001
      *
      ***  Get the next number for a batch for this stream from
      ***     DTAARA/EDBULK
      *
     C           *NAMVAR   DEFN           EDBULK
     C           *LOCK     IN   EDBULK
     C                     SELEC
     C           USTREM    WHEQ '0'
     C                     Z-ADDUBTCH0    WBATNN
     C           USTREM    WHEQ '1'
     C                     Z-ADDUBTCH1    WBATNN
     C           USTREM    WHEQ '2'
     C                     Z-ADDUBTCH2    WBATNN
     C           USTREM    WHEQ '3'
     C                     Z-ADDUBTCH3    WBATNN
     C           USTREM    WHEQ '4'
     C                     Z-ADDUBTCH4    WBATNN
     C           USTREM    WHEQ '5'
     C                     Z-ADDUBTCH5    WBATNN
     C           USTREM    WHEQ '6'
     C                     Z-ADDUBTCH6    WBATNN
     C           USTREM    WHEQ '7'
     C                     Z-ADDUBTCH7    WBATNN
     C           USTREM    WHEQ '8'
     C                     Z-ADDUBTCH8    WBATNN
     C           USTREM    WHEQ '9'
     C                     Z-ADDUBTCH9    WBATNN
     C                     ENDSL
     C                     OUT  EDBULK
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#LAST
      *****************************************************************
      *                                                               *
      *  #LAST  - Subroutine to do Program Termination                *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           #LAST     BEGSR
      *
      ***     Update DTAARA/EDBULK
      *
     C           *LOCK     IN   EDBULK
     C                     SELEC
     C           USTREM    WHEQ '0'
     C                     Z-ADDWBATNN    UBTCH0
     C           USTREM    WHEQ '1'
     C                     Z-ADDWBATNN    UBTCH1
     C           USTREM    WHEQ '2'
     C                     Z-ADDWBATNN    UBTCH2
     C           USTREM    WHEQ '3'
     C                     Z-ADDWBATNN    UBTCH3
     C           USTREM    WHEQ '4'
     C                     Z-ADDWBATNN    UBTCH4
     C           USTREM    WHEQ '5'
     C                     Z-ADDWBATNN    UBTCH5
     C           USTREM    WHEQ '6'
     C                     Z-ADDWBATNN    UBTCH6
     C           USTREM    WHEQ '7'
     C                     Z-ADDWBATNN    UBTCH7
     C           USTREM    WHEQ '8'
     C                     Z-ADDWBATNN    UBTCH8
     C           USTREM    WHEQ '9'
     C                     Z-ADDWBATNN    UBTCH9
     C                     ENDSL
     C                     OUT  EDBULK
      *
      *****Delete*current*record*from*PF/EDBUTFPD************************ CED001
      *****This*file*is*actually*the*overridden*PF/EDBUTxPD.**The*current CED001
      *****record*will*be*the*first*one*so*a*simple*READ*will*access*it.* CED001
      ***********                                                      ** CED001
     C***********          READ EDBUTFPD                 02            ** CED001
      ***********                                                      ** CED001
      ********error if record not found                                ** CED001
      ***********                                                      ** CED001
     C************IN02     IFEQ *ON                                    ** CED001
     C***********          Z-ADD005       DBASE            ***************CED001
     C***********          MOVEL'EDBUTFPD'DBFILE           * DB ERROR 05**CED001
     C***********          MOVE '1st recd'DBKEY            ***************CED001
     C***********          EXSR *PSSR                                  ** CED001
     C***********          END                                         ** CED001
      ***********                                                      ** CED001
     C***********          DELETEDBUTFPD                               ** CED001
      ***  Delete record from Failed Bulk Transfers PF/EDFBUTPD           CED001
     C           TFROK     DELETEDFBUTPD             02                   CED001
     C                     COMIT                                          CED001
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
      *
      ***  ERROR REPORT  ED0020AU
      *
     C                     OPEN ED0020AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSEED0020AU
      *
     C                     DUMP
     C                     END
      *
      ** Exit program
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      ***
     OEDBULKPDE                HDR
     O                                   20 'MIDAS_BULK_TRANSFER '
     O                                   24 '4096'
     O                         EBENTI    34
     O                                   40 'UNIQUE'
     O                                   42 'PT'
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
