     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  GENOPT(*OPTIMIZE)                                            *
/*EXI *  TEXT('Midas ED Flag files for bulk transfer')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Export Data Interface Module                   *
      *                                                               *
      *  ED0029 - Flag Files for Bulk Transfer                        *
      *                                                               *
      *  Function:  This program flags any files which are ready for  *
      *             transmission through the bulk transfer files by   *
      *             moving index records from EDSAVXPD to EDSAVEPD.   *
      *                                                               *
      *             If called during the COB (in addition to the call *
      *             from EDC0030 at the end of the COB) it indicates  *
      *             files that are available for transfer during the  *
      *             COB rather than at the end.                       *
      *                                                               *
      *  Called By: EDC0030 - ED COB Termination Processing           *
      *                                                               *
      *  Calls:     AOMEXPR0 - Access Object for ED ICD               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CED002 *CREATE     DATE 13MAY97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CED002  - Created for Midas ED Enhancements                  *
      *                                                               *
      *****************************************************************
      *
     FEDSAVXPDUF  E           K        DISK         KINFSR *PSSR
      * Files ready for Bulk Transfer
      *
     FEDSAVEPDO   E                    DISK         KINFSR *PSSR
      * Files ready for Bulk Transfer
      *
      /TITLE FUNCTION OF INCICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    01  - READ OF EDBUTFPD                                     *
      *                                                               *
      *    98  - Date Format: DDMMYY (off); MMDDYY (on)               *
      *  90-99 - Used by Standard Subroutines                         *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      *
     E                    CPY@    1   1 80
      *  Array for Object Copyright Statement.
      *
      *****************************************************************
      *
     IDSFDY     E DSDSFDY
      ** Short Data Structure for Access Objects
      *
     ISDMEXP    E DSSDMEXPPD
      * ED ICD Details
     I*
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      ** PROGRAM STATUS DATA STRUCTURE
     I           SDS
     I                                      244 253 WSID
     I                                      254 263 USID
      *****************************************************************
      *  P R O G R A M     S T A R T                                  *
      *****************************************************************
      *
      ***  Perform Initialisation.
      *
     C                     EXSR #INIT
      *
      * Bulk Transfer Enabled?
     C           E7BUEN    IFEQ 'Y'
      *   Perform Detail Processing.
     C                     EXSR #DETL
     C                     ENDIF
      *
      *****************************************************************
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                     LR
     C                     RETRN
      *                                                               *
      *****************************************************************
      *                                                               *
      *  #INIT  - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
     C           #INIT     BEGSR
      *
      *
      * Copyright Statement.
     C                     MOVEACPY@      ACT@   80
      *
      * Access ED ICD
     C                     CALL 'AOMEXPR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDMEXP    PARM SDMEXP    DSFDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'SDEXPMPD'DBFILE           * DB ERROR 01 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  #DETL  - Flag files for transfer                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           #DETL     BEGSR
      *
      * Get File
     C                     READ EDSAVXPD                 01
      *
      * Process all Files
     C           *IN01     DOWEQ*OFF
      *
      *
      *   Flag file for transfer by moving from SAVX to SAVE
     C                     WRITEEDSAVED0               80
     C                     DELETEDSAVXD0               80
      *
      * Get Next File
     C                     READ EDSAVXPD                 01
     C                     ENDDO
      *
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
      *
      * Check to see that *PSSR has not already been called.
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'ED0029'  DBPGM
     C                     DUMP
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     ENDIF
      *
      *
      * Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
