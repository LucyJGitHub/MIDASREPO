     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED FX deals file special processing (i/c)')
      *****************************************************************
      *                                                               *
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0120 -  FX Deals File Special Processing - Input Cycle     *
      *                                                               *
      *  Function: This program is one of the 'Special Processing     *
      *            programs' that are called when the Drip Feed       *
      *            process encounters a journal entry from a certain  *
      *            file. In this case the file is PF/DEALSDB. On      *
      *            encountering certain combinations of deal type,    *
      *            RECI and journal entry type certain processing     *
      *            is carried out.                                    *
      *                                                               *
      *  Called By: ED0060 - Extract Journal entries within comit     *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CED004             Date 25Jun97               *
      *                 104193             Date 05JUN96               *
      *                 101973             Date 31JAN96               *
      *                 101971 *CREATE     DATE 17JUL95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CED004 - Include extraction of nostro transfer records (NX)  *
      *           and PS deals                                        *
      *  104193 - Remove FX0380 reference as this is now specified    *
      *           on EDDFTFPD.                                        *
      *  101973 - Phase V Modifications:                              *
      *           -Amended module title and other 'Midas data Export' *
      *            References                                         *
      *           -Exclude PS deals too.                              *
      *           -Correct deletion of journal entries after special  *
      *            processing programs by handling it within those    *
      *            programs.                                          *
      *           -Removed processing by Relative Record Number as    *
      *            this does not work across two files.               *
      *           -Improved error handling.                           *
      *           NOTE: No attempt has been made to retain unwanted   *
      *            Phase 2 code as commented out text.                *
      *  101971 - Phase II Modifications                              *
      *                                                               *
      *****************************************************************
     FEDNEWJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDNEWJD4
      * New journal entries by journal sequence no.
      *
     FEDOLDJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDOLDJD4
     F                                              KCOMIT
      * Old journal entries by journal sequence no.
      *
     FEDJRNEPDO   E                    DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDJRNED1
     F/COPY WNCPYSRC,ED0120F001
      *
      /TITLE FUNCTION OF INCICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    01  -  EoF EDNEWJL4 or EDOLDJL4                            *
      *    50  -  Condition 'BEGIN' (on) or 'END' (off) for commit rcd*
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  PROCES - Perform Detail Processing                           *
      *  DET1   - Perform FX Swap Insertion processing                *
      *  DET2   - Perform FX Swap Deletion processing                 *
      *  DET3   - Perform FX Option Takedown Insert/Delete process    *
      *  BEGEND - Write a EDJRNEPD commit BEGIN or END record         *
      *  DELET1 - Delete a record from LF/EDNEWJL4 or LF/EDOLDJL4     *
      *  WRITE  - Write a EDJRNEPD record                             *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     E                    DATA1     125  1
     E                    DATA2    3971  1
     E                    IN2       397  1
      ***  Arrays for holding parameters or where length exceeds 256
      *
     I/TITLE I-SPECIFICATIONS
      *-----------------------------------------------------------
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for Journal record data parameter
     I                                        1 125 IN1
     I                                        6  150INSEQN
     I                                        6  15 ALSEQN
     I                                       17  18 INENTT
     I                                       57  66 INPGM
      *
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for Entry Specific data in parm
     I                                        1 397 IN2
     I/COPY WNCPYSRC,ED0120I001
     I                                        1   1 RECI
     I                                       20  21 DTYP
     I                                      212 2120FSLI
     I                                      285 2900SODN
     I                                      397 397 CHTP
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for EDJRNEPD output
     I                                        1 125 OUT1
     I                                        1   50JOENTL
     I                                        6  150JOESQN
     I                                       16  16 JOCODE
     I                                       17  18 JOENTT
     I                                       19  24 JODATE
     I                                       25  300JOTIME
     I                                       31  40 JOJOB
     I                                       41  50 JOUSER
     I                                       51  560JONBR
     I                                       57  66 JOPGM
     I                                       67  76 JOOBJ
     I                                       77  86 JOLIB
     I                                       87  96 JOMBR
     I                                       97 1060JOCTRR
     I                                      107 107 JOFLAG
     I                                      108 1170JOCCID
     I                                      108 117 AJCCID
     I                                      118 125 JORES
     I/COPY WNCPYSRC,ED0120I002
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Parameter entry list
      *
     C           *ENTRY    PLIST
      *IN PARAMETERS
     C                     PARM           DATA1
     C                     PARM           DATA2
     C                     PARM           SRCF    1
      *OUT PARAMETERS
     C                     PARM JOOBJ     CDFILE  8
     C                     PARM ALSEQN    CDBKEY 10
     C                     PARM           WRTCD1  1
     C                     PARM           RECCNT  50
      *
      ***  Copyright Statement and initialisation
      *
     C                     MOVEACPY@      MKI@   80
     C                     Z-ADD*ZERO     RECCNT
     C/COPY WNCPYSRC,ED0120C001
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                     LR               *
     C                     RETRN                                      *
      *                                                               *
      *================================================================
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
      *** Set-up the data from the input parameters so that it can be used.
      *
     C                     MOVEADATA1     IN1
     C                     MOVEADATA2     IN2
     C                     MOVEADATA1     OUT1      P
     C                     MOVEADATA2     JOESD     P
      *
      *** Start by deleting the record read in by ED0060.
      *** Use the journal sequence number from the incoming data parameter
      *** to delete the record from 'Old' or 'New' as appropriate.
      *
     C           SRCF      IFEQ 'N'                        B1
      *
     C           INSEQN    DELETEDNEWJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 01 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ELSE                            X1
      *
     C           INSEQN    DELETEDOLDJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD002       DBASE            ***************
     C                     MOVEL'EDOLDJL4'DBFILE           * DB ERROR 02 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
      ****Check*for*FX*update*-*changes*by*other*programs*(such*as*by**   104193
      ****Confirmation*and*Pay/Receive*processing)*are*unwanted.*******   104193
      *                                                                   104193
     C***********INPGM     IFEQ 'FX0380'                   B1             104193
      *
      **  Figure out which case this is and execute the
      **   appropriate subroutine
      *
      ****Case*-*no process for Nostro Transfers                          CED004
      ***********no process for Arbitrage Deals either              101973CED004
      *
     C           DTYP      IFEQ 'NX'                       B2
     C                     EXSR WRITE                                     CED004
     C                     ELSE                            X2             CED004
     C***********DTYP      OREQ 'PS'                                101973CED004
     C           DTYP      IFEQ 'PS'                       B2.1           CED004
     C                     Z-ADDINSEQN    JOCCID                          CED004
     C                     EXSR WRITE                                     CED004
     C/COPY WNCPYSRC,ED0120C002
     C                     ELSE                            X2
      *
      **  Case - Swap Insertion
      *
     C           DTYP      IFEQ 'SW'                       B3
     C           RECI      ANDEQ'D'
     C           INENTT    ANDEQ'PT'
     C           CHTP      ANDEQ'I'
     C                     EXSR DET1
     C                     ELSE                            X3
      *
      **  Case - no process for preparatory read/write Swap delete
      *
     C           DTYP      IFEQ 'SW'                       B4
     C           RECI      ANDEQ'D'
     C           INENTT    ANDEQ'UP'
     C           CHTP      ANDEQ'I'
     C                     ELSE                            X4
      *
      **  Case - Swap deletion
      *
     C           DTYP      IFEQ 'SW'                       B5
     C           RECI      ANDEQ'R'
     C           INENTT    ANDEQ'UP'
     C           CHTP      ANDEQ'R'
     C                     EXSR DET2
     C                     ELSE                            X5
      *
      **  Case - Insert of Option takedown
      *
     C           DTYP      IFEQ 'OT'                       B6
     C           RECI      ANDEQ'D'
     C           INENTT    ANDEQ'PT'
     C                     EXSR DET3
     C                     ELSE                            X6
      *
      **  Case - Deletion of an Option Takedown
      *
     C           DTYP      IFEQ 'OT'                       B7
     C           RECI      ANDEQ'R'
     C           INENTT    ANDEQ'UP'
     C                     EXSR DET3
     C                     ELSE                            X7
      *
      ***  Otherwise - write the record to the batch for DTA (this will
      ***              include FPs etcetera and Swap amendments).
      *
     C                     EXSR WRITE
     C/COPY WNCPYSRC,ED0120C003
      *
     C                     END                             E7
     C                     END                             E6
     C                     END                             E5
     C                     END                             E4
     C                     END                             E3
     C                     END                             E2.1           CED004
     C                     END                             E2
      *
     C***********          ENDIF                           E1             104193
     C                     ENDSR
      *
      /TITLE SR/DET1
      *****************************************************************
      *                                                               *
      *  DET1 - Process for Swap Insertion                            *
      *                                                               *
      *  Called By: PROCES                                            *
      *                                                               *
      *****************************************************************
      *
     C           DET1      BEGSR
      *
      **  If First or Second leg of Swap then write the journal record to
      **  PF/EDJRNEPD. It is possible, because PF/DEALSDB is written to on
      **  authorisation (not insert) of a deal, that FSLI has not yet been
      **  set for this swap.
      *
     C           FSLI      IFEQ 1                          B1
     C           FSLI      OREQ 2
      *
     C                     EXSR WRITE
     C/COPY WNCPYSRC,ED0120C004
      *
      ***  Increment journal sequence number so that the next record can
      ***  be accessed directly and then deleted.
      *
     C                     ADD  2         INSEQN
     C                     EXSR DELET1
      *
      ***  ELSE this cannot be treated as the 1st/2nd leg of a swap
      ***  at this stage so force it to be an Outright FX deal for DTA.
      *
     C                     ELSE                            X1
      *
     C                     MOVEL'CX'      DTYP
     C                     Z-ADD*ZEROS    SODN
     C***GCMS TEAM REPORT CX DEALS ARE MISSING END OF RECORD              CED004
     C******               MOVEAIN2       JOESD     P                     CED004
     C                     MOVEAIN2       JOESD                           CED004
      *
      ** Write the record to EDJRNEPD
      *
     C                     EXSR WRITE
     C/COPY WNCPYSRC,ED0120C005
     C                     END                             E1
      *
     C                     ENDSR
      *
      /TITLE SR/DET2
      *****************************************************************
      *                                                               *
      *  DET2 - Process for Swap Deletion                             *
      *                                                               *
      *  Called By: PROCES                                            *
      *                                                               *
      *****************************************************************
      *
     C           DET2      BEGSR
      *
      ***  Write the 1st leg deletion to PF/EDJRNEPD.
      *
     C                     EXSR WRITE
      *
      ***  Increment journal sequence number so that the next record can
      ***  be accessed directly and then deleted.
      *
     C                     ADD  2         INSEQN
     C                     EXSR DELET1
      *
     C                     ENDSR
      *
      /TITLE SR/DET3
      *****************************************************************
      *                                                               *
      *  DET3 - Process for Option Takedown                           *
      *                                                               *
      *  Called By: PROCES                                            *
      *                                                               *
      *****************************************************************
      *
     C           DET3      BEGSR
      *
      ***  Write a BEGIN group into PF/EDJRNEPD
      *
     C                     MOVE *ON       *IN50
     C                     EXSR BEGEND
      *
      ***  As a BEGIN has just been written move the data back into
      ***  the file buffer and change commitment group identity
      *
     C                     MOVEADATA1     OUT1      P
     C                     MOVEADATA2     JOESD     P
     C                     MOVEL'A'       AJCCID
      *
      ***  Write to the export batch (PF/EDJRNEPD)
      *
     C                     EXSR WRITE
      *
      ***  Read the second part of the Option Takedown (i.e. the update
      ***  after image for the original option).  This is accessed by
      ***  journal sequence number.
      *
     C                     ADD  2         INSEQN
      *
     C           SRCF      IFEQ 'O'                        B1
     C           INSEQN    CHAINEDOLDJD4             01
      *
      *** If found then delete the record
      *
     C           *IN01     IFEQ *OFF                       B2
     C                     DELETEDOLDJD4
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
      *** If from 'New' or not found on 'Old' then try 'New'
      *
     C           SRCF      IFEQ 'N'                        B1
     C           *IN01     OREQ *ON
      *
     C           INSEQN    CHAINEDNEWJD4             01
      *
     C           *IN01     IFEQ *ON                        B2
     C                     Z-ADD003       DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 03 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
      *** delete the record
      *
     C                     DELETEDNEWJD4
      *
     C                     ENDIF                           E1
      *
      *** Write the second record to PF/EDJRNEPD
      *
     C                     EXSR WRITE
      *
      ** Write End Commit
     C                     MOVE *OFF      *IN50
     C                     EXSR BEGEND
      *
     C                     ENDSR
     C/COPY WNCPYSRC,ED0120C006
      *
      /TITLE SR/WRITE
      *****************************************************************
      *                                                               *
      *  WRITE - Write a detail record to EDJRNEPD                    *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           WRITE     BEGSR
      *
     C                     WRITEEDJRNED1
      *
     C                     ADD  1         RECCNT
      *
     C                     ENDSR
      *
      /TITLE SR/DELET1
      *****************************************************************
      *                                                               *
      *  DELET1 - Delete a record from LF/EDNEWJL4 or PF/EDOLDJL4     *
      *                                                               *
      *  Called By: DET1, DET2                                        *
      *                                                               *
      *****************************************************************
      *
     C           DELET1    BEGSR
      *
      ***  If the record to be processed is from 'Old' ....
      *
     C           SRCF      IFEQ 'O'                        B1
      *
     C           INSEQN    DELETEDOLDJD4             01
      *
     C                     ENDIF                           E1
      *
      *** If from 'New' or not found on 'Old' then try 'New'
      *
     C           SRCF      IFEQ 'N'                        B1
     C           *IN01     OREQ *ON
      *
     C           INSEQN    DELETEDNEWJD4             01
      *
      *** If the record is not found then database error
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD004       DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 04 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
     C                     ENDSR
      *
      /TITLE SR/BEGEND
      *****************************************************************
      *                                                               *
      *  BEGEND - Write a BEGIN or END record to EDJRNEPD             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           BEGEND    BEGSR
      *
      **  Initialize EDJRNEPD format
      *
     C                     CLEAREDJRNED1
      *
     C           *IN50     IFEQ *ON                        B1
     C                     MOVEL'BEGIN'   JOOBJ
     C                     ELSE                            X1
     C                     MOVEL'END'     JOOBJ
     C                     ENDIF                           E1
      *
     C                     WRITEEDJRNED1
     C                     ADD  1         RECCNT
      *
     C                     ENDSR
      *
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
     C                     MOVE 'E'       WRTCD1
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'ED0120'  DBPGM
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     DUMP
     C                     END                             E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Finastra International Limited 2001
