     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED FF trans file special processing (i/c)')
      *****************************************************************
      *                                                               *
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0160 -  FF Transactions File Special Processing            *
      *            (Input Cycle)                                      *
      *                                                               *
      *  Function: This program is one of the 'Special Processing     *
      *            programs' that are called when the Drip Feed       *
      *            process encounters a journal entry from a certain  *
      *            file.  In this case the file is PF/TRANSD.  On     *
      *            encountering an insert of an Option Exercise,      *
      *            the next TRANSD record in the commitment group is  *
      *            deleted because this is the amendment to the       *
      *            original Options deal and is not required by GM,   *
      *            and will cause problems for DTA as it shares the   *
      *            commit id with Option Exercise record.             *
      *                                                               *
      *  Called By: ED0060 - Extract Journal entries within comit     *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 101973 *CREATE     Date 29JAN96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  101973 - Phase V Modifications                               *
      *                                                               *
      *****************************************************************
      *
     FEDNEWJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDNEWJD4
      * New journal entries by journal sequence no.
      *
     FEDOLDJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDOLDJD4
     F                                              KCOMIT
      * Old journal entries by journal sequence no.
      *
     FEDJRNEPDO   E                    DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDJRNED1
      * Journal entries for Export to DTA
      *
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    01  -  EoF EDNEWJL4 or EDOLDJL4                            *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     E                    DATA1     125  1
     E                    DATA2    3971  1
      *
     I/TITLE I-SPECIFICATIONS
      *-----------------------------------------------------------
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for Journal record data parameter
     I                                        1 125 IN1
     I                                        6  150INSEQN
     I                                        6  15 ALSEQN
     I                                      108 1170INCCID
      *
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for Entry Specific data in parm
     I                                        1 112 IN2
     I                                       41  41 TRTY
     I                                      112 112 CHTP
      *
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for EDJRNEPD output
     I                                        1 125 OUT1
     I                                        1   50JOENTL
     I                                        6  150JOSEQN
     I                                       16  16 JOCODE
     I                                       17  18 JOENTT
     I                                       19  24 JODATE
     I                                       25  300JOTIME
     I                                       31  40 JOJOB
     I                                       41  50 JOUSER
     I                                       51  560JONBR
     I                                       57  66 JOPGM
     I                                       67  76 JOOBJ
     I                                       77  86 JOLIB
     I                                       87  96 JOMBR
     I                                       97 1060JOCTRR
     I                                      107 107 JOFLAG
     I                                      108 1170JOCCID
     I                                      118 125 JORES
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Parameter entry list
      *
     C           *ENTRY    PLIST
     C                     PARM           DATA1
     C                     PARM           DATA2
     C                     PARM           SRCF    1
     C                     PARM JOOBJ     CDFILE  8
     C                     PARM ALSEQN    CDBKEY 10
     C                     PARM           WRTCD1  1
     C                     PARM           RECCNT  50
      *
      ***  Copyright Statement.
      *
     C                     MOVEACPY@      MKI@   80
      *
      *
      ***  Move input parameters data to output data structures and
      ***  write the record read in calling program to PF/EDJRNEPD.
      *
     C                     MOVEADATA1     IN1
     C                     MOVEADATA2     IN2
     C                     MOVEADATA1     OUT1      P
     C                     MOVEADATA2     JOESD     P
      *
     C                     WRITEEDJRNED1
     C                     Z-ADD1         RECCNT
      *
      *** Use the journal sequence number from the incoming data parameter
      *** to delete the record from 'Old' or 'New' as appropriate.
      *
     C           SRCF      IFEQ 'N'                        B1
      *
     C           INSEQN    DELETEDNEWJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 01 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ELSE                            X1
      *
     C           INSEQN    DELETEDOLDJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD002       DBASE            ***************
     C                     MOVEL'EDOLDJL4'DBFILE           * DB ERROR 02 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
      ***  If this was a new Exercise Option then read and delete the next
      ***  PF/TRANSD record for this commitment group as it is unwanted.
      ***  There will be such a record if this is an insert or delete;
      ***  This is 3 or 4 records after the first. (We skip over the
      ***  intervening insert or update of PF/TRSETD.)  The next records
      ***  are the update for PF/TRANSD for the option contracts that are
      ***  being exercised.
      *
     C           TRTY      IFEQ 'E'                        B1
      *
      *** At the time of writing, a bug in FF module means that CHTP cannot
      *** always be relied on for Updates to option Exercise Transactions
      *** so JOENTT has to be checked too.
      *
     C           JOENTT    IFEQ 'PT'                       B2
     C           JOENTT    OREQ 'UP'
     C           CHTP      ANDEQ'D'
      *
     C           JOENTT    IFEQ 'PT'                       B3
     C                     ADD  3         INSEQN
     C                     ELSE                            X3
     C                     ADD  4         INSEQN
     C                     ENDIF                           B3
      *
      *** If from 'Old' then delete the 2nd record on 'Old'.
      *
     C           SRCF      IFEQ 'O'                        B3
      *
     C           INSEQN    DELETEDOLDJD4             01
      *
     C                     ENDIF                           E3
      *
      *** If from 'New' or not found on 'Old' then try 'New'
      *
     C           SRCF      IFEQ 'N'                        B3
     C           *IN01     OREQ *ON
      *
     C           INSEQN    DELETEDNEWJD4             01
      *
      *** If the record is not found then database error
      *
     C           *IN01     IFEQ '1'                        B4
     C                     Z-ADD003       DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 03 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E4
      *
     C                     ENDIF                           E3
      *
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     MOVE *BLANK    WRTCD1
     C                     SETON                     LR
     C                     RETRN
      *
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
     C                     MOVE 'E'       WRTCD1
      *
      ***  Check to see that *PSSR has not already been called.
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'ED0160'  DBPGM
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     DUMP
     C                     END                             E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
