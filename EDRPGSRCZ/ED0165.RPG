     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED Accounts master special processing (COB)')    *
      *****************************************************************
      *                                                               *
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0165 -  ED Special processing for ACCNT - COB              *
      *                                                               *
      *  Function: Program creates bulk transfer batches from         *
      *            file PF/ACCNTAB. The DTA Preamble type used is     *
      *            the Drip Feed type, NOT the bulk transfer type.    *
      *            This is not the same as the rest of Bulk Transfer. *
      *                                                               *
      *  Called By: EDC0020 - Bulk Transfer Component                 *
      *                                                               *
      *  Calls:     AOBANKR0 - Access Object for Bank Details ICD     *
      *             AOMEXPR0 - Access Object for ED ICD               *
      *             EDC0024  - Bulk Transfer Member Creation          *
      *                                                               *
      *  Notes:  i) ED0165AU is only printed for Database Errors, not *
      *             each time this program runs.                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CRE075             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CED002  *CREATE    Date 11Jun97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CED002 - ED Special Processing Program                       *
      *                                                               *
      *****************************************************************
     F*
     FACCNT   IP AE           K        DISK
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
     FGLACBTL0IS AE           K        DISK
      **  Account Balances Today
     FEDFBUTPDUF  E           K        DISK         KINFSR *PSSR
      **  List of files to transfer to DTA
     FEDBULKPDO   F    4096            DISK         KINFSR *PSSR      UC
      **  Bulk Transfer data
     FEDSAVXPDO   E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      **  Batches to send to DTA
     FEDBUAUPDO   E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
      **  Audit File
     FED0165AUO   E                    PRINTER                        UC
      *   Audit Report
      *
      /TITLE FUNCTION OF INCICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    32  - Do 'Close the Batch' processing                      *
      *    33  - NO EDFBUTPD Record Found To Delete                   *
      *                                                               *
      *    98  - Date Format: DDMMYY (off); MMDDYY (on)               *
      *  90-99 - Used by Standard Subroutines                         *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #INIT  - Initial Processing                                  *
      *  #DETL  - Detail Processing                                   *
      *  #OPNBH - Open New Batch                                      *
      *  #CLSBH - Close Batch Processing                              *
      *  #CLOSE - Termination processing                              *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     E                    DATA     4096  1
      *
     I/TITLE I-SPECIFICATIONS
      *
      *-----------------------------------------------------------
     IACCNTABF    01
     I                                              CNUM    M5
     I                                              CCY     M4
     I                                              ACOD    M3
     I                                              ACSQ    M2
     I                                              BRCA    M1
      *-----------------------------------------------------------
     IGLACBLF     02
     I              RECI                            ARECI
     I              CNUM                            ACNUM   M5
     I              CCY                             ACCY    M4
     I              ACOD                            AACOD   M3
     I              ACSQ                            AACSQ   M2
     I              BRCA                            ABRCA   M1
     I              LDBL                            ALDBL
     I              CLBL                            ACLBL
      *-----------------------------------------------------------
     IPSDS       SDS
      *
      **  Program status data structure.
      *
     I                                      244 253 WJOB
     I                                      254 263 WUSER
     I                                      264 2690WJOBNO
     I*
      *-----------------------------------------------------------
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
      *-----------------------------------------------------------
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
      *-----------------------------------------------------------
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
      *-----------------------------------------------------------
     ISDMEXP    E DSSDMEXPPD
      ** ED ICD Details
      *
      *-----------------------------------------------------------
     IACCDS     E DSACCNTAB
      ** Account Details
      *
      *-----------------------------------------------------------
     IEDBULK      DS
      ** ED ICD Details
     I                                        1   60UBTCH0
     I                                        7  120UBTCH1
     I                                       13  180UBTCH2
     I                                       19  240UBTCH3
     I                                       25  300UBTCH4
     I                                       31  360UBTCH5
     I                                       37  420UBTCH6
     I                                       43  480UBTCH7
     I                                       49  540UBTCH8
     I                                       55  600UBTCH9
      *
      *-----------------------------------------------------------
     IWBATCH      DS                             10
      ** Batch Name field
     I                                        1   3 WEDDTX
     I                                        4   40WSTREM
     I                                        5  100WBATNN
      *
      *-----------------------------------------------------------
     IWOVR        DS                             54
      ** DS for QCMDEXC
     I                                       44  53 WDSBAT
      *
      *-----------------------------------------------------------
      ** Constant for QCMDEXC
     I              'OVRDBF FILE(EDBULKPD-C         QOVR
     I              ') TOFILE(EDBULKPD) M-
     I              'BR(XXXXXXXXXX)'
      *
      *-----------------------------------------------------------
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *-----------------------------------------------------------
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ** If the 1st cycle processing indicator is off
      *
     C           *IN10     IFEQ '0'                        B01
      *
      ** Perform Initialisation.
     C                     EXSR #INIT
     C                     ENDIF                           E01
      *
     C           *INMR     IFEQ *OFF                       B01
      *
      ** If ACCNT record read (with no matching GLACBTL0 record)
     C           *IN01     IFEQ *ON                          B02
      *
      ** Account opened today ?
     C           DACC      IFEQ 0                              B03
     C                     MOVE 'I'       CHTP
     C                     Z-ADDBJRDNB    LCD
     C                     EXSR #DETL
     C                     ELSE                                X03
      ** Record inserted and deleted today - skip to next record
     C                     GOTO NOPROC
     C                     ENDIF                               E03
     C                     ENDIF                             E02
      *
      ** If GLACBTL0 record read (with no matching ACCNT record)
      ** ACCNT record dropped today
     C           *IN02     IFEQ *ON                          B02
     C                     GOTO NOPROC
     C                     ENDIF                             E02
      *
     C                     ELSE                            X01
      ** MR is ON and both ACCNT and GLACBTL0 records exist
      *
      ** If ACCNT record read
     C           *IN01     IFEQ *ON                          B02
      ** All processing is performed on the GLACBTL0 record - skip
     C                     GOTO NOPROC
     C                     ELSE                              X02
      ** GLACBTL0 record read
      *
     C           ARECI     IFEQ 'C'                            B03
      *
      ** Record not live => account must be re-opened after closure
     C           DACC      IFEQ 0                                B04
     C                     MOVE 'I'       CHTP
     C                     Z-ADDBJRDNB    LCD
     C                     EXSR #DETL
     C                     ELSE                                  X04
      *
      ** Account was closed earlier - skip to next record
     C                     GOTO NOPROC
     C                     ENDIF                                 E04
      *
     C                     ELSE                                X03
      ** Account previously open
      *
      ** Balance Changed?
     C           DACC      IFEQ 0                                B04
     C           LDBL      IFNE ALDBL                                B05
     C           CLBL      ORNE ACLBL
     C                     MOVE 'A'       CHTP
     C                     Z-ADDBJRDNB    LCD
     C                     EXSR #DETL
     C                     ELSE                                      X05
      *
      ** Balance Not Changed Today
      ** Therefore No extraction is required => skip to next record
     C                     GOTO NOPROC
     C                     ENDIF                                     E05
      *
     C                     ELSE                                  X04
      *
      ** Account Closed Today
     C                     MOVE 'C'       CHTP
     C                     Z-ADDBJRDNB    LCD
     C                     EXSR #DETL
     C                     ENDIF                                 E04
     C                     ENDIF                               E03
     C                     ENDIF                             E02
     C                     ENDIF                           E01
      *
      ** No processing necessary, continue to next record
     C           NOPROC    TAG
      *
     CLR                   EXSR #CLOSE
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#INIT
      *****************************************************************
      *                                                               *
      *  #INIT - Initial Processing.                                  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           #INIT     BEGSR
      *
      *** Program entry
      *
     C           *ENTRY    PLIST
     C                     PARM           USVLIB  7
     C                     PARM           UFILEN 10
     C                     PARM           UMEMBN 10
     C                     PARM           USQCD   3
     C                     PARM           USTREM  1
      *
      ***  Copyright Statement.
      *
     C                     MOVEACPY@      MKI@   80
      *
      *** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD1  7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD1    IFNE *BLANKS
     C                     Z-ADD01        DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 01 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      *** Access ED ICD Details
      *
     C                     CALL 'AOMEXPR0'
     C                     PARM '*MSG   ' WRTCD2  7
     C                     PARM '*FIRST ' WOPTN
     C           SDMEXP    PARM SDMEXP    DSFDY
      *
     C           WRTCD2    IFNE *BLANKS
     C                     Z-ADD02        DBASE            ***************
     C                     MOVEL'SDMEXPPD'DBFILE           * DB ERROR 02 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      ***  Initialisations
      ***  Initialise Bulk Transfer Audit File fields
     C                     MOVE *BLANKS   EABTCH
     C                     MOVE *BLANKS   EAFNME
     C                     MOVE *BLANKS   EAMNME
     C                     Z-ADD*ZERO     EARECT
      *
      ***  Name of program
      *
     C                     MOVEL'ED0165'  DBPGM
      *
      ***  File and member names for PF/EDBUAUPD Audit file
      *
     C                     MOVEL'ACCNTAB' EAFNME
     C                     MOVEL'ACCNTAB' EAMNME
      *
      ***  Components of batch name and batch location fields
      *
     C                     MOVEL'EDB'     WEDDTX
     C                     MOVELUSTREM    WSTREM
      *
      ***  Get the next number for a batch for this stream from
      ***     DTAARA/EDBULK
      *
     C           *NAMVAR   DEFN           EDBULK
     C           *LOCK     IN   EDBULK
     C                     SELEC
     C           USTREM    WHEQ '0'
     C                     Z-ADDUBTCH0    WBATNN
     C           USTREM    WHEQ '1'
     C                     Z-ADDUBTCH1    WBATNN
     C           USTREM    WHEQ '2'
     C                     Z-ADDUBTCH2    WBATNN
     C           USTREM    WHEQ '3'
     C                     Z-ADDUBTCH3    WBATNN
     C           USTREM    WHEQ '4'
     C                     Z-ADDUBTCH4    WBATNN
     C           USTREM    WHEQ '5'
     C                     Z-ADDUBTCH5    WBATNN
     C           USTREM    WHEQ '6'
     C                     Z-ADDUBTCH6    WBATNN
     C           USTREM    WHEQ '7'
     C                     Z-ADDUBTCH7    WBATNN
     C           USTREM    WHEQ '8'
     C                     Z-ADDUBTCH8    WBATNN
     C           USTREM    WHEQ '9'
     C                     Z-ADDUBTCH9    WBATNN
     C                     ENDSL
     C                     OUT  EDBULK
      *
      ***  More initialisation.
      *
     C                     Z-ADDWBATNN    WBATXX  60
     C                     Z-ADD1         WCOMIT  90
     C                     Z-ADD0         WCOUNT  50
      *
      ** Set on 1st Cycle Indicator
      *
     C                     MOVE '1'       *IN10
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#DETL
      *****************************************************************
      *                                                               *
      *  #DETL - Detail Processing.                                   *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : #CLSBH                                            *
      *             #OPNBH                                            *
      *                                                               *
      *****************************************************************
      *
     C           #DETL     BEGSR
      *
      *** If record found and record count >= maximum batch size
      *
     C           WCOUNT    IFGE E7BUMR
     C                     EXSR #CLSBH
     C                     ENDIF
      *
     C           *IN32     IFEQ *OFF
     C                     SETON                     32
     C                     EXSR #OPNBH
     C                     ENDIF
      *
     C                     MOVEAACCDS     DATA
      *
      *** Write record to PF/EDBULKPD
      *
     C                     EXCPTDTL
     C                     ADD  1         WCOUNT
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#OPNBH
      *****************************************************************
      *                                                               *
      *  #OPNBH - Open new Batch.                                     *
      *                                                               *
      *  Called By: #DETL                                             *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           #OPNBH    BEGSR
      *
      *** Increment batch name
      *
     C                     ADD  1         WBATNN
      *
      *** Call EDC0024 to create new member of PF/EDBULKPD
      *
     C                     CALL 'EDC0024'
     C                     PARM           WBATCH
     C                     PARM           WRTCD4  1
      *
     C           WRTCD4    IFNE *BLANKS
     C                     Z-ADD04        DBASE            ***************
     C                     MOVEL'EDC0024 'DBFILE           * DB ERROR 04 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      *** Override to new member of PF/EDBULKPD - Do this in RPG
      *** rather than in EDC0024 as the override is needed at
      *** this level.
      *
     C                     MOVELQOVR      WOVR
     C                     MOVELWBATCH    WDSBAT
     C                     CALL 'QCMDEXC'
     C                     PARM           WOVR
     C                     PARM 54        QLEN   155
      *
      *** Open PF/EDBULKPD now the correct member exists
      *** Write header record of PF/EDBULKPD
      *
     C                     OPEN EDBULKPD
     C                     EXCPTHDR
      *
      *** Add one to record count.
      *
     C                     ADD  1         WCOUNT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#CLSBH
      *****************************************************************
      *                                                               *
      *  #CLSBH - Close Batch Processing.                             *
      *                                                               *
      *  Called By: #DETL                                             *
      *             #CLOSE                                            *
      *  Calls    : #BATCH                                            *
      *                                                               *
      *****************************************************************
      *
     C           #CLSBH    BEGSR
      *
     C                     CLOSEEDBULKPD
      *
      *** Set up data for BATCH LOCATION DETAILS
      *
     C                     MOVELUSVLIB    EFLIBR
     C                     MOVEL'/'       EFSEP1
     C                     MOVEL'EDBULKPD'EFFILE
     C                     MOVEL'.'       EFSEP2
     C                     MOVELWBATCH    EFMEMB
     C                     MOVEL*BLANKS   EFBLNK
     C                     MOVELUSQCD     EFSQCD
      *
     C                     WRITEEDSAVXD0
      *
      *** Set up data for Audit file and write to it
      *
     C                     MOVELWBATCH    EABTCH
     C                     Z-ADDWCOUNT    EARECT
     C                     WRITEEDBUAUD0
      *
      *** Set record count to zero.
      *
     C                     Z-ADD0         WCOUNT
      *
      *** Set off the 1st extract record of batch flag
      *
     C                     SETOF                     32
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      /TITLE SR/#CLOSE
      *****************************************************************
      *                                                               *
      *  #CLOSE - Program Termination Processing.                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : #INIT                                             *
      *             #CLSBH                                            *
      *                                                               *
      *****************************************************************
      *
     C           #CLOSE    BEGSR
      *
      ** If no records have been processed
      *
     C           WCOUNT    IFEQ 0
     C                     EXSR #INIT
     C                     ENDIF
      *
      ** If 1st extract record of batch flag is still off and
      ** batch number in program is the same as that read from EDBULK,
      ** no extract records have been generated
      *
     C           *IN32     IFEQ *OFF                       B01
     C           WBATNN    ANDEQWBATXX
      *
      *** Set up fields for PF/EDBUAUPD and WRITE.
     C                     MOVEL'No Batch'EABTCH
     C                     Z-ADD*ZERO     EARECT
     C                     WRITEEDBUAUD0
     C                     ENDIF                           E01
      *
      ** If 1st extract record of batch flag is on - close the batch
     C           *IN32     IFEQ *ON
     C                     EXSR #CLSBH
     C                     ENDIF
      *
      ***     Update DTAARA/EDBULK
     C           *LOCK     IN   EDBULK
     C                     SELEC
     C           USTREM    WHEQ '0'
     C                     Z-ADDWBATNN    UBTCH0
     C           USTREM    WHEQ '1'
     C                     Z-ADDWBATNN    UBTCH1
     C           USTREM    WHEQ '2'
     C                     Z-ADDWBATNN    UBTCH2
     C           USTREM    WHEQ '3'
     C                     Z-ADDWBATNN    UBTCH3
     C           USTREM    WHEQ '4'
     C                     Z-ADDWBATNN    UBTCH4
     C           USTREM    WHEQ '5'
     C                     Z-ADDWBATNN    UBTCH5
     C           USTREM    WHEQ '6'
     C                     Z-ADDWBATNN    UBTCH6
     C           USTREM    WHEQ '7'
     C                     Z-ADDWBATNN    UBTCH7
     C           USTREM    WHEQ '8'
     C                     Z-ADDWBATNN    UBTCH8
     C           USTREM    WHEQ '9'
     C                     Z-ADDWBATNN    UBTCH9
     C                     ENDSL
     C                     OUT  EDBULK
      *
      ***  Delete record from Failed Bulk Transfers PF/EDFBUTPD
     C           *LIKE     DEFN EDTRFN    W#FILN
     C                     MOVELUFILEN    W#FILN
     C           W#FILN    DELETEDFBUTPD             33
     C                     COMIT
      *
      ** Program End
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
      *
      ***  ERROR REPORT  ED0165AU
      *
     C                     OPEN ED0165AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSEED0165AU
      *
     C                     DUMP
     C                     END
      *
      ** Exit program
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      ***
     OEDBULKPDE                HDR
     O                                   20 'MIDAS_BULK_TRANSFER '
     O                                   24 '4096'
     O                                   42 'ACCNTAB   UNIQUEPT'
     OEDBULKPDE                DTL
     O                         DATA    4096
**  CPY@
(c) Finastra International Limited 2001
