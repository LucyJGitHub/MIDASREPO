     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED A/c rept code special processing (i/c)')
      *****************************************************************
      *                                                               *
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0180 - Account Code-Report Code special processing /       *
      *           Input cycle.                                        *
      *                                                               *
      *  Function: This program is one of the 'Special Processing     *
      *            programs' that are called when the Drip Feed       *
      *            process encounters a journal entry from a certain  *
      *            file. In this case the file is PF/SDACRCPD.        *
      *                                                               *
      *  Called By: ED0060 - Extract Journal entries within comit     *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CED002 *CREATE     Date 30APR97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CED002  - Midas ED Enhancements                              *
      *                                                               *
      *****************************************************************
      *
     FSDACRCL0IF  E           K        DISK         KINFSR *PSSR
      * Account Code-Report Codes table
      *
     FEDNEWJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDNEWJD4
      * New journal entries by journal sequence
      *
     FEDOLDJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDOLDJD4
      * Old journal entries by journal sequence
      *
     FEDJRNEPDO   E                    DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDJRNED1
      *
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    01  -  EoF EDNEWJL4 or EDOLDJL4                            *
      *    40  -  Access to Account Code-Report codes file            *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Initial Processing                                  *
      *  PROCES - Perform Detail Processing                           *
      *  WRITE  - Write a EDJRNEPD record                             *
      *  FORMAT - Format SDACRCPD drip feed record                    *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      **  Array for Object Copyright Statement.
      *
     E                    DATA1     125  1
     E                    DATA2    3971  1
      *
     I/TITLE I-SPECIFICATIONS
      *
     IDBERR       DS                            256
      ** Data structure for data-base error processing.
      *
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     I            DS
      ** Data Structure for Journal record data parameter
      *
     I                                        1 125 IN1
     I                                        1   50INENTL
     I                                        6  150INSEQN
     I                                        6  15 ALSEQN
     I                                       16  16 INCODE
     I                                       17  18 INENTT
     I                                       19  24 INDATE
     I                                       25  300INTIME
     I                                       31  40 INJOB
     I                                       41  50 INUSER
     I                                       51  560INNBR
     I                                       57  66 INPGM
     I                                       67  76 INOBJ
     I                                       77  86 INLIB
     I                                       87  96 INMBR
     I                                       97 1060INCTRR
     I                                      107 107 INFLAG
     I                                      108 1170INCCID
     I                                      118 125 INRES
      *
     I            DS
      ** Data Structure for Entry Specific data in parm
      *
     I                                        1 253 IN2
     I                                        1   1 IRSNO
     I**********                              2   5 IRSCD                                     CGL029
     I**********                              6   9 IACCD                                     CGL029
     I**********                             10  10 IDRCR                                     CGL029
     I**********                          P  11  130ILCDR                                     CGL029
     I**********                          P  14  140ITYLC                                     CGL029
     I                                        2  11 IRSCD                                     CGL029
     I                                       12  21 IACCD                                     CGL029
     I                                       22  22 IDRCR                                     CGL029
     I                                    P  23  250ILCD                                      CGL029
     I                                       26  26 ITYLC                                     CGL029
      *
     I            DS
      ** Data Structure for EDJRNEPD output
      *
     I                                        1 125 OUT1
     I                                        1   50JOENTL
     I                                        6  150JOSEQN
     I                                       16  16 JOCODE
     I                                       17  18 JOENTT
     I                                       19  24 JODATE
     I                                       25  300JOTIME
     I                                       31  40 JOJOB
     I                                       41  50 JOUSER
     I                                       51  560JONBR
     I                                       57  66 JOPGM
     I                                       67  76 JOOBJ
     I                                       77  86 JOLIB
     I                                       87  96 JOMBR
     I                                       97 1060JOCTRR
     I                                      107 107 JOFLAG
     I                                      108 1170JOCCID
     I                                      118 125 JORES
      *
      *
      *****************************************************************
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      **  Parameter entry list
      *
     C           *ENTRY    PLIST
      ** Parameters in
      *
     C                     PARM           DATA1
     C                     PARM           DATA2
     C                     PARM           SRCF    1
      ** Parameters out
      *
     C                     PARM JOOBJ     CDFILE  8
     C                     PARM ALSEQN    CDBKEY 10
     C                     PARM           WRTCD1  1
     C                     PARM           RECCNT  50
      *
      **  Key list for SDACRCL0
      *
     C           KEY1      KLIST
     C                     KFLD           KRSNO   1
     C**********           KFLD           KRSCD   4                                           CGL029
     C**********           KFLD           KACCD   4                                           CGL029
     C                     KFLD           KRSCD  10                                           CGL029
     C                     KFLD           KACCD  10                                           CGL029
     C                     KFLD           KDRCR   1
      *
      **  Copyright Statement and initialisation
      *
     C                     MOVEACPY@      ACT@   80
     C                     Z-ADD*ZERO     RECCNT
      *
      **  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
     C                     SETON                     LR
     C                     RETRN
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *================================================================
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
      ** Set-up the data from the input parameters so that it can be u sed.
      *
     C                     MOVEADATA1     IN1
     C                     MOVEADATA2     IN2
      *
     C           IRSCD     IFNE '****'
     C           IACCD     ANDNE'    '
     C                     EXSR FORMAT
     C                     MOVELIN1       OUT1      P
     C                     MOVELIN2       JOESD     P
      *
      **  Write to the batch member
      *
     C                     EXSR WRITE
     C                     ENDIF
      *
      **  Continue by deleting the record read in by ED0060.
      **  Use the journal sequence number from the incoming data parameter
      **  to delete the record from 'Old' or 'New' as appropriate.
      *
     C           SRCF      IFEQ 'N'                        B1
      *
     C           INSEQN    DELETEDNEWJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 01 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ELSE                            X1
      *
     C           INSEQN    DELETEDOLDJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD002       DBASE            ***************
     C                     MOVEL'EDOLDJL4'DBFILE           * DB ERROR 02 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WRITE
      *****************************************************************
      *                                                               *
      *  WRITE - Write a detail record to EDJRNEPD                    *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           WRITE     BEGSR
      *
     C                     WRITEEDJRNED1
     C                     ADD  1         RECCNT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/FORMAT
      *****************************************************************
      *                                                               *
      *  FORMAT - Format SDACRCPD drip feed record for EDJRNEPD       *
      *                                                               *
      *  Called By: PROCES                                            *
      *                                                               *
      *****************************************************************
      *
     C           FORMAT    BEGSR
      *
      ** Set up key list
      *
     C                     MOVE IRSNO     KRSNO
     C                     MOVE IRSCD     KRSCD
     C                     MOVE IACCD     KACCD
      *
      ** Invert debit/credit indicator
      *
     C           IDRCR     IFEQ '0'                        B1
     C                     MOVE '1'       KDRCR
     C                     ELSE                            X1
     C                     MOVE '0'       KDRCR
     C                     ENDIF                           E1
      *
      ** Find opposite record
      *
     C           KEY1      CHAINSDACRCL0             40
      *
      ** Opposite record found
      *
     C           *IN40     IFEQ '0'                        B1
      *
     C           INENTT    IFEQ 'UP'                       B2
     C           INENTT    OREQ 'PT'
     C                     MOVE '2'       IDRCR
     C                     MOVE 'UP'      INENTT
     C                     MOVE 'A'       ITYLC
     C                     ENDIF                           E2
      *
     C           INENTT    IFEQ 'DL'                       B2
     C                     MOVE G6DRCR    IDRCR
     C                     MOVE 'UP'      INENTT
     C                     MOVE 'A'       ITYLC
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
     C                     MOVE 'E'       WRTCD1
      *
      **  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'ED0180'  DBPGM
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     DUMP
     C                     ENDIF                           E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Finastra International Limited 2001
