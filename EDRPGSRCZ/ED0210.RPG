     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED Security trades reconciliation extract')      *
      *****************************************************************
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0210 -  ED Securities Trades Reconciliation Extract        *
      *                                                               *
      *  Function: This program is one of the Reconciliation Report   *
      *            Extract programs.  This program processes the      *
      *            Securities Trading module and Dealing Module       *
      *            Negotiable Asset Deals.  These are combined as     *
      *            Global Manager treats them identically.            *
      *            NOTE: For efficiency, as this program processes a  *
      *            number of potentially very large files, the Logic  *
      *            Cycle is exploited.                                *
      *                                                               *
      *  Called By: EDC0210                                           *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL096             Date 22Sep14               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 06Jul06               *
      *                 CDL038             Date 10May05               *
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 101973 *CREATE     Date 09FEB96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  101973 - Phase V Modifications                               *
      *                                                               *
      *****************************************************************
      *
     FEDTRADL1IP  E           K        DISK         KINFSR *PSSR
      * SE Trades by Branch and Currency
      *
     FEDDEALL3IS  E           K        DISK         KINFSR *PSSR
      * MM NA Deals by Branch and Currency
      *
     FEDREC1PDO   E                    DISK         KINFSR *PSSR
      * ED SE Reconciliation Data
      *
     FED0210AUO   E                    PRINTER                        UC
      * Program Audit report
      *
     F/COPY WNCPYSRC,ED0210F001
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    11  -  TRADSDF                                             *
      *    12  -  DEALSDC                                             *
      *    13  -  DEALSDD                                             *
      *    14  -  DEALSDE                                             *
      *    31  -  Extract to write                                    *
      *    40  -  First Cycle                                         *
      *    41  -  SDBANKR0 error                                      *
      *    L2  -  Level break for Branch                              *
      *    L1  -  Level break for Currency                            *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     I/TITLE I-SPECIFICATIONS
      *-----------------------------------------------------------
     ITRADSDF     11
     I              TDBA                            BRCA  L2M2
     I              TNMC                            CCY   L1M1
     IDEALSDCF    12
     I                                              BRCA  L2M2
     I                                              CCY   L1M1
     IDEALSDDF    13
     I                                              BRCA  L2M2
     I                                              CCY   L1M1
     IDEALSDEF    14
     I                                              BRCA  L2M2
     I                                              CCY   L1M1
     IPSDS       SDS
      ** Program Status
     I                                      244 253 WSID
     I                                      254 263 USRID
     I                                      276 281 WDATE
     I                                      282 287 WTIME
      *
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     IDSFDY     E DSDSFDY
      *
      ** Midas Access Object Short Data Structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Bank Details
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  First cycle processing
      *
     C           *IN40     IFEQ *OFF
      *
      ***  Copyright Statement.
      *
     C                     MOVEACPY@      MKI@   80
     C                     Z-ADD1         EJTRTY
     C/COPY WNCPYSRC,ED0210C001
      *
      **  Get run date for comparison against value date and
      **  report title (used on error report)
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     41
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     SETON                     40
      *
     C                     ENDIF
      *
     C   L2                MOVELBRCA      EJBRCA
     C   L1                MOVELCCY       EJCCYC
     C   L1                SETOF                     31
      *
      **  SECURITY TRADES
      *
     C           *IN11     IFEQ *ON
      *
      **  Accumulate if trade is not in the past; (it is not safe to rely
      **  on RECI because a trade could be fully settled on value date and
      **  would have a RECI of 'S' but should be included in the total).
      *
     C           BJRDNB    IFLE TDVD
     C                     ADD  1         EJTCOT
     C                     ADD  TCSR      EJTVAL
     C                     ADD  ITRA      EJTVAL
     C                     SETON                     31
     C                     ENDIF
      *
      **  NEGOTIABLE ASSET DEALS
      *
     C                     ELSE
      *
      **  Accumulate if deal is not VALUED in the past (maturity date of
      **  the underlying security is irrelevant)
      *
     C           BJRDNB    IFLE VDAT
     C                     ADD  1         EJTCOT
     C                     SETON                     31
      *
     C           *IN12     IFEQ *ON
     C                     ADD  PCPL      EJTVAL
     C                     ENDIF
      *
     C           *IN13     IFEQ *ON
     C                     ADD  PUPR      EJTVAL
     C                     ENDIF
      *
     C           *IN14     IFEQ *ON
     C                     ADD  SAPR      EJTVAL
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
     C/COPY WNCPYSRC,ED0210C002
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: MAIN                                              *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
      *
      ***  Set up LDA data
      *
     C                     MOVEL'ED0210'  DBPGM
      *
      ***  Set up trade/deal key, unless error occurred in 1st cycle process
      *
     C           *IN41     IFEQ *OFF
      *
     C                     MOVEL'001'     DBASE
     C                     MOVELBRCA      KEY1    7 P
     C                     MOVE CCY       KEY1
     C                     MOVELKEY1      DBKEY     P
      *
     C           *IN11     IFEQ *ON
     C                     MOVEL'EDTRADL1'DBFILE
     C                     MOVE TDRF      DBKEY
     C                     ENDIF
      *
     C           *IN12     IFEQ *ON
     C           *IN13     OREQ *ON
     C           *IN14     OREQ *ON
     C                     MOVEL'EDDEALL3'DBFILE
     C                     MOVE DLNO      DBKEY
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  Output LDA
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
      *
      **  Print audit report ED0210AU
      *
     C                     OPEN ED0210AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSEED0210AU
      *
     C                     DUMP
      *
     C                     END                             E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      **********************************************************************
     OEDREC1D0T        L1 31
     O                         EJBRCA
     O                         EJCCYC
     O                         EJTRTY
     O                         EJTCOT B
     O                         EJTVAL B
      **********************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
