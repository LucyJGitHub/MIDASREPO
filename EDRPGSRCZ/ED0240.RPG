     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED FF transactions reconciliation extract')
      *****************************************************************
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0240 -  ED Futures & Options Module Reconciliation Extract *
      *                                                               *
      *  Function: This program is one of the Reconciliation Report   *
      *            Extract programs.  This program processes the      *
      *            Futures and Options module.                        *
      *                                                               *
      *  Called By: EDC0240                                           *
      *                                                               *
      *  Calls:     None                                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CFF004             Date 02OCT96               *
      *                 101973 *CREATE     Date 09FEB96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CFF004 - Increase in size of Price Fields,(Recompiled Only)  *
      *  101973 - Phase V Modifications                               *
      *                                                               *
      *****************************************************************
      *
     FEDTRANL1IP  E           K        DISK         KINFSR *PSSR
      * FF Transactions by Branch and Currency
      *
     FEDREC4PDO   E                    DISK         KINFSR *PSSR
      * ED FF Reconciliation Data
      *
     FED0240AUO   E                    PRINTER                        UC
      * Program Audit report
     F/COPY WNCPYSRC,ED0240F001
      *
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    31  -  Extract to write                                    *
      *    40  -  First Cycle                                         *
      *    41  -  SDBANKR0 error                                      *
      *    L2  -  Level break for Branch                              *
      *    L1  -  Level break for Currency                            *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     I/TITLE I-SPECIFICATIONS
      *-----------------------------------------------------------
     ITRANSDF
     I                                              BRCA  L2
     I                                              ISCY  L1
     IPSDS       SDS
      ** Program Status
     I                                      244 253 WSID
     I                                      254 263 USRID
     I                                      276 281 WDATE
     I                                      282 287 WTIME
      *
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     IDSFDY     E DSDSFDY
      *
      ** Midas Access Object Short Data Structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Bank Details
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Copyright Statement.
      *
     C           *IN40     IFEQ *OFF
     C                     MOVEACPY@      MKI@   80
     C                     Z-ADD4         EJTRTY
     C/COPY WNCPYSRC,ED0240C001
      *
      **  Get run date for comparison against transaction date and
      **  report title (used on error report)
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     41
     C                     EXSR *PSSR
     C                     ENDIF
     C                     SETON                     40
     C                     ENDIF
      *
     C   L2                MOVELBRCA      EJBRCA
     C   L1                MOVELISCY      EJCCYC
     C   L1                SETOF                     31
      *
     C           BJRDNB    IFLE TRSD
     C                     SETON                     31
     C                     ADD  1         EJTCOT
      *
      ***  Calculate number of contracts x contract price
      *
     C           COPR      MULT NUCO      WVALUE 156H
     C                     ADD  WVALUE    EJTVAL
     C/COPY WNCPYSRC,ED0240C002
      *
     C                     ENDIF
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: MAIN                                              *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
      *
      ***  Set up LDA data
      *
     C                     MOVEL'ED0240'  DBPGM
      *
      ***  Set up deal key if the error did not occur in 1st cycle process
      *
     C           *IN41     IFEQ *OFF
     C                     MOVEL'EDTRANL1'DBFILE
     C                     MOVEL'001'     DBASE
     C                     MOVELBRCA      KEY1    7 P
     C                     MOVE ISCY      KEY1
     C                     MOVELKEY1      DBKEY     P
     C                     MOVE TNBR      DBKEY
     C                     ENDIF
      *
      ***  Output LDA
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
      *
      **  Print audit report ED0240AU
      *
     C                     OPEN ED0240AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSEED0240AU
      *
     C                     DUMP
      *
     C                     END                             E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      **********************************************************************
     OEDREC4D0T        L1 31
     O                         EJBRCA
     O                         EJCCYC
     O                         EJTRTY
     O                         EJTCOT B
     O                         EJTVAL B
      **********************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
