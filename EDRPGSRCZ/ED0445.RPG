     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  GENOPT(*OPTIMIZE)                                            *
/*EXI *  TEXT('Midas ED Bulk transfer - cash flow schedule proc.')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Export Data Interface Module                   *
      *                                                               *
      *  ED0445 - Bulk Transfer (Cash Flow Schedule Processing).      *
      *                                                               *
      *  Function: Program creates bulk transfer batches in COB for   *
      *            file DLCFSCPD (Cash Flow Schedule Special Pro.)    *
      *                                                               *
      *  Phase(s): COB                                                *
      *                                                               *
      *  Called By: EDC0020 - Bulk Transfer Component                 *
      *  CallS    : EDC0024 - Create New Batch                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 122942             Date 12Sep97               *
      *                 CED003  *CREATE    Date 10Jun97               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  122942 - Batch header should be Drip feed type.              *
      *  CED003 - Extract IRC Details for IRSs in Midas ED Module.    *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     FDEALSDG IF  E                    DISK         KINFSR *PSSR
      ** FRA/IRS Deals
     FDLCFSCPDIF  E           K        DISK         KINFSR *PSSR
      ** Cash Flow Schedule
     FEDFBUTPDUF  E           K        DISK         KINFSR *PSSR
      *   Failed Bulk Transfers
     FEDBULKPDO   F    4096            DISK         KINFSR *PSSR      UC
      *   Bulk Transfer data
     FEDSAVXPDO   E                    DISK         KINFSR *PSSR A
     F                                              KCOMIT
      *   Batches to send to DTA
     FEDBUAUPDO   E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
      *   Bulk Transfer Audit File
     FED0445AUO   E                    PRINTER                        UC
      *   Audit Report
      *
      *****************************************************************
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    81  - Do 'Close the Batch' processing                      *
      *    82  - READ indicator for DEALSDG                           *
      *    83  - General CHAIN, READE and DELET indicator             *
      *    98  - Date Format: DDMMYY (off); MMDDYY (on)               *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #DETL1 - Determine if relevent record on DEALSDG             *
      *  #DETL2 - Determine if relevent record on DLCFSCPD            *
      *  #DETL3 - Write preamble and DLCFSCPD detail                  *
      *  #BATCH - Create batch                                        *
      *  #INIT  - Initial Processing                                  *
      *  #LAST  - Termination processing                              *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      ** Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WJOB
     I                                      254 263 WUSER
     I                                      264 2690WJOBNO
      *
     IDSFDY     E DSDSFDY
      ** Data Structure for Access Objects
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISDMEXP    E DSSDMEXPPD
      ** ED ICD Details
      *
     IBLKOUT      DS
      ** Data Structure for EDBULKPD output
     I                                        1   50JOENTL
     I                                        6  150JOSEQN
     I                                       16  16 JOCODE
     I                                       17  18 JOENTT
     I                                       19  24 JODATE
     I                                       25  300JOTIME
     I                                       31  40 JOJOB
     I                                       41  50 JOUSER
     I                                       51  560JONBR
     I                                       57  66 JOPGM
     I                                       67  76 JOOBJ
     I                                       77  86 JOLIB
     I                                       87  96 JOMBR
     I                                       97 1060JOCTRR
     I                                      107 107 JOFLAG
     I                                      108 1170JOCCID
     I                                      118 125 JORES
      *
     IDATAST      DS                           3971
      ** Data Structure for EDBULKPD output - DLCFSCPD detail.
      *
     IEDBULK      DS
      ** ED ICD Details
     I                                        1   60UBTCH0
     I                                        7  120UBTCH1
     I                                       13  180UBTCH2
     I                                       19  240UBTCH3
     I                                       25  300UBTCH4
     I                                       31  360UBTCH5
     I                                       37  420UBTCH6
     I                                       43  480UBTCH7
     I                                       49  540UBTCH8
     I                                       55  600UBTCH9
      *
     IDLCFSD    E DSDLCFSCPD
      ** PF/DLCFSCPD details
     I              DLNO                            MDLNO
     I              OTIN                            MOTIN
     I              PRDT                            MPRDT
     I              CFLA                            MCFLA
     I              COHO                            MCOHO
     I              DTPR                            MDTPR
     I                                       20  22 OTCCY
      *
     IWBATCH      DS                             10
      ** Batch Name field
     I                                        1   3 WEDDTX
     I                                        4   40WSTREM
     I                                        5  100WBATNN
      *
     IWOVR        DS                             54
      *  DS for QCMDEXC
     I                                       44  53 WDSBAT
      *
     ITFROK       DS
      * Setup Data structure for Deletion from Failed Bulk Transfers
     I                                        1  10 XFILEN
     I                                       11  20 XMEMBN
      *
      *  Constant for QCMDEXC
     I              'OVRDBF FILE(EDBULKPD-C         QOVR
     I              ') TOFILE(EDBULKPD) M-
     I              'BR(XXXXXXXXXX)'
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      **  Perform Initialisation.
      *
     C                     EXSR #INIT
      *
      **  Perform Initial Detail Processing.
      *
     C                     EXSR #DETL1
      *
      **  Perform Final Processing.
      *
     C                     EXSR #LAST
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *================================================================
      *
     C                     SETON                     LR
     C                     RETRN
      *
      *================================================================
      /EJECT
      *****************************************************************
      /TITLE SR/#DETL1
      *****************************************************************
      *                                                               *
      *  #DETL1   - Determine if relevent record on DEALSDG           *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : #BATCH                                            *
      *                                                               *
      *****************************************************************
      *
     C           #DETL1    BEGSR
      *
     C                     READ DEALSDG                  82
      *
     C           *IN82     DOWEQ*OFF
      *
      ** If record live and deal type is an IRS trade:-
     C           RECI      IFEQ 'D'
     C           DTYP      ANDEQ'RS'
     C           RECI      OREQ 'D'
     C           DTYP      ANDEQ'RX'
      *
      ** If 'Our Payment Frequency' is 'C'.
     C           UIPFR     IFEQ 'C'
     C                     MOVE 'U'       KOTIN
     C                     MOVE UCUCY     OTCCY
     C                     EXSR #DETL2
     C                     ENDIF
      *
      ** If 'Their Payment Frequency' is 'C'.
     C           TIPFR     IFEQ 'C'
     C                     MOVE 'T'       KOTIN
     C                     MOVE TCUCY     OTCCY
     C                     EXSR #DETL2
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ DEALSDG                  82
      *
     C                     ENDDO
      *
      *** Check for end of batch, else batch not created.
      *
     C           WCOUNT    IFGT 0
     C                     EXSR ENDBAT
     C                     ELSE
     C                     MOVEL'No Batch'EABTCH
     C                     Z-ADD*ZERO     EARECT
     C                     WRITEEDBUAUD0
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      /TITLE SR/#DETL2
      *****************************************************************
      *                                                               *
      *  #DETL2   - Determine if relevent record on DLCFSCPD          *
      *                                                               *
      *  Called By: #DETL1                                            *
      *  Calls    : #DETL3                                            *
      *                                                               *
      *****************************************************************
      *
     C           #DETL2    BEGSR
      *
     C           DLCFKY    CHAINDLCFSCPD             83
      *
     C           *IN83     DOWEQ*OFF
     C                     EXSR #DETL3
     C           DLCFKY    READEDLCFSCPD                 83
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#DETL3
      *****************************************************************
      *                                                               *
      *  #DETL3   - Write preamble and detail                         *
      *                                                               *
      *  Called By: #DETL2                                            *
      *  Calls    : ENDBAT                                            *
      *                                                               *
      *****************************************************************
      *
     C           #DETL3    BEGSR
      *
      ** New Batch?
      *
     C           WCOUNT    IFEQ 0
     C                     EXSR #BATCH
     C                     ENDIF
      *
      ** Set up record preamble data.
      *
     C                     TIME           WTMDT  120
     C                     MOVELWTMDT     JOTIME
     C                     MOVE WTMDT     JODATE
     C                     MOVE *BLANKS   JOOBJ
     C                     MOVEL'DLCFSCPD'JOOBJ
     C                     MOVE *BLANKS   JOMBR
     C                     MOVEL'DLCFSCPD'JOMBR
     C                     Z-ADDWCOMIT    JOCCID
      *
      ** Move data into array DATAST.
      *
     C                     MOVE DLNO      MDLNO
     C                     MOVE OTIN      MOTIN
     C                     MOVE PRDT      MPRDT
     C                     MOVE CFLA      MCFLA
     C                     MOVE COHO      MCOHO
     C                     MOVE DTPR      MDTPR
      *
     C                     MOVE *BLANKS   DATAST
     C                     MOVELDLCFSD    DATAST
      *
      ** Increment commitment cycle number and record count.
     C                     ADD  1         WCOMIT
     C                     ADD  1         WCOUNT
      *
      ** Write record to PF/EDBULKPD
     C                     EXCPTDTL
      *
      ** Check for end of batch?
     C                     EXSR ENDBAT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ENDBAT
      *****************************************************************
      *                                                               *
      *  ENDBAT - Subroutine to check for end of batch                *
      *                                                               *
      *  Called By: #DETL1 and #DETL3                                 *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           ENDBAT    BEGSR
      *
      ** If record found and record count => max. no. recs in batch,
      ** OR no record found, set on indicator 81 to condition
      ** further processing.
      *
     C           *IN82     IFEQ *OFF
     C           WCOUNT    IFGE E7BUMR
     C                     MOVE *ON       *IN81
     C                     ELSE
     C                     MOVE *OFF      *IN81
     C                     ENDIF
     C                     ELSE
     C                     MOVE *ON       *IN81
     C                     ENDIF
      *
      *** If indicator 81 is on, do the 'close the batch' processing.
      *
     C           *IN81     IFEQ *ON
     C                     CLOSEEDBULKPD
      *
      *** Set up data for BATCH LOCATION DETAILS
      *
     C                     MOVELUSVLIB    EFLIBR
     C                     MOVEL'/'       EFSEP1
     C                     MOVEL'EDBULKPD'EFFILE
     C                     MOVEL'.'       EFSEP2
     C                     MOVELWBATCH    EFMEMB
      *
     C                     WRITEEDSAVXD0
      *
      *** Set up data for Audit file and write to it
      *
     C                     MOVELWBATCH    EABTCH
     C                     Z-ADDWCOUNT    EARECT
     C                     WRITEEDBUAUD0
      *
      *** Set record count to zero.
     C                     Z-ADD0         WCOUNT
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#BATCH
      *****************************************************************
      *                                                               *
      *  #BATCH - Subroutine to create a batch                        *
      *                                                               *
      *  Called By: #DETL1                                            *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           #BATCH    BEGSR
      *
      *** Increment batch name
      *
     C                     ADD  1         WBATNN
      *
      *** Call EDC0024 to create new member of PF/EDBULKPD
      *
     C                     CALL 'EDC0024'
     C                     PARM           WBATCH
     C                     PARM           WRTCD1  1
      *
     C           WRTCD1    IFNE *BLANKS
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'EDC0024 'DBFILE           * DB ERROR 01 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      *** Override to new member of PF/EDBULKPD - Do this in RPG
      *** rather than in EDC0024 as the override is needed at
      *** this level.
      *
     C                     MOVELQOVR      WOVR
     C                     MOVELWBATCH    WDSBAT
     C                     CALL 'QCMDEXC'
     C                     PARM           WOVR
     C                     PARM 54        QLEN   155
      *
      *** Open PF/EDBULKPD now the correct member exists
      *** Write header record of PF/EDBULKPD
      *
     C                     OPEN EDBULKPD
     C                     EXCPTHDR
      *
      *** Add one to record count.
      *
     C                     ADD  1         WCOUNT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#INIT
      *****************************************************************
      *                                                               *
      *  #INIT  - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           #INIT     BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           USVLIB  7
     C           XFILEN    PARM           UFILEN 10
     C           XMEMBN    PARM           UMEMBN 10
     C                     PARM           USQCD   3
     C                     PARM           USTREM  1
      *
      ** Partial Key list for chain to DLCFSCPD - Deal No and Our/Their
      ** Indicator.
     C           DLCFKY    KLIST
     C                     KFLD           DLNO
     C                     KFLD           KOTIN   1
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      **  Name of program
     C                     MOVEL'ED0445'  DBPGM
      *
      ** Read in Installation Data
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     END
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD7  7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD7    IFNE *BLANKS
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 02 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END
      *
      *** Access ED ICD Details
      *
     C                     CALL 'AOMEXPR0'
     C                     PARM '*MSG   ' WRTCD7
     C                     PARM '*FIRST ' WOPTN
     C           SDMEXP    PARM SDMEXP    DSFDY
      *
     C           WRTCD7    IFNE *BLANKS
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'SDMEXPPD'DBFILE           * DB ERROR 03 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Definitions
     C           *LIKE     DEFN EABTCH    WBCNNM
      *
      ** File and member names for PF/EDBUAUPD Audit file
      *
     C                     MOVEL'DLCFSCPD'EAFNME
     C                     MOVEL'DLCFSCPD'EAMNME
      *
      ** Components of batch name and batch location fields
      *
     C                     MOVE *BLANKS   WBCNNM
     C                     MOVEL'EDB'     WEDDTX
     C                     MOVELUSTREM    WSTREM
     C                     MOVELUSQCD     EFSQCD
      *
      ** Get the next number for a batch for this stream from
      ** DTAARA/EDBULK
      *
     C           *NAMVAR   DEFN           EDBULK
     C           *LOCK     IN   EDBULK
      *
     C                     SELEC
     C           USTREM    WHEQ '0'
     C                     Z-ADDUBTCH0    WBATNN
     C           USTREM    WHEQ '1'
     C                     Z-ADDUBTCH1    WBATNN
     C           USTREM    WHEQ '2'
     C                     Z-ADDUBTCH2    WBATNN
     C           USTREM    WHEQ '3'
     C                     Z-ADDUBTCH3    WBATNN
     C           USTREM    WHEQ '4'
     C                     Z-ADDUBTCH4    WBATNN
     C           USTREM    WHEQ '5'
     C                     Z-ADDUBTCH5    WBATNN
     C           USTREM    WHEQ '6'
     C                     Z-ADDUBTCH6    WBATNN
     C           USTREM    WHEQ '7'
     C                     Z-ADDUBTCH7    WBATNN
     C           USTREM    WHEQ '8'
     C                     Z-ADDUBTCH8    WBATNN
     C           USTREM    WHEQ '9'
     C                     Z-ADDUBTCH9    WBATNN
     C                     ENDSL
      *
     C                     OUT  EDBULK
      *
      ** More initialisation.
      *
     C                     Z-ADD1         WCOMIT  90
     C                     Z-ADD0         WCOUNT  50
      *
     C                     Z-ADD147       JOENTL
     C                     Z-ADD1         JOSEQN
     C                     MOVE 'R'       JOCODE
     C                     MOVE 'UP'      JOENTT
     C                     MOVE WJOB      JOJOB
     C                     MOVE WUSER     JOUSER
     C                     Z-ADDWJOBNO    JONBR
     C                     MOVEL'ED0445'  JOPGM
     C                     MOVEL'Bulk '   JOLIB
     C                     MOVE 'Trans'   JOLIB
     C                     Z-ADD0         JOCTRR
     C                     MOVE '1'       JOFLAG
     C                     Z-ADDWCOMIT    JOCCID
     C                     MOVE *BLANKS   JORES
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/#LAST
      *****************************************************************
      *                                                               *
      *  #LAST  - Subroutine to do Program Termination                *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : None.                                             *
      *                                                               *
      *****************************************************************
      *
     C           #LAST     BEGSR
      *
      ** Update DTAARA/EDBULK
      *
     C           *LOCK     IN   EDBULK
      *
     C                     SELEC
     C           USTREM    WHEQ '0'
     C                     Z-ADDWBATNN    UBTCH0
     C           USTREM    WHEQ '1'
     C                     Z-ADDWBATNN    UBTCH1
     C           USTREM    WHEQ '2'
     C                     Z-ADDWBATNN    UBTCH2
     C           USTREM    WHEQ '3'
     C                     Z-ADDWBATNN    UBTCH3
     C           USTREM    WHEQ '4'
     C                     Z-ADDWBATNN    UBTCH4
     C           USTREM    WHEQ '5'
     C                     Z-ADDWBATNN    UBTCH5
     C           USTREM    WHEQ '6'
     C                     Z-ADDWBATNN    UBTCH6
     C           USTREM    WHEQ '7'
     C                     Z-ADDWBATNN    UBTCH7
     C           USTREM    WHEQ '8'
     C                     Z-ADDWBATNN    UBTCH8
     C           USTREM    WHEQ '9'
     C                     Z-ADDWBATNN    UBTCH9
     C                     ENDSL
      *
     C                     OUT  EDBULK
      *
      ***  Delete record from Failed Bulk Transfers PF/EDFBUTPD
     C           TFROK     DELETEDFBUTPD             83
     C                     COMIT
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
      *
      ***  ERROR REPORT  ED0445AU
      *
     C                     OPEN ED0445AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSEED0445AU
      *
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
      *
     OEDBULKPDE                HDR
     O***********************************20 'MIDAS_BULK_TRANSFER '        122942
     O                                   20 'MIDAS_DRIP_FEED     '        122942
     O                                   24 '4096'
     O***********************************42 'DLCFSCPD  UNIQUEPT'          122942
     OEDBULKPDE                DTL
     O                         BLKOUT   125
     O                         DATAST  4096
      *
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
