     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED Market control special processing (i/c)')
      *****************************************************************
      *                                                               *
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0490 - FF Market Control special processing program (i/c)  *
      *                                                               *
      *  Function: This program is part of the special processing     *
      *            for PF/MKCTLD and PF/PRICSD. When the drip feed    *
      *            process encounters a journal entry from PF/MKCTLD  *
      *            then this program is called to initiate the        *
      *            processing for these files.                        *
      *                                                               *
      *  Called By: ED0060 - Extract Journal entries within comit     *
      *                                                               *
      *  Calls: EDC0491 - ED Special processing for MKCTLD/PRICSD.    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CED005 *C *CREATE  Date 23Jul97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CED005 - Midas Ed Enhancements                               *
      *                                                               *
      *****************************************************************
      *
     FEDNEWJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDNEWJD4
      * New journal entries by journal sequence
      *
     FEDOLDJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDOLDJD4
      * Old journal entries by journal sequence
      *
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    01  -  EoF EDNEWJL4 or EDOLDJL4                            *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  PROCES - Perform Detail Processing                           *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      **  Array for Object Copyright Statement.
      *
     E                    DATA1     125  1
     E                    DATA2    3971  1
      ** Arrays for input parameters
      *
      *-----------------------------------------------------------
     IMKCTL     E DSMKCTLD
      *
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for EDJRNEPD output
     I                                        1 125 OUT1
     I                                        1   50JOENTL
     I                                        6  150JOSEQN
     I                                       16  16 JOCODE
     I                                       17  18 JOENTT
     I                                       19  24 JODATE
     I                                       25  300JOTIME
     I                                       31  40 JOJOB
     I                                       41  50 JOUSER
     I                                       51  560JONBR
     I                                       57  66 JOPGM
     I                                       67  76 JOOBJ
     I                                       77  86 JOLIB
     I                                       87  96 JOMBR
     I                                       97 1060JOCTRR
     I                                      107 107 JOFLAG
     I                                      108 1170JOCCID
     I                                      118 125 JORES
      *-----------------------------------------------------------
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *-----------------------------------------------------------
      **
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Parameter entry list
     C           *ENTRY    PLIST
      *IN PARAMETERS
     C                     PARM           DATA1
     C                     PARM           DATA2
     C                     PARM           SRCF    1
      *OUT PARAMETERS
     C                     PARM           CDFILE  8
     C                     PARM           CDBKEY 10
     C                     PARM           WRTCD1  1
     C                     PARM           RECCNT  50
      *
      ***  Copyright Statement and initialisation
      *
     C                     MOVEACPY@      MKI@   80
     C                     Z-ADD*ZERO     RECCNT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                     LR               *
     C                     RETRN                                      *
      *                                                               *
      *================================================================
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
      ** Set-up the data from the input parameters so that it can be used.
      *
     C                     MOVEADATA1     OUT1
     C                     MOVEADATA2     MKCTL     P
      *
      ** If the end of center status indicator = M then call EDC0491
      ** to generate drip feed records for PRICSD.
      *
     C           ECAI      IFEQ 'M'
      *
     C                     MOVELJOSEQN    CDBKEY
      *
     C                     CALL 'EDC0491'
     C                     PARM           DATA1
     C                     PARM           DATA2
     C                     PARM           SRCF
     C                     PARM JOOBJ     CDFILE
     C                     PARM           CDBKEY
     C                     PARM           WRTCD1
     C                     PARM           RECCNT
     C                     PARM MRKT      MKTC    2
     C                     ENDIF
      *
      ** Continue by deleting the record read in by ED0060.
      ** Use the journal sequence number from the incoming data parameter
      ** to delete the record from 'Old' or 'New' as appropriate.
      *
     C           SRCF      IFEQ 'N'                        B001
      *
     C           JOSEQN    DELETEDNEWJD4             01
      *
     C           *IN01     IFEQ '1'                        B002
     C                     Z-ADD01        DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 01 *
     C                     MOVE JOSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E002
      *
     C                     ELSE                            X001
      *
     C           JOSEQN    DELETEDOLDJD4             01
      *
     C           *IN01     IFEQ '1'                        B002
     C                     Z-ADD02        DBASE            ***************
     C                     MOVEL'EDOLDJL4'DBFILE           * DB ERROR 02 *
     C                     MOVE JOSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E002
      *
     C                     ENDIF                           E001
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
     C                     MOVE 'E'       WRTCD1
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'ED0490'  DBPGM
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     DUMP
     C                     ENDIF                           E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Finastra International Limited 2001
