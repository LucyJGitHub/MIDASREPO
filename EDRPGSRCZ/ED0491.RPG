     H        1
      *****************************************************************
/*OVRF*  OVRDBF FILE(EDPCSJPD) TOFILE(EDNEWJPD)                       *
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED Market prices special processing (i/c)')
      *****************************************************************
      *                                                               *
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0491 - Market Prices file special processing program (i/c) *
      *                                                               *
      *  Function: This program is part of the special processing     *
      *            for file PF/MKCTLD and PF/PRICSD. This program     *
      *            extracts all the journal entries for PF/PRICSD     *
      *            for the End of Centre processing amd then writes   *
      *            these to PF/EDJRNEPD.                              *
      *                                                               *
      *  Called By: EDC0491 - MKCTLD/PRISCD special processing.       *
      *                                                               *
      *  Notes:  i) Created with override for EDPCSJPD to EDNEWJPD    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CED005 *CREATE     Date 23JUL97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CED005 - Midas ED Enhancements                               *
      *                                                               *
      *****************************************************************
      *
     FEDJRNEPDO   E                    DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDJRNED1
      *
     FEDPCSJPDIF  E           K        DISK         KINFSR *PSSR      UC
     F            QJORDJE                           KRENAMEEDPCSJD1
      * Journal entries for DLPCSCPD
      *
     FEDBFRJPDIF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDBFRJD1
      *
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    40  -  Read of EDPCSJPD                                    *
      *    41  -  Read of EDBFRJPD                                    *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Initial Processing                                  *
      *  PROCES - Perform Detail Processing                           *
      *  WRITE  - Write a EDJRNEPD record                             *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
     E                    CPY@    1   1 80
      **  Array for Object Copyright Statement.
      *
     E                    DATA1     125  1
     E                    DATA2    3971  1
      ** Arrays for input parameters
      *
     I/TITLE I-SPECIFICATIONS
      *-----------------------------------------------------------
     IWOVR        DS                            152
      **  DS for QCMDEXC
     I                                       32  33 WMKT
     I                                       63  720WDSEQN
     I                                       81  90 TOSEQN
      *
      *-----------------------------------------------------------
      **  Constant for QCMDEXC
      *
     I              'DSPJRN JRN(ICJRN) FI-C         QOVR
     I              'LE((PRICSD XX)) RCVR-
     I              'NG(*CURCHAIN) FROMEN-
     I              'T(XXXXXXXXXX) TOENT(-
     I              'XXXXXXXXXX) ENTTYP(U-
     I              'P PT) PGM(FF0310) OU-
     I              'TPUT(*OUTFILE) OUTFI-
     I              'LE(EDPCSJPD)'
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for EDJRNEPD output
     I                                        1 125 OUT1
     I                                        1   50JOENTL
     I                                        6  150JOSEQN
     I                                       16  16 JOCODE
     I                                       17  18 JOENTT
     I                                       19  24 JODATE
     I                                       25  300JOTIME
     I                                       31  40 JOJOB
     I                                       41  50 JOUSER
     I                                       51  560JONBR
     I                                       57  66 JOPGM
     I                                       67  76 JOOBJ
     I                                       77  86 JOLIB
     I                                       87  96 JOMBR
     I                                       97 1060JOCTRR
     I                                      107 107 JOFLAG
     I                                      108 1170JOCCID
     I                                      118 125 JORES
      *-----------------------------------------------------------
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *-----------------------------------------------------------
     IMKTFC       DS                             12
     I                                       11  12 MCTR
      *-----------------------------------------------------------
      **
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Parameter entry list
     C           *ENTRY    PLIST
      *IN PARAMETERS
     C                     PARM           DATA1
     C                     PARM           DATA2
     C                     PARM           SRCF    1
      *OUT PARAMETERS
     C                     PARM           CDFILE  8
     C                     PARM           CDBKEY 10
     C                     PARM           WRTCD1  1
     C                     PARM           RECCNT  50
     C                     PARM           MKTC    2
      *
      ** Perform Initial Processing
      *
     C                     EXSR INIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                     LR               *
     C                     RETRN                                      *
      *                                                               *
      *================================================================
      *
      *****************************************************************
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  INIT - Subroutine to do the Initial Processing.              *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ***  Copyright Statement and initialisation
      *
     C                     MOVEACPY@      MKI@   80
     C                     Z-ADD*ZERO     RECCNT
      *
      ** Set-up the data from the input parameters so that it can be used.
      *
     C                     MOVEADATA1     OUT1      P
      *
      ** Obtain last FC entry for Market centre. (From data in EDBFRJPD
      ** created in calling program.)
      *
     C           JOSEQN    SETGTEDBFRJPD
     C           MCTR      DOUEQMKTC
     C           *IN41     OREQ '1'
     C                     READPEDBFRJPD                 41
     C                     MOVELJOESD     MKTFC
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
      ** Obtain all details of changes to PRICSD made by FF0310 for
      ** this End of centre processing.
      *
     C           *IN41     IFEQ '0'
     C                     MOVELQOVR      WOVR
     C                     MOVELMKTC      WMKT
     C                     Z-ADDJOSEQN    WDSEQN
     C                     MOVELCDBKEY    TOSEQN
     C                     CALL 'QCMDEXC'              01
     C                     PARM           WOVR
     C                     PARM 152       QLEN   155
      *
      ** Read first record of PRICSD changes
      *
     C           *IN01     IFEQ '0'
     C                     OPEN EDPCSJPD
     C                     READ EDPCSJPD                 40
      *
      ** Do until end of file
      *
     C           *IN40     DOWEQ'0'
      *
      ** If market centre of journal entry is same as journal entry
      ** from input parameter, write details to EDJRNEPD.
      *
     C           JOMBR     IFEQ MKTC
      *
     C                     MOVELJOSEQN    JOCCID
      *
      ** Write to the batch member
      *
     C                     EXSR WRITE
      *
     C                     ENDIF
      *
      ** Read next record
      *
     C                     READ EDPCSJPD                 40
     C                     ENDDO
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WRITE
      *****************************************************************
      *                                                               *
      *  WRITE - Write a detail record to EDJRNEPD                    *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           WRITE     BEGSR
      *
     C                     WRITEEDJRNED1
     C                     ADD  1         RECCNT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
     C                     MOVE 'E'       WRTCD1
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'ED0491'  DBPGM
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     DUMP
     C                     ENDIF                           E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
