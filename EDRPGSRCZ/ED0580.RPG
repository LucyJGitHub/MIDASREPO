     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ED MM deals amends special processing (i/c)')    *
      *****************************************************************
      *                                                               *
      *  Midas  -  Midas Export Data Module                           *
      *                                                               *
      *  ED0580 -  MM Deals AMENDS Special Processing - Input Cycle   *
      *                                                               *
      *  Function: This program is one of the 'Special Processing     *
      *            programs' that are called when the Drip Feed       *
      *            process encounters a journal entry from a certain  *
      *            file.                                              *
      *                                                               *
      *  Called By: ED0060 - Extract Journal entries within comit     *
      *                                                               *
      *  Calls:     *None                                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 06Jul06               *
      *                 CDL038             Date 10May05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CED007   *CREATE   DATE 20AUG97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CED007 - Midas ED Enhancements for GIB Repos                 *
      *                                                               *
      *****************************************************************
      *
     FEDNEWJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDNEWJD4
      * New journal entries by journal sequence
      *
     FEDOLDJL4UF  E           K        DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDOLDJD4
      * Old journal entries by journal sequence
      *
     FDEALSC  IF  E           K        DISK         KINFSR *PSSR
     F            DEALSDCF                          KRENAMEDEALFILE
      * All Live Deals by Deal No.
     FEDJRNEPDO   E                    DISK         KINFSR *PSSR
     F            QJORDJE                           KRENAMEEDJRNED1
      *
      *****************************************************************
      /TITLE FUNCTION OF INCICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *    01  -  EoF EDNEWJL4 or EDOLDJL4                            *
      *    40  -  EoF on LF/DEALSC                                    *
      *                                                               *
      *  U7+U8 - Database Error                                       *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  PROCES - Perform Detail Processing                           *
      *  WRITE  - Write a EDJRNEPD record                             *
      *  *PSSR  - Program error handling routine                      *
      *                                                               *
      *****************************************************************
     E/TITLE E-SPECIFICATIONS
      *****************************************************************
     E                    CPY@    1   1 80
      ***  Array for Object Copyright Statement.
      *
     E                    DATA1     125  1
     E                    D2       3971  1
     I/TITLE I-SPECIFICATIONS
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for Journal record data parameter
     I                                        1 125 IN1
     I                                        1   50INENTL
     I                                        6  150INSEQN
     I                                        6  15 ALSEQN
     I                                       16  16 INCODE
     I                                       17  18 INENTT
     I                                       19  24 INDATE
     I                                       25  300INTIME
     I                                       31  40 INJOB
     I                                       41  50 INUSER
     I                                       51  560INNBR
     I                                       57  66 INPGM
     I                                       67  76 INOBJ
     I                                       77  86 INLIB
     I                                       87  96 INMBR
     I                                       97 1060INCTRR
     I                                      107 107 INFLAG
     I                                      108 1170INCCID
     I                                      118 125 INRES
      *
      *-----------------------------------------------------------
     I            DS
      ** Data Structure for EDJRNEPD output
     I                                        1 125 OUT1
     I                                        1   50JOENTL
     I                                        6  150JOSEQN
     I                                       16  16 JOCODE
     I                                       17  18 JOENTT
     I                                       19  24 JODATE
     I                                       25  300JOTIME
     I                                       31  40 JOJOB
     I                                       41  50 JOUSER
     I                                       51  560JONBR
     I                                       57  66 JOPGM
     I                                       67  76 JOOBJ
     I                                       77  86 JOLIB
     I                                       87  96 JOMBR
     I                                       97 1060JOCTRR
     I                                      107 107 JOFLAG
     I                                      108 1170JOCCID
     I                                      118 125 JORES
      *-----------------------------------------------------------
     IDBERR       DS                            256
      * Data structure for data-base error processing.
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *-----------------------------------------------------------
      * Data structure for defining Deal No. as numeric.
     I                                        1   6 DLNUM
     I                                        1   60DELNUM
      *-----------------------------------------------------------
      **
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      ***  Parameter entry list
     C           *ENTRY    PLIST
      *IN PARAMETERS
     C                     PARM           DATA1
     C                     PARM           D2
     C                     PARM           SRCF    1
      *OUT PARAMETERS
     C                     PARM JOOBJ     CDFILE  8
     C                     PARM ALSEQN    CDBKEY 10
     C                     PARM           WRTCD1  1
     C                     PARM           RECCNT  50
      *
      ***  Copyright Statement and initialisation
      *
     C                     MOVEACPY@      MKI@   80
     C                     Z-ADD*ZERO     RECCNT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                     LR               *
     C                     RETRN                                      *
      *                                                               *
      *================================================================
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
      *** Set-up the data from the input parameters so that it can be used.
      *
     C                     MOVEADATA1     IN1
     C                     MOVEAD2,2      DLNUM
      *
     C                     MOVEADATA1     OUT1      P
     C                     MOVEAD2        JOESD     P
      *
      ***  Read corresponding Deal record for amendment
      ***  or if does not exist then dataabase error.
      *
     C           DELNUM    CHAINDEALFILE             40
      *
     C           *IN40     IFEQ '1'                        B1
     C                     Z-ADD01        DBASE            ***************
     C                     MOVEL'DEALSDC 'DBFILE           * DB ERROR 01 *
     C                     MOVE DLNO      DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E1
      *
      ***  Copy value of Journal sequence into commitment id.
      *
     C                     Z-ADDJOSEQN    JOCCID
      *
      ***  Copy value of REPO indicator to journal entry data
      *
     C                     MOVEADRID      D2,838
     C                     MOVEAD2        JOESD     P
      *
      ***  Write to the batch member
      *
     C                     EXSR WRITE
      *
      ***  Continue by deleting the record read in by ED0060.
      ***  Use the journal sequence number from the incoming data parameter
      ***  to delete the record from 'Old' or 'New' as appropriate.
      *
     C           SRCF      IFEQ 'N'                        B1
      *
     C           INSEQN    DELETEDNEWJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD02        DBASE            ***************
     C                     MOVEL'EDNEWJL4'DBFILE           * DB ERROR 02 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ELSE                            X1
      *
     C           INSEQN    DELETEDOLDJD4             01
      *
     C           *IN01     IFEQ '1'                        B2
     C                     Z-ADD03        DBASE            ***************
     C                     MOVEL'EDOLDJL4'DBFILE           * DB ERROR 03 *
     C                     MOVE INSEQN    DBKEY            ***************
     C                     EXSR *PSSR
     C                     ENDIF                           E2
      *
     C                     ENDIF                           E1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WRITE
      *****************************************************************
      *                                                               *
      *  WRITE - Write a detail record to EDJRNEPD                    *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           WRITE     BEGSR
      *
     C                     WRITEEDJRNED1
     C                     ADD  1         RECCNT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine.                *
      *                                                               *
      *  Called By: Various                                           *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           *** *PSSR  ***
      *
     C                     SETON                     U7U8LR
     C                     MOVE 'E'       WRTCD1
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK                     B1
     C                     MOVE 'Y'       WRUN    1
     C                     MOVEL'ED0580'  DBPGM
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
     C                     DUMP
     C                     ENDIF                           E1
      *
      ** Exit program
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE COMPILE-TIME ARRAYS
      ***
**  CPY@
(c) Finastra International Limited 2001
