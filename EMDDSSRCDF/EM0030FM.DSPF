     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Sub-line type maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Exposure Management Module                           *
     A*                                                               *
     A*  DSPF/EM0030S# - Edit Sub-Limit Types                         *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI002             Date 19Aug98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  CTI002 - *CREATE                                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/EMTFSLL0 EMTFSL)
     A                                      CA03(03 'Exit')
     A                                      CA09(09 'Add Record')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A          R SLSFL                     TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  I  6 10
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A            #TYPE     R        O  6 15REFFLD(SLTYPE)
     A            #DESC     R        O  6 20REFFLD(SLDESC)
     A            #LIMIT        11Y 0O  6 51EDTCDE(Z)
      *
     A          R SLCTL                     SFLCTL(SLSFL)
     A                                      SFLSIZ(0026)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                  5  9'Sel  Type  Description            -
     A                                                   Limit'
     A                                  1 24'Trade Finance Sub-Line Maintenance'
     A                                      DSPATR(HI)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A            #PGM          10A  O  1  2
     A                                  3  2'A=Amend,  D=Delete,  E=Enquire'
     A                                      COLOR(BLU)
      *
     A          R SLTRL
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 22  2'F3=Exit   F9=Add'
     A                                      DSPATR(HI)
     A*
     A          R SLRCD
     A                                      CF10(10 'Confirm')
     A                                      CA12(12 'Return')
     A                                  1 24'Trade Finance Sub-Type Maintenance'
     A                                      DSPATR(HI)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A            #PGM          10A  O  1  2
     A            #TYPE     R        B  7 38REFFLD(SLTYPE)
     A  41
     AO 42                                  DSPATR(PR)
     A                                  7 20'Sub-Limit Type'
     A                                 10 20'Description'
     A            #DESC     R        B 10 38REFFLD(SLDESC)
     A  41                                  DSPATR(PR)
     A N41                             23  3'F10=Confirm'
     A N41                             23 17'F12=Return'
     A  41                             23  3'F12=Return'
     A          R DLTRCD
     A                                      CA12(12 'Return')
     A                                      CF10(10 'Confirm')
     A                                  2 30'Sub-Limit Maintenance'
     A                                      DSPATR(HI)
     A                                  2 67DATE
     A                                      EDTCDE(Y)
     A                                  3 67TIME
     A            #PGM          10   O  2  3
     A                                 23  4'F10=Confirm    F12=Return'
     A                                  8 22'Warning.  You are about to delete'
     A                                 10 22'a Trade Finance Sub-Type.  This wi-
     A                                      ll'
     A                                 12 22'purge ALL records from the Exposur-
     A                                      e'
     A                                 14 22'Management system that are associa-
     A                                      ted'
     A                                 16 22'with this sub-type'
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A* 99N99                               SFLEND
     A            #PGM                      SFLPGMQ
