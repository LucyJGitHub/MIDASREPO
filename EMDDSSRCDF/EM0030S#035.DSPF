     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Select sub-line type')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Exposure Management Module                           *
     A*                                                               *
     A*  DSPF/EM0030FM - Select Sub-Lines                             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CTI002             Date 19Aug98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CTI002 - *CREATE                                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/EMTFSLL0 EMTFSL)
     A                                      CA03(03 'Exit')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A*
     A          R STSFL                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  I  6 10
     A            #TYPE     R        O  6 15REFFLD(SLTYPE)
     A            #DESC     R        O  6 20REFFLD(SLDESC)
     A*
     A          R STCTL                     SFLCTL(STSFL)
     A                                      SFLSIZ(0026)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                  5  9'Sel'
     A                                  5 14'Type'
     A                                  5 20'Description'
     A                                  1 33'Select Sub-Line'
     A                                      DSPATR(HI)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A            #PGM          10A  O  1  2
     A                                  3  2'1=Select'
     A                                      COLOR(BLU)
     A*
     A          R STTRL
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 22  2'F3=Exit'
     A                                      DSPATR(HI)
      *
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A* 99N99                               SFLEND
     A            #PGM                      SFLPGMQ
