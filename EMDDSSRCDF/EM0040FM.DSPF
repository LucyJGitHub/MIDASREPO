     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Customer sub-limit maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Exposure Management Module                           *
     A*                                                               *
     A*  DSPF/EM0040FM - Customer sub-limit maintenance               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI002  *CREATE    Date 18Sep98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CTI002 - *CREATE                                             *
     A*                                                               *
     A*****************************************************************
     A*
     A*-------------------------------------------------------------------------
     A*  THE FOLLOWING ARE SOME OF THE KEYWORDS THAT MIGHT BE SPECIFIED
     A*  FOR A DISPLAY FILE.
     A*-------------------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R EM0040S1                  SFL
     A  53                                  SFLNXTCHG
     A            SSEL           1A  B  8  5
     A N61 59N68                            DSPATR(HI)
     A  61                                  DSPATR(RI)
     A N59
     AO 68                                  DSPATR(ND)
     A  61                                  DSPATR(PC)
     A N59
     AO 68                                  DSPATR(PR)
      **Start of change CGL029
     A**********  SACNO          4A  B  8  7                                                  CGL029
     A            SACNO         10A  B  8  4
      **End of change CGL029
     A N60N59N68                            DSPATR(HI)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A  59
     AO 68                                  DSPATR(PR)
      **Start of change CGL029
     A**********  #TYPE     R        O  8 14REFFLD(EMTFSL/SLTYPE *LIBL/EMTFSLPD)              CGL029
     A**********  SACCN         25A  O  8 18                                                  CGL029
     A            #TYPE     R        O  8 16REFFLD(EMTFSL/SLTYPE *LIBL/EMTFSLPD)
     A            SACCN         25A  O  8 19
      **End of change CGL029
     A            SLIM          15A  B  8 47
     A N62N68                               DSPATR(HI)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A            SSCL           2A  B  8 70
     A N63N68                               DSPATR(HI)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A            SHLIM         15A  H
     A          R EM0040C1                  SFLCTL(EM0040S1)
     A                                      SFLSIZ(0028)
     A                                      SFLPAG(0014)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CA03
     A N68 59                               CF09
     A N68                                  CF04
     A                                      RTNCSRLOC(&@@CRC1 &@@CFC1 &@@CPC1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                      SFLCSRRRN(&@@RNS1)
     A  52                                  SFLDSP
     A                                      SFLDSPCTL
     A  54                                  SFLINZ
     A  51                                  SFLCLR
     A  50                                  SFLEND
     A            @@RNC1         4S 0H      SFLRCDNBR
     A                                  1 13'EM0040F1'
     A                                  1 26'Customer Sub-Limit Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  5  5'D=Delete'
     A N59
     AO 68                                  DSPATR(ND)
     A                                  6  6'Account'
     A                                  6 51'Limit'
     A                                  6 68'Scaling'
     A                                  7  7'Code'
      **Start of change CGL029
     A**********                        7 12'SubLine'                                         CGL029
     A                                  7 13'SubLine'
      **End of change CGL029
     A                                  7 51'Amount'
     A                                  7 68'Factor'
     A            @@CPC1         4S 0H
     A            @@CFC1        10A  H
     A            @@CRC1        10A  H
     A                                  3 20'Customer'
     A            SCNUM          6A  O  3 30
     A                                  3 39'Description'
     A            SCSSN         10A  O  3 52
     A            @@RNS1         5S 0H
     A                                  6 22'Account'
     A                                  7 23'Name'
     A                                  5 31'Total sub-limit'
     A            SSBLI         15A  O  5 47OVRDTA
     A  65                                  DSPATR(RI)
     A                                  4 31'Total T/F limit'
     A            STFLI         15A  O  4 47OVRDTA
     A  66                                  DSPATR(RI)
     A          R EM0040F1
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                 23 64'F9=Add'
     A N59
     AO 68                                  DSPATR(ND)
     A                                 23  4'F3=Exit'
     A                                 23 13'F4=Prompt'
     A  68                                  DSPATR(ND)
     A          R EM0040S2                  SFL
      **Start of change CGL029
     A**********  SACNO          4A  O  8  7                                                  CGL029
     A**********  SACCN         25A  O  8 18                                                  CGL029
     A            SACNO         10A  O  8  4
     A            SACCN         25A  O  8 19
      **End of change CGL029
     A            SLIM          15A  O  8 47
     A            SSEL           1A  O  8  5
     A            SSCL           2A  O  8 70
     A          R EM0040C2                  SFLCTL(EM0040S2)
     A                                      SFLSIZ(0028)
     A                                      SFLPAG(0014)
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  67                                  SFLCLR
     A                                  6 22'Account'
     A                                  7 23'Name'
     A                                  6  6'Account'
     A                                  6 51'Limit'
     A                                  6 68'Scaling'
     A                                  7  7'Code'
     A                                  7 51'Amount'
     A                                  7 68'Factor'
     A                                  1 31'Confirm Delete'
     A                                  3  5'Press Enter to confirm your choice-
     A                                      s for D=Delete'
     A                                  4  5'Press F12 to return to change your-
     A                                       choices'
      **Start of change CGL029
     A**********                        7 12'SubLine'                                         CGL029
     A                                  7 13'SubLine'
      **End of change CGL029
     A          R EM0040F2
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                 23  4'F12=Cancel'
