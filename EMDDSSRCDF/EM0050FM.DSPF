     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Sub-Line Account Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Exposure Management Module                           *
     A*                                                               *
     A*  DSPF/EM0050FM - Sub-Line Account Code Maintenance            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI002  *CREATE    Date 12Oct98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CTI002 - *CREATE                                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A*
     A          R EM0050S1                  SFL
     A  53                                  SFLNXTCHG
     A            SSEL           1A  B  8  2
     A N61 59N68                            DSPATR(HI)
     A  61                                  DSPATR(RI)
     A N59
     AO 68                                  DSPATR(ND)
     A  61                                  DSPATR(PC)
     A N59
     AO 68                                  DSPATR(PR)
      **Start of change CGL029
     A**********  STYPE     R        B  8  6REFFLD(TYPE)                                      CGL029
     A            STYPE     R        B  8  4REFFLD(TYPE)
      **End of change CGL029
     A N63N68                               DSPATR(HI)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
      **Start of change CGL029
     A**********  SACOD     R        B  8 10REFFLD(ACODA)                                     CGL029
     A            SACOD     R        B  8  6REFFLD(AFPN)
      **End of change CGL029
     A N60N68                               DSPATR(HI)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
      **Start of change CGL029
     A**********  SDESC         25   O  8 18                                                  CGL029
     A**********  SOFFA     R        B  8 46REFFLD(ACODA)                                     CGL029
     A            SDESC         25   O  8 17
     A            SOFFA     R        B  8 43REFFLD(AFPN)
      **End of change CGL029
     A N62N68                               DSPATR(HI)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  68                                  DSPATR(PR)
     A            SOFFN     R        O  8 54REFFLD(ACCN)
      **Start of change CGL029
     A**********  SHACOD         4   H                                                        CGL029
     A**********  SHTYPE         1   H                                                        CGL029
     A**********  SHOFFA         4   H                                                        CGL029
     A            SHACOD        10   H
     A            SHTYPE         1   H
     A            SHOFFA        10   H
      **End of change CGL029
     A*
     A          R EM0050C1                  SFLCTL(EM0050S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA03
     A N68 59                               CF09
     A N68                                  CF04
     A                                      RTNCSRLOC(&@@CRC1 &@@CFC1 &@@CPC1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                      SFLCSRRRN(&@@RNS1)
     A  52                                  SFLDSP
     A                                      SFLDSPCTL
     A  54                                  SFLINZ
     A  51                                  SFLCLR
     A  50                                  SFLEND
     A            @@RNC1         4S 0H      SFLRCDNBR
     A                                  1 13'EM0050F1'
      **Start of change CGL029
     A**********                        5 10'Exposure                          -              CGL029
     A**********                              Offset'                                         CGL029
     A**********                        6  4'Sub   Account                     -              CGL029
     A**********                                    Balance'                                  CGL029
     A**********                        7  4'Line  Code    Description         -              CGL029
     A**********                                    Account Description'                      CGL029
     A                                  6  3'Sub'
     A                                  7  3'Lin'
     A                                  5  7'Exposure'
     A                                  6  7'Account'
     A                                  7  7'Code'
     A                                  7 17'Description'
     A                                  5 44'Offset'
     A                                  6 44'Balance'
     A                                  7 44'Account'
     A                                  7 54'Description'
      **End of change CGL029
     A                                  4  2'D=Delete'
     A N59
     AO 68                                  DSPATR(ND)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A            @@CPC1         4S 0H
     A            @@CFC1        10A  H
     A            @@CRC1        10A  H
     A            @@RNS1         5S 0H
     A                                  1 27'Sub-Line Account Code Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A          R EM0050F1
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A  68                                  DSPATR(ND)
     A                                 23 64'F9=Add'
     A N59
     AO 68                                  DSPATR(ND)
     A          R EM0050S2                  SFL
     A            SSEL           1A  O  9  2
      **Start of change CGL029
     A**********  STYPE     R        O  9  6REFFLD(EMTFSA/SATYPE *LIBL/EMTFSAL0)              CGL029
     A**********  SACOD     R        O  9 10REFFLD(ACODA)                                     CGL029
     A**********  SDESC         25   O  9 18                                                  CGL029
     A**********  SOFFA     R        O  9 46REFFLD(ACODA)                                     CGL029
     A            STYPE     R        O  9  4REFFLD(EMTFSA/SATYPE *LIBL/EMTFSAL0)
     A            SACOD     R        O  9  6REFFLD(AFPN)
     A            SDESC         25   O  9 17
     A            SOFFA     R        O  9 43REFFLD(AFPN)
      **End of change CGL029
     A            SOFFN     R        O  9 54REFFLD(@A5UPA7/A5ACCN *LIBL/SDACODL-
     A                                      0)
     A          R EM0050C2                  SFLCTL(EM0050S2)
     A                                      CF12
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  67                                  SFLCLR
     A                                      SFLSIZ(0028)
     A                                      SFLPAG(0014)
     A                                  1 31'Confirm Delete'
     A                                  3  5'Press Enter to confirm your choice-
     A                                      s for D=Delete'
     A                                  4  5'Press F12 to return to change your-
     A                                       choices'
      **Start of change CGL029
     A**********                        6 10'Exposure                          -              CGL029
     A**********                              Offset'                                         CGL029
     A**********                        7  4'Sub   Account                     -              CGL029
     A**********                                    Balance'                                  CGL029
     A**********                        8  4'Line  Code    Description         -              CGL029
     A**********                                    Account Description'                      CGL029
     A                                  7  3'Sub'
     A                                  8  3'Lin'
     A                                  6  7'Exposure'
     A                                  7  7'Account'
     A                                  8  7'Code'
     A                                  8 17'Description'
     A                                  6 44'Offset'
     A                                  7 44'Balance'
     A                                  8 44'Account'
     A                                  8 54'Description'
      **End of change CGL029
     A                                  5  2'D=Delete'
     A          R EM0050F2
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                 23  4'F12=Cancel'
