     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Customer sub-limit enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Exposure Management Module                           *
     A*                                                               *
     A*  DSPF/EM0340FM - DISPLAY SUB-LIMIT AMOUNTS                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI002             Date 19Aug98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CTI002 - *CREATE                                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/EMTFCBPD EMTFCB)
     A                                      CA03(03 'Exit')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*****************************************************************
     A          R SFL340                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            SFLRRN         5S 0H
     A            SFLSEL         1A  I  8  2
     A            CBACNO    R        O  8  4
      **Start of change CGL029
     A**********  #DESC         17A  O  8  9                                                  CGL029
     A**********  SEXP          14Y 0O  8 27EDTCDE(M)                                         CGL029
     A**********  SSCLA         14Y 0O  8 43EDTCDE(M)                                         CGL029
     A**********  CBLIM     R        O  8 59EDTCDE(Z)                                         CGL029
     A**********  #PCT           5Y 2O  8 75EDTWRD('   .  ')                                  CGL029
     A            #DESC         17A  O  8 16
     A            SEXP          14Y 0O  8 36EDTCDE(M)
     A            SSCLA         14Y 0O  8 53EDTCDE(M)
     A            CBLIM     R        O  9  6EDTCDE(Z)
     A            #PCT           5Y 2O  9 23EDTWRD('   .  ')
      **End of change CGL029
     A*****************************************************************
     A          R CTL340                    SFLCTL(SFL340)
     A                                      SFLSIZ(9999)
      **Start of change CGL029
     A**********                            SFLPAG(0014)                                      CGL029
     A                                      SFLPAG(7)
      **End of change CGL029
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A            #PGM          10A  O  1  2
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A                                  1 25'Customer Trade Finance Sub-Limits'
     A                                      DSPATR(HI)
     A                                  5  2'1=Select'
     A                                      COLOR(BLU)
      **Start of change CGL029
     A**********                        7  4'Code Description              Expo-              CGL029
     A**********                            sure        Exposure           Limi-              CGL029
     A**********                            t    %'                                           CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********                        6  3'Account                        Act-              CGL029
     A**********                            ual          Scaled                -              CGL029
     A**********                                Util'                                         CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A                                  6  4'Acnt Code'
     A                                      DSPATR(HI)
     A                                  6 16'Description'
     A                                      DSPATR(HI)
     A                                  6 35'Actual Exposure'
     A                                      DSPATR(HI)
     A                                  6 52'Scaled Exposure'
     A                                      DSPATR(HI)
     A                                  7 16'Limit'
     A                                      DSPATR(HI)
     A                                  7 23'Util %'
     A                                      DSPATR(HI)
      **End of change CGL029
     A            CBCUST    R        O  4  2
     A            CBDESC        45A  O  4  9
     A                                  3  2'Account'
     A                                  3 10'Details'
     A                                  3 18'in'
     A                                  3 21'000 ''S'
     A            A6CYNM    R        O  3 28REFFLD(SDCURRD0/A6CYNM *LIBL/SDCURR-
     A                                      PD)
     A*****************************************************************
     A          R TRL340
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit'
     A                                      DSPATR(HI)
     A            TXT1          20   O 23 12DSPATR(HI)
     A*****************************************************************
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
     A*****************************************************************
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A            #PGM                      SFLPGMQ
     A* ***************************************************************
     A          R SFL340L                   SFL
     A            SFLL           5S 0H
     A            SLTYPE    R        O  8  5REFFLD(EMTFSL/SLTYPE *LIBL/EMTFSLPD)
     A            SLDESC    R        O  8  9REFFLD(EMTFSL/SLDESC *LIBL/EMTFSLPD)
     A            SLEXP     R        O  8 40REFFLD(EMTFSL/SLEXP *LIBL/EMTFSLPD)
     A                                      EDTCDE(M)
     A            SLLIM     R        O  8 57REFFLD(EMTFSL/SLEXP *LIBL/EMTFSLPD)
     A                                      EDTCDE(M)
     A* ***************************************************************
     A          R CTL340L                   SFLCTL(SFL340L)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0014)
     A                                      CA06(06)
     A  40                                  SFLDSP
     A N41                                  SFLDSPCTL
     A  41                                  SFLCLR
     A                                      OVERLAY
     A            #PGM          10A  O  1  2
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A                                  1 25'Customer Trade Finance Sub-Limits'
     A                                      DSPATR(HI)
     A                                  3  2'Account'
     A                                  3 10'Details'
     A                                  3 18'in'
     A                                  3 21'000 ''S'
     A            CBCUST    R        O  4  2
     A            CBDESC        45A  O  4  9
     A            A6CYNM    R        O  3 28REFFLD(SDCURRD0/A6CYNM *LIBL/SDCURR-
     A                                      PD)
     A                                  6  4'Sub'
     A                                      DSPATR(HI)
     A                                  7  3'Line  Description                 -
     A                                                Exposure            Limit'
     A                                      DSPATR(HI)
