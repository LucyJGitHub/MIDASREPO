     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Detailed sub-limit enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Exposure Management Module                           *
     A*                                                               *
     A*  DSPF/EM0350FM - DISPLAY SUB-LIMIT AMOUNTS : DETAILED         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CTI002             Date 19Aug98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CTI002 - *CREATE                                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/EMTFEXPD EMTFEX)
     A                                      CA03(03 'Exit')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A          R SFL350                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            SFLRRN         5S 0H
     A            #DTER          6Y 0O  8  5EDTCDE(Y)
     A            EXUAMT    R        O  8 19EDTCDE(Z)
     A            EQUIV         15Y 0O  8 47EDTCDE(Z)
     A            EXCURR    R        O  8 37
     A            #DTEV          6Y 0O  8 65EDTCDE(Y)
     A            EXRDTE    R        H
     A            EXVDTE    R        H
     A            #DRCR          6   O  8 75
     A          R CTL350                    SFLCTL(SFL350)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A            #PGM          10A  O  1  2
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A                                  7  7'Date               Exposure   Ccy -
     A                                                 Equivalent      Date'
     A                                      DSPATR(HI)
     A                                  1 26'Trade Finance Sub-Limit Postings'
     A                                      DSPATR(HI)
     A                                  3  2'Account'
     A                                  3 10'Details'
     A                                  3 18'in'
     A                                  3 21'000 ''S'
     A            A6CYNM    R        O  3 28REFFLD(SDCURRD0/A6CYNM *LIBL/SDCURR-
     A                                      PD)
     A            EXCNUM    R        O  4  2
     A            CBDESC        45A  O  4 10
     A            EXACNO    R        O  5  2
      **Start of change CGL029
     A**********  ACDESC        45A  O  5 10                                                  CGL029
     A            ACDESC        45A  O  5 14
      **End of change CGL029
     A                                  6  6'Posting                           -
     A                                                                 Value'
     A                                      DSPATR(HI)
     A          R TRL350
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit'
     A                                      DSPATR(HI)
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A* 99N99                               SFLEND
     A            #PGM                      SFLPGMQ
