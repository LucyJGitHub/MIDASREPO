     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Sub-line display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Exposure Management Module                           *
     A*                                                               *
     A*  DSPF/EM0360FM - DISPLAY SUB-LINES                            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CTI002             Date 19Aug98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CTI002 - *CREATE                                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A          R SFL360SL                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  34                                  SFLNXTCHG
     A            SLTSL          1A  I  7  3
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A            SLTYPE    R        O  7  7REFFLD(EMTFSL/SLTYPE *LIBL/EMTFSLL0)
     A            SLDESC    R        O  7 11REFFLD(EMTFSL/SLDESC *LIBL/EMTFSLL0)
     A            SLEXP     R        O  7 42REFFLD(EMTFSL/SLEXP *LIBL/EMTFSLL0)
     A                                      EDTCDE(M)
     A            SLLIM         15Y 0O  7 59EDTCDE(Z)
     A            SLUTIL         5Y 2O  7 75EDTWRD('   .  ')
     A            SLRRN          5S 0H
     A          R CTL360SL                  SFLCTL(SFL360SL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0013)
     A                                      CA03(03 'Exit')
     A  30                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  88                                  SFLEND
     A                                      OVERLAY
     A                                  6  2'Sel Type Description              -
     A                                                    Exposure           Li-
     A                                      mit Util %'
     A            #PGM          10A  O  1  2
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  1 23'Exposure Management Sub-Line Enqui-
     A                                      ry'
     A                                      DSPATR(HI)
     A                                  5  2'1=Select'
     A                                      COLOR(BLU)
     A                                  3  2'Account'
     A                                  3 10'Details'
     A                                  3 18'in'
     A                                  3 21'000 ''S'
     A            A6CYNM    R        O  3 28REFFLD(SDCURRD0/A6CYNM *LIBL/SDCURR-
     A                                      PD)
     A          R SFL360SA                  SFL
     A  43                                  SFLNXTCHG
     A            SARRN          5S 0H
      **Start of change CGL029
     A**********  SLTSA          1A  I  7  4                                                  CGL029
     A            SLTSA          1A  I  8  3
      **End of change CGL029
     A N44N45                               DSPATR(HI)
     A  44N45                               DSPATR(RI)
     A  44N45                               DSPATR(PC)
     A  45                                  DSPATR(ND)
     A  45                                  DSPATR(PR)
      **Start of change CGL029
     A**********  SAACOD    R        O  7  7REFFLD(EMTFSA/SAACOD *LIBL/EMTFSAL0)              CGL029
     A**********                            EDTCDE(Z)                                         CGL029
     A**********  SAEXP     R        O  7 40REFFLD(EMTFSA/SAEXP *LIBL/EMTFSAL0)               CGL029
     A**********                            EDTCDE(M)                                         CGL029
     A**********  A5ACCN    R        O  7 13REFFLD(@A5UPA7/A5ACCN *LIBL/SDACODL-              CGL029
     A**********                            0)                                                CGL029
     A**********  SALIM     R        O  7 58REFFLD(EMTFSA/SALIM *LIBL/EMTFSAL0)               CGL029
     A**********                            EDTCDE(Z)                                         CGL029
     A**********  SAUTIL         5Y 2O  7 74EDTWRD('   .  ')                                  CGL029
     A            SAACOD    R        O  8  7REFFLD(EMTFSA/SAACOD *LIBL/EMTFSAL0)
     A                                      EDTCDE(Z)
     A            A5ACCN    R        O  8 19REFFLD(@A5UPA7/A5ACCN *LIBL/SDACODL-
     A                                      0)
     A            SAEXP     R        O  8 46REFFLD(EMTFSA/SAEXP *LIBL/EMTFSAL0)
     A                                      EDTCDE(M)
     A            SALIM     R        O  8 63REFFLD(EMTFSA/SALIM *LIBL/EMTFSAL0)
     A                                      EDTCDE(Z)
     A            SAUTIL         5Y 2O  9  8EDTWRD('   .  ')
      **End of change CGL029
     A          R CTL360SA                  SFLCTL(SFL360SA)
     A                                      SFLSIZ(0100)
      **Start of change CGL029
     A**********                            SFLPAG(0013)                                      CGL029
     A                                      SFLPAG(7)
      **End of change CGL029
     A                                      CA03(12)
     A  40                                  SFLDSP
     A N41                                  SFLDSPCTL
     A  41                                  SFLCLR
     A  88                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
      **Start of change CGL029
     A**********                        5  6'Account'                                         CGL029
     A**********                        6  2'Sel  Code  Description            -              CGL029
     A**********                                        Exposure            Lim-              CGL029
     A**********                            it Util %'                                        CGL029
     A                                  6  2'Sel'
     A                                  6  7'Acnt Code'
     A                                  6 19'Description'
     A                                  6 53'Exposure'
     A                                  6 73'Limit'
     A                                  7  8'Util %'
      **End of change CGL029
     A            #PGM          10A  O  1  2
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  1 22'Exposure Management Sub-Line Accou-
     A                                      nts'
     A                                      DSPATR(HI)
     A                                  4  3'1=Select'
     A                                      COLOR(BLU)
     A                                  3  2'Account'
     A                                  3 10'Details'
     A                                  3 18'in'
     A                                  3 21'000 ''S'
     A            A6CYNM    R        O  3 28REFFLD(SDCURRD0/A6CYNM *LIBL/SDCURR-
     A                                      PD)
     A                                  4 13'Sub-Line Type :'
     A            SLTYPE    R        O  4 29REFFLD(EMTFSL/SLTYPE *LIBL/EMTFSLL0)
     A            SLDESC    R        O  4 31REFFLD(EMTFSL/SLDESC *LIBL/EMTFSLL0)
     A          R SFL360CB                  SFL
     A  53                                  SFLNXTCHG
     A            CBRRN          5S 0H
     A            SLTCB          1A  I  7  3
     A N54N55                               DSPATR(HI)
     A  54N55                               DSPATR(RI)
     A  54N55                               DSPATR(PC)
     A  55                                  DSPATR(ND)
     A  55                                  DSPATR(PR)
     A            CBCUST    R        O  7  6REFFLD(EMTFCB/CBCUST *LIBL/EMTFCBL0)
     A            BBCRNM    R        O  7 14REFFLD(@BBREBG/BBCRNM *LIBL/SDCUSTL-
     A                                      0)
     A            CBEXP     R        O  7 36REFFLD(EMTFCB/CBEXP *LIBL/EMTFCBL0)
     A                                      EDTCDE(M)
     A            CBLIM     R        O  7 54REFFLD(EMTFCB/CBLIM *LIBL/EMTFCBL0)
     A                                      EDTCDE(Z)
     A            CBUTIL         5Y 2O  7 70EDTWRD('   .  ')
     A          R CTL360CB                  SFLCTL(SFL360CB)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0013)
     A                                      CA03(24)
     A  50                                  SFLDSP
     A N51                                  SFLDSPCTL
     A  51                                  SFLCLR
     A  88                                  SFLEND
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRATR
     A                                  5  5'Customer'
     A                                  6  2'Sel Number  Description           -
     A                                              Exposure            Limit U-
     A                                      til %'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A            #PGM          10A  O  1  2
     A                                  4  2'1=Select'
     A                                      COLOR(BLU)
     A                                  1 21'Exposure Management Customer Sub-L-
     A                                      imits'
     A                                      DSPATR(HI)
     A                                  4 14'Account Code :'
     A            SAACOD    R        O  4 29REFFLD(EMTFSA/SAACOD *LIBL/EMTFSAL0)
     A            A5ACCN    R        O  4 36REFFLD(@A5UPA7/A5ACCN *LIBL/SDACODL-
     A                                      0)
     A                                  3  2'Account'
     A                                  3 10'Details'
     A                                  3 18'in'
     A                                  3 21'000 ''S'
     A            A6CYNM    R        O  3 28REFFLD(SDCURRD0/A6CYNM *LIBL/SDCURR-
     A                                      PD)
     A          R TRL360
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  3'F3=Exit'
     A          R SFLMSG                       SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MSGCTL
     A                                      SFLCTL(SFLMSG)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(2)
     A                                      SFLDSPCTL SFLDSP
     A            #PGM                      SFLPGMQ
