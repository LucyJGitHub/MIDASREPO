      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM MM Deals agst a security by cust')
     A********************************************************************
     A*                                                                  *
     A*  Midas EXPOSURE MANAGEMENT ENQUIRY                     *
     A*                                                                  *
     A*    EM1102FM - CUSTOMER DEALS ENQUIRY                             *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 06Feb97               *
      *                 S01253             Date 17Apr90               *
     A*                   S01199             DATE 03NOV89             *
     A*                   E11071             DATE 25JUN87                *
     A*                   E10667             DATE 20JAN87                *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     A*      112563 - Header and footer changes required for GUI         *
     A*               Multilanguage support.                             *
     A*      S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS.           *
     A*      S01199 - HELP CHANGES                                       *
     A*      E11071 - CHANGE 'SECURITY' TO 'FACILITY'.                   *E11071
     A*      E10667 - IF OUTSTANDING MM DEALS IS CALLED UP FOR A       ***
     A*               CUSTOMER WITH SOME TRNS., THEN CMD-5 IS TAKEN
     A*               AND A CUSTOMER WITH NO TRNS. IS SELECTED
     A*               INSTEAD OF A BLANK SCREEN LINES ARE DISPLAYED.
     A*                                                                  *
     A********************************************************************
     A***
     A***  WHEN CREATING THIS FILE TAKE THE OPTION
     A***        RSTDSP  *YES
     A***        ------------
     A*****************************************************************
     A                                      CHGINPDFT PRINT(MPRINT)
     A                                      CF01(01)
     A                                      CF12(02)
     A                                      CF05(05)
     A                                      CF03(07)
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A          R EM1102F1                  SLNO(01)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A* ERASE LINES APPEARING AS PER E10667                /E10641/
     A                                      OVERLAY
     A                                      ERASE(EM1102SC EM1102SF)
     A*                                                    /E10641/
     A                                      BLINK
     A*** Screen Header
     A*
     A                                      CLRL(24)
     A  08                              1 12'OUTSTANDING FX DEALS BY CUSTOMER +
     A                                      ENQUIRY'
     A                                      DSPATR(HI UL)
     A  09                              1 12'OUTSTANDING MM DEALS BY CUSTOMER +
     A                                      ENQUIRY'
     A                                      DSPATR(HI UL)
     A*                                                          /E11071/
     A**10******************************1 04'OUTSTANDING DEALS AGAINST A +
     A**************************************SECURITY BY CUSTOMER ENQUIRY'
     A  10                              1 04'OUTSTANDING DEALS AGAINST A +
     A                                      FACILITY BY CUSTOMER ENQUIRY'
     A*                                                          /E11071/
     A                                      DSPATR(HI UL)
     A            MTIME          6Y 0O  1 65EDTWRD('  .  .  ')
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A*
     A**                                             Begin /112563/
     A                                  2  2'EM1102' DSPATR(HI)
     A**                                             End   /112563/
     A                                 03 02'Customer'
     A  14                                  DSPATR(ND)
     A            CNUMWF         6A  O 03 11
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            CRNM          20A  O 03 18
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            CSNM          10A  O 03 52
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            CTWN          10A  O 04 18
     A  14                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A     84 08                       04 73'Related'
     A     84                          05 04'Type'
     A     84                          05 15'Deal no.'
     A     84                          05 25'Deal'
     A     84                          05 33'Value'
     A     84                          05 41'Maturity'
     A     84                          05 50'Ccy'
     A     84                          05 63'Amount'
     A     84 08                       05 73'Deal no.'
     A*** Screen Header
     A*
     A*
     A          R EM1102SF                  SFL
     A            LEOPSF         1A  B 06 02DSPATR(UL HI)
     A            DTYPWF        10A  O 06 04DSPATR(HI)
     A            DLNO           6Y 0B 06 15DSPATR(HI PR)
     A            DDATE          7A  O 06 24DSPATR(HI)
     A            VDATE          7A  O 06 33DSPATR(HI)
     A            NARR1          8A  O 06 41DSPATR(HI)
     A            CCY            3A  O 06 50DSPATR(HI)
     A            AMT1WF        17A  O 06 56DSPATR(HI)
     A            RDNO           6A  O 06 75DSPATR(HI)
     A                                 07 08'REVERSED'
     A  15                                  DSPATR(ND)
     A                                      DSPATR(RI)
     A            LINE2         56A  O 07 19DSPATR(HI)
     A*
     A          R EM1102SC                  SFLCTL(EM1102SF)
     A                                      BLINK
     A  18                                  SFLDSPCTL
     A  20                                  SFLDSP
     A                                      SFLSIZ(100)
     A                                      SFLPAG(7)
     A N20                                  SFLCLR
     A N11                                  SFLEND
     A                                      TEXT('SUBFILE CONTROL REC')
     A                                      OVERLAY
     A            RCDNBR         3  0H      SFLRCDNBR
     A            MSG           79A  O 20 02
     A  16                                  DSPATR(ND)
     A                                      DSPATR(RI)
     A            LEOPUP         1A  B 21 02DSPATR(HI UL)
     A  17                                  DSPATR(ND PR)
     A            LEOPTX        60A  O 21 05DSPATR(HI)
     A  17                                  DSPATR(ND)
     A**                                             Begin /112563/
     A* 17 ******                      22 02'Press F1 to return to Exposure enq-
     A***********                           uiry,F12 to return to previous scre-
     A***********                           en'
     A***********                      23 02'Press F3 to return to menu,F5 for -
     A***********                           Shortname enquiry'
     A  17                             22 02'F1=Exposure enquiry  F12=Previous -
     A                                      screen'
     A                                 23 02'F3=Exit  F5=Shortname enquiry'
     A**                                             End   /112563/
     A                                 24 02'or enter customer number or short-
     A                                       name'
     A            CUST          10A  B 24 41DSPATR(HI UL)
     A                                 24 52'for new enquiry'
