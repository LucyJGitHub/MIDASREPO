      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Daily Delivery Risk Display File')
     A********************************************************************
     A*                                                                  *
     A*  Midas  EXPOSURE MANAGEMENT MODULE                    *
     A*                                                                  *
     A*      EM1500FM - DAILY DELIVERY RISK ENQUIRY                      *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*      Last Amend No. CDL002       DATE 17SEP97                    *
     A*      Prev Amend No. E39162       DATE 30MAY92                    *
     A*                     E29106       DATE 01OCT91                    *
     A*                     E28443       DATE 11SEP91                    *
     A*                                                                  *
     A*----------------------------------------------------------------  *
     A*                                                                  *
     A*      CDL002 - FX Netting. Add Deal Type as hidden field.         *
     A*      E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                 *
     A*      E29106 - F12 SHOULD ONLY BE ENABLED IF THIS PROGRAM CALLED  *
     A*               FROM EM0301 (SAME AS F1)                           *
     A*               POSITION CURSOR ON CUSTOMER FIELD                  *
     A*      E28443 - HELP TEXT TAG ADDED                                *
     A*                                                                  *
     A********************************************************************
     A*         /////////////////////////////////////////
     A*        // WARNING: When creating this display //
     A*       //   file, take options RSTDSP(*YES)   //
     A*      //                       SHARE (*YES)  //
     A*     /////////////////////////////////////////
     A********************************************************************
     A*                                              Begin /CDL002/
     A                                      REF(MFRFL)
     A*                                              End   /CDL002/
     A                                      INDARA
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A*                                                    /E28443/
     A*                                                    /E28443/
     A  01                                  CF01
     A                                      CF03(03)
     A                                      CF05(05)
     A*                                                    /E29106/
     A***********                           CF12(12)
     A***********                           CF24
     A  01                                  CF12(12)
     A  01                                  CF24
     A*                                                    /E29106/
     A          R EM1500F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A*
     A            USRID         10A  O 01 02
     A            PGMNM         10A  O 01 13
     A            RUNA           7A  O 01 73
     A            WSID          10A  O 02 02
     A                                 02 28'Daily Delivery Risk Enquiry'
     A                                      DSPATR(HI UL)
     A                                 02 72TIME
     A*
     A          R EM1500F1                  TEXT('FOOTER')
     A                                      OVERLAY
     A  01N02                          23 02'1=Details   F1=Initial Enquiry'
     A  01N02                          23 35'F3=Main Menu   F5=Refresh'
     A  01N02                          23 63'F24=More Keys'
     A*
     A  01 02                          23  2'F12=Previous   Rollup'
     A  01 02                          23 26'Rolldown   F24=More Keys'
     A*
     A N01N02                          23  2'1=Details   F3=Main Menu'
     A*                                                    /E29106/
     A*N01N02****                      23 29'F5=Refresh   F12=Previous'
     A*N01N02****                      23 57'Rollup   F24=More Keys'
     A***********
     A*N01*02****                      23  2'Rolldown   F24=More Keys'
     A N01                             23 29'F5=Refresh   Rollup'
     A N01                             23 51'Rolldown'
     A*                                                    /E29106/
     A*
     A          R EM1500S0                  SFL
     A  28
     AO 29                                  SFLNXTCHG
     A            SBSCLT         1A  B 12  4
     A N29                                  DSPATR(HI UL)
     A  29                                  DSPATR(RI)
     A  29
     AO 05                                  DSPATR(PC)
     A            SBBSNR         2A  O 12  7DSPATR(HI)
     A            SBDESC        17A  O 12 10DSPATR(HI)
     A            SBCCY          3A  O 12 30DSPATR(HI)
     A            SBAMT         16A  O 12 36DSPATR(HI)
     A            SBCEQA        16A  O 12 58DSPATR(HI)
     A            SBDLNO         6D  H
     A*                                              Begin /CDL002/
     A            SBDTYP    R        H      REFFLD(DTYP)
     A*                                              End   /CDL002/
     A*
     A          R EM1500C0                  SFLCTL(EM1500S0)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A                                      SFLSIZ(50)
     A                                      SFLPAG(7)
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A            DSPREC         4  0H      SFLRCDNBR(CURSOR)
     A                                  4  4'Customer:'
     A            OCSSN         10A  O  4 14DSPATR(HI)
     A                                      OVRDTA
     A            OCRNM         20A  O  4 25DSPATR(HI)
     A                                      OVRDTA
     A            OCTWN         10A  O  4 46DSPATR(HI)
     A                                      OVRDTA
     A                                  5  4'Date:'
     A            ODATE          7A  O  5 10DSPATR(HI)
     A                                      OVRDTA
     A                                  5 20'Currency:'
     A            OCCY           3A  O  5 30DSPATR(HI)
     A                                      OVRDTA
     A  88                              5 36'Branch:'
     A  88        OBRCA          3A  O  5 44DSPATR(HI)
     A  88                                  OVRDTA
     A                                  6  4'Transactions:-  FX:'
     A            OUTFX          1A  O  6 24DSPATR(HI)
     A                                      OVRDTA
     A                                  6 28'MM:'
     A            OUTMM          1A  O  6 32DSPATR(HI)
     A                                      OVRDTA
     A                                  6 36'LE:'
     A            OUTLE          1A  O  6 40DSPATR(HI)
     A                                      OVRDTA
     A                                  6 44'SE-Counterparty:'
     A            OUTCP          1A  O  6 61DSPATR(HI)
     A                                      OVRDTA
     A*
     A                                  8  4'Overall DDR Limit.:'
     A            EMLMCY         3A  O  8 24OVRDTA
     A N40                                  DSPATR(HI)
     A            OVRAMT        11A  O  8 28OVRDTA
     A N40                                  DSPATR(HI)
     A                                  8 43'FX MDDR Limit:'
     A                                      OVRATR
     A  43                                  DSPATR(ND)
     A            DLLMCY         3A  O  8 58OVRDTA
     A N41N43                               DSPATR(HI)
     A  43                                  DSPATR(ND)
     A*/COPY WNCPYSRC,EM1500FM05
     A            DDRAMT        11A  O  8 62OVRDTA
     A N41N43                               DSPATR(HI)
     A  43                                  DSPATR(ND)
     A*
     A*/COPY WNCPYSRC,EM1500FM01
     A                                  8 74'(In'
     A                                      OVRATR
     A*/COPY WNCPYSRC,EM1500FM02
     A                                  9  4'Delivery amount.......:'
     A            ODELIV        11A  O  9 28OVRDTA
     A                                      DSPATR(HI)
     A*
     A                                  9 43'Available........:'
     A                                      OVRATR
     A  42                                  DSPATR(ND)
     A            OAVAIL        11A  O  9 62OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(ND)
     A*
     A*/COPY WNCPYSRC,EM1500FM03
     A                                  9 74' 000s)'
     A                                      OVRATR
     A*/COPY WNCPYSRC,EM1500FM04
     A*
     A                                 11  7'Transaction          Currency'
     A                                 11 43'Amount'
     A                                 11 54'Limit currency equivalent'
     A*
     A          R EM1500F2                  TEXT('KEYINPUT')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 20  2'Customer:'
     A            SCUST         10A  B 20 12OVRDTA
     A N20                                  DSPATR(HI UL)
     A  20                                  DSPATR(RI)
     A*                                                    /E29106/
     A**20*******
     A*O*05******                            DSPATR(PC)
     A                                      DSPATR(PC)
     A*                                                    /E29106/
     A                                 20 26'Date:'
     A            SDATE          6A  B 20 32OVRDTA
     A N23                                  DSPATR(HI UL)
     A  23                                  DSPATR(RI PC)
     A                                 20 42'Currency:'
     A            SCCY           3A  B 20 52OVRDTA
     A N22                                  DSPATR(HI UL)
     A  22                                  DSPATR(RI PC)
     A  88                             20 59'Branch:'
     A  88        SBRCA          3A  B 20 67OVRDTA
     A  88N21                               DSPATR(HI UL)
     A  88 21                               DSPATR(RI PC)
     A                                 21  2'Transactions:- FX:'
     A            SCRFX          1A  B 21 21OVRDTA
     A N24                                  DSPATR(HI UL)
     A  24                                  DSPATR(RI PC)
     A                                 21 24'MM:'
     A            SCRMM          1A  B 21 28OVRDTA
     A N25                                  DSPATR(HI UL)
     A  25                                  DSPATR(RI PC)
     A                                 21 31'LE:'
     A            SCRLE          1A  B 21 35OVRDTA
     A N26                                  DSPATR(HI UL)
     A  26                                  DSPATR(RI PC)
     A                                 21 38'SE-Counterparty:'
     A            SCRCP          1A  B 21 55OVRDTA
     A N27                                  DSPATR(HI UL)
     A  27                                  DSPATR(RI PC)
     A*
     A          R EM1500F3                  OVERLAY
     A                                 13 24'No details for this combination'
     A*
     A          R EM1500S1                  SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A*
     A          R EM1500C1                  SFLCTL(EM1500S1)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  53                                  SFLEND
     A                                      SFLINZ
     A                                      SFLSIZ(17)
     A                                      SFLPAG(1)
     A            PGMQ                      SFLPGMQ
     A*
     A          R EM1500F4                  OVERLAY
     A                                  8  4' '
     A                                 13  4' '
