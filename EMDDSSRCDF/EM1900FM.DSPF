      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EM Customer Shortname enquiry')
     A********************************************************************
     A*                                                                  *
     A*  Midas EXPOSURE MANAGEMENT ENQUIRY                     *
     A*                                                                  *
     A*    EM1900FM - CUSTOMER SHORTNAME  ENQUIRY                        *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01253             Date 17Apr90               *
     A*                 S01199             Date 03NOV89               *
     A*                                                                  *
     A********************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*      S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS.           *
     A*      S01199 - HELP CHANGES                                       *
     A********************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03(07)
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A                                      ROLLUP(12)
     A                                      ROLLDOWN(13)
     A*
     A          R EM1900F1                  KEEP
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A*** Screen Header
     A*
     A            SCNID          2A  B 01 02DSPATR(PR ND)
     A                                  1 26'CUSTOMER SHORTNAME ENQUIRY'
     A                                      DSPATR(HI UL)
     A            MTIME          6Y 0O  1 65EDTWRD('  .  .  ')
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A*
     A N14                              3 02'Opt'
     A N14                              3 07'Shortname'
     A N14                              3 23'Cust.no.'
     A N14                              3 32'Report name'
     A N14                              3 57'Report town'
     A*
     A*
     A          R EM1900SF                  SFL
     A            LEOPSF         1A  B 04 03DSPATR(UL HI)
     A            CSNM          10A  O 04 07DSPATR(HI)
      * Begin /CSD027/
     A**********  CNUM           6Y 0B 04 23DSPATR(HI PR)                                     CSD027
     A            CNUM           6A  B 04 23DSPATR(HI PR)
      * End /CSD027/
     A            CRNM          20A  O 04 32DSPATR(HI)
     A            CTWN          10A  O 04 57DSPATR(HI)
     A*
     A          R EM1900SC                  SFLCTL(EM1900SF)
     A                                      BLINK
     A  18                                  SFLDSPCTL
     A  20                                  SFLDSP
     A                                      SFLSIZ(18)
     A                                      SFLPAG(18)
     A N20                                  SFLCLR
     A                                      TEXT('SUBFILE CONTROL REC')
     A                                      OVERLAY
     A                                      KEEP
     A*           RCDNBR         3  0H      SFLRCDNBR
     A            MSG           79A  O 22 02
     A                                      DSPATR(HI)
     A  16                                  DSPATR(ND)
     A                                      DSPATR(RI)
     A                                 23 02'Press F3 to return to menu,or -
     A                                      enter "1" against required customer'
     A                                 24 02'Or enter shortname,customer no. -
     A                                      or start of shortname to scan'
     A            SHNMIN        10A  B 24 64
     A                                      DSPATR(UL HI)
