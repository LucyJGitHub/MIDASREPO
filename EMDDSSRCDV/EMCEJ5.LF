      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas EM Exposure mgmt limits - company level')        *
      *****************************************************************
      *                                                               *
      *  Midas - Exposure Management module                           *
      *                                                               *
      *  JLF/EMCEJ5 - Exposure management limits - Company level      *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD053948 *CREATE   Date 13Apr20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD053948 - MM-Limit not displayed in "Exposure by customer/  *
      *             parent enquiry" menu when S01433 is ON.           *
      *             Fix is to introduce new logical files to hold     *
      *             the MM-Limit details. New files will be used only *
      *             when S01433 is switched on.                       *
      *             This new file is counterpart of EMCEJ2.           *
      *                                                               *
      *****************************************************************
      *
     A                                      DYNSLT
     A                                      JDFTVAL
      *
     A          R EMCEJF1                   JFILE(EMCEXX2 SDCLLMPD +
     A                                      SDLIMTPD SDLIMTPD)
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(MCUNA DZCUST)
     A          J                           JOIN(2 3)
     A                                      JFLD(DZFXLT A2LMTY)
     A          J                           JOIN(2 4)
     A                                      JFLD(DZMMTY A2LMTY)
      *
     A            RECI                      JREF(1)
     A            MCUNA
     A            FLTY                      RENAME(DZFXLT)
     A            FLAM                      RENAME(DZFLAM)
     A            FLED                      RENAME(DZFLED)
     A            FPER                      RENAME(A2PL1P)  JREF(3)
     A            MFXE
     A            MFXB
     A            LLTY                      RENAME(DZMMTY)
     A            LLAM                      RENAME(DZMMAM)
     A            LLED                      RENAME(DZMMED)
     A            MPER                      RENAME(A2PL1P)  JREF(4)
     A            MMME
     A            MMMB
     A            MLEG
     A            MLEE
     A            MLEB
     A            MLAE
     A            MLAB
     A            MREG
     A            MREE
     A            MREB
     A            MSTE
     A            MSTB
     A            MSIE      R
     A            MSCE      R
     A            MSPO      R
     A            LCD                       JREF(1)
     A            CHTP                      JREF(1)
     A            MXPT
     A            DZLEVL                    JREF(2)
     A            DZFRLA
     A            MFRE
     A            MFRO
     A            DZIRLM
     A            MIRE
     A            MIRO
     A            MFUE
     A            MFUO
     A            MOPE
     A            MOPO
     A            TFEX
     A            TFGR
     A            TFOF
      *
     A          K MCUNA
     A          O RECI                      COMP(NE 'D')
     A          O DZLEVL                    COMP(NE 'C')
      *
