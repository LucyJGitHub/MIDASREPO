      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Sub-Line Report')
      *****************************************************************
      *                                                               *
      *  Midas - Exposure Management Module                           *
      *                                                               *
      *  PRTF/EM0670P1 - Sub-Line Limits Report                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI002  *CREATE    Date 24Sep98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HDR670                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            PGM           10   O  1  2
     A                                  1 21'Trade Finance Exposure and L-
     A                                      imits'
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  2 35'by Sub-Line'
     A                                  3 69'PAGE :'
     A            PAGNBR         3  0   3 76EDTCDE(Z)
     A                                  3  2'Account'
     A                                  3 10'Details'
     A                                  3 18'in'
     A                                  3 21'000 ''S'
     A            A6CYNM    R        O  3 28REFFLD(SDCURRD0/A6CYNM *LIBL/SDCURR-
     A                                      PD)
     A                                  5  4'Type'
     A                                  5 12'Description'
     A                                  5 55'Exposure'
     A                                  5 74'Limit'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DTL670                    SPACEB(1)
     A            #TYPE     R              6REFFLD(EMTFSL/SLTYPE *LIBL/EMTFSLPD)
     A            #DESC     R             13REFFLD(EMTFSL/SLDESC *LIBL/EMTFSLPD)
     A            #DEXP     R             44REFFLD(EMTFSA/SAEXP *LIBL/EMTFSAPD)
     A                                      EDTCDE(K)
     A            #DLIM     R             60REFFLD(EMTFSA/SALIM *LIBL/EMTFSAPD)
     A                                      EDTWRD('   ,   ,   ,   ,   ')
      *
      *-------------------*
      ***  Report Summary *
      *-------------------*
      *
     A          R TRL670                    SPACEB(1)
     A                                     1'Grand'
     A                                     7'Total'
     A            #TDESC    R             13REFFLD(EMTFSL/SLDESC *LIBL/EMTFSLPD)
     A            #TEXP     R             44REFFLD(EMTFSA/SAEXP *LIBL/EMTFSAPD)
     A                                      EDTCDE(K)
     A            #TLIM     R             60REFFLD(EMTFSA/SALIM *LIBL/EMTFSAPD)
     A                                      EDTWRD('   ,   ,   ,   ,   ')
      *
      *-------------------*
      ***  No data Record *
      *-------------------*
      *
     A          R NODATA
     A                                    51'** NO DATA TO REPORT **'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Trailer Record *
      *-------------------*
      *
     A          R TRAILER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
