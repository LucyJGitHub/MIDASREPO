      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('EM errors report')
      *****************************************************************
      *                                                               *
      *  Midas - Exposure Management Module                           *
      *                                                               *
      *  PRTF/EM0720P1 - EM errors report                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI002  *CREATE    Date 30Oct98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HDR720                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            PGM           10   O  1  2
     A                                  1 21'Exposure Management Errors'
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  3 69'PAGE :'
     A            PAGNBR         3  0   3 76EDTCDE(Z)
     A                                  3  2'Program'
     A            #PGMN     R           3 10REFFLD(EMTRCK/PGMN *LIBL/EMTRCKPD)
     A                                  5  4'Message'
     A                                  5 13'Description'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DTL720                    SPACEB(1)
     A            #MSGID    R              4REFFLD(EMTRCK/MSGID *LIBL/EMTRCKPD)
     A            #MSGDT    R             13REFFLD(EMTRCK/MSGDAT *LIBL/EMTRCKPD)
      *
      *------------------------------*
      ***  Report Summary for detail *
      *------------------------------*
      *
     A          R RU1720                    SPACEB(1)
     A                                     7'Total for'
     A            #PGMN     R             17REFFLD(EMTRCK/PGMN *LIBL/EMTRCKPD)
     A            #TERRP         4S       44EDTWRD(' ,   ')
      *
      *-------------------*
      ***  Report Summary *
      *-------------------*
      *
     A          R TRL720                    SPACEB(2)
     A                                     3'TOTAL'
     A            #TERRG         4S       44EDTWRD(' ,   ')
      *
      *-------------------*
      ***  No data Record *
      *-------------------*
      *
     A          R NODATA
     A                                    51'** NO DATA TO REPORT **'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Trailer Record *
      *-------------------*
      *
     A          R TRAILER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
