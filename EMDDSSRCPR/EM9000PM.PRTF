      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas EM Print exposure scaling file')
      *****************************************************************
      *                                                               *
      *  Midas - Exposure Management Module                           *
      *                                                               *
      *  PRTF/EM9000PM - Print Exposure Scaling File                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R ZZPAGHDR
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'Reference'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##URPT        53        45TEXT('User Report Title')
     A                                   111'Date'
     A            ##MRDT         7       116TEXT('Midas Run Date')
     A                                   124'Page'
     A                                   129PAGNBR
     A                                      EDTCDE(3)
     A                                   111'Time'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A            ##AURT        53        45TEXT('Audit/Request Title')
     A                                      SPACEB(1)
     A            ##BJNA        53        45TEXT('Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R ZZCOLHDG                  TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                   118'Last'
     A                                   127'Change'
     A                                     6'Limit category'
     A                                      SPACEB(1)
     A                                    30'Include in totals'
     A                                    49'Scaling type'
     A                                    75'1'
     A                                    82'2'
     A                                    89'3'
     A                                    96'4'
     A                                   103'5'
     A                                   110'6'
     A                                   118'change'
     A                                   127' type'
      *=========================================================================
     A          R ZZDTLRCD                  TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            XRLICF        23         6TEXT('Limit category fun field')
     A            XRITFF         3        37TEXT('Include in totals fun fld')
     A            XRSTYF        12        49TEXT('Scaling type function fld')
     A N79                                64'Periods:'
     A            XRPRD1         5  0     73TEXT('Period 1')
     A                                      EDTCDE(4)
     A            XRPRD2         5  0     80TEXT('Period 2')
     A                                      EDTCDE(4)
     A            XRPRD3         5  0     87TEXT('Period 3')
     A                                      EDTCDE(4)
     A            XRPRD4         5  0     94TEXT('Period 4')
     A                                      EDTCDE(4)
     A            XRPRD5         5  0    101TEXT('Period 5')
     A                                      EDTCDE(4)
     A            XRPRD6         5  0    108TEXT('Period 6')
     A                                      EDTCDE(4)
     A            XRACDT         7       118TEXT('alpha change date')
     A            XRALCH         6       127TEXT('alpha of last change')
     A N79                                60'Percentages:'
     A                                      SPACEB(1)
     A N79        XRPR1P         3  0     75TEXT('Period 1 percentage')
     A                                      EDTCDE(4)
     A            XRPR2P         3  0     82TEXT('Period 2 percentage')
     A                                      EDTCDE(4)
     A            XRPR3P         3  0     89TEXT('Period 3 percentage')
     A                                      EDTCDE(4)
     A            XRPR4P         3  0     96TEXT('Period 4 percentage')
     A                                      EDTCDE(4)
     A            XRPR5P         3  0    103TEXT('Period 5 percentage')
     A                                      EDTCDE(4)
     A            XRPR6P         3  0    110TEXT('Period 6 percentage')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of Limit category
     A  99N99     XRLMCA         2         1SPACEB(1)
      * 'Hidden' internal version of Include in totals
     A  99N99     XRINTO         1         1SPACEB(1)
      * 'Hidden' internal version of Scaling type
     A  99N99     XRSCTY         1         1SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     XRLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     XRTYLC         1         1SPACEB(1)
      *=========================================================================
     A          R ZZFINTTL                  TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    56'No of Calculated Recs..:'
     A            ZTNCRC         5  0     81TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     ZTCON          1         1SPACEB(1)
      *=========================================================================
     A          R ZZENDRPT                  TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    60'*** END OF REPORT ***'
      *=========================================================================
