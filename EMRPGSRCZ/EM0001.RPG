     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas EM Set up EM/TI interface feature')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  EM0001- Installation program                                 *
      *                                                               *
      *  Function: This program will be called by the implementation  *
      *            program and will install files to be journalled,   *
      *            populate trade finance files.                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CTI002  *CREATE    Date 28Aug98               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CTI002 - *CREATE                                             *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Use of indicators.                                           *
      *                                                               *
      * 97      General Chain Indicator                               *
      * 99      Read  of SDACODL1                                     *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     FSDACODL0UF  E           K        DISK
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      *
      *---------------------------------------------------------------*
      /SPACE 3
     ILDA       E DSLDA
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IIPSDS      SDS
     I                                      244 253 JOB
     I                                      254 263 USER
      ***  Program status data structure
      *
      * ===============================================================
      *
     C           *ENTRY    PLIST
     C                     PARM           RTCDE   7
      *
     C           *NAMVAR   DEFN           LDA   256
      *
     C           *LOVAL    SETLLSDACODL0
     C                     READ SDACODL0                 99
     C           *IN99     DOWEQ*OFF
      *
     C           A5TFCT    IFEQ *BLANK
     C                     MOVE 'N'       A5TFCT
     C                     UPDAT@A5UPA7                98
      *
      * Error ?
     C           *IN98     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'SDACODPD'DBFILE           *DB ERROR 001 *
     C                     MOVEL'EM0001'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ SDACODL0                 99
     C                     ENDDO
      *
     C           *INU7     IFEQ *OFF
     C           *INU8     ANDEQ*OFF
     C                     CALL 'EM0452'
     C                     ENDIF
      *
     C           *INU7     IFEQ *OFF
     C           *INU8     ANDEQ*OFF
     C                     CALL 'EM0730'
     C                     ENDIF
      *
     C                     SETON                       LRRT
     C                     RETRN
      * ===============================================================
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVEL'*ERROR*' RTCDE
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
