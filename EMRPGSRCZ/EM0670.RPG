     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('EM Sub-Line Report')                                   *
      *****************************************************************
      *                                                               *
      *  Midas - Exposure Management Module                           *
      *                                                               *
      *  EM0670 - EM Sub-line report                                  *
      *                                                               *
      *  Function:  This program produces a report over exposure      *
      *             management sub-lines.                             *
      *                                                               *
      *  Called By:        - Menu option                              *
      *                                                               *
      *  Calls: AOMMODR0 - Access Midas moduleds details              *
      *         AOSARDR0 - Access SAR details                         *
      *         DBERRCTL - Error processing                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD030956           Date 09Oct14               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CTI002  *CREATE    Date 24Sep98               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD030956 - Additional changes to BFM-TI enhancement          *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  nnnnnn - (fix details)                                       *
      *                                                               *
      *****************************************************************
      *                                                               *
      * Indicator Usage                                               *
      * 01 - Printer Overflow                                         *
      * 80 - EOF EMTFSLL0                                             *
      * 89 - GENERAL ERROR INDICATOR                                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      *  GETLIM - Get limits and exposures                            *
      *  *INZSR - Initial processing                                  *
      *  *PSSR  - Error processing                                    *
      *                                                               *
      *****************************************************************
      *
      * Main input file Sub-Line Types
     FEMTFSLL0IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      * Sub-Line Accounts File
     FEMTFSAL0IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      * Currency File
     FSDCURRL1IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      * Exposure Management Control File
     FSDEXPML0IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      * Printer File
     FEM0670P1O   E             01     PRINTER
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      * ---------------------------------------------------------------
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      * ---------------------------------------------------------------
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
      * ---------------------------------------------------------------
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      * ---------------------------------------------------------------
      * MODULE DETAILS ACCESSED VIA ACCESS PROGRAM
     ISDMMOD    E DSSDMMODPD
      *
      * FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     IDSFDY     E DSDSFDY
      *
      * ---------------------------------------------------------------
      * MAIN PROCESSING
      *
      * Read all records in EMTFSLL0
      * Get limits and exposures from EMTFSAL0
      * If change in sub line type (SLTYPE) write summary
      * At EOF, write totals
     C                     READ EMTFSLL0                 80
     C           *IN80     DOWEQ*OFF
      *
      * Move report data
     C                     MOVELSLTYPE    #TYPE
     C                     MOVELSLDESC    #DESC
      *
      * Get limits and exposures for sub-line
     C                     EXSR GETLIM
      *
      * Write Header ??
     C           *IN01     IFEQ *ON
     C                     ADD  1         PAGNBR
     C                     WRITEHDR670                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'HDR670'  DBFILE           *DB ERROR 001 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     MOVE *OFF      *IN01
     C                     ENDIF
      *
     C                     WRITEDTL670                 89
      *
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'DTL670'  DBFILE           *DB ERROR 002 *
     C                     MOVELSLTYPE    DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      * Set data printed field to 'Y'
     C                     MOVE 'Y'       WDATA
      *
     C                     ENDIF
      *
     C                     READ EMTFSLL0                 80
     C                     ENDDO
      *
     C           WDATA     IFEQ 'Y'
     C                     WRITETRL670                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'TRL670'  DBFILE           *DB ERROR 003 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ELSE
      *
      * No data report
     C                     Z-ADD1         PAGNBR
     C                     WRITEHDR670                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD9         DBASE            ***************
     C                     MOVEL'HDR670'  DBFILE           *DB ERROR 009 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     WRITENODATA                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD10        DBASE            ***************
     C                     MOVEL'NODATA'  DBFILE           *DB ERROR 010 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     WRITETRAILER                89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'TRL670'  DBFILE           *DB ERROR 004 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     SETON                         LR
     C                     RETRN
      *
      * ---------------------------------------------------------------
      * Subroutine GETLIM
      *            Get limits and exposures
      * ---------------------------------------------------------------
     C           GETLIM    BEGSR
      *
      * Zeroise fields
     C                     Z-ADD*ZEROS    #DEXP
     C                     Z-ADD*ZEROS    #DLIM
      *
     C                     SETOF                         81
     C           SLTYPE    SETLLEMTFSAL0
     C           SLTYPE    READEEMTFSAL0                 81
     C           *IN81     DOWEQ*OFF
      *
      * DIVIDE LIMITS AND EXPOSURES BY 1000
     C           SAEXP     IFNE 0
     C                     DIV  1000      SAEXP     H
     C                     ENDIF
      *
      * Do not divide limits by 1000 as they are input as 000's
      *
     C                     ADD  SAEXP     #DEXP
     C                     ADD  SALIM     #DLIM
      *
     C           SLTYPE    READEEMTFSAL0                 81
     C                     ENDDO
      *
      * Accumulate for totals
     C                     ADD  #DEXP     #TEXP
     C                     ADD  #DLIM     #TLIM
      *
     C                     ENDSR
      * ---------------------------------------------------------------
      * Subroutine *INZSR
      *            Initialisation routine
      * ---------------------------------------------------------------
     C           *INZSR    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
     C                     SETON                         01Write Header
      *
      ** Init data printed field
      *
     C                     MOVE *BLANKS   WDATA   1
      *
      * Both Trade Innovation and feature CTI002 must be on.
      ** Access MIDAS Modules details via access program
      *  (database error handling done in access program)
     C                     CALL 'AOMMODR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
      **  Check for data base error.
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDMMODPD'DBFILE           * DBERROR 005 *
     C                     MOVEL'*FIRST ' DBKEY  29
     C                     Z-ADD5         DBASE
     C                     SETON                     89
     C                     ENDIF
      *
      ** Access SAR details file to determine if CTI002 is switched ON
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*VERIFY' POPTN   7
     C                     PARM 'CTI002'  PSARD   6
     C                     MOVE 'N'       CTI002  1
      *
      ** If core feature is switched ON, set on indicator that will
      ** condition the rest of the processing
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       CTI002
     C                     ELSE
      *
      ** else, database error (return code other than *NRF)
      *
     C           PRTCD     IFNE '*NRF   '
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE            ***************
     C                     MOVEL'SCSARDPD'DBFILE           *DB ERROR 006 *
     C                     MOVEL'CTI002'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      ****Check*to*see*if*Trade*Innovation*installed*and*CTI002********                     MD030956
     C********** BGN3ST    IFNE 'Y'                                                         MD030956
     C********** CTI002    ORNE 'Y'                                                         MD030956
     C**********           SETON                         LR                                 MD030956
     C**********           RETRN                                                            MD030956
     C**********           ENDIF                                                            MD030956
      *
      * Get Exposure Management ICD Details
     C           *LOVAL    SETLLSDEXPML0
     C                     READ SDEXPML0                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD007       DBASE            ***************
     C                     MOVEL'SDEXPMPD'DBFILE           *DB ERROR 007 *
     C                     MOVEL'CTI002'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Get currency Name
     C           BUCYCD    CHAINSDCURRL1             89
     C           *IN89     IFEQ *ON                        No Records
     C           *LOCK     IN   LDA
     C                     Z-ADD008       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           *DB ERROR 008 *
     C                     MOVEL'CTI002'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
