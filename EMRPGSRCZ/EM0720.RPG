     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('EM errors Report')                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Exposure Management Module                           *
      *                                                               *
      *  EM0720 - EM Error report                                     *
      *                                                               *
      *  Function:  This program produces a report over exposure      *
      *             management errors.                                *
      *                                                               *
      *  Called By:        - Menu option                              *
      *                                                               *
      *  Calls: AOMMODR0 - Access Midas moduleds details              *
      *         AOSARDR0 - Access SAR details                         *
      *         DBERRCTL - Error processing                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD030956           Date 09Oct14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CTI002  *CREATE    Date 30Oct98               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (fix details)                                       *
      *                                                               *
      * Indicator Usage                                               *
      * 01 - Printer Overflow                                         *
      * 80 - EOF EMTRCKPD                                             *
      * 89 - GENERAL ERROR INDICATOR                                  *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      *  *INZSR - Initial processing                                  *
      *  *PSSR  - Error processing                                    *
      *                                                               *
      *****************************************************************
      *
      * Keep Track in Exposure
     FEMTRCKPDIF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      * Printer File
     FEM0720P1O   E             01     PRINTER
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      * ---------------------------------------------------------------
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      * ---------------------------------------------------------------
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
      * ---------------------------------------------------------------
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      * ---------------------------------------------------------------
      * MODULE DETAILS ACCESSED VIA ACCESS PROGRAM
     ISDMMOD    E DSSDMMODPD
      *
      * FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     IDSFDY     E DSDSFDY
      *
      * ---------------------------------------------------------------
      * MAIN PROCESSING
      *
      * Read all records in EMTRCKPD
      * If change in program name (PGMN) write summary
     C                     READ EMTRCKPD                 80
     C           *IN80     DOWEQ*OFF
      *
     C           WPGMN     IFNE PGMN
     C           WDATA     IFEQ 'Y'
     C                     WRITERU1720                 89
     C                     Z-ADD0         #TERRP
     C                     MOVELPGMN      WPGMN
      *
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD7         DBASE            ***************
     C                     MOVEL'RU1720'  DBFILE           *DB ERROR 007 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     SETON                     01
      *
     C                     ELSE
     C                     MOVELPGMN      WPGMN
     C                     ENDIF
      *
     C                     ENDIF
      *
      * Move report data
     C                     MOVELPGMN      #PGMN
      *
      * Write Header ??
     C           *IN01     IFEQ *ON
     C                     ADD  1         PAGNBR
     C                     WRITEHDR720                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'HDR720'  DBFILE           *DB ERROR 001 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     MOVE *OFF      *IN01
     C                     ENDIF
      *
     C                     ADD  1         #TERRP
     C                     ADD  1         #TERRG
      *
     C                     MOVELMSGID     #MSGID
     C                     MOVELMSGDAT    #MSGDT
     C                     WRITEDTL720                 89
      *
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'DTL720'  DBFILE           *DB ERROR 002 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C           PGMN      CAT  MSGID:1   DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
      *
      * Set data printed field to 'Y'
     C                     MOVE 'Y'       WDATA
      *
     C                     ENDIF
      *
     C                     READ EMTRCKPD                 80
     C                     ENDDO
      *
     C           WDATA     IFEQ 'Y'
      *
     C                     WRITERU1720                 89
     C                     Z-ADD0         #TERRP
     C                     MOVELPGMN      WPGMN
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD8         DBASE            ***************
     C                     MOVEL'RU1720'  DBFILE           *DB ERROR 008 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     WRITETRL720                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'TRL720'  DBFILE           *DB ERROR 003 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ELSE
      *
      * No data report
     C                     Z-ADD1         PAGNBR
     C                     WRITEHDR720                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD9         DBASE            ***************
     C                     MOVEL'HDR720'  DBFILE           *DB ERROR 009 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     WRITENODATA                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD10        DBASE            ***************
     C                     MOVEL'NODATA'  DBFILE           *DB ERROR 010 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     WRITETRAILER                89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'TRL720'  DBFILE           *DB ERROR 004 *
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     SETON                         LR
     C                     RETRN
      * ---------------------------------------------------------------
      * Subroutine *INZSR
      *            Initialisation routine
      * ---------------------------------------------------------------
     C           *INZSR    BEGSR
      *
     C           *LIKE     DEFN PGMN      WPGMN
      *
     C                     Z-ADD0         #TERRP
     C                     Z-ADD0         #TERRG
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
     C                     SETON                         01Write Header
      *
      ** Init data printed field
      *
     C                     MOVE *BLANKS   WDATA   1
      *
      * Both Trade Innovation and feature CTI002 must be on.
      ** Access MIDAS Modules details via access program
      *  (database error handling done in access program)
     C                     CALL 'AOMMODR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
      **  Check for data base error.
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDMMODPD'DBFILE           * DBERROR 005 *
     C                     MOVEL'*FIRST ' DBKEY  29
     C                     Z-ADD5         DBASE
     C                     SETON                     89
     C                     ENDIF
      *
      ** Access SAR details file to determine if CTI002 is switched ON
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*VERIFY' POPTN   7
     C                     PARM 'CTI002'  PSARD   6
     C                     MOVE 'N'       CTI002  1
      *
      ** If core feature is switched ON, set on indicator that will
      ** condition the rest of the processing
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       CTI002
     C                     ELSE
      *
      ** else, database error (return code other than *NRF)
      *
     C           PRTCD     IFNE '*NRF   '
     C           *LOCK     IN   LDA
     C                     Z-ADD006       DBASE            ***************
     C                     MOVEL'SCSARDPD'DBFILE           *DB ERROR 006 *
     C                     MOVEL'CTI002'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      ****Check*to*see*if*Trade*Innovation*installed*and*CTI002********                     MD030956
     C********** BGN3ST    IFNE 'Y'                                                         MD030956
     C********** CTI002    ORNE 'Y'                                                         MD030956
     C**********           SETON                         LR                                 MD030956
     C**********           RETRN                                                            MD030956
     C**********           ENDIF                                                            MD030956
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
