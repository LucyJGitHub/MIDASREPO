/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('TI Product Code/CSSF Product Type Trigger Module')    */
/*********************************************************************/
/*                                                                   */
/*       Midas - European Reporting                                  */
/*                                                                   */
/*       ERLU13CM - Midas TI Product Code/CSSF Product Type Trigger  */
/*                      Module                                       */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2005           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CER001             Date 25Apr05              */
/*       Prev Amend No. ULX024  *CREATE    Date 03Sep99              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CER001 - LUX Upgrade to MidasPlus                           */
/*       ULX024 - Trade Innovation/CSSF Reporting Interface          */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&SBS)
 
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(289)
             DCL        VAR(&INLLIBL) TYPE(*CHAR) LEN(275)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(70)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SBS) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBDLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QLFDJOBD) TYPE(*CHAR) LEN(20)
             DCL        VAR(&LEN) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PARM) TYPE(*CHAR) LEN(1000)
             DCL        VAR(&OFFSET) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&LIBCNT) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&OFFLEN) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&ERRCDE) TYPE(*CHAR) LEN(4)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2005')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Check Jobd and Setup Variables */
 
             CHGVAR     VAR(&JOBDLIB) VALUE(&SBS *CAT 'XLIB    ')
             CHGVAR     VAR(&JOBD) VALUE('MIDASIC   ')
 
             CHKOBJ     OBJ(&JOBDLIB/&JOBD) OBJTYPE(*JOBD)
 
             CHGVAR     VAR(&QLFDJOBD) VALUE(&JOBD *CAT &JOBDLIB)
             CHGVAR     VAR(%BIN(&LEN 1 4)) VALUE(1000)
             CHGVAR     VAR(%BIN(&ERRCDE 1 4)) VALUE(0)
 
/* Use API to actually access information */
 
             CALL       PGM(QWDRJOBD) PARM(&PARM &LEN JOBD0100 +
                          &QLFDJOBD &ERRCDE)
 
/* Retrieve from offset, the initial Library List defined at the jobd Level */
 
 INLLIBL:    CHGVAR     VAR(&OFFSET) VALUE(%BIN(&PARM 361 4))
             CHGVAR     VAR(&OFFSET) VALUE(&OFFSET + 1)
             CHGVAR     VAR(&LIBCNT) VALUE(%BIN(&PARM 365 4))
             CHGVAR     VAR(&OFFLEN) VALUE(11 * &LIBCNT)
             CHGVAR     VAR(&INLLIBL) VALUE(%SST(&PARM &OFFSET +
                          &OFFLEN))
             MONMSG     MSGID(MCH3601)
 
/* Change Environment Library list */
 
             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *CAT +
                          &INLLIBL *CAT ')')
 
             CALL       PGM(QCMDEXC) PARM(&CMD 289)
 
/* Normal Termination */
 
             GOTO       CMDLBL(END)
 
/* Abnormal Termination */
 
 ABNOR:
             RCVMSG     MSGDTA(&MSGDTA) MSGID(&MSGID)
             IF         COND(&MSGID *NE '       ') THEN(DO)
             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(&MSGDTA)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
 END:        CHGVAR     VAR(&CPYFLD) VALUE('(c) Misys International +
                          Banking Systems Ltd. 2005')
 
             ENDPGM
