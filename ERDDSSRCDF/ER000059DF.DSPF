      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Update MX files for Bundesbank Screen')       *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ER000059DF - Midas ER Update MX files for Bundesbank Screen  *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD025621           Date 31Mar14               *
      *                 AR690511 *CREATE   Date 31Jan11               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD025621 - Include an optional MQ Manager Field.             *
      *  AR690511 - Rename from SM000059DF to ER000059DF              *
      *             (Child: AR690512)                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *
      * --------------------------------------------------------------*
      *   Detail Record Format                                        *
      * --------------------------------------------------------------*
      *
     A          R SM59DETL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2011')
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2TEXT('User Profile')
     A            ZADATE         7A  O  1 73TEXT('Rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 73TIME
     A                                  1 13'ER000059'
     A                                  1 37'Bridge'
     A                                  2 25'Update of MX Files for Bundesbank'
     A                                      DSPATR(HI)
     A                                  6  3'Message Queue . . . . :'
      ** Start of change MD025621
     A                                  7  3'MQ Manager (optional) :'
     A**********                        7  3'Next Export Date  . . :'                       MD025621
     A                                  8  3'Next Export Date  . . :'
      ** End of change MD025621
     A            SMSGQ         48   B  6 27
     A                                      DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
      ** Start of change MD025621
     A            SMQMG         48   B  7 27
     A                                      DSPATR(HI)
     A N25N90                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
     A**********  SEXND          6   B  7 27                                                MD025621
     A            SEXND          6   B  8 27
      ** End of change MD025621
     A                                      DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A                                      OVRATR
     A                                      OVRDTA
      *
      * --------------------------------------------------------------*
      * * Footer record format                                        *
      * --------------------------------------------------------------*
      *
     A          R SM59FOOT                  TEXT('Footer')
     A                                      OVERLAY
     A N28                             23  2'F3=Exit   F5=Refresh'
     A  28                             23  2'ENTER=Confirm   F12=Cancel'
     A  28                                   DSPATR(HI)
      *
      * --------------------------------------------------------------*
      * * Message subfile record format                               *
      * --------------------------------------------------------------*
      *
     A          R SM59MSGR                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control record format                       *
      * --------------------------------------------------------------*
      *
     A          R SM59MSGCTL                TEXT('Program messages')
     A                                      SFLCTL(SM59MSGR)
     A                                      OVERLAY SFLINZ SFLPAG(1) SFLSIZ(3)
     A                                      SFLDSPCTL SFLDSP
     A  90
     AON90                                  SFLEND
     A            PGMNAM                    SFLPGMQ
      *
