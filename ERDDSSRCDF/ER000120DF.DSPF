      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas @24 KWG display transmission file history')      *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ER000120DF - Midas @24 KWG display transmission file history *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG23947           Date 20May09               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23947 - Menu Item should show correct symbol "§"          *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R ER000120S0                TEXT('(C) Misys -
     A                                      International Banking-
     A                                      Systems Ltd. 2008')
      *
     A                                      SFL
      *
     A  60                                  SFLNXTCHG
     A            D1SEL          1   B  8  3
     A  68                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A N69                                  DSPATR(UL HI)
     A            D1FNM         20   H      TEXT('File Name')
      *
     A            D1FNR          8  0   8  6TEXT('File Number')
     A                                      DSPATR(HI)
      *
     A            D1UTP          1      8 16TEXT('Function Type')
     A                                      DSPATR(HI)
      *
     A            D1DAT          8  0   8 19TEXT('Creation date')
     A                                      EDTWRD('  /  /    ')
     A                                      DSPATR(HI)
      *
     A            D1TIM          6  0   8 30TEXT('Creation time')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
      *
     A            D1NRC          7      8 40TEXT('Number of records')
     A                                      DSPATR(HI)
      *
     A            D1SIZ          8      8 48TEXT('Size in KB')
     A                                      DSPATR(HI)
      *
     A            D1STS         23      8 58TEXT('Status')
     A  79                                  COLOR(RED)
     A N79                                  DSPATR(HI)
      *
     A          R ER000120C0                TEXT('Subfile control.')
      *
     A                                      SFLCTL(ER000120S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  61                                  SFLDSP
     A N62                                  SFLDSPCTL
     A  62                                  SFLCLR
     A  63                                  SFLEND
     A                                      SFLCSRRRN(&DDSCR1)
      *
      ** Command Keys
      *
     A                                      VLDCMDKEY(29 'Valid comm-
     A                                      and key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Refresh.')
     A  64                                  ROLLUP(25 'Next page.')
     A  65                                  ROLLDOWN(26 'Previous +
     A                                      page.')
      *
      ** Set Offs
      *
     A                                      SETOF(71 'Invalid: D2FNR')
     A                                      SETOF(72 'Invalid: D2DAT')
      *
     A            DDSCR1         5  0H
      *
     A            DDSFR1         4  0H      SFLRCDNBR(CURSOR)
     A                                  1  2USER
      *
     A            DDPGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ER000120'
      *
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
      *
     A            DDJOB         10      2  2TEXT('*JOB')
      **Start Of Change - BUG23947
     A**********                        2 23'@24 KWG Transmission +                         BUG23947
     A                                  2 23'§24c KWG Transmission +
     A                                      File History'
     A                                      DSPATR(HI)
      **End Of Change - BUG23947
      *
     A                                  2 71TIME
     A                                  4  6'  File  '
     A                                  4 15'Typ'
     A                                  4 19' Creation '
     A                                  4 30'Creation'
     A                                  4 40'Num. of'
     A                                  4 48'  Size  '
     A                                  4 58'Status'
     A                                  5  6' Number '
     A                                  5 19'   Date   '
     A                                  5 30'  Time  '
     A                                  5 40'Records'
     A                                  5 48'  in KB '
      *
     A            D2FNR          8  0B  6  6TEXT('File Number')
     A                                      EDTCDE(Z)
     A  71                                  DSPATR(RI PC)
     A N71                                  DSPATR(UL HI)
      *
     A            D2DAT          8  0B  6 19TEXT('Creation date')
     A                                      EDTWRD('  /  /    ')
     A  72                                  DSPATR(RI PC)
     A N72                                  DSPATR(UL HI)
      *
     A          R ER000120F0                TEXT('Command keys')
     A                                 23  2'E=Enquire response +
     A                                      D=Display details +
     A                                      R=Recreate  F3=Main Menu +
     A                                      F5=Refresh'
      *
     A          R ER000120F1
     A                                      OVERLAY
     A                                  9  5'(No records to display)'
      *
     A          R ER000120S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R ER000120C1                TEXT('Program messages')
     A                                      SFLCTL(ER000120S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N99                                  SFLEND
     A            DDPGM                     SFLPGMQ
      *
