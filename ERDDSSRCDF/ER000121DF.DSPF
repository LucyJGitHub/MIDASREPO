      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas @24 KWG display transmission details')           *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ER000121DF - Midas @24 KWG display transmission details      *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER068             Date 25Feb14               *
      *                 CER059             Date 19Jul10               *
      *                 BUG23947           Date 20May09               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER068 - Upgrade CER047 to be compatible with BaFin 3.2.2    *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23947 - Menu Item should show correct symbol "§"          *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R ER000121S0                TEXT('(C) Finastra -
     A                                      International Banking-
     A                                      Limited 2008')
      *
     A                                      SFL
      *
     A  60                                  SFLNXTCHG
     A            D1SEL          1   B  8  3
     A  68                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A N69                                  DSPATR(UL HI)
      *
     A            D1VDT          8  0H      TEXT('Valid from date')
      *
     A            D1VTM          6  0H      TEXT('Valid from time')
      *
      **Start of change CER068
     A**********  D1ACC         20   H      TEXT('Account Number')                            CER068
     A            D1ACC         50   H      TEXT('Account Number')
      **End of change CER068
      *
     A            D1REF         12   H      TEXT('Reference')
      *
     A            D1DTP          1      8  7TEXT('Data Type')
     A                                      DSPATR(HI)
      *
      **Start of change CER068
     A**********  D1ACCX        10      8 10TEXT('Account Number')                            CER068
     A            D1ACCX        50      8 10TEXT('Account Number')
      **End of change CER068
     A                                      DSPATR(HI)
      *
      **Start of change CER068
     A**********  D1REFX        10      8 21TEXT('Reference')                                 CER068
     A            D1REFX        10      9 21TEXT('Reference')
      **End of change CER068
     A                                      DSPATR(HI)
      *
      **Start of change CER068
     A**********  D1CCY          3      8 32TEXT('Currency')                                  CER068
     A            D1CCY          3      9 32TEXT('Currency')
      **End of change CER068
     A                                      DSPATR(HI)
      *
      **Start of change CER068
     A**********  D1CDT          8  0   8 37TEXT('Creation date')                             CER068
     A            D1CDT          8  0   9 37TEXT('Creation date')
      **End of change CER068
     A                                      EDTWRD('  /  /    ')
     A                                      DSPATR(HI)
      *
      **Start of change CER068
     A**********  D1VDT1         8  0   8 49TEXT('Valid from date')                           CER068
     A            D1VDT1         8  0   9 49TEXT('Valid from date')
      **End of change CER068
     A                                      EDTWRD('  /  /    ')
     A                                      DSPATR(HI)
      *
      **Start of change CER068
     A**********  D1DDT          8  0   8 61TEXT('Deletion date')                             CER068
     A            D1DDT          8  0   9 61TEXT('Deletion date')
      **End of change CER068
     A                                      EDTWRD('  /  /    ')
     A                                      DSPATR(HI)
      *
     A          R ER000121C0                TEXT('Subfile control.')
     A                                      SFLCTL(ER000121S0)
      **Start of change CER068
     A**********                            SFLPAG(14) SFLSIZ(15)                             CER068
     A                                      SFLPAG(7) SFLSIZ(14)
      **End of change CER068
     A                                      BLINK OVERLAY
     A  61                                  SFLDSP
     A N62                                  SFLDSPCTL
     A  62                                  SFLCLR
     A  63                                  SFLEND
     A                                      SFLCSRRRN(&DDSCR1)
      *
      ** Command keys
      *
     A                                      VLDCMDKEY(29 'Valid comm-
     A                                      and key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous +
     A                                      screen.')
     A  64                                  ROLLUP(25 'Next page.')
     A  65                                  ROLLDOWN(26 'Previous +
     A                                      page.')
      *
      ** Set Offs
      *
     A                                      SETOF(71 'Invalid: D2ACC')
      *
     A            DDSCR1         5  0H
      *
     A            DDSFR1         4  0H      SFLRCDNBR(CURSOR)
     A                                  1  2USER
      *
     A            DDPGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ER000121'
      *
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
      *
     A            DDJOB         10      2  2TEXT('*JOB')
      * Start Of Changes - Bug23947                                                         BUG23947
     A**********                        2 25'@24 KWG Transmission +                         BUG23947
     A                                  2 25'§24c KWG Transmission +                        BUG23947
     A                                      Details'
     A                                      DSPATR(HI)
      * End Of Changes - Bug23947                                                           BUG23947
     A                                  2 71TIME
     A                                  4  6'Typ'
     A                                  4 10' Account- '
     A                                  4 21'Reference '
     A                                  4 32'Ccy'
     A                                  4 37' Creation '
     A                                  4 49'Valid from'
     A                                  4 61' Deletion '
     A                                  5 10'  Number  '
     A                                  5 37'   date   '
     A                                  5 49'   date   '
     A                                  5 61'   date   '
      *
      **Start of change CER068
     A**********  D2ACC         10   B  6 10TEXT('Account Number')                            CER068
     A            D2ACC         50   B  6 10TEXT('Account Number')
      **End of change CER068
     A  71                                  DSPATR(RI PC)
     A N71                                  DSPATR(UL HI)
      *
     A          R ER000121F0                TEXT('Command keys')
     A                                 23  2'E=Enquire authority +
     A                                      holders   F3=Main Menu +
     A                                      F12=Previous screen'
      *
     A          R ER000121F1
     A                                      OVERLAY
     A                                  9  5'(No records to display)'
      *
     A          R ER000121S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
      *
     A            DDPGM                     SFLPGMQ
      *
     A          R ER000121C1                TEXT('Program messages')
     A                                      SFLCTL(ER000121S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N99                                  SFLEND
     A            DDPGM                     SFLPGMQ
      *
