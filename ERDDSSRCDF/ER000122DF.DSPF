      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas @24 KWG display transmitted auth.Holders')       *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ER000122DF - Midas @24 KWG display transmitted authority     *
      *               holders                                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG23947           Date 20May09               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23947 - Menu Item should show correct symbol "§"          *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R ER000122S0                TEXT('(C) Misys -
     A                                      International Banking-
     A                                      Systems Ltd. 2008')
      *
     A                                      SFL
      *
     A  60                                  SFLNXTCHG
      *
     A            D1PTP          1      6  5TEXT('Person Type')
     A                                      DSPATR(HI)
      *
     A            D1CUS          6      6  8TEXT('Customer Number')
     A                                      DSPATR(HI)
      *
     A            D1NAM         60      6 17TEXT('Name')
     A                                      DSPATR(HI)
     A                                  7  8'Birth .:'
      *
     A            D1BDT          8  0   7 17TEXT('Date of birth')
     A                                      EDTWRD('  /  /    ')
     A                                      DSPATR(HI)
     A                                  8  8'Address:'
      *
     A            D1STR         60      8 17TEXT('Street')
     A                                      DSPATR(HI)
      *
     A            D1TWN         60      9 17TEXT('Town')
     A                                      DSPATR(HI)
      *
     A          R ER000122C0                TEXT('Subfile control.')
     A                                      SFLCTL(ER000122S0)
     A                                      SFLPAG(4) SFLSIZ(16)
     A                                      BLINK OVERLAY
     A                                      SFLMODE(&DDSMD1)
     A  61                                  SFLDSP
     A N62                                  SFLDSPCTL
     A  62                                  SFLCLR
     A  63                                  SFLEND
     A N28                                  SFLDROP(CA11)
     A  28                                  SFLFOLD(CA11)
     A                                      SFLCSRRRN(&DDSCR1)
      *
      ** Command keys
      *
     A                                      VLDCMDKEY(29 'Valid comm-
     A                                      and key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous +
     A                                      screen.')
      *
     A            DDSCR1         5  0H
      *
     A            DDSMD1         1   H
      *
     A            DDSFR1         4  0H      SFLRCDNBR(CURSOR)
     A                                  1  2USER
      *
     A            DDPGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ER000122'
      *
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
      *
     A            DDJOB         10      2  2TEXT('*JOB')
      * Start Of Changes - Bug23947                                                         BUG23947
     A**********                        2 21'@24 KWG Transmitted +                          BUG23947
     A                                  2 21'§24c KWG Transmitted +                         BUG23947
     A                                      Authority Holders'
     A                                      DSPATR(HI)
      * End Of Changes - Bug23947                                                           BUG23947
     A                                  2 71TIME
     A                                  4  4'Typ'
     A                                  4  8'Cus/Auth'
     A                                  4 17'Name'
     A                                  5  8'Number'
      *
     A          R ER000122F0                TEXT('Command keys')
     A                                 23  2'F3=Main Menu +
     A                                      F11=Toggle address +
     A                                      F12=Previous screen'
      *
     A          R ER000122F1
     A                                      OVERLAY
     A                                  9  5'(No records to display)'
      *
     A          R ER000122S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R ER000122C1                TEXT('Program messages')
     A                                      SFLCTL(ER000122S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N99                                  SFLEND
     A            DDPGM                     SFLPGMQ
      *
