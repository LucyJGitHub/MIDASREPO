      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas @24 KWG display transmission file reponse')      *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ER000123DF - Midas @24 KWG display transmission file response*
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG23947           Date 20May09               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23947 - Menu Item should show correct symbol "§"          *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *
     A          R ER000123H0                TEXT('(C) Finastra -
     A                                      International Banking-
     A                                      Limited 2008')
     A                                      BLINK OVERLAY
      *
      ** Command keys
      *
     A                                      VLDCMDKEY(29 'Valid +
     A                                                   command key')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous +
     A                                      screen.')
      *
     A                                  1  2USER
     A            DDPGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ER000123'
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
      *
     A            DDJOB         10      2  2TEXT('*JOB')
      * Start Of Changes - Bug23947                                                         BUG23947
     A**********                        2 22'@24 KWG Transmission +                         BUG23947
     A                                  2 22'§24c KWG Transmission +                        BUG23947
     A                                      response enquiry'
     A                                      DSPATR(HI)
      * End Of Changes - Bug23947                                                           BUG23947
     A                                  2 71TIME
     A                                  4  2'Error'
     A                                  5  2'Code'
     A                                  5  7'Description'
      *
     A          R ER000123F0                TEXT('Command keys')
     A                                 23  2'F3=Main Menu -
     A                                      F12=Previous screen'
      *
     A          R ER000123S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(7)
     A            DDMGKY                    SFLMSGKEY
      *
     A            DDPGM                     SFLPGMQ
      *
     A          R ER000123C0                TEXT('Program messages')
     A                                      SFLCTL(ER000123S0)
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N99                                  SFLEND
     A            DDPGM                     SFLPGMQ
      *
