      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas @24 KWG data extraction Report')                 *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ER000150DF - Midas @24 KWG data extraction Reporting         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2008            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER047  *CREATE    Date 19May08               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R ER000150F0                TEXT('(C) Misys -
     A                                      International Banking-
     A                                      Systems Ltd. 2008')
     A                                      BLINK OVERLAY
      *
      ** Command keys
      *
     A                                      VLDCMDKEY(29 'Valid +
     A                                                   command key')
     A                                      CA03(03 'Exit.')
      *
     A                                  1  2USER
     A            DDPGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ER000150'
     A                                  1 32'Extract File Report'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
      *
     A            DDJOB         10      2  2TEXT('*JOB')
     A                                  2 71TIME
      *
     A                                  8  8'Range of file numbers to +
     A                                       print . . . :'
     A            DDFRFNR        8  0B  8 50EDTCDE(3)
     A  71                                  DSPATR(RI PC)
     A N71                                  DSPATR(UL HI)
      *
     A                                  8 60'to '
     A            DDTOFNR        8  0B  8 64EDTCDE(3)
     A  72                                  DSPATR(RI PC)
     A N72                                  DSPATR(UL HI)
      *
     A                                  9  8'(leave both zero for +
     A                                       *ALL)'
     A                                 11  8'Range of date to print +
     A                                       (DDMMYY)  . :'
     A            DDFRDAT        6   B 11 50
     A  73                                  DSPATR(RI PC)
     A N73                                  DSPATR(UL HI)
      *
     A                                 11 60'to '
     A            DDTODAT        6   B 11 64
     A  74                                  DSPATR(RI PC)
     A N74                                  DSPATR(UL HI)
      *
     A                                 12  8'(leave both blank for +
     A                                       *ALL)'
      *
     A                                 23  2'F3=Exit'
      *
     A          R ER000150S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
      *
     A          R ER000150C0                TEXT('Program messages')
     A                                      SFLCTL(ER000150S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N99                                  SFLEND
     A            DDPGM                     SFLPGMQ
      *
