      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Transaction Type Decision Table Display')     *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  DSPF/ER000180DF - Midas ER Transaction Type Decision Table   *
      *                    Display                                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. BUG26779           Date 27Nov09               *
      *                 BUG25878           Date 03Sep09               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26779 - Change Request on GSART Table                     *
      *  BUG25878 - German Features Link Testing fix                  *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CF05(05)
     A N11                                  CF09(09)
     A                                      CF12(12)
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(26)
      *
     A          R ER000180F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2009')
      *
     A                                  1  7'ER000180DF'
     A            #1RUNA    R        O  1116REFFLD(RUNA)
     A                                  1125TIME
     A                                  2 44'Bundesbank Decision Table -
     A                                       - Transaction Types'
     A                                      DSPATR(HI)
      *
     A          R ER000180S1                SFL
     A  95                                  SFLNXTCHG
      *
     A            #1ACTN         1A  B  9  7TEXT('Action Code')
     A                                      DSPATR(HI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A            #1MMOD         2A  O  9 11TEXT('Module Id')
     A                                      DSPATR(HI)
     A            #1TRTY         2A  O  9 14TEXT('Transaction Type')
     A                                      DSPATR(HI)
     A            #1TRST         2A  O  9 18TEXT('Transaction Subtype')
     A                                      DSPATR(HI)
     A            #1TRCL         4A  O  9 22TEXT('Class')
     A                                      DSPATR(HI)
     A N23                                  DSPATR(ND)
     A            #1DESC        30A  O  9 30TEXT('Description')
     A                                      DSPATR(HI)
     A            #1GRI          1A  B  9 68TEXT('Extract to GRI')
     A                                      DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
      **Start of change BUG26779
     A**********  #1BUCL         2A  B  9 79TEXT('Business Classi-                          BUG26779
     A            #1BUCL         2A  B  9 77TEXT('Business Classi-
      **End of change BUG26779
     A                                           fication Class')
     A                                      DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
      **Start of change BUG26779
     A**********  #1BUSU         2A  B  9 94TEXT('Business Classi-
     A            #1BUSU         2A  B  9 86TEXT('Business Classi-
      **End of change BUG26779
     A                                           fication Sub-Class')
     A                                      DSPATR(HI)
     A N25N90                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
      **Start of change BUG26779
     A            #1BUTY         2A  B  9 96TEXT('Business Classi-
     A                                           fication Type')
     A                                      DSPATR(HI)
     A N26N90                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
      **End of change BUG26779
      *
     A          R ER000180C1                SFLCTL(ER000180S1)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 10'Transaction'
     A                                  4 65'Extract'
      **Start of change BUG26779
     A**********                        4 75'Business'                                      BUG26779
     A                                  4 74'Business'
     A**********                        4 90'Business'                                      BUG26779
     A                                  4 85'Classification'
      **End of change BUG26779
     A                                  5 10'Mod'
     A                                  5 14'Type'
     A  23                              5 22'Class'
      **End of change BUG26779
     A                                  5 30'Description'
     A                                  5 65'to GRI'
      **Start of change BUG26779
     A**********                        5 75'Classific.'                                    BUG26779
     A                                  5 74'Class'
     A**********                        5 90'Classific.'                                    BUG26779
     A                                  5 83'Sub-Class'
     A                                  5 95'Type'
      **End of change BUG26779
      **Start of change BUG25878
      **Start of change BUG26779
     A**********                        6  6'Opt'                                           BUG26779
     A N90                              6  6'Opt'
      **End of change BUG26779
      **End of change BUG25878
     A                                  6 14'Stype'
      **Start of change BUG26779
     A**********                        6 75'Class'                                         BUG26779
     A**********                        6 90'Sub-Class'                                     BUG26779
      **End of change BUG26779
     A            #1KMOD         2A  B  7 11TEXT('Module Id')
     A                                      DSPATR(HI PC)
     A            #1KTYP         2A  B  7 14TEXT('Type')
     A                                      DSPATR(HI PC)
     A            #1KSTP         2A  B  7 18TEXT('Stype')
     A                                      DSPATR(HI PC)
     A            #1KCLS         4A  B  7 22TEXT('Class')
     A                                      DSPATR(HI PC)
     A N23                                  DSPATR(PR ND)
     A            #1KDES        30A  B  7 30TEXT('Description')
     A                                      DSPATR(HI PC)
     A            #1KGRI         1A  B  7 68TEXT('Extract to GRI')
     A                                      DSPATR(HI PC)
      **Start of change BUG26779
     A**********  #1KBUC         2A  B  7 79TEXT('Business Classif-                         BUG26779
     A**********                                 ication Class')                            BUG26779
     A            #1KBUC         2A  B  7 77TEXT('Business Classif-
     A                                           ication Class')
      **End of change BUG26779
     A                                      DSPATR(HI PC)
      **Start of change BUG26779
     A**********  #1KBUS         2A  B  7 94TEXT('Business Classif-                         BUG26779
     A**********                                 ication Sub-Class')                        BUG26779
     A            #1KBUS         2A  B  7 86TEXT('Business Classif-
     A                                           ication Sub-Class')
     A                                      DSPATR(HI PC)
     A            #1KBCT         2A  B  7 96
      **End of change BUG26779
     A                                      DSPATR(HI PC)
      *
     A          R ER000180S2                SFL
     A  95                                  SFLNXTCHG
      *
     A            #2MMOD         2A  B  8 11TEXT('Module Id')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A            #2TRTY         2A  B  8 14TEXT('Transaction Type')
     A                                      DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A            #2TRST         2A  B  8 18TEXT('Transaction Subtype')
     A                                      DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A            #2TRCL         4A  B  8 22TEXT('Class')
     A                                      DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A N23                                  DSPATR(PR ND)
     A            #2DESC        30A  O  8 30TEXT('Description')
     A                                      DSPATR(HI)
     A            #2GRI          1A  B  8 68TEXT('Extract to GRI')
     A                                      DSPATR(HI)
     A N35                                  DSPATR(UL)
     A  35                                  DSPATR(RI PC)
      **Start of change BUG26779
     A**********  #2BUCL         2A  B  8 79TEXT('Business Classifi-                        BUG26779
     A            #2BUCL         2A  B  8 77TEXT('Business Classifi-
      **End of change BUG26779
     A                                           cation Class')
     A                                      DSPATR(HI)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(RI PC)
      **Start of change BUG26779
     A**********  #2BUSU         2A  B  8 94TEXT('Business Classifi-                        BUG26779
     A            #2BUSU         2A  B  8 86TEXT('Business Classifi-
      **End of change BUG26779
     A                                           cation Sub-Class')
     A                                      DSPATR(HI)
     A N37                                  DSPATR(UL)
     A  37                                  DSPATR(RI PC)
      **Start of change BUG26779
     A            #2BUTY         2A  B  8 96TEXT('Business Classifi-
     A                                           cation Type')
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI PC)
      **End of change BUG26779
      *
     A          R ER000180C2                SFLCTL(ER000180S2)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #2SFRN         4S 0H      SFLRCDNBR
      *
     A                                  4 10'Transaction'
     A                                  4 65'Extract'
      **Start of change BUG26779
     A**********                        4 75'Business'                                      BUG26779
     A                                  4 74'Business'
     A**********                        4 90'Business'                                      BUG26779
     A                                  4 85'Classification'
      **End of change BUG26779
     A                                  5 10'Mod'
     A                                  5 14'Type'
     A  23                              5 22'Class'
     A                                  5 30'Description'
     A                                  5 65'to GRI'
      **Start of change BUG26779
     A**********                        5 75'Classific.'                                    BUG26779
     A                                  5 74'Class'
     A**********                        5 90'Classific.'                                    BUG26779
     A                                  5 83'Sub-Class'
     A                                  5 95'Type'
      **End of change BUG26779
     A                                  6 14'Stype'
      **Start of change BUG26779
     A**********                        6 75'Class'                                         BUG26779
     A**********                        6 90'Sub-Class'                                     BUG26779
      **End of change BUG26779
      *
     A          R ER000180S3                TEXT('Program Messages')
     A                                      SFL
     A                                      SFLMSGRCD(26)
     A            #3MSGK                    SFLMSGKEY
     A            #3PGMQ                    SFLPGMQ
      *
     A          R ER000180C3                TEXT('Program Messages')
     A                                      SFLCTL(ER000180S3)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #3PGMQ                    SFLPGMQ
      *
     A          R ER000180F2                OVERLAY
     A N11N90                          25  2'D=Delete    F3=Main menu    +
     A                                      F5=Refresh    F9=Add Mode    +
     A                                      F12=Previous'
     A  11                             25  2'F3=Main menu    F5=Refresh    +
     A                                      F12=Previous'
     A  90                             25  2'F3=Main menu    F12=Previous'
