     A********************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas N/A Purchased Revaluation Maintenance DF')
      *****************************************************************
     A*                                                                  *
     A*        MIDAS/ABS - EUROPEAN REPORTING                            *
     A*                                                                  *
     A*        ER0010DF - N/A PURCHASED REVALUATION MAINTENANCE - DSPF   *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend no. 000000             Date 00/00/00              *
     A*                                                                  *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      HELP
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      CA12(12 'Cancel')
     A          R ER0010S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *-------------------------------------------------------------------------
     A          R ER0010C0                  SFLCTL(ER0010S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  30                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A                                 23  4'F3=Exit'
     A                                      DSPATR(HI)
     A                                 23 20'F5=Refresh'
     A                                      DSPATR(HI)
     A                                 23 39'F12=Cancel'
     A                                      DSPATR(HI)
     A          R ER0010S1                  SFL
     A  63                                  SFLNXTCHG
     A            DSDLNO         6  0O  7  4TEXT('DEAL NUMBER')
     A            DSNARA        20A  O  7 15TEXT('DEAL NARRATIVE')
     A            DSFAVA        13A  O  8  7TEXT('FACE VALUE')
     A            DSPUPR        11  7O  8 23TEXT('PURCHASE PRICE')
     A                                      EDTWRD('   0.       ')
     A            DSCLPR        11Y 7B  8 41TEXT('CLOTURE PRICE IN %')
     A                                      EDTWRD('   0.       -')
     A  31                                  DSPATR(PR)
     A N31                                  DSPATR(UL)
     A            DSPORE         1A  B  8 58TEXT('POST REVALUATION')
     A  31                                  DSPATR(PR)
     A N31                                  DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A            DSREAM        18A  O  8 60TEXT('REVALUATION AMOUNT')
     A            DSAMTO        13  0H      TEXT('CONVERTED AMOUNT')
     A          R ER0010C1                  SFLCTL(ER0010S1)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A N30                                  ROLLUP(25)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  80                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  30                                  SFLEND
     A            RRN            3S 0H      SFLRCDNBR
     A                                  1  2'ER0010DF'
     A                                      DSPATR(HI)
     A                                  1 73TIME
     A                                      DSPATR(HI)
     A            BJMRDT         7      1 65
     A                                      DSPATR(HI)
     A                                  1 20'N/A purchased revaluation maintena-
     A                                      nce'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DHDLNO         6Y 0B  5  4
     A                                      EDTWRD('      ')
     A                                      DSPATR(UL)
     A                                      CHECK(MF)
     A                                  3  4'Number'
     A                                      DSPATR(HI)
     A            DHNARA        20A  B  5 15
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
     A                                  3 15'Narrative'
     A                                      DSPATR(HI)
     A                                  4  9'Face value'
     A                                      DSPATR(HI)
     A                                  4 23'Purchase Price'
     A                                      DSPATR(HI)
     A                                  3 43'Cloture'
     A                                      DSPATR(HI)
     A                                  3 56'Post'
     A                                      DSPATR(HI)
     A                                  3 69'Revaluation'
     A                                      DSPATR(HI)
     A                                  4 42'Price in %'
     A                                      DSPATR(HI)
     A                                  4 56'Reval'
     A                                      DSPATR(HI)
     A                                  4 71'Amount'
     A                                      DSPATR(HI)
