      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas European Reporting - Decision Table')
     A********************************************************************
     A*                                                                  *
     A*        MIDAS/ABS - EUROPEAN REPORTING                            *
     A*                                                                  *
     A*        ER0020DF - DECISION TABLE                                 *
     A*                                                                  *
      *  (c) Finastra International Limited 2005                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG10111R          Date 26Jan06               *
      *                 BUG10111           Date 25Jan06               *
      *                 CER001             Date 25Apr05               *
      *                 000000             Date 00Xxx00               *
     A*                                                                  *
     A********************************************************************
     A*
      *  MD046248 - Finastra Rebranding                               *
      *  BUG10111R-Display error message                              *
      *  BUG10111-Display error message - cannot process subfile      *
      *           request                                             *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A          R ER0020S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *-------------------------------------------------------------------------
     A          R ER0020C0                  SFLCTL(ER0020S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  30                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A                                 23  2'X=Select'
     A                                      DSPATR(HI)
     A                                 23 12'F3=Main Menu'
     A                                      DSPATR(HI)
     A                                 23 26'F5=Refresh'
     A                                      DSPATR(HI)
     A          R ER0020S1                  SFL
     A            DSSELE         1A  B 17  4TEXT('SELECTION')
     A                                      VALUES('X' ' ')
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A            DSCTRY         2A  O 17 11TEXT('COUNTRY')
     A            DSFAVA        35A  O 17 22TEXT('NARRATIVE')
     A            DSCCTR         2A  H      TEXT('OUR COUNTRY CODE')
     A          R ER0020C1                  SFLCTL(ER0020S1)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
      **Start of change BUG10111R
      **Start*of*change*BUG10111****                                                       BUG10111R
     A**********                            SFLINZ                                         BUG10111R
      **End*of*change*BUG10111******                                                       BUB10111R
      **End of change BUG10111R
     A N30                                  ROLLUP(25)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  80                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  30                                  SFLEND
     A            RRN            3S 0H      SFLRCDNBR
     A                                  1  2'ER0020DF'
     A                                  1 73TIME
     A            BJMRDT         7      1 65
     A                                  1 30'Decision Table'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 10'Code'
     A                                      DSPATR(HI)
     A                                  4 22'Description'
     A                                      DSPATR(HI)
     A            MODE           7A     3 57
     A                                      DSPATR(HI)
     A            DHACOD         1A  B  6  4
     A                                      VALUES('X' ' ')
     A N20N15                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  15                                  DSPATR(PR)
     A                                  6 10'ACOD'
     A                                  6 22'Account codes table'
     A            DHTRNT         1A  B  7  4
     A                                      VALUES('X' ' ')
     A N20N15                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  15                                  DSPATR(PR)
     A                                  7 10'TRNT'
     A                                  7 22'Transaction types table'
     A                                 15  9'Country'
     A                                      DSPATR(HI)
     A                                 15 22'Narrative'
     A                                      DSPATR(HI)
