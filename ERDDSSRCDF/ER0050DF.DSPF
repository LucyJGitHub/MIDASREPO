      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Book code/Investment type - DSPF')
     A*****************************************************************
     A*                                                               *
     A*   MIDAS/ABS  EUROPEAN REPORTING                               *
     A*                                                               *
     A*   ER0050DF - BOOK CODE / INVESTMENT TYPES MAINTENANCE         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     A*                                                               *
     A*================================================================
     A*
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ERL370 TLI         Date 02Sep93               *
     A*
     A*================================================================
     A*
      *  CER001 - LUX Upgrade to MidasPlus                            *
     A*   ERL370 - BOOK CODE / INVESTMENT TYPE DEVELOPMENT            *
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R #SFLRCD
     A                                      SFL
     A                                      TEXT('Subfile record.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(87 'Protect non-key fields.')
     A                                      INDTXT(88 'Protect key fields.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      INDTXT(33 'Invalid #1SEL')
     A                                      INDTXT(34 'Invalid #1ATCD')
     A                                      INDTXT(35 'Invalid #1AUCD')
     A                                      INDTXT(36 'Invalid #1A1TX')
     A                                      INDTXT(37 'Invalid #1CMNB')
     A  84                                  SFLNXTCHG
     A            #1DBRC        47A  H      TEXT('Existing record fields')
     A            #1LCD          5S 0H      TEXT('Last Change Date')
     A            #1TYLC         1A  H      TEXT('Type of Last Change')
     A            #1SEL          1A  B  8  2VALUES('D')
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  79N33                               DSPATR(ND)
     A N79N33                               DSPATR(UL)
     A N79N33                               DSPATR(HI)
     A            #1BOKC         2A  B  8 11TEXT('Book Code')
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N88N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1INVT         3A  B  8 27TEXT('Investment Type')
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N88N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A            #1TIFA         1A  B  8 47TEXT('Trading/Investment/Fixed')
     A  87                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N87N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #1EVAM         1A  B  8 74TEXT('Evaluation Method')
     A                                      CHECK(RZ)
     A  78                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N78N37                               DSPATR(UL)
     A N37                                  DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A                                      INDTXT(89 'ADD mode.')
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2ATCD')
     A                                      SETOF(32 'Invalid: #2AUCD')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N25N80 81                            SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A            ##PGM         10A  O  1  2TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  1 49TEXT('*JOB')
     A N25                                  OVRDTA
     A            ##MRDT         7A  O  1 62TEXT('midas rundate')
     A N25                                  OVRDTA
     A            ##TME          6Y 0O  1 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  2 20'BOOK CODE / INVESTMENT TYPE MAINTE-
     A                                      NANCE'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  4 10'Book'
     A                                  4 26'Invest.'
     A                                  4 40'Trading/Investment'
     A                                  4 70'Evaluation'
     A                                  5 10'code'
     A                                  5 27'type'
     A                                  5 43'Fixed assets'
     A                                  5 72'Method'
     A N89        #2BOKC         2A  B  6 11TEXT('Book Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A N89        #2INVT         3A  B  6 27TEXT('Investment type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4 54'Resident'
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - BIS')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
