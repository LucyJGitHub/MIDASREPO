      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Save Paramaters Maintenance Screen')
     A*****************************************************************
     A*                                                               *
     A*  MIDAS/ABS STANDING DATA MODULE                               *
     A*                                                               *
     A*  ER1740FM   SAVE PARAMETERS MAINTENANCE                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     A*  NEW DSPF CREATED FRO 50314 ADI     DATE 28MAY93              *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ------             Date --XXX--               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A          R ER1740F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      CF03
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1 15'European Reporting Save Parameters'
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 49' Maintenance'
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            WSID           3A  O  1 57
     A                                      OVRATR
     A            RUNA           7A  O  1 62
     A                                      OVRATR
     A                                  1 70TIME
     A                                      OVRATR
     A                                  4  6'Medium for first backup'
     A                                      OVRATR
     A            BOBD           4A  B  4 40DSPATR(HI)
     A                                      OVRATR
     A N71                                  DSPATR(UL)
     A  71                                  DSPATR(RI PC)
     A                                  4 50'Valid Values: SAVF or TAPE'
     A                                      OVRATR
     A                                  5  6'Device for first backup'
     A                                      OVRATR
     A            BOBN          10A  B  5 40DSPATR(HI)
     A                                      OVRATR
     A N72                                  DSPATR(UL)
     A  72                                  DSPATR(RI PC)
     A                                  7  6'Medium for second backup'
     A                                      OVRATR
     A            INCD           4A  B  7 40DSPATR(HI)
     A                                      OVRATR
     A N73                                  DSPATR(UL)
     A  73                                  DSPATR(RI PC)
     A**                                    VALUES('SAVF' 'TAPE')
     A                                  7 50'Valid Values: SAVF or TAPE'
     A                                      OVRATR
     A                                  8  6'Device for second backup'
     A                                      OVRATR
     A            INCN          10A  B  8 40DSPATR(HI)
     A N74                                  DSPATR(UL)
     A  74                                  DSPATR(RI PC)
     A                                 23  2'F3=Exit'
     A                                      OVRATR
     A                                      COLOR(BLU)
      *-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  75
     AON75                                  SFLEND
     A            WWPGM                     SFLPGMQ
