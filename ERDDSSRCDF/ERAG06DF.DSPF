      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AGDL Customers Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/ERAG06DF - AGDL Customer maintenance                    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX010  *CREATE    Date 14Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX010 - AGDL reporting statistiques sur les dépôts garantis *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      **Start of Change CER001
     A**********                            MSGLOC(24)                                        CER001
      **End of Change CER001
     A                                      HELP
     A                                      HLPPNLGRP('WHOLE' ERAG06HP)
     A                                      HLPTITLE('HELP')
     A****************************************************************
     A* HEADER FORMAT                                                *
     A****************************************************************
     A          R ERAG06F0                  TEXT('(c) Finastra International +
     A                                      Limited 2005')
     A                                      CA03
     A                                      CA05
      *
      ***  Help for positionning the subfile
     A          H                           HLPPNLGRP('SFLP' ERAG06HP)
     A                                      HLPARA(08 008 08 013)
      *
      ***  Help Tag for Action Code.
     A          H                           HLPPNLGRP('TANN' ERAG06HP)
     A                                      HLPARA(11 002 11 002)
      ***  Help Tag for Linked customer
      *
     A          H                           HLPPNLGRP('LINK' ERAG06HP)
     A                                      HLPARA(11 060 11 065)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'ERAG06DF'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 25'Customers Maintenance for the AGDL'
     A                                      DSPATR(UL HI)
      *
     A****************************************************************
     A* RECORD SUBFILE FOR AGDLCUV0                                  *
     A****************************************************************
     A          R ERAG06F1
     A                                      SFL
     A  80                                  SFLNXTCHG
     A            WTANN          1A  B 11 02
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A            WCUST          6A  O 11 08
     A                                      DSPATR(HI)
     A            WNAME         20A  O 11 19
     A                                      DSPATR(HI)
     A            WTOWN         10A  O 11 44
     A                                      DSPATR(HI)
     A            WLINK          6A  B 11 60
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A****************************************************************
     A* CONTROL SUBFILE FOR AGDLCUV0                                 *
     A****************************************************************
     A          R ERAG06F2
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A N35                                  ROLLUP(36)
     A                                      SFLCTL(ERAG06F1)
     A  20                                  SFLDSPCTL
     A  21                                  SFLDSP
     A                                      SFLPAG(11)
     A                                      SFLSIZ(12)
     A  23                                  SFLCLR
     A  35                                  SFLEND(*MORE)
     A                                 06 08'Customer'
     A                                 06 19'Name'
     A                                 06 44'Town'
     A                                 06 60'Linked'
     A                                 07 08'Number'
     A                                 07 60'Customer'
     A            ZCUS           6A  B 08 08
     A N83                                  DSPATR(UL HI)
     A  71
     AO 83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A            ZA78          78A  O 09 02
     A            RANGF1         4S 0H      SFLRCDNBR(CURSOR)
     A****************************************************************
     A* RECORD SUBFILE FOR MESSAGES                                  *
     A****************************************************************
     A          R ERAG06F3
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A****************************************************************
     A* CONTROL SUBFILE FOR MESSAGES                                 *
     A****************************************************************
     A          R ERAG06F4
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A                                      SFLCTL(ERAG06F3)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N65                                  SFLEND
     A            PGMQ                      SFLPGMQ
     A                                 23  3'F3=Exit   F5=Refresh   D=Delete'
     A****************************************************************
     A* WINDOW                                                       *
     A****************************************************************
     A          R ERAG06F5
     A                                      CA12
     A                                      WINDOW(17 4 5 72)
     A                                      WDWBORDER((*COLOR BLU))
     A                                 03 12'Press Enter to confirm deletion +
     A                                      or F12 to cancel'
     A                                      COLOR(WHT)
