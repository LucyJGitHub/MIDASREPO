     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas AGDL Deposit file maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module                                *
     A*                                                               *
     A*  DSPF/ERAG12DF - AGDL deposit file maintenance                *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2005            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CER001             Date 25Apr05               *
     A*  Prev Amend No. 166702             Date 02Sep99               *
     A*                 ULX010  *CREATE    Date 15Jul99               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CER001 - LUX Upgrade to MidasPlus                            *
     A*  166702 - Add customer number of the Deal                     *
     A*  ULX010 - AGDL reporting statistiques sur les dépôts garantis *
     A*           Change Control                                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
      **Start of Change CER001
     A**********                            MSGLOC(24)                                        CER001
      **End of Change CER001
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      HELP
     A                                      HLPPNLGRP(WHOLE ERAG12HP)
     A                                      HLPTITLE('HELP')
     A****************************************************************
     A* HEADER FORMAT                                                *
     A****************************************************************
     A          R ERAG12F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2005')
     A                                      CA03
     A                                      CA05
      *
      ***  Help Tag for Action Code.
     A          H                           HLPPNLGRP('TANN' ERAG12HP)
     A                                      HLPARA(11 010 11 010)
      *
      ***  Help Tag for Number participants
     A          H                           HLPPNLGRP('PART' ERAG12HP)
     A                                      HLPARA(11 027 11 028)
      ***  Help Tag for Linked customer 1
      *
     A          H                           HLPPNLGRP('LINK' ERAG12HP)
     A                                      HLPARA(11 034 11 039)
      ***  Help Tag for Linked customer 2
      *
     A          H                           HLPPNLGRP('LINK' ERAG12HP)
     A                                      HLPARA(11 045 11 050)
      ***  Help Tag for Linked customer 3
      *
     A          H                           HLPPNLGRP('LINK' ERAG12HP)
     A                                      HLPARA(11 056 11 061)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'ERAG12DF'
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 25'Deposit Maintenance for the AGDL'
     A                                      DSPATR(UL HI)
     A*
     A****************************************************************
     A* RECORD SUBFILE FOR AGDLDLV0                                  *
     A****************************************************************
     A          R ERAG12F1                  SFL
     A  80                                  SFLNXTCHG
     A            WTANN          1A  B 11  4
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A            WPART          2A  B 11 27
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A                                      CHECK(FE)
     A                                      CHECK(RZ)
     A            WLIN1          6A  B 11 34
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(UL)
     A N42                                  DSPATR(HI)
     A            WLIN2          6A  B 11 45
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43                                  DSPATR(UL)
     A N43                                  DSPATR(HI)
     A            WLIN3          6A  B 11 56
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N44                                  DSPATR(UL)
     A N44                                  DSPATR(HI)
     A            WDEAL          6A  O 11  9DSPATR(HI)
     A****** Begin 166701 *****
     A            WCNUM          6   O 11 17DSPATR(HI)
     A****** End   166701 *****
     A****************************************************************
     A* CONTROL SUBFILE FOR AGDLDLV0                                 *
     A****************************************************************
     A          R ERAG12F2                  SFLCTL(ERAG12F1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A N35                                  ROLLUP(36)
     A  20                                  SFLDSPCTL
     A  21                                  SFLDSP
     A  23                                  SFLCLR
     A  35                                  SFLEND(*MORE)
     A                                  6 27'Nbr'
     A                                  6 45'Linked'
     A                                  7 27'Part'
     A                                  7 44'Customers'
     A            ZCPT           6A  B  8  9
     A N83                                  DSPATR(UL)
     A N83                                  DSPATR(HI)
     A  71
     AO 83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A            ZA78          78A  O  9  2
     A            RANGF1         4S 0H      SFLRCDNBR(CURSOR)
     A                                  7 34'<--------'
     A                                  7 55'------>'
     A                                  6  9'Deal'
     A                                  7  9'Number'
     A                                  6  4'Sel'
     A                                  6 17'Customer'
     A                                  7 17'Number'
     A****************************************************************
     A* RECORD SUBFILE FOR MESSAGES                                  *
     A****************************************************************
     A          R ERAG12F3
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A****************************************************************
     A* CONTROL SUBFILE FOR MESSAGES                                 *
     A****************************************************************
     A          R ERAG12F4
     A                                      CA03
     A                                      CA05
     A                                      OVERLAY
     A                                      SFLCTL(ERAG12F3)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N65                                  SFLEND
     A            PGMQ                      SFLPGMQ
     A                                 23  3'F3=Exit   F5=Refresh   D=Delete'
     A****************************************************************
     A* WINDOW                                                       *
     A****************************************************************
     A          R ERAG12F5
     A                                      CA12
     A                                      WINDOW(17 4 5 72)
     A                                      WDWBORDER((*COLOR BLU))
     A                                 03 12'Press Enter to confirm deletion +
     A                                      or F12 to cancel'
     A                                      COLOR(WHT)
