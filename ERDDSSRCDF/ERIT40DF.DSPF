      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Decision table Transaction types')            *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  ERIT40DF  - ER Italy - Decision table Transaction types      *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. MMI033             Date 25Mar99               *
      *                 UIT021             Date 13JAN97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  MMI033 : Midas Italy Puma/2 Extract Change 3                 *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      HELP
     A                                      HLPDOC(H025@000 ERITINS ABSENG/ITL)
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A N15                                  CA09(09 'Add Mode')
     A                                      CA12(12 'Previous')
      *-------------------------------------------------------------------------
     A          R ERIT40S0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *-------------------------------------------------------------------------
     A          R ERIT40C0                  SFLCTL(ERIT40S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  30
     AON30                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A N16                             23  2'D=Delete'
     A N16                                  DSPATR(HI)
     A  15                                  DSPATR(ND)
     A  16                             23  2'F9=Change'
     A  16                                  DSPATR(HI)
     A  15                                  DSPATR(ND)
     A                                 23 13'F3=Main Menu'
     A                                      DSPATR(HI)
     A                                 23 27'F5=Refresh'
     A                                      DSPATR(HI)
     A N16                             23 39'F9=Add Mode'
     A N16                                  DSPATR(HI)
     A  15                                  DSPATR(ND)
     A N16                             23 52'F12=Previous'
     A N16                                  DSPATR(HI)
     A  16                             23 39'F12=Previous'
     A  16                                  DSPATR(HI)
      *-------------------------------------------------------------------------
     A          R ERIT40S1                  SFL
     A  63                                  SFLNXTCHG
     A            DSSELE         1A  B  8  2TEXT('SELECTION')
     A  20                                  DSPATR(RI PC)
     A N15N20                               DSPATR(UL)
     A  16
     AO 15                                  DSPATR(PR)
     A  16                                  DSPATR(ND)
     A            DSMOD          2A  B  8  4TEXT('TRANSACTION MOD.')
     A  15
     AON16                                  DSPATR(PR)
     A  16N15                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A            DSTYPE         8A  B  8  7TEXT('TRANSACTION TYPE')
     A  15
     AON16                                  DSPATR(PR)
     A  16N15                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A            DSNARA        25A  O  8 16TEXT('NARRATIVE')
     A            DSDEBI         7A  B  8 42TEXT('DEBIT')
     A                                      CHECK(MF)
     A  24                                  DSPATR(RI PC)
     A N15N24                               DSPATR(UL)
     A  15                                  DSPATR(PR)
      *
      ** Start MMI033
     A            DSDEBE         2A  B  8 50TEXT('EFF.DEBIT')
     A  28                                  DSPATR(RI PC)
     A  15                                  DSPATR(PR)
     A N15N28                               DSPATR(UL)
      ** End   MMI033
      *
     A            DSCRED         7A  B  8 54TEXT('CREDIT')
     A                                      CHECK(MF)
     A  25                                  DSPATR(RI PC)
     A N15N25                               DSPATR(UL)
     A  15                                  DSPATR(PR)
      *
      ** Start MMI033
     A            DSCREE         2A  B  8 62TEXT('EFF.DEBIT')
     A  29                                  DSPATR(RI PC)
     A N15N29                               DSPATR(UL)
     A  15                                  DSPATR(PR)
      ** End   MMI033
      *
     A            DSCPDB         7A  B  8 65TEXT('COMPL. DEBIT')
     A                                      CHECK(MF)
     A  26                                  DSPATR(RI PC)
     A N15N26                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A            DSCPCR         7A  B  8 73TEXT('COMPL. CREDIT')
     A                                      CHECK(MF)
     A  27                                  DSPATR(RI PC)
     A N15N27                               DSPATR(UL)
     A  15                                  DSPATR(PR)
      *-------------------------------------------------------------------------
     A          R ERIT40C1                  SFLCTL(ERIT40S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N30                                  ROLLUP(35)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  80                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  30                                  SFLEND
     A          H                           HLPDOC(H025@001 ERITINS ABSENG/ITL)
     A                                      HLPARA(8 002 21 002)
     A          H                           HLPDOC(H025@002 ERITINS ABSENG/ITL)
     A                                      HLPARA(8 004 21 005)
     A          H                           HLPDOC(H025@003 ERITINS ABSENG/ITL)
     A                                      HLPARA(8 007 21 014)
     A          H                           HLPDOC(H025@004 ERITINS ABSENG/ITL)
     A                                      HLPARA(8 047 21 053)
     A          H                           HLPDOC(H025@005 ERITINS ABSENG/ITL)
     A                                      HLPARA(8 055 21 061)
     A          H                           HLPDOC(H025@006 ERITINS ABSENG/ITL)
     A                                      HLPARA(8 063 21 069)
     A          H                           HLPDOC(H025@007 ERITINS ABSENG/ITL)
     A                                      HLPARA(8 071 21 077)
     A            RRN            4S 0H      SFLRCDNBR
     A                                  1  2'ERIT40DF'
     A                                  1 73TIME
     A            BJMRDT         7A  O  1 65
     A                                  2 19'Decision Table - Transactions type-
     A                                      s -'
     A                                      DSPATR(HI)
     A            PARCTR         2A  O  2 57DSPATR(HI)
     A                                  4  4'Transaction'
     A                                      DSPATR(HI)
     A                                  4 46'Line Items'
     A                                      DSPATR(HI)
     A                                  4 65'Compl. Items'
     A                                      DSPATR(HI)
     A                                  5  4'Mod.'
     A                                      DSPATR(HI)
     A                                  5  9'Type'
     A                                      DSPATR(HI)
     A                                  5 16'Description'
     A                                      DSPATR(HI)
     A                                  5 42'Debit'
     A                                      DSPATR(HI)
     A                                  5 54'Credit'
     A                                      DSPATR(HI)
     A                                  5 65'Debit'
     A                                      DSPATR(HI)
     A                                  5 73'Credit'
     A                                      DSPATR(HI)
     A            DHMOD          2A  B  6  4
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
     A            DHTYPE         8A  B  6  7
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
     A            DHNARA        25A  B  6 16
     A                                      CHECK(LC)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
     A            DHDEBI         7A  B  6 42
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
      *
      ** Start MMI033
     A            DHDEBE         2A  B  6 50
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
      ** End   MMI033
      *
     A            DHCRED         7A  B  6 54
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
      *
      ** Start MMI033
     A            DHCREE         2A  B  6 62
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
      ** End   MMI033
      *
     A            DHCPDB         7A  B  6 65
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
     A            DHCPCR         7A  B  6 73
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  16                                  DSPATR(PR ND)
