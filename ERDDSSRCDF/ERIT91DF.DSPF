      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Italy I.C.D. - display file')                 *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  ERIT91DF - ER Italy I.C.D. - display file                    *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. UIT021  *Create    Date 13JAN97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      HELP
     A                                      HLPDOC(H019@000 ERITINS ABSENG/ITL)
     A                                      CA03(03 'Exit')
     A N50                                  CA05(05 'Refresh')
     A                                      CA12(12 'Cancel')
     A          R ERIT91FM
     A                                      OVERLAY
     A          H                           HLPDOC(H019@001 ERITINS ABSENG/ITL)
     A                                      HLPARA(10 004 10 045)
     A          H                           HLPDOC(H019@002 ERITINS ABSENG/ITL)
     A                                      HLPARA(12 004 12 046)
     A          H                           HLPDOC(H019@003 ERITINS ABSENG/ITL)
     A                                      HLPARA(14 004 14 046)
     A                                  1  2'ERIT91DF'
     A                                  1 72TIME
     A            BJMRDT         7      1 62
     A                                  2 25'European Reporting'
     A                                      DSPATR(HI)
     A            DDUCCD         2      2 44
     A                                      DSPATR(HI)
     A                                  2 47'- I.C.D.'
     A                                      DSPATR(HI)
     A*
     A  71                             10  4'LE Repayments Management (Y/N)'
     A  71        DDLERP         1A  B 10 45
     A  50                                  DSPATR(PR)
     A N60N50                               DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A*
     A                                 12  4'Collateral Reviewal Days in Advanc-
     A                                      e'
     A            DDCORD         2A  B 12 45
     A  50                                  DSPATR(PR)
     A N61N50                               DSPATR(UL)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A*
     A*
     A                                 14  4'Provisions Reviewal Days in Advanc-
     A                                      e'
     A            DDPRRV         2A  B 14 45
     A  50                                  DSPATR(PR)
     A N62N50                               DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A*
     A          R ERIT91S0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *-------------------------------------------------------------------------
     A          R ERIT91C0                  SFLCTL(ERIT91S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N30                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
     A N50                             23  2'F3=Main Menu   F5=Refresh   +
     A                                      F12=Previous'
     A  50                             23  2'F3=Main Menu   F12=Previous'
      *
