      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ABS ER Guarantees/Collateral/Provisions Main')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  DSPF/ERL910DF - Guarantees/Collateral/Provisions Maintenance *
      *                  Prompt                                       *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX012             Date 28Jul98               *
      *                 ULX012             Date 28Jul98               *
      *                 ULX004             Date 02JAN97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  FS0001 - Changes to allow myultilanguage compilation         *
      *  ULX012 - Guarantees and Collateral Management enhancements   *
      *  ULX004 - CAPITAL ADEQUACY                                    *
      *                                                               *
     A*****************************************************************
     A*
     A**Start of change CER001
     A**********                            DSPSIZ(24 80 *DS3)                                CER001
     A                                      DSPSIZ(*DS4)
     A**End of change CER001
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03(03)
     A                                      PRINT(*LIBL/MPRINT)
     A*
     A*----------------------------------------------------------------
     A*  Format ERL910S0 : Subfile Record Error Message
     A*----------------------------------------------------------------
     A*
     A          R ERL910S0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A*  Format ERL910C0 : Subfile Control Error Message
     A*----------------------------------------------------------------
     A*
     A          R ERL910C0                  SFLCTL(ERL910S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  50
     AON50                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A*  Format ERL910C1 : Initial Screen
     A*----------------------------------------------------------------
     A*
     A          R ERL910C1                  TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
     A                                      OVERLAY
     A            DDPPGM        10A  O  1 13
     A            DDUSER        10A  O  1  2
     A                                  1 67'DATE'
     A            DDDATE         7A  O  1 73
     A                                  2 19'Guarantees/Collateral/Provisions M-
     A                                      aintenance'
     A                                      DSPATR(HI)
     A                                  5  4'Receiving Customer :'
     A            DDCUST        10A  B  5 26DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 23  4'F3=Exit'
     A                                  2 67'TIME'
     A            DDTIME         6Y 0O  2 73EDTWRD('  :  :  ')
     A            DDSWID        10A  O  2  2
     A                                 23 15'? = Select customer'
     A*
     A*----------------------------------------------------------------
     A*  Format ERL910S2 : Subfile Record
     A*----------------------------------------------------------------
     A          R ERL910S2                  SFL
     A  78                                  SFLNXTCHG
     A            SFACTN         1A  B  9  2VALUES('A' 'E' 'D' ' ')
     A                                      CHECK(ER)
     A                                      DSPATR(HI)
     A            SFUTSI         1A  O  9  4DSPATR(HI)
     A**Start of change CER001
     A**********  SFUTSR        18A  O  9  6DSPATR(HI)                                        CER001
     A            SFUTSR        24A  O  9  6DSPATR(HI)
     A**End of change CER001
     A*>> ULX012 >>                                                       ULX012
     A*******     SFGSEQ         3A  O  9 25                              ULX012
     A*N66***                               DSPATR(HI)                    ULX012
     A**66***                               COLOR(RED)                    ULX012
     A**66***                               DSPATR(RI)                    ULX012
     A*******     SFSBYI         1A  O  9 29DSPATR(HI)                    ULX012
     A*******     SFSBYR        15A  O  9 31DSPATR(HI)                    ULX012
     A*******     SFGCCY         3A  O  9 47DSPATR(HI)                    ULX012
     A*******     SFGPER         3A  O  9 51DSPATR(HI)                    ULX012
     A*******     SFGAMT        14A  O  9 55DSPATR(HI)                    ULX012
     A*******     SFGVBY        10A  O  9 70DSPATR(HI)                    ULX012
     A**Start of change CER001
     A**********  SFINSA         1A  O  9 25DSPATR(HI)
     A            SFINSA         1A  O  9 31DSPATR(HI)                                        CER001
     A**End of change CER001
     A N62                                  DSPATR(ND)
     A**Start of change CER001
     A**********  SFGSEQ         3A  O  9 27                                                  CER001
     A            SFGSEQ         3A  O  9 33
     A**End of change CER001
     A N66                                  DSPATR(HI)
     A  66                                  COLOR(RED)
     A  66                                  DSPATR(RI)
     A**Start of change CER001
     A**********  SFSBYI         1A  O  9 31DSPATR(HI)                                        CER001
     A            SFSBYI         1A  O  9 37DSPATR(HI)
     A**********  SFSBYR        15A  O  9 33DSPATR(HI)                                        CER001
     A            SFSBYR        21A  O  9 39DSPATR(HI)
     A**********  SFGCCY         3A  O  9 49DSPATR(HI)                                        CER001
     A            SFGCCY         3A  O  9 61DSPATR(HI)
     A**********  SFGPER         3A  O  9 53DSPATR(HI)                                        CER001
     A            SFGPER         3A  O  9 65DSPATR(HI)
     A**********  SFGAMT        13A  O  9 57DSPATR(HI)                                        CER001
     A            SFGAMT        13A  O  9 69DSPATR(HI)
     A**********  SFGVBY        10A  O  9 71DSPATR(HI)                                        CER001
     A            SFGVBY        10A  O  9 83DSPATR(HI)
     A**End of change CER001
     A*<< ULX012 <<                                                       ULX012
     A            SFBRCA         3A  H
     A            SFRVEX         1A  H
     A*
     A*----------------------------------------------------------------
     A*  Format ERL910C2 : Subfile Control
     A*----------------------------------------------------------------
     A*
     A          R ERL910C2                  SFLCTL(ERL910S2)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A N61                                  SFLDSPCTL
     A N61                                  SFLDSP
     A  61                                  SFLCLR
     A  79                                  SFLEND(*MORE)
     A N79                                  ROLLUP(25)
     A                                      CA12(12)
     A                                      CA09(09)
     A                                      CA05(05)
     A            WWRRN          4S 0H      SFLRCDNBR
     A            DDPPGM        10A  O  1 14
     A            DDUSER        10A  O  1  3
     A                                  1 67'DATE'
     A            DDDATE         7A  O  1 73
     A                                  2 67'TIME'
     A            DDTIME         6Y 0O  2 73EDTWRD('  :  :  ')
     A                                  2 19'Guarantees/Collateral/Provisions M-
     A                                      aintenance'
     A                                      DSPATR(HI)
     A                                  5  4'Receiving Customer :'
     A            DDCUST        10A  H
     A            SVCSSN        10A  O  5 26DSPATR(HI)
     A            SVCRNM        20A  O  5 40DSPATR(HI)
     A            SVCRTN        10A  O  5 62DSPATR(HI)
     A*>> ULX012 >>                                                       ULX012
     A*******                           7  4'Used to Secure'              ULX012
     A*******                           7 25'Seq'                         ULX012
     A*******                           7 29'Secured by'                  ULX012
     A*******                           7 47'Ccy'                         ULX012
     A*******                           7 52'%'                           ULX012
     A*******                           7 55'Amount'                      ULX012
     A*******                           7 70'Given by'                    ULX012
     A                                  7  4'Used to Secure'
     A**Start of change CER001
     A**62******                        7 23'Gen'                                             CER001
     A  62                              7 29'Gen'
     A**********                        7 27'Seq'                                             CER001
     A                                  7 33'Seq'
     A**********                        7 31'Secured by'                                      CER001
     A                                  7 37'Secured by'
     A**********                        7 49'Ccy'                                             CER001
     A                                  7 61'Ccy'
     A**********                        7 54'%'                                               CER001
     A                                  7 66'%'
     A**********                        7 57'Amount'                                          CER001
     A                                  7 69'Amount'
     A**********                        7 71'Given by'                                        CER001
     A                                  7 83'Given by'
     A**End of change CER001
     A*<< ULX012 <<                                                       ULX012
     A            DDSWID        10A  O  2  3
     A*
     A*----------------------------------------------------------------
     A*  Format ERL910C3 : Amend Enquire or Delete Screen
     A*----------------------------------------------------------------
     A*
     A          R ERL910C3
     A                                      CA12(12)
     A  10                                  CA10(10)
     A                                      OVERLAY
     A            DDPPGM        10   O  1 13
     A            DDUSER        10A  O  1  2
     A                                  1 67'DATE'
     A            DDDATE         7A  O  1 73
     A                                  2 67'TIME'
     A            DDTIME         6Y 0O  2 73EDTWRD('  :  :  ')
     A                                  2 19'Guarantees/Collateral/Provisions M-
     A                                      aintenance'
     A                                      DSPATR(HI)
     A*****FS0001 begin
     A                                  3 68' EXPIRED '
     A                                      TEXT('Expired indicator')
     A********************************* 3 68TEXT('Expired indicator')
     A************************************* ' EXPIRED '
     A*****FS0001 end
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(BL)
     A N65                                  DSPATR(ND)
     A                                  5  4'Receiving customer........:'
     A            D3CSSN        10A  O  5 34DSPATR(HI)
     A            D3CRNM        20A  O  5 46DSPATR(HI)
     A            D3CRTN        10A  O  5 68DSPATR(HI)
     A                                  6  4'Used to secure............:'
     A            D3UTSI         1A  B  6 34DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N80N81                            DSPATR(UL)
     A  80
     AO 81                                  DSPATR(PR)
     A**Start of change CER001
     A**********  D3UTSR        18A  B  6 37DSPATR(HI)                                        CER001
     A            D3UTSR        24A  B  6 37DSPATR(HI)
     A**End of change CER001
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N80N81                            DSPATR(UL)
     A  80
     AO 81                                  DSPATR(PR)
     A                                  7  4'Secured by................:'
     A            D3SBYI         1A  B  7 34DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33N80N82                            DSPATR(UL)
     A  80
     AO 82                                  DSPATR(PR)
     A**Start of change CER001
     A**********  D3SBYR        15A  B  7 37DSPATR(HI)                                        CER001
     A            D3SBYR        21A  B  7 37DSPATR(HI)
     A**End of change CER001
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34N80N82                            DSPATR(UL)
     A  80
     AO 82                                  DSPATR(PR)
     A                                  8  4'Currency..................:'
     A            D3GCCY         3A  B  8 34DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35N80N83                            DSPATR(UL)
     A  80
     AO 83                                  DSPATR(PR)
     A                                  9  4'Guarantee percentage......:'
     A            D3GPER         3A  B  9 34DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36N80N84                            DSPATR(UL)
     A  80
     AO 84                                  DSPATR(PR)
     A                                 10  4'Guarantee amount..........:'
     A            D3GAMT        14A  B 10 34DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37N80N85                            DSPATR(UL)
     A  80
     AO 85                                  DSPATR(PR)
     A                                 11  4'Given by..................:'
     A            D3GVBY        10A  B 11 34DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38N80N86                            DSPATR(UL)
     A  80
     AO 86                                  DSPATR(PR)
     A                                 13  4'Guarantee code............:'
     A            D3GCOD         2A  B 13 34DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N39N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 14  4'Guarantee sequence........:'
     A            D3GSEQ         3A  B 14 34DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40N80N87                            DSPATR(UL)
     A  80
     AO 87                                  DSPATR(PR)
     A                                 16  4'Start date................:'
     A            D3STDT         6A  B 16 34DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 17  4'End date..................:'
     A            D3ENDT         6A  B 17 34DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 18  4'Review date...............:'
     A            D3RVDT         6A  B 18 34DSPATR(HI)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 18 44'Frequency..:'
     A            D3RVFQ         1A  B 18 58DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N44N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 18 63'Day no..:'
     A            D3RVDY         2A  B 18 75DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N45N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 19  4'Used to secure loans......:'
     A            D3UTSL         1A  B 19 34DSPATR(HI)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N47N80                               DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 20  4'Narrative.................:'
     A            D3NARR        30A  B 20 34DSPATR(HI)
     A                                      CHECK(LC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A                                 21  4'Additional comment........:'
     A            D3ACOM        30A  B 21 34DSPATR(HI)
     A                                      CHECK(LC)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N46N80N88                            DSPATR(UL)
     A  80
     AO 88                                  DSPATR(PR)
     A*****FS0001 begin
     A                                 22 34'ACTION AMEND NOT ALLOWED'
     A                                      TEXT('Expired Mode')
     A******************************** 22 34TEXT('Expired Mode')
     A************************************* 'ACTION AMEND NOT ALLOWED'
     A*****FS0001 end
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(BL)
     A N67                                  DSPATR(ND)
     A                                 23  4'F3=Exit   F12=Previous'
     A  10                             23 29'F10=Delete'
     A            DDSWID        10A  O  2  2
     A*
     A*----------------------------------------------------------------
     A*  Format ERL910C4 : End of Screen
     A*----------------------------------------------------------------
     A*
     A          R ERL910C4
     A                                      OVERLAY
     A                                 23  2'A=Amend D=Delete E=Enquire F3=E-
     A                                      xit F5=Refresh F9=Go to ''Add'' m-
     A                                      ode F12=Previous'
