      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ABS Guarantees/Collat./Prov. Select Customer')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  DSPF/ERL910WN - Guarantees/Collateral/Provisions Maintenance *
      *                  Select Customer                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX004  *CREATE    Date 02Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - CAPITAL ADEQUACY                                    *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(69)
     A          R DUMMY                     ASSUME
     A                                  1  2' '
     A*-------------------------------------------------------------------------
     A**  FORMAT WINDOW HEADER
     A*-------------------------------------------------------------------------
     A          R ERL910H
     A                                      WINDOW(2 20 19 50)
     A                                      TEXT('Screen Header')
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR HI))
     A                                      USRRSTDSP
     A                                  5  2'-----------------------------------
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                  2 15'Select Customer Number'
     A                                      DSPATR(HI)
     A                                  4 14'Cust.Nbr'
     A                                      DSPATR(HI)
     A                                  4 30'Customer Short Name'
     A                                      DSPATR(HI)
     A                                  4  3'Sel'
     A                                      DSPATR(HI)
     A*-------------------------------------------------------------------------
     A**  FORMAT WINDOW RECORD
     A*-------------------------------------------------------------------------
     A          R ERL910E                   SFL
     A                                      TEXT('Subfile Record Format')
     A            WNSELE         1A  B  6  4CHECK(ER)
     A                                      VALUES(' ' '1')
     A            WNCUST         6A  O  6 14
     A            WNCSSN        10A  O  6 30
     A*-------------------------------------------------------------------------
     A**  FORMAT WINDOW CONTROL
     A*-------------------------------------------------------------------------
     A          R ERL910C                   SFLCTL(ERL910E)
     A                                      SFLSIZ(1000)
     A                                      SFLPAG(0011)
     A                                      WINDOW(ERL910H)
     A                                      TEXT('Subfile Control Record')
     A                                      OVERLAY
     A N68N63                               SFLDSP
     A N68                                  SFLDSPCTL
     A  68                                  SFLCLR
     A                                      CA12(12)
     A            W001RG         4S 0H      SFLRCDNBR
     A*-------------------------------------------------------------------------
     A**  FORMAT WINDOW LOW
     A*-------------------------------------------------------------------------
     A          R ERL910L
     A                                      WINDOW(ERL910H)
     A                                      OVERLAY
     A                                 18 30'F12=Previous screen'
     A                                 18  4'1 to select'
