      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas TI Product Code/Type Combination Screen')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  DSPF/ERLU13DF - Midas TI Product Code/Type Combination Screen*
      *                  Maintenance                                  *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX024  *CREATE    Date 02Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX024 - Trade Innovation/CSSF Reporting Interface           *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A N15                                  CA09(09 'Add Mode')
      *
      *----------------------------------------*
      ***  Screen 1 Format for Subfile Details *
      *----------------------------------------*
      *
     A          R ERLU13S1                  TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Ltd. 2005')
     A                                      SFL
     A  63                                  SFLNXTCHG
     A            DSSELE         1A  B  8  4TEXT('SELECTION')
     A                                      DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N15N17N20                            DSPATR(UL)
     A  18                                  DSPATR(BL)
     A  16
     AO 15
     AO 17                                  DSPATR(PR)
     A  16                                  DSPATR(ND)
     A            DSTIPC         4A  B  8 11TEXT('TI Prod. Code')
     A                                      DSPATR(HI)
     A N15 16N21                            DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  15
     AON16                                  DSPATR(PR)
     A            DSTIPT         4A  B  8 20TEXT('CSSF TYPE')
     A                                      DSPATR(HI)
     A N15 16N22                            DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  15
     AON16                                  DSPATR(PR)
     A            DSTIPD        35A  B  8 29TEXT('Description')
     A                                      DSPATR(HI)
     A N15N17N23                            DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  15
     AO 17                                  DSPATR(PR)
     A                                      CHECK(LC)
      *
      *----------------------------------------*
      ***  Screen 1 Format for Subfile Control *
      *----------------------------------------*
      *
     A          R ERLU13C1                  SFLCTL(ERLU13S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N60                                  ROLLUP(35)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  80                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  60                                  SFLEND
      *
     A            SFLREC         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'ERLU13DF'
     A                                  2 22'TI Product Code / CSSF Type Maint+
     A                                      enance'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 72
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 72REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4 10'Product'
     A                                  4 20'CSSF'
     A                                  4 29'Description'
     A                                  5 11'Code'
     A                                  5 20'Type'
     A            DHTIPC         4A  B  7 11
     A                                      DSPATR(HI)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
     A            DHTIPT         4A  B  7 20
     A                                      DSPATR(HI)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
      *
      *-------------------------------------*
      ***  Screen Format for Bottom Details *
      *-------------------------------------*
      *
     A          R ERLU13F2
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *
      *---------------------------------*
      ***  Error message Subfile Format *
      *---------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *--------------------------------------------*
      ***  Error message Subfile Controller Format *
      *--------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  61
     AON61                                  SFLEND
     A            ##PGM                     SFLPGMQ
