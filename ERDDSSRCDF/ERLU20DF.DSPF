      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Amount Due Weighting Rates Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  DSPF/ERLU20DF - Amount Due Weighting Rates Maintenance       *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX012  *CREATE    Date 12Aug98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX012 - Guarantees and Collateral Management enhancements   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A N15                                  CA09(09 'Add Mode')
      *
      *----------------------------------------*
      ***  Screen 1 Format for Subfile Details *
      *----------------------------------------*
      *
     A          R ERLU20S1                  TEXT('(c) Finastra International +
     A                                      Limited 2005')
     A                                      SFL
     A  63                                  SFLNXTCHG
     A            DSSELE         1A  B  8  2TEXT('SELECTION')
     A                                      DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N15N17N20                            DSPATR(UL)
     A  18                                  DSPATR(BL)
     A  16
     AO 15
     AO 17                                  DSPATR(PR)
     A  16                                  DSPATR(ND)
     A            DSECSC         4A  B  8  7TEXT('ECONOMIC SECTOR CODE')
     A                                      DSPATR(HI)
     A N15 16N21                            DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  15
     AON16                                  DSPATR(PR)
     A            DSECON        35A  O  8 13TEXT('ECONOMIC SECTOR NARRATIVE')
     A                                      DSPATR(HI)
     A            DSZABI         1A  B  8 51TEXT('ZONE A/B')
     A                                      DSPATR(HI)
     A N15 16N22                            DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  15
     AON16                                  DSPATR(PR)
     A            DSCLAT         1A  B  8 57TEXT('CLAIM''S TYPE')
     A                                      DSPATR(HI)
     A N15 16N23                            DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  15
     AON16                                  DSPATR(PR)
     A            DSCLAM         5A  B  8 61TEXT('CLAIM''S MATURITY')
     A                                      DSPATR(HI)
     A N15 16N24                            DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  15
     AON16                                  DSPATR(PR)
     A            DSWEIG         3A  B  8 74TEXT('WEIGHTING RATE')
     A                                      DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A N15N25N17                            DSPATR(UL)
     A  15
     AO 17                                  DSPATR(PR)
     A            D2WEIG         3A  H
     A            D2PECO         3S 0H
      *
      *----------------------------------------*
      ***  Screen 1 Format for Subfile Control *
      *----------------------------------------*
      *
     A          R ERLU20C1                  SFLCTL(ERLU20S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N60                                  ROLLUP(35)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  80                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  60                                  SFLEND
     A            SFLREC         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'ERLU20C1'
     A                                  2 25'Amount Due Weighting Rates'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            SMRDT     R        O  1 72
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 67'TIME'
     A            STIME     R     Y  O  2 72REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  7'<---------Economic Sector Code----+
     A                                      ------>'
     A                                  4 49'Zone'
     A                                  4 59'Claim''s'
     A                                  4 71'Weighting'
     A                                  5 50'A/B'
     A                                  5 56'Type Maturity'
     A                                  5 73'Rate'
     A            DHECSC         4A  B  7  7
     A                                      DSPATR(HI)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
     A            DHZABI         1A  B  7 51
     A                                      DSPATR(HI)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
     A            DHCLAT         1A  B  7 57
     A                                      DSPATR(HI)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
     A            DHCLAM         5A  B  7 61
     A                                      DSPATR(HI)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
     A            DHWEIG         3A  B  7 74
     A                                      DSPATR(HI)
     A N20N16                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  16                                  DSPATR(PR ND)
      *
      *-------------------------------------*
      ***  Screen Format for Bottom Details *
      *-------------------------------------*
      *
     A          R ERLU20F2
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A                                      OVRDTA
      *
      *---------------------------------*
      ***  Error message Subfile Format *
      *---------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *--------------------------------------------*
      ***  Error message Subfile Controller Format *
      *--------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  61
     AON61                                  SFLEND
     A            ##PGM                     SFLPGMQ
