     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Investment Company Codes Table Maint.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - European Reporting                                   *
     A*                                                               *
     A*  DSPF/ERLU55DF - Investment Company Codes Table Maintenance   *
     A*                                                               *
      *  (c) Finastra International Limited 2005                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX008  *CREATE    Date 17Feb00               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     A*  ULX008 - CSSF Statutory Reporting : Commissariat aux Bourses *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      **************************************************************************
     A          R ERLU55S0                  TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            SACTN          1   B  8  5
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            SINCC          4   O  8  7TEXT('Investment Company Code')
     A                                      DSPATR(HI)
     A            SINNM         60   O  8 19TEXT('Investment Company Name')
     A                                      DSPATR(HI)
     A**************************************************************************
     A          R ERLU55C0                  SFLCTL(ERLU55S0)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      CA03(03 'Exit.')
     A                                      CA05(05 'Refresh')
     A                                      CF09(09 'Insert')
     A N70                                  ROLLUP(27 'Next page.')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 70                            SFLEND
     A                                      SFLSIZ(0099)
     A                                      SFLPAG(0013)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 14'ERLU55S1'
     A                                  1 26'Investment Company Codes Table +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
     A            ##SFRC         4S 0H      SFLRCDNBR
     A                                  4  7'Company'
     A                                  5  7'Code'
     A                                  5 19'Company Name'
     A            DINCC          4A  B  6  7TEXT('Investment Company Code')
     A                                      OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A            DINNM         60A  B  6 19TEXT('Investment Company Name')
     A                                      OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
      **************************************************************************
     A          R #MSGRC1                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      **************************************************************************
     A          R #MSGCT1                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRC1)
     A                                      SFLPAG(01) SFLSIZ(15)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A N26 27                          23  2'D=Delete  F3=Exit  F5=Refresh +
     A                                       F9=Add Mode  F12=Previous'
     A  26N27                          23  2'1=Select   F3=Exit'
      **************************************************************************
     A          R ERLU55F2                  TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
      *
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      CA10(10 'Delete')
     A                                      CA12(12 'Previous')
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            SUSER     R           1  2REFFLD(USER)
     A                                  1 14'ERLU55F2'
     A                                  1 26'Investment Company Codes Table +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 67'DATE'
     A            SRUNA     R           1 72REFFLD(RUNA)
      *
     A            SWSID     R           2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                 10  2'Investment Company Code :'
     A            FINCC          4   B 10 28
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N34N36                               DSPATR(UL)
     A N34 36                               DSPATR(RI PC)
     A  34                                  DSPATR(PR)
     A                                 11  2'Name :'
     A            FINNM         60   B 11  9
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N34N37                               DSPATR(UL)
     A N34 37                               DSPATR(RI PC)
     A  34                                  DSPATR(PR)
      *
     A  35N38                          23  2'F3=Exit   F5=Refresh   +
     A                                      F12=Previous'
     A N35 38                          23  2'F3=Exit   F12=Previous   +
     A                                      F10=Delete'
      **************************************************************************
     A          R #MSGRC2                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      **************************************************************************
     A          R #MSGCT2                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRC2)
     A                                      SFLPAG(01) SFLSIZ(15)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
