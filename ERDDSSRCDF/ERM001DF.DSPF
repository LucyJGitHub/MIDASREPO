      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ER Select List for Voce/Sottovoce Codes')        *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  ERM001DF - Select List for Voce/Sottovoce Codes              *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC139               Date 10Feb21             *
      *  Prev Amend No. MMI001   *Create     Date 20Nov98             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  MMI001 - Upgrade Italian Library.                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/QSYSPRT)
      *
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Copyright Midas-Kapiti in-
     A                                      ternational LTD. 1998')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(34 'Invalid £1SEL')
     A  84                                  SFLNXTCHG
     A            #1SEL          1A  B  9  3VALUES('1')
     A                                      CHECK(AB)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #10KEY         7A  O  9 10DSPATR(HI)
     A            #10NAR        30A  O  9 33DSPATR(HI)
      *=========================================================================
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #20COD')
     A                                      SETOF(32 'Invalid: #20KEY')
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2
     A N25                                  OVRDTA
     A            ##PGM         10A  O  1 13
     A N25                                  OVRDTA
     A            ##MRDT         7A  O  1 72
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2
     A N25                                  OVRDTA
     A                                  2 32'IR Internal Table'
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 71EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5 10'Key'
     A                                  5 33'Narrative'
     A N25                                  OVRATR
     A            #20KEY         7A  B  7 10
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #20NAR        30A  B  7 33
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit        -
     A                                                                         -
     A                                               '
      *=========================================================================
     A          R #MSGRCD
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
