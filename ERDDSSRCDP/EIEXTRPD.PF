      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas ER Production Extract')                          *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  EIEXTRPD - Production Extract                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. LUC139               Date 11Jan21             *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. 225555             Date 30Oct06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CPK025             Date 02May06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX046             Date 18Oct02               *
      *                 CEU007             Date 15Oct98               *
      *                 CAD007             Date 22Jul98               *
      *                 ULX004             Date 10MAR97               *
      *                 UIT021             Date 28JAN97               *
      *                 ER_R10             Date 27OCT94               *
      *                 ERL428 PT          Date 27AUG94               *
      *                 ERL417 PT          Date 27AUG94               *
      *                 ER0336 TLI         Date 05APR93               *
      *                 ER0322 TLI         Date 16FEB93               *
      *                 ER0321 TLI         Date 15FEB93               *
      *                 ER0205 AF          Date 14JAN93               *
      *                 ER0101 ADS         Date 24NOV92               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *           Apply UIT021 and MMI033                             *
      *  225555 - Add discount/premium amount and amount to amortize  *
      *           and 'Prix d'acquisition'                            *
      *  CPK025 - Deletion of ERFRF field reference file.             *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX046 - Circulars 2002/170-174-175 Integration              *
      *  CEU007 - Holidays Changes for Reporting Interface.           *
      *  CAD007 - Midas CAD adaptation to Data Dictionary version 1.6 *
      *  ULX004 - Capital Adequacy                                    *
      *           (UNL002 Reference is replaced with ULX004)          *
      *           (UCH001 Reference is replaced with ULX004)          *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *  ER_R10 - EUROPEAN REPORTING - LUXEMBOURG UPGRADE IN R10      *
      *           Add level and branch/company code for Limits        *
      *  ERL428 - Add customer classification                         *
      *  ERL416 - Add loan processing type                            *
      *  ER0336 - Add 'Credit Type' + 'Security denomination'         *
      *  ER0322 - Add 'Deal Type code'                                *
      *  ER0321 - Add 'Country of Citizenship'                        *
      *  ER0205 - New fields 'Bank non Bank indicator' + 'Second      *
      *           Currency'                                           *
      *  ER0101 - Decision Key lenght from 6 to 8 (RECOMPILATION)     *
      *                                                               *
      *                                                               *
      *****************************************************************
     A          R EIEXTRF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A* Rec 1
     A            EXRECT         3S 0       TEXT('Record Type')
     A                                      COLHDG('Record' 'Type')
     A            EXLOTD         3A         TEXT('Lot Descriptor')
     A                                      COLHDG('Lot' 'Descriptor')
     A            EXRIND         4          TEXT('Record Identifier')
     A                                      COLHDG('record' 'Identifier')
     A            EXREFI         8S 0       TEXT('Internal reference number')
     A                                      COLHDG('Internal' 'ref.' 'number')
     A            EXOSHA         8S 0       TEXT('Origin Code Schema A')
     A                                      COLHDG('Origin' 'Code' 'Schema A')
     A            EXREPP         8S 0       TEXT('Reporting Period')
     A                                      COLHDG('Reporting' 'Period')
     A            EXREPD         8S 0       TEXT('Reporting Date')
     A                                      COLHDG('Reporting' 'Date')
     A            EXBRCD         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
     A            EXTREF        21A         TEXT('Transaction Reference')
     A                                      COLHDG('Transaction' 'Reference')
     A            EXDECK         8A         TEXT('Decision Table Key')
     A                                      COLHDG('Decision' 'Table' 'Key')
     A            EXCCYC         3          TEXT('Currency Code')
     A                                      COLHDG('Currency' 'Code')
     A            EXORCC         2          TEXT('Origin Country Code')
     A                                      COLHDG('Origin' 'Country' 'Code')
     A            EXCUCC         2          TEXT('Customer Country Code')
     A                                      COLHDG('Customer' 'Country' 'Code')
     A            EXCUST         6A         TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            EXCSSN        10A         TEXT('Customer Shortname')
     A                                      COLHDG('Customer' 'Shortname')
     A            EXIRTT         1S 0       TEXT('Interest Rate Type')
     A                                      COLHDG('Interest' 'Rate' 'Type')
     A            EXPOOL         1S 0       TEXT('Code Pool Creditexport')
     A                                      COLHDG('Code' 'Pool' 'Creditexport')
     A            EXDIST         1S 0       TEXT('Discount Type')
     A                                      COLHDG('Discount' 'Type')
     A            EXRCSI         1          TEXT('Recourse Indicator')
     A                                      COLHDG('Recourse' 'Indicator')
     A            EXPCNB         6A         TEXT('Parent Customer Number')
     A                                      COLHDG('Parent' 'Customer' 'Number')
     A            EXPRCC         2          TEXT('Parent Country Code')
     A                                      COLHDG('Parent' 'Country' 'Code')
     A            EXSTRD         8S 0       TEXT('Start Date')
     A                                      COLHDG('Start' 'Date')
     A            EXENDD         8S 0       TEXT('End Date')
     A                                      COLHDG('End' 'Date')
     A            EXREVD         8S 0       TEXT('Revision Date')
     A                                      COLHDG('Revision' 'Date')
     A            EXAMNT        16S 0       TEXT('Amount in Currency')
     A                                      COLHDG('Amount' 'in Currency')
     A            EXDRCR         1S 0       TEXT('Debit/Credit indicator')
     A                                      COLHDG('Dr/Cr' 'Ind')
     A            EXNOMA        16S 0       TEXT('Nominal Amount')
     A                                      COLHDG('Nominal' 'Amount')
     A            EXFCUS         6A         TEXT('Facility Customer Number')
     A                                      COLHDG('Facility' 'Cust.' 'Num.')
     A            EXFACT         3S 0       TEXT('Facility type')
     A                                      COLHDG('Facility' 'type')
     A            EXFCNO         2S 0       TEXT('Facility number')
     A                                      COLHDG('Facility' 'number')
      * Rec 2
     A            EXMARA        16S 0       TEXT('Margin Amount')
     A                                      COLHDG('Margin' 'Amount')
     A            EXRICA        16S 0       TEXT('Risk Center Amount')
     A                                      COLHDG('Risk' 'Center' 'Amount')
      * Rec 3
     A            EXTRIN         1A         TEXT('Trade / Investment')
     A                                      COLHDG('Trade /' 'Investment')
     A            EXCLPR        15P 8       TEXT('Cloture Price in %')
     A                                      COLHDG('Cloture' 'Price in %')
     A            EXPBCP        15P 8       TEXT('Previous Cloture Price')
     A                                      COLHDG('Previous' 'Cloture' 'Price')
     A            EXSVMC        12          TEXT('Code SVM titre')
     A                                      COLHDG('Code' 'SVM' 'Titre')
     A            EXSECQ         1S 0       TEXT('Security Quotation')
     A                                      COLHDG('Security' 'Quotation')
     A            EXSECD        20          TEXT('Security Denomination')
     A                                      COLHDG('Security' 'Denomination')
     A            EXSECY        45          TEXT('Security Denomination')
     A                                      COLHDG('Security' 'Denomination')
     A            EXCESC         1S 0       TEXT('Cession Code')
     A                                      COLHDG('Cession' 'Code')
     A            EXEXEA        16S 0       TEXT('Exercice Amount')
     A                                      COLHDG('Exercise' 'Amount')
     A            EXREAA        16S 0       TEXT('Realisation Amount')
     A                                      COLHDG('Realisation' 'Amount')
     A            EXPREA        16S 0       TEXT('Previous Realisation Amount')
     A                                      COLHDG('Prev.' 'Realis.' 'Amount')
     A            EXNETA        16S 0       TEXT('Netto Accounting Amount')
     A                                      COLHDG('Netto' 'Account.' 'Amount')
     A            EXSESN        10A         TEXT('Security Shortname')
     A                                      COLHDG('Security' 'Shortname')
      * Rec 4
     A            EXQEVL         1S 0       TEXT('Quarter Risk Evaluation code')
     A                                      COLHDG('Quarter Risk' 'Eval. code')
     A            EXREVL         1S 0       TEXT('Report Risk Evaluation code')
     A                                      COLHDG('Report Risk' 'Eval. code')
     A            EXCRIS         1S 0       TEXT('Commercial Risk amount code')
     A                                      COLHDG('Commercial Risk' 'Amt code')
     A            EXQCOM         1S 0       TEXT('Quarter Commercial Risk sort')
     A                                      COLHDG('Quarter Comm' 'Risk sort')
     A            EXRCOM         1S 0       TEXT('Report Commercial Risk sort')
     A                                      COLHDG('Report Comm' 'Risk sort')
     A            EXQINC        16S 0       TEXT('Quarter Increase Amount')
     A                                      COLHDG('Quarter Increase' 'Amount')
     A            EXQDEC        16S 0       TEXT('Quarter Decrease Amount')
     A                                      COLHDG('Quarter Decrease' 'Amount')
      * Rec 5
     A            EXRICC         9          TEXT('Risk Centre Code')
     A                                      COLHDG('Risk' 'Centre' 'Code')
     A            EXCRNM        20          TEXT('Customer Report Name')
     A                                      COLHDG('Customer' 'Report' 'Name')
     A            EXRISA        16S 0       TEXT('Risk Amount')
     A                                      COLHDG('Risk' 'Amount')
      * Rec 6
     A            EXBNBI         1A         TEXT('Bank/non-Bank Indicator')
     A                                      COLHDG('Bank/non-Bank' 'Indicator')
     A            EXCCY2         3          TEXT('Second Currency')
     A                                      COLHDG('Second' 'Currency')
     A            EXCNCZ         2          TEXT('Country of Citizenship')
     A                                      COLHDG('Country' 'Citizenship')
     A            EXDTCD         1S 0       TEXT('Deal Type Code')
     A                                      COLHDG('Deal Type' 'Code')
     A            EXCRET         1S 0       TEXT('Credit Type')
     A                                      COLHDG('Credit' 'Type')
     A            EXPTYP         2S 0       TEXT('Loan Proc Type')
     A                                      COLHDG('Loan' 'Proc' 'Type')
     A            EXCLAS         1          TEXT('Customer Classification')
     A                                      COLHDG('Cust' 'Clas')
     A            EXLEVL         1          TEXT('Level')
     A                                      COLHDG('Level')
     A            EXBCCD         3          TEXT('Branch/Company Code')
     A                                      COLHDG('Branch/Company' 'Code')
     A            EXACCR        16S 0       TEXT('Accrued Interest')
     A                                      COLHDG('Accrued' 'Interest')
     A            EXITAC        16S 0       TEXT('Interest to Accrue')
     A                                      COLHDG('Interest' 'to Accrue')
     A            EXSLID         8S 0       TEXT('Start/Last Interest Date')
     A                                      COLHDG('Start/Last' 'Interest' 'Dat-
     A                                      e')
     A            EXNIDT         8S 0       TEXT('Next Interest Due Date')
     A                                      COLHDG('Next Interest' 'Due Date')
     A            EXRETB         1A         TEXT('Retail Indicators')
     A                                      COLHDG('Retail' 'Indicators')
     A            EXISLC         2A         TEXT('Issuer Country of Location')
     A                                      COLHDG('Issuer' 'Country of' -
     A                                      'Location')
     A            EXISAM        16S 0       TEXT('Issue Amount')
     A                                      COLHDG('Issue' 'Amount')
     A            EXBICN         6A         TEXT('Branch Internal Customer Nr.')
     A                                      COLHDG('Branch' 'Internal' -
     A                                      'Customer Nr.')
     A            EXCTPY         4S 0       TEXT('Type of Counterparty')
     A                                      COLHDG('Type of' 'Counterparty')
     A            EXNOTD         3S 0       TEXT('Number of Notice Days')
     A                                      COLHDG('Number of' 'Notice' -
     A                                      'Days')
     A            EXNPVA        16S 0       TEXT('Net Present Value')
     A                                      COLHDG('Net' 'Present' 'Value')
     A            EXRSKF         7S 5       TEXT('FF Risk Factor')
     A                                      COLHDG('FF' 'Risk' 'Factor')
     A            EXPLAG         1A         TEXT('SE Pledge Agreement')
     A                                      COLHDG('SE' 'Pledge' 'Agreement')
     A            EXATYP         1A         TEXT('Account Type')
     A                                      COLHDG('Account Type')
     A            EXPRMP        16S 0       TEXT('Premium for Options')
     A                                      COLHDG('Premium' 'for Options')
     A            EXCNTP         4S 0       TEXT('Type of Customer')
     A                                      COLHDG('Type of' 'Customer')
     A            EXISSR         6A         TEXT('Issuer Number')
     A                                      COLHDG('Issuer' 'Number')
     A            EXFINR         6S 0       TEXT('Fiduciary Number')
     A                                      COLHDG('Fiduciary' 'Number')
     A            EXDBCD         1A         TEXT('Doubtful Code')
     A                                      COLHDG('Doubtful' 'Code')
     A            EXACBL         1A         TEXT('Account Blocked Code')
     A                                      COLHDG('Account' 'Blocked' 'Code')
     A            EXFCTI         1A         TEXT('Facility type indicator')
     A            EXCADF        16S 0       TEXT('Capital Amount Doubtful')
     A            EXIADF        16S 0       TEXT('Interest Amount Doubtful')
     A            EXMPRA        16S 0       TEXT('Matured Principal Amount')
     A            EXIMAM        16S 0       TEXT('Interest on Matured Amount')
     A            EXMUPR        16S 0       TEXT('Matured Unpaid Principal')
     A            EXIMUP        16S 0       TEXT('Interest on Matured Unpaid')
     A            EXLNAC         1S 0       TEXT('Activity Code of Line')
     A            EXCRRT        11S 7       TEXT('Credit Rate')
     A                                      COLHDG('Credit Rate')
      *
     A            EXDERT        11S 7       TEXT('Debit Rate')
     A                                      COLHDG('Debit Rate')
