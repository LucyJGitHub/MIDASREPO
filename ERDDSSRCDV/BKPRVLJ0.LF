      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas ER Book Positions Revaluation')
      *****************************************************************
      *                                                               *
      *    MIDAS/ABS   SECURITIES TRADING RELEASE 2MODULE             *
      *                                                               *
      *    JLF/BKPRVLJ0 -  BOOK POSITIONS REVALUATION                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *****************************************************************
      *                                                               *
      *         /////////////////////////////////////////             *
      *        // WARNING: When creating this logical //              *
      *       //   file, take option                 //               *
      *      //                  MAINTENANCE (*DLY) //                *
      *     /////////////////////////////////////////                 *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. 246619             Date 29Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 UPG402             Date 02Oct04               *
      *                 ER_R10             Date 27Oct94               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  246619 - Recompile over changes in PF/BKPOSD.                *
      *  CER001 - LUX Upgrade to MidasPlus (Recompile only)           *
      *  UPG402 - Recompile due to change in BKPHDD for R4.02 upgrade *
     A*   ER_R10 - EUROPEAN REPORTING - LUXEMBOURG UPGRADE IN R10     *
     A*****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R BKPBKF                    JFILE(BKPHDD BKPOSD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A*  Join Book Positions Header to Book Positions
     A          J                           JOIN(1 2)
     A* ER_R10 >>>
     A************                          JFLD(BHBR BPBR)
     A                                      JFLD(BHBA BPBA)
     A* ER_R10 <<<
     A                                      JFLD(BHSC BPSC)
     A                                      JFLD(BHBK BPBK)
     A                                      JFLD(BHMK BPMK)
     A                                      JFLD(BHTV BPTV)
     A                                      JFLD(LPSD BPPD)
     A                                      JFLD(RECI RECI)
     A*  Book Position Header Fields
     A            RECI                      JREF(2)
     A* ER_R10 >>>
     A************BHBR
     A            BHBA
     A* ER_R10 <<<
     A            BHBK
     A            BHSC
     A            NCRY
     A            BHMK
     A            BHTV
     A            LAVP
     A*  Book Position Fields
     A            NPSN
     A*
     A* ER_R10 >>>
     A**********K*BHBR
     A          K BHBA
     A* ER_R10 <<<
     A          K BHBK
     A          K BHSC
     A          K NCRY
     A*
     A          S RECI                      COMP(EQ 'D')
