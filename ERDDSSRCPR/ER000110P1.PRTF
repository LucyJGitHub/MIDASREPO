      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas @24 KWG response file Report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - European reporting Module                            *
      *                                                               *
      *  PRTF/ER000110P1 - Midas @24 KWG response file Report         *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER047  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R ER00011R0                 TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2008')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ER000110P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                    99'DATE'
     A            BJMRDT    R            104REFFLD(MRDT)
     A                                   113'PAGE'
     A                                   118PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    99'TIME'
     A                                   104TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'Response File Report'
     A                                      SPACEA(1)
     A                                    55'--------------------'
     A                                      SPACEA(1)
      *
     A                                     5'Response for file . . .:'
     A            R11FNR         8S 0     30TEXT('Transmission file +
     A                                           number')
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                     5'Full path of resp.file :'
     A            R11PTH        80        30TEXT('Full path')
     A                                      SPACEA(1)
      *
     A                                     5'Date/Time of resp.file :'
     A            R11DAT        10        30TEXT('Date of creation')
     A            R11TIM         8        41TEXT('Time of creation')
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A                                     2'  Error   '
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *
     A                                     2'Class Type'
     A                                    13'Description'
     A                                      SPACEA(1)
      *
     A                                     2'----------------------+
     A                                      -----------------------+
     A                                      -----------------------+
     A                                      -----------------------+
     A                                      ----------------'
     A                                      SPACEA(1)
      *
     A          R ER000110R1
      *
     A            R11ECL         2  0      4TEXT('Error Class')
      *
     A            R11ETP         2  0      9TEXT('Error Type')
      *
     A            R11DSC       110        13TEXT('Error description')
     A  31                                  HIGHLIGHT
     A                                      SPACEA(1)
      *
      ** Error Record Format
      *
     A          R ER000110R2
     A                                     4'ERROR reading response +
     A                                       file.'
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                    33'Database error'
     A                                      HIGHLIGHT
      *
     A            DBASE          3S 0     48TEXT('Database error')
     A                                      HIGHLIGHT
      *
     A                                    52'occured. Program +
     A                                      terminated|'
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R ER000110R3
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(1)
