      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ER Account Codes Decision Table List -Detail')   *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  PRTF/ER000175P1 - Midas ER Account Codes Decision Table List *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG26779           Date 27Nov09               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26779 - Change Request on GSART Table                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2009')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE ER000175P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   111'DATE'
     A            BJMRDT    R            116REFFLD(MRDT)
     A                                   124'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   111'TIME'
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N50                                43'Bundesbank Decision Table - Accoun-
     A                                      t Codes - LIST'
     A                                      SPACEA(1)
      *
     A N50                                43'-----------------------------------
     A                                      --------------'
      *
     A  50                                42'Bundesbank Decision Table - Accoun-
     A                                      t Codes - AUDIT'
     A                                      SPACEA(1)
      *
     A  50                                42'-----------------------------------
     A                                      ---------------'
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     4'Account'
     A                                      SPACEB(001)
     A                                    55'Extract'
     A                                    66'Business'
      **Start of change BUG26779
     A**********                          82'Business'                                      BUG26779
     A                                    78'Classification'
      **End of change BUG26779
     A                                   108'Last Activity'
      *
     A                                     4'Code'
     A                                      SPACEB(001)
     A                                    18'Name'
     A                                    48'Type'
     A                                    55'to GRI'
      **Start of change BUG26779
     A**********                          64'Classification'                                BUG26779
     A                                    66'Class'
     A**********                          80'Classification'                                BUG26779
     A                                    75'Sub-Class'
     A                                    88'Type'
      **End of change BUG26779
     A                                   108'Action'
     A                                   121'Date'
      *
      **Start of change BUG26779
     A**********                          67'Class'                                         BUG26779
     A**********                            SPACEB(001)                                     BUG26779
     A**********                          82'Sub-Class'                                     BUG26779
      **End of change BUG26779
     A                                      SPACEA(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
      *
     A            R3ACOD        10         4TEXT('Account Code')
     A            R3ACCN        25        18TEXT('Account Name')
     A            R3ACTY         1        49TEXT('Account Type')
     A            R3GRI          1        57TEXT('Extract to GRI')
      **Start of change BUG26779
     A**********  R3BUCL         2        68TEXT('Business Classn Class')                   BUG26779
     A**********  R3BUSU         2        85TEXT('Business Classn Subclass')                BUG26779
     A            R3BUCL         2        67TEXT('Business Classn Class')
     A            R3BUSU         2        77TEXT('Business Classn Subclass')
     A            R3BUTY         2        88TEXT('Business Classn Type')
      **End of change BUG26779
     A            R3CHTP         6       108TEXT('Type of Last change')
     A            R3LCD          7       121TEXT('Last Change date')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
