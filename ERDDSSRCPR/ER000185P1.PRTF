      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ER Transaction Type Decision Table List')        *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  PRTF/ER000185P1 - Midas ER Transaction Type Decision Table   *
      *                    List                                       *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 BUG26779           Date 27Nov09               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG26779 - Change Request on GSART Table                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      ** HEADP1 - REPORT HEADER RECORD FORMAT
      *****************************************************************
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
      *
     A                                      SKIPB(003)
      *
     A                                     6'REFERENCE ER000185P1'
      *
     A            BJURPT    R             37 REFFLD(URPT)
      *
     A                                   101'DATE'
      *
     A            BJMRDT    R            106REFFLD(MRDT)
      *
     A                                   119'PAGE'
      *
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
      *
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A N70                                37'Bundesbank Decision Table - -
     A                                       Transaction Types - LIST'
      *
     A                                      SPACEB(002)
     A N70                                37'-----------------------------------
     A                                      -------------------'
      *
     A                                      SPACEB(001)
     A  70                                37'Bundesbank Decision Table - -
     A                                       Transaction Types - AUDIT'
      *
     A                                      SPACEB(002)
     A  70                                37'-----------------------------------
     A                                      -------------------'
      *
     A                                      SPACEB(001)
     A                                     6'Module'
     A                                      SPACEB(002)
      *
     A                                    13'Transaction'
      *
     A                                    61'Extract'
      *
     A                                    71'Business'
      *
      **Start of change BUG26779
     A**********                          86'Business'                                      BUG26779
     A                                    82'Classification'
      **End of change BUG26779
      *
     A                                    99'Last'
      *
     A                                   104'Activity'
      *
     A                                     7'Id'
     A                                      SPACEB(001)
      *
     A                                    13'Type'
      *
     A                                    18'Subtype'
      *
     A                                    26'Class'
      *
     A                                    32'Description'
      *
     A                                    61'to GRI'
      *
      **Start of change BUG26779
     A**********                          69'Classification'                                BUG26779
     A                                    71'Class'
      *
     A**********                          84'Classification'                                BUG26779
     A                                    79'Sub-Class'
     A                                    92'Type'
      **End of change BUG26779
      *
     A                                    99'Action'
      *
     A                                   111'Date'
      *
      **Start of change BUG26779
     A**********                          71'Class'                                         BUG26779
     A**********                            SPACEB(001)                                     BUG26779
      *
     A**********                          86'Sub-Class'                                     BUG26779
      **End of change BUG26779
      *
     A                                      SPACEA(002)
      *
      *****************************************************************
      ** DETAIL - DETAIL RECORD FORMAT
      *****************************************************************
      *
     A          R DETAIL
      *
      * Module Id
     A            R4MMOD         2A        7
      *
      * Transaction Type
     A            R4TRTY         2A       13
      *
      * Transaction Subtype
     A            R4TRST         2A       20
      *
      * Class
     A N22        R4TRCL         4A       26
      *
      * Description
     A            R3DESC        30A       32
      *
      * Extract to GRI
     A            R4GRI          1A       64
      *
      * Business Classification Code
      **Start of change BUG26779
     A**********  R4BUCL         2A       73                                                BUG26779
     A            R4BUCL         2A       72
      **End of change BUG26779
      *
      * Business Classification Sub-Class
      **Start of change BUG26779
     A**********  R4BUSU         2A       89                                                BUG26779
     A            R4BUSU         2A       82
     A            R4BUTY         2A       93TEXT('Business Classn Type')
      **End of change BUG26779
      *
      * Last Activity Action
     A            R4TYLC        10A       99
      *
      * Last Change Date
     A            R4LCD          7A      111
     A                                      SPACEA(001)
      *
      *****************************************************************
      ** TRAILP1 - "END OF REPORT" RECORD FORMAT
      *****************************************************************
      *
     A          R TRAILP1
      *
     A                                      SPACEB(002)
     A                                    55'***  END OF REPORT  ***'
      *
