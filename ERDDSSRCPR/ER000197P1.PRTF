      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ER Transaction/Account Bal. Extract - Detail')   *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  PRTF/ER000197P1 - Midas ER Bundesbank Transaction/Account    *
      *                    Balance Extract Report - Detail            *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2009')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A                                     2
     A                                      'REFERENCE ER000197P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   111
     A                                      'DATE'
     A            BJMRDT    R            116
     A                                      REFFLD(MRDT)
     A                                   124
     A                                      'PAGE'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   111
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   116
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    54
     A                                      'BUNDESBANK EXTRACT REPORT'
     A                                      SPACEB(001)
      *
     A                                    54
     A                                      '-------------------------'
     A                                      SPACEB(001)
     A                                     2
     A                                      'Business'
     A                                      SPACEB(001)
     A                                     2
     A                                      'Class'
     A                                      SPACEB(001)
     A                                    16
     A                                      'Transaction'
     A                                    33
     A                                      'Account'
     A                                    42
     A                                      'Ccy'
     A                                    46
     A                                      'Ctry'
     A                                    52
     A                                      'Customer'
     A                                    68
     A                                      'Report Name'
     A                                    88
     A                                      'Amount'
     A                                   104
     A                                      'Amount'
     A                                   117
     A                                      'Debit/'
     A                                     2
     A                                      'Sub-Class'
     A                                      SPACEB(001)
     A                                    18
     A                                      'Reference'
     A                                    35
     A                                      'Code'
     A                                    54
     A                                      'Number'
     A                                    87
     A                                      'Local Ccy'
     A                                   103
     A                                      'Base Ccy'
     A                                   117
     A                                      'Credit'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(001)
     A            RGSKL          2A  O     3
     A                                      TEXT('Business Class')
     A            RRSUKL         2A  O     6
     A                                      TEXT('Business Classification Sub-C-
     A                                      lass')
     A            RGSRE         20A  O    10
     A                                      TEXT('Transaction Reference')
     A            RACOD         10A  O    32
     A                                      TEXT('Account Code')
     A            RWHIS          3A  O    43
     A                                      TEXT('Currency')
     A            RCOLC          2A  O    48
     A                                      TEXT('Country Code')
     A            RKDNR         10A  O    52
     A                                      TEXT('Customer Number')
     A            RCRNM         20A  O    64
     A                                      TEXT('Customer Report Name')
     A            RALCY         14A  O    86
     A                                      TEXT('Amount Local Currency')
     A            RABCY         14A  O   102
     A                                      TEXT('Amount Base Currency')
     A            RDCIN          1A  O   119
     A                                      TEXT('Debit/Credit')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
