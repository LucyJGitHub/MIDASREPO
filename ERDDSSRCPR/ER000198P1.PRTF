      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ER Bundesbank Cash Flow Extr Report - Detail')   *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  PRTF/ER000198P1 - Midas ER Bundesbank Cash Flow Extr Report  *
      *                    - Detail                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER045  *CREATE    Date 20Jul09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2009')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE ER000198P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   111'DATE'
     A            BJMRDT    R            116REFFLD(MRDT)
     A                                   124'PAGE'
     A                                   129PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   111'TIME'
     A                                      SPACEB(1)
     A                                   116TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45'BUNDESBANK CASH FLOW EXTRACT REPOR-
     A                                      T - AUDIT'
     A                                      SPACEB(2)
      *
     A                                    45'-----------------------------------
     A                                      ---------'
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     3'Trans'
     A                                      SPACEB(001)
     A                                    12'Ccy'
     A                                    16'Time Band'
      *
     A                                     4'Ref'
     A                                      SPACEB(001)
     A                                    16'Set 1:'
     A                                    23'Up to 1m'
     A                                    42'1m to 3m'
     A                                    61'3m to 6m'
     A                                    80'6m to 12m'
     A                                    99'Over 1yr'
      *
     A                                    16'Set 2:'
     A                                      SPACEB(001)
     A                                    23'Up to 3m'
     A                                    42'3m to 1yr'
     A                                    61'1yr to 5yrs'
     A                                    80'Over 5yrs'
     A                                      SPACEA(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A                                      SPACEB(1)
      *
     A            RGSRE          6   O     3TEXT('Transaction Ref')
     A            RWHIS          3A  O    12TEXT('Currency')
     A            RLB1G         18   O    23TEXT('Time Band 1 Up To 1m')
     A            RLB2G         18A  O    42TEXT('Time Band 1m To 3m')
     A            RLB3G         18A  O    61TEXT('Time Band 3m To 6m')
     A            RLB4G         18A  O    80TEXT('Time Band 6m To 1yr')
     A            RLB5G         18A  O    99TEXT('Time Band More Than 1yr')
     A            RLB1B         18A  O    23TEXT('Time Band 1 Up To 3m')
     A                                      SPACEB(001)
     A            RLB2B         18A  O    42TEXT('Time Band 3m To 1yr')
     A            RLB3B         18A  O    61TEXT('Time Band 1yr To 5yrs')
     A            RLB4B         18A  O    80TEXT('Time Band More Than 5yrs')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
