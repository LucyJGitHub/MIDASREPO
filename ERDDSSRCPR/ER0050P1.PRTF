      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ER Book code/Investment type - PRTF')
     A*****************************************************************
     A*                                                               *
     A*   MIDAS/ABS  EUROPEAN REPORTING                               *
     A*                                                               *
     A*   ER0050P1 - BOOK CODE / INVESTMENT TYPE - REPORT PRTF        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     A*                                                               *
      *****************************************************************
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. 132893             Date 20Apr98               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *   132893 - BOOK CODE / INVESTMENT TYPE DEVELOPMENT            *
      *            RCF ON DBA2.3                                      *
      *                                                               *
     A*****************************************************************
     A                                      INDARA
     A          R ZZPAGHDR
     A                                      TEXT('Standard report header   ')
     A                                      SKIPB(003)
     A                                      SPACEA(001)
      *.........................................................................
     A                                     2
     A                                      'Reference'
     A                                    12'ER0050P'
     A            ##URPT        53   O    45
     A                                      TEXT('User Report Title')
     A                                   111
     A                                      'Date'
     A            ##MRDT         7   O   116
     A                                      TEXT('midas rundate')
     A                                   124
     A                                      'Page'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                   111
     A                                      'Time'
     A                                      SPACEB(001)
     A                                   116
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    54
     A                                      'BOOK CODE / INVESTMENT TYPE'
     A                                      SPACEB(001)
     A                                    54
     A                                      '---------------------------'
     A                                      SPACEB(001)
      *=========================================================================
     A          R ZZCOLHDG
     A                                      TEXT('Detail line headings.')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     6'Book'
     A                                    30'Investment'
     A                                    70'Trading/Investment'
     A                                   100'Evaluation'
     A                                     6'code'
     A                                      SPACEB(001)
     A                                    33'type'
     A                                    73'Fixed assets'
     A                                   102'method'
     A                                     6'----'
     A                                      SPACEB(1)
     A                                    30'----------'
     A                                    70'------------------'
     A                                   100'----------'
     A                                      SPACEA(1)
      *=========================================================================
     A          R ZZDTLRCD
     A                                      TEXT('Detail line.             ')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            XRBOKC         2   O     7
     A                                      TEXT('Book code')
     A            XRINVT         3   O    33
     A                                      TEXT('Investment type')
     A            XRTIFA         1   O    78
     A                                      TEXT('Trading/Investment/Fixed')
     A            XREVAM         1   O   105
     A                                      TEXT('Evaluation method')
      *=========================================================================
     A          R ZZFINTTL
     A                                      TEXT('Final totals             ')
     A                                      SPACEB(002)
     A  76                                57
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                    53
     A                                      'Number of Records Printed..:'
     A                                      SPACEB(003)
     A            ZTNCRC         5  0O    82
     A                                      TEXT('No of Calculated Recs')
     A                                      EDTCDE(4)
      *=========================================================================
     A          R ZZENDRPT
     A                                      TEXT('End of report            ')
     A                                      SPACEB(002)
      *.........................................................................
     A                                    60
     A                                      '*** END OF REPORT ***'
      *=========================================================================
     A          R ZZERROR
     A                                      TEXT('Database error           ')
     A                                      SPACEB(002)
      *.........................................................................
     A                                    45
     A                                      '*** DATABASE ERROR - LISTING IS NO-
                                            T COMPLETE ***'
      *=========================================================================
