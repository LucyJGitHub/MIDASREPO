      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ER Lump-Sum Provision Rates - Printer')
     A********************************************************************
     A*                                                                  *
     A*      MIDAS/ABS  EUROPEAN REPORTING - LUXEMBOURG                  *
     A*                                                                  *
     A*      ER0060P1  - LUMP-SUM PROVISION RATES - PRINTER FILE         *
     A*                                                                  *
      *  (c) Finastra International Limited 2005                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ERL371             Date 16Dec93               *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     A*  ERL371  - IML 1.4 DEVELOPMENT                                   *
     A*                                                                  *
     A********************************************************************
     A*
     A                                      REF(SDBANKPD)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A          R ER0060H                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SKIPB(3)
     A*
     A                                    07'REFERENCE ER0060P1'
     A            BJURPT    R             37
     A                                   101'DATE'
     A            BJMRDT    R            106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    39'L U M P-S U M   P R O V I S I S I -
     A                                      O N   R A T E S'
     A                                      SPACEA(1)
     A                                    39'-----------------------------------
     A                                      ---------------'
     A                                      SPACEA(6)
     A                                    14'LUMP-SUM      NARRATIVE           -
     A                                                              LUMP-SUM'
     A                                      SPACEA(1)
     A                                    14'  CODE                            -
     A                                                                CODE  '
     A                                      SPACEA(1)
     A                                    14'--------      ---------           -
     A                                                              --------'
     A                                      SPACEA(2)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DETAIL PROCESSING
     A**---------------------------------------------------------------
     A*
     A          R ER0060D                   SPACEA(1)
     A            P1CODE         2A  O    17
     A            P1NARA        40A  O    28
     A            P1RATE         5A  O    74
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ER0060E                   SPACEB(2)
     A                                    54'** END OF REPORT **'
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DATABASE ERROR MESSAGE.
     A**---------------------------------------------------------------
     A*
     A          R ER0060DB                  TEXT('DATABASE ERROR MSG')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'ER0060P1'
     A            BJURPT    R             37
     A                                   101'DATE'
     A            BJMRDT    R            106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    39'L U M P-S U M   P R O V I S I S I -
     A                                      O N   R A T E S'
     A                                      SPACEA(1)
     A                                    39'-----------------------------------
     A                                      ---------------'
     A                                      SPACEA(3)
     A*
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(15)
     A            DBASE          3  0     48
     A                                    52'IN PROGRAM'
     A            DBPGM         10        63
     A                                      SPACEA(3)
     A*
     A                                    33'FILE'
     A            DBFILE        10        38
     A                                    48', KEY'
     A            DBKEY         29        54
     A                                      SPACEA(2)
     A                                    54'** END OF REPORT **'
     A*
