     A****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas Generate N/A Purchased Reval. Postings')
      *****************************************************************
     A*                                                              *
     A*     MIDAS/ABS - EUROPEAN REPORTING                           *
     A*                                                              *
     A*     PRTF/ER0510P1 - GENERATE N/A PURCHASED REVAL. POSTINGS   *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     A*
     A*****************************************************************
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   CER001 - LUX Upgrade to MidasPlus                           *
      *                                                               *
      *****************************************************************
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A          R R001                      TEXT('(c) Misys International +
     A                                      Banking Systems, Ltd. +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     2'Reference'
     A                                    12'ER0510P1'
     A            BJURPT        53        37
     A                                   101'Date'
     A            BJMRDT         7       106
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    50'Generate N/A Purchased Revaluation-
     A                                       Postings'
     A                                      SPACEA(1)
     A                                    50'__________________________________-
     A                                      _________'
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   OUTPUT RUNNING TITLE TO PRINTER FILE
     A**---------------------------------------------------------------
     A*
     A          R R005
     A                                     2'Branch :'
     A            XRBRCD         3A       11
     A                                    15'-'
     A            XRBRNM        30A       17
     A                                      SPACEA(1)
     A                                     2'Ccy    :'
     A            XRCCY          3A       11
     A                                    15'-'
     A            XRCYNM        14A       17
     A                                      SPACEA(2)
     A                                     2'Deal'
     A                                    11'Reference'
     A                                    34'Customer'
     A                                    49'Purchase Price'
     A                                    72'Face value'
     A                                    85'Closure Price'
     A                                   105'Revaluation'
     A                                   120'Post'
     A                                      SPACEA(1)
     A                                     2'Number'
     A                                    34'Number'
     A                                    49'   in %'
     A                                    72'in Base Ccy'
     A                                    87'   in % '
     A                                   107'  Amount '
     A                                   120'Reval'
     A                                      SPACEA(1)
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -------------------'
     A                                      SPACEA(2)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR EACH DETAIL LINE
     A**---------------------------------------------------------------
     A*
     A          R R002
     A
     A            XRDLNO         6         2
     A            XRNARA        20        11
     A            XRCNUM         6        34
     A            XRPUPR        14        49
     A            XRFAVA        18        66
     A            XRCLPR        14        85
     A            XRREAM        18       102
     A            XRPORE         1       122
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   OUTPUT SUB-TOTALS TO PRINTER FILE
     A**---------------------------------------------------------------
     A*
     A          R R006
     A                                      SPACEB(3)
     A                                    55'Profit'
     A                                    80'Loss'
     A                                   110'Total'
     A                                      SPACEA(1)
     A                                    55'------'
     A                                    80'----'
     a                                   110'-----'
     A                                      SPACEA(1)
     A                                     7'Sub-total Post Revaluation ''Y'' :'
     A            XRSTY1        18        49
     A            XRSTY2        18        70
     A            XRSTY3        18       100
     A                                      SPACEA(1)
     A                                     7'Sub-total Post Revaluation ''N'' :'
     A                                    48'('
     A                                    67')'
     A            XRSTN1        18        49
     A            XRSTN2        18        70
     A            XRSTN3        18       100
     A                                      SPACEA(1)
     A                                   103'=============='
     A                                      SPACEA(1)
     A                                    68'Profit and Loss sub-total'
     A            XRSTOT        18       100
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT IF NO DETAILS TO PRINT
     A**---------------------------------------------------------------
     A*
     A          R R003
     A                                      SPACEB(3)
     A                                    56'*** No Details to Report ***'
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R R004
     A                                      SPACEB(3)
     A                                    60'*** End of Report ***'
     A**---------------------------------------------------------------
     A**   OUTPUT TOTALS TO PRINTER FILE
     A**---------------------------------------------------------------
     A*
     A          R R007
     A                                      SKIPB(3)
     A                                     2'Reference'
     A                                    12'ER0510P1'
     A            BJURPT        53        37
     A                                   101'Date'
     A            BJMRDT         7       106
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    50'Generate N/A Purchased Revaluation-
     A                                       Postings'
     A                                      SPACEA(1)
     A                                    50'__________________________________-
     A                                      _________'
     A                                      SPACEA(1)
     A                                     2'Branch :'
     A            XRBRCD         3A       11
     A                                    15'-'
     A            XRBRNM        30A       17
     A                                      SPACEA(3)
     A                                    55'Profit'
     A                                    80'Loss'
     A                                   110'Total'
     A                                      SPACEA(1)
     A                                    55'------'
     A                                    80'----'
     a                                   110'-----'
     A                                      SPACEA(1)
     A                                    11'Total Post Revaluation ''Y'' :'
     A            XRTOY1        18        49
     A            XRTOY2        18        70
     A            XRTOY3        18       100
     A                                      SPACEA(1)
     A                                    11'Total Post Revaluation ''N'' :'
     A                                    48'('
     A                                    67')'
     A            XRTON1        18        49
     A            XRTON2        18        70
     A            XRTON3        18       100
     A                                      SPACEA(1)
     A                                   103'=============='
     A                                      SPACEA(1)
     A                                    71'Profit and Loss Posted'
     A            XRTOT         18       100
