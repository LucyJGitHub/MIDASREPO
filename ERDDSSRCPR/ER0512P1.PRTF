     A****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas Generate Book Positions Reval. Postings')
      *****************************************************************
     A*                                                              *
     A*     MIDAS/ABS - EUROPEAN REPORTING                           *
     A*                                                              *
     A*     PRTF/ER0512P1 - GENERATE BOOK POSITION REVAL. POSTINGS   *
     A*                                                              *
      *  (c) Finastra International Limited 2005                      *
     A*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 132893             Date 15Apr98               *
      *                 ER0134 TL          Date 22DEC92              *
     A*                                                              *
     A* -------------------------------------------------------------*
     A*                                                              *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                           *
     A*  132893 - SPF/RCF for IBLC/BNB on DBA2.3                     *
     A*  ER0134 - CHANGE REPORT LAYOUT                               *
     A****************************************************************
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A          R R001                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     2'Reference'
     A                                    12'ER0512P1'
     A            BJURPT        53        37
     A                                   101'Date'
     A            BJMRDT         7       106
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*** 132893 BEGIN ******
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*** 132893 END ******
     A                                    50'Generate Book Positions Revaluatio-
     A                                      n Postings'
     A                                      SPACEA(1)
     A                                    50'__________________________________-
     A                                      __________'
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   OUTPUT RUNNING TITLE TO PRINTER FILE
     A**---------------------------------------------------------------
     A*
     A          R R005
     A                                     2'Branch :'
     A            XRBRCD         3A       11
     A                                    15'-'
     A            XRBRNM        30A       17
     A                                      SPACEA(1)
     A                                     2'Book   :'
     A            XRBKCD         2A       11
     A                                    15'-'
     A            XRBKNM        30A       17
     A                                      SPACEA(2)
     A                                     2'Security'
     A* ER0134                            13'Currency'
     A* ER0134                            23'Market'
     A                                    18'Currency'
     A                                    28'Market'
     A                                    48'Average Price'
     A                                    67'Nominal Position'
     A                                    91'Market Price'
     A                                   113'Revaluation'
     A                                   127'Post'
     A                                      SPACEA(1)
     A* ER0134                             2'Shortname'
     A                                     2'Decription'
     A* ER0134                            15'Code'
     A                                    20'Code'
     A                                    52'   in %'
     A                                    94'   in % '
     A                                   113'  Amount '
     A                                   127'Reval'
     A                                      SPACEA(1)
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      SPACEA(2)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR EACH DETAIL LINE
     A**---------------------------------------------------------------
     A*
     A          R R002
     A
     A* ER0134 Begin
     A            SDESC         41         2
     A                                      SPACEA(1)
     A* ER0134 End
     A            XRSESN        10         2
     A* ER0134    XRCCY          3        15
     A* ER0134    XRMARK         1        26
     A            XRCCY          3        18
     A            XRMARK         1        31
     A            XRAVPR        19        43
     A            XRNPSN        21        62
     A            XRMKPR        19        84
     A            XRREAM        21       105
     A            XRPORE         1       129
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   OUTPUT SUB-TOTALS TO PRINTER FILE
     A**---------------------------------------------------------------
     A*
     A          R R006
     A                                      SPACEB(3)
     A                                    55'Profit'
     A                                    80'Loss'
     A                                   110'Total'
     A                                      SPACEA(1)
     A                                    55'------'
     A                                    80'----'
     a                                   110'-----'
     A                                      SPACEA(1)
     A                                     7'Sub-total Post Revaluation ''Y'' :'
     A            XRSTY1        18        49
     A            XRSTY2        18        70
     A            XRSTY3        18       100
     A                                      SPACEA(1)
     A                                     7'Sub-total Post Revaluation ''N'' :'
     A                                    48'('
     A                                    67')'
     A            XRSTN1        18        49
     A            XRSTN2        18        70
     A            XRSTN3        18       100
     A                                      SPACEA(1)
     A                                   103'=============='
     A                                      SPACEA(1)
     A                                    68'Profit and Loss sub-total'
     A            XRSTOT        18       100
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT IF NO DETAILS TO PRINT
     A**---------------------------------------------------------------
     A*
     A******    R R003                                                    132893
     A******                                SPACEB(3)                     132893
     A******                              56'*** No Details to Report ***'132893
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R R004
     A                                      SPACEB(3)
     A*** 132893 BEGIN ************
     A******                              60'*** End of Report ***'
     A                                    48'*** End of Report for Branch'
     A            EBRCH          3A       77
     A                                    81'***'
     A*** 132893 END *************
     A**---------------------------------------------------------------
     A**   OUTPUT TOTALS TO PRINTER FILE
     A**---------------------------------------------------------------
     A*
     A          R R007
     A                                      SKIPB(3)
     A                                     2'Reference'
     A                                    12'ER0512P1'
     A            BJURPT        53        37
     A                                   101'Date'
     A            BJMRDT         7       106
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    50'Generate Book Positions Revaluatio-
     A                                      n Postings'
     A                                      SPACEA(1)
     A                                    50'__________________________________-
     A                                      __________'
     A                                      SPACEA(1)
     A                                     2'Branch :'
     A            XRBRCD         3A       11
     A                                    15'-'
     A            XRBRNM        30A       17
     A                                      SPACEA(3)
     A                                    55'Profit'
     A                                    80'Loss'
     A                                   110'Total'
     A                                      SPACEA(1)
     A                                    55'------'
     A                                    80'----'
     a                                   110'-----'
     A                                      SPACEA(1)
     A                                    11'Total Post Revaluation ''Y'' :'
     A            XRTOY1        18        49
     A            XRTOY2        18        70
     A            XRTOY3        18       100
     A                                      SPACEA(1)
     A                                    11'Total Post Revaluation ''N'' :'
     A                                    48'('
     A                                    67')'
     A            XRTON1        18        49
     A            XRTON2        18        70
     A            XRTON3        18       100
     A                                      SPACEA(1)
     A                                   103'=============='
     A                                      SPACEA(1)
     A                                    71'Profit and Loss Posted'
     A            XRTOT         18       100
