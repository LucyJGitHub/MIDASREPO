      *****************************************************************   ULX004
/*STD *  PRTFAU                                                       *   ULX004
/*EXI *  TEXT('Midas ER Extract Customer Position-Audit List')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  PRTF/ER0630AU - ER Extract Customer Position-Audit List      *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX004             Date 28Mar97               *
      *                 ER0348 TLI         Date 01Jun93               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - Capital Adequacy                                    *
      *  ER0348 - Written for Customer Positions Development          *
      *****************************************************************
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SKIPB(3)
     A                                     2'Reference'
     A                                    12'ER0630AU'
     A            BJURPT        53        37
     A                                   101'Date'
     A            BJMRDT         7       106
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    50'ER European Reporting Extract CP'
     A                                      SPACEA(1)
     A                                    50'________________________________'
     A                                      SPACEA(3)
     A          R ERROR
     A                                     5'DATA BASE ERROR IN PROGRAM :'
     A            DBPGM         10        34
     A                                      SPACEA(1)
     A                                     5'FILE :'
     A*>>> ULX004 Begin >>>
     A*****       DBFILE        10        12
     A            DBFILE         8        12
     A*<<< ULX004 End <<<
     A                                      SPACEA(1)
     A                                     5'KEY  :'
     A            DBKEY         29        12
     A                                      SPACEA(1)
     A                                     5'AT NUMBER :'
     A            DBASE          3  0     17
     A                                      SPACEA(2)
     A                                    50'********************************'
     A*
     A          R DETAIL
     A                                    49'NUMBER OF EXTRACTED RECORDS : '
     A                                      SPACEB(2)
     A            NREC           5  0     79EDTWRD('    0')
     A*>>> ULX004 Begin >>>
      *------------------------------*
      ***  No Details record format  *
      *------------------------------*
      *
     A          R NODLI
     A                                      SPACEB(003)
     A                                    46
     A                                      '** NO DETAILS TO REPORT **'
      *
     A*<<< ULX004 End <<<
