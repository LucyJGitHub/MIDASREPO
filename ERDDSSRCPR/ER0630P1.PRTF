      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ER Customer Positions - Audit Report')
     A*****************************************************************
     A*                                                               *
     A*    ER0630P1 - PRODUCTION EXTRACT AUDIT REPORT                 *
     A*               CUSTOMER POSITIONS                              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ERL439   PT        Date 03Sep94               *
      *                 ER0357   TLi       Date 08Sep93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     A*    ERL439 - CUSTOMER AND SECURITY SHORTNAMES WERE INVERSED    *
     A*    ER0357 - AUDIT LIST CREATION FOR CUSTOMER POSITIONS EXTRACT*
     A*                                                               *
     A*****************************************************************
     A*
     A          R ER0630H1                  TEXT('(c) Misys International +
     A                                      Banking Systems, Ltd. +
     A                                      2005')
     A                                      SKIPB(003)
     A                                     2'Reference  ER0630P1'
     A            BJURPT        53   O    37
     A                                   101'Date'
     A            BJMRDT         7   O   107
     A                                   119'Page'
     A                                   125PAGNBR
     A                                      EDTCDE(Z)
     A                                    35'LUXEMBOURG'
     A                                      SPACEB(002)
     A                                    46'EUROPEAN REPORTING CUSTOM'
     A                                    71'ER POSITIONS - AUDIT REPORT'
     A                                    35'-----------'
     A                                      SPACEB(001)
     A                                    46'-------------------------'
     A                                    71'---------------------------'
     A                                     2'Branch  Customer  Report  '
     A                                      SPACEB(002)
     A                                    35'Security'
     A                                    55'Amount in'
     A                                    75'Ccy       Amount in'
     A                                   105'Ccy'
     A                                     2'        Number    Name'
     A                                      SPACEB(001)
     A                                    35'Shortname'
     A                                    55'Currency'
     A                                    75'          Base Currency'
     A                                     6'                               '
     A                                      SPACEB(001)
     A*%%***********************************************************************
     A          R ER0630D1
     A                                      SPACEB(001)
     A            XRCSBR         3A  O     2
     A            XRCSCN         6A  O    10
     A* ERL439 START
     A************XRCSSN        10A  O    35
     A************XRCSSC        10A  O    20
     A            XRCSSN        10A  O    20
     A            XRCSSC        10A  O    35
     A* ERL439 END
     A            XRIAMT        23A  O    50
     A            XRNMCY         3A  O    75
     A            XROAMT        23A  O    80
     A            XRCYCD         3A  O   105
     A*%%***********************************************************************
     A          R ER0630D2
     A                                      SPACEB(003)
     A                                    83'===================='
     A                                    42'TOTAL FOR THIS BRANCH :'
     A                                      SPACEB(001)
     A            XRTOT         23A  O    80
     A*%%***********************************************************************
     A          R ER0630E1
     A                                      SPACEB(004)
     A                                    52'***  NO DETAILS TO REPORT  ***'
     A*%%***********************************************************************
     A          R ER0630E2
     A                                      SPACEB(004)
     A                                    55'***  END OF REPORT  ***'
     A*%%***********************************************************************
