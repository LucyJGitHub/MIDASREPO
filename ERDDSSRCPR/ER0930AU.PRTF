      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas¦ABS ER Collateral Review Report')
      *****************************************************************
      *                                                               *
      *  Finastra - European Reporting                                *
      *                                                               *
      *  PRTF/ER0930AU - Collateral Review Report - Audit             *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX004  *CREATE    Date 06Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - Capital Adequacy                                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADAU
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      SKIPB(003)
     A                                     6'Reference ER0930AU'
     A            BJURPT    R        O    39REFFLD(URPT)
     A                                   101'Date'
     A            BJMRDT    R        O   106REFFLD(MRDT)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                   101'Time'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
     A                                    45'Guarantees and Collateral Review R-
     A                                      eport'
     A                                      SPACEA(001)
     A                                    45'----------------------------------
     A                                      ------'
     A                                      SPACEA(002)
      *
     A          R DBERROR
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
      *
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
     A          R NODTLS
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                    47'***  No details to report ***'
     A                                      SPACEB(3)
