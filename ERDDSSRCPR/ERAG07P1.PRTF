      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas AGDL Customers list')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/ERAG07P1 - Customers details for the AGDL Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 15May06               *
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX010  *CREATE    Date 07APR98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027A - Change Customer Number to alpha; post-103 build.   *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX010 - AGDL reporting statistiques sur les d{pôts garantis *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ERAG07P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    37'Customers Details for the AGDL +
     A                                      Report'
     A                                      SPACEA(1)
      *
     A                                    37'-------------------------------+
     A                                      ------'
     A                                      SPACEA(3)
      *
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A                                     3'Customer'
     A                                    19'<------------Customer------------>'
     A                                    60'Linked'
     A                                      SPACEA(1)
     A                                     3'Number'
     A                                    19'Name'
     A                                    47'Town'
     A                                    60'Customers'
     A                                      SPACEA(1)
     A                                     3'----------------------------------'
     A                                    37'--------------------------------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP1
     A                                      SPACEB(1)
      * Start /CSD027A/
     A**********  ADCUST         6  0      3                                                 CSD027A
     A            ADCUST         6A        3
      * End /CSD027A/
     A            BBCRNM        20        19
     A            BBCRTN        10        47
      * Start /CSD027A/
     A**********  ADLINK         6  0     60                                                 CSD027A
     A**********                            EDTCDE(Z)                                        CSD027A
     A            ADLINK         6A       60
      * End /CSD027A/
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP1
     A                                    54'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *----------------------------------------*
      ***  No record record format *
      *----------------------------------------*
      *
     A          R NOLIP1
     A                                    53'***  NO DETAILS TO REPORT  ***'
     A                                      SPACEB(3)
