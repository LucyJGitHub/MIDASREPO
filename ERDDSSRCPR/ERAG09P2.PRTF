      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas AGDL Consolidated AGDL Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/ERAG09P2 - Consolidated AGDL Report                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX010  *Create    Date 19Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus (Recompile only)           *
      *  ULX010 - AGDL reporting statistiques sur les dépôts garantis *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2
     A                                      TEXT('(c) Misys Interna+
     A                                      tional Banking Systems +
     A                                      Ltd. 2005')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE ERAG09P2'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                      SPACEA(001)
     A                                   101
     A                                      'PAGE'
     A                                   106
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    46
     A                                      'Consolidated AGDL Report'
     A                                      SPACEA(001)
      *
     A                                    46
     A                                      '------------------------'
     A                                      SPACEA(003)
      *
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A                                     3
     A                                      'Customer'
     A                                    15
     A                                      '<----Report name--->'
     A                                    45
     A                                      '<----Report town--->'
     A                                    74
     A                                      '<--Owned in     ---->'
     A            #2GCUR         3        86
     A                                      SPACEA(001)
     A                                     3
     A                                      'Linked'
     A                                      SPACEA(001)
     A                                     3
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------------------'
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP2
     A            #2LINK         6  0      3
     A                                      SPACEB(001)
     A            #2NAME        20        15
     A            #2TOWN        20        45
     A            #2TTOW        21        74
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP2
     A                                      SPACEB(003)
     A                                     3
     A                                      'Total : '
     A            #2COUN         5  0     12
     A                                    54
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
      *
      *----------------------------------------*
      ***  No record record format *
      *----------------------------------------*
      *
     A          R NOLIP2
     A                                      SPACEB(003)
     A                                    53
     A                                      '***  NO DETAILS TO REPORT  ***'
