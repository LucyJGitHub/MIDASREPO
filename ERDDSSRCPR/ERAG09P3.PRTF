      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas AGDL Summary Report')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/ERAG09P3 - AGDL Summary Report                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX010  *Create    Date 25Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus (Recompile only)           *
      *  ULX010 - AGDL reporting statistiques sur les dépôts garantis *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP3                    TEXT('(c) Misys Interna+
     A                                      tional Banking Systems +
     A                                      Ltd. 2005')
      *
     A                                      SKIPB(3)
     A                                     3'A adresser à :'
     A                                    53'Nom de l''établissement :'
     A                                      SPACEA(1)
     A                                     3'Institut Monétaire'
     A                                    53'_________________________'
     A                                      SPACEA(1)
     A                                     3'Luxembourgeois'
     A                                    53'_________________________'
     A                                      SPACEA(1)
     A                                     3'L-2983 Luxembourg'
     A                                      SPACEA(3)
      *
     A                                    23'STATISTIQUES SUR LES DEPOTS GARANT+
     A                                      IS'
     A                                      SPACEA(1)
      *
     A                                    23'----------------------------------+
     A                                      --'
      *
     A                                      SPACEA(3)
     A                                     6'Par ordre de grandeur'
     A                                    31'     Volume des'
     A                                    50'Nombre de '
     A                                    61'Dépots garantis'
     A                                      SPACEA(1)
     A                                    31'Dépots'
     A                                    38'(en mio'
     A            #3CUR1         3        46
     A                                    49')'
     A                                    50'  Droits'
     A                                    61'(en mio'
     A            #3CUR2         3        69
     A                                    72')'
     A                                      SPACEA(3)
      *
     A            #3LIM1        21         6
     A            #3CUR3         3        27
     A            #3IWN1        18        31
     A            #3NBI          9        50
     A            #3IWN2        18        60
     A                                      SPACEA(2)
     A            #3LIM2        21         6
     A            #3CUR4         3        27
     A            #3SWN1        18        31
     A            #3NBS          9        50
     A            #3SWN2        18        60
     A                                      SPACEA(2)
      *
     A                                    31'------------------'
     A                                    50'---------'
     A                                    60'------------------'
     A                                      SPACEA(2)
      *
     A                                     6'Total'
     A            #3TWN1        18        31
     A            #3NB           9        50
     A            #3TWN2        18        60
     A                                      SPACEA(2)
      *
     A                                     6'Signatures autorisées'
     A                                      SPACEB(2)
     A                                    59'Nom de l''employé :'
     A                                     6'et cachet : '
     A                                      SPACEB(1)
     A                                    59'___________________'
     A                                    59'Tél :______________'
     A                                      SPACEB(1)
      *
