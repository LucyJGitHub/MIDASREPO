      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas AGDL Deposit file list')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/ERAG14P1 - Deposit details for the AGDL report          *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX010  *CREATE    Date 15Jul99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX010 - AGDL reporting statistiques sur les d{pôts garantis *
      *           Change Control                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2005')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     6
     A                                      'REFERENCE ERAG14P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    37
     A                                      'Deposit details for the AGDL Repor-
     A                                      t'
     A                                      SPACEB(002)
      *
     A                                    37
     A                                      '-----------------------------------
     A                                      -'
     A                                      SPACEB(001)
      *
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A                                     3
     A                                      'Deal Number'
     A                                      SPACEB(003)
      *
      *--------------------------------*
      ***  Detail header record format *
      *--------------------------------*
      *
     A                                    19
     A                                      'Cust Number'
     A                                    31
     A                                      'Number of'
     A                                    47
     A                                      'Linked'
     A                                    31
     A                                      'Participants'
     A                                      SPACEB(001)
     A                                    47
     A                                      'Customer'
     A            ZA104        104         3
     A                                      SPACEB(001)
      *
      *-------------------------*
      ***  Detail record format *
      *-------------------------*
      *
     A          R DETAP1
     A                                      SPACEB(001)
     A            RRDEAL         6  0      3
     A            RRCNUM         6  0    +10
     A            RRPART         2  0    +10
     A            RRLIN1         6  0    +10
     A                                      EDTCDE(Z)
     A            RRLIN2         6  0     +2
     A                                      EDTCDE(Z)
     A            RRLIN3         6  0     +2
     A                                      EDTCDE(Z)
      *
      *--------------------------------*
      ***  End of report record format *
      *--------------------------------*
      *
     A          R ENDP1
     A                                      SPACEB(003)
     A                                    54
     A                                      '***  END OF REPORT  ***'
      *
      *----------------------------------------*
      ***  No record record format *
      *----------------------------------------*
      *
     A          R NOLIP1
     A                                      SPACEB(003)
     A                                    53
     A                                      '***  NO DETAILS TO REPORT  ***'
