      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ER IT - Decision Table Account code')            *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  PRTF/ERIT35P1 - ER IT - Decision Table Account code List     *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997.          *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. UIT021  *Create    Date 13JAN97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------*
      * Report Header Record Format *
      *-----------------------------*
      *
     A          R HEADLI                    TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997.')
     A                                      SKIPB(3)
     A                                     2'REFERENCE ERIT35P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    50'ER Decision Table'
     A            XX             2        68
     A                                    71'- Account Code'
     A                                      SPACEA(1)
     A                                    50'__________________________________-
     a                                      _'
     A                                      SPACEA(3)
     A                                     2'ACCOUNT'
     A                                    50'LINE ITEMS'
     A                                    75'COMPL. ITEMS'
     A                                   100'LAST ACTIVITY'
     A                                      SPACEA(1)
     A                                     3'CODE'
     A                                    10'NAME'
     A                                    36'TYPE'
     A                                    47'DEBIT'
     A                                    57'CREDIT'
     A                                    73'DEBIT'
     A                                    85'CREDIT'
     A                                   100'ACTION   DATE'
     A                                      SPACEA(1)
      *
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------'
     A                                      SPACEA(1)
      *
      *-----------------------------*
      * Report Detail Record Format *
      *-----------------------------*
      *
     A          R DETLI
     A
     A            XRACOD         4         3
     A            XRNARA        25        10
     A            XRTYPE         1        37
     A            XRDEBI         7  0     46
     A            XRCRED         7  0     56
     A            XRCPDB         7  0     72
     A            XRCPCR         7  0     84
     A            XRTYLC         1       102
     A            XRLCD          7       107
     A                                      SPACEA(1)
      *
      *-----------------------------*
      * End of Report Record Format *
      *-----------------------------*
      *
     A          R ENDLI
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
