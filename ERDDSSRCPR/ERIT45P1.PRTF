      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ER IT - Decision table Transaction Type')        *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  PRTF/ERIT45P1 - ER IT - Decision table Transaction Type List *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997.          *
      *                                                               *
      *  Last Amend No. LUC139             Date 10Feb21               *
      *  Prev Amend No. UIT021 *Create     Date 13JAN97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT021 - BOI Monthly Reporting                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------*
      * Report Header Record Format *
      *-----------------------------*
      *
     A          R HEADLI                    TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997.')
     A                                      SKIPB(3)
     A                                     2'REFERENCE ERIT45P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    50'ER Decision Table'
     A            XX             2        68
     A                                    71'- Transaction Type'
     A                                      SPACEA(1)
     A                                    50'__________________________________-
     A                                      _____'
     A                                      SPACEA(3)
     A                                     2'MOD.'
     A                                     7'TRANSACTION'
     A                                    61'LINE ITEMS'
     A                                    82'COMPL. ITEMS'
     A                                   105'LAST ACTIVITY'
     A                                      SPACEA(1)
     A                                     3'ID'
     A                                     7'TYPE'
     A                                    17'DESCRIPTION'
     A                                    59'DEBIT   CREDIT'
     A                                    81'DEBIT   CREDIT'
     A                                   105'ACTION   DATE'
     A                                      SPACEA(1)
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------'
     A                                      SPACEA(1)
      *
      *------------------------------*
      * Report Details Record Format *
      *------------------------------*
      *
     A          R DETLI
     A
     A            XRMOD          2         3
     A            XRTYPE         8         7
     A            XRNARA        35        17
     A            XRDEBI         7  0     58
     A            XRCRED         7  0     66
     A            XRCPDB         7  0     80
     A            XRCPCR         7  0     88
     A            XRTYLC         1       107
     A            XRLCD          7       112
     A                                      SPACEA(1)
      *
      *-----------------------------*
      * End of Report Record Format *
      *-----------------------------*
      *
     A          R ENDLI
     A                                      SPACEB(3)
     A                                    56'*** END OF REPORT ***'
