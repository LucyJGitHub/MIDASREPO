      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ER IT Interface to FRS - Audit List')            *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  PRTF/ERIT60AU - ER IT Interface to FRS - Audit List          *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997.          *
      *                                                               *
      *  Last Amend No. MD058100           Date 20May21               *
      *  Prev Amend No. LUC139             Date 08Jan21               *
      *                 UIT021             Date 05mar97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD058100 - Rollup of additional BoI onsite fixes at FbMidas  *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - BOI Monthly Reporting.                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------*
      * Report Header Record Format *
      *-----------------------------*
      *
     A          R HEADAU                    TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      *
     A                                      SKIPB(3)
     A                                     2'REFERENCE ERIT60AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    50'Reporting Interface Processing'
     A                                      SPACEA(1)
     A                                    50'------------------------------'
     A                                      SPACEA(3)
      *
     A          R DBERAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3        30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
     A                                      SPACEA(2)
      *
      *-----------------------*
      * Control record format *
      *-----------------------*
      *
     A          R TRLRAU
     A                                     6'NUMBER OF RECORDS'
     A                                      SPACEA(1)
     A                                     6'-----------------'
     A                                      SPACEA(2)
     A                                     2'MIDAS Extract Input File     :'
     A            RCD            6  0     33
     A                                      EDTCDE(Z)
     A                                    42'(EREXTRPD)'
     A                                      SPACEA(1)
     A                                     2'Reporting Interface generated:'
     A            WTR            6  0     33
     A                                      EDTCDE(Z)
     A                                    42'('
     A            ERXXTR         8        43
     A                                    51')'
     A                                      SPACEA(2)
      *
      *--------------------------*
      * No Details record format *
      *--------------------------*
      *
     A          R NODLI
     A                                      SPACEB(003)
     A                                    46
     A                                      '** NO DETAILS TO REPORT **'
