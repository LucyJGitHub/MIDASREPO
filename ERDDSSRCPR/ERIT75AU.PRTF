      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ER IT Guarantees Management - Audit List')       *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting - Italy                           *
      *                                                               *
      *  PRTF/ERIT75AU - ER IT Guarantees Management - Audit List     *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. MD058100           Date 20May21               *
      *  Prev Amend No. LUC139             Date 04Jan21               *
      *                 UIT021             Date 19Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058100 - Rollup of additional BoI onsite fixes at FbMidas  *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------*
      * Report Header Record Format *
      *-----------------------------*
      *
     A          R HEADAU
     A                                      SKIPB(3)
     A                                     2'REFERENCE ERIT75AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    50'Guarantees Management - Audit'
     A                                      SPACEA(1)
     A                                    50'-----------------------------'
     A                                      SPACEA(2)
      *
     A          R DBERAU
     A                                     5'DATA BASE ERROR IN PROGRAM :'
     A            DBPGM         10        34
     A                                      SPACEA(1)
     A                                     5'FILE :'
     A            DBFILE        10        12
     A                                      SPACEA(1)
     A                                     5'KEY  :'
     A            DBKEY         29        12
     A                                      SPACEA(1)
     A                                     5'AT NUMBER :'
     A            DBASE          3        17
     A                                      SPACEA(2)
      *
     A          R TRLRAU
     A                                     6'NUMBER OF RECORDS'
     A                                      SPACEA(1)
     A                                     6'-----------------'
     A                                      SPACEA(2)
     A                                     2'MIDAS Extract Input File     :'
     A            RCD            6  0     33EDTWRD('    0 ')
     A                                    42'(ERNLTRPD)'
     A                                      SPACEA(1)
     A                                     2'MIDAS Extract Output File    :'
     A            WTR            6  0     33EDTWRD('    0 ')
     A                                    42'('
     A            ERXXTR         8        43
     A                                    51')'
     A                                      SPACEA(2)
     A                                    52'E N D   O F   R E P O R T'
     A                                      SPACEA(1)
     A                                    52'*************************'
