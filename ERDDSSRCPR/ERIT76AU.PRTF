      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ER IT Specific Provisions Setup - Audit')        *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting - Italy                           *
      *                                                               *
      *  PRTF/ERIT76AU - ER IT Specific Provisions Setup - Audit List *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. MD058100           Date 20May21               *
      *  Prev Amend No. LUC139             Date 04Jan21               *
      *                 UIT021             Date 19Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058100 - Rollup of additional BoI onsite fixes at FbMidas  *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------*
      * Report Header Record Format *
      *-----------------------------*
      *
     A          R HEADAU
     A                                      SKIPB(3)
     A                                     2'REFERENCE ERIT76'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    50'Specific Provisions Setup - Audit'
     A                                      SPACEA(1)
     A                                    50'---------------------------------'
     A                                      SPACEA(2)
      *
     A          R DBERAU
     A                                     5'DATA BASE ERROR IN PROGRAM :'
     A            DBPGM         10        34
     A                                      SPACEA(1)
     A                                     5'FILE :'
     A            DBFILE        10        12
     A                                      SPACEA(1)
     A                                     5'KEY  :'
     A            DBKEY         29        12
     A                                      SPACEA(1)
     A                                     5'AT NUMBER :'
     A            DBASE          3        17
     A                                      SPACEA(2)
      *
     A          R TRLRAU
     A                                     6'NUMBER OF RECORDS'
     A                                      SPACEA(1)
     A                                     6'-----------------'
     A                                      SPACEA(2)
     A                                     2'MIDAS Extract Input File     :'
     A            RCD            6  0     33EDTWRD('    0 ')
     A                                    42'(ERNLWKPD)'
     A                                      SPACEA(1)
     A                                     2'Number of Updated Records    :'
     A            UPD            6  0     33EDTWRD('    0 ')
     A                                      SPACEA(2)
     A                                    52'E N D   O F   R E P O R T'
     A                                      SPACEA(1)
     A                                    52'*************************'
