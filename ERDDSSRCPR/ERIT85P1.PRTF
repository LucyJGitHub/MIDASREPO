      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ER Production Extract Report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting - Italy                           *
      *                                                               *
      *  PRTF/ERIT85P1 - Production Extract Report                    *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. MD058100           Date 20May21               *
      *  Prev Amend No. LUC139             Date 04Jan21               *
      *                 UIT021             Date 26FEB97               *
      *  Prev Amend No.                    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058100 - Rollup of additional BoI onsite fixes at FbMidas  *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  UIT021 - Bank Of Italy Monthly Reporting                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **************
      ***  HEADER  *
      **************
     A          R ERIT85H1
     A                                      TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
      *
     A                                      SKIPB(003)
     A                                     2'REFERENCE ERIT85P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    36'Italy - European Reporting +
     A                                      Production Extract - Audit Report'
     A                                      SPACEA(001)
     A                                    36'---------------------------+
     A                                      ---------------------------------'
     A                                      SPACEA(002)
      *
     A                                     2'BOI VOCE/SOTTOVOCE :'
     A            RLINI          7   O    23
     A            RNARR         30   O    32
     A                                      SPACEA(002)
      *
     A                                     2'UIC'
     A                                     7'CCY'
     A                                    36'DECISION'
     A                                    46'UIC'
     A                                    51'CTRY'
     A                                    57'----------CUSTOMER----------'
     A                                   116'COMPLEMENTARY'
     A                                      SPACEA(001)
     A                                     2'CCY'
     A                                     7'CODE'
     A                                    13'ORIGIN OF DATA'
     A                                    38'KEY'
     A                                    46'CTR'
     A                                    51'CODE'
     A                                    57'NUMBER'
     A                                    65'REPORT NAME'
     A                                    87'AMOUNT IN ORIGIN. CCY'
     A                                   110'D/C'
     A                                   115'ACCOUNTING ITEM'
     A                                      SPACEA(001)
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -------------------------'
     A                                      SPACEA(1)
      *
      *******************
      ***  DETAIL LINE  *
      *******************
     A          R ERIT85D1
      *
     A                                      SPACEB(001)
     A  20        RUCCY          3   O     2TEXT('UIC Currency code')
     A  20        RCCY           3   O     7TEXT('Currency code')
     A            ROOD1          4   O    13TEXT('Origin of data')
     A            ROOD2         15   O    19TEXT('Origin of data')
     A            RDECK          8   O    36TEXT('Decision Key')
     A            RUCTR          3   O    46TEXT('UIC Country code')
     A            RCTR           2   O    52TEXT('Country code')
     A            RCUST          6   O    57TEXT('Customer number')
     A            RCRNM         20   O    65TEXT('Customer report name')
     A            RBVFX         22   O    87TEXT('Amount in original currency')
     A            RCRDR          1   O   111TEXT('Debit/credit indicator')
     A            RCPAC          7   O   119TEXT('Complementary accounting item-
     A                                      ')
      *
      **********************
      ***  TOTAL CURRENCY  *
      **********************
     A          R ERIT85T1
      *
     A                                      SPACEB(001)
     A                                    87'---------------------'
     A                                      SPACEA(001)
     A                                    13'Total for UIC Currency'
     A            RUCCY          3   O    36
     A                                    40'(Currency code'
     A            RCCY           3   O    55
     A                                    58'):'
     A            RTOCCY        22   O    87TEXT('Total Currency')
     A            RDCCCY         1A  O   111TEXT('Debit/credit ind. tot.ccy')
     A                                      SPACEA(001)
      *
      *********************
      ***  End of report  *
      *********************
     A          R ERIT85E1
      *
     A                                      SPACEB(003)
     A                                    54'***  END OF REPORT  ***'
      *
