      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ER Italy I.C.D. report')                         *
      *****************************************************************
      *                                                               *
      *   Midas - European Reporting                                  *
      *                                                               *
      *   ERIT95P1 - Italy I.C.D. report                              *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997           *
      *                                                               *
      *  Last Amend No. LUC139            Date 10Feb21                *
      *  Prev Amend No. UIT021  *Create   Date 13JAN97                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R ERIT95F1
     A                                      TEXT('(C) Midas-Kapiti Internationa-
     A                                      l Ltd. 1997')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE ERIT95P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    52'ER '
     A                                      SPACEB(002)
     A            P1UCCD         2        55
     A                                    58'Installation Control Data'
      *
     A                                    52'-------------------------------'
     A                                      SPACEB(001)
      *
     A                                     4'LE Repayments Management (Y/N) . .-
     A                                       . . . . . . . . . . . . . . :'
     A                                      SPACEB(004)
     A            P1LERP         1        70
      *
     A                                     4'Collateral Reviewal Days in Advanc-
     A                                      e. . . . . . . . . . . . . . :'
     A                                      SPACEB(002)
     A            P1CORD         2        70
      *
     A                                     4'Provisions Reviewal Days in Advanc-
     A                                      e. . . . . . . . . . . . . . :'
     A                                      SPACEB(002)
     A            P1PRRV         2        70
      *
     A                                     4'Last Activity Action . . . . . . .-
     A                                       . . . . . . . . . . . . . . :'
     A                                      SPACEB(002)
     A            P1TYLC         1        70
      *
     A                                     4'Last Activity Date . . . . . . . .-
     A                                       . . . . . . . . . . . . . . :'
     A                                      SPACEB(002)
     A            P1LCD          7        70
      *
     A                                    57'*** END OF REPORT ***'
     A                                      SPACEB(003)
