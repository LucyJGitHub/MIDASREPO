      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ER Records Type 29 Generation - Audit List')
      *****************************************************************
      *                                                               *
      *  Midas - European Repoerting Module                           *
      *                                                               *
      *  PRTF/ERLU151AU - Records Type 29 Generation - Audit          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX046  *CREATE    Date 29Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX046 - Circulars 2002/170-174-175 Integration              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2005')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE ERLU151AU'
     A            RBJURPT   R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            RBJMRDT   R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                    46
     A                                      'Records Type 29 Generation - AUDIT'
     A                                      SPACEA(001)
      *
     A                                    46
     A                                      '----------------------------------'
     A                                      SPACEA(003)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                     6
     A                                      'Number of records'
     A                                      SPACEA(001)
     A                                     6
     A                                      '-----------------'
     A                                      SPACEA(002)
     A                                     6
     A                                      'MIDAS Extract Input File :'
     A            RCOUNT         6  0     37
     A                                      EDTCDE(3)
     A                                    48
     A                                      '(ERLUTRPD)'
     A                                      SPACEA(001)
     A                                     6
     A                                      'Records Type 29 generated:'
     A            RGENER         6  0     37
     A                                      EDTCDE(3)
     A                                    48
     A                                      '(ERLU29PP)'
     A                                      SPACEA(005)
     A                                    52
     A                                      'E N D   O F   R E P O R T'
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
