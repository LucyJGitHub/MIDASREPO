      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ER CAB Extracted Transactions Report')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  PRTF/ERLU75P1 - CAB Extracted Transactions Report            *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX008             Date 25Jul00               *
      *                 180074             Date 08Jun00               *
      *                 ULX008  *CREATE    Date 23Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX008 - "Commissariat aux Bourses" - Change Control C       *
      *  180074 - Wrong Details Printed                               *
      *  ULX008 - CSSF Statutory Reporting : Commissariat aux Bourses *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Finastra +
     A                                      International Banking +
     A                                      Limited 2005')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ERLU57P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            RDATE          7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    45'CAB Extracted Transactions Report'
     A                                    85'BRANCH'
     A            RBRCD          3        92
     A                                    96'-'
     A            RBRNM         30        98
     A                                   132' '
     A                                      SPACEA(1)
      *
     A                                    45'---------------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     3'TRADE'
     A                                    12'ACT'
     A                                    17'P/S'
     A                                    21'SECURITY'
     A                                    33'SECURITY'
     A                                    42'REPORT NAME'
     A                                    76'SECURITY CODE'
     A                                    93'CCY'
      *** >>> 180074                                                      180074
     A****                               108'NOMINAL'                     180074
     A                                   107'NOMINAL'
      *** <<< 180074                                                      180074
     A                                   119'PRICE'
     A                                      SPACEA(1)
      *
     A                                     3'COUNTERPARTY'
     A                                    34'CITIZ.'
     A                                    42'INVESTMENT COMPANY NAME'
     A                                   104'CODE'
     A*** ULX008 BEGIN ***
     A                                   111'MARKET CENTRE'
     A*** ULX008 END ***
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A            RMODU          2         3
     A            RTRAN          6         6
     A            RACTN          1        14
     A            RPSID          2        17
     A            RSESN         10        21
     A            RSRPN         40        33
     A            RSCOD         15        76
     A            RICUR          3        93
      *** >>> 180074                                                      180074
     A***         RNOML         17        98                              180074
     A***         RPRIC         14       116                              180074
     A            RNOML         16        98
     A            RPRIC         16       115
      *** <<< 180074                                                      180074
     A                                      SPACEA(1)
     A            RCNUM          6         3
     A            RCSSN         10        11
     A            RCRTN         10        21
     A            RCNCZ          2        35
     A            RINNM         60        42
     A            RINCC          4       104
     A*** ULX008 BEGIN ***
     A            RMRKT          2       111
     A*** ULX008 END ***
     A                                      SPACEA(2)
      *
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
      *
