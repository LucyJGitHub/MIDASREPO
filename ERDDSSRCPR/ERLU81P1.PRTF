      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas ER CAD Audit Report - List')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  PRTF/ERLU81P1 - CAD Audit - List                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. CAD005 PT          Date 20Jul98               *
      *                 ULX004             Date 28MAR97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  CAD005 - Title changed to include 'Integrated'               *
      *  ULX004 - Capital Adequacy                                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2005')
      *
     A                                      SKIPB(003)
     A                                     3
     A                                      'REFERENCE ERLU81P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
      *CAD005 Begin ***************************************************
     A*********                           40
     A*********                             'LUXEMBURG EUROPEAN REPORTING - CAD-
     A*********                              AUDIT REPORT'
     A*********                             SPACEA(001)
     A*********                           40
     A*********                             '-----------------------------------
     A*********                             -------------'
     A*********                             SPACEA(002)
      *
     A                                    35
     A                                      'LUXEMBURG EUROPEAN REPORTING - INT-
     A                                      EGRATED CAD AUDIT REPORT'
     A                                      SPACEA(001)
     A                                    35
     A                                      '-----------------------------------
     A                                      ------------------------'
     A                                      SPACEA(002)
      *CAD005 End *****************************************************
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     3
     A                                      'PRODUCT CODE:'
     A            RPRDC          4A       17
     A            RPNAR         60A       24
     A                                      SPACEA(001)
      *
     A                                     3
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------'
     A                                      SPACEA(001)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETLP1
     A                                      SPACEA(002)
      *
     A                                     3
     A                                      'ORIGIN OF DATA:'
     A**Start of change CER001
     A**********  RORIG         20A       +1                                                  CER001
     A            RORIG         26A       +1
     A**End of change CER001
     A                                    +7
     A                                      'DECISION KEY:'
     A**Start of change CER001
     A**********  RDECK          8A       +1                                                  CER001
     A            RDECK         14A       +1
     A**End of change CER001
     A                                    +3
     A                                      'CURRENCY:'
     A            RCCYL          3A       +1
     A                                    +4
     A                                      'BOOK TYPE:'
     A            RBKTP          1A       +1
     A                                     6
     A                                      'CUSTOMER NUMBER:'
     A                                      SPACEB(001)
     A            RCUST          6A       +1
     A                                    +4
     A                                      'REPORT NAME:'
     A            RCRNM         20A       +1
     A                                    +5
     A                                      'COUNTRY:'
     A            RCUCL          2A       +1
     A                                    +6
     A                                      'ECONOMIC SECTOR:'
     A            RECOS          4A       +1
     A                                     6
     A                                      'ISSUER NUMBER:'
     A                                      SPACEB(001)
     A            RISSR          6A       +3
     A                                    +4
     A                                      'REPORT NAME:'
     A            RIRNM         20A       +1
     A                                    +5
     A                                      'COUNTRY:'
     A            RLISS          2A       +1
     A                                    +6
     A                                      'ECONOMIC SECTOR:'
     A            RISEC          4A       +1
     A                                    +3
     A                                      'SECURITY:'
     A            RSESN         10A       +1
     A                                    41
     A                                      'IN ORIGINAL CCY'
     A                                      SPACEB(002)
     A                                    66
     A                                      'IN REPORTING CCY'
     A                                     9
     A                                      'BOOK VALUE:'
     A                                      SPACEB(001)
     A            RAMNT         21A      +15
     A            RLAMT         21A       +5
     A                                    +6
     A                                      'DATE A:'
     A            RDATA          7A      +10
     A                                     9
     A                                      'NOTIONAL VALUE:'
     A                                      SPACEB(001)
     A            RNOMA         21A      +11
     A            RLNOM         21A       +5
     A                                    +6
     A                                      'DATE B:'
     A            RDATB          7A      +10
     A                                     9
     A                                      'MARKET VALUE 1:'
     A                                      SPACEB(001)
     A            RMKT1         21A      +11
     A            RLMK1         21A       +5
     A                                    +6
     A                                      'EXERCISE DATE:'
     A            REXDA          7A       +3
     A                                     9
     A                                      'MARKET VALUE 2:'
     A                                      SPACEB(001)
     A            RMKT2         21A      +11
     A            RLMK2         21A       +5
     A                                    +6
     A                                      'SWAP MATURITY:'
     A            RFMSW          7A       +3
     A                                     9
     A                                      'DERIVATIVE MARKET VALUE:'
     A                                      SPACEB(001)
     A            RDEMK         21A       +2
     A            RLDMK         21A       +5
     A                                    +6
     A                                      'START DATE:'
     A            RSTDA          7A       +6
     A                                     9
     A                                      'ORIGINAL CONTRACT VALUE:'
     A                                      SPACEB(001)
     A            RORCV         21A       +2
     A            RLOCV         21A       +5
     A                                    +6
     A                                      'MATURITY DATE:'
     A            RFMDT          7A       +3
     A                                     9
     A                                      'OPTION EXERCISE PRICE:'
     A                                      SPACEB(001)
     A            REXPR         21A       +4
     A            RLEXP         21A       +5
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
