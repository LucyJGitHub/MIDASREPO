     A****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas ER ASSETS / LIABILITIES - Audit Report')
     A*****************************************************************
     A*                                                               *
     A*    ERLU90P1 - PRODUCTION EXTRACT AUDIT REPORT                 *
     A*                                                               *
      *  (c) Finastra International Limited 2005                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 61875  TL          Date 15Oct93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     A*    61875 - NEW LIST WITH ASSETS AND LIABILITIES               *
     A*                                                               *
     A*****************************************************************
     A*
     A          R ERLU90H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      SKIPB(003)
     A                                     2'Reference  ERLU90'
     A            BJURPT        53   O    37
     A                                   101'Date'
     A            BJMRDT         7   O   107
     A                                   119'Page'
     A                                   125PAGNBR
     A                                      EDTCDE(Z)
     A                                    35'LUXEMBOURG'
     A                                      SPACEB(002)
     A                                    46'EUROPEAN REPORTING PRODUC'
     A                                    71'TION EXTRACT - AUDIT REPORT'
     A                                    35'-----------'
     A                                      SPACEB(001)
     A                                    46'-------------------------'
     A                                    71'---------------------------'
     A*%%***********************************************************************
     A          R ERLU90H3
     A                                      SPACEB(002)
     A  60                                 2'ASSETS'
     A N60                                 2'LIABILITIES'
     A                                    60'Amount in Report currency'
     A                                      SPACEA(001)
     A  60                                 2'------'
     A N60                                 2'-----------'
     A                                    60'-------------------------'
     A                                      SPACEA(1)
     A*%%***********************************************************************
     A          R ERLU90D2
     A                                      SPACEB(001)
     A            BKSCHA         8  0O     2
     A            BKNARR        30A  O    15
     A            P1TOT2        16  3O    60EDTWRD(' ,   ,   ,   , 0 .   ')
     A            P2DRCR         1A  O    86
     A*%%***********************************************************************
     A          R ERLU90D3
     A                                      SPACEB(001)
     A                                    60'====================='
     A                                      SPACEA(1)
     A                                    17'Total for'
     A  61                                27'ASSETS :'
     A N61                                27'LIABILITIES :'
     A            BKTOT         16  3O    60EDTWRD(' ,   ,   ,   , 0 .   ')
     A            P3DRCR         1A  O    86
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A          R ERLU90D4
     A                                      SPACEB(002)
     A                                    17'**********************************-
     A                                      ***********************************'
     A                                      SPACEA(1)
     A                                    17'ASSETS LESS LIABILITIES :'
     A            DIFFA         16  3O    60EDTWRD(' ,   ,   ,   , 0 .   ')
     A            DRCR4          1A  O    86
     A                                      SPACEA(1)
     A                                    17'**********************************-
     A                                      ***********************************'
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A          R ERLU90E1
     A                                      SPACEB(004)
     A                                    52'***  NO DETAILS TO REPORT  ***'
     A*%%***********************************************************************
     A          R ERLU90E2
     A                                      SPACEB(004)
     A                                    55'***  END OF REPORT  ***'
     A*%%***********************************************************************
