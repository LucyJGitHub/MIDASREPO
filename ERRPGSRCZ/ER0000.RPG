     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER-Set Up Stat.Ret. flag & ERSTAT')
     H****************************************************************
     H*                                                              *
     H*          MIDAS ABS - EUROPEAN REPORTING                      *
     H*                                                              *
     H*        ER0000 - ER SET UP STAT. RETURNS FLAG AND EXTRACT     *
     H*                 PROCESSING CONTROL DATA AREA                 *
     H*                                                              *
      *  (c) Finastra International Limited 2005                      *
     H*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX024             Date 03Sep99               *
      *                 ER_R10             Date 18Oct94               *
     H*                                                              *
     F*****************************************************************
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F* ULX024 - Trade Innovation/CSSF Reporting Interface            *
     F* ER_R10   -   EUROPEAN REPORTING - LUXEMBOURG UPGRADE IN R10   *
     F*****************************************************************
     FSDMMODPDUF  E                    DISK
      *
     ILDA        UDS                            256
     I                                      132 183 DBLOT
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
      *
     IERSTAT     UDS                            256
     I                                        1   1 SDGL
     I                                        2   2 DLMM
     I                                        3   3 DLFX
     I                                        4   4 NAPS
     I                                        5   5 CSLN
     I                                        6   6 FCLT
     I                                        7   7 SECS
     I                                        8   8 FUOP
     I                                        9   9 FIIN
     I                                       10  10 TIIN                  ULX024
      * Program status data structure
     I           SDS
     I                                     *PROGRAM PGM
     C*================================================================
     C                     MOVE *BLANKS   DBLOT            *****************
      *
     C                     READ SDMMODD0                 80
     C           *IN80     IFEQ '1'                        - B1
     C                     MOVELPGM       DBPGM
     C                     MOVEL'001'     DBASE            *****************
     C                     MOVEL'SDMMODPD'DBFILE           ** DB ERROR 02 **
     C                     MOVEL'*NO RCD 'DBKEY            *****************
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     DUMP
     C                     GOTO EOJ
     C                     END                             - E1
      *
      * Set Statutory Returns Flag on if necessary:
     C***********BGSTRT    IFNE 'Y'                                       ER_R10
     C           BGLRIN    IFNE 'Y'                                       ER_R10
     C***********          MOVE 'Y'       BGSTRT                          ER_R10
     C                     MOVE 'Y'       BGLRIN                          ER_R10
     C                     UPDATSDMMODD0
     C                     END
      *
      * Set ERSTAT flags corresponding to existing modules:
     C           BGSDGL    IFEQ 'Y'
     C                     MOVE 'Y'       SDGL
     C                     END
     C           BGDLMM    IFEQ 'Y'
     C                     MOVE 'Y'       DLMM
     C                     MOVE 'Y'       NAPS
     C                     END
     C           BGDLFX    IFEQ 'Y'
     C                     MOVE 'Y'       DLFX
     C                     END
     C           BGCSLN    IFEQ 'Y'
     C                     MOVE 'Y'       CSLN
     C                     MOVE 'Y'       FCLT
     C                     END
     C           BGSECS    IFEQ 'Y'
     C                     MOVE 'Y'       SECS
     C                     END
     C           BGFUOP    IFEQ 'Y'
     C                     MOVE 'Y'       FUOP
     C                     END
     C           BGFIIN    IFEQ 'Y'
     C                     MOVE 'Y'       FIIN
     C                     END
     C           BGN3ST    IFEQ 'Y'                                       ULX024
     C                     MOVE 'Y'       TIIN                            ULX024
     C                     END                                            ULX024
      *
     C           EOJ       TAG
     C                     MOVE '1'       *INLR
     C                     RETRN
