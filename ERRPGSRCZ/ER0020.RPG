     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Decision Table Selection')                       *
     F********************************************************************
     F*                                                                  *
     F*    MIDAS/ABS - EUROPEAN REPORTING                                *
     F*                                                                  *
     F*    ER0020 - DECISION TABLE SELECTION                             *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     F*                                                                  *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER045             Date 20Jul09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG10111R          Date 26Jan06               *
      *                 BUG10111           Date 25Jan06               *
      *                 CER001             Date 25Apr05               *
      *                 132893             Date 16Apr98                  *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank (Recompile)  *
      *  BUG10111R-Display error message                              *
      *  BUG10111-Display error message - cannot process subfile      *
      *           request (recompile)                                 *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F*  132893  -  SPF/RCF for IBLC/BNB on DBA2.3                       *
     F*                                                                  *
     F********************************************************************
     F*
     F*
     F*****************************************************************
     F*
     FSDCTRYL1IF  E           K        DISK
     F*
     FERICDRL1IF  E           K        DISK
     F*
     FSDBANKPDIF  E           K        DISK
     F*
     FER0020DFCF  E                    WORKSTN
     F                                        RRN   KSFILE ER0020S1
     E*****************************************************************
     E                    CPY@    1   1 80
     I*****************************************************************
     I* PROGRAM DATA STRUCTURE
     IPGMDS     ESDSY2PGDSP
     I*
     ILDA         DS                            256
     I******                                132 141 DBFILE                132893
     I                                      134 141 DBFILE                132893
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DS TO SETUP THE NAME OF CALLED PROGRAM
     I            DS
     I                                        1   2 ER
     I                                        3   4 XX
     I                                        5   6 YY
     I                                        1   6 ERXXYY
     I*
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     IDSFDY     E DSDSFDY                                                 132893
      *                                                                   132893
     ISDMMOD    E DSSDMMODPD                                              132893
      *                                                                   132893
     I** DS TO SETUP THE NAME OF COMPONENT                                132893
     I            DS                                                      132893
     I                                        1   3 ERC                   132893
     I                                        4   5 WXX                   132893
     I                                        6   7 WYY                   132893
     I                                        4   7 WXXYY                 132893
     I*
     C**************************************************************************
     C* RECEIVE ACTION CODE PARAMETER 'A' ,'E' OR 'P'.
     C*
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C*
     C**************************************************************************
     C* MAIN PROCESSING                                                        *
     C**************************************************************************
     C* CALL INIT SUBROUTINE
     C**************************************************************************
     C                     EXSR INIT                       INITIATION
     C*
     C                     EXSR MAIN
     C* END OF PGM
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C**************************************************************************
     C** MAIN  - MAIN SUBROUTINE
     C**************************************************************************
     C*
     C           MAIN      BEGSR
     C*
     C                     EXSR INISFL                     INIT. SUBFILE
     C                     EXSR LOASFE                     LOAD SUBFILE ENQUIRY
     C                     Z-ADD1         RRN
     C*
     C* DO WHILE NOT F3 PRESSED
     C           *INKC     DOWEQ'0'                        - B1
     C*
     C* WRITE SUBFILE
     C                     WRITEER0020C0                   BOTTOM
     C                     EXFMTER0020C1                   SUBFILE
     C*
     C** CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C** ENTER HAS BEEN PRESSED
     C           *IN45     IFEQ '0'                        - B2
     C*
     C** MORE THAN ONE DECISION TABLE CHOOSEN
     C           DHACOD    IFNE *BLANKS                    - B3
     C           DHTRNT    ANDNE*BLANKS
     C                     MOVE '1'       *IN20
     C                     MOVE 'ER20001' ZAMSID
     C                     EXSR ZASNMS
     C                     ITER
     C                     ELSE                            - X3
     C                     MOVE '0'       *IN20
     C                     END                             - E3
     C*
     C** IF CORRECT SELECTION ON DECISION TABLE
     C           DHACOD    IFNE *BLANKS                    - B3
     C           DHTRNT    ORNE *BLANKS
     C*
     C                     MOVE 'ER'      ER
     C                     MOVE 'ERC'     ERC                             132893
     C           DHACOD    IFEQ 'X'                        - B4
     C                     MOVE '30'      YY
     C                     MOVE '30'      WYY                             132893
     C                     END                             - E4
     C           DHTRNT    IFEQ 'X'                        - B4
     C                     MOVE '40'      YY
     C                     MOVE '40'      WYY                             132893
     C                     END                             - E4
     C           ACTION    IFEQ 'P'                        - B4
     C                     MOVE '5'       YY
     C                     MOVE '5'       WYY                             132893
     C                     END                             - E4
     C*
     C** Retrive first changed records
     C           *IN81     IFEQ *ON                                                        BUG10111R
     C                     READCER0020S1                 32
     C*
     C           *IN32     IFEQ '0'                        - B4
     C                     MOVE DSCCTR    XX
     C                     MOVE DSCCTR    WXX                             132893
     C                     MOVE DSCTRY    PARCTR  2
     C                     MOVE *BLANKS   DSSELE
     C                     UPDATER0020S1
     C                     ELSE                            - X4
     C           *LOVAL    SETLLERICDRL1
     C                     READ ERICDRL1                 88
     C                     MOVE VDICCD    XX
     C                     MOVE VDICCD    WXX                             132893
     C                     MOVE VDUCCD    PARCTR
     C                     END                             - E4
     C                     ENDIF                                                           BUG10111R
     C*
     C** CALL PROGRAM
     C                     MOVE *BLANKS   RETRN   1
     C                     UNLCKLDA
     C*
     C           ACTION    IFEQ 'P'                        - B4
     C                     MOVEL*BLANKS   WWCLP   7                       132893
     C           ERC       CAT  WXXYY     WWCLP                           132893
     C******               CALL ERXXYY                 55                 132893
     C******               PARM           PARCTR                          132893
      *                                                                   132893
     C                     CALL 'FC0040X'              55                 132893
     C                     PARM *BLANKS   @RTCD                           132893
     C                     PARM ERXXYY    @RNAM  10                       132893
     C                     PARM WWCLP     @COTT  10                       132893
     C                     PARM '10001'   @CSEQ   5                       132893
     C                     PARM PARCTR    @PARM 100                       132893
     C                     PARM           @MBRC   1                       132893
     C                     PARM 'S'       @LVL    1                       132893
     C                     PARM *BLANKS   @ETTY   3                       132893
     C                     ELSE                            - X4
     C                     CALL ERXXYY                 55
     C                     PARM           ACTION
     C                     PARM           RETRN
     C                     PARM           PARCTR
     C                     END                             - E4
     C*
     C                     IN   LDA
     C*
     C** PROGRAM CALLED NOT FOUND
     C           *IN55     IFEQ '1'                        - B4
     C** CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C                     MOVE 'ER20002' ZAMSID
     C                     MOVELERXXYY    ZAMSDA
     C                     EXSR ZASNMS
     C                     ITER
     C                     END                             - E4
     C*
     C** CHECK RETURN CODE FROM CALLED PROGRAM
     C           RETRN     IFEQ '3'                        - B4
     C                     MOVE '1'       *INKC
     C                     ITER
     C                     END                             - E4
     C*
     C           RETRN     IFEQ 'E'                        - B4
     C           *LOCK     IN   LDA                                       132893
     C                     MOVELERXXYY    DBFILE
     C                     MOVE RETRN     DBKEY
     C                     MOVEL'002'     DBASE
     C                     OUT  LDA                                       132893
     C                     EXSR *PSSR
     C                     ELSE                            - X4
     C                     MOVE *BLANKS   DHACOD
     C                     MOVE *BLANKS   DHTRNT
     C                     END                             - E4
     C*
     C                     END                             - E3
     C*
     C                     END                             - E2
     C*
     C** REFRESH PROCESSING ==> REBUILD SUBFILE
     C           *IN05     IFEQ '1'                        - B2
     C                     MOVE *BLANKS   DHACOD
     C                     MOVE *BLANKS   DHTRNT
     C                     MOVE '0'       *IN20
     C                     EXSR INISFL
     C                     EXSR LOASFE
     C                     Z-ADD1         RRN
     C                     END                             - E2
     C*
     C                     END                             - E1
     C*
     C                     ENDSR
     C*
     C**************************************************************************
     C* INITIALIZE SUBFILE
     C**************************************************************************
     C*
     C           INISFL    BEGSR
     C*
     C                     MOVE '0'       *IN80
     C                     MOVE '0'       *IN81
     C                     WRITEER0020C1
     C                     MOVE '1'       *IN80
     C                     Z-ADD0         RRN     30
     C*
     C                     ENDSR
     C*
     C**************************************************************************
     C* LOAD SUBFILE
     C**************************************************************************
     C*
     C           LOASFE    BEGSR
     C*
     C* INIT SCREEN LINE NUMBER
     C                     Z-ADD0         WWCPT   20
     C*
     C           *LOVAL    SETLLERICDRL1
     C*
     C* READ RECORD
     C                     READ ERICDRL1                 30
     C*
     C* LOAD SUBFILE
     C           *IN30     DOWEQ'0'                        - B1
     C*
     C           VDUCCD    IFNE *BLANKS                    - B2
     C*
     C           VDUCCD    CHAINSDCTRYL1             88
     C           *IN88     IFEQ '1'                        - B3
     C           *LOCK     IN   LDA                                       132893
     C                     MOVEL'SDCTRYL1'DBFILE
     C                     MOVE VDUCCD    DBKEY
     C                     MOVEL'001'     DBASE
     C                     OUT  LDA                                       132893
     C                     EXSR *PSSR
     C                     ELSE                            - X3
     C*
     C* LOAD FIELDS
     C                     ADD  1         RRN        81
     C                     ADD  1         WWCPT            SCREEN LINE MBR
     C*
     C* SET UP SUBFILE FIELDS
     C*
     C                     MOVE *BLANKS   DSSELE
     C                     MOVE VDUCCD    DSCTRY
     C                     MOVELA4CNNM    DSFAVA
     C                     MOVE VDICCD    DSCCTR
     C* WRITE IN SUBFILE
     C                     WRITEER0020S1
     C*
     C                     END                             - E3
     C                     END                             - E2
     C*
     C* READ NEXT RECORD
     C                     READ ERICDRL1                 30
     C*
     C                     END                             - E1
     C*
     C                     ENDSR
     C**************************************************************************
     C* INITIAL ROUTINE                                                        *
     C**************************************************************************
     C*
     C           INIT      BEGSR
     C*
     C           *NAMVAR   DEFN           LDA
     C                     MOVEL'ER0020'  DBPGM                           132893
     C*
     C** INITIALIZE ALL INDICATORS
     C                     MOVE '0'       *IN
     C*
     C** SETUP 'AMEND'
     C                     MOVE '0'       *IN15
     C*
     C** SETUP MODE FOR SCREEN ACCORDING TO PARAMETER PASSED
     C           ACTION    IFEQ 'P'                        - B1
     C                     MOVEL'PRINT  ' MODE
     C                     ELSE                            - X1
     C           ACTION    IFEQ 'E'                        - B2
     C                     MOVEL'ENQUIRY' MODE
     C                     ELSE                            - X2
     C                     MOVE 'AMEND  ' MODE
     C                     END                             - E2
     C                     END                             - E1
     C*
     C*   Select the program MSGQ for error msg.
     C                     MOVEL'*'       TOPQ   10
     C                     MOVEL'*PRV'    TOPRQ   5
     C                     MOVEL'*'       DDPGMQ
     C**********           MOVEL'ERUSRMSG'PMSGF  10                                           CER001
     C                     MOVEL'STUSRMSG'PMSGF  10                                           CER001
     C*
     C*   Fill in fields for subroutine ZASNMS
     C                     MOVEL'ER0020'  ZAPGM  10        Message queue
     C**********           MOVEL'ERUSRMSG'ZAMSGF 10        Message file                       CER001
     C                     MOVEL'STUSRMSG'ZAMSGF 10        Message file                       CER001
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
     C*
     C                     READ SDBANKPD                 88
     C*
     C                     CALL 'AOMMODR0'                                132893
     C                     PARM '*DBERR ' @RTCD   7                       132893
     C                     PARM '*FIRST ' @OPTN   7                       132893
     C           SDMMOD    PARM SDMMOD    DSFDY                           132893
      *                                                                   132893
     C           BGMBIN    IFEQ 'Y'                                       132893
     C                     MOVEL'Y'       @MBRC                           132893
     C                     ENDIF                                          132893
     C                     ENDSR
     C****************************************************************
     C*  ZASNMS : SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE            *
     C****************************************************************
     C           ZASNMS    BEGSR
     C*
     C**   message file specified use PMSGF
     C                     MOVELPMSGF     ZAMSGF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C**
     C**   Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C                     MOVEL*BLANK    ZAMSGF           Message file
     C                     MOVEL*BLANK    ZAMSID           Message id.
     C**
     C                     ENDSR
     C*
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - PROGRAM ERROR HANDLING ROUTINE                 *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY -                                                *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C                     DUMP
     C*
     C           #PSSR0    ENDSR
     C********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
