     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER Lump-Sum Provision Rates List')
     F********************************************************************
     F*                                                                  *
     F*     MIDAS/ABS  EUROPEAN REPORTING - LUXEMBOURG                   *
     F*                                                                  *
     F*     ER0060P   - LUMP-SUM PROVISION RATES - LIST                  *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. 132893             Date 15Apr98                  *
      *                 ERL371     CA      Date 16Dec93                  *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F*  132893  - RCF ON DBA2.3                                         *
     F*  ERL371  - IML 1.4 DEVELOPMENT                                   *
     F*                                                                  *
     F********************************************************************
     F*
     FSDBANKL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     FERLUMPL0IF  E           K        DISK
     F                                              KINFSR *PSSR
     F*
     FER0060P1O   E                    PRINTER
     F                                              KINFSR *PSSR
     F                                              KINFDS SPOOL1           1328
     F*****************************************************************   A
      *
      *   88 : Working indicator
      *   89 : Working indicator
      *
     *********************************************************************
      /EJECT
     *********************************************************************
     E*
     E** Array for Copyright
     E                    CPY@    1   1 80
     E** Array for ZEDIT
     E                    ZA1        16  1
     E                    ZA2        16  1
     *********************************************************************
      /EJECT
     *********************************************************************
     I**  Local Data Area to identify database errors
     ILDA         DS                            256
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
      ** File Information Data Structure for MMNNNNPY.                    132893
     ISPOOL1      DS                                                      132893
     I                                       83  92 SFILE1                132893
     I                                    B 123 1240SFNUM1                132893
      *                                                                   132893
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C*
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C*  MAIN Processing
     C*
     C********************************************************************
     C*
     C** INITIAL PROCESSING
     C                     EXSR P002
     C*
     C** DETAIL  PROCESSING
     C                     EXSR P003
     C*
     C** FINAL   PROCESSING
     C                     EXSR P004
     C*
     C                     SETON                     LR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C*  SUBROUTINE P002 - Initial processing
     C*
     C********************************************************************
     C           P002      BEGSR
      *
      ** Receive Parameter List.                                   132893
     C           *ENTRY    PLIST                                   132893
     C                     PARM           #SEQ    5                132893
     C                     PARM           #RLEV   1                132893
     C                     PARM           #RENT   3                132893
      *                                                            132893
      * RCF processing                                             132893
      *                                                            132893
     C                     EXSR RCFP1                              132893
      *                                                            132893
      *                                                            132893
      ** Bank information
     C           *LOVAL    SETLLSDBANKL1
     C                     READ SDBANKL1                 89
     C           *IN89     IFEQ *ON                        - B1
     C*
     C**  DBERROR 001
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDBANKL1'DBFILE
     C                     MOVEL'ER0060P' DBPGM
     C                     MOVEL'BANK'    DBKEY
     C                     EXSR *PSSR
     C                     ENDIF                           - E1
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C*  SUBROUTINE P003 - Detail processing
     C*
     C********************************************************************
     C           P003      BEGSR
     C*
     C** Output header
     C                     WRITEER0060H
     C*
     C                     READ ERLUMPL0                 89
     C*
     C           *IN89     DOWEQ*OFF                       - B1
     C*
     C** New page processing
     C           *IN88     IFEQ *ON
     C                     WRITEER0060H
     C                     MOVE '0'       *IN88
     C                     ENDIF
     C*
     C** Set up the printer fields
     C                     MOVE LSCODE    P1CODE
     C                     MOVE LSNARA    P1NARA
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE LSRATE    ZFIELD
     C                     Z-ADD2         ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    P1RATE
     C*
     C** Output detail
     C                     WRITEER0060D                88
     C*
     C** Read next record
     C                     READ ERLUMPL0                 89
     C*
     C                     ENDDO                           - E1
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C*  SUBROUTINE P004 - Final processing
     C*
     C********************************************************************
     C           P004      BEGSR
     C*
     C** Produce the run control report
     C           *IN88     IFEQ *ON
     C                     WRITEER0060H
     C                     END
     C*
     C                     WRITEER0060E
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
      ***************************************************************** 132893
      /TITLE SR/RCFP1                                                   132893
      ***************************************************************** 132893
      *                                                               * 132893
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. * 132893
      *                                                               * 132893
      ***************************************************************** 132893
     C           RCFP1     BEGSR                                        132893
      *                                                                 132893
      ** Ensure Report Spool File recorded by RCF.                      132893
     C                     Z-ADDSFNUM1    ZSNUM   60                    132893
      *                                                                 132893
     C                     CALL 'ZSFILE'                                132893
     C                     PARM #SEQ      SEQ     5                     132893
     C                     PARM           SENTY   3                     132893
     C                     PARM           SFILE1                        132893
     C                     PARM           ZSNUM                         132893
     C                     PARM           ZSERR   1                     132893
      *                                                                 132893
      ** If Error occurs during ZSFILE processing, then return to the   132893
      ** Calling Program.                                               132893
     C           ZSERR     IFEQ 'Y'                                     132893
     C                     SETON                     U7U8LR             132893
     C                     RETRN                                        132893
     C                     ENDIF                                        132893
      *                                                                 132893
     C                     ENDSR                                        132893
     C********************************************************************
     C*
     C* SUBROUTINE *PSSR - Error management subroutine
     C*
     C********************************************************************
     C           *PSSR     BEGSR
     C*
     C**  Print error message:
     C           DBASE     IFNE 0
     C                     WRITEER0060DB
     C                     ELSE
     C                     Z-ADD999       DBASE
     C                     END
     C*
     C                     DUMP
     C*
     C**  Update LDA with error information
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     OUT  LDA
     C*
     C                     MOVE '1'       *INLR
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     RETRN
     C*
     C                     ENDSR
     C********************************************************************
     C/EJECT
     C*******************************************************************
     C**
     C**  ZEDIT SUBROUTINE TO INSERT A DECIMAL POINT INTO A NUMERIC FIELD
     C**  AND TO BLANK OUT LEADING ZEROES.
     C**      INPUT FIELDS:   ZFIELD 16/
     C*                       ZADEC
     C*                       ZADIG - NOT ACTUALLY USED
     C*
     C*       ARRAYS    ZA1 ,ZA2   BOTH 16 ELEMENTS, EACH ONE BYTE LONG.
     C*
     C*       OUTPUT FIELD   ZFIELD
     C*
     C*******************************************************************
     C           ZEDIT     BEGSR                           ***  ZEDIT  **
     C**
     C**   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.
     C                     Z-ADDZADIG     ZADIG   20
     C                     Z-ADDZADEC     ZADEC   10
     C**
     C*       SET UP WORK FIELDS
     C*
     C                     MOVEA' '       ZA1
     C                     MOVEA' '       ZA2
     C*
     C                     Z-ADD16        Z1      20
     C                     Z-ADD16        Z2      20
     C*
     C           16        SUB  ZADEC     ZADEC2  20
     C*
     C*
     C                     MOVEAZFIELD    ZA1
     C*
     C*       CHECK TO SEE IF THERE ARE ANY DECIMAL PLACES
     C*
     C           ZADEC     COMP 0                        91
     C   91                GOTO ZT20
     C**
     C           ZT10      TAG                             ** ZT10 TAG **
     C**
     C*
     C*       SET UP DECIMALS
     C*
     C                     MOVE ZA1,Z1    ZA2,Z2
     C           Z1        SUB  1         Z1
     C           Z2        SUB  1         Z2
     C*
     C           Z2        COMP 0                        90
     C   90                GOTO ZT30
     C*
     C*       CHECK IF END OF DECIMALS
     C**
     C           Z2        COMP ZADEC2               90
     C   90                GOTO ZT10
     C*
     C**
     C*       PUT IN DECIMAL PLACE
     C*
     C                     MOVE '.'       ZA2,Z2
     C           Z2        SUB  1         Z2
     C*
     C           ZT20      TAG                             ** ZT20 TAG **
     C*
     C*       SET UP INTEGERS
     C*
     C                     MOVE ZA1,Z1    ZA2,Z2
     C           Z1        SUB  1         Z1
     C           Z2        SUB  1         Z2
     C**
     C           Z2        COMP 0                    90
     C   90                GOTO ZT20
     C*
     C*       PUT IN LEADING BLANKS
     C*
     C           ZT30      TAG                             ** ZT30 TAG **
     C**
     C                     Z-ADD1         Z2
     C**
     C           ZT40      TAG                             ** ZT40 TAG **
     C*
     C           ZA2,Z2    COMP '0'                  9090
     C   90      ZA2,Z2    COMP ' '                  9090
     C*
     C   90                GOTO ZT50
     C*
     C                     MOVE ' '       ZA2,Z2
     C           Z2        ADD  1         Z2
     C*
     C           Z2        COMP 16                     90
     C   90                GOTO ZT40
     C*
     C*       IF NO INTEGERS PUT IN LEADING ZERO
     C*
     C           ZT50      TAG                             ** ZT50 TAG **
     C*
     C                     Z-ADDZADEC2    Z2
     C  N91      Z2        SUB  1         Z2
     C           ZA2,Z2    COMP ' '                      90
     C   90                MOVE '0'       ZA2,Z2
     C*
     C*       SET UP OUTPUT FIELD
     C*
     C           ZT60      TAG                             ** ZT60 TAG **
     C*
     C                     MOVEAZA2       ZFIELD 16
     C**
     CSR         ZEND      ENDSR                           ** ZEND **
     C********************************************************************
     C/EJECT
     C********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
