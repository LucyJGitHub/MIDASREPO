     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('MIDAS¦ABS ER Collateral Review Report')
      *****************************************************************
      *                                                               *
      *  Midas¦ABS - European Reporting Module                        *
      *                                                               *
      *  ER0930   - ER Collateral Review Report                       *
      *                                                               *
      *  Function:  This program prints a new report that shows       *
      *             details of Collaterals that have reached the      *
      *             renewal period.                                   *
      *                                                               *
      *  (phase(s)) Call during Close of Business                     *
      *                                                               *
      *  Called By: RPG/ERC0930 - Er Collateral Review Report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 12May06               *
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX004             Date 06Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027A- Conversion of cust. no. to alpha (post 103 build)   *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - Capital Adequacy                                    *
      *                                                               *
      *****************************************************************
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  ===========================================                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *  ===============================                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *  BRBRK  - Process a change in branch                          *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZSEDIT - Edit an Amount                                      *
      *                                                               *
      *****************************************************************
      /TITLE FILES DESCRIPTION
      *****************************************************************
      *
      * Guarantees and Collaterals Extract File     Prefix RT
     FERGUEXL2IF  E           K        DISK
      *
     FER0930AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FER0930P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *
      *****************************************************************
      /TITLE E-SPECIFICATIONS
      *****************************************************************
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
      *****************************************************************
      /TITLE I-SPECIFICATIONS
      *****************************************************************
      *
      ** Local data area for database errors
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I                                        1 256 DBERR
      *
      ** Program data structure.
     IPGMDS     ESDSY2PGDSP
      *
      ** Data Area giving Installation Control Details
     IRUNDAT    E DSRUNDAT
      *
      ** File Information Data Structure for MMNNNNPY.
     ISPOOL1      DS
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
      ** File Information Data Structure for MMNNNNAU.
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      **  Dummy Data Structures used by Access Programs.
      *
      ** DS for access programs, long data structure
     IDSSDY     E DSDSSDY
      *
      ** External DS for Bank Details
     ISDBANK    E DSSDBANKPD
      *
      ** External DS for Customer Details
     ISDCUST    E DSSDCUSTPD
      *
      ** External DS for Branch Details
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          QQDFC#                                    CER001
      *
      ** External DS for Currency Details
     ISDCURR    E DSSDCURRPD
      *
      /COPY ZSRSRC,ZSEDITZ2
      /EJECT
      *****************************************************************
      **         P R O G R A M    S T A R T                           *
      *****************************************************************
      *
      ** Execute initial processing
     C                     EXSR INIT
      *
      ** Perform Detail Processing.
     C                     EXSR PROCES
      *
      **  Output Audit Report and End Program.
     C                     EXSR AUDIT
      *
      *****************************************************************
      **         P R O G R A M    E N D                               *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
     C           PROCES    BEGSR
      *
      ** Read first record from File.
     C                     READ ERGUEXD0                 89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records).
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ELSE
     C                     MOVE RTBRCA    WWBRCA  3
     C                     ENDIF
      *
      ** Do while End of File.
     C           *IN89     DOWEQ'0'                        - B1
      *
      ** If Secured by indicator not equal to 'S' (Specific Provisions)
     C           RTSBYI    IFNE 'S'                        - B2
      *
      ** Count records read.
     C                     ADD  1         WCOUNT
      *
      ** If change of branch OR first time processing
     C           RTBRCA    IFNE WWBRCA                     - B3
     C           WFIRST    OREQ *BLANKS
     C                     EXSR BRBRK
     C                     MOVE RTBRCA    WWBRCA
     C                     ENDIF                           - E3
      *
      ** Process Report Lines.
     C                     EXSR REPORT
     C                     ENDIF                           - E2
      *
      ** Read next record.
     C                     READ ERGUEXD0                 89
      *
     C                     ENDDO                           - E1
      *
      ** If no records selected go to subroutine AUDIT
     C           WCOUNT    IFEQ 0                          - B1
     C                     EXSR AUDIT
     C                     ELSE                            - X1
      *
      ** Write final records
     C                     EXSR WP1END
     C                     END                             - E1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
     C           REPORT    BEGSR
      *
      ** Fill file' fields into report' fields at correct formats
      *
     C                     Z-ADDRTRVDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRVDT            Review Date
      *
     C                     MOVE RTRVFQ    RRVFQ            Review Frequency
      *
      ** Access SDCUSTPD to retrieve Receiving Customer Details
     C                     MOVE RTRCUS    @KEY1   6
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1   6
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B1
     C                     MOVELRTRCUS    DBKEY     P
     C                     MOVE 'SDCUSTPD'DBFILE           ***************
     C                     Z-ADD002       DBASE            * DB ERROR 02 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF                           - E1
      *
     C                     MOVELBBCRNM    RRRNM            Receiving Cust name
      *
     C                     MOVELBBCRTN    RRRTN            Receiving Cust town
      *
     C                     MOVE RTUTSI    RUTSI            Used to secure ind
      *
     C                     MOVELRTUTSR    RUTSR            Used to secure ref
      *
     C                     MOVE RTUTSC    RUTSC            Used to secure ccy
      *
      ** Retrieve number of decimal place to format Used to secure amount
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM RTUTSC    @KEY2   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B2
     C                     MOVELRTUTSC    DBKEY     P
     C                     MOVE 'SDCURRPD'DBFILE           ***************
     C                     Z-ADD003       DBASE            * DB ERROR 03 *
     C                     EXSR *PSSR                      ***************
     C                     END                             - E2
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE RTUTSA    ZFLD15           Used to Secure Amt
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RUTSA
     C                     MOVE RTGSEQ    RGSEQ            Guarantee Sequence
      *
     C                     MOVE RTSBYI    RSBYI            Secured by indicator
      *
     C           RTSBYI    IFNE 'P'                        - B2
     C                     MOVELRTSBYR    RSBYR            Secured by reference
     C                     ELSE                            - X2
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE RTGPER    ZFLD15           Guarantee Percentage
     C                     Z-ADD0         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    ZOUT05  5
     C                     MOVE '%'       ZOUT05
     C                     MOVELZOUT05    RSBYR
     C                     END                             - E2
      *
     C                     MOVE RTGCCY    RGCCY            Guarantee currency
      *
      ** Access SDCUSTPD to retrieve Given by Customer Details
     C                     MOVE RTGVBY    @KEY1   6
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1   6
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B1
     C                     MOVELRTGVBY    DBKEY     P
     C                     MOVE 'SDCUSTPD'DBFILE           ***************
     C                     Z-ADD004       DBASE            * DB ERROR 04 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF                           - E1
      *
     C                     MOVELBBCRNM    RGRNM            Given By Cust name
      *
     C                     MOVELBBCRTN    RGRTN            Given By Cust town
      *
      *
     C                     MOVE RTGCOD    RGCOD            Guarantee Code
      *
      ** Retrieve number of decimal place to format Guarantee Amount
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM RTGCCY    @KEY2   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B2
     C                     MOVELRTGCCY    DBKEY     P
     C                     MOVE 'SDCURRPD'DBFILE           ***************
     C                     Z-ADD005       DBASE            * DB ERROR 05 *
     C                     EXSR *PSSR                      ***************
     C                     END                             - E2
      *
      ** Secured by indicator is equal to 'P' as amount
     C           RTSBYI    IFEQ 'P'                        - B1
      *
     C           RTUTSA    MULT RTGPER    WWUTSA 150
     C           WWUTSA    DIV  100       WWUTSA
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE WWUTSA    ZFLD15           Secured by amount in
     C                     Z-ADDA6NBDP    ZDECS            original ccy
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RGAMT
      *
     C                     ELSE                            - X1
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE RTGAMT    ZFLD15           Secured by amount in
     C                     Z-ADDA6NBDP    ZDECS            original ccy
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RGAMT
     C                     END
      *
     C                     MOVELRTNARR    RNARR            Narrative
      *
      ** Write out the record.
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           WP1REC    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
     C                     Z-ADD2         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Detail Record.
     C                     WRITEDETAIL
      *
      ** Reset Secured by Reference field
     C                     MOVE *BLANKS   RSBYR
     C                     MOVE *BLANKS   RGAMT
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/BRBRK
      *****************************************************************
      *                                                               *
      *  BRBRK  - Subroutine to process a change in branch            *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           BRBRK     BEGSR
      *
      ** If printer file is open and entity is not blank, print footer
     C           WFIRST    IFEQ 'Y'                        - B1
     C           #RENT     ANDNE*BLANKS
      *
      ** Output "End of Report"
     C                     EXSR WP1END
     C                     CLOSEER0930P1
     C                     MOVE *BLANKS   WFIRST  1
     C                     END                             - E1
      *
      ** Get branch name using access object
     C                     CALL 'AOBRCHR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM RTBRCA    @KEY2   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B1
     C                     MOVELRTBRCA    DBKEY     P
     C                     MOVE 'SDBRCHPD'DBFILE           ***************
     C                     Z-ADD006       DBASE            * DB ERROR 06 *
     C                     EXSR *PSSR                      ***************
     C                     END                             - E1
      *
     C                     MOVELRTBRCA    RBRCA            Branch code
     C                     MOVELA8BRNM    RBRNM            Branch name
      *
      ** Open printer file ER0920P1 if entity is not blank
      ** or the printer file is not yet open
     C           #RENT     IFNE *BLANKS                    - B1
     C           WFIRST    ORNE 'Y'
     C                     OPEN ER0930P1
     C                     MOVE 'Y'       WFIRST
      *
      ** Ensure Report Spool File recorded by RCF
     C           #RENT     IFNE *BLANKS                    - B2
     C                     MOVE RTBRCA    SENTY
     C                     END                             - E2
      *
     C                     EXSR RCFP1
      *
      ** Output report header
     C                     WRITEHEADP1
      *
     C                     END                             - E1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFP1     BEGSR
      *
      ** Ensure Report Spool File recorded by RCF.
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM #SEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           WP1END    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Total Format.
     C                     WRITEENDP1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
     C           AUDIT     BEGSR
      *
      ** If there is a DB Error, Output the DB Error Information.
     C           *INU7     IFEQ *ON
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
     C           WCOUNT    IFEQ 0
     C                     WRITEHEADAU
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      ACT@   80
      *
      ** Receive Parameter List.
     C           *ENTRY    PLIST
     C                     PARM           #SEQ    5
     C                     PARM           #RLEV   1
     C                     PARM           #RENT   3
      *
      ** Initialise LDA field.
     C                     MOVEL'ER0930'  DBPGM
      *
      ** Access Bank details via access program
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
     C           @RTCD     IFNE *BLANK                     - B1
     C                     MOVEL'BANK'    DBKEY     P
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     END                             - E1
      *
      ** Check System Date Format, DDMMYY (*IN98 off)  MMDDYY (*IN98 on)
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ** Access RUNDAT for multibranching indicator
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'M'                        - B1
     C                     SETON                     37
     C                     END                             - E1
      *
      ** RCF Processing for Audit File.
     C                     EXSR RCFAU
      *
      ** Report Work fields.
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         WCOUNT 150
      *
     C                     MOVE 'J'       ZECODE
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFAU     BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF.
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM #SEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called.
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/ZDATE1
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
      * Convert Day number to date
      *****************************************************************
      /TITLE SR/ZSEDIT
      *****************************************************************
     C/COPY ZSRSRC,ZSEDITZ3
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
