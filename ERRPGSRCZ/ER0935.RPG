     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('MIDAS ER Specific Provisions Maintenance Today')
      *****************************************************************
      *                                                               *
      *  Midas¦ABS - European Reporting Module                        *
      *                                                               *
      *  ER0935   - ER Specific Provisions Maintenace Today Report    *
      *                                                               *
      *  Function:  This program prints a new report that shows       *
      *             specific provisions which are inserted, amended   *
      *             or deleted today.                                 *
      *                                                               *
      *  (phase(s)) Call on request during input cycle                *
      *             Call during close of business                     *
      *                                                               *
      *  Called By: RPG/ERC0935 - Specific Provisions Maint. Today    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027A            Date 12May06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX004             Date 06Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027A- Conversion of cust. no. to alpha (post 103 build)   *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - Capital Adequacy                                    *
      *                                                               *
      *****************************************************************
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  ===========================================                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *   50  - Mode '*AUDIT'                                         *
      *   51  - Report by branch                                      *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /TITLE SUBROUTINE INDEX
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *  ===============================                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *  BRBRK  - Process a change in branch                          *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE1 - Validate Date and Convert into Midas Day Number     *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *                                                               *
      *****************************************************************
      /TITLE FILES DESCRIPTION
      *****************************************************************
      *
      * Guarantees/Collateral Detail file           Prefix RR
     FERGUARL2IF  E           K        DISK
      *
     FER0935AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FER0935P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      **  Facility Types
     F*FCLTY   IF  E           K        DISK                              ULX004
     F**********  FCLTYHHF                          KIGNORE               ULX004
     F**********  FCLTYFNF                          KIGNORE               ULX004
      **  Loans
     F*CLOAN   IF  E           K        DISK                              ULX004
     F**********  CLOANHHF                          KIGNORE               ULX004
     F**********  CLOANZ1F                          KIGNORE               ULX004
     F**********  CLOANCKF                          KIGNORE               ULX004
      **  MM Deals
     F*DEALS   IF  E           K        DISK                              ULX004
     F**********  DEALSDAF                          KIGNORE               ULX004
     F**********  DEALSDBF                          KIGNORE               ULX004
     F**********  DEALSDDF                          KIGNORE               ULX004
     F**********  DEALSDEF                          KIGNORE               ULX004
     F**********  DEALSDGF                          KIGNORE               ULX004
     F**********  DEALSDFF                          KIGNORE               ULX004
      **  A/C master
     F*ACCNT   IF  E           K        DISK                              ULX004
     F**********  ACCNTAAF                          KIGNORE               ULX004
     F**********  ACCNTACF                          KIGNORE               ULX004
      **  A/C master by Retail A/C
     F*ACNUM   IF  E           K        DISK                              ULX004
     F**********  ACCNTABF                          KRENAMEACCNUMF        ULX004
      *
      *****************************************************************
      /TITLE E-SPECIFICATIONS
      *****************************************************************
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
      *****************************************************************
      /TITLE I-SPECIFICATIONS
      *****************************************************************
      *
      ** Local data area for database errors
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I                                        1 256 DBERR
      *
      ** Program data structure.
     IPGMDS     ESDSY2PGDSP
      *
      ** Data Area giving Installation Control Details
     IRUNDAT    E DSRUNDAT
      *
      ** File Information Data Structure for MMNNNNPY.
     ISPOOL1      DS
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
      ** File Information Data Structure for MMNNNNAU.
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IRRUTSR      DS
     I                                        1   30SSFACT
     I                                        4   50SSFCNO
      *
     I                                        1   60SSRREF
      *
     I**********                              1   60SSCUST                                   CSD027A
     I                                        1   6 SSCUST                                   CSD027A
     I                                        7   9 SSCURR
     I**********                             10  130SSACOD                                    CER001
     I**********                             14  150SSACSQ                                    CER001
     I**********                             16  18 SSBRCA                                    CER001
     I                                       10  190SSACOD                                    CER001
     I                                       20  210SSACSQ                                    CER001
     I                                       22  24 SSBRCA                                    CER001
      *
     I                                        1  100SSACNO
     IRRSBYR      DS
     I**********                              1   60WKCUST                                   CSD027A
     I                                        1   6 WKCUST                                   CSD027A
      *
      **  Dummy Data Structures used by Access Programs.
      *
      ** DS for access programs, long data structure
     IDSSDY     E DSDSSDY
      *
      ** External DS for Bank Details
     ISDBANK    E DSSDBANKPD
      *
      ** External DS for Customer Details
     ISDCUST    E DSSDCUSTPD
      *
      ** External DS for Branch Details
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          QQDFA1                                    CER001
      *
      ** External DS for Currency Details
     ISDCURR    E DSSDCURRPD
      *
      /COPY ZSRSRC,ZSEDITZ2
      /EJECT
      *****************************************************************
      **         P R O G R A M    S T A R T                           *
      *****************************************************************
      *
      ** Execute initial processing
     C                     EXSR INIT
      *
      ** Perform Detail Processing.
     C                     EXSR PROCES
      *
      **  Output Audit Report and End Program.
     C                     EXSR AUDIT
      *
      *****************************************************************
      **         P R O G R A M    E N D                               *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
     C           PROCES    BEGSR
      *
      ** Read first record from File.
     C                     READ ERGUARD0                 89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records).
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ELSE
     C                     MOVE RRBRCA    WWBRCA  3
     C                     ENDIF
      *
      ** Do while not End of File.
     C           *IN89     DOWEQ'0'                        - B1
      *
      ** If Secured by indicator equals 'S' (Specific Provisions)
     C           RRSBYI    IFEQ 'S'                        - B2
      *
      ** If received parameter is '*Full'
      ** OR received parameter is '*Audit' AND last change date is equal
      ** to the run date
     C           *IN50     IFEQ *OFF                       - B3   *FULL
     C           *IN50     OREQ *ON                               *AUDIT
     C           RRLCD     ANDEQBJRDNB                            TODAY RECORD
      *
      ** Count records read.
     C                     ADD  1         WCOUNT
      *
      ** If change of branch OR first time processing
     C           RRBRCA    IFNE WWBRCA                     - B4
     C           WFIRST    OREQ *BLANKS
     C                     EXSR BRBRK
     C                     MOVE RRBRCA    WWBRCA
     C**********           Z-ADDRRRCUS    WWRCUS  60                                         CSD027A
     C                     MOVE RRRCUS    WWRCUS  6                                          CSD027A
     C                     ENDIF                           - E4
      *
     C           RRRCUS    IFNE WWRCUS                     - B4
     C           *IN50     ANDEQ*OFF                              *FULL
      *
      ** Output report header
     C                     WRITEHEADP1
     C**********           Z-ADDRRRCUS    WWRCUS                                             CSD027A
     C                     MOVE RRRCUS    WWRCUS                                             CSD027A
     C                     ENDIF                           - E4
      *
      ** Process Report Lines.
     C                     EXSR REPORT
     C                     ENDIF                           - E3
     C                     ENDIF                           - E2
      *
      ** Read next record.
     C                     READ ERGUARD0                 89
      *
      ** Continue or not ?
     C           *IN51     IFEQ *ON                        - B2   By branch
     C           RRBRCA    ANDNEWWBRCA
     C                     MOVE *ON       *IN89
     C                     ENDIF                           - E2
      *
     C                     ENDDO                           - E1
      ** If no records selected go to subroutine AUDIT
      *
     C           WCOUNT    IFEQ 0                          - B1
     C                     EXSR AUDIT
     C                     ELSE                            - X1
      *
      ** Write final records
     C                     EXSR WP1END
     C                     END                             - E1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
     C           REPORT    BEGSR
      *
      ** Fill file' fields into report' fields at correct formats
      *
      *
      ** Access SDCUSTPD to retrieve Customer Details
     C                     MOVE RRRCUS    @KEY1   6
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1   6
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B1
     C                     MOVELRRRCUS    DBKEY     P
     C                     MOVE 'SDCUSTPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF                           - E1
      *
     C                     MOVELBBCRNM    RCNAM            Receiving Cust name
      *
     C                     MOVELBBCRTN    RCTOW            Receiving Cust town
      *
     C                     MOVE RRUTSI    RUTSI            Used to secure ind
      *
     C                     MOVELRRUTSR    RUTSR            Used to secure ref
      *
     C                     MOVE RRGSEQ    RGSEQ            Guarantee Sequence
      *
     C                     Z-ADDRRSTDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RSTDT            Start date
      *
     C                     Z-ADDRRENDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    RENDT            End date
      *
     C                     Z-ADDRRRVDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RRVDT            Review date
      *
     C                     MOVE RRRVFQ    RRVFQ            Review Frequency
      *
     C           RRRVDY    IFEQ 0                          - B1
     C                     MOVE *BLANKS   RRVDY            Review Day
     C                     ELSE                            - X1
     C                     MOVE RRRVDY    RRVDY            Review Day
     C                     END                             - E1
      *
     C                     MOVE RRGCCY    RGCCY            Guarantee currency
      *
     C                     MOVE RRSBYI    RSBYI            Secured by indicator
      *
     C                     MOVE *BLANKS   WWCCY   3
      *
     C*****************************************************
     C* RETRIEVE CURRENCY OF THE USED TO SECURE REFERENCE *
     C*****************************************************
      *
      **  Used to Secure Indicator is equal to 'F' as Facility
     C********** RRUTSI    IFEQ 'F'                        - B1           ULX004
     C**********           MOVE RRRCUS    WWCNUM                          ULX004
     C**********           MOVE SSFACT    WWFACT                          ULX004
     C**********           MOVE SSFCNO    WWFCNO                          ULX004
     C**********           MOVE 'A'       WWRCTP                          ULX004
      *
      **  Access to Facility file with Used to Secur Reference
     C********** WFCLTY    CHAINFCLTY                80                   ULX004
     C********** *IN80     IFEQ *ON                        - B2           ULX004
     C**********           MOVELRRRCUS    DBKEY     P                     ULX004
     C**********           MOVE RRUTSR    DBKEY                           ULX004
     C**********           MOVE 'FCLTY   'DBFILE           ***************ULX004
     C**********           Z-ADD002       DBASE            * DB ERROR 02 *ULX004
     C**********           EXSR *PSSR                      ***************ULX004
     C**********           ELSE                            - X2           ULX004
     C**********           MOVE FCCY      WWCCY                           ULX004
     C**********           END                             - E2           ULX004
     C**********           ELSE                            - X1           ULX004
      *
      **  Used to Secure Indicator is equal to 'L' as Loan in Lending
     C********** RRUTSI    IFEQ 'L'                        - B2           ULX004
      *
      **  Access to loan file with Used to Secure Reference
     C********** SSRREF    CHAINCLOAN                80                   ULX004
     C********** *IN80     IFEQ *ON                        - B3           ULX004
     C**********           MOVELSSRREF    DBKEY     P                     ULX004
     C**********           MOVE 'CLOAN   'DBFILE           ***************ULX004
     C**********           Z-ADD003       DBASE            * DB ERROR 03 *ULX004
     C**********           EXSR *PSSR                      ***************ULX004
     C**********           ELSE                            - X3           ULX004
     C**********           MOVE CCY       WWCCY                           ULX004
     C**********           END                             - E3           ULX004
     C**********           ELSE                            - X2           ULX004
      *
      **  Used to Secure Indicator is equal to 'D' as Loan in Dealing
     C********** RRUTSI    IFEQ 'D'                        - B3           ULX004
      *
      **  Access to Deals file with Used to Secure Reference
     C********** SSRREF    CHAINDEALS                80                   ULX004
     C********** *IN80     IFEQ *ON                        - B4           ULX004
     C**********           MOVELSSRREF    DBKEY     P                     ULX004
     C**********           MOVE 'DEALS   'DBFILE           ***************ULX004
     C**********           Z-ADD004       DBASE            * DB ERROR 04 *ULX004
     C**********           EXSR *PSSR                      ***************ULX004
     C**********           ELSE                            - X4           ULX004
     C**********           MOVE CCY       WWCCY                           ULX004
     C**********           END                             - E4           ULX004
     C**********           ELSE                            - X3           ULX004
      *
      **  Used to Secure Indicator is equal to 'G' as General Ledger
     C********** RRUTSI    IFEQ 'G'                        - B4           ULX004
     C**********           MOVE SSCUST    WWCNUM                          ULX004
     C**********           MOVELSSCURR    WWCURR                          ULX004
     C**********           MOVE SSACOD    WWACOD                          ULX004
     C**********           MOVE SSACSQ    WWACSQ                          ULX004
     C**********           MOVELSSBRCA    WWBRCA                          ULX004
      *
      **  Check if account is live
     C********** WACCNT    CHAINACCNT                80                   ULX004
     C********** *IN80     IFEQ *ON                        - B5           ULX004
     C**********           MOVELSSRREF    DBKEY     P                     ULX004
     C**********           MOVE 'ACCNT   'DBFILE           ***************ULX004
     C**********           Z-ADD005       DBASE            * DB ERROR 05 *ULX004
     C**********           EXSR *PSSR                      ***************ULX004
     C**********           ELSE                            - X5           ULX004
     C**********           MOVE CCY       WWCCY                           ULX004
     C**********           END                             - E5           ULX004
     C**********           ELSE                            - X4           ULX004
      *
      **  Used to Secure Indicator is equal to 'R' as Retail
     C********** RRUTSI    IFEQ 'R'                        - B5           ULX004
      *
      **  Access to account file with Used to Reference (Retail number)
     C********** SSACNO    CHAINACNUM                80                   ULX004
     C********** *IN80     IFEQ *ON                        - B6           ULX004
     C**********           MOVELSSACNO    DBKEY     P                     ULX004
     C**********           MOVE 'ACNUM   'DBFILE           ***************ULX004
     C**********           Z-ADD006       DBASE            * DB ERROR 06 *ULX004
     C**********           EXSR *PSSR                      ***************ULX004
     C**********           ELSE                            - X6           ULX004
     C**********           MOVE CCY       WWCCY                           ULX004
     C**********           END                             - E6           ULX004
     C**********           END                             - E5           ULX004
     C**********           END                             - E4           ULX004
     C**********           END                             - E3           ULX004
     C**********           END                             - E2           ULX004
     C**********           END                             - E1           ULX004
      *
     C                     MOVE RRGCCY    WWCCY            * Store G.Ccy  ULX004
     C********** RRSBYR    IFNE *BLANKS                    - B1           ULX004
     C                     MOVELRRSBYR    RSBYR     P      Secured by reference
     C                     MOVE WWCCY     RGCCY            Guarantee currency
     C**********           ELSE                            - X1           ULX004
      *
     C           RRGAMT    IFNE *ZEROS                     - B1
     C           RRSBYI    IFNE 'N'                          B2           ULX004
      *
      ** Retrieve number of decimal place to format amount
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM WWCCY     @KEY2   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B3
     C                     MOVELWWCCY     DBKEY     P
     C                     MOVE 'SDCURRPD'DBFILE           ***************
     C                     Z-ADD007       DBASE            * DB ERROR 07 *
     C                     EXSR *PSSR                      ***************
     C                     END                             - E3
     C                     Z-ADDA6NBDP    ZDECS                           ULX004
     C                     ELSE                              X2           ULX004
     C                     Z-ADD0         ZDECS                           ULX004
     C                     ENDIF                             E2           ULX004
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE RRGAMT    ZFLD15           Guarantee Amount
     C***********          Z-ADDA6NBDP    ZDECS                           ULX004
     C                     EXSR ZSEDIT
     C***********          MOVE ZOUT21    RSBYR                           ULX004
     C                     MOVE ZOUT21    RPAMT                           ULX004
     C                     MOVE WWCCY     RGCCY            Guarantee currency
     C                     ENDIF                             E1           ULX004
      *
     C***********          ELSE                            - X2           ULX004
      *
     C***********RRGPER    IFNE *ZEROS                     - B3           ULX004
     C                     MOVE *BLANKS   ZFLD15
     C                     MOVE RRGPER    ZFLD15           Guarantee Percentage
     C                     Z-ADD0         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    ZOUT05  5
     C                     MOVE '%'       ZOUT05
     C***********          MOVELZOUT05    RSBYR     P                     ULX004
     C                     MOVELZOUT05    RPAMT                           ULX004
     C***********          ELSE                            - X3           ULX004
     C***********          MOVE *BLANKS   RSBYR                           ULX004
     C***********          END                             - E3           ULX004
     C***********          END                             - E2           ULX004
     C***********          END                             - E1           ULX004
      *
     C                     MOVE RRCHTP    RCHTP            Change type
      *
     C                     MOVE RRRCUS    RRCUS            Receiving Customer
      *
     C                     MOVE RRGCOD    RGCOD            Guarantee Code
      *
     C           RRSBYI    IFEQ 'S'                        - B1
     C           RRSBYR    ANDNE*BLANKS
     C                     MOVE WKCUST    RRGVBY
     C                     END                             - E1
      *
      ** Access SDCUSTPD to retrieve Given by Customer Details
     C********** RRGVBY    IFNE *ZEROS                     - B1                              CSD027A
     C           RRGVBY    IFNE *BLANKS                    - B1                              CSD027A
     C                     MOVE RRGVBY    @KEY1   6
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1   6
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B2
     C                     MOVELRRGVBY    DBKEY     P
     C                     MOVE 'SDCUSTPD'DBFILE           ***************
     C                     Z-ADD008       DBASE            * DB ERROR 08 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF                           - E2
      *
     C                     MOVE RRGVBY    RGVBY            Given by cust numb
     C                     MOVELBBCRNM    RGNAM            Given By Cust name
     C                     ELSE                            - X1
     C                     MOVE *BLANKS   RGVBY            Given by cust numb
     C                     MOVE *BLANKS   RGNAM            Given By Cust name
     C                     ENDIF                           - E1
      *
     C                     MOVELRRNARR    RNARR            Narrative
     C                     MOVE *OFF      *IN60                           ULX004
     C           RNARR     IFEQ *BLANKS                      B1           ULX004
     C                     MOVE *ON       *IN60               1           ULX004
     C                     ENDIF                             E1           ULX004
      *
     C                     MOVE RRUTSL    RUTSL                           ULX004
      *
      ** Write out the record.
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           WP1REC    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
     C           RNARR     IFEQ *BLANKS                      B1           ULX004
     C                     Z-ADD2         RQDLN1
     C                     ELSE                                           ULX004
     C                     Z-ADD3         RQDLN1                          ULX004
     C                     ENDIF                             E1           ULX004
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Detail Record.
     C                     WRITEDETAIL
      *
     C                     MOVE *BLANKS   RSBYR                           ULX004
     C                     MOVE *BLANKS   RPAMT                           ULX004
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/BRBRK
      *****************************************************************
      *                                                               *
      *  BRBRK  - Subroutine to process a change in branch            *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           BRBRK     BEGSR
      *
      ** If printer file is open and entity is not blank, print footer
     C           WFIRST    IFEQ 'Y'                        - B1
     C           #RENT     ANDNE*BLANKS
      *
      ** Output "End of Report"
     C                     EXSR WP1END
     C                     CLOSEER0935P1
     C                     MOVE *BLANKS   WFIRST  1
     C                     END                             - E1
      *
      ** Get branch name using access object
     C                     CALL 'AOBRCHR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM RRBRCA    @KEY2   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C           @RTCD     IFNE *BLANKS                    - B1
     C                     MOVELRRBRCA    DBKEY     P
     C                     MOVE 'SDBRCHPD'DBFILE           ***************
     C                     Z-ADD009       DBASE            * DB ERROR 09 *
     C                     EXSR *PSSR                      ***************
     C                     END                             - E1
      *
     C                     MOVELRRBRCA    RBRCA            Branch code
     C                     MOVELA8BRNM    RBRNM            Branch name
      *
      ** Open printer file ER0935P1 if entity is not blank
      ** or the printer file is not yet open
     C           #RENT     IFNE *BLANKS                    - B1
     C           WFIRST    ORNE 'Y'
     C                     OPEN ER0935P1
     C                     MOVE 'Y'       WFIRST
      *
      ** Ensure Report Spool File recorded by RCF
     C           #RENT     IFNE *BLANKS                    - B2
     C                     MOVE RRBRCA    SENTY
     C                     END                             - E2
      *
     C                     EXSR RCFP1
      *
      ** Output report header
     C                     WRITEHEADP1
      *
     C                     END                             - E1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFP1     BEGSR
      *
      ** Ensure Report Spool File recorded by RCF.
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM #SEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           WP1END    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Total Format.
     C                     WRITEENDP1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
     C           AUDIT     BEGSR
      *
      ** If there is a DB Error, Output the DB Error Information.
     C           *INU7     IFEQ *ON
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
     C           WCOUNT    IFEQ 0
     C                     WRITEHEADAU
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      ACT@   80
      *
      ** Receive Parameter List.
     C           *ENTRY    PLIST
     C                     PARM           #PARM   6
     C                     PARM           #SEQ    5
     C                     PARM           #RLEV   1
     C                     PARM           #RENT   3
      *
      **  KLIST to access on FCLTY
     C********** WFCLTY    KLIST                                          ULX004
     C**********           KFLD           WWCNUM  60                      ULX004
     C**********           KFLD           WWFACT  30                      ULX004
     C**********           KFLD           WWFCNO  20                      ULX004
     C**********           KFLD           WWRCTP  1                       ULX004
      *
      **  KLIST to access on ACCNT
     C********** WACCNT    KLIST                                          ULX004
     C**********           KFLD           WWCNUM                          ULX004
     C**********           KFLD           WWCURR  3                       ULX004
     C**********           KFLD           WWACOD  40                      ULX004
     C**********           KFLD           WWACSQ  20                      ULX004
     C**********           KFLD           WWBRCA  3                       ULX004
      *
      ** Retrieve entry parameter
     C                     MOVEL#PARM     WWMODE  6
     C           WWMODE    IFEQ '*AUDIT'
     C                     MOVE *ON       *IN50
     C                     ELSE
     C                     MOVE *OFF      *IN50
     C                     ENDIF
      *
      ** Initialise LDA field.
     C                     MOVEL'ER0935'  DBPGM
      *
      ** Access Bank details via access program
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
     C           @RTCD     IFNE *BLANK                     - B1
     C                     MOVEL'BANK'    DBKEY     P
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD010       DBASE            * DB ERROR 10 *
     C                     EXSR *PSSR                      ***************
     C                     END                             - E1
      *
      ** Check System Date Format, DDMMYY (*IN98 off)  MMDDYY (*IN98 on)
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ** Access RUNDAT for multibranching indicator
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'M'                        - B1
     C                     SETON                     37
     C                     END                             - E1
      *
      ** RCF Processing for Audit File.
     C                     EXSR RCFAU
      *
      ** Report Work fields.
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         WCOUNT 150
      *
     C                     MOVE 'J'       ZECODE
      *
      ** Test if branch selected
     C           #RENT     IFNE *BLANKS                    - B1
     C           #RENT     ANDNE'ALL'
     C                     MOVE *ON       *IN51
     C           #RENT     SETLLERGUARD0
     C                     ENDIF                           - E1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFAU     BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF.
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM #SEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called.
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       WLDA  256
     C           *LOCK     IN   WLDA
     C                     MOVELDBERR     WLDA
     C                     OUT  WLDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/ZDATE1
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
      * Convert Day number to date
      *****************************************************************
      *****************************************************************
      /TITLE SR/ZSEDIT
      *****************************************************************
     C/COPY ZSRSRC,ZSEDITZ3
      * Input fields  : ZFIELD (16A), ZADEC (1,0)
      * Output fields : ZFIELD (16A)
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2005
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
