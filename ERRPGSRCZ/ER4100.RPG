     H        1    Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER Minerva Interface - Build IRMITTPD table')    *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy                    *
      *                                                               *
      *  ER4100 - Minerva Interface - Build IRMITTPD table based      *
      *           over Minerva table 300                              *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1998           *
      *                                                               *
      *  Last Amend No. LUC139             Date 04Jan21               *
      *  Prev Amend No. MMI131             Date 12Mar08               *
      *                 MMI128             Date 28Sep07               *
      *                 MMI104                  21Jan03               *
      *                 MMI100                  05Nov02               *
      *                 MMI033 *create          01Jul98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  MMI131 - Process also Bank of Italy table.                   *
      *           Program rewritten to Midas standards.               *
      *  MMI128 - Exclude derived fields (i.e. those marked with W).  *
      *  MMI104 - Puma field codes increased to 5 digits.             *
      *  MMI100 - R4.1 Upgrade - renamed from IR4100 to ER4100        *
      *  MMI033 - Minerva Interface                                   *
      *                                                               *
     F*****************************************************************
     FIRM300PDIP  E           K        DISK
     FIRMITTPDO   E                    DISK                      A
      *
     E                    COV        15  8
     E                    COF     1   3  5 0
     E                    DUP       999  5 0
      *
     IARCT        DS
     I                                        1  43 CHIAVE
     I                                       44 160 DATI
     I                                        1   3 LEFT
     I                                        4 160 RIGHT
      *
     IDATA        DS
     I                                        1   1 TABC
     I                                        2   6 VOCEA
     I                                        2   60VOCE
     I                                        7   80SVOC
     I                                       17  170DIVI
     I                                       21  22 PROG
     I                                       34 153 BLOCKS
      /EJECT
      *
      * The input table can come either from Minerva (in which case all
      * records would start with the code 300) or from Bank of Italy.
      *
     C           LEFT      IFEQ '300'
     C                     MOVELRIGHT     DATA
     C                     ELSE
     C                     MOVELARCT      DATA
     C                     ENDIF
      *
      * Check this is a valid record
      *
     C           TABC      IFEQ 'C'
     C           VOCEA     ANDNE*BLANKS
     C           PROG      ANDNE'00'
      *
      * When key fields change reset work array that will be used to
      * avoid output of duplicate records
      *
     C           VOCE      IFNE PTVOCE
     C           SVOC      ORNE PTSVOC
     C           DIVI      ORNE PTDIVI
     C                     Z-ADDVOCE      PTVOCE
     C                     Z-ADDSVOC      PTSVOC
     C                     Z-ADDDIVI      PTDIVI
     C                     MOVEA*ZEROS    DUP
     C                     Z-ADD1         W       40
      *
      * Output mandatory fields - that might not be defined on input
      * table.
      *
     C                     DO   3         X
     C                     MOVELCOF,X     PTCAMP
     C                     EXSR CHKWRT
     C                     ENDDO
     C                     ENDIF
      *
      * Move data part of input record into work array and process
      * all 15 blocks.
      *
     C                     MOVEABLOCKS    COV
      *
     C                     DO   15        X       20
      *
     C           COV,X     IFNE *BLANKS
     C                     MOVELCOV,X     PTCAMP
     C                     EXSR CHKWRT
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDIF
      *
      /EJECT
      ***************************************************************
      *
      * CHKWRT - Check if a field needs to be output and if yes do so
      *
      ***************************************************************
      *
     C           CHKWRT    BEGSR
      *
      * Set output flag to 'Y'
      *
     C                     MOVE 'Y'       OUTFLG  1
      *
      * If field number is followed by 'W ' then this is a Derived
      * field, and therefore is not required
      *
     C                     MOVE COV,X     CHKW03  3
     C                     MOVELCHKW03    CHKW02  2
     C           CHKW02    IFEQ 'W '
     C                     MOVE 'N'       OUTFLG
     C                     ENDIF
      *
      * For following Voce/Sottovoce/field combinations output
      * also derived fields
      *
     C           PTCAMP    IFEQ 30
     C           PTCAMP    OREQ 204
     C           PTCAMP    OREQ 277
     C                     MOVE 'Y'       OUTFLG
     C                     ENDIF
      *
     C           PTVOCE    IFEQ 1209
     C           PTVOCE    OREQ 1228
     C           PTVOCE    OREQ 1851
     C           PTVOCE    OREQ 1922
     C           PTSVOC    IFEQ 2
     C           PTCAMP    ANDEQ4
     C                     MOVE 'Y'       OUTFLG
     C                     ENDIF
     C                     ENDIF
      *
      * Field 00277 is not required for 1026.83 and 1017.40
      *
     C           PTVOCE    IFEQ 1083
     C           PTSVOC    ANDEQ26
     C           PTCAMP    ANDEQ277
     C           PTVOCE    OREQ 1017
     C           PTSVOC    ANDEQ40
     C           PTCAMP    ANDEQ277
     C                     MOVE 'N'       OUTFLG
     C                     ENDIF
      *
      * Check if field has already been output
      *
     C           OUTFLG    IFEQ 'Y'
     C                     Z-ADD1         Z       20
     C           PTCAMP    LOKUPDUP,Z                    02
     C           *IN02     IFEQ *ON
     C                     MOVE 'N'       OUTFLG
     C                     ELSE
     C                     MOVELPTCAMP    DUP,W
     C                     ADD  1         W
     C                     ENDIF
     C                     ENDIF
      *
      * If all above checks are ok, then output record
      *
     C           OUTFLG    IFEQ 'Y'
      *
      * If 'Divisa' is 0, then output two records (one with Divisa = 1
      * and one with Divisa = 2)
      *
     C           DIVI      IFEQ 0
     C                     Z-ADD1         PTDIVI
     C                     WRITEIRMITT
     C                     Z-ADD2         PTDIVI
     C                     WRITEIRMITT
     C                     Z-ADD0         PTDIVI
     C                     ELSE
     C                     WRITEIRMITT
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
**
00030  COD. ANAGRAFICO AZIENDALE CLIENTE
00204  NUMERO CONTO
00277  COD.ABB. UNIVOCO AZIENDALE
