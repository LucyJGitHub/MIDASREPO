     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Spot Trade Historic Postings Merge')
     F********************************************************************
     F*                                                                  *
     F*     MIDAS/ABS  EUROPEAN MODULE                                   *
     F*                                                                  *
     F*     ER5349  -  SPOT TRADE HISTORIC POSTINGS A/C MERGE            *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Aug05               *
      *  Prev Amend No. 132893             Date 15Apr98               *
      *                 57072              Date 06Aug93               *
     F*                                                                  *
     F********************************************************************
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F*  132893 - SPF/RCF for IBLC/BNB on DBA2.3                         *
     F*  57072  - HASH TOTALS NOT CUMULATED CORRECTLY                    *
     F********************************************************************
     FTABLE   IF  F      96 12AI     2 DISK
     F***GEACPO1 IP  F     271  1AI     1 DISK                                                CER001
     F***GEACPO1ZIS  F     160  1AI     1 DISK                                                CER001
     FGLSTX1L0IP  F     271  1AI     1 DISK                                                   CER001
     FGLSTX1L1IS  F     160  1AI     1 DISK                                                   CER001
     F*ER5349P1O   F     132            PRINTER                           132893
     FER5349AUO   F     132            PRINTER                            132893
     FEODPOPD O   F     271            DISK
     FEODPOZZ UF  E                    DISK                      A
     F*****************************************************************
     F* 03            ACCOUNT POSTINGS RECORD
     F* 04            GEACPO1Z TRAILER
     F* 05            TABLE FILE I.C.D RECORD 1
     F* 07            TABLE FILE CATCHALL
     F* 09            GEACPO1/GEACPO1Z CATCHALL
     F*
     F*       25      DISCREPANCY BETWEEN INPUT AND CALC. TOTALS
     F*       33      FIRST CYCLE OVER
     F*       34      PRINT TITLES
     F*       35      DETAIL RECORDS ON PRINTER FILE
     F*
     F*       U8      FILE OUT OF BALANCE
     F*       LR      LAST RECORD READ
     F**************************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E                    NARR    1   1 35               POST NAME
     E**************************************************************************
      /EJECT
     I*
     I**   TABLE FILE - INSTALLATION CONTROL RECORD 1
     I*
     ITABLE   NS  05   1 CD   2 C0   3 C1
     I       AND      12 C1  13 C0
     I                                        1   1 RECI
     I                                        2   30RECT
     I                                       12  13 RCDE
     I                                       14  66 TITL
     I                                       67  67 DATF
     I                                       68  700DFBR
     I                                       74  74 MODI1
     I                                       75  75 MODI2
     I                                       76  76 MODI3
     I                                    P  77  790RUND
     I                                       80  86 RUNA
     I                                       87  870SFLG
     I                                       88  88 MODI4
     I                                    P  90  920LCD
     I                                       93  93 CHTP
     I                                    P  94  960TNLU
     I                                        1  96 LOT
     I*
     I**   CATCHALL FOR DELETED RECORDS
     I*
     I        NS  07
     I*
     I** ACCOUNT POSTINGS FILES
     I*
     I***GEACPO1 NS  03   1 CP                                                                CER001
     IGLSTX1L0NS  03   1 CP                                                                   CER001
     I                                    P  74  803PSTA
     I                                        1 271 ACPRC
     I        NS  09
     I*
     I***GEACPO1ZNS  04   1 CT                                                                CER001
     IGLSTX1L1NS  04   1 CT                                                                   CER001
     I                                        2   60NORE
     I                                    P   7  140ZZINT
     I                                    P  15  160ZZDEC
     I        NS  09
     I*
     I** DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I**************************************************************************
      /EJECT
     C*
     C**   FIRST CYCLE CALCULATIONS.
     C*
     C                     SETOF                     3435
     C   33                GOTO DETAIL
     C                     EXSR ZSYSTM
     C*
     C                     SETON                     3334
     C                     Z-ADD0         RECRED
     C                     Z-ADD0         WWTRN   50
     C*
     C**   DETAIL CALCULATIONS.
     C*
     C           DETAIL    TAG                             ** DETAIL TAG *
     C*
     C   03      RECRED    ADD  1         RECRED  50
     C   03      TOTACP    ADD  1         TOTACP  50
     C*
     C**   IF DETAIL RECORD, ADD POSTING AMOUNT
     C**   TO HASH TOTAL.
     C*
     C     N03N09          GOTO TOTAL
     C   09                GOTO END
     C*
     C**   ACCUMULATE OVERALL HASH TOTALS
     C*
     C                     Z-ADDPSTA      ZZAMT  153
     C                     Z-ADDPSTORI    ZZTOTI
     C                     Z-ADDPSTORD    ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    PSTORI 150
     C                     Z-ADDZZTOTD    PSTORD  30
     C*
     C**   ACCUMULATE HASH TOTALS FOR EACH FILE
     C*
     C                     Z-ADDPISTRI    ZZTOTI
     C                     Z-ADDPISTRD    ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    PISTRI 150
     C                     Z-ADDZZTOTD    PISTRD  30
     C                     GOTO END
     C*
     C** IF TRAILER RECORD, MAINTAIN TOTALS FOR GEACPO1 FILE
     C**       - CALC VALUES - SAVE FOR GEACPO1 IN INN,I,D
     C**       - FILE VALUES - ACCUM.FROM TRLR FOR GEACPO1Z IN TRN,I,D
     C*
     C           TOTAL     TAG                             ** TOTAL TAG **
     C*
     C                     ADD  NORE      WWTRN   50
     C                     Z-ADDZZINT     ZZTOTI
     C                     Z-ADDZZDEC     ZZTOTD
     C                     Z-ADDWWTRI     ZZAMTI
     C                     Z-ADDWWTRD     ZZAMTD
     C                     EXSR GLZSUM
     C                     Z-ADDZZTOTI    WWTRI  150
     C                     Z-ADDZZTOTD    WWTRD   30
     C*
     C**   TRAILER FOR GEACPO1-SAVE CALC VALUES
     C*
     C                     Z-ADDRECRED    WWINN   50
     C                     Z-ADDPISTRI    WWINI  150
     C                     Z-ADDPISTRD    WWIND   30
     C*
     C           END       TAG                             ** END TAG   **
     C*
     C** TOTAL TIME CALCULATIONS.
     C*
     CLR                   SETON                     35
     C*
     C**   COMPARE AND PRINT CALC/TRLR CONTROLS FOR GEACPO1 FILE
     C*
     CLR                   SUB  4         WWTRN
     CLR                   EXSR COMP
     CLR                   Z-ADDTOTACP    TOTREC  50
     C*
     CLR                   READ EODPOZZ                  89
     CLR         *IN89     IFEQ '1'                        - E1
     CLR                   Z-ADDTOTREC    NORE1
     CLR                   Z-ADDPSTORI    HRWN
     CLR                   Z-ADDPSTORD    HRDC
     CLR                   WRITEAPOSTZZF
     CLR                   ELSE
     CLR                   ADD  TOTREC    NORE1
     C*R*******************ADD  PSTORI    HRWN                            57072
     C*R*******************ADD  PSTORD    HRDC                            57072
     CLR                   Z-ADDHRWN      ZZTOTI                          57072
     CLR                   Z-ADDHRDC      ZZTOTD                          57072
     CLR                   Z-ADDPSTORI    ZZAMTI                          57072
     CLR                   Z-ADDPSTORD    ZZAMTD                          57072
     CLR                   EXSR GLZSUM                                    57072
     CLR                   Z-ADDZZTOTI    HRWN   150                      57072
     CLR                   Z-ADDZZTOTD    HRDC    30                      57072
     C*                                                                   57072
     CLR                   UPDATAPOSTZZF
     CLR                   END
     C*
     CLR                   Z-ADDTOTACP    TOTREC  50
      /EJECT
     C*********************************************************************
     C* COMP    SR TO COMPARE AND PRINT CALC/TRLR CONTROLS FOR GEACPO1 FILE
     C**********************************************************************
     C           COMP      BEGSR                           ** COMP SR   **
     C*
     C           WWINN     COMP WWTRN                2525
     C  N25      WWINI     COMP WWTRI                2525
     C  N25      WWIND     COMP WWTRD                2525
     C   25                SETON                     U8
     C                     EXCPT
     C*
     C                     ENDSR                           ** ENDSR     **
     C********************************************************************
      /EJECT
     C********************************************************************
     C**   ZSYSTM SR. TO CHAIN TO THE INSTALLATION CONTROL DATA RECORD.
     C********************************************************************
     C           ZSYSTM    BEGSR                           *** ZSYSTM ***
     C*
     C**   SET UP KEY FOR INSTALLATION CONTROL RECORD.
     C*
     C                     MOVEL'01      'ZTABKY 12
     C                     MOVE '  10'    ZTABKY
     C*
     C           ZTABKY    CHAINTABLE                99    ON NOT THERE
     C  N99      RECI      COMP 'D'                  9999  ON, IS DELETED
     C*
     C**   CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
     C*
     C           DATF      COMP 'M'                      98MMDDYY, 98 ON
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C* SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL (ZZTOTI,ZZTOTD)
     C*   IND '99' IS SET BY AN OVERFLOW ERROR
     C********************************************************************
     C*
     CSR         GLZADD    BEGSR                           *** GLZADD ****
     C*
     CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
     C*
     C** SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
     C*
     CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
     CSR                   Z-ADDZZAMT     ZZWK1   33
     CSR                   MOVE ZZWK1     ZZAMTD  30       DECIMAL FIELD
     C*
     C** BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
     C*
     CSR                   EXSR GLZSUM
     CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C* SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINES -
     C*                        GLZADD, GLZSUB, GLZCMP.
     C*          PARAMETERS PASSED -
     C*                     I/P ZZAMTI ZZAMTD
     C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
     C*
     C********************************************************************
     CSR         GLZSUM    BEGSR                           *** GLZSUM ****
     C*
     CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     CSR                   SETOF                     919293
     CSR                   SETOF                     949599
     C*
     C**   DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
     C*
     CSR         ZZAMTI    COMP 0                      9293
     CSR 93      ZZAMTD    COMP 0                      9293
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*
     C**   DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
     C*
     CSR         ZZTOTI    COMP 0                      9193
     CSR 93      ZZTOTD    COMP 0                      9193
     C*
     C**   IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
     C*
     CSR 93                Z-ADDZZAMTI    ZZTOTI
     CSR 93                Z-ADDZZAMTD    ZZTOTD
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*
     C**   IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
     C*
     CSR 91N92
     CORN91 92             GOTO ZZOFPS
     C*
     CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     CSR         ZZWK2     COMP 999                  93    '93' = CARRY
     CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
     C*
     CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
     CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
     C*
     C** IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
     C*
     CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     CSRN99                Z-ADDZZWK2     ZZTOTD
     CSRN99                Z-ADDZZWK3     ZZTOTI
     C*
     C** IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
     C*
     CSR 99                Z-ADD0         ZZAMT  153
     CSR                   GOTO ZZSEND
     C*
     C** THE 'SIGNS' ARE DIFFERENT
     C*
     CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
     C*
     C** IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
     C*
     CSR 92                Z-SUBZZAMTI    ZZAMTI 150
     CSR 92                Z-SUBZZAMTD    ZZAMTD  30
     C*
     C** IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
     C*
     CSR 91                Z-SUBZZTOTI    ZZTOTI
     CSR 91                Z-SUBZZTOTD    ZZTOTD
     C*
     C** BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
     C*
     CSR         ZZTOTI    COMP ZZAMTI               93  95
     CSR 95      ZZTOTD    COMP ZZAMTD               93  95
     C*
     C** IF ZZTOT EQ. ZZAMT RETURN ZERO.
     C*
     CSR 95                Z-ADD0         ZZTOTI
     CSR 95                Z-ADD0         ZZTOTD
     CSR 95                GOTO ZZSEND
     C*
     C** IF ZZTOT GT. ZZAMT.
     C*
     CSR 93      ZZAMTD    COMP ZZTOTD               94
     CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
     CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
     CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
     C*
     C** IF ZZAMT GT. ZZTOT.
     C*
     CSRN93      ZZTOTD    COMP ZZAMTD               94
     CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
     CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
     CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
     C*
     C** REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
     C*
     CSR                   SETOF                     94
     CSR 93 91
     CORN93 92             SETON                     94
     CSR 94                Z-SUBZZTOTI    ZZTOTI
     CSR 94                Z-SUBZZTOTD    ZZTOTD
     C*
     C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
     C*
     CSR 92                Z-SUBZZAMTI    ZZAMTI
     CSR 92                Z-SUBZZAMTD    ZZAMTD
     CSR         ZZSEND    TAG                             ** ZZSEND TAG *
     C*
     C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
     C*
     CSR                   SETOF                     96
     CSR         ZZTOTD    COMP 0                        91
     CSR 91      ZZTOTI    COMP 0                      96
     CSR 96                MOVE '.000-'   ZZNEGD  5
     CSR                   ENDSR                             ** ENDSR **
     C********************************************************************
      /EJECT
     O********************************************************************
     O*
     O** POSTINGS RECORDS DETAIL
     O*
     OEODPOPD D        03
     O                         ACPRC    271
     O*
     O**  CONTROL TOTALS OF NUMBER OF RECORDS READ FROM GEACPO1 FILE
     O**  AND NUMBER OF RECORDS WRITTEN TO OUTPUT FILE
     O*
     O*ER5349P1D  203   34                                                132893
     O******                             22 'REFERENCE ER5349'            132893
     OER5349AUD  203   34                                                 132893
     O                                   22 'REFERENCE ER5349AU'          132893
     O                         TITL      89
     O                                  104 'DATE'
     O                         RUNA     113
     O                                  122 'PAGE'
     O                         PAGE  Z  127
     O        D  1     34
     O                                   55 'SPOT TRADE HISTORIC'
     O                                   71 ' POSTINGS MERGE '
     O                                   92 '- FILE CONTROL REPORT'
     O        D  2     34
     O                                   55 '-------------------'
     O                                   71 '----------------'
     O                                   92 '---------------------'
     O        D  1     34
     O                                   50 'INPUT'
     O                                   96 'CALCULATED'
     O        D  2     34
     O                                   41 'READ'
     O                                   63 'VALUE'
     O                                   84 'READ'
     O                                  106 'VALUE'
     O        E 11     35
     O                         NARR      35
     O                         WWTRN 3   41
     O                         WWTRI Z   60
     O                         WWTRD     63
     O                         WWINN 3   84
     O                         WWINI Z  103
     O                         WWIND    106
     O                 25               126 '** DISCREPANCY **'
     O        T 22     LR
     O                                   29 'NUMBER OF ACCOUNT PO'
     O                                   43 'STINGS WRITTEN'
     O                         TOTACP3   67
     O        T  2     LR
     O                                   29 'VALUE OF ACCOUNT PO'
     O                                   43 'STINGS WRITTEN'
     O                         PSTORIZ   64
     O                         PSTORD    67
     O******  T 2      LR                                                 132893
     O******                             67 '*** END OF REPORT ***'       132893
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
**  NARR
NEW SPOT HISTORIC REVALUATIONS
