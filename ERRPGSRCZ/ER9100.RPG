     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas European Reporting I.C.D. - maintenance')        *
     F********************************************************************
     F*                                                                  *
     F*    MIDAS/ABS - European Reporting                                *
     F*                                                                  *
     F*    ER9100 - European Reporting I.C.D. maintenance                *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     F*                                                                  *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER045             Date 20Jul09               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 03May06               *
      *                 CER001             Date 25Apr05               *
      *                 UPG402             Date 04Oct04               *
      *                                    Date 00Xxx00               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER045 - German Features - Reporting Bundesbank (Recompile)  *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CSD027A - Conversion of customer number to alpha (post       *
      *            build 103). Recompiled.                            *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  UPG402 - Recompile due to R4.02 upgrade                      *
     F*                                                                  *
     F********************************************************************
     F*
     FSDCTRYL1IF  E           K        DISK
     F*
     FSDCURRL1IF  E           K        DISK
     F*
     FSDBRCHL1IF  E           K        DISK
     F*
     FERICDRL0UF  E           K        DISK
     F                                              KCOMIT
     F*
     FERICDRL1IF  E           K        DISK
     F            ERICDRD0                          KRENAMEERICDRD1
     F*
     FSDBANKL1IF  E           K        DISK
     F*
     FER9100DFCF  E                    WORKSTN
     F                                        RRN   KSFILE ER9100S1
     E*****************************************************************
     E                    CPY@    1   1 80
     I*****************************************************************
     I* PROGRAM DATA STRUCTURE
     IPGMDS     ESDSY2PGDSP
     I*
     ILDA         DS                            256
     I                                      132 141 WWFILE
     I                                      142 170 WWKEY
     I                                      171 180 WWPGM
     I                                      181 1830WWASE
     I*
     I** DS TO SETUP THE NAME OF CALLED PROGRAM
     I            DS
     I                                        1   2 ER
     I                                        3   4 XX
     I                                        5   6 YY
     I                                        1   6 ERXX91
     I*
     I            DS
     I                                        1  150WORK15
     I                                        1  150ZS1
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
      /EJECT
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C**************************************************************************
     C* CALL INIT SUBROUTINE
     C**************************************************************************
     C                     EXSR INIT                       INITIATION
     C*
     C                     EXSR MAIN
     C*
     C                     ROLBK
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C**************************************************************************
     C* MAIN PROCESSING                                                        *
     C**************************************************************************
     C*
     C           MAIN      BEGSR
     C*
     C                     EXSR INISFL                     INIT. SUBFILE
     C                     EXSR LOASFE                     LOAD SUBFILE ENQUIRY
     C                     Z-ADD1         RRN
     C*
     C** WRITE SUBFILE
     C           *LOVAL    SETLLERICDRL1
     C                     READ ERICDRL1                 31
     C           *IN31     IFEQ '1'                        - B1
     C                     MOVEL'ER9100'  DBPGM
     C                     MOVEL'003'     DBASE            *****************
     C                     MOVEL'ERICDRL1'DBFILE           ** DB ERROR 03 **
     C                     MOVEL' NONE '  DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             - E1
     C*
     C** DO WHILE NOT F3 PRESSED
     C           *INKC     DOWEQ'0'                        - B1
     C*
     C                     WRITEER9100C0                   BOTTOM
     C                     EXFMTER9100C1                   SUBFILE
     C*
     C** CLEAR MESSAGES FROM PROGRAM MESSAGE QUEUE
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C*
     C** ENTER HAS BEEN PRESSED
     C           *IN45     IFEQ '0'                        - B2
     C                     MOVE '0'       *IN21
     C                     MOVE '0'       *IN22
     C                     MOVE '0'       *IN29
     C*
     C** Retrive all changed records
     C                     READCER9100S1                 32
     C*
     C           *IN32     DOWEQ'0'                        - B3
     C*
     C           DSUCCD    IFNE *BLANK                     - B4
     C*
     C** CHECK IF USER COUNTRY CODE EXIST
     C           DSUCCD    CHAINSDCTRYL1             88
     C           *IN88     IFEQ '1'                        - B5
     C                     MOVE '1'       *IN29
     C                     MOVE '1'       *IN63
     C                     MOVE 'ER09108' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE DSUCCD    VDUCCD
     C                     MOVE '0'       *IN29
     C                     END                              - E5
     C*
     C** CHECK IF BRANCH CODE EXIST
     C           DSBRCD    CHAINSDBRCHL1             88
     C           *IN88     IFEQ '1'                        - B5
     C                     MOVE '1'       *IN21
     C                     MOVE '1'       *IN63
     C                     MOVE 'ER09103' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE DSBRCD    VDBRCD
     C                     MOVE '0'       *IN21
     C                     END                              - E5
     C*
     C** CHECK IF CURRENCY EXIST
     C           DSCCYC    CHAINSDCURRL1             88
     C           *IN88     IFEQ '1'                         - B5
     C                     MOVE '1'       *IN22
     C                     MOVE '1'       *IN63
     C                     MOVE 'ER09102' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
     C                     MOVE DSCCYC    VDCCYC
     C                     MOVE '0'       *IN22
     C                     END                              - E5
     C                     END                              - E4
     C*
     C           DSUCCD    IFEQ *BLANK                     - B4
     C                     MOVE *BLANK    DSBRCD
     C                     MOVE *BLANK    DSCCYC
     C                     END                             - E4
     C*
     C           *IN21     IFEQ '0'                        - B4
     C           *IN22     ANDEQ'0'
     C           *IN29     ANDEQ'0'
     C*
     C           DSICCD    CHAINERICDRD0             88
     C                     MOVE DSUCCD    VDUCCD
     C                     MOVE DSBRCD    VDBRCD
     C                     MOVE DSCCYC    VDCCYC
     C                     Z-ADDBJRDNB    VDLCD
     C                     MOVE 'A'       VDTYLC
     C*
     C                     UPDATERICDRD0
     C*
     C                     COMIT
     C*
     C                     END                             - E4
     C                     UPDATER9100S1
     C*
     C                     READCER9100S1                 32
     C                     ENDDO                           - E3
     C*
     C** CALL COUNTRY ICD PROGRAM
     C*
     C           *IN21     IFEQ '0'                        - B3
     C           *IN22     ANDEQ'0'
     C           *IN29     ANDEQ'0'
     C           *LOVAL    SETLLERICDRL1
     C                     READ ERICDRL1                 31
     C           *IN31     IFEQ '1'                        - B4
     C                     MOVEL'ER9100'  DBPGM
     C                     MOVEL'004'     DBASE            *****************
     C                     MOVEL'ERICDRL1'DBFILE           ** DB ERROR 04 **
     C                     MOVEL' NONE '  DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             - E4
     C*
     C** DO WHILE NOT F3 PRESSED
     C           *INKC     DOWEQ'0'                        - B4
     C           *IN31     ANDNE'1'
     C*
     C** IF USER COUNTRY CODE NOT BLANK
     C*
     C           VDUCCD    IFNE *BLANKS                    - B5
     C                     MOVE 'ER'      ER
     C                     MOVE VDICCD    XX
     C                     MOVE '91'      YY
     C*
     C** CALL PROGRAM
     C                     MOVE *BLANKS   RETRN   2
     C                     UNLCKLDA
     C*
     C                     CALL ERXX91
     C                     PARM           ACTION
     C                     PARM           RETRN
     C                     IN   LDA
     C                     END                             - E5
     C*
     C** CHECK RETURN CODE FROM CALLED PROGRAM
     C           RETRN     IFEQ '03'                       - B5
     C                     MOVE '1'       *INKC
     C                     ITER
     C                     END                             - E5
     C*
     C           RETRN     IFEQ 'ER'                       - B5
     C                     MOVELERXX91    DBPGM  10
     C                     MOVE RETRN     DBKEY  29
     C                     MOVEL'001'     DBASE   30
     C                     EXSR *PSSR
     C                     END                             - E5
     C*
     C           RETRN     IFEQ '12'                       - B5
     C                     READPERICDRL1                 31
     C                     ELSE
     C                     READ ERICDRL1                 31
     C                     END                             - E5
     C*
     C                     END                             - E4
     C                     END                             - E3
     C*
     C                     END                             - E2
     C*
     C*
     C** REFRESH PROCESSING ==> REBUILD SUBFILE
     C           *IN05     IFEQ '1'                        - B2
     C                     MOVE VDUCCD    DSUCCD
     C                     MOVE VDBRCD    DSBRCD
     C                     MOVE VDCCYC    DSCCYC
     C                     MOVE '0'       *IN21
     C                     MOVE '0'       *IN22
     C                     MOVE '0'       *IN29
     C                     EXSR INISFL                     INIT. SUBFILE
     C                     EXSR LOASFE                     LOAD SUBFILE ENQUIRY
     C*
     C                     END                             - E2
     C                     END                             - E1
     C*
     C                     ENDSR
     C*
     C**************************************************************************
     C* INITIALIZE SUBFILE
     C**************************************************************************
     C*
     C           INISFL    BEGSR
     C*
     C                     MOVE '0'       *IN80
     C                     MOVE '0'       *IN81
     C                     WRITEER9100C1
     C                     MOVE '1'       *IN80
     C                     Z-ADD0         RRN     30
     C*
     C                     ENDSR
     C*
     C**************************************************************************
     C* LOAD SUBFILE
     C**************************************************************************
     C*
     C           LOASFE    BEGSR
     C*
     C* INIT SCREEN LINE NUMBER
     C                     Z-ADD0         WWCPT   20
     C*
     C           *LOVAL    SETLLERICDRL1
     C*
     C* READ RECORD
     C                     READ ERICDRL1                 30
     C*
     C* LOAD SUBFILE
     C                     MOVE '0'       *IN63
     C           *IN30     DOWEQ'0'                        - B1
     C*
     C           *IN30     IFEQ '1'                        - B2
     C                     MOVE *BLANKS   DSICCD
     C                     MOVE *BLANKS   DSICNA
     C                     MOVE *BLANKS   DSUCCD
     C                     MOVE *BLANKS   DSBRCD
     C                     MOVE *BLANKS   DSCCYC
     C                     ELSE                            - X2
     C                     MOVE VDICCD    DSICCD
     C                     MOVE VDICNA    DSICNA
     C                     MOVE VDUCCD    DSUCCD
     C                     MOVE VDBRCD    DSBRCD
     C                     MOVE VDCCYC    DSCCYC
     C                     END                             - E2
     C* LOAD FIELDS
     C                     ADD  1         RRN        81
     C                     ADD  1         WWCPT            SCREEN LINE MBR
     C*
     C* SET UP SUBFILE FIELDS
     C*
     C*
     C* WRITE IN SUBFILE
     C                     WRITEER9100S1
     C*
     C* READ NEXT RECORD
     C                     READ ERICDRL1                 30
     C*
     C                     END                             - E1
     C*
     C                     ENDSR
     C**************************************************************************
     C* INITIAL ROUTINE                                                        *
     C**************************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVE '0'       *IN
     C*
     C*   Select the program MSGQ for error msg.
     C                     MOVEL'*'       TOPQ   10
     C                     MOVEL'*PRV'    TOPRQ   5
     C                     MOVEL'*'       DDPGMQ
     C**********           MOVEL'ERUSRMSG'PMSGF  10                                           CER001
     C                     MOVEL'STUSRMSG'PMSGF  10                                           CER001
     C*
     C*   Fill in fields for subroutine ZASNMS
     C                     MOVEL'ER9100'  ZAPGM  10        Message queue
     C**********           MOVEL'ERUSRMSG'ZAMSGF 10        Message file                       CER001
     C                     MOVEL'STUSRMSG'ZAMSGF 10        Message file                       CER001
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
     C*
     C**  GET INSTALLATION CONTROL DATA RECORD 1:
     C           *LOVAL    SETLLSDBANKL1
     C                     READ SDBANKL1                 88
     C**
     C**  IF RECORD NOT FOUND:
     C           *IN88     IFEQ '1'                        - B1
     C                     MOVEL'ER9100'  DBPGM
     C                     MOVEL'002'     DBASE            *****************
     C                     MOVEL'SDBANKL1'DBFILE 10        ** DB ERROR 02 **
     C                     MOVEL' NONE '  DBKEY            *****************
     C                     EXSR *PSSR
     C                     END                             - E1
     C*
     C           ACTION    IFEQ 'E'
     C                     MOVE '1'       *IN50
     C                     END
     C*
     C                     ENDSR
     C****************************************************************
     C*  ZASNMS : SEND MESSAGE TO PROGRAM'S MESSAGE QUEUE            *
     C****************************************************************
     C           ZASNMS    BEGSR
     C*
     C**   message file specified use PMSGF
     C                     MOVELPMSGF     ZAMSGF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
     C**
     C**   Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C                     MOVEL*BLANK    ZAMSGF           Message file
     C                     MOVEL*BLANK    ZAMSID           Message id.
     C**
     C                     ENDSR
     C*
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - PROGRAM ERROR HANDLING ROUTINE                 *
     C**                                                              *
     C**   CALLS     -                                                *
     C**                                                              *
     C**   CALLED BY -                                                *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C** UPDATE LDA WITH ERROR INFORMATION
     C*
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELDBFILE    WWFILE
     C                     MOVELDBKEY     WWKEY
     C                     MOVELDBPGM     WWPGM
     C                     MOVELDBASE     WWASE
     C                     OUT  LDA
     C*
     C                     DUMP
     C*
     C           #PSSR0    ENDSR
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
