     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas European Reporting I.C.D. - setup')
     F********************************************************************
     F*                                                                  *
     F*    MIDAS/ABS - European Reporting                                *
     F*                                                                  *
     F*    ER9100WG  - European Reporting I.C.D. - setup                 *
     F*                                                                  *
      *  (c) Finastra International Limited 2005                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ER1004             Date 03Nov92               *
      *------------------------------------------------------------------*
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      * ER1004 - Set up output files specific to each statutory reporting*
      *          (or other application)                                  *
     F********************************************************************
     F*                                                                  *
     F*                                                                  *
     F*****************************************************************
     F*
     FSDBANKL1IF  E                    DISK
     FERICDRL0UF  E           K        DISK                      A
     FEROUTFPDUF  E           K        DISK                      A        ER1004
     F*
     F*
     E*****************************************************************
     E*
     E                    ICCD   20  20  2
     E                    ICNA    1  20 20
     E                    CPY@    1   1 80
     I*****************************************************************
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*                                                                   ER
     ILDA        UDS                            256
     I                                      132 183 DBLOT
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I**
      **  D.s. FOR FORMATTING DEFAULT OUTPUT FILE    :                    ER1004
     I            DS                                                      ER1004
     I                                        1  10 OUTF                  ER1004
     I                                        3   4 OUTF34                ER1004
     I*
     I*****************************************************************
     C*
     C                     MOVE '0'       *IN
     C*
     C**  GET INSTALLATION CONTROL DATA RECORD 1:
     C           1         CHAINSDBANKL1             01
     C**
     C**  IF RECORD NOT FOUND:
     C           *IN01     IFEQ '1'
     C                     MOVEL'ER9100WG'DBPGM
     C                     MOVEL'001'     DBASE            *****************
     C                     MOVEL'SDBANKL1'DBFILE           ** DB ERROR 01 **
     C                     MOVEL' NONE '  DBKEY            *****************
     C                     EXSR *PSSR
     C                     END
     C*
     C                     Z-ADD1         X       30
     C           ICCD,X    DOWNE*BLANK
     C                     MOVE ICCD,X    VDICCD  2
     C           VDICCD    CHAINERICDRL0             10
     C           *IN10     IFEQ '1'
     C                     RESETERICDRD0
     C                     MOVE ICCD,X    VDICCD
     C                     MOVELICNA,X    VDICNA
     C                     Z-ADDBJRDNB    VDLCD
     C                     MOVE 'I'       VDTYLC
     C                     WRITEERICDRD0
     C                     END
      *
     C                     EXSR OUTFSR                                    ER1004
      *
     C                     ADD  1         X
     C                     END
     C*
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*****************************************************************
      ** OUTFSR S/R   Write default record                                ER1004
     C*================================================================   ER1004
     C           OUTFSR    BEGSR                                          ER1004
      *                                                                   ER1004
     C           KOUTF     KLIST                                          ER1004
     C                     KFLD           VDICCD                          ER1004
     C                     KFLD           OUTF                            ER1004
      *                                                                   ER1004
     C                     MOVEL'ERXXTRPD'OUTF             XX=BE,NL,etc...ER1004
     C                     MOVELVDICCD    OUTF34                          ER1004
      *                                                                   ER1004
     C           KOUTF     CHAINEROUTFF0             10                   ER1004
     C           *IN10     IFEQ '1'                                       ER1004
     C                     MOVELICCD,X    ERICCD           Ctry/Applicat. ER1004
     C                     MOVELOUTF      EROUTF           File name      ER1004
     C                     MOVEL'M'       ERFREQ           Monthly        ER1004
     C                     WRITEEROUTFF0                                  ER1004
     C                     END                                            ER1004
      *                                                                   ER1004
     C                     ENDSR                                          ER1004
     C*================================================================
     C*
     C** *PSSR  S/R
     C*================================================================
     C           *PSSR     BEGSR
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
     C**
     C                     ENDSR
     C*****************************************************************
**  COUNTRY CODE
LU
**  COUNTRY NARRATIVE
Luxembourg
**  CPY@
(c) Finastra International Limited 2005
