     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AGDL Account file list')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  ERAG04 - AGDL Reporting account file list                    *
      *                                                               *
      *  Function:  This program enables the user to liste account    *
      *             file using a menu option                          *
      *  (phase(s)) Input cycle and end of year                       *
      *                                                               *
      *  Called By: ERCAG04 - AGDL account file list                  *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027A            Date 11May06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX010  *CREATE    Date 27Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027A- Conversion of cust. no. to alpha (post 103 build)   *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX010 - AGDL Reporting statistiques sur les d{pôts garantis *
      *                                                               *
      *****************************************************************
      /SPACE 3
      ****************************************************************
      * USING OF INDICATORS                                          *
      *--------------------------------------------------------------*
      * 66 = OVERFLOW FOR ERAG04P1                                   *
      * 70 = READ FOR AGDLACV0                                       *
      ****************************************************************
      /SPACE 3
      ****************************************************************
      * CALL SUBROUTINES                                             *
      *--------------------------------------------------------------*
      * SRINIT = PROGRAMME INITIALIZATION                            *
      * *PSSR  = DATA BASE ERROR                                     *
      *  RCFP1  - RCF Processing for P1 Report                       *
      *  RCFAU  - RCF Processing for Audit Report                    *
      ****************************************************************
      /EJECT
     FAGDLACV0IF  E           K        DISK         KINFSR *PSSR
      *
      ** AGDL account file
      *
     FERAG04P1O   E             66     PRINTER
     F                                              KINFDS SPOOL1
      *
      ** Details report
      *
     FERAG04AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      *
      ** Audit report
      *
      /SPACE 3
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Data Structure for bank details
      *
     ISDCUST    E DSSDCUSTPD
      *
      ** Data Structure for customer details
      *
     IDSFDY     E DSDSFDY
      *
      ** Short Data Structure for acces programs
      *
     IDSSDY     E DSDSSDY
      *
      ** Long Data Structure for acces programs
      *
     I           SDS
      *
      ***  Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      *
      ***  File Information Data Structure for ERAG04P1
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
     ISPOOLU      DS
      *
      ***  File Information Data Structure for ERAG04AU
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      ****************************************************************
      ** Program Start                                               *
      ****************************************************************
      *
     C                     EXSR SRINIT
      *
      ** Read AGDLACV0
      *
     C                     READ AGDLACV0                 70
      *
     C           *IN70     IFEQ '1'                        - B1
     C                     WRITENOLIP1
     C                     ENDIF                           - E1
      *
     C           *IN70     DOWEQ*OFF                       - B2
      *
      ** If new client acces to customer detail
      *
     C           ALCUST    IFNE CMCNUM                     - B3
     C**********           Z-ADDALCUST    CMCNUM  60                                         CSD027A
     C                     MOVE ALCUST    CMCNUM  6                                          CSD027A
     C                     MOVE ALCUST    WKCNUM  6
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM WKCNUM    WKEY10 10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS                    - B4
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE            ****************
     C                     MOVEL'SDCUSTPD'DBFILE           * DB ERROR 002 *
     C                     MOVELWKCNUM    DBKEY     P      ****************
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           - E4
     C                     ENDIF                           - E3
      *
     C           *IN66     IFEQ *ON                        - B5
     C                     WRITEHEADP1
     C                     ENDIF                           - E5
     C                     MOVE '0'       *IN66
     C                     WRITEDETAP1
      *
     C                     READ AGDLACV0                 70
     C                     ENDDO                           - E2
      *
     C                     WRITEENDP1
      ****************************************************************
      ** Program End                                                 *
      ****************************************************************
     C                     SETON                       LR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SRINIT - Subroutine to do program initialisation              *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           ** SRINIT **
      *
      ** Reset indicators
      *
     C                     MOVE *OFF      *IN
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
      *
      ** Set up ZA96 field for the report
      *
     C                     MOVE *ALL'-'   ZA104
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
      *
      ***  Access to bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS                    - B1
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY     P      ****************
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           - E1
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           BJDFIN    IFEQ 'M'                        - B1
     C                     SETON                     98
     C                     ENDIF                           - E1
      *
     C                     WRITEHEADP1
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  RCF Processing for File.
      *
     C                     EXSR RCFP1
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           DBASE     IFEQ 0                          - B1
     C                     Z-ADD999       DBASE
     C                     ENDIF                           - E1
      *
      ***  Print error message in Audit report
      *
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     WRITEENDAU
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2005
