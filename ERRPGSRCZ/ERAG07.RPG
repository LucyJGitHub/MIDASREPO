     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AGDL Customers list')
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  ERAG07 - AGDL Reporting customers list                       *
      *                                                               *
      *  Function:  This program enables the user to liste customers  *
      *             using a menu option                               *
      *  (phase(s)) Input cycle and end of year                       *
      *                                                               *
      *  Called By: ERCAG07 - AGDL Customers list                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 11May06               *
      *  Prev Amend No. CER001             Date 25Aug05               *
      *                 ULX010  *CREATE    Date 09Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027A- Conversion of cust. no. to alpha (post 103 build)   *
      *  CER001 - LUX Upgrade to MidasPlus (Recompile only)           *
      *  ULX010 - AGDL Reporting statistiques sur les d{pôts garantis *
      *                                                               *
      *****************************************************************
      /EJECT
     FAGDLCUV0IF  E           K        DISK         KINFSR *PSSR
      *
      ** AGDL account file
      *
     FERAG07P1O   E             66     PRINTER
     F                                              KINFDS SPOOL1
      *
      ** Details report
      *
     FERAG07AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      *
      ** Audit report
      *
      /SPACE 3
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
      *
      ** Data Structure for bank details
      *
     ISDCUST    E DSSDCUSTPD
      *
      ** Data Structure for customer details
      *
     IDSFDY     E DSDSFDY
      *
      ** Short Data Structure for acces programs
      *
     IDSSDY     E DSDSSDY
      *
      ** Long Data Structure for acces programs
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      *
      ***  File Information Data Structure for ERAG07P1
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
     ISPOOLU      DS
      *
      ***  File Information Data Structure for ERAG07AU
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      ****************************************************************
      ** Program Start                                               *
      ****************************************************************
      *
     C                     EXSR SRINIT
      *
      ** Read AGDLCUV0
      *
     C                     READ AGDLCUV0                 70
     C           *IN70     IFEQ '1'                        B1
     C                     WRITENOLIP1
     C                     ENDIF                           E1
      *
     C           *IN70     DOWEQ*OFF                       B1
      *
      ** If new client acces to customer detail
      *
     C           ADCUST    IFNE CMCNUM                     B2
     C**********           Z-ADDADCUST    CMCNUM  60                                         CSD027A
     C                     MOVE ADCUST    CMCNUM  6                                          CSD027A
     C                     MOVE ADCUST    WKCNUM  6
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY   ' WOPTN   7
     C                     PARM WKCNUM    WKEY10 10
     C                     PARM *BLANKS   WKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           WRTCD     IFNE *BLANKS                    B3
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE            ****************
     C                     MOVEL'SDCUSTPD'DBFILE           * DB ERROR 002 *
     C                     MOVELWKCNUM    DBKEY     P      ****************
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           E3
     C                     ENDIF                           E2
      *
     C           *IN66     IFEQ *ON                        B2
     C                     WRITEHEADP1
     C                     ENDIF                           E2
     C                     MOVE '0'       *IN66
     C                     WRITEDETAP1
      *
     C                     READ AGDLCUV0                 70
     C                     ENDDO                           E1
     C                     WRITEENDP1
      ****************************************************************
      ** Program End                                                 *
      ****************************************************************
     C                     SETON                       LR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SRINIT - Subroutine to do program initialisation              *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls:        *pssr                                           *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           ** SRINIT **
      *
      ** Reset indicators
      *
     C                     MOVE *OFF      *IN
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      ***  Access to bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS                    - B1
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY     P      ****************
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           - E1
      *
     C           BJDFIN    IFEQ 'M'                        - B1
     C                     SETON                     98
     C                     ENDIF                           - E1
      *
      *
     C                     WRITEHEADP1
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  RCF Processing for File.
      *
     C                     EXSR RCFP1
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: Main                                               *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK                     - B1
     C                     MOVE 'Y'       @RUN    1
     C                     ENDIF                           - E1
      *
     C           DBASE     IFEQ 0                          - B1
     C                     Z-ADD999       DBASE
     C                     ENDIF                           - E1
      *
      ***  Print error message in Audit report
      *
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     WRITEENDAU
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
