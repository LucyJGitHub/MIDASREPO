     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas AGDL Reporting deposit file setup')              *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  ERAG13 - AGDL Reporting deposit file setup                   *
      *                                                               *
      *  Function:  This program enables the user to filled account   *
      *             file using a menu option                          *
      *  (phase(s)) Input cycle and end of year                       *
      *                                                               *
      *  Called By: ERCAG13 - AGDL deposit file setup                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 11May06               *
      *                 CER001             Date 25Aug05               *
      *                 UPG402             Date 04Oct04               *
      *                 175783             Date 08Mar00               *
      *                 166701             Date 02Sep99               *
      *                 ULX010  *CREATE    Date 24Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompile)                                         *
      *  CSD027A- Conversion of cust. no. to alpha (post 103 build)   *
      *  CER001 - LUX Upgrade to MidasPlus (Recompile only)           *
      *  UPG402 - Recompile due to upgrade to R4.02                   *
      *  175783 - Do not report deal with VDAT > EOY                 *
      *  166701 - Number of records truncated                         *
      *           (Just recompiled)                                   *
      *  ULX010 - AGDL Reporting statistiques sur les d{pôts garantis *
      *           Change Control CC0001                               *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     FDEALSDC IF  E           K        DISK         KINFSR *PSSR
     FAGDLDLV0UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      *
      ** AGDL deposit file
      *
     FERAG13AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      *
      ** Audit report
      *
      /SPACE 3
     E                    CPY@    1   1 80
      ** Array containing economic sector to be extracted
      *
     E                    XABB       50  3   XABC    6
      *
      ** Array containing Copyright statement
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ISDBANK    E DSSDBANKPD
     ISDBRCH    E DSSDBRCHPD
      *
      ** Data Structure for bank details
      *
     IDSFDY     E DSDSFDY
      *
      ** Short Data Structure for acces programs
      *
     IDSSDY     E DSDSSDY
      *
      ** Long Data Structure for acces programs
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOLU      DS
      *
      ***  File Information Data Structure for ERAG02AU
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      ****************************************************************
      ** Program Start                                               *
      ****************************************************************
      *
     C                     EXSR SRINIT
      *
      ** Read account file
      *
     C                     READ DEALSDC                  70
      *
     C           *IN70     DOWEQ*OFF                       - B1
      *
      ** Check live record
      *
     C           RECI      IFEQ 'D'                        - B2
     C           VDAT      ANDLEBJEYD                                     175783
     C           DLST      ANDNE'FI'                                      175783
      *
     C           DTYP      IFEQ 'CD'                       - B3
     C           DTYP      OREQ 'TD'
     C           PSTM      IFNE 04                         - B3.1
     C           PSTM      ANDNE05
     C           PSTM      ANDNE14
      *
      ** Check if customer is the initial branch customer
      ** for the branch
      *
     C                     MOVE CNUM      WKCNUM  6
     C                     Z-ADD1         J       20
     C           BRCA      LOKUPXABB,J                   21
     C           *IN21     IFEQ '1'                        - B4
     C           WKCNUM    IFNE XABC,J                     - B5
      *
      *
     C                     EXSR SRWRIT
      *
     C                     ENDIF                           - E5
     C                     ENDIF                           - E4
     C                     ENDIF                           - E3
     C                     ENDIF                           - E3.1
     C                     ENDIF                           - E2
      *
     C                     READ DEALSDC                  70
      *
     C                     ENDDO                           - E1
      ****************************************************************
      ** Program End                                                 *
      ****************************************************************
     C                     WRITEDETA
     C                     WRITEENDAU
     C                     SETON                       LR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * SRINIT - Subroutine to do program initialisation              *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           ** SRINIT **
      *
      ** Reset indicators
      *
     C                     MOVE *OFF      *IN
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
      *
      ***  Access to bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           WRTCD     IFNE *BLANKS                    - B1
     C           *LOCK     IN   LDA
     C                     Z-ADD001       DBASE            ****************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 001 *
     C                     MOVEL'*FIRST'  DBKEY     P      ****************
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           - E1
      *
      ** Set Date Format Indicator *IN98  on if date format is MMDDYY
      *
     C           BJDFIN    IFEQ 'M'                        - B1
     C                     SETON                     98
     C                     ENDIF                           - E1
      *
      ***  Access to branch Details for loading all the branches
      ***  for the bank (50 branches maximum)
      ***  array XABB = branch code / array XABC = branch customer
      *
     C                     CALL 'AOBRCHR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C                     PARM *BLANKS   WKEY3   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C           WRTCD     IFNE *BLANKS                    - B1
     C           *LOCK     IN   LDA
     C                     Z-ADD002       DBASE            ****************
     C                     MOVEL'SDBRCHPD'DBFILE           * DB ERROR 002 *
     C                     MOVEL'*FIRST'  DBKEY     P      ****************
     C                     MOVELPGM       DBPGM     P
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           - E1
      *
     C                     Z-ADD0         J
     C           WRTCD     DOWEQ*BLANKS                    - B1
     C                     ADD  1         J
     C                     MOVE A8BRCD    XABB,J
     C                     MOVE A8BICN    XABC,J
      *
     C                     CALL 'AOBRCHR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*NEXT  ' WOPTN   7
     C                     PARM *BLANKS   WKEY3   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C                     ENDDO                           - E1
      *
     C                     WRITEHEADAU
     C                     Z-ADD*ZEROS    RREXTR
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      * SRWRIT - Subroutine to write a record to AGDLDLV0             *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRWRIT    BEGSR                           ** SRWRIT **
      *
      ** Acces to AGDLDLV0
      *
     C           DLNO      CHAINAGDLDLV0             72
      *
      ** Add a record if the key doesn't exist
      *
     C           *IN72     IFEQ '1'                        - B1
     C                     MOVE DLNO      AMDEAL
     C                     MOVE CNUM      AMCNUM
     C                     Z-ADD1         AMPART
     C**********           Z-ADD0         AMLIN1                                             CSD027A
     C**********           Z-ADD0         AMLIN2                                             CSD027A
     C**********           Z-ADD0         AMLIN3                                             CSD027A
     C                     MOVE *BLANKS   AMLIN1                                             CSD027A
     C                     MOVE *BLANKS   AMLIN2                                             CSD027A
     C                     MOVE *BLANKS   AMLIN3                                             CSD027A
     C                     ADD  1         RREXTR
     C                     WRITEAGDLDLD0
     C                     COMIT
     C                     ENDIF                           - E1
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK                     - B1
     C                     MOVE 'Y'       @RUN    1
     C                     ENDIF                           - E1
      *
     C           DBASE     IFEQ 0                          - B1
     C                     Z-ADD999       DBASE
     C                     ENDIF                           - E1
      *
      ***  Print error message in Audit report
      *
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     WRITEENDAU
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
