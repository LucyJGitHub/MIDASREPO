     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER Lot ID & Decision Key Processing')            *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy                    *
      *                                                               *
      *  ERIT62 - Lot ID & Decision Key Processing                    *
      *                                                               *
      *  Called By: RPG/ERIT60                                        *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997.          *
      *                                                               *
      *  Last Amend No. MD058100           Date 20May21               *
      *  Prev Amend No. LUC139 *Amend      Date 29Apr21               *
      *                 LUC139 *Amend      Date 16Mar21               *
      *                 LUC139             Date 04Jan21               *
      *                 MMI033             Date 31Mar99               *
      *                 UIT021             Date 29Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD058100 - Rollup of additional BoI onsite fixes at FbMidas  *
      *  LUC139 - retrieve 4 leftmost chars from account code         *
      *  LUC139 - Set EXDECK = 14A                                    *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  MMI033 - Midas Italy Puma/2 Extract Change 3                 *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *                                                               *
      *****************************************************************
     FERITACL1IF  E           K        DISK
     F* N3 - ER Italy Account Code File
      *
     FERITTTL2IF  E           K        DISK
     F* N4 - ER Italy Decision table - Transaction Type File
      *
     FACKEY   IF  E           K        DISK                               MMI033
     F            ACKEYAKF                          KIGNORE               MMI033
      *===============================================================*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      *===============================================================*
      *
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
     I**
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I**
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      * Program status data structure
     I           SDS
     I                                     *PROGRAM PGM
     C*================================================================
      *
     C                     EXSR #INIT
     C                     EXSR #SCHA
      *
     C                     RETRN
     C/EJECT
     C**=================================================================
      **
      **  #INIT       Program initialisation sub-routine
      **
      *-----------------------------------------------------------
     C           #INIT     BEGSR
      *-----------------------------------------------------------
      *
     C           *ENTRY    PLIST
      *  Input Parameters
     C                     PARM           EXLOTD  3        Lot descriptor
     C                     PARM           EXDECK 14        Decision key
     C                     PARM           EXDRCR  10       Dr/Cr indicator
      *  Output Parameters
     C                     PARM           ICLINI  70       Line Item code
     C                     PARM           ICRESV  20       Real S.V.      MMI033
     C                     PARM           ICCPAC  70       Complementary Item
     C                     PARM           ICCOMP 15        Account code   MMI033
     C                     PARM           ERROR   1        Eror code
      *
     C           *NAMVAR   DEFN           LDA
     C                     UNLCKLDA
      *
      **
     C                     MOVE '0'       *INU7
      **
     C                     ENDSR
     C**=================================================================
      **
      **  #SCHA       Retrieve Line Item Code (Voce/Sottovoce) and
      **              Complementary Item Code
      *-----------------------------------------------------------
     C           #SCHA     BEGSR
      *-----------------------------------------------------------
      *
      **  Access Decision table :
      *     Account extract:
     C           EXLOTD    IFEQ 'GLA'                      - B1
     C                     MOVELEXDECK    KYACOD 10
     C           KYACOD    CHAINERITACL1             80
      **   If record not found, or rubrique = 0, not to be reported:
     C           *IN80     IFEQ '0'                        - B2
     C           EXDRCR    IFEQ 0                          - B3
     C                     MOVELN3RBND    ICLINI                 Line Debit
     C                     MOVELN3RBED    ICRESV                 Line DebiMMI033
     C                     MOVELN3CPND    ICCPAC                 Compl. Debit
     C                     ELSE                            - X3
     C                     MOVELN3RBNC    ICLINI                 Line Credit
     C                     MOVELN3RBEC    ICRESV                 Line CredMMI033
     C                     MOVELN3CPNC    ICCPAC                 Compl. Credit
     C                     END                             - E3
     C                     MOVELEXDECK    WCAM04  4                       MMI033
     C                     MOVE WCAM04    ICCOMP                 Line CredMMI033
      *                                                                   MMI033
     C                     END                             - E2
      *
     C                     ELSE                            - X1
      *
      *     Transaction record:
     C                     MOVELEXDECK    KYTRNT  8
     C           KYTRNT    CHAINERITTTL2             80
     C           *IN80     IFEQ '0'                        - B2
     C           EXDRCR    IFEQ 0                          - B3
     C                     MOVELN4RBND    ICLINI                 Line Debit
     C                     MOVELN4CPND    ICCPAC                 Compl. Debit
     C                     ELSE                            - X3
     C                     MOVELN4RBNC    ICLINI                 Line Credit
     C                     MOVELN4CPNC    ICCPAC                 Compl. Credit
     C                     END                             - E3
     C                     END                             - E2
      *                                                                   MMI033
     C                     MOVELEXDECK    WCAM04                          MMI033
     C                     MOVE WCAM04    WCAM02  2                       MMI033
     C                     MOVELWCAM04    WCAM03  3                       MMI033
     C                     MOVE 'V'       WCAM03  3                       MMI033
     C                     MOVELWCAM03    WCAM05  5                       MMI033
     C                     MOVE WCAM02    WCAM05  5                       MMI033
     C                     MOVE 'A'       WCAM10 10                       MMI033
     C                     MOVELWCAM05    WCAM10                          MMI033
     C                     MOVELWCAM10    WCAM14 14                       LUC139
     C*                                                                   MMI033
     C********** WCAM10    CHAINACKEYALF             80             MMI033LUC139
     C           WCAM14    CHAINACKEYALF             80                   LUC139
     C           *IN80     IFEQ '0'                        - B2           MMI033
     C           EXDRCR    IFEQ 0                          - B3           MMI033
     C**********           MOVE ACD1      ICCOMP                    MMI033LUC139
     C                     MOVELACD1      WCAM04                          LUC139
     C                     ELSE                            - X3           MMI033
     C**********           MOVE ACD4      ICCOMP                    MMI033LUC139
     C                     MOVELACD4      WCAM04                          LUC139
     C                     END                             - E3           MMI033
     C                     MOVE WCAM04    ICCOMP                          LUC139
     C                     END                             - E2           MMI033
      *                                                                   MMI033
     C                     END                             - E1
      *
     C                     ENDSR
      **=================================================================
      **
      **  #DBERR      Program-detected exception OR monitored error
      **              sub-routine.
      **
     C*================================================================
     C           #DBERR    BEGSR
     C*
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
     C**
     C                     ENDSR
     C********************************************************************
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997.
