     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER Customer Details Processing')                 *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module - Italy                    *
      *                                                               *
      *  ERIT63 - Customer Details Processing                         *
      *                                                               *
      *  Called By: RPG/ERIT60                                        *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 1997.          *
      *                                                               *
      *  Last Amend No. LUC139             Date 04Jan21               *
      *  Prev Amend No. MMI033             Date 31Mar99               *
      *                 UIT021             Date 29Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  MMI033 - Midas Italy Puma/2 Extract Change 3                 *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *                                                               *
      *****************************************************************
      *
     FSDCUT2L0IF  E           K        DISK
     F* SC - Italy Customers Extension File
     FSDCUSTL1IF  E           K        DISK                               MMI033
     FIRFXTBL1IF  E           K        DISK                               MMI033
      *
      *===============================================================*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      *===============================================================*
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
     I**
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I**
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      * Program status data structure
     I           SDS
     I                                     *PROGRAM PGM
      **=================================================================
     C                     EXSR #INIT
      **=================================================================
      *
      **  Access Customers extension file  :
     C           EXCUST    CHAINSDCUT2L0             80
      **   If record not found:
     C           *IN80     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA
     C                     MOVEL'631'     DBASE            ******************
     C                     MOVEL'SdCut2l0'DBFILE           ** DB ERROR 631 **
     C                     MOVELEXCUST    DBKEY            ******************
     C                     EXSR #DBERR
     C                     ELSE                            - X1
     C                     MOVE PCCONS    ICCONS
     C                     MOVE PCCPTY    ICCPTY
     C                     MOVE PCECSG    ICECSG
     C                     MOVE PCFIID    ICFIID
     C                     MOVE PCDBCD    ICDBCD
     C                     MOVE PCECBC    ICECBC
     C***********          MOVE PCPRCT    ICPRCT                          MMI033
     C                     MOVEL'BBK'     WBBK                            MMI033
     C                     MOVELPCPROV    WCODPR                          MMI033
     C           KEYBBK    CHAINIRFXTBL1             59                   MMI033
     C  N59                MOVE F0PNAR    ICPRCT                          MMI033
     C   59                MOVE '00000'   ICPRCT                          MMI033
     C                     MOVE PCCNTP    ICCNTP
     C                     MOVE PCFSCD    ICFSCD
     C                     MOVE PCBNTP    ICBNTP
     C                     MOVE PCCUCR    ICCUCR
     C                     MOVE PCCRTP    ICCRTP
     C                     MOVE PCCRNM    ICCRNM
     C                     MOVE PCFCCR    ICFCCR
     C                     MOVE PCHOLC    ICHOLC
     C                     MOVE PCHLCD    ICHLCD
     C                     MOVE PCCUAD    ICCUAD
     C                     MOVE PCCUCT    ICCUCT
     C                     MOVE PCCCCN    ICCCCN
     C                     MOVE PCCCCL    ICCCCL
     C***********          MOVE PCCCCD    ICCCCD                          MMI033
     C                     MOVE PCCURF    ICCURF
     C                     MOVE PCCPPT    ICCPPT
     C                     END                             - E1
      *
     C           EXCUST    CHAINSDCUSTL1             80                   MMI033
      *                                                                   MMI033
      **   If record not found:                                           MMI033
      *                                                                   MMI033
     C           *IN80     IFEQ '1'                        - B1           MMI033
     C           *LOCK     IN   LDA                                       MMI033
     C                     MOVEL'632'     DBASE            ***************MMI033
     C                     MOVEL'SDCUSTPD'DBFILE           ** DB ERROR 632MMI033
     C                     MOVELEXCUST    DBKEY            ***************MMI033
     C                     EXSR #DBERR                                    MMI033
     C                     ELSE                            - X1           MMI033
     C           BBBNBI    IFEQ 'Y'                                       MMI033
     C                     MOVEL'02'      ICCOTP                          MMI033
     C                     ELSE                            - X1           MMI033
     C                     MOVEL'00'      ICCOTP                          MMI033
     C                     END                                            MMI033
     C                     END                             - E1           MMI033
      *                                                                   MMI033
     C**=================================================================
      *
     C                     RETRN
      **=================================================================
      **
      **  #INIT       Program initialisation sub-routine
      **
      *----------------------------------------------------------------
     C           #INIT     BEGSR
      *----------------------------------------------------------------
      *
     C           *NAMVAR   DEFN           LDA
     C                     UNLCKLDA
      *
      ** Parameters received/transfered to Main Component:
     C           *ENTRY    PLIST
      *  Input Parameters
     C                     PARM           EXCUST  6
      *  Output Parameters
     C                     PARM           ICCONS  10       Consolidation code
     C                     PARM           ICCPTY  40       Counterparty code
     C                     PARM           ICECSG  30       Economical sector
     C                     PARM           ICFIID  9        Financial counter. Id
     C                     PARM           ICDBCD  10       Doubtfull code
     C                     PARM           ICECBC  30       Economical Brch class
     C                     PARM           ICPRCT  50       Italian Province code
     C                     PARM           ICCNTP  50       Counterparty type
     C                     PARM           ICFSCD 110       Fiscal code
     C                     PARM           ICBNTP  1        Beneficiary type
     C***************      PARM           ICCPPA  10       Counterparty particip
     C                     PARM           ICCUCR  90       Customer CR number
     C                     PARM           ICCRTP  10       Customer CR type
     C                     PARM           ICCRNM 60        Customer CR name
     C                     PARM           ICFCCR 16        Customer CR fiscal co
     C                     PARM           ICHOLC 30        Customer CR residence
     C                     PARM           ICHLCD  60       CAB cust. residence c
     C                     PARM           ICCUAD 40        Customer address
     C                     PARM           ICCUCT 40        Customer city
     C                     PARM           ICCCCN  7        CCIAA customer number
     C                     PARM           ICCCCL 15        CCIAA cust. residence
     C                     PARM           ICCCCD  3        Convention code CD's
     C                     PARM           ICCURF  10       Centr. report. filter
     C                     PARM           ICCPPT  20       Counterparty particip
     C                     PARM           ICCOTP  2        Type of Cred.OpMMI033
     C                     PARM           ERROR   1
      *
     C                     MOVE '0'       *INU7
      *
     C           KEYBBK    KLIST                                          MMI033
     C                     KFLD           WBBK    3                       MMI033
     C                     KFLD           WCODPR 18                       MMI033
      *                                                                   MMI033
      *** Default ICFIID to 7990001W0
      *
     C           '7990001' CAT  'W0':0    ICFIID           Financial instit.
      *
     C                     ENDSR
      *================================================================
      **
      **  #DBERR      DATA BASE ERROR
      **
      *----------------------------------------------------------------
     C           #DBERR    BEGSR
      *----------------------------------------------------------------
      **
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     MOVE 'Y'       ERROR
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
      **
     C                     ENDSR
      ********************************************************************
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997.
