     H        1
     F*------------------------------------------------------------------*
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER EXTRACT LENDING FUTURE REPAYMENTS')           *
      *****************************************************************
      *                                                               *
      *  Midas -  European Reporting - Italy                          *
      *                                                               *
      *  ERIT79 - EXTRACT LENDING FUTURE REPAYMENTS                   *
      *                                                               *
      *  Function:  This program generates the payment for Loans      *
      *                                                               *
      *  Called By: ERC0702 as part of ERREPORT job                   *
      *                                                               *
      *  (C) Copyright Misys 2002                                     *
      *                                                               *
      *  Last Amend No. MD058100           Date 20May21               *
      *  Prev Amend No. LUC139             Date 04Jan21               *
      *                 225425             Date 03Mar04               *
      *                 MMI094             Date 08Oct02               *
      *                 208262                  08Aug02               *
      *                 197061                  30Aug01               *
      *                 MMI075                  09AUG00               *
      *                                                               *
      *------------------------------------------------------------------*
      *                                                               *
      *  MD058100 - Rollup of additional BoI onsite fixes at FbMidas  *
      *  LUC139 - Upgrade UCI Italian Returns                         *
      *  225425 - Record reference (used for 204 and 277) to contain  *
      *           only Loan Number.                                   *
      *  MMI094 - Rewritten                                           *
      *  208262 - Divide by zero error when a mortgage is about to    *
      *           mature and therefore next repayment date is zero.   *
      *  197061 - Cannot handle amounts above 9 digits                *
      *  MMI075 - Midas Italy Puma/2 : created                        *
      *                                                               *
     F*------------------------------------------------------------------*
     FEREXTRL0IF  E           K        DISK
     FERLVENL0IF  E           K        DISK
     FCLOAN   IF  E           K        DISK
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     FERICDRL1IF  E           K        DISK
     FSDCURRL1IF  E           K        DISK
     FSDCRT1L0IF  E           K        DISK
     FSDCUSTL0IF  E           K        DISK
     FSDCUT2L0IF  E           K        DISK
     FERITTRPDO   E                    DISK
     E*                                                                ***
     E/COPY ZSRSRC,ZDATE9Z1
     I*                                                                ***
     IEXTREF      DS                             21
     I                                        1   6 KLNRF
     I                                       20  21 CHKREP
     I*
     IDSORAP      DS                             35
     I                                        1   4 EXRIND
     I                                        6  11 LNRF
     I********                               16  20 REX                   225425
     I********                               21  23 EXBRCD                LUC139
     I                                       27  29 EXBRCD                LUC139
      *
     IIRSTAT    E DSIRSTAT
      *
     I/COPY ZSRSRC,ZDATE9Z2
     I/COPY ZSRSRC,ZDAT10Z1
      /EJECT
      *****************************************************************
      *                                                               *
      * MAINLINE                                                      *
      *                                                               *
      *****************************************************************
      *
     C                     EXSR SRINIT
      *
      ** Process all extracted Loans and Participations
      *
     C                     READ EREXTRL0                 LR
     C           *INLR     DOWEQ*OFF
      *
     C           EXLOTD    IFEQ 'LEN'
     C           EXLOTD    OREQ 'PPU'
      *
      * Ignore Repayments records generated by ER0623
      *
     C           CHKREP    IFNE 'REP'
      *
     C           KLOAN     CHAINCLOAN                01
     C           *IN01     IFEQ *OFF
     C                     EXSR PROCES
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     READ EREXTRL0                 LR
     C                     ENDDO
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * PROCES - Process all repayments related to one loan           *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
     C           LNRF      CHAINERLVENL0             02
      *
      * Access data related to loan ccy and customer
      *
     C           *IN02     IFEQ *OFF
     C           EXCCYC    CHAINSDCRT1L0             03
     C           EXCCYC    CHAINSDCURRL1             04
     C           EXCUST    CHAINSDCUT2L0             05
     C           EXCUST    CHAINSDCUSTL0             06
     C                     ENDIF
      *
      * If all data found, extract each future repayment
      *
     C           *IN02     DOWEQ*OFF
     C           *IN03     ANDEQ*OFF
     C           *IN04     ANDEQ*OFF
     C           *IN05     ANDEQ*OFF
     C           *IN06     ANDEQ*OFF
      *
     C           EDAT      IFGT IREMD
     C           BRCA      ANDEQEXBRCD
      *
     C           ETYP2     IFEQ 'R1'
     C           ETYP2     OREQ 'M1'
      *
     C                     EXSR OUTPUT
      *
     C                     ENDIF
     C                     ENDIF
      *
     C           LNRF      READEERLVENL0                 02
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * OUTPUT -  Format output fields and write a record to the      *
      *           Italian Returns extract file                        *
      *                                                               *
      *****************************************************************
     C           OUTPUT    BEGSR
      *
     C                     CLEARERITTRD0
      *
      * Voce/Sottovoce
      *
     C           BBBNBI    IFEQ 'Y'
     C                     Z-ADD0390352   ICLINI
     C                     ELSE
     C                     Z-ADD0390302   ICLINI
     C                     END
      *
      * Divisa
      *
     C           EXCCYC    IFEQ VDCCYC
     C                     Z-ADD1         ICCYDG
     C                     ELSE
     C                     Z-ADD2         ICCYDG
     C                     ENDIF
      *
      * Residenza
      *
     C           EXCUCC    IFEQ VDICCD
     C                     Z-ADD1         ICRSDG
     C                     ELSE
     C                     Z-ADD2         ICRSDG
     C                     ENDIF
      *
      * Durata
      *
     C           EXENDD    IFLE LIMITD
     C                     Z-ADD1         ICIDUR
     C                     ELSE
     C                     Z-ADD2         ICIDUR
     C                     ENDIF
      *
     C                     MOVELEXCUST    ICCUST
     C                     Z-ADDPCECSG    ICECSG
     C                     Z-ADDPBUCCY    ICUCCY
     C                     MOVEL'070'     ICEDIV
     C                     MOVE '*'       ICEND
      *
      * Transaction reference
      *
     C                     MOVELDSORAP    ICNOTE
     C                     MOVELDSORAP    ICORAP
     C                     MOVEL'ROLL'    ICORAP
      *
      * Value date
      *
     C                     Z-ADDEDAT      @@DAYN
     C                     EXSR ZDATE9
     C                     Z-ADD@@VDT     ICENDD
      *
      * Principal in Original Ccy
      *
     C                     Z-ADD100       FACTOR  41
     C                     DO   A6NBDP
     C                     DIV  10        FACTOR
     C                     ENDDO
     C           EAMT      MULT FACTOR    ICUNSC
      *
      * Interest in Original Ccy
      *
     C           OTHA      MULT FACTOR    ICUINC
      *
      * Convert amounts in Reporting Currency
      *
     C           CCY       IFEQ VDCCYC
      *
     C                     Z-ADDICUNSC    ICUNSL
     C                     Z-ADDICUINC    ICINCR
      *
     C                     ELSE
      *
     C                     Z-ADDICUNSC    CNVAMT
     C                     EXSR CNVCCY                                 es
     C                     Z-ADDCNVAMT    ICUNSL    H
      *
     C                     Z-ADDICUINC    CNVAMT
     C                     EXSR CNVCCY                                 es
     C                     Z-ADDCNVAMT    ICINCR    H
      *
     C                     ENDIF
      *                                                                   225425
      * Decision Table Key                                                225425
      *                                                                   225425
     C                     MOVE EXDECK    ICDECK                          225425
      *
     C                     ADD  1         LASTSQ
     C                     Z-ADDLASTSQ    ICSQCT
     C                     WRITEERITTRD0
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * CNVCCY - Convert amount from Original Currency to Reporting   *
      *          Currency.                                            *
      *                                                               *
      *****************************************************************
      *
     C           CNVCCY    BEGSR
      *
      * Original to Base
      *
     C           A6MDIN    IFEQ 'M'
     C                     MULT A6SPRT    CNVAMT 248
     C                     ELSE
     C                     DIV  A6SPRT    CNVAMT    H
     C                     ENDIF
      *
      * Base to Reporting
      *
     C           VDMDIN    IFEQ 'D'
     C                     MULT VDSPRT    CNVAMT    H
     C                     ELSE
     C                     DIV  VDSPRT    CNVAMT    H
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SRINIT - Initial Processing                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C**********           MOVE '1 REX'   REX                             225425
     C                     MOVE 'A'       KRECT
      *
     C           KLOAN     KLIST
     C                     KFLD           KLNRF   6
     C                     KFLD           KRECT   1
      *
      * Retrieve DTAARA for reporting date and last sequence nbr
      *
     C           *NAMVAR   DEFN           IRSTAT
     C                     IN   IRSTAT
      *
     C                     Z-ADDIRSQCT    LASTSQ  60
      *
      * Setup limit date for short-medium term as 18 months from
      * reporting date
      *
     C                     Z-ADDIREMD     @@DAYN  50
     C                     EXSR ZDATE9
     C                     MOVEL@@VDT     LIMITD  80
     C           @@Z71M    IFLE 6
     C                     ADD  10604     LIMITD
     C                     ELSE
     C                     ADD  19404     LIMITD
     C                     END
      *
      * Access ER ICD to retrieve Reporting Currency
      *
     C           'IT'      CHAINERICDRL1             10
      *
      * Retrieve data of Reporting Currency
      *
     C           VDCCYC    CHAINSDCURRL1             11
      *
     C                     Z-ADDA6SPRT    VDSPRT 138       Spot rate
     C                     MOVE A6MDIN    VDMDIN  1        Mult/Div Ind
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - ABNORMAL TERMINATION                                 *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      *
     C/COPY ZSRSRC,ZDATE9Z3
      *
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
