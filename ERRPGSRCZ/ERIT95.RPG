     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER I.C.D. maintenance')                          *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting - Italy                           *
      *                                                               *
      *  ERIT95 - European Italy  I.C.D. maintenance                  *
      *                                                               *
      *  (c) Copyright Midas-Kapiti International 1997.               *
      *                                                               *
      *  Last Amend No. LUC139                  Date 10Feb21          *
      *  Prev Amend No. UIT021  *Create         Date 13JAN97          *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  UIT021 - Bank of Italy Monthly Reporting                     *
      *                                                               *
      *****************************************************************
      *
     FERIT95P1O   E             01     PRINTER      KINFDS SPOOL1     UC
      *                                Printing report.
     FERIT95AUO   E                    PRINTER      KINFDS SPOOLU
      *                                Audit report.
     FERITICL1IF  E           K        DISK
      *                                European reporting I.C.D For Italy
     FERICDRL1IF  E           K        DISK
      *                                European reporting I.C.D
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  -------------------------------------------                  *
      *                                                               *
      *  01   - Overflow on ERIT95                                    *
      *  50   - No details to report.                                 *
      *  81   - End Of File on ERITICL1.                              *
      *  82   - Record not found in ERICDRL1.                         *
      *  95   - Database error.                                       *
      *  98   - Date in format MMDDYY.                                *
      *  LR   - End processing.                                       *
      *
      *  U8+U8- Database error.                                       *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMMYY)        *
      *                                                               *
      *****************************************************************
     E/EJECT
      *
      ***  Array for errors messages.
     E                    MES     1   3 80               DB error msg.
      *
      ***  Array for dates conversion.
      /COPY ZSRSRC,ZDATE2Z1
      *
      ***  Array containing Copyright statement.
     E                    CPY@    1   1 80
      *
      /SPACE 3
      *
      ***  Local data area for database error details
     ILDA       E DSLDA                         256
      *
      ***  Get program name and exception data from PSDS.
     IPSDS       SDS
     I                                     *PROGRAM #PGM
     I                                       91 170 WWEXCP
      *
      ***  File Information Data Structure - AU report.
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  File Information Data Structure - P1 report.
     ISPOOL1      DS
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680WWLINE
      *
      ***  External data structures for Bank Details.
     ISDBANK    E DSSDBANKPD
      *
      ***  First DS for access programs, short data structure.
     IDSFDY     E DSDSFDY
      *
      /EJECT
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *
      ***  Closing of printer file.
      *
     C                     CLOSEERIT95P1
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFP1  - RCF Processing for Printing Report                  *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Midas Bank I.C.D. Access Object program.            *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ***  Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PRLEV   1
     C                     PARM           PENT    3
      *
      ***  Initialise LDA program field.
     C                     MOVEL#PGM      DBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
     C           WRTCD     IFNE *BLANKS                    *B1
     C                     MOVEL'01'      P2ERR            ***************
     C                     Z-ADD1         X       20       * DB ERROR 01 *
     C                     MOVE *BLANKS   P2EDTA           ***************
     C                     MOVE *ON       *IN95
     C                     EXSR AUDIT
      *
     C                     ENDIF                           *E1
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
     C           BJDFIN    IFEQ 'M'                        *B1
     C                     SETON                     98
      *
     C                     ENDIF                           *E1
      *
      ***  Opening of printer file.
     C                     OPEN ERIT95P1
      *
      ***  RCF Processing for Printing File.
     C                     EXSR RCFP1
      *
      ***  RCF Processing for Audit File.
     C                     EXSR RCFAU
      *
      ***  Set on overflow indicator to print report heading.
     C                     SETON                     01
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Access Italy I.C.D.
     C                     READ ERITICL1                 81
     C           *IN81     IFEQ '1'                        *B1
     C                     SETON                     50    No details to report
     C                     MOVEL'02'      P2ERR            ***************
     C                     Z-ADD2         X                * DB ERROR 02 *
     C                     MOVE *BLANKS   P2EDTA           ***************
     C                     MOVE '1'       *IN95
     C                     EXSR AUDIT
      *
     C                     END                             *E1
      *
      ***  Access I.C.D. file to retrieve the user country code for print.
     C           N1ICCD    CHAINERICDRL1             82    *B1
     C           *IN82     IFEQ '1'
     C                     MOVEL'03'      P2ERR            ***************
     C                     Z-ADD3         X                * DB ERROR 03 *
     C                     MOVE *BLANKS   P2EDTA           ***************
     C                     MOVE '1'       *IN95
     C                     EXSR AUDIT
      *
     C                     END                             *E1
      *
      ***  Process report lines.
     C                     EXSR REPORT
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
     C                     MOVE VDUCCD    P1UCCD
     C                     MOVE N1LERP    P1LERP
     C                     MOVE N1CORD    P1CORD
     C                     MOVE N1PRRV    P1PRRV
     C                     MOVE N1TYLC    P1TYLC
     C                     Z-ADDN1LCD     ZDAYNO  50
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    P1LCD
      *
      ***  Write out the record.
     C                     WRITEERIT95F1
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES.                          *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR
      *
      ***  If DB error, setup message and switches.
     C           *IN95     IFEQ '1'                        *B1
     C                     MOVEAMES,X     P2MSG
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
      *
     C                     END                             *E1
      *
     C                     WRITEHEADAU
      *
     C           *IN50     IFEQ *ON                        *B1     No details
     C                     WRITENODTLS                             to report
      *
     C                     END                             *E1
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM SFNUM1    ZSNUM   60
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'                        *B1
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     END                             *E1
      *
     C                     ENDSR
      *
      /EJECT
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM SFNUMU    ZSNUMU  60
     C                     PARM *BLANK    ZSERR   1
     C*
     C***  If an error occurred during ZSFILE processing,
     C***  return to the calling program.
     C*
     C           ZSERR     IFEQ 'Y'                        *B1
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     END                             *E1
      *
     C                     ENDSR
      *
      /EJECT
      *
      **************************************************************************
      /TITLE SR/ZDATE2
      **************************************************************************
      /COPY ZSRSRC,ZDATE2Z2
      *
      /EJECT
      *
      **************************************************************************
** MES -  Array to contain error messages for audit report
ICD1 record not found in SDBANKPD
ICD1 record not found in ERITICPD
ICD1 record not found in ERICDRPD
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(C) Copyright Midas-Kapiti International Ltd. 1997.
