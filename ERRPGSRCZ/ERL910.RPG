     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('MIDAS ABS ER Guarantees/Collateral/Provisions Ma5n')   *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ERL910  - Guarantees/Collateral/Provisions Maintenance       *
      *                                                               *
      *  Function:  This program is used to enter guarantee, security *
      *             and collateral details associated with a facility,*
      *             a loan, an account, a book position or a          *
      *             security.                                         *
      *                                                               *
      *  (phase(s)) *all                                              *
      *                                                               *
      *  Called By: ERC0910- Guarantees/Collateral/Provision Maint.   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. CDL096             Date 22Sep14               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CLE064             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 06May06               *
      *                 CER001             Date 25apr05               *
      *                 UPG402             Date 04Oct04               *
      *                 UPGCSW             Date 27Sep02               *
      *                 210004             Date 26Sep02               *
      *                 187689             Date 21Dec00               *
      *                 ULX008             Date 19Jul00               *
      *                 ULX008             Date 23Mar00               *
      *                 ULX008             Date 25Feb00               *
      *                 ULX012             Date 09Feb00               *
      *                 164197             Date 30Jul99               *
      *                 CAD029             Date 27Jan99               *
      *                 147610 PT          Date 26Nov98               *
      *                 ULX012             Date 28Jul98               *
      *                 142421 PT          Date 18Sep98               *
      *                 CAD015             Date 20Sep98               *
      *                 129190             Date 28JAN98               *
      *                 127865             Date 17DEC97               *
      *                 128582             Date 08JAN98               *
      *                 128098 PT          Date 07JAN98               *
      *                 128241 PT          Date 07JAN98               *
      *                 ULX004 MLD & IS    Date 31DEC96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CSD027A - Conversion Of Customer Number to Alpha             *
      *  CER001 - LUX Upgrade to MidasPlus                             *
      *  UPG402 - Recompile due to R4.02 upgrade                      *
      *         - Rename CRSK in TRADSD                               *
      *  UPGCSW - Due to an upgrade of swift changes 2002 rename      *
      *           variable V to W (confict with hook ZSRSRC/ZCOTRZ1   *
      *           Amend ZSRSRC,ZALIGNZ2
      *  210004 - Mismatch between ',' and '.' in Amend mode.         *
      *           Link with fix 206456 in ZEDIT. So, apply same fix   *
      *           in SR/ZALIGN and SR/ZFRPED.                         *
      *  187689 - GRD currency in EMU                                 *
      *           Recompiled due to changes in PF/SDCUSTX6            *
      * ULX008 - "Commissariat aux Bourses" - Change control C        *
      *          Recompiled due to changes in PF/ERLUICPD             *
      * ULX008 - "Commissariat aux Bourses" - Change control A        *
      *          Recompiled due to changes in PF/ERLUICPD             *
      *  ULX008 - CSSF Statuatory Reporting : Commissariat aux Bourses*
      *           Recompiled due to changes in PF/SDCUSTX6            *
      *  ULX012 - Pledges and Multiple Guarantors Management          *
      *           Recompile only due to changes in PF/SDCUSTX6        *
      *  164197 - If used to secure is a ledger in 'IN' ccy,          *
      *           Authorise to input 'EUR' as currency even if secured*
      *           by is type 'S'.                                     *
      *  CAD029 - 'Generated Automatically' improperly displayed      *
      *  147610 - Message id. ERN0905 replaced by ERN0933             *
      *  ULX012 - Guarantees and Collateral Management enhancements   *
      *  142421 - Files must be opened in enquiry mode also           *
      *  CAD015 - Receiving customer must be the original borrower    *
      *           in case of participations purchased                 *
      *  129190 - No branch setup when input done via Security Input. *
      *  127865 - Problem when no all module are on within the bank.  *
      *  128582 - No validation done when garantees maintains via a   *
      *           loan input.                                         *
      *  128098 - Allow loan processing type 70                       *
      *  128241 - Allow decimal places for 'nominal' as nominal       *
      *           currency decimal places                             *
      *  ULX004 - Capital Adequacy                                    *
      *         - IML Circular 97/138                                 *
      *         - Guarantees for EMU                                  *
      *           Percentage 0 must be displayed                      *
      *                                                               *
      *****************************************************************
      *
      *            Display File
     FERL910DFCF  E                    WORKSTN      KINFDS DSPFDS
     F                                        WWRRN KSFILE ERL910S2
      *
      *            Display File Selection Custmoer
     FERL910WNCF  E                    WORKSTN
     F                                        W001RGKSFILE ERL910E
      *
      *            Input Files
     FERGUARL0IF  E           K        DISK
     F            ERGUARD0                          KRENAMEERGUARD2
     F                                              KINFSR *PSSR
      *
      *            Customer Details By Cust Nbr
     FSDCUSTL5IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      *            Customer Details By Short Name
     FSDCUSTL2IF  E           K        DISK
     F                                              KINFSR *PSSR
     F***SDCUSTY6IF  E           K        DISK                                         ULX012 CER001
     FSDCUX2L0IF  E           K        DISK                                                   CER001
     F                                              KINFSR *PSSR          ULX012
     FERLUICL0IF  E           K        DISK                               ULX012
     F                                              KINFSR *PSSR          ULX012
      *
      *            Facility Types
     FFCLTY   IF  E           K        DISK                           UC
     F            FCLTYHHF                          KIGNORE
     F            FCLTYFNF                          KIGNORE
     F                                              KINFSR *PSSR
     F                                              KCOMIT
      *
      *            Loans
     FCLOAN   IF  E           K        DISK                           UC
     F            CLOANHHF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F                                              KINFSR *PSSR
     F                                              KCOMIT
      *
      *            MM Deals
     FDEALSALLIF  E           K        DISK
     F            DEALSDBF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     F            MMDENSP0                          KIGNORE
     F            FXDEALP0                          KIGNORE
     F            DLBHISD0                          KIGNORE
     F            DLCHISD0                          KIGNORE
     F            DLDHISD0                          KIGNORE
     F            DLEHISD0                          KIGNORE
     F            DLGHISD0                          KIGNORE
     F                                              KINFSR *PSSR
     F                                              KINFDS FDEAL
      *
      *            A/C master
     FACCNT   IF  E           K        DISK
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
     F                                              KINFSR *PSSR
      *
      *            A/C master by Retail A/C
     FACNUM   IF  E           K        DISK
     F            ACCNTABF                          KRENAMEACCNUMF
     F                                              KINFSR *PSSR
      *
      *            SE Trades
     FTRADS   IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      *            Book Positions Header
     FBKPHD   IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      *            Securities
     FSECTY   IF  E           K        DISK                           UC
     F                                              KINFSR *PSSR
     F                                              KCOMIT
      *
      *            Customer Positons
     FCPOSN   IF  E           K        DISK
     F                                              KINFSR *PSSR
      *
      *            ER Guarantess & Collateral File
     FERGUARPDO   E           K        DISK
     F                                              KINFSR *PSSR
     F                                              KCOMIT
      *
      *            ER Guarantess & Collateral File - Retrieval LF
     FERGUARL1IF  E           K        DISK
     F            ERGUARD0                          KRENAMEERGUARD1
     F                                              KINFSR *PSSR
     FERGUARL2UF  E           K        DISK
     F            ERGUARD0                          KRENAMEERGUARD3
     F                                              KINFSR *PSSR
     F                                              KCOMIT
      *****************************************************************
      /TITLE FUNCTION OF INDICATORS
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  ===========================================                  *
      *                                                               *
      * 01       - From SFL screen when new customer input            *
      * 03       - F3 pressed                                         *
      * 05       - F5 pressed                                         *
      * 09       - F9 pressed                                         *
      * 10       - Delete mode                                        *
      * 12       - F12 pressed                                        *
      * 25       - Rollup                                             *
      * 30       - Error on customer first screen                     *
      * 31       - Error on used to secur indicator                   *
      * 32       - Error on used to secure reference                  *
      * 33       - Error on secured by indicator                      *
      * 34       - Error on secured by reference                      *
      * 35       - Error on guarantee currency                        *
      * 36       - Error on guarantee percentage                      *
      * 37       - Error on guarantee amount                          *
      * 38       - Error on given by                                  *
      * 39       - Error on guarantee code                            *
      * 40       - Error on guarantee sequence                        *
      * 41       - Error on start date                                *
      * 42       - Error on end date                                  *
      * 43       - Error on review date                               *
      * 44       - Error on frequency                                 *
      * 45       - Error on day number                                *
      * 46       - Error on additional comment                        *
      * 47       - Error on Used to Secure Loans                      *
      * 51       - Initial screen                                     *
      * 52       - SFL screen                                         *
      * 53       - Details screen                                     *
      * 54       - Details screen on Window                           *
      * 59       - ERL0999 warning message displayed to confirm exit  *
      * 60       - At least 1 record to COMIT                         *
      * 61       - SFLDSP                                             *
      * 62       - Feature ULX012 *on, Conditonne field in DSPF       *   ULX012
      * 63       - SFLDSP on window                                   *
      * 64       - Load SFL Window                                    *
      * 70       - General error                                      *
      * 71       - Chain failed & work indicator                      *
      * 72       - Access on ERGUARL1                                 *
      * 73       - Readc on SFL                                       *
      * 74       - Chain on ERGUARL1 in A,D,E                         *
      * 75       - Chain on ERGUARL0 for cumul                        *
      * 76       - Condition Field 'Generated Automatically'          *   ULX012
      * 77       - Record locked by another user                      *
      * 78       - SFLNXTCHG                                          *
      * 79       - SFLEND                                             *
      * 80       - Protect mode in D and E                            *
      * 81       - Protect Used To Secure Ind.& Ref.                  *
      * 82       - Protect Secured By Ind. & Ref.                     *
      * 83       - Protect Guarantee Currency                         *
      * 84       - Protect Guarantee Percentage                       *
      * 85       - Protect Guarantee Amount                           *
      * 86       - Protect Given By                                   *
      * 87       - Protect Guarantee Sequence                         *
      * 88       - Protect Additional Comment                         *
      * 90-99    - Standard SR                                        *
      *****************************************************************
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZEDITZ1
      /COPY ZSRSRC,ZFRQAZ1
      *
      **  Guarantee Code Table
     E******              GUA    17  17  2                                ULX004
     E                    GUA    18  18  2                                ULX004
      **  Guarantee Code Table for specific provision
     E                    GCSP    1   1  2
      **  Guarantee Code Table for general ledger
     E                    GCGL    9   9  2
      **  Guarantee Code Table for retail account
     E                    GCRT    9   9  2
      **  Guarantee Code Table for MM deposit
     E                    GCMM    9   9  2
      **  Guarantee Code Table for customer position
     E******              GCCP   10  10  2                                ULX004
     E                    GCCP   11  11  2                                ULX004
      **  Guarantee Code Table for nominal
     E******              GCNO   10  10  2                                ULX004
     E                    GCNO   11  11  2                                ULX004
      **  Guarantee Code Table for amount
     E                    GCAM   13  13  2
      **  Guarantee Code Table for percentage
     E                    GCPR   13  13  2
     E                    CPY@    1   1 80
      /COPY ZSRSRC,ZHOLE
      *--------------------------------------------------------------------
     IMMDELDP0
     I              HKDDLT                          DDLT
     I              HKMTYP                          DTYP
     I              HKCNUM                          CNUM
     I              HKBRCA                          BRCA
     I              HKCCY                           CCY
     I************* HKAMNP                          PCPL
     IMMDENBP0
     I              HLDDLT                          DDLT
     I              HLMTYP                          DTYP
     I              HLCNUM                          CNUM
     I              HLBRCA                          BRCA
     I              HLCCY                           CCY
     I************  HLAMNP                          PUPR
     ITRADSDF                                                                                 UPG402
     I              CRSK                            TDCRSK                                    UPG402
      *
      ** Guarantees Transaction (Externally Defined) Data Structure
     IE1PARM    E DSERL910DA
      *
      **  Program data structure.
     IPGMDS     ESDSY2PGDSP
      *
      **  Program status
     IDSPFDS      DS
     I                                    B 378 3790CPFPOS
     I*
     IFDEAL       DS
     I** File status data structure - record format
     I                                     *RECORD  @FORM
      *
      **  Local Data Area for DATABASE ERROR Details
     ILDA       E DSLDA                         256
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                127865
     I@MMOD     E DSSDMMODPD                                              127865
      *
      **  Data Area of Menu User
     IZMUSER      DS                             17
     I                                       11  13 USRBCH
      *
      **  DS for compilation  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      **  Redefine amounts of DEALSALL record format MMDELDP0
     I            DS
     I                                    P   1   80HKAMNP
     I                                    P   2   80PCPL
      *
      **  Redefine amounts of DEALSALL record format MMDENBP0
     I            DS
     I                                    P   1   80HLAMNP
     I                                    P   2   80PUPR
      *
      **  DS Customer input on first screen
     I            DS
     I                                        1  10 DDCUST
     I                                        1   6 D1CUST
     I                                        7  10 D2CUST
      *
      **  DS Customer (GIVEN BY) input on third screen
     I            DS
     I                                        1  10 D3GVBY
     I                                        1   6 D3CUST
     I                                        7  10 D4CUST
      *
      **  DS for Used to Secure Reference on 3 Screen
     ID3UTSR      DS
     I                                        1   30SSFACT
     I                                        1   3 @@FACT                                    CER001
     I                                        4   50SSFCNO
     I                                        4   5 @@FCNO                                    CER001
      *
     I                                        1   60SSRREF
     I                                        1   6 SSLREF                                    CLE148
      *
     I**********                              1   60SSCNUM                                   CSD027A
     I                                        1   6 SSCNUM                                   CSD027A
     I                                        7   9 SSCCY
     I**********                             10  130SSACOD                                    CER001
     I**********                             14  150SSACSQ                                    CER001
     I**********                             16  18 SSBRCA                                    CER001
     I                                       10  190SSACOD                                    CER001
     I                                       20  210SSACSQ                                    CER001
     I                                       22  24 SSBRCA                                    CER001
      *
     I                                        1  100SSACNO
      *
     I                                        1   6 SSTDRF
      *
     I                                        1  10 SSBHSC
     I                                       11  13 SSBHBA
     I                                       14  15 SSBHBK
     I                                       16  16 SSBHMK
      *
     I                                        1  10 SSSESN
      *
      **  DS for Secure by Reference on 3 Screen
     ID3SBYR      DS
     I                                        1   60SBRREF
      *
     I**********                              1   60SBCUST                                   CSD027A
     I                                        1   6 SBCUST                                   CSD027A
     I**********                              7  100SBACOD                                    CER001
     I**********                             11  120SBACSQ                                    CER001
     I**********                             13  15 SBBRCA                                    CER001
     I                                        7  160SBACOD                                    CER001
     I                                       17  180SBACSQ                                    CER001
     I                                       19  21 SBBRCA                                    CER001
      *
     I                                        1  100SBACNO
      *
     I                                        1   3 SBCSBA
     I                                        4  13 SBCSSC
      *
     I                                        1  10 SBSESN
     IRRSBYR      DS                                                        ULX004
     I                                        1  10 RRSESN                  ULX004
      *
      **  External DS for Bank Details
     ISDBANK    E DSSDBANKPD
      *
      **  External DS for SE Trade ICD Details
     ISDSTRD    E DSSDSTRDPD
      *
      **  External DS for CCY Details
     ISDCURR    E DSSDCURRPD
      *
      /COPY ZSRSRC,ZAOZ1
      /COPY ZSRSRC,ZAOZ2
      /COPY ZSRSRC,ZHOLI
      /COPY ZSRSRC,ZHOLDS
      ********************************************************************
      /EJECT
      *===============================================================*
      *                                                               *
      *          P R O G R A M   E N T R Y                            *
      *                                                               *
      *===============================================================*
      *
      *****************************************************************
      *                                                               *
      * INDEX OF SUBROUTINES                                          *
      * MAIN     - MAIN PROCESSING                                    *
      * #INIT    - SR INITIAL ROUTINE                                 *
      * #MAIN    - SR MAIN PROCESSING                                 *
      * #TERM    - SR TERMINATION PGM                                 *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      * #REFR    - SR REFRESH SCREEN                                  *
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                        *
      * #ROLL    - SR ROLLUP                                          *
      * #S003    - SR ENTER ON SFL SCREEN                             *
      * #PRV2    - SR PREVIOUS SCREEN (FROM SCREEN 2)                 *
      * #PRV3    - SR PREVIOUS SCREEN (FROM SCREEN 3)                 *
      * #S004    - SR ENTER ON DETAILS SCREEN                         *
      * #CUMU    - SR CUMUL GUARANTEE AMOUNT                          *
      * #U003    - SR WRITE OR AMEND RECORD ON GUARANTEE FILE         *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      * #WLOAD   - SR LOAD SFL                                        *
      * #WSCR3   - SR SETUP FIELDS FOR SCREEN 3 DETAILS               *
      * #WPRO3   - SR PROTECT FIELDS FOR SCREEN 3 DETAILS             *
      * #ACTN    - SR ACTION CODE ON SFL                              *
      * #DELT    - SR DELETE MODE                                     *
      * #AMEQ    - SR SETUP FIELD IN AMEND ENQUIRY OR DELETE MODE     *
      * #V002    - SR AUTHORISATION ACTION CODE FOR USER              *
      * #WNSFL   - SR LOAD SFL ON WINDOW                              *
      * #S002    - SR ENTER ON WINDOW                                 *
      * #PRV4    - SR F12 ON WINDOW                                   *
      * *PSSR    - SR DATA BASE ERROR                                 *
      * ZASNMS   - SR SEND ERROR MESSAGE                              *
      *                                                               *
      * EXTERNAL ROUTINES CALLED                                      *
      * AOBANKR0 - PGM ACCESS BANK DETAILS                            *
      * Y2CLMSC  - PGM CLEAR MSGQ                                     *
      * AOCURRR0 - PGM ACCESS CURRENCY DETAILS                        *
      * ZVACTU   - PGM CHECK USER ACTION CODE FOR MONOBRANCHING       *
      * ZVACTBU  - PGM CHECK USER ACTION CODE FOR MULTIBRANCHING      *
      * Y2SNMGC  - PGM SEND ERROR MESSAGE                             *
      *                                                               *
      *****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           E1PARM
      *
      **  KLIST to access on ERGUARL0
      *
     C           $ERGU0    KLIST
     C                     KFLD           WWSBYI  1
     C**********           KFLD           WWSBYR 15                                           CER001
     C                     KFLD           WWSBYR 21                                           CER001
      *
      **  KLIST to access on ERGUARL1
      *
     C           $ERGU1    KLIST
     C                     KFLD           KKRCUS           * Receiving C/m
     C                     KFLD           KKUTSI           * U/T/S Ind.
     C                     KFLD           KKUTSR           * U/T/S Refer.
     C                     KFLD           KKGSEQ           * Guarantee Seq
      *
      **  KLIST to access on ERGUARL2
      *
     C           $ERGU2    KLIST
     C                     KFLD           KKBRCA           * Branch Code
     C                     KFLD           KKRCUS           * Receiving C/m
     C                     KFLD           KKUTSI           * U/T/S Ind.
     C                     KFLD           KKUTSR           * U/T/S Refer.
     C                     KFLD           KKGSEQ           * Guarantee Seq
      *
      **  KLIST to access on FCLTY
      *
     C           $FCLTY    KLIST
     C                     KFLD           KKCNUM           * Customer Num.
     C                     KFLD           KKFACT           * Facility Type
     C                     KFLD           KKFCNO           * Facility No.
     C                     KFLD           KKRCTP           * Record Type
      *
      **  KLIST to access on ACCNT
      *
     C           $ACCNT    KLIST
     C                     KFLD           KKCNUM           * Customer Num.
     C                     KFLD           KKCCY            * Currency
     C                     KFLD           KKACOD           * Account Code
     C                     KFLD           KKACSQ           * Account Seq.
     C                     KFLD           KKBRCA           * Branch Code
      *
      **  KLIST to access on BKPHD
      *
     C           $BKPHD    KLIST
     C                     KFLD           KKBHSC           * Security
     C                     KFLD           KKBHBA           * Branch Code
     C                     KFLD           KKBHBK           * Book Code
     C                     KFLD           KKBHMK           * Market
     C                     KFLD           KKBHTV           * T/V Date Bas.
      *
      **  KLIST to access on CPOSN
      *
     C           $CPOSN    KLIST
     C                     KFLD           KKCSBA           * Branch Code
     C                     KFLD           KKCSSC           * Security
     C                     KFLD           KKCSCN           * Customer Num.
      *
     C           *LIKE     DEFN RRRCUS    KKRCUS           * Receiving C/m
     C           *LIKE     DEFN RRUTSI    KKUTSI           * U/T/S Ind.
     C           *LIKE     DEFN RRUTSR    KKUTSR           * U/T/S Refer.
     C           *LIKE     DEFN RRGSEQ    KKGSEQ           * Guarantee Seq
     C           *LIKE     DEFN CNUM      KKCNUM           * Customer Num.
     C           *LIKE     DEFN FACT      KKFACT           * Facility Type
     C           *LIKE     DEFN FCNO      KKFCNO           * Facility No.
     C           *LIKE     DEFN RCTP      KKRCTP           * Record Type
     C           *LIKE     DEFN CCY       KKCCY            * Currency
     C           *LIKE     DEFN ACOD      KKACOD           * Account Code
     C           *LIKE     DEFN ACSQ      KKACSQ           * Account Seq.
     C           *LIKE     DEFN BRCA      KKBRCA           * Branch Code
     C           *LIKE     DEFN BHSC      KKBHSC           * Security
     C           *LIKE     DEFN BHBA      KKBHBA           * Branch Code
     C           *LIKE     DEFN BHBK      KKBHBK           * Book Code
     C           *LIKE     DEFN BHMK      KKBHMK           * Market
     C           *LIKE     DEFN BHTV      KKBHTV           * T/V Date Bas.
     C           *LIKE     DEFN CSBA      KKCSBA           * Branch Code
     C           *LIKE     DEFN CSSC      KKCSSC           * Security
     C           *LIKE     DEFN CSCN      KKCSCN           * Customer Num.
      *
     C                     MOVE *BLANKS   @RTCD   7        * AO Parameter
     C                     MOVE *BLANKS   @OPTN   7        * AO Paramrter
     C                     MOVE *BLANKS   @DSNB   3        * AO Parameter
     C                     MOVE *BLANKS   @KEY1   3        * AO Parameter
     C**********           Z-ADD0         @@CNUM  60       * Customer No.                    CSD027A
     C                     MOVE *BLANKS   @@CNUM  6        * Customer No.                    CSD027A
     C           *LIKE     DEFN E1UTSC    @@UTSC           * U/T/S Ccy
     C                     Z-ADD*ZEROS    @@UTSA 130       * U/T/S Amount
     C                     Z-ADD*ZEROS    @@AMNT 140       * Guarantee Amt
     C                     Z-ADD*ZEROS    @@GAMT 150       * Guarantee Amt
     C           *LIKE     DEFN E1GCCY    @@GCCY           * Guarantee Ccy
     C           *LIKE     DEFN E1GCCY    @@DFCY           * Guarantee Ccy
     C           *LIKE     DEFN E1GCCY    @@CURR           * Guarantee Ccy
     C           *LIKE     DEFN RRGSEQ    @@GSEQ           * Guarantee Seq
     C           *LIKE     DEFN RRGVBY    @@GVBY           * Given By
     C           *LIKE     DEFN RRSTDT    @@STDT           * Start Date
     C           *LIKE     DEFN RRENDT    @@ENDT           * End Date
     C           *LIKE     DEFN RRRVDT    @@RVDT           * Review Date
     C           *LIKE     DEFN E1BRCA    @@BRCA           * Branch Code
     C                     Z-ADD*ZEROS    SVRRN   40       * Save SFL RRN
     C                     Z-ADD*ZEROS    WWRRN            * Work SFL RRN
     C           *LIKE     DEFN BBCUST    WWCUST           * Work field
     C           *LIKE     DEFN NMDP      WWNMDP           * Dec. Places  128241
      *
      * WWSBYI   - WORK FIELD ACCESS ON GUARANTEE FILE FOR CUMUL         *
      * WWSBYR   - WORK FIELD ACCESS ON GUARANTEE FILE FOR CUMUL         *
      * WWWPER   - WORK FIELD VALIDATE PERCENTAGE                        *
      * WWVALD   - WORK FIELD VALIDATE NEXT DATE                         *
      * WWVLDM   - WORK FIELD NEXT DATE                                  *
      * WWVVDM   - WORK FIELD NEXT DATE                                  *
      * WWDAT1   - WORK FIELD VALIDATE NEXT DATE                         *
      * WWDAT2   - WORK FIELD VALIDATE NEXT DATE                         *
      * WWDAY    - WORK FIELD VALIDATE DAY NUMBER                        *
      * WWLINE   - WORK FIELD LINE IN SFL                                *
      * WWGVBY   - WORK FIELD ACCESS ON CUSTOMER DETAILS                 *
      * WWRANG   - WORK FIELD SAVE RRN                                   *
      * WWWORK   - WORK FIELD CUMUL AMOUNT                               *
      * WWAMT1   - WORK FIELD CUMULATE AMOUNT                            *
      * WWSBYI   - WORK FIELD ACCESS ON GUARANTEE FILE FOR CUMUL         *
      * WWSBYR   - WORK FIELD ACCESS ON GUARANTEE FILE FOR CUMUL         *
      * WWCUST   - WORK FIELD CUSTOMER                                   *
      *
      *===============================================================*
      *
      ** Perform Initialisation routine
      *
     C                     EXSR #INIT
      *
     C                     EXSR #MAIN
      *
     C                     EXSR #TERM
      *
     C                     RETRN
      *
      /EJECT
      *===============================================================*
      *                                                               *
      *          #INIT  - INITIALISATION PROCESSING                   *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * AOBANKR0 - PGM ACCESS BANK DETAILS                            *
      * *PSSR    - SR DATA BASE ERROR                                 *
      * Y2CLMSC  - PGM CLEAR MSGQ                                     *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * MAIN     - MAIN PROCESSING                                    *
      *                                                               *
      *****************************************************************
     C           #INIT     BEGSR
      *
      **  Open Files under Commitment Control (except when called
      **    from Window Enquiries)
     C*                                                                   127865
     C** CALL ACCESS PROGRAM FOR MIDAS MODULES DETAILS                    127865
     C                     CALL 'AOMMODR0'                                127865
     C                     PARM '*MSG    '@RTCD   7                       127865
     C                     PARM '*FIRST  '@OPTN   7                       127865
     C           @MMOD     PARM @MMOD     DSSDY                           127865
     C*                                                                   127865
     C           @RTCD     IFNE *BLANK                                    127865
     C                     MOVEL'SDMMODPD'DBASE             ************* 127865
     C                     MOVEL'99'      DBFILE            *DBERROR 099* 127865
     C                     MOVEL@OPTN     DBKEY             ************* 127865
     C                     OUT  LDA                                       127865
     C                     EXSR *PSSR                                     127865
     C                     END                                            127865
      *
     C***********E1ACTN    IFNE 'E'                        *----------1   142421
     C           BGSECS    IFEQ 'Y'                                       127865
     C                     OPEN SECTY
     C                     ENDIF                                          127865
     C           BGCSLN    IFEQ 'Y'                                       127865
     C                     OPEN FCLTY
     C                     ENDIF                                          127865
     C           E1UTSI    IFNE 'F'                        *---------2
     C           BGCSLN    ANDEQ'Y'                                       127865
     C                     OPEN CLOAN
     C                     ENDIF                           *---------2
     C*********************ENDIF                           *----------1   142421
      *
      **  Get Installation Control Data Record 1
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDBANK    PARM SDBANK    DSFDY
     C           @RTCD     IFNE *BLANKS                    *----------1
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVEL'SDBANKPD'DBFILE           * DB ERROR 01 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF                           *----------1
      *
      ** Set indicator *IN98 to indicate which date format to use
      *
     C           BJDFIN    IFEQ 'M'                        *----------1
     C                     MOVE *ON       *IN98
     C                     ENDIF                           *----------1
      *
      **  Get Securities Trading Installation Control Data Record 1
      *
     C           BGSECS    IFEQ 'Y'                                       127865
     C                     CALL 'AOSTRDR0'
     C                     PARM '*MSG   ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDSTRD    PARM SDSTRD    DSFDY
     C           @RTCD     IFNE *BLANKS                    *----------1
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDSTRDPD'DBFILE           * DB ERROR 02 *
     C                     MOVEL'*FIRST'  DBKEY            ***************
     C                     EXSR *PSSR
     C                     OUT  LDA
     C                     ENDIF                           *----------1
     C                     ENDIF                                          127865
      *                                                                   ULX012
      **  Determine if feature ULX012 is installed                        ULX012
     C                     MOVE 'N'       ULX012  1                       ULX012
     C                     CALL 'AOSARDR0'                                ULX012
     C                     PARM '       ' @RTCD   7                       ULX012
     C                     PARM '*VERIFY' @OPTN   7                       ULX012
     C                     PARM 'ULX012'  @SARD   6                       ULX012
      *                                                                   ULX012
      **  If feature ULX012 is installed.                                 ULX012
     C           @RTCD     IFEQ *BLANKS                                   ULX012
     C                     MOVE 'Y'       ULX012                          ULX012
     C                     MOVE *ON       *IN62                           ULX012
     C                     ENDIF                                          ULX012
      *                                                                   ULX012
      **  Get LMI Installation Control Data                               ULX012
     C********** BJCNCD    CHAINERLUICL0             99                                ULX012 CER001
     C           'LU'      CHAINERLUICL0             99                                       CER001
     C           *IN99     IFEQ *ON                                       ULX012
     C           *LOCK     IN   LDA                                       ULX012
     C                     Z-ADD3         DBASE            ***************ULX012
     C                     MOVEL'ERLUICPD'DBFILE           * DB ERROR 03 *ULX012
     C**********           MOVELBJCNCD    DBKEY            ***************             ULX012 CER001
     C                     MOVEL'LU'      DBKEY                                               CER001
     C                     EXSR *PSSR                                     ULX012
     C                     OUT  LDA                                       ULX012
     C                     ENDIF                                          ULX012
      *
      **  Header Screen Details
      *
     C                     TIME           DDTIME
     C                     MOVEL##JOB     DDSWID
     C                     MOVEL##USR     DDUSER
     C                     MOVEL##PGM     DDPPGM
     C                     MOVELBJMRDT    DDDATE
     C                     MOVEL*BLANKS   DDCUST
      *
      **  Clear SFL
      *
     C                     MOVE *ON       *IN61
     C                     WRITEERL910C2
     C                     MOVE *OFF      *IN61
      *
      **  Select the program MSGQ for error msg.
      *
     C                     MOVEL'*'       TOPQ
     C                     MOVEL'*PRV'    TOPRQ
     C                     MOVEL'*'       DDPGMQ
     C**********           MOVEL'ERUSRMSG'PMSGF  10                                           CER001
     C                     MOVEL'STUSRMSG'PMSGF  10                                           CER001
      *
      **  Fill in fields for subroutine ZASNMS
      *
     C                     MOVEL'ERL910'  ZAPGM  10        Message queue
     C**********           MOVEL'ERUSRMSG'ZAMSGF 10        Message file                       CER001
     C                     MOVEL'STUSRMSG'ZAMSGF 10        Message file                       CER001
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.
     C                     MOVEL*BLANK    ZAMSDA132        Message data.
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.
      *
      **  Initialize program message queue
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM           TOPQ   10
     C                     PARM           TOPRQ   5
      *
      **  Retrieve Default User Branch
      *
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
      *
     C           *NAMVAR   DEFN           LDA
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #MAIN  - MAIN PROCESSING                             *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * Y2CLMSC  - PGM CLEAR MSGQ                                     *
      * #TERM    - SR TERMINATION PGM                                 *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      * #REFR    - SR REFRESH SCREEN                                  *
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                        *
      * #ROLL    - SR ROLLUP                                          *
      * #S003    - SR ENTER ON SFL SCREEN                             *
      * #DELT    - SR DELETE MODE                                     *
      * #PRV3    - SR PREVIOUS SCREEN (FROM SCREEN 3)                 *
      * #S004    - SR ENTER ON DETAILS SCREEN                         *
      * #S002    - SR ENTER ON WINDOW                                 *
      * #PRV4    - SR F12 ON WINDOW                                   *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * MAIN     - MAIN PROCESSING                                    *
      *                                                               *
      *****************************************************************
     C           #MAIN     BEGSR
      *
      **  Process/Setup Screen 1 dependent upon parameter input
      *
     C                     MOVE E1RCUS    D1CUST           * Receiving C/m
     C           DDCUST    IFNE *BLANKS                    *----------1
     C                     EXSR #S001
     C                     ELSE                            X----------1
     C                     MOVE *ON       *IN51
     C                     ENDIF                           *----------1
      *
      **  Display each Screen dependent upon indicator
      *
     C           *INLR     DOWEQ*OFF                       *----------1
     C                     TIME           DDTIME
     C                     WRITEERL910C0
     C   51                EXFMTERL910C1                   1 Screen
     C   52                WRITEERL910C4                   Bottom Screen
     C   52                EXFMTERL910C2                   2 Screen SFL
     C   53                EXFMTERL910C3                   3 Screen Details
     C   54                WRITEERL910H                    Window header
     C   54                WRITEERL910L                    Window footer
     C   54                EXFMTERL910C                    Window
      *
      **  Initialize program message queue
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM           TOPQ   10
     C                     PARM           TOPRQ   5
      *
      **  Send warning message if exit to window program requested but
      **    no updates performed (first check if message already shown)
      *
     C           *IN03     IFEQ *ON                        *----------1
     C           *IN59     ANDEQ*ON
     C           E1ACTN    IFEQ 'I'                        *---------2
     C           E1ACTN    OREQ 'A'
     C           E1ACTN    OREQ 'D'
     C                     MOVE *ON       *IN60
     C                     ELSE                            X---------2
     C                     MOVE *OFF      *IN59
     C                     ENDIF                           *---------2
     C                     ELSE                            X----------1
     C                     MOVE *OFF      *IN59
     C                     ENDIF                           *----------1
      *
     C           *IN03     IFEQ *ON                        *----------1
     C           *IN60     ANDEQ*OFF
     C           E1ACTN    IFEQ 'I'                        *---------2
     C           E1ACTN    OREQ 'A'
     C           E1ACTN    OREQ 'D'
     C                     MOVEL*BLANKS   ZAMSDA
     C                     MOVEL'ERL0999' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN59
     C                     ELSE                            X---------2
     C                     MOVE *ON       *IN60
     C                     ENDIF                           *---------2
     C                     ENDIF                           *----------1
      *
     C   51 03 60          CAS            #TERM            S-F3 on 1 Screen
     C   51                CAS            #S001            S-Enter on 1 Screen
     C   52 03 60          CAS            #TERM            S-F3 on 2 Screen
     C   52 05             CAS            #REFR            S-F5 on 2 Screen
     C   52 09             CAS            #ADDM            S-F9 on 2 Screen
     C   52 25             CAS            #ROLL            S-Rollup
     C   52 12             CAS            #PRV2            S-F12 on 2 Screen
     C   52                CAS            #S003            S-Enter on 2 Screen
     C   53 03 60          CAS            #TERM            S-F3 on 3 Screen
     C   53 10             CAS            #DELT            S-F10 on 3 Screen
     C   53 12             CAS            #PRV3            S-F12 on 3 Screen
     C   53                CAS            #S004            S-Enter on 3 Screen
     C   54 12             CAS            #PRV4            S-F12 on Window
     C   54                CAS            #S002            S-Enter on Window
     C                     ENDCS                           *---------2
     C                     ENDDO                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #TERM  - TERMINATION PROCESSING                      *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * MAIN     - MAIN PROCESSING                                    *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #TERM     BEGSR
      *
      **   End of pgm
      *
     C           E1ACTN    IFNE 'E'                        *----------1
     C                     CLOSESECTY
     C                     CLOSEFCLTY
     C           E1UTSI    IFNE 'F'                        *---------2
     C                     CLOSECLOAN
     C                     ENDIF                           *---------2
     C                     ENDIF                           *----------1
     C**********           ROLBK
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #S001  - SCREEN 1 PROCESSING (Customer No. Input)    *
      *                                                               *
      *===============================================================*
      *                                                               *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      * ZASNMS   - SR SEND ERROR MESSAGE                              *
      * #V002    - SR AUTHORISATION ACTION CODE FOR USER              *
      * #WSCR3   - SR SETUP FIELDS FOR SCREEN DETAILS                 *
      * #WLOAD   - SR LOAD SFL                                        *
      * #WNSFL   - SR LOAD SFL ON WINDOW                              *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #S001     BEGSR
      *
      **  Reset Work Indicators
      *
     C                     EXSR #WSETP
      *---------------------------------------------------------------*
      **  If a RECEIVING CUSTOMER Number has been entered, then validate
      *
     C           DDCUST    IFNE *BLANKS                    *----------1
      *
      **  No selection required
      *
     C           DDCUST    IFNE '?'                        *---------2
      *
      **  Access with Customer SHORT NAME
      *
     C           DDCUST    CHAINSDCUSTL2             71
     C           *IN71     IFEQ *ON                        *--------3
      *
      **  If not found, access with CUST NUMBER
      *
     C           D1CUST    CHAINSDCUSTL5             71
     C           *IN71     IFEQ *ON                        *-------4
     C           D2CUST    ORNE *BLANKS
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN51
     C                     MOVEL*BLANKS   ZAMSDA
     C                     MOVEL'ERN0901' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
      *
      **  If found & program called from a Window, ensure Customer No.
      **    selection is same as parameter
      *
     C                     MOVE BBCUST    @@CNUM           * Customer No.
     C           *IN71     IFEQ *OFF                       *--------3
     C           E1ACTN    ANDNE*BLANKS
     C           E1RCUS    ANDNE@@CNUM
     C           E1SBYI    ANDNE'D'
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN51
     C                     MOVEL'ERL0951' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *--------3
      *
      **  Selection required of Customer on window SFL
      *
     C                     ELSE                            X---------2
     C                     MOVE *ON       *IN30
     C                     EXSR #WNSFL
     C                     ENDIF                           *---------2
      *
     C                     ELSE                            X----------1
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN51
     C                     MOVEL'ERN0903' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *----------1
      *---------------------------------------------------------------*
      **  If Customer number or short name are found and live,
      **  check if it is not a parent
      *
     C           *IN30     IFEQ *OFF                       *----------1
     C           BBPAIN    IFEQ 'P'                        *---------2
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN51
     C                     MOVEL'ERN0902' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      *---------------------------------------------------------------*
     C                     ELSE                            X---------2
      *
      ** Customer number or short name valid & not parent,
      **   Check if any Guarantee records exist
      *
     C                     MOVELBBCSSN    SVCSSN 10 P
     C                     MOVELBBCRNM    SVCRNM 20 P
     C                     MOVELBBCRTN    SVCRTN 10 P
     C                     MOVE BBCUST    @@CNUM           * Customer No.
     C                     MOVE BBBRCD    @@BRCA           * Branch Code
     C                     MOVE BBBRCD    WWBRCA  3        * Branch Code  ULX012
     C           @@CNUM    SETLLERGUARL1                 72
      *
      ** No records found; set Action Code for INSERT
      *
     C           *IN72     IFEQ *OFF                       *--------3
     C           E1ACTN    IFNE 'E'                        *-------4
     C                     MOVEL'I'       @ZACTN
     C                     ELSE                            X-------4
     C                     MOVEL'ERN0013' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN51
     C                     ENDIF                           *-------4
      *
      ** Records found; set Action Code as per program parameter
      **   (default is ENQUIRE)
      *
     C                     ELSE                            X--------3
     C           E1ACTN    IFNE *BLANKS                    *-------4
     C                     MOVELE1ACTN    @ZACTN
     C                     ELSE                            X-------4
     C                     MOVEL'E'       @ZACTN
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
      *---------------------------------------------------------------*
      **  Check if User is authorised to ACTION CODE
      *
     C           *IN30     IFEQ *OFF                       *--------3
     C                     EXSR #V002
      *
     C           @ERR      IFNE *ZEROS                     *-------4
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN51
      *
     C                     ELSE                            X-------4
      *
      **  User is authorised; Clear SFL and Load data (unless Customer No.
      **    was not found on ERGUARL1)
      *
     C                     MOVELDDCUST    SVCUST 10
     C                     Z-ADD*ZEROS    WWRRN
     C                     Z-ADD*ZEROS    SVRRN
     C                     MOVE *ON       *IN61
     C                     WRITEERL910C2
     C                     MOVE *OFF      *IN61
     C           *IN72     IFEQ *ON                        *------5
     C                     EXSR #WLOAD
     C                     ENDIF                           *------5
      *
      **  If no records found, display Screen 3, else display Screen 2 SFL
      *
     C           WWRRN     IFEQ *ZEROS                     *------5
     C           @ZACTN    OREQ 'I'
     C                     MOVE *ON       *IN53
     C                     MOVE *ON       *IN01
     C                     EXSR #WSCR3
     C                     EXSR #WPRO3
     C                     ELSE                            X------5
     C                     MOVE *ON       *IN52
     C                     ENDIF                           *------5
      *
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
     C                     ENDIF                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #WNSFL - SCREEN 1 SUB-PROCESSING (Customer No.Window)*
      *                                                               *
      *===============================================================*
      *                                                               *
      * #WNSFL   - SR LOAD SFL ON WINDOW                              *
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      *                                                               *
      *****************************************************************
     C           #WNSFL    BEGSR
      *
     C                     Z-ADD*ZEROS    W001RG
     C                     MOVE *ON       *IN68
     C                     WRITEERL910C
     C                     MOVE *OFF      *IN68
     C**********           Z-ADD*ZEROS    SVRCUS  60                                         CSD027A
     C                     MOVE *BLANKS   SVRCUS  6                                          CSD027A
      *
     C           *LOVAL    SETLLERGUARL1
     C                     READ ERGUARL1                 64
      *
      ***  Not End of File
      *
     C           *IN64     DOWEQ*OFF                       *----------1
      *
      ***  Load each different customer
      *
     C           RRRCUS    IFNE SVRCUS                     *---------2
     C                     MOVE RRRCUS    XXCUST  6
      *
      ***  Check customer number
      *
     C           XXCUST    CHAINSDCUSTL5             71
     C           *IN71     IFEQ *OFF                       *--------3
     C                     MOVELBBCUST    WNCUST
     C                     MOVELBBCSSN    WNCSSN    P
      *
     C                     ELSE                            X--------3
     C                     MOVELXXCUST    WNCUST
     C                     MOVEL'ERROR'   WNCSSN    P
     C                     ENDIF                           *--------3
      *
     C                     MOVEL*BLANKS   WNSELE
     C                     ADD  1         W001RG
     C                     WRITEERL910E
     C**********           Z-ADDRRRCUS    SVRCUS                                             CSD027A
     C                     MOVE RRRCUS    SVRCUS                                             CSD027A
     C                     ENDIF                           *---------2
      *
     C                     READ ERGUARL1                 64
     C                     ENDDO                           *----------1
      *
     C           W001RG    IFEQ *ZEROS                     *----------1
     C                     MOVE *ON       *IN63
     C                     ELSE                            X----------1
     C                     Z-ADD1         W001RG
     C                     ENDIF                           *----------1
      *
     C                     MOVE *ON       *IN54            * Screen 1 Win
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #PRV4  - SCREEN 1 PROCESSING (Cmd Key 12 on Window) )*
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #PRV4     BEGSR
      *
     C                     EXSR #WSETP
     C                     MOVEL*BLANKS   DDCUST
     C                     MOVE *ON       *IN51
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #S002  - SCREEN 2 PROCESSING (Sub-File of Customers) *
      *                                                               *
      *===============================================================*
      *                                                               *
      * #S002    - SR ENTER ON WINDOW                                 *
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #S002     BEGSR
      *
     C                     EXSR #WSETP
      *
     C           W001RG    IFNE *ZEROS
     C                     READCERL910E                  71
      *
     C           *IN71     IFEQ '0'
     C                     MOVELWNCUST    DDCUST
     C                     MOVE *ON       *IN51
     C                     ELSE
     C                     MOVE *ON       *IN54            * Screen 1 Win
     C                     ENDIF
      *
     C                     ELSE
     C                     MOVEL*BLANKS   DDCUST
     C                     MOVE *ON       *IN51
     C                     ENDIF
      *
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #S003  - SCREEN 2 PROCESSING (Sub-File of Guarantees)*
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      * ZASNMS   - SR SEND ERROR MESSAGE                              *
      * #V002    - SR AUTHORISATION ACTION CODE FOR USER              *
      * #ACTN    - SR ACTION CODE ON SFL                              *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #S003     BEGSR
      *
      **  Reset work indicators
      *
     C                     EXSR #WSETP
     C                     MOVE *OFF      *IN01
      *
     C                     READCERL910S2                 73
      *
      **  Selection on SFL --->
      **  If program called from a Window, ensure Transaction
      **    selection is same as parameter
      *
     C           *IN73     IFEQ *OFF                       *----------1
     C           E1ACTN    ANDNE*BLANKS
     C           SFACTN    ANDEQ'A'
     C           *IN73     OREQ *OFF
     C           E1ACTN    ANDNE*BLANKS
     C           SFACTN    ANDEQ'D'
      *
     C           E1UTSI    IFEQ 'F'                        *---------2
     C           E1UTSR    ANDNESFUTSR
     C           E1UTSI    OREQ 'L'
     C           E1UTSR    ANDNESFUTSR
     C           E1UTSI    OREQ 'S'
     C           E1UTSR    ANDNESFUTSR
     C           E1SBYI    OREQ 'D'
     C           E1SBYR    ANDNESFSBYR
     C                     MOVE *ON       *IN73
     C                     MOVEL'ERL0949' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *---------2
     C                     ENDIF                           *----------1
      *
      **  Validate input on SFL screen
      *
     C                     SELEC
      *
      **  Selection on SFL and same customer ---> check if user is authorised to
      **  action code
      *
     C           *IN73     WHEQ *OFF                       S----------1
     C           DDCUST    ANDEQSVCUST
      *
     C                     MOVELSFACTN    @ZACTN
     C                     EXSR #V002
      *
     C           @ERR      IFNE *ZEROS                     *---------2
     C                     MOVE *ON       *IN52
     C                     ELSE                            X---------2
     C                     EXSR #ACTN
     C                     ENDIF                           *---------2
      *
      **  Enter pressed without input
      *
     C                     OTHER                           S----------1
     C                     Z-ADDCPFPOS    WWRRN
     C                     MOVE *ON       *IN52
     C                     ENDSL                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #REFR  - SCREEN 2 F5 REFRESH (Sub-File of Guarantees)*
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      * #WLOAD   - SR LOAD SFL                                        *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #REFR     BEGSR
      *
      **  Reset work indicators
      *
     C                     EXSR #WSETP
      *
     C                     MOVELSVCUST    DDCUST
      *
      **  Clear SFL
      *
     C                     Z-ADD*ZEROS    WWRRN
     C                     Z-ADD*ZEROS    SVRRN
     C                     MOVE *ON       *IN61
     C                     WRITEERL910C2
     C                     MOVE *OFF      *IN61
      *
     C           @@CNUM    SETLLERGUARL1                 72
      *
     C                     EXSR #WLOAD
     C                     MOVE *ON       *IN52
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #ADDM  - SCREEN 2 F9 INSERT (Sub-File of Guarantees) *
      *                                                               *
      *===============================================================*
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                        *
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      * #V002    - SR AUTHORISATION ACTION CODE FOR USER              *
      * ZASNMS   - SR SEND ERROR MESSAGE                              *
      * #WSCR3   - SR SETUP FIELDS FOR SCREEN DETAILS                 *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #ADDM     BEGSR
      *
      **  Reset work indicators
      *
     C                     MOVE *OFF      *IN65
     C                     MOVE *OFF      *IN67
     C                     EXSR #WSETP
      *
      **  Check if user is authorized to action code
      *
     C                     MOVEL'I'       @ZACTN
     C                     EXSR #V002
      *
     C           @ERR      IFNE *ZEROS                     B1
     C                     MOVE *ON       *IN52
     C                     ELSE                            X1
     C                     Z-ADDCPFPOS    WWRANG  40
     C                     MOVE *ON       *IN53
     C                     EXSR #WSCR3
     C                     EXSR #WPRO3
      *
     C                     ENDIF                           E1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #ROLL  - SCREEN 2 ROLL KEY (Sub-File of Guarantees)  *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      * #WLOAD   - SR LOAD SFL                                        *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #ROLL     BEGSR
      *
      **  Reset work indicators
      *
     C                     EXSR #WSETP
      *
      **  Load SFL and redisplay screen SFL
      *
     C                     EXSR #WLOAD
     C                     MOVE *ON       *IN52
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #PRV2  - SCREEN 2 PROCESSING (Cmd Key 12)            *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      * ROUTINE USES VARIABLES:                                       *
      *                                                               *
      *****************************************************************
     C           #PRV2     BEGSR
      *
      **  Reset work indicators
      *
     C                     EXSR #WSETP
      *
      **  Display screen 1
      *
     C                     MOVEL*BLANKS   DDCUST
     C                     MOVE *ON       *IN51
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #S004  - SCREEN 3 PROCESSING (Details of Guarantees) *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      * ZASNMS   - SR SEND ERROR MESSAGE                              *
      * ZALIGN   -                                                    *
      * AOCURRR0 - PGM ACCESS CURRENCY DETAILS                        *
      * #CUMU    - SR CUMUL GUARANTEE AMOUNT                          *
      * ZDATE1   -                                                    *
      * ZFRQA    -                                                    *
      * #U003    - SR WRITE OR AMEND RECORD ON GUARANTEE FILE         *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #S004     BEGSR
      *
      **  Reset work indicators
      *
     C                     EXSR #WSETP
      *
     C           E1ACTN    IFNE 'I'                        *----------1
     C                     Z-ADD*ZEROS    @@UTSA           * U/T/S Amount
     C                     ENDIF                           *----------1
     C                     Z-ADD0         @@GAMT           * Guarantee Amt
     C                     Z-ADD*ZEROS    WWAMT1 132       Princ.Amount
     C                     Z-ADD*ZEROS    WWWORK 150       Work field
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 87       - Protect Guarantee Sequence
      *
     C           SFACTN    IFEQ 'A'                        *----------1
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN87
     C                     ENDIF                           *----------1
      *
     C           SFACTN    IFEQ 'I'                        *----------1
     C           SFACTN    OREQ 'A'                        * Insert/Amend
     C           SFRVEX    ANDNE'E'                        * Not Expired
     C           *IN09     OREQ *ON
     C           *IN01     OREQ *ON
      *---------------------------------------------------------------*
      **  USED TO SECURE IND. must be 'F' - Facility
      **                              'L' - Loan in Lending
      **                              'D' - Loan in Dealing
      **                              'G' - General Ledger Account
      **                              'R' - Retail Account
      **                              'T' - SE Trade
      **                              'P' - SE Book Position
      **                              'S' - Security
      **                              'N' - Negotiable Asset Purchased
      **                              'X' - Global Exposure               ULX012
      *
     C           D3UTSI    IFNE 'F'                        *---------2
     C           D3UTSI    ANDNE'L'
     C           D3UTSI    ANDNE'D'
     C           D3UTSI    ANDNE'G'
     C           D3UTSI    ANDNE'R'
     C           D3UTSI    ANDNE'T'
     C           D3UTSI    ANDNE'P'
     C           D3UTSI    ANDNE'S'
     C           D3UTSI    ANDNE'N'
      *                                                                   ULX012
     C           ULX012    IFEQ 'N'                                       ULX012
     C           ULX012    OREQ 'Y'                                       ULX012
     C           D3UTSI    ANDNE'X'                                       ULX012
      *                                                                   ULX012
     C                     MOVE *ON       *IN31
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN0906' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                                          ULX012
     C                     ENDIF                           *---------2
      *
      *---------------------------------------------------------------*
      ** Find USED TO SECURE REFERENCE related transaction dependent
      **   upon Used To Secure Indicator
      *
      **  Validate 'F' as Facility
      *
     C           D3UTSI    IFEQ 'F'                        *---------2
     C           E1ACTN    ANDNE'I'
     C                     MOVE @@CNUM    KKCNUM           * Customer Num.
     C           @@FACT    IFEQ *BLANKS                                                       CER001
     C                     Z-ADD*ZEROS    KKFACT                                              CER001
     C                     ELSE                                                               CER001
     C                     MOVE SSFACT    KKFACT           * Facility Type
     C                     ENDIF                                                              CER001
     C           @@FCNO    IFEQ *BLANKS                                                       CER001
     C                     Z-ADD*ZEROS    KKFCNO                                              CER001
     C                     ELSE                                                               CER001
     C                     MOVE SSFCNO    KKFCNO           * Facility No.
     C                     ENDIF                                                              CER001
     C                     MOVE 'A'       KKRCTP           * Record Type
      *
      **  Access to Facility file with Used to Secure Reference
      *
     C           $FCLTY    CHAINFCLTY                71
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN0907' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Facility transaction
      *
     C                     ELSE                            X--------3
     C                     MOVELBRCA      @@BRCA           * Branch Code
     C                     MOVELFCCY      @@UTSC           * U/T/S Ccy
     C                     Z-ADDFAMT      @@UTSA           * U/T/S Amount
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate 'L' as Loan in Lending
      *
     C           D3UTSI    IFEQ 'L'                        *---------2
     C           E1ACTN    ANDNE'I'
      *
      **  Access to loan file with Used to Secure Reference
      *
     C********** SSRREF    CHAINCLOAN                71                                       CLE148
     C           SSLREF    CHAINCLOAN                71                                       CLE148
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN0909' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Check valid loan type
      *
     C           PTYP      IFNE 61                         *-------4
     C           PTYP      ANDNE62
     C           PTYP      ANDNE63
     C           PTYP      ANDNE64
     C           PTYP      ANDNE65
     C           PTYP      ANDNE70                                        128098
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9100' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      **  Receiving customer must be the same as the customer of loan
      *
     C***********@@CNUM    IFNE CNUM                       *------5       CAD015
     C           PTYP      IFEQ 61                         *------5       CAD015
     C           @@CNUM    ANDNECNUM                                      CAD015
     C           PTYP      OREQ 62                                        CAD015
     C           @@CNUM    ANDNECNUM                                      CAD015
     C           PTYP      OREQ 63                                        CAD015
     C           @@CNUM    ANDNECNUM                                      CAD015
     C           PTYP      OREQ 70                                        CAD015
     C           @@CNUM    ANDNECNUM                                      CAD015
     C           PTYP      OREQ 64                                        CAD015
     C           @@CNUM    ANDNEOLNO                                      CAD015
     C           PTYP      OREQ 65                                        CAD015
     C           @@CNUM    ANDNEOLNO                                      CAD015
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9110' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Loan transaction
      *
     C                     ELSE                            X------5
     C                     MOVELBRCA      @@BRCA           * Branch Code
     C                     MOVELCCY       @@UTSC           * U/T/S Ccy
     C                     Z-ADDCPAM      @@UTSA           * U/T/S Amount
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate 'D' as Loan in Dealing
      *
     C           D3UTSI    IFEQ 'D'                        *---------2
      *
      **  Access to Deals file with Used to Secure Reference
      *
     C           SSRREF    CHAINDEALSALL             71
     C           *IN71     IFEQ *ON                        *--------3
     C           @FORM     OREQ 'DEALSDCF'
     C           RECI      ANDNE'D'                        * from DEALSDC
     C           @FORM     OREQ 'MMDELDP0'
     C           DDLT      ANDNE*BLANKS                    * from MMDELDPP
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9111' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Valid deal type
      *
     C           DTYP      IFNE 'IP'                       *-------4
     C           DTYP      ANDNE'CL'
     C           DTYP      ANDNE'DL'
     C           DTYP      ANDNE'TI'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9112' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      **  Receiving customer must be the same as customer of deal
      *
     C           @@CNUM    IFNE CNUM                       *------5
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9113' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Deal transaction
      *
     C                     ELSE                            X------5
     C                     MOVELBRCA      @@BRCA           * Branch Code
     C                     MOVELCCY       @@UTSC           * U/T/S Ccy
     C                     Z-ADDPCPL      @@UTSA           * U/T/S Amount
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate 'G' as general ledger
      *
     C           D3UTSI    IFEQ 'G'                        *---------2
     C           E1ACTN    ANDNE'I'
     C                     MOVE SSCNUM    KKCNUM           * Customer Num.
     C                     MOVELSSCCY     KKCCY            * Currency
     C                     MOVE SSACOD    KKACOD           * Account Code
     C                     MOVE SSACSQ    KKACSQ           * Account Seq.
     C                     MOVELSSBRCA    KKBRCA           * Branch Code
      *
      **  Access to Account file with Used to Secure Reference -
      **    check if account is live and open
      *
     C           $ACCNT    CHAINACCNT                71
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C           ACST      ORNE ' '
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9114' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Receiving customer must be the same as the customer account
      **    (Used To Secure Reference could contain a different Customer No.)
      *
     C           @@CNUM    IFNE CNUM                       *-------4
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9115' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from General Ledger Account
      *
     C                     ELSE                            X-------4
     C                     MOVELBRCA      @@BRCA           * Branch Code
     C                     MOVELCCY       @@UTSC           * U/T/S Ccy
     C                     Z-ADDLDBL      @@UTSA           * U/T/S Amount
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate 'R' as retail
      *
     C           D3UTSI    IFEQ 'R'                        *---------2
     C           E1ACTN    ANDNE'I'
      *
      **  Access to account file with Used to Secure Reference
      **    (i.e. Retail Account Number) - check if account is live & open
      *
     C           SSACNO    CHAINACNUM                71
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C           ACST      ORNE ' '
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9116' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Receiving customer must be the same as the customer account
      *
     C           @@CNUM    IFNE CNUM                       *-------4
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9117' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Retail Account
      *
     C                     ELSE                            X-------4
     C                     MOVELBRCA      @@BRCA           * Branch Code
     C                     MOVELCCY       @@UTSC           * U/T/S Ccy
     C                     Z-ADDCLBL      @@UTSA           * U/T/S Amount
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate 'T' as SE Trade
      *
     C           D3UTSI    IFEQ 'T'                        *---------2
     C           E1ACTN    ANDNE'I'
      *
      **  Access to SE Trades file with Used to Secure Reference
      *
     C           SSTDRF    CHAINTRADS                71
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C           RECI      ANDNE'S'
     C           RECI      ANDNE'A'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0952' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Receiving customer must be the same as the customer of trade
      *
     C           @@CNUM    IFNE TCNR                       *-------4
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0951' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from SE Trade transaction
      *
     C                     ELSE                            X-------4
     C                     MOVE TDBA      @@BRCA           * Branch Code
     C                     MOVE TNMC      @@UTSC           * U/T/S Ccy
     C                     Z-ADDTCTR      @@UTSA           * U/T/S Amount
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate 'P' as SE Book Position
      *
     C           D3UTSI    IFEQ 'P'                        *---------2
     C           E1ACTN    ANDNE'I'
     C                     MOVE SSBHSC    KKBHSC           * Security
     C                     MOVE SSBHBA    KKBHBA           * Branch Code
     C                     MOVE SSBHBK    KKBHBK           * Book Code
     C                     MOVE SSBHMK    KKBHMK           * Market
     C                     MOVE BVSKTV    KKBHTV           * T/V Date Bas.
      *
      **  Access to SE Book Position file with Used to Secure Reference
      *
     C           $BKPHD    CHAINBKPHD                71
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0953' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Access to Security file with Used to Secure Reference
      *
     C           SSBHSC    CHAINSECTY                71
     C           *IN71     IFEQ *ON                        *-------4
     C           RECI      ORNE 'D'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0954' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      **  Receiving customer must be the same as the issuer of security
      *
     C           @@CNUM    IFNE ISSR                       *------5
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0951' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Book Position transaction
      *
     C                     ELSE                            X------5
     C                     MOVE SSBHBA    @@BRCA           * Branch Code
     C                     MOVELNMCY      @@UTSC           * U/T/S Ccy
     C**********           Z-ADD??????    @@UTSA           * U/T/S Amount
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate 'S' as Security
      *
     C           D3UTSI    IFEQ 'S'                        *---------2
     C           E1ACTN    ANDNE'I'
      *
      **  Access to Security file with Used to Secure Reference
      *
     C           SSSESN    CHAINSECTY                71
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0954' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Receiving customer must be the same as the issuer of security
      **    (Do not do test when program called from Window as Issuer
      **    can be amended and update will not yet have been commited,
      **    i.e. Securities Maintenance SE0040)
      *
     C           @@CNUM    IFNE ISSR                       *-------4
     C           E1ACTN    ANDNE'A'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0951' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Security
      *
     C                     ELSE                            X-------4
      *
      ** Retrieve issuer's branch
      *
     C                     MOVE ISSR      WWCUST
     C           WWCUST    CHAINSDCUSTL5             71
     C           *IN71     IFEQ *ON                        *-------5
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL*BLANKS   ZAMSDA
     C                     MOVEL'ERN0966' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------5
     C                     MOVE BBBRCD    @@BRCA
      *
     C                     MOVE NMCY      @@UTSC           * U/T/S Ccy
     C**********           Z-ADD??????    @@UTSA           * U/T/S Amount
      *
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
     C           D3UTSI    IFEQ 'S'                        *---------2  129190
     C           E1ACTN    ANDEQ'I'                                     129190
      *                                                                 129190
      ** Retrieve issuer's branch                                       129190
      *                                                                 129190
     C           D1CUST    CHAINSDCUSTL5             71                 129190
     C           *IN71     IFEQ *ON                        *---------3  129190
     C                     MOVE *ON       *IN32                         129190
     C                     MOVE *ON       *IN70                         129190
     C                     MOVEL*BLANKS   ZAMSDA                        129190
     C                     MOVEL'ERN0966' ZAMSID                        129190
     C                     EXSR ZASNMS                                  129190
     C                     ENDIF                           *---------3  129190
     C                     MOVE BBBRCD    @@BRCA                        129190
     C                     ENDIF                           *---------2  129190
      *
      **  Validate 'N' as Negotiable Asset Purchased
      *
     C           D3UTSI    IFEQ 'N'                        *---------2
     C           E1ACTN    ANDNE'I'
      *
      **  Access to Deals file with Used to Secure Reference
      *
     C           SSRREF    CHAINDEALSALL             71
     C           *IN71     IFEQ *ON                        *--------3
     C           @FORM     OREQ 'DEALSDDF'
     C           RECI      ANDNE'D'                        * from DEALSDD
     C           @FORM     OREQ 'MMDENBP0'
     C           DDLT      ANDNE*BLANKS                    * from MMDENBPP
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9111' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
      **  Valid deal type
      *
     C           DTYP      IFNE 'C2'                       *-------4
     C           DTYP      ANDNE'BP'
     C           DTYP      ANDNE'BD'
     C           DTYP      ANDNE'TB'
     C           DTYP      ANDNE'BA'
     C           DTYP      ANDNE'TA'
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9112' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      **  Receiving customer must be the same as customer of deal
      *
     C           @@CNUM    IFNE ISCN                       *------5
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9113' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Deal transaction
      **   (U/T/S Amount = Purchase Price - Prior Interest)
      *
     C                     ELSE                            X------5
     C                     MOVELBRCA      @@BRCA           * Branch Code
     C                     MOVELCCY       @@UTSC           * U/T/S Ccy
     C                     Z-ADDPUPR      @@UTSA           * U/T/S Amount
     C           DTYP      IFEQ 'C2'                       *-----6
     C           DTYP      OREQ 'BP'
     C           DTYP      OREQ 'BD'
     C*********            SUB  PINT1     @@UTSA           * U/T/S Amount
     C                     ENDIF                           *-----6
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *                                                                   ULX012
      ** Validate 'X' as Global Exposure                                  ULX012
      *                                                                   ULX012
     C           ULX012    IFEQ 'Y'                        *---------2    ULX012
     C           D3UTSI    ANDEQ'X'                                       ULX012
     C           *IN81     ANDEQ*OFF                       Protect Ind    ULX012
      *                                                                   ULX012
      ** 'X' value may only be entered when the 'Guar & coll Auto Gen'    ULX012
      ** flag maintained on the receiving customer has been set to 'Y'    ULX012
      *                                                                   ULX012
     C                     MOVE @@CNUM    KKCUST  6                       ULX012
     C********** KKCUST    CHAINSDCUSTY6             99                                ULX012 CER001
     C           KKCUST    CHAINSDCUX2L0             99                                       CER001
     C           *IN99     IFEQ '1'                                       ULX012
     C           VFAUTG    ORNE 'Y'                                       ULX012
     C                     MOVE *ON       *IN31                           ULX012
     C                     MOVE *ON       *IN70                           ULX012
     C                     MOVEL'ERN9150' ZAMSID                          ULX012
     C                     MOVEL*BLANKS   ZAMSDA                          ULX012
     C                     EXSR ZASNMS                                    ULX012
     C                     ENDIF                                          ULX012
      *                                                                   ULX012
      ** Reference must be left blank when the global exposure is         ULX012
      ** covered (i.e Use to secure ind is 'X')                           ULX012
     C           D3UTSR    IFNE *BLANKS                                   ULX012
     C                     MOVE *ON       *IN32                           ULX012
     C                     MOVE *ON       *IN70                           ULX012
     C                     MOVEL'ERN9151' ZAMSID                          ULX012
     C                     MOVEL*BLANKS   ZAMSDA                          ULX012
     C                     EXSR ZASNMS                                    ULX012
     C                     ENDIF                                          ULX012
      *                                                                   ULX012
     C                     ENDIF                           *---------2    ULX012
      *---------------------------------------------------------------*
      **  SECURED BY INDICATOR must be : 'D' as Time Deposit
      **                                 'G' as General Ledger Account
      **                                 'R' as Retail Account
      **                                 'A' as Amount
      **                                 'P' as Percentage
      **                                 'S' as Specific Provisions
      **                                 'C' as Customer Position
      **                                 'N' as Nominal
     C           D3SBYI    IFNE 'D'                        *---------2
     C           D3SBYI    ANDNE'G'
     C           D3SBYI    ANDNE'R'
     C           D3SBYI    ANDNE'A'
     C           D3SBYI    ANDNE'P'
     C           D3SBYI    ANDNE'S'
     C           D3SBYI    ANDNE'C'
     C           D3SBYI    ANDNE'N'
     C                     MOVE *ON       *IN33
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9124' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *---------2
      *
      *** If 'Used to secured' is a security, 'secured by' is a percentage
      *
     C           D3UTSI    IFEQ 'S'                        *---------2
     C           D3SBYI    ANDNE'P'
     C                     MOVE *ON       *IN33
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0965' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *---------2
      *                                                                   ULX012
      * When a global exposure is secured (i.e Used to secure 'X')        ULX012
      * the 'Secured by indicator' cannot be 'P' (Percentage) or          ULX012
      * 'S' (Specific Provisions)                                         ULX012
     C           ULX012    IFEQ 'Y'                        *---------2    ULX012
     C           D3UTSI    ANDEQ'X'                                       ULX012
     C           *IN82     ANDEQ*OFF                       Protect Ind    ULX012
      *                                                                   ULX012
     C           D3SBYI    IFEQ 'P'                                       ULX012
     C           D3SBYI    OREQ 'S'                                       ULX012
     C                     MOVE *ON       *IN33                           ULX012
     C                     MOVE *ON       *IN70                           ULX012
     C                     MOVEL'ERN9152' ZAMSID                          ULX012
     C                     MOVEL*BLANKS   ZAMSDA                          ULX012
     C                     EXSR ZASNMS                                    ULX012
     C                     ENDIF                                          ULX012
      *                                                                   ULX012
     C                     ENDIF                           *---------2    ULX012
      *
      *--------------------------------------------------------------
      *   Secured by   I                                             I
      *----------------I----------------------------------------------
      * Flag I  Ref.   I   CCY   I   Perc.   I   Amount  I   Given byI
      *------I---------I---------I-----------I-----------I-----------I
      *  D   I Deposit I Blank   I Optional  I Blank     I Blank     I
      *------I---------I---------I-----------I-----------I-----------I
      *  G   I Account I Mand.   I Optional  I Blank     I Blank     I
      *------I---------I---------I-----------I-----------I-----------I
      *  R   I Account I Blank   I Optional  I Blank     I Blank     I
      *------I---------I---------I-----------I-----------I-----------I
      *  P   I Blank   I Blank   I Mand.     I Blank     I Mand.     I
      *------I---------I---------I-----------I-----------I-----------I
      *  A   I Blank   I OptionalI Optional  I Mand.     I Mand.     I
      *------I---------I---------I-----------I-----------I-----------I
      *  S   I Account I Blank   I Optional  I Blank     I Blank     I   S1
      *------I---------I---------I-----------I-----------I-----------I
      *  S   I Blank   I Blank   I Mand.     I Blank     I Mand.     I   S2
      *------I---------I---------I-----------I-----------I-----------I
      *  S   I Blank   I Blank   I Blank     I Mand.     I Mand.     I   S3
      *------I---------I---------I-----------I-----------I-----------I
      *  C   I PositionI Blank   I Optional  I Blank     I Mand.     I
      *------I---------I---------I-----------I-----------I-----------I
      *  N   I SecurityI Blank   I Optional  I Mand.     I Blank     I
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------*
      ** Find SECURED REFERENCE related transaction dependent
      **   upon Secured By Indicator
      *
      **  Validate Secured By 'D' as Deposit
      *
     C           D3SBYI    IFEQ 'D'                        *---------2
     C********** E1ACTN    ANDNE'I'                                      128582
      *
      **  Validate only when INSERT
      *
     C           *IN09     IFEQ *ON                        *--------3
     C           *IN01     OREQ *ON
      *
     C           D3SBYR    IFEQ *BLANKS                    *-------4
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9118' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      ** 87       - Protect Guarantee Sequence
      *
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     MOVE *ON       *IN87
     C                     ENDIF                           *--------3
      *
      **  Access to Deals file with Secured By Reference
      *
     C           *IN34     IFEQ *OFF                       *--------3
     C           SBRREF    CHAINDEALSALL             71
     C           *IN71     IFEQ *ON                        *-------4
     C           @FORM     OREQ 'DEALSDCF'
     C           RECI      ANDNE'D'                        * from DEALSDC
     C           @FORM     OREQ 'MMDELDP0'
     C           DDLT      ANDNE*BLANKS                    * from MMDELDPP
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9111' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      **  Valid deal type
      *
     C           DTYP      IFNE 'IT'                       *------5
     C           DTYP      ANDNE'CD'
     C           DTYP      ANDNE'CI'
     C           DTYP      ANDNE'TD'
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9120' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Deal transaction
      *
     C                     ELSE                            X------5
     C                     Z-ADDPCPL      @@GAMT           * Guarantee Amt
     C                     MOVELCCY       @@GCCY           * Guarantee Ccy
     C                     MOVE CNUM      @@GVBY           * Given By
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate Secured By 'G' as General Ledger account
      **  Validate Secured By 'S' as Specific Provison account
      *
     C           D3SBYI    IFEQ 'G'                        *---------2
     C           D3SBYI    OREQ 'S'
     C           D3SBYR    ANDNE*BLANKS
      *
      **  Validate only when INSERT
      *
     C           *IN09     IFEQ *ON                        *--------3
     C           *IN01     OREQ *ON
      *
     C           D3SBYR    IFEQ *BLANKS                    *-------4
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9118' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 87       - Protect Guarantee Sequence
      *
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN87
      *
     C           D3SBYI    IFEQ 'G'
      *
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      *
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     ELSE
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     ENDIF
     C                     ENDIF                           *--------3
      *
      ** Build key to ACCNT file
      *
     C                     MOVE SBCUST    KKCNUM           * Customer Num.
     C           D3SBYI    IFEQ 'G'
     C           D3SBYI    OREQ 'S'                                       164197
     C           D3GCCY    ANDEQ'EUR'                                     164197
     C                     MOVELD3GCCY    KKCCY            * Currency
     C                     ELSE
     C                     MOVEL@@UTSC    KKCCY            * Currency
     C                     ENDIF
     C                     MOVE SBACOD    KKACOD           * Account Code
     C                     MOVE SBACSQ    KKACSQ           * Account Seq.
     C                     MOVELSBBRCA    KKBRCA           * Branch Code
      *
      **  Access to Account file with Secured By Reference -
      **    check if account is live and open
      *
     C           *IN34     IFEQ *OFF                       *--------3
     C           $ACCNT    CHAINACCNT                71
     C           *IN71     IFEQ *ON                        *-------4
     C           RECI      ORNE 'D'
     C           ACST      ORNE ' '
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9114' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      ** Verify type of General Ledger Account
      *
     C           ATYP      IFEQ 'R'                        *------5
     C           D3SBYI    ANDEQ'S'
     C           ATYP      OREQ 'R'
     C           D3SBYI    ANDEQ'G'
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0955' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X------5
      *
      **  Check if retail, take cleared balance else ledger balance
      *
     C********** ATYP      IFEQ 'R'                        *-----6
     C**********           Z-ADDCLBL      @@GAMT           * Guarantee Amt
     C**********           ELSE                            X-----6
     C                     Z-ADDLDBL      @@GAMT           * Guarantee Amt
     C**********           ENDIF                           *-----6
      *
      ** Verify Account Balance is negative (i.e. in Credit)
      *
     C           @@GAMT    IFGE *ZEROS                     *-----6
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9131' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from General Ledger Account
      *
     C                     ELSE                            X-----6
     C                     MOVELCCY       @@GCCY           * Guarantee Ccy
     C                     MOVE CNUM      @@GVBY           * Given By
     C                     ENDIF                           *-----6
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate Secured By 'R' as Retail Account
      *
     C           D3SBYI    IFEQ 'R'                        *---------2
      *
      **  Validate only when INSERT
      *
     C           *IN09     IFEQ *ON                        *--------3
     C           *IN01     OREQ *ON
      *
     C           D3SBYR    IFEQ *BLANKS                    *-------4
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9118' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      ** 87       - Protect Guarantee Sequence
      *
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     MOVE *ON       *IN87
     C                     ENDIF                           *--------3
      *
      **  Access to account file with Secured By Reference
      **    (i.e. Retail Account Number) - check if account is live & open
      *
     C           SBACNO    CHAINACNUM                71
     C           *IN71     IFEQ *ON                        *--------3
      *
      ** Build key to ACCNT file
      *
     C                     MOVE SBCUST    KKCNUM           * Customer Num.
     C                     MOVELD3GCCY    KKCCY            * Currency
     C                     MOVE SBACOD    KKACOD           * Account Code
     C                     MOVE SBACSQ    KKACSQ           * Account Seq.
     C                     MOVELSBBRCA    KKBRCA           * Branch Code
      *
      **  Access to Account file with Secured By Reference -
      **    check if account is live and open
      *
     C           $ACCNT    CHAINACCNT                71
     C           *IN71     IFEQ *ON                        *-------4
     C           RECI      ORNE 'D'
     C           ACST      ORNE ' '
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9116' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
     C           RECI      IFNE 'D'                        *-------4
     C           ACST      ORNE ' '
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9116' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store key to ACCNT file
      *
     C                     ELSE                            X-------4
     C                     MOVE CNUM      SBCUST           * Customer Num.
     C                     MOVE ACOD      SBACOD           * Account Code
     C                     MOVE ACSQ      SBACSQ           * Account Seq.
     C                     MOVELBRCA      SBBRCA           * Branch Code
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
      *
      ** Verify type of General Ledger Account
      *
     C           *IN34     IFEQ *OFF                       *--------3
     C           ATYP      IFNE 'R'                        *-------4
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0966' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      ** Verify Account Balance is negative (i.e. in Credit)
      *
     C                     Z-ADDCLBL      @@GAMT           * Guarantee Amt
     C           @@GAMT    IFGE *ZEROS                     *------5
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9131' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Retail Account
      *
     C                     ELSE                            X------5
     C                     MOVELCCY       @@GCCY           * Guarantee Ccy
     C                     MOVE CNUM      @@GVBY           * Given By
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate Secured By 'C' as Customer Position
      *
     C           D3SBYI    IFEQ 'C'                        *---------2
      *
      **  Validate only when INSERT
      *
     C           *IN09     IFEQ *ON                        *--------3
     C           *IN01     OREQ *ON
      *
     C           D3SBYR    IFEQ *BLANKS                    *-------4
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9118' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 87       - Protect Guarantee Sequence
      *
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN87
     C                     ENDIF                           *--------3
      *
      ** Find Customer Number from Given By field (validate later)
      *
     C           D3GVBY    IFNE *BLANKS                    *--------3
      *
      ** Access with Customer SHORT NAME
      *
     C           D3GVBY    CHAINSDCUSTL2             71
     C           *IN71     IFEQ *ON                        *-------4
      *
      ** If not found, access with CUST NUMBER
      *
     C           D3CUST    CHAINSDCUSTL5             71
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
      *
      ** Build key to CPOSN file
      *
     C                     MOVE SBCSBA    KKCSBA           * Branch Code
     C                     MOVE SBCSSC    KKCSSC           * Security
     C           D3GVBY    IFNE *BLANKS                    *--------3
     C           *IN71     ANDEQ*OFF
     C                     MOVELBBCUST    KKCSCN           * Customer Num.
     C                     ELSE                            X--------3
     C                     MOVEL*BLANKS   KKCSCN           * Customer Num.
     C                     ENDIF                           *--------3
      *
      **  Access to Customer Postions file with Secured By Reference
      *
     C           *IN34     IFEQ *OFF                       *--------3
     C           $CPOSN    CHAINCPOSN                71
     C           *IN71     IFEQ *ON                        *-------4
     C           RECI      ORNE 'D'
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0956' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      **  Check if trade/value date, take nominal balance (trade or value)
      *
     C           BVSKTV    IFEQ 'T'                        *------5
     C*******              Z-ADDCSNT      @@GAMT           * Guarantee AmtULX012
     C                     Z-ADDCSNV      @@GAMT           * Guarantee AmtULX012
     C                     ELSE                            X------5
     C*******              Z-ADDCSNV      @@GAMT           * Guarantee AmtULX012
     C                     Z-ADDCSNT      @@GAMT           * Guarantee AmtULX012
     C                     ENDIF                           *------5
      *
      ** Verify Nominal Balance is positive (i.e. in Credit)
      *
     C           @@GAMT    IFLE *ZEROS                     *------5
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0957' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ELSE                            X------5
      *
      **  Access to Security file with Security No. for Nominal Currency
      *
     C           CSSC      CHAINSECTY                71
      *
      ** Store data from Customer Position
      *
     C           *IN71     IFEQ *OFF
     C                     MOVE NMCY      @@GCCY           * Guarantee Ccy
     C                     ELSE
     C                     MOVE *BLANKS   @@GCCY           * Guarantee Ccy
     C                     ENDIF
     C                     MOVELCSCN      @@GVBY           * Given By
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
      *
     C                     ENDIF                           *---------2
      *
      **  Validate Secured By 'N' as Nominal
      *
     C           D3SBYI    IFEQ 'N'                        *---------2
      *
      **  Validate only when INSERT
      *
     C           *IN09     IFEQ *ON                        *--------3
     C           *IN01     OREQ *ON
      *
     C           D3SBYR    IFEQ *BLANKS                    *-------4
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9118' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 83       - Protect Guarantee Currency
      ** 86       - Protect Given By
      ** 87       - Protect Guarantee Sequence
      *
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN86
     C                     MOVE *ON       *IN87
     C                     ENDIF                           *--------3
      *
      **  Access to Security file with Secured By Reference
      *
     C           SBSESN    CHAINSECTY                71
     C           *IN71     IFEQ *ON                        *--------3
     C           RECI      ORNE 'D'
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0954' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Store data from Security
      *
     C                     ELSE                            X--------3
      *                                                                   128241
     C                     Z-ADD0         @@GAMT           * Guarantee Amt
     C                     MOVELNMCY      @@GCCY           * Guarantee Ccy
     C                     MOVE ISSR      @@GVBY           * Given By
     C                     Z-ADDNMDP      WWNMDP                          128241
      *                                                                   128241
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *
      **  Validate Secured By 'P' as Percentage
      **  Validate Secured By 'A' as Amount
      *
     C           D3SBYI    IFEQ 'P'                        *---------2
     C           D3SBYI    OREQ 'A'
      *
      **  Validate only when INSERT
      *
     C           *IN09     IFEQ *ON                        *--------3
     C           *IN01     OREQ *ON
      *
     C           D3SBYR    IFNE *BLANKS                    *-------4
     C                     MOVE *ON       *IN34
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9118' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 87       - Protect Guarantee Sequence
      *
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN87
      *
     C           D3SBYI    IFEQ 'P'                        *-------4
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
      *
      ** Store data
      *
     C                     Z-ADD0         @@GAMT           * Guarantee Amt
     C**********           MOVELD3GCCY    @@GCCY           * Guarantee Ccy
     C**********           MOVEL*BLANKS   @@GVBY           * Given By
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  GUARANTEE CURRENCY
      *
     C                     MOVE *BLANKS   @@CURR
     C*
      ** Retrieve the default currency
      **
      ** If no transaction filled in 'secured by' or 'specifc provision',
      ** use the transaction currency from the 'used to secured'
      *
     C           D3SBYR    IFEQ *BLANKS                    *--------2
     C           D3SBYI    OREQ 'S'
      *
     C                     MOVE @@UTSC    @@DFCY           * Default Ccy
      *
      ** Otherwise, use the transaction currency of the 'secured by'
      *
     C                     ELSE                            X--------2
      *
     C                     MOVE @@GCCY    @@DFCY           * Default Ccy
      *
     C                     ENDIF                           *--------2
      *
     C           D3SBYI    IFEQ 'S'                                       164197
     C           D3GCCY    ANDEQ'EUR'                                     164197
     C                     MOVE @@GCCY    @@DFCY                          164197
     C                     ENDIF                                          164197
      *
      ** Validate the guarantee currency
      *
     C           D3GCCY    IFEQ *BLANKS                    *---------2
      *
      ** The currency input is mandatory for 'secured by' = 'G'
      *
     C           D3SBYI    IFEQ 'G'                        *---------3
     C           ULX012    OREQ 'Y'                                       ULX012
     C           D3UTSI    ANDEQ'X'                                       ULX012
     C           D3SBYR    ANDEQ*BLANKS                                   ULX012
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0959' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** If no currency enterd, set the default
      *
     C                     ELSE                            X---------3
      *
     C                     MOVE @@DFCY    D3GCCY           * Guarantee Ccy
     C                     MOVE @@DFCY    @@CURR           * Guarantee Ccy
      *
     C                     ENDIF                           *--------3
      *
      ** Currency is entered
      *
     C                     ELSE                            X---------2
      *
      ** Validate currency is entered
      *
     C           D3SBYI    IFEQ 'D'                        *---------3
     C           D3SBYI    OREQ 'R'
     C           D3SBYI    OREQ 'P'
     C           D3SBYI    OREQ 'S'
     C           D3SBYI    OREQ 'C'
     C           D3SBYI    OREQ 'N'
      *
      **  Guarantee Currency must be the same as the default value
      **    (use currency from transaction)
      *
     C           D3GCCY    IFNE @@DFCY                     *-------4
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0958' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
     C                     MOVE D3GCCY    @@CURR           * Guarantee Ccy
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY    '@OPTN
     C                     PARM D3GCCY    @KEY1
     C           SDCURR    PARM SDCURR    DSFDY
      *
      ** Validate currency entered exists and is a dealing currency
      *
     C           @RTCD     IFNE *BLANKS                    *-------4
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9128' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
     C           A6DLCI    IFNE 'Y'                        *------5
     C                     MOVE *ON       *IN35
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9132' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X------5
      *
     C                     MOVE D3GCCY    @@CURR           * Guarantee Ccy
      *
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  GUARANTEE PERCENTAGE
      *
      ** If the percentage is not filled, 100 % is assume
      *
     C           D3GPER    IFEQ *BLANKS                    *---------2
      *
     C                     MOVE 100       WWWPER  30
     C                     MOVE 100       D3GPER
      *
     C                     ELSE                            X---------2
      *
      ** Validate and align Guarantee Percentage
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE D3GPER    ZFIELD
     C                     MOVE *ZEROS    ZADEC
     C                     MOVE 3         ZADIG
     C                     EXSR ZALIGN
     C           *IN99     IFEQ *ON                        *--------3
     C                     MOVE *ON       *IN36
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9122' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
     C                     MOVE ZFIELD    WWWPER  30
     C           WWWPER    IFLT 1                          *-------4
     C           WWWPER    ORGT 100
     C                     MOVE *ON       *IN36
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9122' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  GUARANTEE AMOUNT
      *
     C                     Z-ADD*ZEROS    @@AMNT
      *
      **  For amount defined in the transaction,
      *
     C           D3SBYI    IFEQ 'D'                        *---------2
     C           D3SBYI    OREQ 'G'
     C           D3SBYI    OREQ 'R'
     C           D3SBYI    OREQ 'P'
     C           D3SBYI    OREQ 'C'
     C           D3SBYI    OREQ 'S'
     C           D3SBYR    ANDNE*BLANKS
      *
      **  Guarantee Amount must NOT be entered
      **  Except for the guarantee automatically generated                ULX012
      *
     C           D3GAMT    IFNE *BLANKS                    *--------3
     C           ULX012    ANDEQ'N'                                       ULX012
     C           ULX012    OREQ 'Y'                                       ULX012
     C           D3GAMT    ANDNE*BLANKS                                   ULX012
     C********** D3INSA    ANDNE'Y'                                                    ULX012 CER001
     C                     MOVE *ON       *IN37
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0960' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *--------3
      *
     C                     ELSE                            X---------2
      *
      ** Validate amount is entered
      *
     C           D3GAMT    IFEQ *BLANKS                    *--------3
      *
      ** Amount is mandatory
      *
     C           D3SBYI    IFEQ 'A'                        *-------4
     C           D3SBYI    OREQ 'N'
      *
     C                     MOVE *ON       *IN37
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0961' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      ** Validate and align Guarantee Amount
      *
     C           D3SBYI    IFEQ 'N'                        *-------4
     C*********************MOVE *ZEROS    ZADEC                           128241
     C                     Z-ADDWWNMDP    ZADEC                           128241
     C                     ELSE                            X-------4
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY    '@OPTN
     C                     PARM D3GCCY    @KEY1
     C           SDCURR    PARM SDCURR    DSFDY
     C                     MOVE A6NBDP    ZADEC
     C                     ENDIF                           *-------4
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE D3GAMT    ZFIELD
     C*********************MOVE 12        ZADIG                           128241
     C           13        SUB  ZADEC     ZADIG                           128241
     C                     EXSR ZALIGN
     C           *IN99     IFEQ *ON                        *-------4
     C                     MOVE *ON       *IN37
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9144' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
     C                     MOVE ZFIELD    @@AMNT           * U/T/S Amount
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    D3GAMT
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  GIVEN BY
      *
      ** If the Given By is blanks,
      *
     C           D3GVBY    IFEQ *BLANKS                    *---------2
      *
      ** The Given By is mandatory,
      *
     C           D3SBYI    IFEQ 'P'                        *--------3
     C           D3SBYI    OREQ 'A'
     C           D3SBYI    OREQ 'C'
     C           D3SBYI    OREQ 'S'
     C           D3SBYR    ANDEQ*BLANKS
     C                     MOVE *ON       *IN38
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0963' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
      *
     C                     MOVEL@@GVBY    D3GVBY           * Given By
      *
     C                     ENDIF                           *--------3
      *
      ** Given By is entered,
      *
     C                     ELSE                            *---------2
      *
      ** Access with Customer SHORT NAME
      *
     C           D3GVBY    CHAINSDCUSTL2             71
     C           *IN71     IFEQ *ON                        *--------3
      *
      ** If not found, access with CUST NUMBER
      *
     C           D3CUST    CHAINSDCUSTL5             71
     C           *IN71     IFEQ *ON                        *-------4
     C           D4CUST    ORNE *BLANKS
     C                     MOVE *ON       *IN38
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN0901' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
      *
      ** Given By should be the counterparty of the Used To secure
      *
     C           D3SBYI    IFEQ 'D'                        *--------3
     C           D3SBYI    OREQ 'G'
     C           D3SBYI    OREQ 'R'
     C           D3SBYI    OREQ 'N'
     C           D3SBYI    OREQ 'S'
     C           D3SBYR    ANDNE*BLANKS
      *
     C                     MOVE @@GVBY    WWCUST
     C           WWCUST    IFNE BBCUST                     *-------4
     C                     MOVE *ON       *IN38
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0962' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
      *
      **  Given By is the Receiving Customer
      *
     C                     MOVE BBCUST    @@GVBY           * Given By
      *
     C                     ENDIF                           *--------3
      *
     C                     ENDIF                           *---------2
      *
      ** If Guarantee Code is 20 - Given By must be different from
      **   Receiving Customer
      *
     C           D3GCOD    IFEQ '20'                       *---------2
     C           *IN38     ANDEQ*OFF
     C           @@GVBY    ANDEQ@@CNUM
     C                     MOVE *ON       *IN38
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0964' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  GUARANTEE CODE
      *
     C           D3GCOD    LOKUPGUA                      71
     C           *IN71     IFEQ *OFF                       *---------2
     C                     MOVE *ON       *IN39
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9133' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ELSE                                                               CER001
     C**********           ENDIF                           *---------2                        CER001
      *
      **  VALIDE GUARANTY CODE ACCOREING THE USED TO SECURED INDICATOR
      *
     C                     SELEC
     C           D3SBYI    WHEQ ' '                        *---------2
     C                     MOVE *OFF      *IN71
      **  Guarantee Code Table for specific provision
     C           D3SBYI    WHEQ 'S'                        *---------2
     C           D3GCOD    LOKUPGCSP                     71
      **  Guarantee Code Table for general ledger
     C           D3SBYI    WHEQ 'G'                        *---------2
     C           D3GCOD    LOKUPGCGL                     71
      **  Guarantee Code Table for retail account
     C           D3SBYI    WHEQ 'R'                        *---------2
     C           D3GCOD    LOKUPGCRT                     71
      **  Guarantee Code Table for MM deposit
     C           D3SBYI    WHEQ 'D'                        *---------2
     C           D3GCOD    LOKUPGCMM                     71
      **  Guarantee Code Table for customer position
     C           D3SBYI    WHEQ 'C'                        *---------2
     C           D3GCOD    LOKUPGCCP                     71
      **  Guarantee Code Table for nominal
     C           D3SBYI    WHEQ 'N'                        *---------2
     C           D3GCOD    LOKUPGCNO                     71
      **  Guarantee Code Table for amount
     C           D3SBYI    WHEQ 'A'                        *---------2
     C           D3GCOD    LOKUPGCAM                     71
      **  Guarantee Code Table for percentage
     C           D3SBYI    WHEQ 'P'                        *---------2
     C           D3GCOD    LOKUPGCPR                     71
     C                     ENDSL                           *---------2
      *
     C           *IN71     IFEQ *OFF                       *---------2
     C                     MOVE *ON       *IN39
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9133' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *---------2
     C                     ENDIF                                                              CER001
      *---------------------------------------------------------------*
      **  GUARANTEE SEQUENCE
      *
     C           D3GSEQ    IFEQ *BLANKS                    *---------2
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9143' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
      ** Validate and align Guarantee Sequence
      *
     C                     ELSE                            X---------2
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE D3GSEQ    ZFIELD
     C                     MOVE *ZEROS    ZADEC
     C                     MOVE 3         ZADIG
     C                     EXSR ZALIGN
     C           *IN99     IFEQ *ON                        *--------3
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9143' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ELSE                            X--------3
     C                     MOVE ZFIELD    @@GSEQ           * Guarantee Seq
      *
     C           @@GSEQ    IFLT 1                          *-------4
     C           @@GSEQ    ORGT 999
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9143' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *-------4
      *                                                                   ULX012
      ** The guarantee sequence must be lower than the 'Start sequence'   ULX012
      ** specified on the ICD (ERLUICDPD)                                 ULX012
     C           ULX012    IFEQ 'Y'                        *-------4      ULX012
     C           @@GSEQ    ANDGEVPSTSQ                                    ULX012
     C********** D3INSA    ANDNE'Y'                                                    ULX012 CER001
     C                     MOVE *ON       *IN40                           ULX012
     C                     MOVE *ON       *IN70                           ULX012
     C                     MOVEL'ERN9153' ZAMSID                          ULX012
     C                     MOVEL*BLANKS   ZAMSDA                          ULX012
     C                     EXSR ZASNMS                                    ULX012
     C                     ENDIF                           *-------4      ULX012
      *
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  START DATE
      *
     C           D3STDT    IFEQ *BLANKS                    *---------2
     C                     MOVE *ON       *IN41
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9134' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X---------2
     C                     MOVE D3STDT    ZDATE
     C                     EXSR ZDATE1
     C           *IN99     IFEQ *ON                        *--------3
     C                     MOVE *ON       *IN41
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9134' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
     C                     MOVE ZDAYNO    @@STDT           * Start Date
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  END DATE
      *
     C           D3ENDT    IFEQ *BLANKS                    *---------2
     C                     MOVE *ON       *IN42
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9145' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X---------2
     C           D3SBYI    IFNE 'S'                        *--------3
     C                     MOVE D3ENDT    ZDATE
     C                     EXSR ZDATE1
     C           *IN99     IFEQ *ON                        *-------4
     C                     MOVE *ON       *IN42
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9145' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
     C           ZDAYNO    IFLE BJRDNB                     *------5
     C           ZDAYNO    ORLE @@STDT
     C                     MOVE *ON       *IN42
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9135' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X------5
     C                     MOVE ZDAYNO    @@ENDT           * End Date
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
      *
     C                     ELSE                            X--------3
     C           D3ENDT    IFNE '999999'                   *-------4
     C                     MOVE D3ENDT    ZDATE
     C                     EXSR ZDATE1
     C           *IN99     IFEQ *ON                        *------5
     C                     MOVE *ON       *IN42
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9145' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ELSE                            X5
     C           ZDAYNO    IFLE BJRDNB                     B6
     C           ZDAYNO    ORLE @@STDT
     C                     MOVE *ON       *IN42
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9135' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ELSE                            X6
     C                     MOVE ZDAYNO    @@ENDT           * End Date
     C                     ENDIF                           E6
     C                     ENDIF                           *------5
     C                     ELSE                            X-------4
     C                     MOVE D3ENDT    @@ENDT           * End Date
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  REVIEW DATE, frequency, day number
      *
     C                     MOVE 'Y'       WWVALD  1
     C           D3RVFQ    IFNE *BLANKS                    B2
      *
      **  If entered must be equal to 'M', 'Q', 'X' OR 'Y'
     C           D3RVFQ    IFNE 'M'                        B3
     C           D3RVFQ    ANDNE'Q'
     C           D3RVFQ    ANDNE'X'
     C           D3RVFQ    ANDNE'Y'
     C                     MOVE 'N'       WWVALD
     C                     MOVEL'ERN9136' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN44
     C                     ENDIF                           E3
      *
      **  If entered number of day in month must also be specified
     C           D3RVDY    IFEQ *BLANKS                    B3
     C                     MOVE 'N'       WWVALD
     C                     MOVEL'ERN9137' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN45
     C                     ENDIF                           E3
     C                     ENDIF                           E2
      *
      **  Number of day in month
     C           D3RVDY    IFNE *BLANKS                    B2
      *
      **  If entered frequency must also been entered
     C           D3RVFQ    IFEQ *BLANKS                    B3
     C                     MOVE 'N'       WWVALD
     C                     MOVEL'ERN9138' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN44
     C                     ENDIF                           E3
      *
      **  If entered must be in range 1-31
     C                     MOVE D3RVDY    WWVLDM  20
     C                     MOVE WWVLDM    WWVVDM  2
     C           WWVVDM    IFNE D3RVDY                     B3
     C           WWVLDM    ORLT 1
     C           WWVLDM    ORGT 31
     C                     MOVE 'N'       WWVALD
     C                     MOVEL'ERN9139' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN45
     C                     ENDIF                           E3
     C                     ENDIF                           E2
      *
      **  Next date
     C                     Z-ADD0         @@RVDT
     C           D3RVDT    IFNE *BLANKS                    B2
      *
      **  If entered must be a valid date
     C                     Z-ADD0         WWDAT1  60
     C                     MOVE *BLANKS   WWDAT2  6
     C                     MOVE D3RVDT    WWDAT1
     C                     MOVE WWDAT1    WWDAT2
      *
     C                     MOVELD3RVDT    ZDATE
     C                     EXSR ZDATE1
     C           *IN99     IFEQ *ON                        B3
     C           D3RVDT    ORNE WWDAT2
     C                     MOVEL'ERN9140' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN43
     C                     ELSE                            X3
     C                     Z-ADDZDAYNO    @@RVDT
     C                     ENDIF                           E3
      *
      **  If entered must not be before run date
     C           @@RVDT    IFLT BJRDNB                     B3
     C                     MOVEL'ERN9141' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN43
     C                     ENDIF                           E3
      *
      **  If entered must be between Start Date and End Date
     C           @@RVDT    IFLT @@STDT
     C           @@RVDT    ORGT @@ENDT                     B3
     C                     MOVEL'ERN0926' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN43
     C                     ENDIF                           E3
      *
     C                     ELSE                            X2
      *
      **  If not entered and frequency and day in month entered calculate it
     C           D3RVFQ    IFNE *BLANKS                    B3
     C           D3RVDY    ANDNE*BLANKS
     C           WWVALD    ANDEQ'Y'
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     MOVELD3RVFQ    ZFREQ
     C                     MOVELD3RVDY    ZMDAY
     C                     MOVELBJLCCY    ZCCY
     C                     EXSR ZFRQA
      *
      ***  If review date calculated greater than end date, use the end date
      *
     C           ZDAYNO    IFGT @@ENDT                     B4
     C           @@ENDT    ANDNE*ZEROS
     C                     Z-ADD@@ENDT    @@RVDT
     C                     ELSE                            X4
     C                     Z-ADDZDAYNO    @@RVDT
     C                     ENDIF                           E4
     C                     ENDIF                           E3
      *
      **  If day in month same as todays date default next date to run date
     C                     MOVELBJMRDT    WWDAY   20
     C           WWVLDM    IFEQ WWDAY                      B3
     C                     Z-ADDBJRDNB    @@RVDT
     C                     ENDIF                           E3
     C                     ENDIF                           E2
      *---------------------------------------------------------------*
      **  Used to secured Loans
      *
      **  The default value is required ('N')
      *
     C           D3UTSL    IFEQ *BLANKS                    *---------2
     C                     MOVE 'N'       D3UTSL
     C                     ENDIF                           *---------2
      *
      **  Valid entries are 'N' or 'Y'
      *
     C           D3UTSL    IFNE 'Y'                        *---------2
     C           D3UTSL    ANDNE'N'
     C                     MOVEL'ERN9146' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN47
     C                     ENDIF                           *---------2
      *
      **  Used to secure loans could be 'Y' only for used to secure = Facility
      *
     C           D3UTSI    IFNE 'F'                        *---------2
     C           D3UTSL    ANDEQ'Y'
     C                     MOVEL'ERN9147' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN47
     C                     ENDIF                           *---------2
      *
      *---------------------------------------------------------------*
      **  Additional NARRATIVE must be blanks for Secured by Ind = 'S'
      *
     C           D3SBYI    IFEQ 'S'                        *---------2
     C           D3ACOM    IFNE *BLANKS                    *--------3
     C                     MOVEL'ERN9142' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN70
     C                     MOVE *ON       *IN46
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  ACCUMULATED GUARANTEE AMOUNT (For multiple transactions)
      *
     C           *IN37     IFEQ *OFF                       *---------2
     C           *IN35     ANDEQ*OFF
     C           D3SBYR    ANDNE*BLANKS
     C           D3SBYI    ANDNE'N'
      *
     C                     EXSR #CUMU
      *
     C           ZZCPER    IFGT 100                        *--------3
     C                     MOVE *ON       *IN36
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN9130' ZAMSID
     C                     MOVELERRPER    ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      ** If valid key data, check for Guarantees & Collateral record
      *
     C           *IN70     IFEQ *OFF                       *---------2
     C                     MOVE @@BRCA    KKBRCA           * Branch Code
     C                     MOVE @@CNUM    KKRCUS           * Receiving C/m
     C                     MOVE D3UTSI    KKUTSI           * U/T/S Ind.
     C                     MOVE D3UTSR    KKUTSR           * U/T/S Refer.
     C                     MOVE D3GSEQ    KKGSEQ           * Guarantee Seq
      *
      **  If record already exists on Guarantee file for INSERT ---> error
      *
     C           $ERGU2    CHAINERGUARL2             7177
      *
      **  If an error in chain (record is locked), send an error message
      *
     C           *IN77     IFNE *ZEROS                     *--------3
     C                     MOVE *ON       *IN31
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERL0950' ZAMSID
     C                     MOVEL##USR     ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X--------3
     C           *IN71     IFEQ *OFF                       *-------4
     C           *IN01     ANDEQ*ON
     C           *IN71     OREQ *OFF
     C           *IN09     ANDEQ*ON
     C                     MOVE *ON       *IN31
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN0908' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
      *
     C                     ELSE                            X-------4
      *
      **  If record does not exist on Guarantee file for AMEND ---> error
      *
     C           *IN71     IFEQ *ON                        *------5
     C           *IN01     ANDEQ*OFF
     C           *IN09     ANDEQ*OFF
     C                     MOVE *ON       *IN31
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN70
     C                     MOVEL'ERN0105' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *------5
     C                     ENDIF                           *-------4
     C                     ENDIF                           *--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      *
      **  Validate Secured By 'S' as Specific Provisions
      *
     C           D3SBYI    IFEQ 'S'                        *---------2
      *
     C                     SELEC
     C           D3SBYR    WHNE *BLANKS                    S--------3
     C           D3GAMT    ANDEQ*BLANKS
     C                     MOVE 'S1'      WSWORK  2
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      ** 88       - Protect Additional Comment
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     MOVE *ON       *IN88
      *
     C           D3SBYR    WHEQ *BLANKS                    S--------3
     C           D3GAMT    ANDEQ*BLANKS
     C                     MOVE 'S2'      WSWORK
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 88       - Protect Additional Comment
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN88
      *
     C           D3SBYR    WHEQ *BLANKS                    S--------3
     C           D3GAMT    ANDNE*BLANKS
     C                     MOVE 'S3'      WSWORK
      *
      ** 83       - Protect Guarantee Currency
      ** 84       - Protect Guarantee Percentage
      ** 88       - Protect Additional Comment
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN84
     C                     MOVE *ON       *IN88
      *
     C                     OTHER                           S--------3
     C                     ENDSL                           S--------3
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      **  If errors redisplay screen otherwise write on file
      *
     C           *IN70     IFEQ *ON                        *---------2
     C                     MOVE *ON       *IN53
     C                     EXSR #WPRO3
     C                     ELSE                            X---------2
     C                     EXSR #U003
     C                     ENDIF                           *---------2
      *---------------------------------------------------------------*
      *
     C                     ELSE                            X----------1
      *
      **  Enter pressed in delete mode
      *
     C           SFACTN    IFEQ 'D'                        *---------2
     C                     MOVEL'ERN0929' ZAMSID
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     MOVE *ON       *IN10
     C                     MOVE *ON       *IN53
     C                     MOVE *ON       *IN80
      *
     C                     ELSE                            X---------2
      *
     C           WWRRN     IFEQ *ZEROS                     *--------3
     C                     MOVE *ON       *IN51
     C                     ELSE                            X--------3
     C           WWRRN     CHAINERL910S2             71
     C                     MOVEL*BLANKS   SFACTN
     C                     UPDATERL910S2
     C                     MOVE *ON       *IN52
     C                     ENDIF                           *--------3
      *
     C                     ENDIF                           *---------2
      *
     C                     ENDIF                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #PRV3  - SCREEN 3 PROCESSING (Cmd Key 12)            *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #WSETP   - SR SETUP WORK INDICATORS                           *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #MAIN    - SR MAIN PROCESSING                                 *
      *                                                               *
      *****************************************************************
     C           #PRV3     BEGSR
      *
      **  Reset work indicators
     C                     EXSR #WSETP
      *
      **  Return from Add mode
     C           *IN09     IFEQ *OFF                       *----------1
      *
     C           SFACTN    IFEQ 'A'                        *---------2
     C           SFACTN    OREQ 'D'
     C           SFACTN    OREQ 'E'
      *
      **  Display SFL screen at same place
      *
     C           WWRRN     IFEQ *ZEROS                     *--------3
     C                     MOVE *ON       *IN51
     C                     ELSE                            X--------3
     C           WWRRN     CHAINERL910S2             71
     C                     MOVEL*BLANKS   SFACTN
     C                     UPDATERL910S2
     C                     MOVE *ON       *IN52
     C                     ENDIF                           *--------3
      *
      **  Display screen 1
      *
     C                     ELSE                            X---------2
     C                     MOVEL*BLANKS   DDCUST
     C                     MOVE *ON       *IN51
     C                     ENDIF                           *---------2
      *
     C                     ELSE                            X----------1
     C                     Z-ADDWWRANG    WWRRN
     C                     MOVE *ON       *IN52
     C                     ENDIF                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #CUMU  - SCREEN 3 PROCESSING (Accumulate Guar. Amnt) *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S004    - SR ENTER ON DETAILS SCREEN                         *
      *                                                               *
      *****************************************************************
     C           #CUMU     BEGSR
      *
      **  Retrieve amount in Guarantee file for the same key
      *
     C                     Z-ADD*ZEROS    ZZCPER  30
      *
     C                     MOVELD3SBYI    WWSBYI
     C                     MOVELD3SBYR    WWSBYR
     C           $ERGU0    SETLLERGUARL0
     C           $ERGU0    READEERGUARL0                 75
      *
     C           *IN75     DOWEQ*OFF                       BW1
     C           RRCHTP    IFNE 'D'
     C           ULX012    ANDEQ'N'                                       ULX012
     C           ULX012    OREQ 'Y'                                       ULX012
     C           RRCHTP    ANDNE'D'                                       ULX012
     C           RRUTSI    ANDNE'X'                                       ULX012
     C                     ADD  RRGPER    ZZCPER
     C                     END
     C           $ERGU0    READEERGUARL0                 75
     C                     ENDDO                           EW1
      *
      **  Save total percentage to display in case of error
      *
     C                     MOVE ZZCPER    EDTPER  30
      *
     C                     ADD  WWWPER    ZZCPER
      *
      **  In case of amend substract current percentage
      *
     C           SFACTN    IFEQ 'A'
     C           EDTPER    SUB  SUBPER    EDTPER
     C                     MOVE EDTPER    ERRPER  30
     C                     SUB  SUBPER    ZZCPER
     C                     ELSE
     C                     MOVE EDTPER    ERRPER
     C                     ENDIF
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      ********************************************************************
      *                                                                  *
      * #U003    - SR WRITE OR AMEND RECORD ON GUARANTEE FILE            *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #WLOAD   - SR LOAD SFL                                           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #S004    - SR ENTER ON DETAILS SCREEN                            *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWWPER   - WORK FIELD VALIDATE PERCENTAGE                        *
      *                                                                  *
      ********************************************************************
     C           #U003     BEGSR
      *
     C                     Z-ADDWWWPER    RRGPER
      *
     C           ULX012    IFEQ 'N'                                       ULX012
     C           ULX012    OREQ 'Y'                                       ULX012
     C********** D3INSA    ANDNE'Y'                                                    ULX012 CER001
     C                     Z-ADD@@AMNT    RRGAMT
     C                     ENDIF                                          ULX012
      *
     C                     MOVEL@@CURR    RRGCCY
     C**********           Z-ADD@@GVBY    RRGVBY           * Given By                        CSD027A
     C                     MOVE @@GVBY    RRGVBY           * Given By                        CSD027A
     C                     Z-ADD@@STDT    RRSTDT           * Start Date
     C                     Z-ADD@@ENDT    RRENDT           * End Date
     C                     Z-ADD@@RVDT    RRRVDT           * Review Date
     C                     MOVELD3RVFQ    RRRVFQ
     C                     MOVE D3RVDY    RRRVDY
     C                     MOVELD3NARR    RRNARR
     C                     MOVELD3ACOM    RRACOM
     C                     MOVEL@@BRCA    RRBRCA           * Branch Code
     C           ULX012    IFEQ 'Y'                                       ULX012
     C           D3UTSI    ANDEQ'X'                                       ULX012
     C                     MOVELWWBRCA    RRBRCA                          ULX012
     C                     ENDIF                                          ULX012
     C                     MOVE D3GCOD    RRGCOD
     C                     MOVE D3UTSL    RRUTSL           * Used to Secure Loan
      *
     C           SFACTN    IFEQ 'I'                        B1
     C           *IN09     OREQ *ON
     C           *IN01     OREQ *ON
     C**********           Z-ADD@@CNUM    RRRCUS                                             CSD027A
     C                     MOVE @@CNUM    RRRCUS                                             CSD027A
     C                     MOVELD3UTSI    RRUTSI
     C                     MOVELD3UTSR    RRUTSR
     C                     MOVE @@GSEQ    RRGSEQ           * Guarantee Seq
     C                     MOVELD3SBYI    RRSBYI
     C                     MOVELD3SBYR    RRSBYR
     C                     MOVEL*BLANKS   RRRVEX
     C                     MOVEL*BLANKS   RRINSA                          ULX012
     C                     Z-ADDBJRDNB    RRLCD
     C                     MOVEL'I'       RRCHTP
     C                     WRITEERGUARD0
     C                     ELSE                            X1
     C                     MOVE BJRDNB    RRLCD
     C                     MOVEL'A'       RRCHTP
     C                     UPDATERGUARD3
     C                     ENDIF                           E1
      *
      **  If program called from the menu, commit the entry.
      *
     C           E1ACTN    IFEQ *BLANKS
     C                     COMIT
     C                     ENDIF
     C*
     C                     MOVE *ON       *IN60            * Comit requ'd
      *
     C                     MOVELDDCUST    SVCUST 10
     C                     Z-ADD*ZEROS    WWRRN
     C                     Z-ADD*ZEROS    SVRRN
     C                     MOVE *ON       *IN61
     C                     WRITEERL910C2
     C                     MOVE *OFF      *IN61
      *
     C           @@CNUM    SETLLERGUARL1                 72
     C                     EXSR #WLOAD
     C           WWRRN     IFEQ *ZEROS
     C                     MOVE *ON       *IN51
     C                     ELSE
     C                     MOVE *ON       *IN52
     C                     ENDIF
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #ACTN  - SCREEN 2 PROCESSING (Validate Action code)  *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * #AMEQ    - SR SETUP FIELD IN AMEND ENQUIRY OR DELETE MODE     *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S003    - SR ENTER ON SFL SCREEN                             *
      *                                                               *
      *****************************************************************
     C           #ACTN     BEGSR
      *
     C                     MOVE *OFF      *IN67
      *
      **  Setup key to access on ERGUARL2
      *
     C                     MOVELSFBRCA    KKBRCA           * Branch Code
     C***********          Z-ADD@@CNUM    KKRCUS           * Receiving C/m                   CSD027A
     C                     MOVE @@CNUM    KKRCUS           * Receiving C/m                   CSD027A
     C                     MOVELSFUTSI    KKUTSI           * U/T/S Ind.
     C                     MOVELSFUTSR    KKUTSR           * U/T/S Refer.
     C                     MOVE SFGSEQ    KKGSEQ           * Guarantee Seq
      *
     C           $ERGU2    CHAINERGUARL2             7477
      *
      **  If an error in chain (record is locked), send an error message
      *
     C           *IN77     IFNE *ZEROS                     *----------1
     C                     MOVEL'ERL0950' ZAMSID
     C                     MOVEL##USR     ZAMSDA
     C                     EXSR ZASNMS
     C                     Z-ADDCPFPOS    WWRRN
     C                     MOVE *ON       *IN52
      *
     C                     ELSE                            X----------1
     C                     Z-ADDRRGPER    SUBPER  30
      *
     C           SFRVEX    IFEQ 'E'                        *---------2
     C                     MOVE *ON       *IN65
     C                     MOVE *ON       *IN66
     C                     ELSE                            X---------2
     C                     MOVE *OFF      *IN65
     C                     MOVE *OFF      *IN66
     C                     ENDIF                           *---------2
      *
      **  Check action code selected & prepare screen
      *
     C                     SELEC                           S---------2
      *
     C           SFACTN    WHEQ 'A'
      *                                                                   ULX012
     C           SFINSA    IFNE 'Y'                                       ULX012
     C                     MOVE *OFF      *IN83                           ULX012
     C                     MOVE *OFF      *IN84                           ULX012
     C                     MOVE *OFF      *IN85                           ULX012
     C                     MOVE *OFF      *IN86                           ULX012
     C                     ELSE                                           ULX012
     C                     MOVE *ON       *IN84                           ULX012
     C                     MOVE *ON       *IN85                           ULX012
     C                     MOVE *ON       *IN83                           ULX012
     C                     MOVE *ON       *IN86                           ULX012
     C                     ENDIF                                          ULX012
      *                                                                   ULX012
     C           SFRVEX    IFEQ 'E'                        B1
     C                     MOVE *ON       *IN53
     C                     MOVE *ON       *IN80
     C                     MOVE *ON       *IN67
     C                     ELSE                            X1
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      ** 82       - Protect Secured By Ind. & Ref.
      ** 87       - Protect Guarantee Sequence
      *
     C                     MOVE *ON       *IN81
     C                     MOVE *ON       *IN82
     C                     MOVE *ON       *IN87
     C                     MOVE *ON       *IN53
     C                     EXSR #WPRO3
      *
     C           RRSBYI    IFEQ 'D'                        B1
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     ENDIF                           E1
      *
     C           RRSBYI    IFEQ 'G'                        B1
      *
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      *
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     ENDIF                           E1
     C           RRSBYI    IFEQ 'R'                        B1
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     ENDIF                           E1
      *
     C           RRSBYI    IFEQ 'P'                        B1
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     ENDIF                           E1
      *
     C           RRSBYI    IFEQ 'S'                        B1
     C                     SELEC
     C           RRSBYR    WHNE *BLANKS
     C                     MOVE 'S1'      WSWORK  2
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 86       - Protect Given By
      ** 88       - Protect Additional Comment
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN86
     C                     MOVE *ON       *IN88
      *
     C           RRSBYR    WHEQ *BLANKS
     C           RRGAMT    ANDEQ*ZEROS
     C                     MOVE 'S2'      WSWORK
      *
      ** 83       - Protect Guarantee Currency
      ** 85       - Protect Guarantee Amount
      ** 88       - Protect Additional Comment
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN85
     C                     MOVE *ON       *IN88
      *
     C           RRSBYR    WHEQ *BLANKS
     C           RRGAMT    ANDNE*ZEROS
     C                     MOVE 'S3'      WSWORK
      *
      ** 83       - Protect Guarantee Currency
      ** 84       - Protect Guarantee Percentage
      ** 88       - Protect Additional Comment
      *
     C                     MOVE *ON       *IN83
     C                     MOVE *ON       *IN84
     C                     MOVE *ON       *IN88
     C                     ENDSL
     C                     ENDIF                           E1
     C                     ENDIF                           E1
     C                     EXSR #AMEQ
      *
     C           SFACTN    WHEQ 'D'
     C                     MOVE *ON       *IN53
     C                     MOVE *ON       *IN80
     C                     MOVE *ON       *IN10
     C                     EXSR #AMEQ
      *
     C           SFACTN    WHEQ 'E'
     C                     MOVE *ON       *IN53
     C                     MOVE *ON       *IN80
     C                     EXSR #AMEQ
      *
     C           SFACTN    WHEQ ' '
     C                     MOVE *ON       *IN52
     C           WWRRN     CHAINERL910S2             71
     C                     UPDATERL910S2
      *
     C                     OTHER
     C                     MOVE *ON       *IN78
      *
     C                     ENDSL                           S---------2
      *                                                                   ULX012
      * No Display 'Generated Automatically' in insert mode               ULX012
      *                                                                   ULX012
     C           SFACTN    IFEQ 'I'                                       ULX012
     C           *IN09     OREQ *ON                                       ULX012
     C           SFINSA    ORNE 'Y'                                       ULX012
     C                     MOVE *OFF      *IN76                           ULX012
     C                     ELSE                                           ULX012
     C                     MOVE *ON       *IN76                           ULX012
     C                     ENDIF                                          ULX012
      *
     C                     ENDIF                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      ********************************************************************
      *                                                                  *
      * #DELT    - SR DELETE MODE                                        *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #WSETP   - SR SETUP WORK INDICATORS                              *
      * #WLOAD   - SR LOAD SFL                                           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - SR MAIN PROCESSING                                    *
      *                                                                  *
      ********************************************************************
     C           #DELT     BEGSR
      *
     C                     EXSR #WSETP
      *
     C                     Z-ADDBJRDNB    RRLCD
     C                     MOVEL'D'       RRCHTP
     C                     UPDATERGUARD3
      *
      **  If program called from the menu, commit the entry.
      *
     C           E1ACTN    IFEQ *BLANKS
     C                     COMIT
     C                     ENDIF
      *
     C                     MOVE *ON       *IN60            * Comit requ'd
      *
      **  Clear SFL
     C                     Z-ADD*ZEROS    WWRRN
     C                     Z-ADD*ZEROS    SVRRN
     C                     MOVE *ON       *IN61
     C                     WRITEERL910C2
     C                     MOVE *OFF      *IN61
      *
     C           @@CNUM    SETLLERGUARL1                 72
     C                     EXSR #WLOAD
      *
     C           WWRRN     IFEQ *ZEROS
     C                     MOVE *ON       *IN51
     C                     ELSE
     C                     MOVE *ON       *IN52
     C                     ENDIF
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #AMEQ    - SR SETUP FIELD IN AMEND ENQUIRY OR DELETE MODE        *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZEDIT    -                                                       *
      * AOCURRR0 - PGM ACCESS CURRENCY DETAILS                           *
      * ZDATE2   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #ACTN    - SR ACTION CODE ON SFL                                 *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWGVBY   - WORK FIELD ACCESS ON CUSTOMER DETAILS                 *
      *                                                                  *
      ********************************************************************
     C           #AMEQ     BEGSR
      *
      **  Setup Fields in Enquire, Delete & Amend mode
     C                     MOVELSVCSSN    D3CSSN    P
     C                     MOVELSVCRNM    D3CRNM    P
     C                     MOVELSVCRTN    D3CRTN    P
     C                     MOVELRRUTSI    D3UTSI           Used Sec. Ind.
     C                     MOVELRRUTSR    D3UTSR           Used Sec. Ref.
     C**********           MOVELRRINSA    D3INSA           Ins/Amd Ind                 ULX012 CER001
     C                     MOVELRRSBYI    D3SBYI           Secured Ind
     C                     MOVELRRSBYR    D3SBYR           Secured by
     C                     MOVELRRGCCY    D3GCCY           Guarantee CCY
      *
     C*******    RRGPER    IFNE *ZEROS                     B1             ULX004
     C                     MOVEL*BLANKS   ZFIELD
     C                     MOVE RRGPER    ZFIELD
     C                     Z-ADD*ZEROS    ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    D3GPER           Guarantee %
     C*******              ELSE                            X1             ULX004
     C*******              MOVE *BLANKS   D3GPER                          ULX004
     C*******              ENDIF                           E1             ULX004
      *
     C                     MOVELRRGCCY    D3GCCY           Guarantee Currency
      *
     C           RRGAMT    IFNE *ZEROS                     *----------1
     C           RRSBYI    IFEQ 'N'                        *---------2
      *                                                                   128241
     C                     Z-ADD0         WWNMDP                          128241
      *                                                                   128241
      **  Access to Security file with Secured By Reference               128241
      *                                                                   128241
     C           SBSESN    CHAINSECTY                71                   128241
     C           *IN71     IFEQ *OFF                       *--------3     128241
     C           RECI      ANDEQ'D'                                       128241
      *                                                                   128241
     C                     Z-ADDNMDP      WWNMDP                          128241
     C                     ENDIF                           *--------3     128241
      *                                                                   128241
     C*********************MOVE *ZEROS    ZADEC                           128241
     C                     Z-ADDWWNMDP    ZADEC                           128241
      *                                                                   128241
     C                     ELSE                            X---------2
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY    '@OPTN
     C                     PARM D3GCCY    @KEY1
     C           SDCURR    PARM SDCURR    DSFDY
     C                     Z-ADDA6NBDP    ZADEC
     C                     ENDIF                           *---------2
      *
     C                     MOVEL*BLANKS   ZFIELD
     C                     MOVE RRGAMT    ZFIELD
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    D3GAMT           Guarantee Amount
     C                     ELSE                            X----------1
     C                     MOVEL*BLANKS   D3GAMT
     C                     ENDIF                           *----------1
      *
     C********** RRGVBY    IFNE *ZEROS                     B1                                CSD027A
     C           RRGVBY    IFNE *BLANKS                    B1                                CSD027A
     C********** RRSBYI    ANDNE'D'
     C********** RRSBYI    ANDNE'G'
     C********** RRSBYI    ANDNE'R'
     C                     MOVE RRGVBY    WWGVBY
     C           WWGVBY    CHAINSDCUSTL5             71
     C                     MOVELBBCSSN    D3GVBY           Given by
     C                     ELSE                            X1
     C                     MOVEL*BLANKS   D3GVBY
     C                     ENDIF                           E1
      *
     C                     MOVE RRGCOD    D3GCOD           Guarantee Code
     C                     MOVE RRGSEQ    D3GSEQ           Guarantee Seq.
      *
     C           RRSTDT    IFNE *ZEROS                     B1
     C                     Z-ADDRRSTDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     D3STDT           Start Date
     C                     ELSE                            X1
     C                     MOVEL*BLANKS   D3STDT
     C                     ENDIF                           E1
      *
     C           RRENDT    IFNE *ZEROS                     B1
     C                     Z-ADDRRENDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     D3ENDT           End Date
     C                     ELSE                            X1
     C                     MOVEL*BLANKS   D3ENDT
     C                     ENDIF                           E1
      *
     C           RRRVDT    IFNE *ZEROS                     B1
     C                     Z-ADDRRRVDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     D3RVDT           Review Date
     C                     ELSE                            X1
     C                     MOVEL*BLANKS   D3RVDT
     C                     ENDIF                           E1
      *
     C                     MOVELRRRVFQ    D3RVFQ           Review Freq.
      *
     C           RRRVDY    IFNE *ZEROS                     B1
     C                     MOVE RRRVDY    D3RVDY           Review Day No
     C                     ELSE                            X1
     C                     MOVEL*BLANKS   D3RVDY
     C                     ENDIF                           E1
      *
     C                     MOVE RRUTSL    D3UTSL           Used to secure Loan
      *
     C                     MOVELRRNARR    D3NARR           Narrative
     C                     MOVELRRACOM    D3ACOM           Additional Comm.
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #V002  - VALIDATE ACTION CODE                        *
      *                                                               *
      *===============================================================*
      *                                                               *
      * #V002    - SR AUTHORISATION ACTION CODE FOR USER              *
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * ZVACTU   - PGM CHECK USER ACTION CODE FOR MONOBRANCHING       *
      * ZVACTBU  - PGM CHECK USER ACTION CODE FOR MULTIBRANCHING      *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                        *
      * #S003    - SR ENTER ON SFL SCREEN                             *
      *                                                               *
      *****************************************************************
     C           #V002     BEGSR
      *
      **  When program called from a Window, check if there is a conflict
      **    in Action Code selected compared to Action Code parameter
      *
     C                     Z-ADD0         @ERR    10
     C           E1ACTN    IFEQ 'E'                        *----------1
     C           @ZACTN    IFEQ 'I'                        *---------2
     C           @ZACTN    OREQ 'A'
     C           @ZACTN    OREQ 'D'
     C                     ADD  1         @ERR
     C*********************MOVEL'ERN0905' ZAMSID                          147610
     C                     MOVEL'ERN0933' ZAMSID                          147610
     C                     MOVEL*BLANKS   ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *---------2
     C                     ENDIF                           *----------1
      *
      **  User should be authorised to Action Code
      **  When Single Branch
      *
     C           @ERR      IFEQ *ZEROS                     *----------1
     C           @ZACTN    ANDNE'E'
      *
     C           BJSBRC    IFNE *BLANKS                    *---------2
      *
     C                     CALL 'ZVACTU'
     C                     PARM           @ZACTN  1
     C                     PARM           @ERR    10
      *
     C                     ELSE                            X---------2
      *
      **  When Multi-Branch
      *
     C                     CALL 'ZVACTBU'
     C                     PARM           @ZACTN  1
     C                     PARM USRBCH    @ZBR    3
     C                     PARM           @ERR    10
      *
     C                     ENDIF                           *---------2
      *
      **  If an error, send an error message
      *
     C           @ERR      IFNE *ZEROS                     *---------2
     C                     MOVEL'ERN0905' ZAMSID
     C                     MOVEL##USR     ZAMSDA
     C                     EXSR ZASNMS
     C                     ENDIF                           *---------2
     C                     ENDIF                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #WSETP - SET UP WORK INDICATORS                      *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      * #REFR    - SR REFRESH SCREEN                                  *
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                        *
      * #ROLL    - SR ROLLUP                                          *
      * #S003    - SR ENTER ON SFL SCREEN                             *
      * #PRV3    - SR PREVIOUS SCREEN (FROM SCREEN                    *
      * #S004    - SR ENTER ON DETAILS SCREEN                         *
      * #DELT    - SR DELETE MODE                                     *
      *                                                               *
      *****************************************************************
     C           #WSETP    BEGSR
      *
     C                     MOVEL' '       @ZACTN
     C                     MOVE *OFF      *IN30
     C                     MOVE *OFF      *IN31
     C                     MOVE *OFF      *IN32
     C                     MOVE *OFF      *IN33
     C                     MOVE *OFF      *IN34
     C                     MOVE *OFF      *IN35
     C                     MOVE *OFF      *IN36
     C                     MOVE *OFF      *IN37
     C                     MOVE *OFF      *IN38
     C                     MOVE *OFF      *IN39
     C                     MOVE *OFF      *IN40
     C                     MOVE *OFF      *IN41
     C                     MOVE *OFF      *IN42
     C                     MOVE *OFF      *IN43
     C                     MOVE *OFF      *IN44
     C                     MOVE *OFF      *IN45
     C                     MOVE *OFF      *IN46
     C                     MOVE *OFF      *IN47
     C                     MOVE *OFF      *IN51
     C                     MOVE *OFF      *IN52
     C                     MOVE *OFF      *IN53
     C                     MOVE *OFF      *IN54
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN78
     C                     MOVE *OFF      *IN80
     C                     MOVE *OFF      *IN81
     C                     MOVE *OFF      *IN82
     C                     MOVE *OFF      *IN83
     C                     MOVE *OFF      *IN84
     C                     MOVE *OFF      *IN85
     C                     MOVE *OFF      *IN86
     C                     MOVE *OFF      *IN87
     C                     MOVE *OFF      *IN88
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #WSCR3 - SET UP FIELDS FOR SCREEN 3 DISPLAY          *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                        *
      *                                                               *
      *****************************************************************
     C           #WSCR3    BEGSR
      *
      **  Setup Fields in Insert Mode (Initialise from parameter data)
      *
     C                     MOVELSVCSSN    D3CSSN    P      *
     C                     MOVELSVCRNM    D3CRNM    P      *
     C                     MOVELSVCRTN    D3CRTN    P      *
     C                     MOVELE1UTSI    D3UTSI           * U/T/S Ind.
     C                     MOVELE1UTSR    D3UTSR           * U/T/S Refer.
     C                     MOVELE1UTSC    @@UTSC           * U/T/S Ccy
     C                     MOVELE1SBYI    D3SBYI           * Sec.By Ind.
     C                     MOVELE1SBYR    D3SBYR           * Sec.By Refer.
     C                     MOVELE1GCCY    D3GCCY           * Guarantee Ccy
     C                     MOVEL*BLANKS   D3GPER           *
     C                     MOVELE1GAMT    @@UTSA           * U/T/S Amount
     C                     MOVEL*BLANKS   D3GAMT           * Guarantee Amt
     C           E1ACTN    IFEQ *BLANKS
     C           E1GVBY    OREQ *ZEROS
     C                     MOVE *BLANKS   D3GVBY
     C                     ELSE
     C                     MOVELE1GVBY    D3GVBY           * Given By No.
     C                     ENDIF
     C                     MOVEL*BLANKS   D3GCOD           *
     C                     MOVEL*BLANKS   D3GSEQ           *
     C           E1ACTN    IFEQ *BLANKS
     C           E1STDT    OREQ *ZEROS
     C                     MOVE *BLANKS   D3STDT
     C                     ELSE
     C                     MOVELE1STDT    D3STDT           * Start Date
     C                     ENDIF
     C           E1ACTN    IFEQ *BLANKS
     C           E1ENDT    OREQ *ZEROS
     C                     MOVE *BLANKS   D3ENDT
     C                     ELSE
     C                     MOVELE1ENDT    D3ENDT           * End Date
     C                     ENDIF
     C           E1ACTN    IFEQ *BLANKS
     C           E1RVDT    OREQ *ZEROS
     C                     MOVE *BLANKS   D3RVDT
     C                     ELSE
     C                     MOVELE1RVDT    D3RVDT           * Review Date
     C                     ENDIF
     C                     MOVELE1RVFQ    D3RVFQ           * Review Freq.
     C           E1ACTN    IFEQ *BLANKS
     C           E1RVDT    OREQ *ZEROS
     C                     MOVE *BLANKS   D3RVDY
     C                     ELSE
     C                     MOVELE1RVDY    D3RVDY           * Review Day
     C                     ENDIF
     C                     MOVEL*BLANKS   D3NARR           *
     C                     MOVEL*BLANKS   D3ACOM           *
     C                     MOVELE1BRCA    @@BRCA           * Branch Code
      *
      * Used to Secure Loans
      *
     C           D3UTSI    IFNE *BLANKS
     C                     MOVE 'N'       D3UTSL
     C                     ENDIF
      *                                                                   ULX012
     C********** ULX012    IFEQ 'Y'                                                    ULX012 CER001
     C**********           MOVE ' '       D3INSA                                       ULX012 CER001
     C**********           ENDIF                                                       ULX012 CER001
      *
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #WPRO3 - PROTECT FIELDS FOR SCREEN 3 DISPLAY         *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                        *
      * #ACTN    - SR ACTION CODE ON SFL                              *
      * #S004    - SR ENTER ON DETAILS SCREEN                         *
      *                                                               *
      *****************************************************************
     C           #WPRO3    BEGSR
      *
      **  Setup protection of fields for Window program call
      *
     C           E1ACTN    IFNE *BLANKS                    *----------1
      *
      **  Setup protection for Securities Maintenance (SE0040W6)
      **  Setup protection for Facilities Maintenance (LE0020W6)
      **  Setup protection for Customer Loans Maintenance (LE0032W6)
      *
     C           E1UTSI    IFEQ 'S'                        *---------2
     C           E1UTSI    OREQ 'F'
     C           E1UTSI    OREQ 'L'
      *
      ** 81       - Protect Used To Secure Ind.& Ref.
      *
     C                     MOVE *ON       *IN81
     C                     ENDIF                           *---------2
      *
      **  Setup protection for Loans/Deposits Maintenance (MM0089W6)
      *
     C           E1SBYI    IFEQ 'D'                        *---------2
      *
      ** 82       - Protect Secured By Ind.& Ref.
      *
     C                     MOVE *ON       *IN82
     C                     ENDIF                           *---------2
     C                     ENDIF                           *----------1
      *                                                                   ULX012
      * No Display 'Generated Automatically' in insert mode               ULX012
      *                                                                   ULX012
     C           E1ACTN    IFEQ 'I'                                       ULX012
     C           SFACTN    OREQ 'I'                                       ULX012
     C           @ZACTN    OREQ 'I'                                       ULX012
     C           *IN09     OREQ *ON                                       ULX012
     C           SFINSA    ORNE 'Y'                                       CAD029
     C                     MOVE *OFF      *IN76                           ULX012
     C                     ELSE                                           ULX012
     C                     MOVE *ON       *IN76                           ULX012
     C                     ENDIF                                          ULX012
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      *===============================================================*
      *                                                               *
      *          #WLOAD - LOAD SUBFILE OF GUARANTEE RECORDS           *
      *                                                               *
      *===============================================================*
      *                                                               *
      * SUBROUTINE CALLS:                                             *
      * ZALIGN   -                                                    *
      * AOCURRR0 - PGM ACCESS CURRENCY DETAILS                        *
      * ZEDIT    -                                                    *
      *                                                               *
      * SUBROUTINE CALLED FROM:                                       *
      * #S001    - SR ENTER ON FIRST SCREEN                           *
      * #REFR    - SR REFRESH SCREEN                                  *
      * #ROLL    - SR ROLLUP                                          *
      * #U003    - SR WRITE OR AMEND RECORD ON GUARANTEE FILE         *
      * #DELT    - SR DELETE MODE                                     *
      *                                                               *
      *****************************************************************
     C           #WLOAD    BEGSR
      *
     C                     READ ERGUARL1                 72
      *
     C                     Z-ADDSVRRN     WWRRN
     C                     Z-ADD0         WWLINE  20
      *
      **  Build SFL with same customer & Change Type is not 'D' as deleted
      *
     C           *IN72     DOWEQ*OFF                       *----------1
     C           WWLINE    ANDLT13
     C           @@CNUM    ANDEQRRRCUS
      *
     C           RRCHTP    IFNE 'D'                        *---------2
      *
     C                     MOVEL*BLANKS   SFACTN
     C                     MOVELRRUTSI    SFUTSI           Used to Sec. Ind.
     C                     MOVELRRUTSR    SFUTSR           Used to Sec. Ref.
     C                     MOVELRRINSA    SFINSA           Ins/Amd Ind    ULX012
      *
     C           RRRVEX    IFEQ 'E'                        *--------3
     C                     MOVE '1'       *IN66
     C                     ELSE
     C                     MOVE '0'       *IN66
     C                     END                             *--------3
      *
     C                     MOVE RRGSEQ    SFGSEQ           Guarantee Seq.
     C                     MOVELRRSBYI    SFSBYI           Secured by Ind.
     C                     MOVELRRSBYR    SFSBYR           Secured by Ref.
     C                     MOVELRRGCCY    SFGCCY           Guarantee CCY
      *
     C                     MOVELRRBRCA    SFBRCA           Branch code  (Hidden)
     C                     MOVELRRRVEX    SFRVEX           Expiry Ind ? (Hidden)
      *
     C*******    RRGPER    IFNE *ZEROS                     *--------3     ULX004
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE RRGPER    ZFIELD
     C                     Z-ADD*ZEROS    ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SFGPER           Guarantee %
     C*******              ELSE                            X--------3     ULX004
     C*******              MOVE *BLANKS   SFGPER                          ULX004
     C*******              ENDIF                           *--------3     ULX004
      *
     C           RRGAMT    IFNE *ZEROS                     *--------3
     C           RRSBYI    IFEQ 'N'                        *-------4
      *                                                                   128241
     C                     Z-ADD0         WWNMDP                          128241
      *                                                                   128241
      **  Access to Security file with Secured By Reference               128241
      *                                                                   128241
     C***********SBSESN    CHAINSECTY                71             128241ULX004
     C           RRSESN    CHAINSECTY                71                   ULX004
     C           *IN71     IFEQ *OFF                       *--------3     128241
     C           RECI      ANDEQ'D'                                       128241
      *                                                                   128241
     C                     Z-ADDNMDP      WWNMDP                          128241
     C                     ENDIF                           *--------3     128241
      *                                                                   128241
     C*********************MOVE *ZEROS    ZADEC                           128241
     C                     Z-ADDWWNMDP    ZADEC                           128241
      *                                                                   128241
     C                     ELSE                            X-------4
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   @RTCD
     C                     PARM '*KEY    '@OPTN
     C                     PARM RRGCCY    @KEY1
     C           SDCURR    PARM SDCURR    DSFDY
     C                     Z-ADDA6NBDP    ZADEC
     C                     ENDIF                           *-------4
     C                     MOVEL*BLANKS   ZFIELD
     C                     MOVE RRGAMT    ZFIELD
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SFGAMT           Guarantee Amount
     C                     ELSE                            X--------3
     C                     MOVEL*BLANKS   SFGAMT
     C                     ENDIF                           *--------3
      *
     C********** RRGVBY    IFNE *ZEROS                     *--------3                        CSD027A
     C           RRGVBY    IFNE *BLANKS                    *--------3                        CSD027A
     C********** RRSBYI    ANDNE'D'
     C********** RRSBYI    ANDNE'G'
     C********** RRSBYI    ANDNE'R'
     C                     MOVE RRGVBY    WWGVBY  6
     C           WWGVBY    CHAINSDCUSTL5             71
     C                     MOVELBBCSSN    SFGVBY           Given by
     C                     ELSE                            X--------3
     C                     MOVEL*BLANKS   SFGVBY           Given by
     C                     ENDIF                           *--------3
      *
     C                     ADD  1         WWRRN
     C                     ADD  1         WWLINE
     C                     WRITEERL910S2
     C                     ENDIF                           *---------2
      *
     C                     MOVE '0'       *IN66
      *
     C           WWLINE    IFLT 13                         *---------2
     C                     READ ERGUARL1                 72
     C                     ENDIF                           *---------2
      *
     C                     ENDDO                           *----------1
      *
     C                     Z-ADDWWRRN     SVRRN
      *
      **  Display 'More...' or 'Bottom'
      *
     C           *IN72     IFEQ *OFF                       *----------1
     C           @@CNUM    ANDEQRRRCUS
     C                     MOVE *OFF      *IN79
     C                     ELSE                            X----------1
     C                     MOVE *ON       *IN79
     C                     ENDIF                           *----------1
      *
     C                     ENDSR
      *===============================================================*
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - SR DATA BASE ERROR                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    - SR INITIAL ROUTINE                                    *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     END
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *--------------------------------------------------------------------
      /COPY ZSRSRC,ZDATE1Z2
      /COPY ZSRSRC,ZDATE2Z2
      /COPY ZSRSRC,ZEDITZ2
      /COPY ZSRSRC,ZALIGNZ2
      /COPY ZSRSRC,ZFRQAZ2
      /COPY ZSRSRC,ZCHKH
      /COPY ZSRSRC,ZACCH
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * ZASNMS   - SR SEND ERROR MESSAGE                                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * Y2SNMGC  - PGM SEND ERROR MESSAGE                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #S001    - SR ENTER ON FIRST SCREEN                              *
      * #ADDM    - SR F9 PRESSED (INSERT MODE)                           *
      * #S003    - SR ENTER ON SFL SCREEN                                *
      * #S004    - SR ENTER ON DETAILS SCREEN                            *
      *                                                                  *
      ********************************************************************
     C           ZASNMS    BEGSR
      *
      **  Message file specified is PMSGF
     C                     MOVELPMSGF     ZAMSGF
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
      **  Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C                     MOVEL*BLANK    ZAMSGF           Message file
     C                     MOVEL*BLANK    ZAMSID           Message id.
      *
     C           ZAEXIT    ENDSR
      ********************************************************************
**  ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**  ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**  ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   ZFMD  -  NUMBER OF DAYS IN THE MONTH
312831303130313130313031
**  GUA - Guarantee Codes
011011121314151620303132414243515217                                      ULX004
**  GCSP - Guarantee Codes for specific provisions
01
**  GCGL - Guarantee Codes for general ledger
113031324142435152
**  GCRT - Guarantee Codes for retail account
113031324142435152
**  GCMM - Guarantee Codes for MM deposit
113031324142435152
**  GCCP - Guarantee Codes for customer position
1314303132414243515217                                                    ULX004
**  GCNO - Guarantee Codes for nominal
1314303132414243515217                                                    ULX004
**  GCAM - Guarantee Codes for amount
10121516203031324142435152
**  GCPR - Guarantee Codes for percentage
10121516203031324142435152
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
