     H        1   Y                       F
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER BCL Reporting - Interest to Accrue')          *
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ERLU116 - Interest to accrue records generation              *
      *                                                               *
      *  Function:  This program generates records type 1 for the     *
      *             interest to accrue with a BCL code 99000000       *
      *                                                               *
      *  (phase(s)) COB                                               *
      *                                                               *
      *  Called By: ERCLU116                                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 225555             Date 01Nov06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 12May06               *
      *                 CER001             Date 25Apr05               *
      *                 213111             Date 11Dec02               *
      *                 ULX047             Date 28Jan03               *
      *                 UPG402             Date 28Jan03               *
      *                 ULX046             Date 31Oct02               *
      *                 197413             Date 19Dec00               *
      *                 187689             Date 19Dec00               *
      *                 182312             Date 30OCT00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (recompiled)                                        *
      *  225555 - Add discount/premium amount, amount to amortize     *
      *           and 'Prix d'Acquisition'. Recompile only over       *
      *           changed ERLUTRPD, EREXTRPD.                         *
      *  CSD027A - Conversion of cust. no. to alpha (post build 103)  *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  213111 - Country of citizenship incorrectly managed          *
      *  ULX047 - CSSF Market Values Calculation                      *
      *  UPG402 - Recompile due to R4.02 upgrade                      *
      *  ULX046 - Circulars 2002/170-174-175 Integration              *
      *           Recompiled due to changes in file layout            *
      *  197413 - SETUP FIELD EXRECT                                  *
      *  187689 - "GRD" currency in EMU                               *
      *  182312 - Interest to accrue incorrectly managed              *
      *                                                               *
      *****************************************************************
     F***ERLUTRL9IP**E           K        DISK                                                CER001
     FERLTRXL5IP  E           K        DISK                                                   CER001
     F            ERLUTRF0                          KRENAMEERLUTRF9
      * EX - ER Extract File
      *      Rcd fmt : ERLUTRF9
      *
     F***EREXTRL5IF**E           K        DISK                                                CER001
     FEREXTXL5IF  E           K        DISK                                                   CER001
      * EX - ER Extract File
      *      Rcd fmt : EREXTRF5
      *
     F***ERBANKPDIF**E                    DISK                                                CER001
     FERBNKXPDIF  E                    DISK                                                   CER001
      * BJ - SD I.C.D.
      *
     F***ERICDRL1IF**E           K        DISK                                                CER001
     FERICDXL1IF  E           K        DISK                                                   CER001
      * VD - ER I.C.D.
      *
     F***ERCURRL1IF  E           K        DISK                            187689
     F*** VR - Currency Codes table                                       187689
     FSDCURRL1IF  E           K        DISK                               187689
     F* A6 - Currency Codes table                                         187689
      *
      **             ** ------------------- **
     F***ERLUTRPDO***E                    DISK                                                CER001
     FERLTRXPDO   E                    DISK                                                   CER001
      * EX - ER LU 'enriched' Extract File
      *      Rcd fmt : ERLUTRF0
      *
     FERL116AUO   E                    PRINTER
      *===============================================================*
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       01         EXTRACT FILE RECORD ID INDICATOR
     F*       80         FILE ACCESS OPERATION FAIL
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F**             *************************
     F**             ** INDICATORS NOT USED **
     F**             ** ------------------- **
     F**             *************************
     F**
     F**      ***************************************************
     F**      * ..   02   03   04   05   06   07   08   09   10 *
     F**      * 11   12   13   14   15   16   17   18   19   20 *
     F**      * 21   22   23   24   25   26   27   28   29   30 *
     F**      * 31   32   33   34   35   36   37   38   39   40 *
     F**      * 41   42   43   44   45   46   47   48   49   50 *
     F**      * 51   52   53   54   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   70 *
     F**      * 71   72   73   74   75   76   77   78   79   .. *
     F**      * 81   82   83   84   85   86   87   88   89   90 *
     F**      * 91   92   93   94   95   96   97   98   99      *
     F**      ***************************************************
     E*
     E** Array for Copyright
     E                    CPY@    1   1 80
      *===============================================================*
      *
     IERLUTRF9    01
     I                                              EXORIGL1
     I                                              EXDRCRL1
     IEREXTRF0                                                            213111
     I              EXCNCZ                          XXCNCZ                213111
     I*
     I** Local Data Area for Database error details
     I*
     ILDA        UDS                            256
     I                                      132 183 DBLOT
     I                                      132 139 DBFILE
     I                                      142 170 DBKEY
     I                                      142 156 DBK1
     I                                      158 163 DBK2
     I                                      165 170 DBK3
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
     I*
     I** Data Structure for Origine of Data
     I*
     IEXORIG      DS
     I                                        1   4 WWRIND
     I**********                              6  20 WWTREF                                    CER001
     I                                        6  26 WWTREF                                    CER001
     I*
     I** Data Structure for Compilation - Copyright Insertion
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      /EJECT
      *================================================================
      *
     C           *INL1     IFEQ '1'                        - B1
     C                     EXSR #SQBK
     C                     END                             - E1
      *
     CLR                   Z-ADDLASTSQ    WWSEQN
      *
      /EJECT
     C*================================================================
     C           #SQBK     BEGSR
      *-----------------------------------------------------------
      ***Access*EREXTRL5*to*retrive*the*interest*to*accrue*(EXITAC)                           CER001
      ** Access EREXTXL5 to retrive the interest to accrue (EXITAC)                           CER001
      *
     C                     MOVE WWRIND    KRIND
     C                     MOVE WWTREF    KTREF
     C                     Z-ADDEXDRCR    KDRCR
     C********** KYEXTR    CHAINEREXTRL5             80                                       CER001
     C           KYEXTR    CHAINEREXTXL5             80                                       CER001
      *
     C           *IN80     IFEQ '0'                        - B1
      *
      ** Currency sequence break:
     C***********EXCCYC    CHAINERCURRL1             80                   187689
     C           EXCCYC    CHAINSDCURRL1             80                   187689
     C           *IN80     IFEQ '1'                        - B2
     C           *LOCK     IN   LDA
     C                     MOVEL'606'     DBASE
     C***********          MOVEL'ERCURRL1'DBFILE                          187689
     C                     MOVEL'SDCURRL1'DBFILE                          187689
     C                     MOVELEXCCYC    DBKEY
     C                     EXSR #DBERR
     C                     ELSE                            - X2
     C***********          Z-ADDVRSPRT    @@RAT1 138                      187689
     C***********          MOVE VRMDIN    @@MDI1  1                       187689
     C***********          MOVE VRNBDP    @@NBD1  10                      187689
     C                     Z-ADDA6SPRT    @@RAT1 138                      187689
     C                     MOVE A6MDIN    @@MDI1  1                       187689
     C                     MOVE A6NBDP    @@NBD1  10                      187689
     C                     END                             - E2
      *
      ** If the interest to accrue is not nill, initialise fields and
      ** write a record on ERLUTRPD
      *
     C           EXITAC    IFNE 0                          - B2
      *
     C                     Z-ADDEXITAC    INAMNT 160
     C                     EXSR CALCON
      *
      ** Set up book values
      *
     C                     Z-ADDOUTAMT    EXLAMT
     C                     Z-ADDEXITAC    EXAMNT
      *
     C                     Z-ADD9         EXDTCD
     C                     Z-ADD99000000  EXSCHA
      *
     C                     Z-ADD0         EXNOMA
     C                     Z-ADD0         EXLNOM
     C                     Z-ADD0         EXMARA
     C                     Z-ADD0         EXLMAR
     C                     Z-ADD0         EXREAA
     C                     Z-ADD0         EXREAL
     C                     Z-ADD0         EXNETA
     C                     Z-ADD0         EXNETL
     C                     Z-ADD0         EXCORA
     C                     Z-ADD0         EXCORL
     C                     Z-ADD0         EXLSEC
     C                     Z-ADD0         EXSECA
     C                     Z-ADD0         EXNGAL
     C                     Z-ADD0         EXNGAM
     C                     Z-ADD0         EXLSAM
     C                     Z-ADD0         EXOPAM
     C                     Z-ADD0         EXLPAM
     C                     Z-ADD0         EXTOTI
     C                     Z-ADD0         EXLTOT
     C                     Z-ADD0         EXNPVA
     C                     Z-ADD0         EXLNPV
     C                     Z-ADD0         EXUNPA
     C                     Z-ADD0         EXLSTA
     C                     Z-ADD0         EXOFOP
     C                     Z-ADD0         EXOFGA
     C                     Z-ADD0         EXOFVE
     C                     Z-ADD0         EXTP10
     C                     MOVE '0'       EXTRIN                         ULX047
     C                     MOVE *BLANKS   EXCTRF                         ULX047
     C                     Z-ADD1         EXRECT                         197413
     C                     MOVEL'INTR'    EXORIG
      *
     C                     ADD  1         LASTSQ
     C                     Z-ADDLASTSQ    EXSEQN
      *
     C                     WRITEERLUTRF0
      *
     C                     ENDIF                           - E2
      *
     C                     ENDIF                           - E1
      *
     C           #SQBKE    ENDSR
      *================================================================
      **
      **   CALCON     Convert Amount to Report CCY
      **
      **   INPUT  : INAMNT 160
      **   OUTPUT : OUTAMT 160
      **
      *-----------------------------------------------------------
     C           CALCON    BEGSR
      *-----------------------------------------------------------
     C                     Z-ADD0         OUTAMT 160
      **
     C           @@MDI1    IFEQ 'M'
     C           @@MDI2    ANDEQ'D'
     C           @@RAT1    MULT @@RAT2    WWRAT  138
     C           INAMNT    MULT WWRAT     OUTAMT    H
     C                     END
      **
     C           @@MDI1    IFEQ 'M'
     C           @@MDI2    ANDEQ'M'
     C           INAMNT    MULT @@RAT1    OUTAMT
     C           OUTAMT    DIV  @@RAT2    OUTAMT    H
     C                     END
      **
     C           @@MDI1    IFEQ 'D'
     C           @@MDI2    ANDEQ'D'
     C           INAMNT    MULT @@RAT2    OUTAMT
     C           OUTAMT    DIV  @@RAT1    OUTAMT    H
     C                     END
      **
     C           @@MDI1    IFEQ 'D'
     C           @@MDI2    ANDEQ'M'
     C           @@RAT1    MULT @@RAT2    WWRAT
     C           INAMNT    DIV  WWRAT     OUTAMT    H
     C                     END
      **
     C           OUTAMT    DIV  10        OUTAMT    H
     C           OUTAMT    MULT 10        OUTAMT    H
      **
     C                     ENDSR
      **=================================================================
      **
      **  *INZSR      Program initialisation sub-routine
      **
      *-----------------------------------------------------------
     C           *INZSR    BEGSR
      *-----------------------------------------------------------
      *
     C           *NAMVAR   DEFN           LDA
     C                     IN   LDA
      *
     C           *ENTRY    PLIST
     C           WWSEQN    PARM           LASTSQ  60       Output Parm
     C           *LIKE     DEFN LASTSQ    WWSEQN
      *                                                    (last seq num)
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'ERLU116' DBPGM
     C                     OUT  LDA
     C                     MOVE '0'       *INU7
      *
      **  Access SD ICD
     C********** 1         SETLLERBANKPD                                                      CER001
     C**********           READ ERBANKPD                 80                                   CER001
     C           1         SETLLERBNKXPD                                                      CER001
     C                     READ ERBNKXPD                 80                                   CER001
     C           *IN80     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA
     C                     MOVEL'603'     DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'BANK'    DBKEY
     C                     EXSR #DBERR
     C                     END                             - E1
      *
      **  Access ER ICD to retrieve local Ccy & Xrate v/s Base ccy :
     C********** 'LU'      CHAINERICDRL1             80                                       CER001
     C           'LU'      CHAINERICDXL1             80                                       CER001
     C           *IN80     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA
     C                     MOVEL'604'     DBASE
     C**********           MOVEL'ERICDRL1'DBFILE                                              CER001
     C                     MOVEL'ERICDXL1'DBFILE                                              CER001
     C                     MOVEL'LU'      DBKEY
     C                     EXSR #DBERR
     C                     ELSE                            - X1
     C***********VDCCYC    CHAINERCURRL1             80                   187689
     C           VDCCYC    CHAINSDCURRL1             80                   187689
     C           *IN80     IFEQ '1'                        - B2
     C           *LOCK     IN   LDA
     C                     MOVEL'605'     DBASE
     C***********          MOVEL'ERCURRL1'DBFILE                          187689
     C                     MOVEL'SDCURRL1'DBFILE                          187689
     C                     MOVELVDCCYC    DBKEY
     C                     EXSR #DBERR
     C                     ELSE                            - X2
     C***********          Z-ADDVRSPRT    @@RAT2 138                      187689
     C***********          MOVE VRMDIN    @@MDI2  1                       187689
     C***********          MOVE VRNBDP    @@NBD2  10                      187689
     C                     Z-ADDA6SPRT    @@RAT2 138                      187689
     C                     MOVE A6MDIN    @@MDI2  1                       187689
     C                     MOVE A6NBDP    @@NBD2  10                      187689
     C                     END                             - E2
     C                     END                             - E1
      *
     C           KYEXTR    KLIST
     C                     KFLD           KRIND   4
     C**********           KFLD           KTREF  15                                           CER001
     C                     KFLD           KTREF  21                                           CER001
     C                     KFLD           KDRCR   10
      *
     C                     ENDSR
      *================================================================
      **
      **  #DBERR      Monitored error
      **
      *-----------------------------------------------------------
     C           #DBERR    BEGSR
      *-----------------------------------------------------------
      **
     C           *LOCK     IN   LDA
     C                     OUT  LDA
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     EXSR #AUDIT
     C                     DUMP
     C                     RETRN
     C**
     C                     ENDSR
      *================================================================
      **
      **  #AUDIT      Audit List
      **
      *-----------------------------------------------------------
     C           #AUDIT    BEGSR
      *-----------------------------------------------------------
      *
     C                     WRITEHEADAU
      *
     C           *INU7     IFEQ '1'
     C                     WRITEDBERROR
     C                     END
      *
     C                     Z-ADDEXSEQN    LASTSQ
     C**
     C                     ENDSR
      *================================================================
      *
      ** Array File Trans. change to ERLTRXPD instead of ERLUTRPD
      *
** FILE TRANS. ERLUTRPD FILE TO REMOVE ALL NON STANDARD CHARACTERS
*EQUATE ERLTRXPD
*EQUATE C144504EC551C552C554C956C164C368406A406FC571C572C5744079407A407D407E407F
*EQUATE C181C282C383C484C585C686C787C888C989D191D292D393D494D595D696D797D898D999
*EQUATE E2A2E3A3E4A4E5A5E6A6E7A7E8A8E9A940E0E4FE
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
