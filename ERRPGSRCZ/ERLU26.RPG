     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER - take Date for CAD Report downloading')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ERLU26 - Take Date for CAD Report downloading                *
      *                                                               *
      *  Function:  This program is used to select a date for the     *
      *             files transfer from ERLUCAPD onto PCfolder        *
      *  (phase(s))                                                   *
      *                                                               *
      *  Called By: ERCLU26 - CAB - Manual Download                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX040  *CREATE    Date 20Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX040 - FIRE - RT30 Generation in Midas                     *
      *                                                               *
      *****************************************************************
     FERLU26DFCF  E                    WORKSTN      KINFSR *PSSR
      ***  Screen file
      *
      /EJECT
      /COPY ZSRSRC,ZDATE1Z1
      *
      ***  Array containing Copyright statement
     E                    CPY@    1   1 80
      *
      /EJECT
     ILDA       E DSLDA                         256
      *
      ***  Data Area giving Installation Control Details
     IRUNDAT    E DSRUNDAT
      *
      *** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM ##PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     I            DS
     I                                        1   6 SDATE
     I                                        1   2 WWDAY
     I                                        5   6 WWYEAR
      *
      /EJECT
      *
      *** Entry PList
      *
     C           *ENTRY    PLIST
     C                     PARM           PDATE   6
     C                     PARM           RETCOD  1
      *
     C                     MOVE *BLANKS   PDATE
     C                     MOVE ' '       RETCOD
      *
      ***  Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
     C**********           MOVEL'ERUSRMSG'PMSGF  10                                           CER001
     C                     MOVEL'STUSRMSG'PMSGF  10                                           CER001
      *
      ***  Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
     C                     MOVELUSER      SUSER     P
     C                     MOVELWSID      SWSID     P
     C                     MOVELAGMRDT    SRUNA     P
      *
      ***  force Date foprmat DDMMYY (*IN98 = *OFF)
      *
     C                     SETOF                     98
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     MOVE *ON       *IN99
     C           *INKC     DOWEQ*OFF
     C           *IN99     ANDEQ*ON
      *
     C                     WRITE#MSGCTL
     C                     EXFMTERLU26F1
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C           *INKC     IFNE *ON
      *
      ***  Validate Date entered
      *
     C                     MOVE *OFF      *IN20
     C                     MOVELSDATE     ZDATE
     C                     EXSR ZDATE1
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN20
     C                     MOVEL'ERL4002' ZAMSID
     C**********           MOVEL'ERUSRMSG'ZAMSGF                                              CER001
     C                     MOVEL'STUSRMSG'ZAMSGF                                              CER001
     C                     EXSR ZASNMS
     C                     ENDIF                           *IN99
     C                     ENDIF                           F3
      *
     C                     ENDDO
      *
      ***  Return parameters
      *
     C           *INKC     IFEQ *OFF
     C                     MOVE WWYEAR    WKWAIT  2        From
     C                     MOVE WWDAY     WWYEAR           DDMMYY
     C                     MOVE WKWAIT    WWDAY            To
     C                     MOVELSDATE     PDATE            YYMMDD
     C                     ELSE                            Leave by F3
     C                     MOVE 'E'       RETCOD
     C                     ENDIF
      *
     C                     SETON                     LR
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * ZASNMS - Send Message to Program's Message Queue              *
      *****************************************************************
     C           ZASNMS    BEGSR
      *
      **  Message file specified use PMSGF
      *
     C           ZAMSGF    IFEQ *BLANKS
     C                     MOVELPMSGF     ZAMSGF
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
      **  Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGM            Program queue
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C                     MOVEL*BLANK    ZAMSGF           Message file
     C                     MOVEL*BLANK    ZAMSID           Message id.
      *
     C                     ENDSR
      ********************************************************************
      *
      /COPY ZSRSRC,ZDATE1Z2
      ********************************************************************
      *COPY ZSRSRC,ZDATE1Z3
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
