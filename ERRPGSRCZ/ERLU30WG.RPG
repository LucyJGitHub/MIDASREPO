     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Decision Table - Account Codes Setup')
      ********************************************************************
      *                                                                  *
      *     MIDAS ABS  EUROPEAN REPORTING                                *
      *                                                                  *
      *     ERLU30WG - DECISION TABLE - ACCOUNT CODES SETUP              *
      *                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX004             Date 27Dec96               *
      *                                                                  *
      *                                                                  *
      ********************************************************************
      *                                                                  *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - CAPITAL ADEQUACY                                    *
      *           IML Circular 97/138                                 *
      *                                                                  *
      ********************************************************************
     FSDBANKL1IF  E                    DISK
     FSDACODPDIF  E                    DISK
     FERLUACL0UF  E           K        DISK                      A
      /EJECT
     E**************************************************************************
     E*
     E                    CPY@    1   1 80
      /EJECT
     I**************************************************************************
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     ILDA        UDS                            256
     I                                      132 183 DBLOT
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
      /EJECT
     C**************************************************************************
     C*
     C**  GET INSTALLATION CONTROL DATA RECORD 1:
     C           1         CHAINSDBANKL1             01
     C**
     C**  IF RECORD NOT FOUND:
     C           *IN01     IFEQ '1'
     C                     MOVEL'ERLU30WG'DBPGM
     C                     MOVEL'001'     DBASE            *****************
     C                     MOVEL'SDBANKL1'DBFILE           ** DB ERROR 01 **
     C                     MOVEL' NONE '  DBKEY            *****************
     C                     EXSR *PSSR
     C                     END
     C*
     C**************************************************************************
     C                     READ SDACODPD                 31
     C           *IN31     DOWEQ'0'
      *
      ** TAKE NOT-TITLE ONLY
     C           A5TOIN    IFNE 'Y'                        - B2
      *
     C**********           MOVE A5ACCD    WWACCD  40                                          CER001
     C                     MOVE A5ACCD    WWACCD 100                                          CER001
     C           WWACCD    CHAINERLUACL0             32
      *
     C           *IN32     IFEQ '1'
      *
     C                     Z-ADDWWACCD    VBACOD
     C                     MOVE 'N'       VBEXTT
     C*********************MOVE 'G'       VBAMTY                          ULX004
     C                     MOVE '0'       VBAMTY                          ULX004
     C                     Z-ADD0         VBRBND
     C                     Z-ADD0         VBRBNC
     C                     Z-ADDBJRDNB    VBLCD
     C                     MOVE 'I'       VBTYLC
     C                     Z-ADD0         VBPRCD                          ULX004
     C                     Z-ADD0         VBPORT                          ULX004
      *
     C                     WRITEERLUACD0
      *
     C                     END
      *
     C                     END                             - E2
      *
     C                     READ SDACODPD                 31
     C                     ENDDO
      *
     C                     SETON                     LR
      **************************************************************************
      * *PSSR  S/R
      **************************************************************************
     C           *PSSR     BEGSR
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
      *
     C                     ENDSR
      **************************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
