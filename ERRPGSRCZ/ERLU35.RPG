     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Decision Table - Account Codes List')
     H*****************************************************************
     H*                                                               *
     H*          MIDAS ABS - EUROPEAN REPORTING                       *
     H*                                                               *
     H*        ERLU35 - DECISION TABLE - ACCOUNT CODES LIST           *
     H*                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX046             Date 22Oct02               *
      *                 132893             Date 15APR98               *
      *                 ULX004             Date 28Oct97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX046 - Circulars 2002/170-174-175 Integration              *
      *           Recompile over new layout of ERLU35P1               *
      *  132893 - SPF/RCF for IBLC/BNB on DBA2.3                      *
      *  ULX004 - Capital Adequacy                                    *
      *         - IML Circular 97/138                                 *
      *                                                               *
      *****************************************************************
     FERLUACL1IF  E           K        DISK
     F*
     FSDACODL1IF  E           K        DISK
     F*
     FSDBANKL1IF  E           K        DISK
     F*
     FERLU35P1O   E             70     PRINTER
     F                                              KINFDS SPOOL1         132893
     FERLU35AUO   E                    PRINTER                            132893
     F                                              KINFDS SPOOLU         132893
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*
     F*       70         OVERFLOW INDICATOR FOR ERLU35P1
     F*       80         EOF ERLUACL1
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F**             *************************
     F**             ** INDICATORS NOT USED **
     F**             ** ------------------- **
     F**             *************************
     F**
     F**      ***************************************************
     F**      * 01   02   03   04   05   06   07   08   09   10 *
     F**      * 11   12   13   14   15   16   17   18   19   20 *
     F**      * 21   22   23   24   25   26   27   28   29   30 *
     F**      * 31   32   33   34   35   36   37   38   39   40 *
     F**      * 41   42   43   44   45   46   47   48   49   50 *
     F**      * 51   52   53   54   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   .. *
     F**      * 71   72   73   74   75   76   77   78   79   .. *
     F**      * 81   82   83   84   85   86   87   88   89   90 *
     F**      * 91   92   93   94   95   96   97   98   99      *
     F**      ***************************************************
     E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
     E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     I*
     I** DATA STRUCTURE FOR RPG 01021 ERROR HANDLING
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     ILDA        UDS                            256
     I                                      132 183 DBLOT
     I******                                132 141 DBFILE                132893
     I                                      134 141 DBFILE                132893
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I**
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I***
     I            DS
     I                                        1   2 WWKEY1
     I                                        3   4 WWKEY2
     I                                        1   4 WWKEY
     I*
     ISPOOLU      DS                                                      132893
      *                                                                   132893
      **  File Information Data Structure - AU report                     132893
     I                                       83  92 SFILEU                132893
     I                                    B 123 1240SFNUMU                132893
     ISPOOL1      DS                                                      132893
      *                                                                   132893
      **  File Information Data Structure - P1 report                     132893
     I                                       83  92 SFIL1                 132893
     I                                    B 123 1240SFNUM1                132893
      /EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C           *ENTRY    PLIST
     C                     PARM           PARCTR  2
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RLEV   1                       132893
     C*
     C*----------------- FIRST CYCLE -----------------------------
     C*
     C                     EXSR #INIT
     C*
     C                     READ ERLUACL1                 80
     C*
     C           *IN80     IFEQ '0'                        - B1
     C                     WRITEHEADLI
     C                     ELSE                            - X1
     C******               WRITEHEADLI                                    132893
     C******               WRITENODLI                                     132893
     C                     WRITEHEADAU                                    132893
     C                     WRITENODTLS                                    132893
     C                     END                             - E1
     C*
     C*------------ DETAIL PROCESSING ----------------------------
     C*
     C           *IN80     DOWEQ'0'                        - B1
     C*
     C                     MOVE VBACOD    XRACOD
     C*********************MOVE VBAMTY    XRAMTY                          ULX004
     C                     MOVE VBAMTY    XRAMTY                          ULX004
     C                     MOVE VBPRCD    XRPRCD                          ULX004
     C                     MOVE VBPORT    XRPORT                          ULX004
     C                     MOVE VBEXTT    XREXTY
     C                     Z-ADDVBRBND    XRDEBI
     C                     Z-ADDVBRBNC    XRCRED
     C                     MOVE VBTYLC    XRTYLC
     C*
     C** SETUP DATE IN FORMAT DDMMMYY
     C                     Z-ADDVBLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    XRLCD
     C*
     C** SETUP NARRATIVE AND TYPE OF ACCOUNT CODE
     C           XRACOD    CHAINSDACODL1             88
     C           *IN88     IFEQ '0'                        - B2
     C                     MOVELA5ACCN    XRNARA
     C                     MOVE A5ACTY    XRTYPE
     C                     ELSE                            - X2
     C                     MOVE *BLANKS   XRNARA
     C                     MOVE *BLANKS   XRTYPE
     C                     END                             - E2
     C*
     C** TEST IF OVERFLOW IN PRINTER FILE
     C           *IN70     IFEQ '1'                        - B2
     C                     WRITEHEADLI
     C                     MOVE '0'       *IN70
     C                     END                             - E2
     C**
     C                     WRITEDETLI
     C*
     C                     READ ERLUACL1                 80
     C**
     C                     END                             - E1
     C**
     C*---------------- END DETAILS PROCESSING --------------------
     C**
     C** WRITE END OF REPORT
     C                     WRITEENDLI
     C*
     C           *IN80     IFEQ '1'                        - B1
     C                     MOVE '1'       *INLR
     C                     END                             - E1
     C*
     C                     RETRN
     C/EJECT
     C**=================================================================
      **
      **  #INIT       SUBROUTINE TO INITIALIZE STATIC FIELDS AND ACCESS STANDING
      **              DATA CALLED ONCE AT START OF PROGRAM FROM MAIN LINE.
     C**
      **     CALLED BY PROGRAM
      **     CALLS     *PSSR
      **
     C**=================================================================
     C**                                                   *****************
     C           #INIT     BEGSR                           ** #INIT BEGSR **
     C**                                                   *****************
     C**  PREPARE LDA:
     C**
     C           *NAMVAR   DEFN           LDA                             132893
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'ERLU35'  DBPGM
     C*
     C** SETUP COUNTRY FOR TITLE
     C                     MOVE PARCTR    XX
     C**
     C**  INITIALIZE INDICATORS:
     C                     MOVE '0'       *IN
     C**
     C**  GET INSTALLATION CONTROL DATA RECORD 1:
     C                     READ SDBANKL1                 80
     C**
     C**  IF RECORD NOT FOUND:
     C           *IN80     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA                                       132893
     C                     MOVEL'01'      DBASE            *****************
     C                     MOVEL'SDBANKL1'DBFILE           ** DB ERROR 01 **
     C                     MOVEL' NONE '  DBKEY            *****************
     C                     OUT  LDA                                       132893
     C                     EXSR *PSSR
     C                     END                             - E1
     C**                                                               *****
      * Record Printer file ERLU35P1 on RCF                               132893
     C                     Z-ADDSFNUM1    ZSNUM                           132893
     C                     MOVELSFIL1     SFILE                           132893
      *                                                                   132893
     C                     EXSR SRZSFL                                    132893
      *                                                                   132893
      * Record Printer file ERLU35AU on RCF                               132893
     C                     Z-ADDSFNUMU    ZSNUM                           132893
     C                     MOVELSFILEU    SFILE                           132893
      *                                                                   132893
     C                     EXSR SRZSFL                                    132893
      *                                                                   132893
     C                     ENDSR
     C*================================================================
     C*
     C**   ZDATE2 SR. TO CONVERT A DAY NUMBER TO DATE FORMATS.
     C**
     C           ZDATE2    BEGSR                           *** ZDATE2 ***
     C**
     C**   CLEAR DATE FIELDS.
     C                     Z-ADD0         ZDATE   60
     C                     MOVEL'       ' ZADATE  7
     C**
     C**   CALCULATION TO DEFINE INPUT DAY NUMBER.
     C                     Z-ADDZDAYNO    ZDAYNO  50
     C**
     C**   DETERMINE YEAR NUMBER.
     C**
     C**   ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
     C           ZDAYNO    SUB  1         ZDAYN1  50   99
     C   99                GOTO ZDEND2
     C**
     C**   CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
     C           ZDAYN1    DIV  1461      ZLYR    20
     C                     MVR            ZDAYN1           SAVE REMAINING
     C**                                                   DAYS
     C**   CALCULATE NUMBER OF REMAINING YEARS.
     C                     Z-ADD1         ZWRK2   20
     C           ZDTAG1    TAG                             ** ZDTAG1 TAG *
     C           ZDAYN1    COMP ZYDY,ZWRK2             90
     C  N90      ZWRK2     ADD  1         ZWRK2
     C  N90                GOTO ZDTAG1
     C           ZWRK2     SUB  1         ZWRK2          91LEAP YR, 91 ON
     C**
     C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
     C  N91      ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
     C**
     C**   CALCULATE ACTUAL YEAR NUMBER.
     C           ZLYR      MULT 4         ZWRK3   30
     C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
     C                     Z-ADDZWRK3     ZYEAR   20
     C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
     C**
     C**   DETERMINE MONTH NUMBER.
     C**
     C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
     C                     SETOF                     9293
     C   91      ZDAYN1    COMP 59                     9293
     C   91N92   ZDAYN1    SUB  1         ZDAYN1
     C**
     C**   CALCULATE MONTH NUMBER.
     C                     Z-ADD2         ZWRK2
     C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
     C           ZDAYN1    COMP ZMDY,ZWRK2             94
     C  N94      ZWRK2     ADD  1         ZWRK2
     C  N94                GOTO ZDTAG2
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C                     Z-ADDZWRK2     ZMTH    20       MONTH
     C**
     C**   DETERMINE DAY OF MONTH.
     C**
     C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
     C           ZDAYN1    ADD  1         ZDAYN1
     C**
     C**   CALCULATE DAY OF MONTH.
     C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
     C**
     C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
     C   93      ZDAY      ADD  1         ZDAY
     C**
     C**   FORMAT DATE, DDMMYY OR MMDDYY.
     C  N98                MOVELZDAY      ZWRK4   40
     C   98                MOVE ZDAY      ZWRK4
     C  N98                MOVE ZMTH      ZWRK4
     C   98                MOVELZMTH      ZWRK4
     C                     MOVELZWRK4     ZDATE
     C                     MOVE ZYEAR     ZDATE
     C**
     C**   FORMAT ALPHA DATE, DDMMMYY.
     C           ZDAY      COMP 10                     95
     C                     MOVELZDAY      ZWRK5   5
     C   95                MOVEL' '       ZWRK5
     C                     MOVE ZMNM,ZMTH ZWRK5
     C                     MOVELZWRK5     ZADATE
     C                     MOVE ZYEAR     ZADATE
     C**
     C           ZDEND2    ENDSR                           * ZDEND2 ENDSR*
     C**
     C********************************************************************
      **
      ** *PSSR  S/R
      **
     C*================================================================
     C           *PSSR     BEGSR
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     WRITEHEADAU                                    132893
     C                     WRITEDBERROR                                   132893
     C                     DUMP
     C                     RETRN
     C**
     C                     ENDSR
     C*****************************************************************   132893
     C* SRZSFL - Subroutine to call ZSFILE                                132893
     C*****************************************************************   132893
     C*                                                                   132893
     C           SRZSFL    BEGSR                                          132893
     C*                                                                   132893
     C                     CALL 'ZSFILE'                                  132893
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RENT   3                       132893
     C                     PARM           SFILE  10                       132893
     C                     PARM           ZSNUM   60                      132893
     C                     PARM           ZSERR   1                       132893
     C*                                                                   132893
     C** If an error occurred during ZSFILE processing,                   132893
     C** return to the calling program.                                   132893
     C*                                                                   132893
     C           ZSERR     IFEQ 'Y'                                       132893
     C                     SETON                     U7U8LR               132893
     C                     RETRN                                          132893
     C                     END                                            132893
     C*                                                                   132893
     C                     ENDSR                                          132893
      ****************************************************************    132893
      /EJECT                                                              132893
      *                                                                   132893
     C********************************************************************
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2005
