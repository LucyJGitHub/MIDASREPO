     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER CAB Extracted Transaction Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  ERLU57 - CAB Extracted Transactions Audit Report             *
      *                                                               *
      *  Function:  This program reports the transactions extracted   *
      *             in PF/ERLUCBPP                                    *
      *  (phase(s)) Input Cycle                                       *
      *             COB                                               *
      *                                                               *
      *  Called By: ERCLU57 - CAB Extracted Transactions Audit Report *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. UPG402             Date 03Oct04               *
      *                 ULX008             Date 25Jul00               *
      *                 180074             Date 08Jun00               *
      *                 179624             Date 07Jun00               *
      *                 179624             Date 29May00               *
      *                 ULX008  *CREATE    Date 23Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  UPG402 - Recompile due to R4.02 upgrade                      *
      *  ULX008 - "Commissariat aux Bourses" - Change Control C       *
      *  180074 - Wrong Details Printed                               *
      *  179624 - If price is a percentage, multiply with 100         *
      *  179624 - Nominal Amount and price are wrongly formatted      *
      *  ULX008 - CSSF Statutory Reporting : Commissariat aux Bourses *
      *                                                               *
      *****************************************************************
     FERLUCBV0IF  E           K        DISK         KINFSR *PSSR
     FERLUCCV1IF  E           K        DISK         KINFSR *PSSR
     FSECTY   IF  E           K        DISK         KINFSR *PSSR          179624
     FTRADS   IF  E           K        DISK         KINFSR *PSSR          ULX008
     FTRANS   IF  E           K        DISK         KINFSR *PSSR          ULX008
     FERLU57AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FERLU57P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  END    - End processing                                      *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *****************************************************************
     E/EJECT
      ** Array containing Copyright statement
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E***/COPY ZSRSRC,ZSEDITZ1                                            180074
     E/COPY ZSRSRC,ZEDITZ1                                                180074
      *
      *****************************************************************
      /SPACE 3
     ILDA       E DSLDA                         256
      *
     IPSDS       SDS
      **  Program Status Data Structure
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ISPOOL1      DS
      **  File Information Data Structure for ERLU57P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      **  File Information Data Structure for ERLU57AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      *
     IDSSDY     E DSDSSDY
      **  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          XXDFAC                                    CER001
     ISDCUST    E DSSDCUSTPD
      *
     I***/COPY ZSRSRC,ZSEDITZ2                                            180074
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
     C                     EXSR INIT
      *
     C                     EXSR PROCES
      *
     C                     EXSR END
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *  PROCES - Subroutine to process interface records             *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
     C           *LOVAL    SETLLERLUCBV0
     C                     READ ERLUCBV0                 89
      *
      **  No records, print 'No details to report' and exit pgm
     C           *IN89     IFEQ *ON
     C                     EXSR END
     C                     ENDIF
      *
     C           *IN89     DOWEQ*OFF
      *
     C           LUBKBR    IFNE WWBRCH
     C                     EXSR #BRCH
     C                     MOVELLUBKBR    WWBRCH  3
     C                     ENDIF
      *
     C                     EXSR WP1REC
      *
      **  Count records output
     C                     ADD  1         RCOUNT
      *
      **  Read next record.
     C                     READ ERLUCBV0                 89
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *****************************************************************
      *
     C           WP1REC    BEGSR
      *
     C                     MOVELLUMODU    RMODU            Module ID
     C                     MOVELLUTRAN    RTRAN            Trade Nbr
      *
     C           LUC013    IFEQ *BLANKS
     C                     MOVEL'I'       RACTN
     C                     ELSE
     C                     MOVEL'D'       RACTN            Action code
     C                     ENDIF
      *
     C           LUC030    IFEQ 'A'
     C                     MOVEL'TP'      RPSID
     C                     ELSE
     C                     MOVEL'TS'      RPSID            Purch/Sale Ind.
     C                     ENDIF
      *
     C                     MOVELLUSSHN    RSESN            Secur. Shortn.
     C                     MOVELLUC023    RSRPN            Secur. Rep Nm
      *
     C           LUC020    IFNE *BLANKS
     C                     MOVE LUC020    RSCOD
     C                     ELSE
     C                     MOVELLUC021    RSCOD            Secur. Code
     C                     ENDIF
      *
     C                     MOVELLUC007    RICUR            ISO Ccy Code
      *
      **  Convert Nominal with 4 decimal places
     C***                  MOVE *BLANKS   ZFLD15                   179624 180074
     C                     MOVE *BLANKS   ZFIELD                          180074
     C           LUC024    IFNE 0
     C******               Z-ADDLUC024    ZFLD15                          179624
     C***                  MOVE LUC024    ZFLD15                   179624 180074
     C                     MOVE LUC024    ZFIELD                          180074
     C                     ELSE
     C******               Z-ADDLUC025    ZFLD15                          179624
     C***                  MOVE LUC025    ZFLD15                   179624 180074
     C                     MOVE LUC025    ZFIELD                          180074
     C                     ENDIF
      *
     C***                  Z-ADD4         ZDECS                           180074
     C***                  MOVE 'J'       ZECODE                          180074
     C***                  EXSR ZSEDIT                                    180074
     C***                  MOVE ZOUT21    RNOML            Nominal        180074
     C                     Z-ADD4         ZADEC                           180074
     C                     EXSR ZEDIT                                     180074
     C                     MOVE ZFIELD    RNOML            Nominal        180074
      *
      **  Convert Price
      *                                                                   180074
      ***  If depot movement : price is percentage format ==> Mult by 100 180074
      *                                                                   180074
     C                     MOVELLUC072    ALPH02  2                       180074
      *                                                                   ULX008
      ***  Retrieve Market Centre Depending On Transaction                ULX008
      *                                                                   ULX008
     C                     SELEC                                          ULX008
     C           ALPH02    WHEQ 'DM'                       Depot Movement ULX008
     C           LUSSHN    IFNE *BLANKS                                   ULX008
     C           LUSSHN    CHAINSECTY                88                   ULX008
     C           *IN88     IFEQ *OFF                                      ULX008
     C                     MOVE SMCC      RMRKT                           ULX008
     C                     ENDIF                                          ULX008
     C                     ENDIF                                          ULX008
      *                                                                   ULX008
     C           ALPH02    WHEQ 'SE'                       SE Trades      ULX008
     C           LUTRAN    CHAINTRADS                88                   ULX008
     C           *IN88     IFEQ *OFF                                      ULX008
     C                     MOVE TMKC      RMRKT                           ULX008
     C                     ENDIF                                          ULX008
      *                                                                   ULX008
     C           ALPH02    WHEQ 'FF'                       Futures & Opt  ULX008
     C                     MOVE LUTRAN    WWTRAN  60                      ULX008
     C           WWTRAN    CHAINTRANS                88                   ULX008
     C           *IN88     IFEQ *OFF                                      ULX008
     C           2         SUBSTISTT:1    RMRKT                           ULX008
     C                     ENDIF                                          ULX008
     C                     ENDSL                                          ULX008
      *                                                                   ULX008
     C           ALPH02    IFEQ 'DM'                       Depot movement 180074
     C                     MULT 100       LUC026                          180074
     C*****                ELSE                                           180074
     C                     END                             DM             180074
      *                                                                   180074
      **  Check if price is a percentage                                  179624
     C           ALPH02    IFEQ 'SE'                       Trade          180074
     C           LUSSHN    CHAINSECTY                89                   179624
     C           *IN89     IFEQ *ON                                       179624
     C                     MOVE 'SECTY'   WBFILE           ***************179624
     C                     Z-ADD005       WWBASE           * DB ERROR 05 *179624
     C                     MOVELLUSSHN    WWBKEY           ***************179624
     C                     EXSR *PSSR                                     179624
     C                     ENDIF                                          179624
      *                                                                   179624
      **  If price is a percentage then multiply with 100                 179624
     C           SPBS      IFEQ 'P'                                       179624
     C                     MULT 100       LUC026                          179624
     C                     ENDIF                                          179624
      *                                                                   179624
     C                     ENDIF                           'SE'           180074
      *                                                                   180074
     C***                  MOVE *BLANKS   ZFLD15                   179624 180074
     C******               Z-ADDLUC026    ZFLD15                          179624
     C***                  MOVE LUC026    ZFLD15                   179624 180074
     C***                  Z-ADD8         ZDECS                           180074
     C***                  EXSR ZSEDIT                                    180074
     C***                  MOVE ZOUT21    RPRIC            Price          180074
     C                     MOVE *BLANKS   ZFIELD                          180074
     C                     MOVE LUC026    ZFIELD                          180074
     C                     Z-ADD8         ZADEC                           180074
     C                     EXSR ZEDIT                                     180074
     C                     MOVE ZFIELD    RPRIC            Price          180074
      *
     C                     MOVE LUCNUM    RCNUM            Cust. Nbr
      *
      **  Acces PF/SDCUSTPD for customer details
     C                     MOVELLUCNUM    @CUST
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @CUST  10
     C                     PARM           @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           @RTCD     IFNE *BLANKS
     C                     MOVE 'SDCUSTPD'WBFILE           ***************
     C                     Z-ADD003       WWBASE           * DB ERROR 03 *
     C                     MOVELLUCNUM    WWBKEY           ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBCSSN    RCSSN            Cust. Shortn.
     C                     MOVELBBCRTN    RCRTN            Report Town
      *
     C                     MOVELLUCOCZ    RCNCZ            Country Cit.
      *
      **  Acces LF/ERLUCCV1 for investment company details
     C           LUC017    IFEQ 'LUX'
     C           LUC018    CHAINERLUCCV1             89
     C           *IN89     IFEQ *ON
     C                     MOVE 'ERLUCCV1'WBFILE           ***************
     C                     Z-ADD004       WWBASE           * DB ERROR 04 *
     C                     MOVELLUC018    WWBKEY           ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELX6INNM    RINNM            Invest. Company
     C                     MOVELLUC018    RINCC            Company Code
     C                     ELSE
     C                     MOVEL'----'    RINCC
     C                     MOVELLUC019    RINNM
     C                     ENDIF
      *
      **  Check that sufficient lines remain for the Format. If not,
      **  write out the Main Headings on a new page.
     C                     Z-ADD3         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADPY
     C                     ENDIF
      *
      ***  Write out Detail Record.
     C                     WRITEDETAIL
     C                     RESETDETAIL
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/#BRCH
      *****************************************************************
      *  #BRCH - Subroutine to manage break on branch                 *
      *****************************************************************
     C           #BRCH     BEGSR
      *
      **  Check if branch exists
     C                     MOVELLUBKBR    @BRCH
     C                     CALL 'AOBRCHR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @BRCH   3
     C           SDBRCH    PARM SDBRCH    DSSDY
     C           @RTCD     IFNE *BLANKS
     C                     MOVE 'SDBRCHPD'WBFILE           ***************
     C                     Z-ADD002       WWBASE           * DB ERROR 02 *
     C                     MOVELLUBKBR    WWBKEY           ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     CLOSEERLU57P1
     C                     OPEN ERLU57P1
     C                     EXSR RCFP1
      *
      **  Set up header
     C                     MOVELLUBKBR    RBRCD
     C                     MOVELA8BRNM    RBRNM
      *
      **  Set up date
     C***        PDAT      IFNE *BLANKS                                   180074
     C           PDAT      IFNE '00000'                                   180074
     C                     MOVE PDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RDATE
     C                     ELSE
     C                     MOVELBJMRDT    RDATE
     C                     ENDIF
      *
     C                     WRITEHEADPY
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/END
      *****************************************************************
      *  END -  Subroutine to end pgm                                 *
      *****************************************************************
     C           END       BEGSR
      *
     C           RCOUNT    IFNE 0
      *
      **  Check that sufficient lines remain for the Format. If not,
      **  write out the Main Headings on a new page.
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADPY
     C                     ENDIF
      *
      **  Write out Total Format.
     C                     WRITETRAILPY
      *
      **  Write out no details to report
     C                     ELSE
     C                     EXSR AUDIT
     C                     ENDIF
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *****************************************************************
     C           AUDIT     BEGSR
      *
     C           WFIRAU    IFEQ 'Y'
     C                     MOVE 'N'       WFIRAU
     C                     EXSR RCFAU
     C                     WRITEHEADAU
     C                     ENDIF
      *
      **  If there is a DB Error, Output the DB Error Information.
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      **  Or, if no records read, Output "No Details" message.
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *  INIT   - Subroutine to do Program Initialisation.            *
      *****************************************************************
     C           INIT      BEGSR
      *
      **  Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      **  Receive Parameter List.
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PDAT    5
      *
      **  Call Access Program for Bank Details - Title, Run Date and
      **  Run Day Number.
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'WBFILE           ***************
     C                     Z-ADD001       WWBASE           * DB ERROR 01 *
     C                     MOVEL*BLANKS   WWBKEY           ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Check System Date Format, DDMMYY (*IN98 off)
      **                            MMDDYY (*IN98 on).
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
     C           *NAMVAR   DEFN           LDA
      *
     C           *LIKE     DEFN DBFILE    WBFILE
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      **  Report Work fields.
     C                     MOVEL'Y'       WFIRAU  1
     C                     MOVEL'Y'       WFIRP1  1
     C                     Z-ADD0         RCOUNT  50
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *  *PSSR  - Error control subroutine                            *
      *****************************************************************
     C           *PSSR     BEGSR
      *
      **  Check to see that *PSSR has not already been called.
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *LOCK     IN   LDA
     C                     MOVELWWBASE    DBASE
     C                     MOVELWWBKEY    DBKEY
     C                     MOVELWBFILE    DBFILE
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     RETRN
      *
     C                     ELSE
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
      **  If *PSSR already run, then just end the program here.
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *****************************************************************
     C           RCFP1     BEGSR
      *
      **  Ensure Report Spool File recorded by RCF.
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      **  If Error occurs during ZSFILE processing, then return to the
      **  Calling Program.
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *****************************************************************
     C           RCFAU     BEGSR
      *
      **  Ensure Audit Spool File recorded by RCF.
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      **  If Error occurs during ZSFILE processing, then return to the
      **  Calling Program.
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C****/COPY ZSRSRC,ZSEDITZ3                                           180074
     C/COPY ZSRSRC,ZEDITZ2                                                180074
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
