     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER LU - CSSF Reporting I/C - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Luxembourg                        *
      *                                                               *
      *  ERLU59 - CSSF Reportinf I/C Prompt                           *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX008             Date 23Feb00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX008 - CSSF Statutory Reporting : Commissariat aux Bourses *
      *                                                               *
      *****************************************************************
     FERLU59DFCF  E                    WORKSTN
      *
      /EJECT
      *
      /COPY ZSRSRC,ZDATE2Z1
      *** Array containing Copyright statement
     E                    CPY@    1   1 80
      /SPACE 3
      *
      *** Local data area for database error details
     ILDA       E DSLDA                         256
      *
      *** Data Area giving Installation Control Details
     IRUNDAT    E DSRUNDAT
      *
      *** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM PGM
     I                                      244 253 SWSID
     I                                      254 263 SUSER
      *
      ***  Working DS to setup P#PARM
     IP#PARM      DS                            100
     I                                        1   5 WWDATE
      *
      /EJECT
      *****************************************************************
      *                         M A I N L I N E S                     *
      *****************************************************************
      *
      ***  Clear screen message file
     C                     CALL 'CLRERMSG'
     C                     PARM PGM       ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
      ***  Set working indicator
     C                     MOVE *ON       *IN75
     C                     MOVEA'0'       *IN,20
      *
     C           *INKC     DOUEQ*ON
     C           *IN75     OREQ *OFF
      *
      ***  Display messages
     C                     WRITE#MSGCTL
      *
      ***  Display main screen
     C                     EXFMTERLU59F0
      *
      ***  Clear screen message file
     C                     CALL 'CLRERMSG'
     C                     PARM PGM       ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
      ***  Reset Indicator
     C                     MOVE *OFF      *IN75
     C                     MOVEA'0'       *IN,20
      *
      ***  Check dates entered
     C           *INKC     IFEQ *OFF
     C                     EXSR VALID
     C                     ELSE
     C                     MOVE 'E'       WWCMD   1
     C                     ENDIF
      *
     C                     ENDDO
      *
      *** Fill P#PARM with the entered tables
     C           WWCMD     IFEQ 'E'
     C                     MOVE *BLANK    P#PARM
     C                     ENDIF
      *
     C                     MOVE WWCMD     P#CMD
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      *                    E N D     P R O G R A M                    *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *   VALID  - Check if Table exist or is *****                   *
      *   ------                                                      *
      * Called by: main line                                          *
      * Calls: None                                                   *
      *****************************************************************
     C           VALID     BEGSR
      *
      ***  Both date must be entered and must be valid
     C           SDATE     IFNE *BLANKS
     C                     MOVE SDATE     ZDATE
     C                     EXSR ZDATE1
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN20
     C                     MOVE *ON       *IN75
      *
      ***  'Must be an valid Date or Blanks.'
     C                     MOVEL'ERL0835' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
     C                     ENDIF
      *
     C           *IN75     IFEQ *OFF
     C           SDATE     IFNE *BLANKS
     C                     MOVELZDAYNO    WWDATE
     C                     ELSE
     C                     MOVE '00000'   WWDATE
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *   SRSMSG - Send message in Subfile message                    *
      *   ------                                                      *
      * Called by : VALID                                             *
      * Calls     : SNDERMSG                                          *
      *                                                               *
      *****************************************************************
     C           SRSMSG    BEGSR
      *
     C                     CALL 'SNDERMSG'
     C                     PARM PGM       ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
      * Clear all fields for default mechanism next time.
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSID           Message Id.
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *   *INZSR - First Routine                                      *
      *   ------   Called automatically at porgram start.             *
      * Called by: main line                                          *
      * Calls: None                                                   *
      *****************************************************************
     C           *INZSR    BEGSR
      *
      *** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Entry parameters
      **     PGM        PARM(&RSEQ &RLEV &RENT &RPARM &RACT &RCMD)
     C           *ENTRY    PLIST
     C                     PARM           P#RSEQ  5
     C                     PARM           P#RLEV  1
     C                     PARM           P#RENT  3
     C                     PARM           P#PARM
     C                     PARM           P#RACT  1
     C                     PARM           P#CMD   1
      *
     C                     MOVE P#CMD     WWCMD
      *
      *** Read in Installation Data
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      *** Set Date Format Indicator *IN98  on if date format is MMDDYY
     C           AGDFF     IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      *** Fill Midas date on screen
     C                     MOVELAGMRDT    SRUNA
      *
      ***  Message file will be German features's one.
     C**********           MOVEL'ERUSRMSG'ZAMSGF                                              CER001
     C                     MOVEL'STUSRMSG'ZAMSGF                                              CER001
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by:                                                    *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     MOVE 'E'       P#CMD
      *
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      /COPY ZSRSRC,ZDATE1Z2
      /COPY ZSRSRC,ZDATE2Z2
      ********************************************************************
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Finastra International Limited 2005
