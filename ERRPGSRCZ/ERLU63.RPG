     H        1   Y
     H****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER IML Reporting - Customer Detl proc.')
     H****************************************************************
     H*                                                              *
     H*          MIDAS ABS - EUROPEAN REPORTING                      *
     H*                                                              *
     H*  ERLU61 - IML REPORTING - CUSTOMER DETAILS PROCESSING        *
     H*                                                              *
     H*  (c) Finastra International Limited 2005                     *
     H*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027A            Date 06May06               *
      *                 CER001             Date 25Aug05               *
     H*                 ULX048             Date 07Aug03               *
     H*                 208821             Date 06sep02               *
     H*                    187689             DATE   20Dec00         *
     H*                    ULX012             Date   09Feb00         *
     H*                    ULX008             DATE   25Feb00         *
     H*                    ULX004             Date   18MAR97         *
     H*                    ERL459 PT          Date   29MAY95         *
     H*                    ERL432 PT          DATE   30AUG94         *
     H*                    ERL371 TLI         DATE   16DEC93         *
     H*                    ERL365 PT          DATE   18AUG93         *
     H*                    ER0336 TLI         DATE   06APR93         *
     H*                    ERL319             DATE   23FEB93         *
     H*                    ER0117             DATE   10DEC92         *
     H*                    ER1006             DATE   DDMMMYY         *
     H*                                                              *
      *--------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027A - Conversion Of Customer Number to Alpha             *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      * ULX048 - Circular CSSF 2003/100 management                   *
      * 208821 - Wrong new economic sector code caused warning C015. *
      * 187689 - "GRD" currency in EMU                               *
      * ULX008 - CSSF Statuatory Reporting : Commissariat aux Bourses*
      *          Recompiled due to changes in PF/SDCUSTX6            *
      * ULX012 - Pledges and Multiple Guarantors Management          *
      *          Recompile only due to changes in PF/SDCUSTX6        *
      * ULX004 - Capital Adequacy                                    *
      *        - IML Circular 97/138                                 *
      *           Add new parameter for EXNCOS                       *
      * ERL459 - CEDEL IS CONSIDERED AS A BANK FROM 01/01/95         *
      * ERL432 - TO MANAGE THE DATABASE ERRORS CORRECTLY             *
      * ERL371 - IML 1.4 DEVELOPMENT                                 *
      * ERL365 - FOR NON BANKS, USE ALWAYS COUNTRY OF LOCATION       *
      * ER0336 - IML 2.3 DEVELOPMENT                                 *
      * ERL319 - SETUP ISO CODE IN COUNTRY OF CITIZENSHIP            *
      * ER0117 - 1. CREDIT RECEIVED AMOUNT COMES NOW FROM LIMITBX6   *
      * ER1006 - NO ERROR WHEN NO LIMIT DEFINED FOR A CUSTOMER
      *          CREDIT RECEIVED AMOUNT IS LINKED TO CRRI .
      *          1. CRRI = Y ONLY FOR BNB & IRG
      *          2 . IN THAT CASRE CRRA = CREDIT LIMIT GRANTED BY BNB
      *             OR IRG TO THE BANK
      *          3. THAT AMT IS PRESENT IN CUSTOMER DL LIMIT FOR MM
      *             LOANS. IT IS AN EXCEPTION 8(THOSE 2 FLDS WILL BE UED
      *             FOR IT DEALS.
      *
     F*****************************************************************
     F***ERCTRYL1IF**E           K        DISK                                                CER001
     FERCTYXL0IF  E           K        DISK                                                   CER001
     F* VQ - ER Country Code Details
      *
     F***SDCUSTY6IF**E           K        DISK                                                CER001
     FSDCUX4L0IF  E           K        DISK                                                   CER001
     F* VF - Customer Extension File
      *
     F***SDFOCSX6IF**E           K        DISK                                         ULX004 CER001
     FSDFOX2PDIF  E           K        DISK                                                   CER001
     F* CA - FF Customers maintenance extension file
      *
     F*****ERLIMTL1IF  E           K        DISK                          ER0117
     F* VS - ER LENDING LIMIT
      *
     F***LIMITBY6IF**E           K        DISK                                                CER001
     FDLLMX2L0IF  E           K        DISK                                                   CER001
     F* VJ - ER Customer Limit Extension File
      *
      *===============================================================*
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       80         FAIL CONDITION FOR FILE ACCESS OPERATION
     F*
     F*       U7         DATABASE ERROR
     F*
     F**             *************************
     F**             ** INDICATORS NOT USED **
     F**             ** ------------------- **
     F**             *************************
     F**
     F**      ***************************************************
     F**      * 01   02   03   04   05   06   07   08   09   10 *
     F**      * 11   12   13   14   15   16   17   18   19   20 *
     F**      * 21   22   23   24   25   26   27   28   29   30 *
     F**      * 31   32   33   34   35   36   37   38   39   40 *
     F**      * 41   42   43   44   45   46   47   48   49   50 *
     F**      * 51   52   53   54   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   70 *
     F**      * 71   72   73   74   75   76   77   78   79   .. *
     F**      * 81   82   83   84   85   86   87   88   89   90 *
     F**      * 91   92   93   94   95   96   97   98   99      *
     F**      ***************************************************
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      *===============================================================*
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     ILDA        UDS                            256
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
     I**
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I**
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      * Program status data structure
     I           SDS
     I                                     *PROGRAM PGM
      **=================================================================
     C                     EXSR #INIT
      **=================================================================
      ** Access CUSTOMER Extension file                                   ERL365
     C********** EXCUST    CHAINSDCUSTY6             80                                ERL365 CER001
     C           EXCUST    CHAINSDCUX4L0             80                                       CER001
      ** If no record is found                                            ERL365
     C           *IN80     IFEQ '1'                        - B1           ERL365
     C           *LOCK     IN   LDA                                       ERL365
     C                     MOVEL'633'     DBASE            ***************ERL365
     C**********           MOVEL'SDCUSTY6'DBFILE                                       ERL365 CER001
     C                     MOVEL'SDCUX4L0'DBFILE                                              CER001
     C                     MOVELEXCUST    DBKEY                           ERL365
     C                     EXSR #DBERR                                    ERL365
     C                     ELSE                            - X1           ERL365
     C                     MOVELVFCONS    EXCONS                          ERL365
     C                     MOVELVFECOS    EXECOS                          ERL365
     C                     MOVELVFNCOS    EXNCOS                          ULX004
     C**********           Z-ADDVFCGRP    EXCGRP                                      ERL365 CSD027A
     C                     MOVE VFCGRP    EXCGRP                                             CSD027A
     C                     Z-ADDVFPAPS    EXPAPS                          ERL371
     C                     MOVE VFPERF    EXPERF                          ULX004
     C                     MOVE VFSMRS    EXSMRS                          187689
     C                     MOVE VFMCID    EXMCID                          ULX048
     C                     ENDIF                           - E1           ERL365
      *
      ***  Access the FF instrument types extension file
      *
     C           EXLOTD    IFEQ 'OPT'                        B1           ULX004
     C           EXLOTD    OREQ 'FTR'                                     ULX004
     C********** EXCUST    CHAINSDFOCSX6             80                                ULX004 CER001
     C           EXCUST    CHAINSDFOX2PD             80                                       CER001
      *
      ***  If no record is found
      *
     C           *IN80     IFEQ *ON                          B2           ULX004
     C           *LOCK     IN   LDA                                       ULX004
     C                     MOVEL'636'     DBASE                           ULX004
     C**********           MOVEL'SDFOCSX6'DBFILE                                       ULX004 CER001
     C                     MOVEL'SDFOX2PD'DBFILE                                              CER001
     C                     MOVELEXCUST    DBKEY                           ULX004
     C                     EXSR #DBERR                                    ULX004
     C                     ELSE                              X2           ULX004
     C                     Z-ADDCANETA    EXNTAG                          ULX004
     C                     ENDIF                             E2           ULX004
     C                     ENDIF                             E1           ULX004
      *
      ***  Access with economic group
      *
     C                     MOVE EXCGRP    WWCGRP  6                       ULX004
     C********** WWCGRP    CHAINSDCUSTY6             80                                ULX004 CER001
     C           WWCGRP    CHAINSDCUX4L0             80                                       CER001
      *
      ***  If no record is found
      *
     C           *IN80     IFEQ *ON                          B1           ULX004
     C           *LOCK     IN   LDA                                       ULX004
     C                     MOVEL'637'     DBASE                           ULX004
     C**********           MOVEL'SDCUSTY6'DBFILE                                       ULX004 CER001
     C                     MOVEL'SDCUX4L0'DBFILE                                              CER001
     C                     MOVELEXCGRP    DBKEY                           ULX004
     C                     EXSR #DBERR                                    ULX004
     C                     ELSE                              X1           ULX004
     C                     MOVE VFCRNM    EXCGRN                          ULX004
     C                     ENDIF                             E1           ULX004
      *
      ***  Access country codes with the economic group country code
      *
     C********** VFCOLC    CHAINERCTRYL1             80                                ULX004 CER001
     C           VFCOLC    CHAINERCTYXL0             80                                       CER001
      *
      ***  If no record is found
      *
     C           *IN80     IFEQ *ON                          B1           ULX004
     C           *LOCK     IN   LDA                                       ULX004
     C                     MOVEL'638'     DBASE                           ULX004
     C**********           MOVEL'ERCTRYL1'DBFILE                                       ULX004 CER001
     C                     MOVEL'ERCTYXL0'DBFILE                                       ULX004 CER001
     C                     MOVELVFCOLC    DBKEY                           ULX004
     C                     EXSR #DBERR                                    ULX004
     C                     ELSE                              X1           ULX004
     C                     MOVE VQISOC    EXCGLC                          ULX004
     C                     ENDIF                             E1           ULX004
      *
      **=================================================================
      **  ACCESS COUNTRY EXTENSION FILE  :
      **   Retrieve Customer ISO country code:                         *****
     C********** EXCUCC    CHAINERCTRYL1             80                                       CER001
     C           EXCUCC    CHAINERCTYXL0             80                                       CER001
      **   If record not found:
     C           *IN80     IFEQ '1'                        - B1
     C           *LOCK     IN   LDA
     C                     MOVEL'631'     DBASE            *****************
     C**********           MOVEL'ERCTRYL1'DBFILE                                              CER001
     C                     MOVEL'ERCTYXL0'DBFILE                                              CER001
     C                     MOVELEXCUCC    DBKEY            *****************
     C                     EXSR #DBERR
     C                     ELSE                            - X1
     C                     MOVELVQISOC    EXCUCL
     C                     END                             - E1
      *                                                                   ERL365
      **   For non banks, overwrite country of citizenship by country     ERL365
      **   of location                                                    ERL365
     C           EXECOS    IFNE 1410                                      ERL365
     C           EXECOS    ANDNE1421                                      ERL365
     C           EXECOS    ANDNE1422                                      ERL365
     C           EXECOS    ANDNE1451                                      ERL459
     C                     MOVELEXCUCL    EXCNCZ                          ERL365
     C                     ELSE                                           ERL365
      *
      **   Retrieve Customer ISO country code for Country of Citizenship  ERL319
     C********** EXCNCZ    CHAINERCTRYL1             80                                ERL319 CER001
     C           EXCNCZ    CHAINERCTYXL0             80                                       CER001
      **   If record not found:                                           ERL319
     C           *IN80     IFEQ '1'                        - B1           ERL319
     C           *LOCK     IN   LDA                                       ERL319
     C                     MOVEL'632'     DBASE            ***************ERL319
     C**********           MOVEL'ERCTRYL1'DBFILE                                       ERL319 CER001
     C                     MOVEL'ERCTYXL0'DBFILE                                              CER001
     C                     MOVELEXCUCC    DBKEY            ***************ERL319
     C                     EXSR #DBERR                                    ERL319
     C                     ELSE                            - X1           ERL319
     C                     MOVELVQISOC    EXCNCZ                          ERL319
     C                     END                             - E1           ERL319
     C                     END                             - E1           ERL365
      *
     C**=================================================================
      ***Access*CUSTOMER*Extension*file                                   ERL365
     C***        EXCUST    CHAINSDCUSTY6             80                   ERL365
      ***If no record is found                                            ERL365
     C***        *IN80     IFEQ '1'                        - B1           ERL365
     C***        *LOCK     IN   LDA                                       ERL365
     C***                  MOVEL'633'     DBASE            ***************ERL365
     C***                  MOVEL'SDCUSTY6'DBFILE                          ERL365
     C***                  MOVELEXCUST    DBKEY                           ERL365
     C***                  EXSR #DBERR                                    ERL365
     C***                  ELSE                            - X1           ERL365
     C***                  MOVELVFCONS    EXCONS                          ERL365
     C***                  MOVELVFECOS    EXECOS                          ERL365
     C***                  Z-ADDVFCGRP    EXCGRP                   ER0336 ERL365
     C***                  END                             - E1           ERL365
      **=================================================================
     C           EXLOTD    IFEQ 'MMD'                      - B1
      *
     C**********           MOVELEXCUST    WWCNUM  60                                         CSD027A
     C                     MOVELEXCUST    WWCNUM  6                                          CSD027A
      *
      ** Retrieve Credit Received indicator:
     C********** WWCNUM    CHAINLIMITBY6             80                                       CER001
     C           WWCNUM    CHAINDLLMX2L0             80                                       CER001
     C           *IN80     IFEQ '1'                        - B2
     C**         *LOCK     IN   LDA
     C**                   MOVEL'635'     DBASE            *****************
     C**                   MOVEL'LIMITBY6'DBFILE
     C**                   MOVELWWCNUM    DBKEY
     C**                   EXSR #DBERR
      *
     C                     END                             - E2
      *
      **=================================================================
      *
     C                     END                             - E2
      **=================================================================
      *
     C                     MOVE '1'       *INLR                       208821
     C                     RETRN
      **=================================================================
      **
      **  #INIT       Program initialisation sub-routine                STANDING
      **
      *----------------------------------------------------------------
     C           #INIT     BEGSR
      *----------------------------------------------------------------
      *                                                                *****
     C           *NAMVAR   DEFN           LDA
     C                     UNLCKLDA
      *
      ** Parameters received/transfered to Main Component:             *****
     C           *ENTRY    PLIST
      *  Input Parameters
     C                     PARM           EXLOTD  3
     C                     PARM           EXCUST  6
     C                     PARM           EXCUCC  2
     C                     PARM           EXBNBI  1                       ERL365
      *  Output Parameters
     C                     PARM           EXCUCL  3
     C                     PARM           EXCONS  10
     C                     PARM           EXECOS  40
     C                     PARM           EXNCOS  40                      ULX004
     C                     PARM           EXCNCZ  2                       ERL319
     C***********          PARM           EXCGRP  60                                  ER0336 CSD027A
     C                     PARM           EXCGRP  6                                          CSD027A
     C                     PARM           EXPAPS  50                      ERL371
     C                     PARM           EXNTAG  10                      ULX004
     C                     PARM           EXCGRN 20                       ULX004
     C                     PARM           EXCGLC  2                       ULX004
     C                     PARM           EXPERF  1                       ULX004
     C                     PARM           EXSMRS  1                       187689
     C                     PARM           EXMCID  1                       ULX048
     C                     PARM           ERROR   1                       ERL432
      *
     C                     MOVE '0'       *INU7
      *
     C                     ENDSR
      *================================================================
      **
      **  #DBERR      DATA BASE ERROR
      **
      *----------------------------------------------------------------
     C           #DBERR    BEGSR
      *----------------------------------------------------------------
      **
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
     C                     MOVE 'Y'       ERROR                           ERL432
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
      **
     C                     ENDSR
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2005
