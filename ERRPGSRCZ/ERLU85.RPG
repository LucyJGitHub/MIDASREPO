     H
     F********************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('MIDAS ER Production Extract Report')
     F********************************************************************
     F*                                                                  *
     F*          MIDAS ABS - EUROPEAN REPORTING                          *
     F*                                                                  *
     F*    ERLU85 - Production Extract - Audit Report                    *
     F*                                                                  *
      *  (c) Finastra International Limited 2005                      *
     F*                                                                  *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Prev Amend No. 225555             Date 01Nov06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 06May06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX046             Date 31Oct02               *
      *                 IL0040             Date 14Jan02               *
     F*                   182312            Date 14Feb01                 *
     F*                   187689            Date 20Dec00                 *
     F*                   157278            DATE 21FEB00                 *
     F*                   CAD025 PT         DATE 08JAN98                 *
     F*                   ULX004            Date 22apr98                 *
     F*                   132893            DATE 15APR98                 *
     F*                   88390  PT         DATE 08JUN95                 *
     F*                   61870             DATE 20MAI94                 *
     F*                   64630  DDJ        DATE 22FEB94                 *
     F*                   61875  TLi        DATE 19OCT93                 *
     F*                   61870  TLi        DATE 15OCT93                 *
     F*                   61874  TLi        DATE 15OCT93                 *
     F*                   ERL356 TLi        DATE 01JUL93                 *
     F*                   ERL343 TLi        DATE 27MAY93                 *
     F*                   ERL349 TLI        DATE 03JUN93                 *
     F*                   ER0336 TLi        DATE 16APR93                 *
     F*                   ERL334 TLi        DATE 01MAR93                 *
     F*                   ERL323 AF         DATE 03MAR93                 *
     F*                   ERL303 TLI        DATE 21JAN93                 *
     F*                   ERL202 ADs        DATE 14JAN93                 *
     F*                   ERL005 ADs        DATE 22DEC92                 *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *    225555 - Add discount/premium amount, amount to amortize      *
      *             and 'Prix d'Acquisition'. Recompile over changed     *
      *             ERLUTRPD.                                            *
      *  CSD027A - Conversion Of Customer Number to Alpha (Recompile) *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F*    ULX046 - Circulars 2002/170-174-175 Integration               *
     F*             Recompiled due to changes in file layout             *
     F*    IL0040 - ULX043 IBLC 2002. The monthly IBLC is no more        *
     F*             required                                             *
     F*    182312 - Credit interest to accrue amounts must be set        *
     F*             positive to get correct totals                       *
     F*    187689 - "GRD" currency in EMU                                *
     F*             Just recompile over new PF/ERLUTRPD description      *
     F*    157278 - FOR ACCOUNT (EXLOTD=GLA') PRINT CORRECT AMOUNT       *
     F*    CAD025 - To print 'CUS.POSITION4 for customer positions       *
     F*             consolidated records.                                *
     F*    ULX004 - IML Circular 97/138                                  *
     F*             Recompile only                                       *
     F*    132893 - SPF/RCF for IBLC/BNB on DBA2.3                       *
     F*    88390  - Report margin amount for codes 302410 & 302420       *
     F*    61870  - RECOMPILE FOR CHANGE OF LENGTH OF IML RUBRIQUE NAME  *
     F*    64630  - FOR SE Security Report Name never initialised        *
     F*    61875  - NEW LIST WITH ASSETS AND LIABILITIES                 *
     F*    61870  - PRINT THE IML RUBRIQUE NAME                          *
     F*    61874  - AMOUNTS SHOULD BE EDITED                             *
     F*    ERL356 - ADD ECONOMIQUE SECTOR CODE AND SUPPRESS CENTURY      *
     F*    ERL343 - IBLC AUDIT LIST                                      *
     F*    ERL349 - RECOMPILED Due to change between EXCUCL and EXCNCZ   *
     F*    ER0336 - IML 2.3 DEVELOPMENT - Add IML rubrique on each page  *
     F*    ERL334 - For Securities print Security Report Name            *
     F*    ERL323 - Add total by currency                                *
     F*    ERL303 - New layout - Only Guaranties                         *
     F*    ERL202 - Recompilation due to LF/ERLUTRL0 changement          *
     F*    ERL005 - Audit List corrections.                              *
     F*                                                                  *
     F********************************************************************
     FIMLNARPDIF  E           K        DISK                               61870
      ** Narrative for IML code                                           61870
     F*                                                                   61870
     F***ERLUTRL0IF**E           K        DISK                                                CER001
     FERLTRXL0IF  E           K        DISK                                                   CER001
      **  Production Extract
     FERLU85P1O   E             97     PRINTER
     F                                              KINFDS SPOOL1         132893
     FERLU86P1O   E             98     PRINTER                            ERL005
     F******                                        KINFDS PRTDS
     F                                              KINFDS SPOOL2         132893
     FERLU85AUO   E                    PRINTER      KINFDS SPOOLU         132893
     F*                                                                   132893
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*
     F*       97         OVERFLOW INDICATOR FOR ERLU85P1
     F*       98         OVERFLOW INDICATOR FOR ERLU86P1                  ERL005
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F**               *************************                         *
     F**               ** INDICATORS NOT USED **                         *
     F**               ** ------------------- **                         *
     F**               *************************                         *
     F**                                                                 *
     F**      ***************************************************        *
     F**      * 01   02   03   04   05   06   07   08   09   10 *        *
     F**      * 11   12   13   14   15   16   17   18   19   20 *        *
     F**      * 21   22   23   24   25   26   27   28   29   30 *        *
     F**      * 31   32   33   34   35   36   37   38   39   40 *        *
     F**      * 41   42   43   44   45   46   47   48   49   50 *        *
     F**      * 51   52   53   54   55   56   57   58   59   60 *        *
     F**      * 61   62   63   64   65   66   67   68   69   70 *        *
     F**      * 71   72   73   74   75   76   77   78   79   80 *        *
     F**      * 81   82   83   84   85   86   87   88   89   90 *        *
     F**      * 91   92   93   94   95   96   97   98   99      *        *
     F**      ***************************************************        *
     F********************************************************************
      *
      /EJECT
      *
      *
      ** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      *
      /EJECT
      *
     I****PRTDS       DS                                                  132893
      *
      **  Printer file information data structure.
     I****                                B 152 1530PAGLEN                132893
     I****                                B 188 1890OVRLIN                132893
     I****                                B 367 3680CURLIN                132893
      *
     ICPYR@#      DS
      *
      **  Data structure for compilation - COPYRIGHT INSERTION
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      *
     I            DS
      *
      **  Data structure for Start Date
     I                                        1   80EXSTRD
     I                                        1   4 YY1
     I                                        5   6 MM1
     I                                        7   8 DD1
     I            DS
      *
      **  Data structure for End Date
     I                                        1   80EXENDD
     I                                        1   4 YY2
     I                                        5   6 MM2
     I                                        7   8 DD2
     I            DS
      *
      **  Data structure for Revision Date
     I                                        1   80EXREVD
     I                                        1   4 YY3
     I                                        5   6 MM3
     I                                        7   8 DD3
     I            DS
      *
      **  Data structure for Start Date new format to print
     I****************************************1   8 P1STRD                ERL356
     I                                        1   6 P1STRD                ERL356
     I                                        1   2 STDD
     I                                        3   4 STMM
     I****************************************5   8 STYY                  ERL356
     I                                        5   6 STYY                  ERL356
     I            DS
      *
      **  Data structure for End Date new format to print
     I****************************************1   8 P1ENDD                ERL356
     I                                        1   6 P1ENDD                ERL356
     I                                        1   2 EDDD
     I                                        3   4 EDMM
     I****************************************5   8 EDYY                  ERL356
     I                                        5   6 EDYY                  ERL356
     I            DS
      *
      **  Data structure for Revision Date new format to print
     I****************************************1   8 P1REVD                ERL356
     I                                        1   6 P1REVD                ERL356
     I                                        1   2 REDD
     I                                        3   4 REMM
     I****************************************5   8 REYY                  ERL356
     I                                        5   6 REYY                  ERL356
      *                                                                   132893
     ILDA         DS                            256                       132893
     I*  DATA AREA DATA STRUCTURE FOR HANDLING DB ERRORS                  132893
     I*                                                                   132893
     I                                      134 141 DBFILE                132893
     I                                      142 170 DBKEY                 132893
     I                                      171 180 DBPGM                 132893
     I                                      181 183 DBASE                 132893
     ISPOOLU      DS                                                      132893
      *                                                                   132893
      **  File Information Data Structure - AU report                     132893
     I                                       83  92 SFILEU                132893
     I                                    B 123 1240SFNUMU                132893
     ISPOOL1      DS                                                      132893
      *                                                                   132893
      **  File Information Data Structure - P1 report                     132893
     I                                       83  92 SFIL1                 132893
     I                                    B 123 1240SFNUM1                132893
     I                                    B 152 1530PAGLEN                132893
     I                                    B 188 1890OVRLIN                132893
     I                                    B 367 3680CURLIN                132893
     ISPOOL2      DS                                                      132893
      *                                                                   132893
      **  File Information Data Structure - P1 report                     132893
     I                                       83  92 SFIL2                 132893
     I                                    B 123 1240SFNUM2                132893
     I                                    B 152 1530PAGLE2                132893
     I                                    B 188 1890OVRLI2                132893
     I                                    B 367 3680CURLI2                132893
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -                                                       *
      * PRTLGN   - S/R TO PRINT REPORT DETAIL LIGNE                      *
      * INIT     - S/R TO INITIALIZE STATIC FIELDS                       *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      *                                                                  *
      ********************************************************************
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C*----------------- FIRST CYCLE -----------------------------
     C*
      *
     C                     EXSR INIT
      *
     C********** *LOVAL    SETLLERLUTRL0                                                      CER001
     C**********           READ ERLUTRL0                 81                                   CER001
     C           *LOVAL    SETLLERLTRXL0                                                      CER001
     C                     READ ERLTRXL0                 81                                   CER001
      *
     C           *IN81     IFEQ '1'
     C******               WRITEERLU85H1                                  132893
     C******               WRITEERLU85E1                                  132893
     C                     WRITEHEADAU                                    132893
     C                     WRITENODTLS                                    132893
     C                     WRITEERLU86H1                                  ERL005
     C                     WRITEERLU86E1                                  ERL005
     C                     END
      *
     C                     Z-ADDEXSCHA    BKSCHA  80
     C                     Z-ADDEXRECT    BKRECT  30                      ERL005
     C                     MOVE EXCCYL    BKCCYL  3                       ERL323
     C*
     C*------------ DETAIL PROCESSING ----------------------------
     C*
      *
      **  Write report header
     C           *IN81     IFEQ '0'
     C*
     C** RETRIEVE IML CODE NARRATIVE                                      61870
     C           EXSCHA    CHAINIMLNARPD             88                   61870
     C           *IN88     IFEQ '1'                                       61870
     C                     MOVE *BLANKS   IMLNAR                          61870
     C                     END                                            61870
     C*                                                                   61870
     C                     WRITEERLU85H1
     C                     WRITEERLU85H2
     C                     WRITEERLU85H3
     C                     WRITEERLU86H1                                  ERL005
     C                     WRITEERLU86H2                                  ERL005
     C                     WRITEERLU86H3                                  ERL005
     C                     END
      *
      ****Do*until*end*of*file*ERLUTRL0                                                       CER001
      **Do until end of file ERLTRXL0                                                         CER001
     C           *IN81     DOWEQ'0'
      *
     C                     MOVEL'0'       *IN50
      *
      **  If there is not enough space on the physical page
      **  for this format :
     C           PAGLEN    SUB  CURLIN    WRKPAG  40
     C           WRKPAG    IFLE 8
      *
      **  Signal artificial overflow
     C                     SETON                     97
      *
      **  Print page header
     C                     WRITEERLU85H1
     C                     WRITEERLU86H1                                  ERL005
     C           EXSCHA    IFNE BKSCHA
     C           EXRECT    ORNE BKRECT                                    ERL005
      *                                                                   ERL323
     C                     MOVE WKTOT1    P1TOT1                          ERL323
      *                                                                   ERL323
     C           WKTOT1    IFGE 0                                         ERL323
     C                     MOVE 'D'       P3DRCR                          ERL323
     C                     ELSE                                           ERL323
     C                     MOVE 'C'       P3DRCR                          ERL323
     C                     END                                            ERL323
     C                     WRITEERLU85D3                                  ERL323
     C                     WRITEERLU86D3                                  ERL323
     C                     Z-ADD0         P1TOT1                          ERL323
     C                     Z-ADD0         WKTOT1                          ERL323
      *                                                                   ERL323
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *
     C                     WRITEERLU85D2
     C*********************WRITEERLU85H2                                  ER0336
     C                     WRITEERLU86D2                                  ERL005
     C*********************WRITEERLU86H2                           ERL005 ER0336
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
     C                     END
      *
     C                     WRITEERLU85H2                                  ER0336
     C                     WRITEERLU86H2                                  ER0336
     C                     WRITEERLU85H3
     C                     WRITEERLU86H3                                  ERL005
     C                     MOVEL'0'       *IN50
     C                     END
      *
      **  Print ligne data
     C                     EXSR PRTLGN
      *
      **  Save Code Schema A to check if rupture
     C                     Z-ADDEXSCHA    BKSCHA  80
      **  Save Record Type to check if rupture                            ERL005
     C                     Z-ADDEXRECT    BKRECT  30                      ERL005
      **  Save Currency to check if rupture                               ERL323
     C                     MOVE EXCCYL    BKCCYL  3                       ERL323
      *
      **  Read next record
     C**********           READ ERLUTRL0                 81                                   CER001
     C                     READ ERLTRXL0                 81                                   CER001
      *
     C           EXSCHA    IFNE BKSCHA                                    61870
     C           EXSCHA    CHAINIMLNARPD             88                   61870
     C           *IN88     IFEQ '1'                                       61870
     C                     MOVE *BLANKS   IMLNAR                          61870
     C                     END                                            61870
     C                     END                                            61870
      *
      **  End CC1
     C                     END
      *
     C           *IN81     IFEQ '1'
     C           EXSCHA    ANDNE0
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *                                                                   ERL323
     C                     MOVE WKTOT1    P1TOT1                          ERL323
      *                                                                   ERL323
     C           WKTOT1    IFGE 0                                         ERL323
     C                     MOVE 'D'       P3DRCR                          ERL323
     C                     ELSE                                           ERL323
     C                     MOVE 'C'       P3DRCR                          ERL323
     C                     END                                            ERL323
      *                                                                   ERL323
     C                     WRITEERLU85D3                                  ERL323
      *
     C                     WRITEERLU85D2
     C                     WRITEERLU85E2
     C                     WRITEERLU86D2                                  ERL005
     C                     WRITEERLU86E2                                  ERL005
     C                     END
      *
      ** CALL NEW LAYOUT PROGRAM ONLY FOR GUARANTIES                      ERL303
     C                     CALL 'ERLU87'                                  ERL303
     C                     PARM           BJMRDT                          ERL303
     C                     PARM           BJURPT                          ERL303
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RLEV   1                       132893
      *
      ** CALL NEW LIST PROGRAM WITH ASSETS AND LIABILITIES                61875
     C                     CALL 'ERLU90'                                  61875
     C                     PARM           BJMRDT                          61875
     C                     PARM           BJURPT                          61875
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RLEV   1                       132893
      *
      *                                                            ERL343  IL0040
      ** CALL IBLC EXTRACT PROGRAM                                 ERL343  IL0040
     C*                    CALL 'ERLUIB89'                         ERL343  IL0040
      *                                                            ERL343  IL0040
      ** CALL IBLC AUDIT LIST                                      ERL343  IL0040
     C*                    CALL 'ERLU89'                           ERL343  IL0040
     C*                    PARM           BJMRDT                   ERL343  IL0040
     C*                    PARM           BJURPT                   ERL343  IL0040
     C*                    PARM           @RSEQ   5                132893  IL0040
     C*                    PARM           @RLEV   1                132893  IL0040
      *
     C                     MOVEL'1'       *INLR
      *
     C                     RETRN
      *
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * PRTLGN   - S/R TO PRINT REPORT DETAIL LIGNE                      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * GLZADD   - S/R TO ADD AMOUNT (ZZAMT) TO TOTAL (ZZTOTI,ZZTOTD)    *
      * ZSEDIT   - S/R TO INSERT DEC. POINT AND SIGN INTO NUM. FIELD     *
      * ZDATE2   - S/R TO CONVERT A DAY NUMBER TO DATE FORMATS           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           PRTLGN    BEGSR
      *
      **  Load detail line
      *
      **  Accumulate controls
     C           RECTOT    ADD  1         RECTOT
      *                                                                   ERL323
     C           EXCCYL    IFNE BKCCYL                                    ERL323
     C           EXCCYL    OREQ BKCCYL                                    ERL323
     C           EXSCHA    ANDNEBKSCHA                                    ERL323
     C                     MOVE WKTOT1    P1TOT1                          ERL323
      *                                                                   ERL323
     C           WKTOT1    IFGE 0                                         ERL323
     C                     MOVE 'D'       P3DRCR                          ERL323
     C                     ELSE                                           ERL323
     C                     MOVE 'C'       P3DRCR                          ERL323
     C                     END                                            ERL323
      *                                                                   ERL323
     C                     WRITEERLU85D3                                  ERL323
     C                     WRITEERLU86D3                                  ERL323
     C                     Z-ADD0         P1TOT1                          ERL323
     C                     Z-ADD0         WKTOT1                          ERL323
     C                     END                                            ERL323
      *                                                                   ERL323
      *
      **  If there is not enough space on the physical page
      **  for this format :
     C           PAGLEN    SUB  CURLIN    WRKPAG  40
     C           WRKPAG    IFLE 8
      *
      **  Signal artificial overflow
     C                     SETON                     97
      *
      **  Print page header
     C                     WRITEERLU85H1
     C                     WRITEERLU86H1                                  ERL005
     C           EXSCHA    IFNE BKSCHA
     C           EXRECT    ORNE BKRECT                                    ERL005
      *                                                                   ERL323
     C                     MOVE WKTOT1    P1TOT1                          ERL323
      *                                                                   ERL323
     C           WKTOT1    IFGE 0                                         ERL323
     C                     MOVE 'D'       P3DRCR                          ERL323
     C                     ELSE                                           ERL323
     C                     MOVE 'C'       P3DRCR                          ERL323
     C                     END                                            ERL323
     C                     WRITEERLU85D3                                  ERL323
     C                     WRITEERLU86D3                                  ERL323
     C                     Z-ADD0         P1TOT1                          ERL323
     C                     Z-ADD0         WKTOT1                          ERL323
      *                                                                   ERL323
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *
     C                     WRITEERLU85D2
     C*********************WRITEERLU85H2                                  ER0336
     C                     WRITEERLU86D2                                  ERL005
     C*********************WRITEERLU86H2                           ERL005 ER0336
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
     C                     END
     C                     WRITEERLU85H2                                  ER0336
     C                     WRITEERLU86H2                                  ER0336
     C                     WRITEERLU85H3
     C                     WRITEERLU86H3                                  ERL005
     C                     MOVEL'0'       *IN50
     C                     END
      *
      **  Print report detail line
     C           EXSCHA    IFNE BKSCHA
     C           EXRECT    ORNE BKRECT                                    ERL005
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *
     C                     WRITEERLU85D2
     C                     WRITEERLU85H1
     C*********************WRITEERLU85H2                                  ER0336
     C                     WRITEERLU86D2                                  ERL005
     C                     WRITEERLU86H1                                  ERL005
     C*********************WRITEERLU86H2                           ERL005 ER0336
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
     C                     WRITEERLU85H2                                  ER0336
     C                     WRITEERLU86H2                                  ER0336
     C                     WRITEERLU85H3
     C                     WRITEERLU86H3                                  ERL005
     C                     END
      *
      **  Reset date format in DDMMYYY for start date
     C           EXSTRD    IFEQ 0
     C                     MOVE *BLANK    P1STRD
     C                     ELSE
     C                     MOVE DD1       STDD
     C                     MOVE MM1       STMM
     C                     MOVE YY1       STYY
     C                     END
      *
      **  Reset date format in DDMMYYY for end date
     C           EXENDD    IFEQ 0
     C                     MOVE *BLANK    P1ENDD
     C                     ELSE
     C                     MOVE DD2       EDDD
     C                     MOVE MM2       EDMM
     C                     MOVE YY2       EDYY
     C                     END
      *
      **  Reset date format in DDMMYYY for revision date
     C           EXREVD    IFEQ 0
     C                     MOVE *BLANK    P1REVD
     C                     ELSE
     C                     MOVE DD3       REDD
     C                     MOVE MM3       REMM
     C                     MOVE YY3       REYY
     C                     END
      *                                                                   182312
     C           EXSCHA    IFEQ 99000000                                  182312
     C           EXAMNT    ANDLT0                                         182312
     C                     Z-SUBEXAMNT    EXAMNT                          182312
     C                     END                                            182312
      *
     C           EXSCHA    IFEQ 30240000                                  ER0336
     C           EXSCHA    OREQ 30241000                                  88390
     C           EXSCHA    OREQ 30242000                                  88390
     C           EXLOTD    IFNE 'GLA'                      B1             157278
     C                     MOVE EXMARA    P1AMNT                          ER0336
     C                     MOVE EXLMAR    P1LAMT                          ER0336
     C                     ELSE                            X1             157278
     C                     MOVE EXAMNT    P1AMNT                          157278
     C                     MOVE EXLAMT    P1LAMT                          157278
     C                     ENDIF                           E1             157278
     C                     ELSE                                           ER0336
     C                     MOVE EXAMNT    P1AMNT
     C                     MOVE EXLAMT    P1LAMT
     C                     END                                            ER0336
      *
     C           EXDRCR    IFEQ 0
      *
     C           EXSCHA    IFEQ 30240000                                  ER0336
     C           EXSCHA    OREQ 30241000                                  88390
     C           EXSCHA    OREQ 30242000                                  88390
     C           EXLOTD    IFNE 'GLA'                                     157278
     C                     ADD  EXLMAR    WKTOT2                          ER0336
     C                     ADD  EXMARA    WKTOT1                          ER0336
     C                     ELSE                                           157278
     C                     ADD  EXLAMT    WKTOT2                          157278
     C                     ADD  EXAMNT    WKTOT1                          157278
     C                     ENDIF                                          157278
     C                     ELSE                                           ER0336
     C                     ADD  EXLAMT    WKTOT2 160
     C                     ADD  EXAMNT    WKTOT1 160                      ERL323
     C                     END                                            ER0336
      *
     C                     ELSE
      *
     C           EXSCHA    IFEQ 30240000                                  ER0336
     C           EXSCHA    OREQ 30241000                                  88390
     C           EXSCHA    OREQ 30242000                                  88390
     C           EXLOTD    IFNE 'GLA'                                     157278
     C                     SUB  EXLMAR    WKTOT2                          ER0336
     C                     SUB  EXMARA    WKTOT1                          ER0336
     C                     ELSE                                           157278
     C                     SUB  EXLAMT    WKTOT2                          157278
     C                     SUB  EXAMNT    WKTOT1                          157278
     C                     ENDIF                                          157278
     C                     ELSE                                           ER0336
     C                     SUB  EXLAMT    WKTOT2
     C                     SUB  EXAMNT    WKTOT1                          ERL323
     C                     END                                            ER0336
      *
     C                     END
      *
     C           EXDRCR    IFEQ 0
     C                     MOVE 'D'       P1DRCR
     C                     ELSE
     C                     MOVE 'C'       P1DRCR
     C                     END
      *                                                                   ERL005
     C                     MOVE EXORIG    EXTREF                          ERL005
      *
      ** FOR SECURITIES PRINT SECURITY REPORT NAME                        ERL334
     C           EXLOTD    IFEQ 'SEC'                                     ERL334
     C           EXLOTD    OREQ 'SEF'                                     ERL334
     C                     MOVE '1'       *IN60                           ERL334
     C*                    MOVELEXSECY    WWSECD                          ER0336
     C                     MOVELEXSECY    WWSECD                          64630
     C                     ELSE                                           ERL334
     C                     MOVE '0'       *IN60                           ERL334
     C                     END                                            ERL334
      *
     C                     MOVELEXCRNM    WWCRNM                          61874
      *
     C           EXLOTD    IFEQ 'CPO'                                     CAD025
     C           ULX046    ANDNE'Y'                                       ULX046
     C                     MOVE *BLANKS   EXCUST                          CAD025
     C                     MOVEL'CUS.'    WWCRNM                          CAD025
     C                     MOVE 'POSITION'WWCRNM                          CAD025
     C                     END                                            CAD025
      *                                                                   CAD025
     C                     WRITEERLU85D1
     C                     WRITEERLU86D1                                  ERL005
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INIT     - S/R TO INITIALIZE STATIC FIELDS                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           INIT      BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           BJMRDT  7
     C                     PARM           BJURPT 53
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RLEV   1                       132893
      *                                                                   132893
      **  PREPARE LDA:                                                    132893
     C           *NAMVAR   DEFN           LDA                             132893
     C                     MOVEL'ERLU85P' DBPGM                           132893
      *                                                                   132893
      * Record Printer file ERLU85AU on RCF                               132893
     C                     Z-ADDSFNUMU    ZSNUM                           132893
     C                     MOVELSFILEU    SFILE                           132893
      *                                                                   132893
     C                     EXSR SRZSFL                                    132893
      *                                                                   132893
      * Record Printer file ERLU85P1 on RCF                               132893
     C                     Z-ADDSFNUM1    ZSNUM                           132893
     C                     MOVELSFIL1     SFILE                           132893
      *                                                                   132893
     C                     EXSR SRZSFL                                    132893
      *                                                                   132893
      * Record Printer file ERLU86P1 on RCF                               132893
     C                     Z-ADDSFNUM2    ZSNUM                           132893
     C                     MOVELSFIL2     SFILE                           132893
      *                                                                   132893
     C                     EXSR SRZSFL                                    132893
      *
      *                                                                   ULX046
      ***  Check if the feature ULX046 is installed.                      ULX046
      *                                                                   ULX046
     C                     MOVEL'N'       ULX046  1                       ULX046
     C                     CALL 'AOSARDR0'                                ULX046
     C                     PARM *BLANKS   @RTCD   7                       ULX046
     C                     PARM '*VERIFY' @OPTN   7                       ULX046
     C                     PARM 'ULX046'  @SARD   6                       ULX046
      *                                                                   ULX046
     C           @RTCD     IFEQ *BLANKS                                   ULX046
     C                     MOVEL'Y'       ULX046                          ULX046
     C                     ENDIF                                          ULX046
      *
      **  Initialize Field
     C                     Z-ADD0         P1TOT1                          ERL323
     C                     Z-ADD0         WKTOT1                          ERL323
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
      *
      **  Initialize indicators
     C                     MOVEL'0'       *IN
      *
      *
      **  Define/Clear constante & variable
     C                     Z-ADD1         RECTOT  50       Total report
      *
      **  Adjust page lengh and overflow as necessary
      **  ERLU85P1                                                        132893
      *                                                                   132893
     C           PAGLEN    SUB  2         WPAGL   40
      *
     C           OVRLIN    IFGE WPAGL
     C           WPAGL     SUB  2         WOVRL   40
     C                     ELSE
     C                     Z-ADDOVRLIN    WOVRL
     C                     END
      *
      **  ERLU86P1                                                        132893
      *                                                                   132893
     C           PAGLE2    SUB  2         WPAGL2  40                      132893
      *                                                                   132893
     C           OVRLI2    IFGE WPAGL2                                    132893
     C           WPAGL2    SUB  2         WOVRL2  40                      132893
     C                     ELSE                                           132893
     C                     Z-ADDOVRLI2    WOVRL2                          132893
     C                     END                                            132893
      *                                                                   132893
     C                     ENDSR
      /EJECT
      ********************************************************************
      *                                                                  *
      * GLZADD   - S/R TO ADD AMOUNT (ZZAMT) TO TOTAL (ZZTOTI,ZZTOTD)    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * GLZSUM   - S/R TO CARRY OUT ADD. FOR SR GLZADD,GLZSUB,GLZCMP     *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * PRTLGN   - S/R TO PRINT REPORT DETAIL LIGNE                      *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
      * NOTE :  IND '99' IS SET BY AN OVERFLOW ERROR                     *
      ********************************************************************
     C           GLZADD    BEGSR                           *** GLZADD ****
     C*
     C                     Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     C   91                GOTO ZZAEND                     AMT = 0.BYPASS
     C*
     C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
     C                     Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
     C                     MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
     C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
     C*
     C                     EXSR GLZSUM
     C           ZZAEND    ENDSR                           ** ZZAEND TAG *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * GLZSUM   - S/R TO CARRY OUT ADD. FOR SR GLZADD,GLZSUB,GLZCMP     *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * GLZADD   - S/R TO ADD AMOUNT (ZZAMT) TO TOTAL (ZZTOTI,ZZTOTD)    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
      *                                                                  *
      *          PARAMETERS PASSED -                                     *
      *                     I/P ZZAMTI ZZAMTD                            *
      *                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.      *
      *                                                                  *
      ********************************************************************
     C           GLZSUM    BEGSR                           *** GLZSUM ****
     C*
     C                     Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     C                     Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     C                     SETOF                     919293
     C                     SETOF                     949599
     C*
     C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
     C           ZZAMTI    COMP 0                      9293
     C   93      ZZAMTD    COMP 0                      9293
     C   93                GOTO ZZSEND                     ZERO BYPASS
     C*
     C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
     C           ZZTOTI    COMP 0                      9193
     C   93      ZZTOTD    COMP 0                      9193
     C*
     C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
     C   93                Z-ADDZZAMTI    ZZTOTI
     C   93                Z-ADDZZAMTD    ZZTOTD
     C   93                GOTO ZZSEND                     ZERO BYPASS
     C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
     C   91N92
     CORN91 92             GOTO ZZOFPS
     C*
     C           ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     C           ZZWK2     COMP 999                  93    '93' = CARRY
     C  N93      ZZWK2     COMP -999                   93    INTO INTEGER.
     C*
     C   93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     C      93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     C   93      ZZTOTI    ADD  ZZWK3     ZZWK3
     C  N93      ZZTOTI    ADD  ZZAMTI    ZZWK3
     C*
     C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
     C  N92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     C   92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     C  N99                Z-ADDZZWK2     ZZTOTD
     C  N99                Z-ADDZZWK3     ZZTOTI
     C*
     C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
     C   99                Z-ADD0         ZZAMT  153
     C                     GOTO ZZSEND
     C*
     C* THE 'SIGNS' ARE DIFFERENT
     C           ZZOFPS    TAG                             ** ZZOFPS TAG**
     C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
     C*
     C   92                Z-SUBZZAMTI    ZZAMTI 150
     C   92                Z-SUBZZAMTD    ZZAMTD  30
     C*
     C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
     C*
     C   91                Z-SUBZZTOTI    ZZTOTI
     C   91                Z-SUBZZTOTD    ZZTOTD
     C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
     C*
     C           ZZTOTI    COMP ZZAMTI               93  95
     C   95      ZZTOTD    COMP ZZAMTD               93  95
     C*
     C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
     C   95                Z-ADD0         ZZTOTI
     C   95                Z-ADD0         ZZTOTD
     C   95                GOTO ZZSEND
     C*
     C* IF ZZTOT GT. ZZAMT.
     C   93      ZZAMTD    COMP ZZTOTD               94
     C   93 94   ZZTOTI    SUB  1         ZZTOTI
     C   93 94   ZZTOTD    ADD  1000      ZZWK2
     C   93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     C   93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     C   93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
     C*
     C*
     C* IF ZZAMT GT. ZZTOT.
     C  N93      ZZTOTD    COMP ZZAMTD               94
     C  N93 94   ZZAMTI    SUB  1         ZZWK3  150
     C  N93 94   ZZAMTD    ADD  1000      ZZWK2
     C  N93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     C  N93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     C  N93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     C  N93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
     C*
     C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
     C                     SETOF                     94
     C   93 91
     CORN93 92             SETON                     94
     C   94                Z-SUBZZTOTI    ZZTOTI
     C   94                Z-SUBZZTOTD    ZZTOTD
     C**
     C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
     C   92                Z-SUBZZAMTI    ZZAMTI
     C   92                Z-SUBZZAMTD    ZZAMTD
     C           ZZSEND    TAG                             ** ZZSEND TAG *
     C**
     C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
     C                     SETOF                     96
     C           ZZTOTD    COMP 0                        91
     C   91      ZZTOTI    COMP 0                      96
     C   96                MOVE '.000-'   ZZNEGD  5
     C                     ENDSR                             ** ENDSR **
      ********************************************************************
     C*****************************************************************   132893
     C* SRZSFL - Subroutine to call ZSFILE                                132893
     C*****************************************************************   132893
     C*                                                                   132893
     C           SRZSFL    BEGSR                                          132893
     C*                                                                   132893
     C                     CALL 'ZSFILE'                                  132893
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RENT   3                       132893
     C                     PARM           SFILE  10                       132893
     C                     PARM           ZSNUM   60                      132893
     C                     PARM           ZSERR   1                       132893
     C*                                                                   132893
     C** If an error occurred during ZSFILE processing,                   132893
     C** return to the calling program.                                   132893
     C*                                                                   132893
     C           ZSERR     IFEQ 'Y'                                       132893
     C                     SETON                     U7U8LR               132893
     C                     RETRN                                          132893
     C                     END                                            132893
     C*                                                                   132893
     C                     ENDSR                                          132893
      ****************************************************************    132893
      /EJECT                                                              132893
**  CPY@
(c) Finastra International Limited 2005
