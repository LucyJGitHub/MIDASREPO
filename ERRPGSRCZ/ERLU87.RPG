     H
     F********************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER Production Extract Report-Only Guaranties')
     F********************************************************************
     F*                                                                  *
     F*    Midas - European Reporting                                    *
     F*                                                                  *
     F*    ERLU87 - Production Extract - Audit Report - Only Guaranties  *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
     F*                                                                  *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *  Last Amend No. 225555             Date 01Nov06                  *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027A            Date 06May06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX046             Date 31Oct02               *
      *                 187689             Date 20Dec00               *
     F*                   132893            DATE 15APR98                 *
     F*                   ULX004            DATE 26MAR97                 *
     F*                   ERL425 PT         DATE 25AUG94                 *
     F*                   71013  DDJ        DATE 15OCT93                 *
     F*                   61874  TLI        DATE 19JAN94                 *
     F*                   ERL371 TLI        DATE 13JAN94                 *
     F*                   ERL349 TLI        DATE 03JUN93                 *
     F*                   ER0336 TLI        DATE 16APR93                 *
     F*                   ERL334 TLi        DATE 01MAR93                 *
     F*                   ERL305 TLi        DATE 26JAN93                 *
     F*                   ERL303 TLi        DATE 21JAN93                 *
     F*                                                                  *
     F********************************************************************
      *                                                                  *
      *    225555 - Add discount/premium amount, amount to amortize      *
      *             and 'Prix d'Acquisition'. Recompile over changed     *
      *             ERLUTRPD.                                            *
      *  CSD027A - Conversion Of Customer Number to Alpha (Recompile) *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     F*    ULX046 - Circulars 2002/170-174-175 Integration               *
     F*             Recompiled due to changes in file layout             *
     F*    187689 - "GRD" currency in EMU                                *
     F*             Just recompile over new PF/ERLUTRPD description      *
     F*    132893 - SPF/RCF for IBLC/BNB on DBA2.3                       *
     F*    ULX004 - CAPITAL ADEQUACY                                     *
     F*           - IML Circular 97/138                                  *
     F*             Recompile only                                       *
     F*    ERL425 - TO PRINT GUARANTOR DETAILS INSTEAD OF COUNTERPARTY   *
     F*    71013  - CHANGE LENGTH OF IML RUBRIC (RECOMPILE)              *
     F*    61874  - AMOUNTS SHOULD BE EDITED                             *
     F*    ERL371 - IML 1.4 DEVELOPMENT                                  *
     F*    ERL349 - RECOMPILED Due to change between EXCUCL and EXCNCZ   *
     F*    ER0336 - IML 2.3 DEVELOPMENT - Add total by Guarantee type    *
     F*    ERL334 - For Securities print Security Report Name            *
     F*    ERL305 - Add Guarantie Type on report                         *
     F*    ERL303 - Written for new layout - Only Guaranties             *
     F********************************************************************
     FIMLNARPDIF  E           K        DISK                               71013
      ** Narrative for IML code                                           71013
     F*                                                                   71013
     F***ERLUTRL1IF**E           K        DISK                                                CER001
     FERLTRXL1IF  E           K        DISK                                                   CER001
      **  Production Extract - Only Guaranties
     FERLU87P1O   E             97     PRINTER
     F                                              KINFDS SPOOL1         132893
     FERLU88P1O   E             98     PRINTER
     F******                                        KINFDS PRTDS
     F                                              KINFDS SPOOL2         132893
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*
     F*       97         OVERFLOW INDICATOR FOR ERLU87P1
     F*       98         OVERFLOW INDICATOR FOR ERLU88P1
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F**               *************************                         *
     F**               ** INDICATORS NOT USED **                         *
     F**               ** ------------------- **                         *
     F**               *************************                         *
     F**                                                                 *
     F**      ***************************************************        *
     F**      * 01   02   03   04   05   06   07   08   09   10 *        *
     F**      * 11   12   13   14   15   16   17   18   19   20 *        *
     F**      * 21   22   23   24   25   26   27   28   29   30 *        *
     F**      * 31   32   33   34   35   36   37   38   39   40 *        *
     F**      * 41   42   43   44   45   46   47   48   49   50 *        *
     F**      * 51   52   53   54   55   56   57   58   59   60 *        *
     F**      * 61   62   63   64   65   66   67   68   69   70 *        *
     F**      * 71   72   73   74   75   76   77   78   79   80 *        *
     F**      * 81   82   83   84   85   86   87   88   89   90 *        *
     F**      * 91   92   93   94   95   96   97   98   99      *        *
     F**      ***************************************************        *
     F********************************************************************
      *
      /EJECT
      *
      *
      ** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      *
      /EJECT
      *
     I****PRTDS       DS                                                  132893
      *
      **  Printer file information data structure.
     I****                                B 152 1530PAGLEN                132893
     I****                                B 188 1890OVRLIN                132893
     I****                                B 367 3680CURLIN                132893
      *
     ICPYR@#      DS
      *
      **  Data structure for compilation - COPYRIGHT INSERTION
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      *
     I            DS
      *
      **  Data structure for Start Date
     I                                        1   80EXSTRD
     I                                        1   4 YY1
     I                                        5   6 MM1
     I                                        7   8 DD1
     I            DS
      *
      **  Data structure for End Date
     I                                        1   80EXENDD
     I                                        1   4 YY2
     I                                        5   6 MM2
     I                                        7   8 DD2
     I            DS
      *
      **  Data structure for Revision Date
     I                                        1   80EXREVD
     I                                        1   4 YY3
     I                                        5   6 MM3
     I                                        7   8 DD3
     I            DS
      *
      **  Data structure for Start Date new format to print
     I                                        1   8 P1STRD
     I                                        1   2 STDD
     I                                        3   4 STMM
     I                                        5   8 STYY
     I            DS
      *
      **  Data structure for End Date new format to print
     I                                        1   8 P1ENDD
     I                                        1   2 EDDD
     I                                        3   4 EDMM
     I                                        5   8 EDYY
     I            DS
      *
      **  Data structure for Revision Date new format to print
     I                                        1   8 P1REVD
     I                                        1   2 REDD
     I                                        3   4 REMM
     I                                        5   8 REYY
      *                                                                   132893
     ILDA1        DS                            256                       132893
     I*  DATA AREA DATA STRUCTURE FOR HANDLING DB ERRORS                  132893
     I*                                                                   132893
     I                                      134 141 DBFILE                132893
     I                                      142 170 DBKEY                 132893
     I                                      171 180 DBPGM                 132893
     I                                      181 183 DBASE                 132893
     ISPOOL1      DS                                                      132893
      *                                                                   132893
      **  File Information Data Structure - P1 report                     132893
     I                                       83  92 SFIL1                 132893
     I                                    B 123 1240SFNUM1                132893
     ISPOOL2      DS                                                      132893
      *                                                                   132893
      **  File Information Data Structure - P1 report                     132893
     I                                       83  92 SFIL2                 132893
     I                                    B 123 1240SFNUM2                132893
     I                                    B 152 1530PAGLEN                132893
     I                                    B 188 1890OVRLIN                132893
     I                                    B 367 3680CURLIN                132893
     I            DS                                                      ULX004
      *                                                                   ULX004
      **  Data structure for Facility reference                           ULX004
     I                                        1  13 P1FACL                ULX004
     I                                        1   6 WWFCUS                ULX004
     I                                        8  10 WWFACT                ULX004
     I                                       12  13 WWFCNO                ULX004
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -                                                       *
      * PRTLGN   - S/R TO PRINT REPORT DETAIL LIGNE                      *
      * INIT     - S/R TO INITIALIZE STATIC FIELDS                       *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      *                                                                  *
      ********************************************************************
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C*----------------- FIRST CYCLE -----------------------------
     C*
      *
     C                     EXSR INIT
      *
     C********** *LOVAL    SETLLERLUTRL1                                                      CER001
     C**********           READ ERLUTRL1                 81                                   CER001
     C           *LOVAL    SETLLERLTRXL1                                                      CER001
     C                     READ ERLTRXL1                 81                                   CER001
      *
     C           *IN81     IFEQ '1'
     C                     WRITEERLU87H1
     C                     WRITEERLU87E1
     C                     WRITEERLU88H1
     C                     WRITEERLU88E1
     C                     END
      *
     C                     Z-ADDEXSCHA    BKSCHA  80
     C                     Z-ADDEXRECT    BKRECT  30
     C                     MOVE EXGUAT    BKGUAT  1                       ER0336
     C*
     C*------------ DETAIL PROCESSING ----------------------------
     C*
      *
      **  Write report header
     C           *IN81     IFEQ '0'
     C*
     C** RETRIEVE IML CODE NARRATIVE                                      71013
     C           EXSCHA    CHAINIMLNARPD             88                   71013
     C           *IN88     IFEQ '1'                                       71013
     C                     MOVE *BLANKS   IMLNAR                          71013
     C                     END                                            71013
     C*                                                                   71013
     C                     WRITEERLU87H1
     C                     WRITEERLU87H2
     C                     WRITEERLU87H3
     C                     WRITEERLU88H1
     C                     WRITEERLU88H2
     C                     WRITEERLU88H3
     C                     END
      *
      ****Do*until*end*of*file*ERLUTRL1                                                       CER001
      **  Do until end of file ERLTRXL1                                                       CER001
     C           *IN81     DOWEQ'0'
      *
     C                     MOVEL'0'       *IN50
      *
      **  If there is not enough space on the physical page
      **  for this format :
     C           PAGLEN    SUB  CURLIN    WRKPAG  40
     C           WRKPAG    IFLE 8
      *
      **  Signal artificial overflow
     C                     SETON                     97
      *
      **  Print page header
     C                     WRITEERLU87H1
     C                     WRITEERLU88H1
      *
     C           EXSCHA    IFNE BKSCHA
     C           EXRECT    ORNE BKRECT
      *                                                                   ER0336
     C                     MOVE WKTOT1    P1TOT1                          ER0336
      *                                                                   ER0336
     C           WKTOT1    IFGE 0                                         ER0336
     C                     MOVE 'D'       P3DRCR                          ER0336
     C                     ELSE                                           ER0336
     C                     MOVE 'C'       P3DRCR                          ER0336
     C                     END                                            ER0336
     C                     WRITEERLU87D3                                  ER0336
     C                     WRITEERLU88D3                                  ER0336
     C                     Z-ADD0         P1TOT1                          ER0336
     C                     Z-ADD0         WKTOT1                          ER0336
      *                                                                   ER0336
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *
     C                     WRITEERLU87D2
     C*********************WRITEERLU87H2                                  ER0336
     C                     WRITEERLU88D2
     C*********************WRITEERLU88H2                                  ER0336
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
     C                     Z-ADDEXSCHA    BKSCHA                          ERL371
     C                     Z-ADDEXRECT    BKRECT                          ERL371
     C                     MOVE EXGUAT    BKGUAT                          ERL371
     C                     WRITEERLU87H1                                  ERL371
     C                     WRITEERLU88H1                                  ERL371
     C                     END
      *
     C                     WRITEERLU87H2                                  ER0336
     C                     WRITEERLU88H2                                  ER0336
     C                     WRITEERLU87H3
     C                     WRITEERLU88H3
     C                     MOVEL'0'       *IN50
     C                     END
      *
      **  Print ligne data
     C                     EXSR PRTLGN
      *
      **  Save Code Schema A to check if rupture
     C                     Z-ADDEXSCHA    BKSCHA  80
      **  Save Record Type to check if rupture
     C                     Z-ADDEXRECT    BKRECT  30
      **  Save Guarantee type to check if rupture                         ER0336
     C                     MOVE EXGUAT    BKGUAT                          ER0336
      *
      **  Read next record
     C**********           READ ERLUTRL1                 81                                   CER001
     C                     READ ERLTRXL1                 81                                   CER001
      *
     C           EXSCHA    IFNE BKSCHA                                    71013
     C           EXSCHA    CHAINIMLNARPD             88                   71013
     C           *IN88     IFEQ '1'                                       71013
     C                     MOVE *BLANKS   IMLNAR                          71013
     C                     END                                            71013
     C                     END                                            71013
      *
      **  End CC1
     C                     END
      *
     C           *IN81     IFEQ '1'
     C           EXSCHA    ANDNE0
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *                                                                   ER0336
     C                     MOVE WKTOT1    P1TOT1                          ER0336
      *                                                                   ER0336
     C           WKTOT1    IFGE 0                                         ER0336
     C                     MOVE 'D'       P3DRCR                          ER0336
     C                     ELSE                                           ER0336
     C                     MOVE 'C'       P3DRCR                          ER0336
     C                     END                                            ER0336
      *                                                                   ER0336
     C                     WRITEERLU87D3                                  ER0336
      *
     C                     WRITEERLU87D2
     C                     WRITEERLU87E2
     C                     WRITEERLU88D2
     C                     WRITEERLU88E2
     C                     END
      *
     C                     MOVEL'1'       *INLR
      *
     C                     RETRN
      *
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * PRTLGN   - S/R TO PRINT REPORT DETAIL LIGNE                      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * GLZADD   - S/R TO ADD AMOUNT (ZZAMT) TO TOTAL (ZZTOTI,ZZTOTD)    *
      * ZSEDIT   - S/R TO INSERT DEC. POINT AND SIGN INTO NUM. FIELD     *
      * ZDATE2   - S/R TO CONVERT A DAY NUMBER TO DATE FORMATS           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           PRTLGN    BEGSR
      *
      **  Load detail line
      *
      **  Accumulate controls
     C           RECTOT    ADD  1         RECTOT
      *                                                                   ER0336
     C           EXGUAT    IFNE BKGUAT                                    ER0336
     C           EXGUAT    OREQ BKGUAT                                    ER0336
     C           EXSCHA    ANDNEBKSCHA                                    ER0336
     C                     MOVE WKTOT1    P1TOT1                          ER0336
      *                                                                   ER0336
     C           WKTOT2    IFGE 0                                         ER0336
     C                     MOVE 'D'       P3DRCR                          ER0336
     C                     ELSE                                           ER0336
     C                     MOVE 'C'       P3DRCR                          ER0336
     C                     END                                            ER0336
      *                                                                   ER0336
     C                     WRITEERLU87D3                                  ER0336
     C                     WRITEERLU88D3                                  ER0336
     C                     Z-ADD0         P1TOT1                          ER0336
     C                     Z-ADD0         WKTOT1                          ER0336
     C                     END                                            ER0336
      *                                                                   ER0336
      *
      **  If there is not enough space on the physical page
      **  for this format :
     C           PAGLEN    SUB  CURLIN    WRKPAG  40
     C           WRKPAG    IFLE 8
      *
      **  Signal artificial overflow
     C                     SETON                     97
      *
      **  Print page header
     C                     WRITEERLU87H1
     C                     WRITEERLU88H1
     C           EXSCHA    IFNE BKSCHA
     C           EXRECT    ORNE BKRECT
      *                                                                   ER0336
     C                     MOVE WKTOT1    P1TOT1                          ER0336
      *                                                                   ER0336
     C           WKTOT1    IFGE 0                                         ER0336
     C                     MOVE 'D'       P3DRCR                          ER0336
     C                     ELSE                                           ER0336
     C                     MOVE 'C'       P3DRCR                          ER0336
     C                     END                                            ER0336
     C                     WRITEERLU87D3                                  ER0336
     C                     WRITEERLU88D3                                  ER0336
     C                     Z-ADD0         P1TOT1                          ER0336
     C                     Z-ADD0         WKTOT1                          ER0336
      *                                                                   ER0336
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *
     C                     WRITEERLU87D2
     C*********************WRITEERLU87H2                                  ER0336
     C                     WRITEERLU88D2
     C*********************WRITEERLU88H2                                  ER0336
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
     C                     END
     C                     WRITEERLU87H2                                  ER0336
     C                     WRITEERLU88H2                                  ER0336
     C                     WRITEERLU87H3
     C                     WRITEERLU88H3
     C                     MOVEL'0'       *IN50
     C                     END
      *
      **  Print report detail line
     C           EXSCHA    IFNE BKSCHA
     C           EXRECT    ORNE BKRECT
     C                     MOVE WKTOT2    P1TOT2
      *
     C           WKTOT2    IFGE 0
     C                     MOVE 'D'       P2DRCR
     C                     ELSE
     C                     MOVE 'C'       P2DRCR
     C                     END
      *
     C                     WRITEERLU87D2
     C                     WRITEERLU87H1
     C*********************WRITEERLU87H2                                  ER0336
     C                     WRITEERLU88D2
     C                     WRITEERLU88H1
     C*********************WRITEERLU88H2                                  ER0336
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
     C                     WRITEERLU87H2                                  ER0336
     C                     WRITEERLU88H2                                  ER0336
     C                     WRITEERLU87H3
     C                     WRITEERLU88H3
     C                     END
      *
      **  Reset date format in DDMMYYY for start date
     C           EXSTRD    IFEQ 0
     C                     MOVE *BLANK    P1STRD
     C                     MOVE *BLANK    P2STRD                          ERL305
     C                     ELSE
     C                     MOVE DD1       STDD
     C                     MOVE MM1       STMM
     C                     MOVE YY1       STYY
     C                     MOVELP1STRD    P2STRD  6                       ERL305
     C                     MOVE STYY      STYY2   2                       ERL305
     C                     MOVE STYY2     P2STRD                          ERL305
     C                     END
      *
      **  Reset date format in DDMMYYY for end date
     C           EXENDD    IFEQ 0
     C                     MOVE *BLANK    P1ENDD
     C                     MOVE *BLANK    P2ENDD                          ERL305
     C                     ELSE
     C                     MOVE DD2       EDDD
     C                     MOVE MM2       EDMM
     C                     MOVE YY2       EDYY
     C                     MOVELP1ENDD    P2ENDD  6                       ERL305
     C                     MOVE EDYY      EDYY2   2                       ERL305
     C                     MOVE EDYY2     P2ENDD                          ERL305
     C                     END
      *
      **  Reset date format in DDMMYYY for revision date
     C           EXREVD    IFEQ 0
     C                     MOVE *BLANK    P1REVD
     C                     MOVE *BLANK    P2REVD                          ERL305
     C                     ELSE
     C                     MOVE DD3       REDD
     C                     MOVE MM3       REMM
     C                     MOVE YY3       REYY
     C                     MOVELP1REVD    P2REVD  6                       ERL305
     C                     MOVE REYY      REYY2   2                       ERL305
     C                     MOVE REYY2     P2REVD                          ERL305
     C                     END
      *
     C                     MOVE EXAMNT    P1AMNT
     C                     MOVE EXLAMT    P1LAMT
      *
     C           EXDRCR    IFEQ 0
     C                     ADD  EXLAMT    WKTOT2 160
     C                     ADD  EXLAMT    WKTOT1 160                      ER0336
     C                     ELSE
     C                     SUB  EXLAMT    WKTOT2
     C                     SUB  EXLAMT    WKTOT1                          ER0336
     C                     END
      *
     C           EXDRCR    IFEQ 0
     C                     MOVE 'D'       P1DRCR
     C                     ELSE
     C                     MOVE 'C'       P1DRCR
     C                     END
      *
     C                     MOVE EXORIG    EXTREF
      *
      ***FOR*SECURITIES*PRINT*SECURITY*REPORT*NAME                 ERL334 ERL425
     C***********EXLOTD    IFEQ 'SEC'                              ERL334 ERL425
     C***********EXLOTD    OREQ 'SEF'                              ERL334 ERL425
     C***********          MOVE '1'       *IN60                    ERL334 ERL425
     C***********          MOVELEXSECY    WWSECD                   ER0336 ERL425
     C***********          ELSE                                    ERL334 ERL425
     C***********          MOVE '0'       *IN60                    ERL334 ERL425
     C***********          END                                     ERL334 ERL425
      *
     C******************** MOVELEXCRNM    WWCRNM                   61874  ERL425
     C                     MOVELEXGURN    WWGURN                          ERL425
      *                                                                   ERL425
      ** Load facility reference if exists                                ULX004
      *                                                                   ULX004
     C           EXFCUS    IFNE *BLANKS                                   ULX004
     C                     MOVE EXFCUS    WWFCUS                          ULX004
     C                     MOVE EXFACT    WWFACT                          ULX004
     C                     MOVE EXFCNO    WWFCNO                          ULX004
     C                     ELSE                                           ULX004
     C                     MOVE *BLANKS   P1FACL                          ULX004
     C                     ENDIF                                          ULX004
      *                                                                   ULX004
     C                     WRITEERLU87D1
     C                     WRITEERLU88D1
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INIT     - S/R TO INITIALIZE STATIC FIELDS                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           INIT      BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           BJMRDT  7
     C                     PARM           BJURPT 53
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RLEV   1                       132893
      *                                                                   132893
      **  PREPARE LDA:                                                    132893
     C           *NAMVAR   DEFN           LDA1                            132893
     C                     MOVEL'ERLU87P' DBPGM                           132893
      *                                                                   132893
      * Record Printer file ERLU87P1 on RCF                               132893
     C                     Z-ADDSFNUM1    ZSNUM                           132893
     C                     MOVELSFIL1     SFILE                           132893
      *                                                                   132893
     C                     EXSR SRZSFL                                    132893
      *                                                                   132893
      * Record Printer file ERLU88P1 on RCF                               132893
     C                     Z-ADDSFNUM2    ZSNUM                           132893
     C                     MOVELSFIL2     SFILE                           132893
      *                                                                   132893
     C                     EXSR SRZSFL                                    132893
      *
      **  Initialize Field
     C                     Z-ADD0         P1TOT2
     C                     Z-ADD0         WKTOT2
     C                     Z-ADD0         P1TOT1                          ER0336
     C                     Z-ADD0         WKTOT1                          ER0336
      *
      **  Initialize indicators
     C                     MOVEL'0'       *IN
      *
      *
      **  Define/Clear constante & variable
     C                     Z-ADD1         RECTOT  50       Total report
      *
      **  Adjust page lengh and overflow as necessary
     C           PAGLEN    SUB  2         WPAGL   40
      *
     C           OVRLIN    IFGE WPAGL
     C           WPAGL     SUB  2         WOVRL   40
     C                     ELSE
     C                     Z-ADDOVRLIN    WOVRL
     C                     END
      *
     C                     ENDSR
      /EJECT
      ********************************************************************
     C*****************************************************************   132893
     C* SRZSFL - Subroutine to call ZSFILE                                132893
     C*****************************************************************   132893
     C*                                                                   132893
     C           SRZSFL    BEGSR                                          132893
     C*                                                                   132893
     C                     CALL 'ZSFILE'                                  132893
     C                     PARM           @RSEQ   5                       132893
     C                     PARM           @RENT   3                       132893
     C                     PARM           SFILE  10                       132893
     C                     PARM           ZSNUM   60                      132893
     C                     PARM           ZSERR   1                       132893
     C*                                                                   132893
     C** If an error occurred during ZSFILE processing,                   132893
     C** return to the calling program.                                   132893
     C*                                                                   132893
     C           ZSERR     IFEQ 'Y'                                       132893
     C                     SETON                     U7U8LR               132893
     C                     RETRN                                          132893
     C                     END                                            132893
     C*                                                                   132893
     C                     ENDSR                                          132893
      ****************************************************************    132893
      /EJECT                                                              132893
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
