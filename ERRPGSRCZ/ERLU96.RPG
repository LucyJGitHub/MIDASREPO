     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas TI Product Code/CSSF Type Combination List')
      *****************************************************************
      *                                                               *
      *  Finastra - European Reporting                                *
      *                                                               *
      *  ERLU96 - Finastra TI Product Code/CSSF Type Combination List *
      *                                                               *
      *  Function:  This program print all records contained in       *
      *             LF/ERTIPTL0 .                                     *
      *                                                               *
      *  Called By: ERCLU96 - Finastra TI Product Code/CSSF Type      *
      *                         Combination List                      *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX024  *CREATE    Date 03Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX024 - Trade Innovation/CSSF Reporting Interface           *
      *                                                               *
      *****************************************************************
     FERTIPTL0IF  E           K        DISK         KINFSR *PSSR
      * Finastra TI Product Code/CSSF Type Combination Table
     FERLU96P1O   E                    PRINTER      KINFDS SPOOL1     UC
      * Finastra TI Product Code/CSSF Type Combination List
     FERLU96AUO   E                    PRINTER      KINFDS SPOOLU
      * Audit report
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      * Array for Copyright
     E/COPY ZSRSRC,ZDATE2Z1
      * Convert Day Number to Date format
      *
      *****************************************************************
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
     IPGMDS     ESDSY2PGDSP
      *
     IDSSDY     E DSDSSDY
      ** First DS for access programs, short data structure
     IDSFDY     E DSDSFDY
      ** First DS for access programs, long data structure
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
     ISDCUST    E DSSDCUSTPD
      ** External data structures for Customer Details
      *
     ISPOOL1      DS
      ** File Information Data Structure for ERLU96P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for ERLU96AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Define parameters list
      *
     C           *ENTRY    PLIST
     C                     PARM           RSEQ    5
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     SETON                     U7U8
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         RCOUNT  50
     C                     MOVE 'N'       WWP1    1
     C                     MOVE 'N'       WWTITL  1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *****************************************************************
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from File.
      *
     C                     READ ERTIPTL0                 89
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If record found
      ***  Do while  End of File.
      *
     C           *IN89     DOWEQ*OFF
     C                     EXSR REPORT
     C                     READ ERTIPTL0                 89
     C                     ENDDO
      *
      ***  Write End of Report.
      *
     C           RCOUNT    IFNE 0
     C                     EXSR WP1END
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           REPORT    BEGSR                           *** REPORT ***
      *
      *** Open printer P1 the first time
      *
     C           WWP1      IFEQ 'N'
     C                     OPEN ERLU96P1
     C                     EXSR RCFP1
     C                     MOVE 'Y'       WWP1
     C                     ENDIF
      *
      *** Print header of printer the first time
      *
     C                     ADD  1         RCOUNT
     C           WWTITL    IFEQ 'N'
     C                     WRITEHEADP1
     C                     MOVE 'Y'       WWTITL
     C                     ENDIF
      *
      *** Clear detail of printer file before setup
      *
     C                     CLEARDETAIL
      *
      *** Prepare Output fields
      *
     C                     MOVELERTIPC    RTIPC
     C                     MOVELERTIPT    RTIPT
     C                     MOVELERTIPD    RTIPD
     C                     MOVELERTCHT    RLCTY
     C                     Z-ADDERTLCD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RLCD
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      *----------------------------------------------------------------
      *
      *    Perform any formatting before printing via ZDATE2
      *
      *----------------------------------------------------------------
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           *** PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           DBASE     IFEQ *ZERO
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM
     C                     Z-ADD999       DBASE
     C                     MOVE ##ERFL    DBFILE
     C                     OUT  LDA
     C                     SETON                     U7U8
     C                     ENDIF
      *
     C                     EXSR AUDIT
     C                     DUMP
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ    5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANKS   ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
**  CPY@
(c) Finastra International Limited 2005
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
