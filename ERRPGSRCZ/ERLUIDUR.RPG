      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER LU Initial Period')
     H********************************************************************
     H*                                                                  *
     H* MIDAS/ABS  EUROPEAN REPORTING - LUXEMBOURG                       *
     H*                                                                  *
     H* RPG/ERLUIDUR - Initial Period                                    *
     H*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Aug05               *
      *  Prev Amend No. 180239             Date 15Jun00               *
      *                 CAD034             Date 18Feb99               *
     H*                CAD023                Date 05Jan99                  *
     H*                CEU007 PTF08          Date 01Dec98                  *
     H*                ULX004                Date 13May98                  *
     H*                ER_R10                Date 18Oct94                  *
     H*                EMFIX ADI             Date 27Jan95                  *
     H*                ER0450 PT             DATE 20JAN95                  *
     H*                                                                    *
     H**********************************************************************
     H*                                                                    *
      *  CER001 - LUX Upgrade to MidasPlus                            *
     H*    180239   - Change calculation of period between start date and  *
     H*               end date whend start date is from a lap year.        *
     H*    CAD034   - SR/ZFWDT must be called with 0 as days forward       *
     H*    CAD023   - Code 30 is: > 3 months <= 6 months                   *
     H*    CEU007 - Holidays Changes For Reporting Interface.              *
     F*    ULX004   -   IML Circular 97/138                                *
     F*    ER_R10   -   EUROPEAN REPORTING - LUXEMBOURG UPGRADE IN R10     *
     F*             -   REVIEW HOLIDAYS PROCESSING                         *
     H*    EMFIX    -   TABLETJ DOESN'T EXIST IN XXERDLIB                  *
     H*    ER0450   -   The end of period must be the first working day    *
     H*                                                                    *
     H**********************************************************************
     H*                                                                  *
     H* FUNCTION:    Calculate number of days between transaction start  *
     H*              and end dates, and set up initial period code :     *
     H*                              - 00  not applicable                *
     H*                              - 05  < 15 calendar days            *ULX004
     H*                              - 10  < 1 month (29 days)           *
     H******                         - 20  1 month < 3 month             *ULX004
     H*                              - 20  <= 3months                    *ULX004
     H*                              - 30  3 month < 6 month             *
     H*                              - 40  6 month < 1 year              *
     H*                              - 50  1 year                        *
     H******                         - 60  > 1 year                      *ULX004
     H*                              - 60  <= 2 years                    *ULX004
     H*                              - 70  <= 5 years                    *ULX004
     H*                              - 80  > 5 years                     *ULX004
     H*                              - 90  undefined                     *
     H*                                                                  *
     F*****************************************************************
     F*                                                                   ER0450
     F**BLETJ IF  E           K        DISK                         ER0450EMFIX
     F*ERTABTJ*IF**E           K        DISK                        EMFIX ER_R10
     F* Holidays                                                          ER0450
     F*                                                                   ER0450
      /EJECT
     E*                                                                   ER0450
     E                    ZHC        50  3                                ER0450
     E*
     E**  ARRAY FOR SR. ZA0680 - CUMULATIVE DAYS IN YEAR FOR 4 YEAR PERIOD
     E                    @YD     4   4  5 0A
     E*
     E**  ARRAY FOR SR. ZA0680 - CUMULATIVE DAYS IN YEAR PER MONTH
     E                    @MD    13  13  5 0A
     E*
     E                    ZYDY    4   4  4 0             ZDATE1/2 SR.ARRAY
     E                    ZMDY   13  13  3 0             ZDATE1/2 SR.ARRAY
     E                    ZMNM   12  12  3               ZDATE2 SR.ARRAY  CEU007
     E*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZHOLE                                                  ER_R10
      /EJECT
      *
     I/COPY ZSRSRC,ZHOLDS                                                 ER_R10
     I/COPY ZSRSRC,ZHOLI                                                  ER_R10
      **  Data Structure for compilation  - Copyright Insertion:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I**  DATA STRUCTURE FOR SR. ZA0680 - DATE INPUT TO SUBROUTINE
     I            DS
     I                                        1   80@@DTIN
     I                                        1   40@@YYYY
     I                                        3   40@@YY
     I                                        1   20@@CC
     I                                        5   60@@MTH
     I                                        7   80@@DAY
     I*
     IWWDATE      DS                              8                       CEU007
     I                                        5   6 WWMTH1                CEU007
      /EJECT
      *===================================================================
     C           *ENTRY    PLIST                           Entry parameter
     C                     PARM           PARM1   80       I: Start Date
     C                     PARM           PARM2   80       I: End Date
     C                     PARM           PARM21  3        I: Currency codER0450
     C                     PARM           PARM3   20       O: Initial duration
     C                     PARM           PARM4   3        I: End Date LocCEU007
     C                     PARM           PARM5   3        I: End Date CcyCEU007
      *===================================================================
      *                                                                   ER0450
     C                     MOVE PARM21    ZCCY                            ER0450
     C***********          MOVE 'Y'       ZOPT                      ER0450ER_R10
      *
      **   If one of the two date is equal to zeros exit pgm.
     C                     MOVELPARM1     WKSTDT  80
     C                     MOVELPARM2     WKENDT  80
      *
     C           WKSTDT    IFEQ 0
     C                     Z-ADD0         INITPR  20
     C                     GOTO SETUP
     C                     END
      *
     C           WKENDT    IFEQ 0
     C                     Z-ADD90        INITPR
     C                     GOTO SETUP
     C                     END
      *                                                                   CEU007
      ***  Check if the feature CEU007 is installed.                      CEU007
      *                                                                   CEU007
     C                     MOVEL'N'       CEU007  1                       CEU007
     C                     CALL 'AOSARDR0'                                CEU007
     C                     PARM '       ' @RTCD   7                       CEU007
     C                     PARM '*VERIFY' @OPTN   7                       CEU007
     C                     PARM 'CEU007'  @SARD   6                       CEU007
      *                                                                   CEU007
     C           @RTCD     IFEQ *BLANKS                                   CEU007
      *                                                                   CEU007
     C                     MOVEL'Y'       CEU007                          CEU007
      *                                                                   CEU007
     C                     ENDIF                                          CEU007
      *
      **   Convert start and end date in Midas format
     C                     MOVELWKSTDT    @@DTIN
     C                     EXSR ZA0680
     C                     MOVE @@MDNO    STDT    50
     C           @@REM     IFEQ 0                                         180239
     C                     MOVE 'Y'       LAP     1                       180239
     C                     END                                            180239
      *
      ***  If the feature CEU007 is installed.                            CEU007
      *                                                                   CEU007
     C           CEU007    IFEQ 'Y'                                       CEU007
      *                                                                   CEU007
      ***  It's existing, check if the end date is a working date         CEU007
      ***  with the "End Date Settlement location Code" and the           CEU007
      ***  "End Date Settlement currency code".                           CEU007
      ***  If it's a working day :                                        CEU007
      ***  =====> Nothing                                                 CEU007
      ***  If it's a no working day and the same month :                  CEU007
      ***  =====> Take the next working day                               CEU007
      ***  If it's a no working day and not the same month :              CEU007
      ***  =====> Take the previous working day                           CEU007
      *                                                                   CEU007
      ***  Convert end date in Midas format                               CEU007
      *                                                                   CEU007
     C                     MOVELWKENDT    @@DTIN                          CEU007
     C                     EXSR ZA0680                                    CEU007
     C                     MOVE @@MDNO    WWENDT  50                      CEU007
      *                                                                   CEU007
     C                     MOVELPARM5     ZCCY                            CEU007
     C                     Z-ADDWWENDT    ZDAYNO                          CEU007
     C                     MOVE PARM4     ZLOC                            CEU007
      *                                                                   CEU007
     C                     EXSR ZCHKH                                     CEU007
      *                                                                   CEU007
      ***  If ZIND = 'H' then the date is a holiday                       CEU007
      ***  End date ==> next working day                                  CEU007
      *                                                                   CEU007
     C           ZIND      DOWEQ'H'                                       CEU007
     C                     ADD  1         ZDAYNO                          CEU007
     C                     EXSR ZCHKH                                     CEU007
     C                     ENDDO                                          CEU007
      *                                                                   CEU007
      ***  Check if it's the same month                                   CEU007
      *                                                                   CEU007
     C                     MOVE *IN98     WW98    1                       CEU007
     C                     MOVE *ON       *IN98                           CEU007
     C                     EXSR ZDATE2                                    CEU007
     C                     MOVELZDATE     WWMTH2  2                       CEU007
     C                     MOVELPARM2     WWDATE                          CEU007
     C                     MOVE WW98      *IN98                           CEU007
      *                                                                   CEU007
     C           WWMTH1    IFNE WWMTH2                                    CEU007
      *                                                                   CEU007
      ***  Take the first date and seach into the another direction       CEU007
      ***  End date ==> previous working day                              CEU007
      *                                                                   CEU007
     C                     MOVEL'H'       ZIND                            CEU007
     C                     Z-ADDWWENDT    ZDAYNO                          CEU007
     C           ZIND      DOWEQ'H'                                       CEU007
     C                     SUB  1         ZDAYNO                          CEU007
     C                     EXSR ZCHKH                                     CEU007
     C                     ENDDO                                          CEU007
      *                                                                   CEU007
     C                     ENDIF                                          CEU007
      *                                                                   CEU007
     C                     Z-ADDZDAYNO    ENDT                            CEU007
      *                                                                   CEU007
     C                     ELSE                                           CEU007
      *                                                                   CEU007
     C                     MOVELWKENDT    @@DTIN
     C                     EXSR ZA0680
     C                     MOVE @@MDNO    ENDT    50
      *                                                                   CEU007
     C                     ENDIF                                          CEU007
      **************************************************************************
     C**   Calcul period between to given date and setup corresponding code
      **************************************************************************
      *
      **   Less than 15 days setup code 05                                ULX004
     C                     Z-ADD0         WWDT    50                      ULX004
     C           ENDT      SUB  STDT      WWDT                            ULX004
     C           WWDT      IFLT 15                                        ULX004
     C                     Z-ADD05        INITPR                          ULX004
     C                     GOTO SETUP                                     ULX004
     C                     END                                            ULX004
      *                                                                   ULX004
      **   Least than one month setup code 10
     C                     Z-ADD0         Y       20
     C                     Z-ADD1         M       20
     C                     EXSR SRINPR                     ONE MONTH
     C                     Z-ADDZDAYNO    MD30    50
      *
     C           ENDT      IFLT MD30
     C                     Z-ADD10        INITPR
     C                     GOTO SETUP
     C                     END
      *
      *****Between one month and three months setup code 20               ULX004
      **   Between one month or equal to 3 months                         ULX004
     C                     Z-ADD3         M
     C                     EXSR SRINPR                     THREE MONTHS
     C                     Z-ADDZDAYNO    MD92    50
      *
     C******     ENDT      IFLT MD92                                      ULX004
     C           ENDT      IFLE MD92                                      ULX004
     C                     Z-ADD20        INITPR
     C                     GOTO SETUP
     C                     END
      *
      **   Between three months and six months setup code 30
     C                     Z-ADD6         M
     C                     EXSR SRINPR                     SIX MONTHS
     C                     Z-ADDZDAYNO    MD183   50
      *
     C***********ENDT      IFLT MD183                                     CAD023
     C           ENDT      IFLE MD183                                     CAD023
     C                     Z-ADD30        INITPR
     C                     GOTO SETUP
     C                     END
      *
      **   Between six months and one year setup code 40
     C                     Z-ADD1         Y
     C                     Z-ADD0         M
     C           LAP       IFEQ 'Y'                                       180239
     C                     Z-ADD1         D       10                      180239
     C                     END                                            180239
     C                     EXSR SRINPR                     ONE YEAR
     C                     Z-ADDZDAYNO    MD365   50
      *
     C           ENDT      IFLT MD365
     C                     Z-ADD40        INITPR
     C                     GOTO SETUP
     C                     END
      *
      **   Equal to one year setup code 50
     C           ENDT      IFEQ MD365
     C                     Z-ADD50        INITPR
     C                     GOTO SETUP
     C                     END
      *
      *****Greater than one year setup code 60                            ULX004
     C******     ENDT      IFGT MD365                                     ULX004
     C******               Z-ADD60        INITPR                          ULX004
     C******               END                                            ULX004
      *                                                                   ULX004
      **   Greater than 1 year or equal to 2 years setup code 60          ULX004
     C                     Z-ADD2         Y                               ULX004
     C                     Z-ADD0         M                               ULX004
     C                     Z-ADD0         D                               180239
     C                     MOVEL*BLANKS   LAP                             180239
     C                     EXSR SRINPR                                    ULX004
     C                     Z-ADDZDAYNO    MD730   50                      ULX004
      *                                                                   ULX004
     C           ENDT      IFLE MD730                                     ULX004
     C                     Z-ADD60        INITPR                          ULX004
     C                     GOTO SETUP                                     ULX004
     C                     END                                            ULX004
      *                                                                   ULX004
      **   Greater than 2 years or equal to 5 years setup code 70         ULX004
     C                     Z-ADD5         Y                               ULX004
     C                     Z-ADD0         M                               ULX004
     C                     EXSR SRINPR                                    ULX004
     C                     Z-ADDZDAYNO    M1825   50                      ULX004
      *                                                                   ULX004
     C           ENDT      IFLE M1825                                     ULX004
     C                     Z-ADD70        INITPR                          ULX004
     C                     GOTO SETUP                                     ULX004
     C                     END                                            ULX004
      *                                                                   ULX004
      **   Greater than five years setup code 80                          ULX004
     C           ENDT      IFGT M1825                                     ULX004
     C                     Z-ADD80        INITPR                          ULX004
     C                     END                                            ULX004
      **
     C           SETUP     TAG                             *** SETUP PARM ***
     C                     MOVELINITPR    PARM3
     C                     RETRN
      *
     C  N98                MOVE '0'       *IN98
      *******************************************************************
      /EJECT
      *******************************************************************
      *  Subroutine to calculate a variable period from a start date to
      *  a end date
      *******************************************************************
     C           SRINPR    BEGSR                           *** INITRD ***
      *
     C                     MOVE WKSTDT    MTHDAY  40
     C                     MOVELMTHDAY    MTH     20
     C                     MOVE MTHDAY    DAY     20
     C                     MOVELWKSTDT    CTYEAR  40
     C                     MOVE CTYEAR    YEAR    20
      *
      **   Add desired calender month or year
     C           DAY       ADD  D         DAY                             180239
     C           YEAR      ADD  Y         YEAR
     C           MTH       ADD  M         MTH
     C           MTH       COMP 12                   50    Greater than
     C   50      MTH       SUB  12        MTH
     C   50      YEAR      ADD  1         YEAR
      *
      **   Reform date into proper format
     C                     MOVELDAY       DYMTH   40
     C                     MOVE MTH       DYMTH
     C                     MOVELDYMTH     ZDATE
     C                     MOVE YEAR      ZDATE
      *
      **   Check whether date is valid
     C           CHECK     TAG                             *** CHECK ***
     C                     EXSR ZDATE1
     C   99      ZDATE     SUB  10000     ZDATE
     C   99                GOTO CHECK
      *
     C*********************EXSR ZDATE5                              ER0450ER_R10
     C                     MOVE *BLANKS   ZLOC                            ER_R10
     C*********************Z-ADD1         ZNRDYS                    ER_R10CAD034
     C                     Z-ADD0         ZNRDYS                          CAD034
     C                     EXSR ZFWDT                                     ER_R10
     C                     Z-ADDZDYNBR    ZDAYNO                          ER0450
      *                                                                   ER0450
     C                     ENDSR                                          ER0450
      *                                                                   ER0450
      ******************************************************************* ER0450
      /EJECT                                                              ER0450
      ***********                                                         ER_R10
     C**************************************************************ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C****SR:ZDATE5   - DETERMINES IF A MIDAS DAY NUMBER DATE IS A  ER0450ER_R10
     C***********       HOLIDAY IN A GIVEN CURRENCY, AND IF IT IS   ER0450ER_R10
     C***********       DETERMINES NEXT WORKING DAY IF REQUIRED.    ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C****INPUTS*     - ZDAYNO (5,0P) DAY NO                        ER0450ER_R10
     C***********       ZCCY   (3  A) CURRENCY                      ER0450ER_R10
     C***********       ZOPT   (1  A) Y OR N                        ER0450ER_R10
     C****OUTPUTS     - ZDYNBR (5,0P) NEXT WORKING DAY NO           ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C**************************************************************ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C***********ZDATE5    BEGSR                           ** ZDATE5ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****Define Input fields.                                     ER0450ER_R10
     C***********          Z-ADDZDAYNO    ZDAYNO  50                ER0450ER_R10
     C***********          MOVE ZCCY      ZCCY    3                 ER0450ER_R10
     C***********          MOVE ZOPT      ZOPT    1                 ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****Define/Reset Outputs.                                    ER0450ER_R10
     C***********          SETOF                     99             ER0450ER_R10
     C***********          Z-ADDZDAYNO    ZDYNBR  50                ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****DETERMINE IF HOLIDAY                                     ER0450ER_R10
     C***********ZDHOL     TAG                             ** ZDHOL ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****CALCULATIONS TO DEFINE/CLEAR WORKFIELDS.                 ER0450ER_R10
     C***********          SETOF                     90             ER0450ER_R10
     C***********          MOVE *BLANKS   ZHC                       ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****SET*KEY TO CHAIN TO THE FIRST HOLIDAY RECORD.            ER0450ER_R10
     C***********ZDYNBR    CHAINTABLETJF             90             ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****STORE*CURRENCIES IF RECORD FOUND.                        ER0450ER_R10
     C************IN90     IFEQ '0'                                 ER0450ER_R10
     C***********RECI      ANDEQ'D'                                 ER0450ER_R10
     C***********          MOVEAHCCY      ZHC,1                     ER0450ER_R10
     C***********          MOVE INEX      ZINEX   1                 ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****SET*KEY TO CHAIN TO THE FIRST HOLIDAY RECORD.            ER0450ER_R10
     C***********          READ TABLETJF                 90         ER0450ER_R10
     C************IN90     IFEQ '0'                                 ER0450ER_R10
     C***********RECI      ANDEQ'D'                                 ER0450ER_R10
     C***********HOL       ANDEQZDYNBR                              ER0450ER_R10
     C***********          MOVEAHCCY      ZHC,26                    ER0450ER_R10
     C***********          ELSE                                     ER0450ER_R10
     C***********          MOVE ZINEX     INEX                      ER0450ER_R10
     C***********          END                                      ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****CHECK*IF CURRENCY ON HOLIDAY.                            ER0450ER_R10
     C***********ZCCY      LOKUPZHC                      94         ER0450ER_R10
     C************IN94     IFEQ '1'                                 ER0450ER_R10
     C***********INEX      ANDEQ'I'                                 ER0450ER_R10
     C************IN94     OREQ '0'                                 ER0450ER_R10
     C***********INEX      ANDEQ'E'                                 ER0450ER_R10
     C***********          SETON                         99         ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C*****CHECK*IF NEXT WORKING DAY REQUIRED.                      ER0450ER_R10
     C***********ZOPT      IFEQ 'Y'                                 ER0450ER_R10
     C***********ZDYNBR    ADD  1         ZDYNBR                    ER0450ER_R10
     C***********          GOTO ZDHOL                               ER0450ER_R10
     C***********          END                                      ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C***********          END                                      ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C***********          END                                      ER0450ER_R10
     C***********                                                   ER0450ER_R10
     C***********          ENDSR                                    ER0450ER_R10
     C***********                                                   ER0450ER_R10
      *******************************************************************
      /EJECT
     C*******************************************************************
     C*                                                               *
     C*  ZA0680 - This subroutine converts YYYYMMDD format date to    *
     C*           Midas day number format (No. days from 31/12/71)    *
     C*
     C* CALLED BY :
     C*
     C* CALLS :
     C*
     C* INPUT  : @@DTIN DATE (YYYYMMDD) - IN DATA STRUCTURE (SIZE : 8N)
     C*
     C* OUTPUT : @@MDNO MIDAS DAY NUMBER  (SIZE : 5N)
     C*
     C* USES :   @@CC   NUMBER OF CENTURIES IN DATE
     C*          @@DAY  DAY NUMBER
     C*          @@REM  REMAINDER FROM DIVIDE
     C*          @@MTH  MONTH NUMBER
     C*          @MD    COMPILATION TIME ARRAY FOR CUMULATIVE DAYS IN
     C*                 MONTH
     C*          @@WK2  WORK FIELD (2,0)
     C*          @@WK5  WORK FIELD (5,0)
     C*          @@YYYY YEAR (4 CHARACTER)
     C*          @@YY   YEAR (2 CHARACTER)
     C*          @YD    COMPILATION TIME ARRAY FOR CUMULATIVE DAYS IN
     C*                 A FOUR YEAR PERIOD
     C*****************************************************************
     C*
     C           ZA0680    BEGSR
     C*
     C**  CLEAR OUT ANY 'OLD' DATA IN FIELD
     C                     Z-ADD0         @@MDNO  50
     C*
     C**   IF DATE GREATER THAN 31/12/2071 NEED EXTRA PROCESSING
     C           @@YYYY    IFGE 2072                       B1
     C*
     C**    MULTIPLY BY NUMBER OF DAYS IN CENTURY (SINCE 1971)
     C           @@CC      SUB  19        @@WK2   20       *1
     C           @@WK2     MULT 36524     @@MDNO           *1
     C*
     C**  YEAR 2000 IS A LEAP YEAR MUST ALLOW EXTRA DAY
     C                     ADD  1         @@MDNO           *1
     C                     END                             E1
     C*
     C           @@YYYY    ADD  28        @@WK2
     C*
     C           @@WK2     DIV  4         @@WK2
     C                     MVR            @@REM   10
     C*
     C**    MULTIPLY BY NUMBER OF DAYS IN FOUR YEAR PERIOD
     C           @@WK2     MULT 1461      @@WK5   50
     C                     ADD  @@WK5     @@MDNO
     C*
     C**  CHECK REMAINDER AND MONTH NUMBER
     C           @@REM     IFEQ 0                          B1
     C           @@MTH     ANDGT2                          B1
     C                     ADD  1         @@MDNO           *1
     C                     END                             E1
     C*
     C**  YEAR NOT LEAP YEAR,  ADD CUMULATIVE DAYS FOR YEAR
     C           @@REM     IFNE 0                          B1
     C                     ADD  @YD,@@REM @@MDNO           *1
     C                     END                             E1
     C*
     C**  ADD MONTHS THIS YEAR
     C                     ADD  @MD,@@MTH @@MDNO
     C*
     C**  ADD DAYS THIS MONTH
     C                     ADD  @@DAY     @@MDNO
     C*
     C           ZA0689    ENDSR                           **ZA0689 TAG**
     C*****************************************************************
      /EJECT
     C********************************************************************
     C**
     C**   ZDATE1 SR. TO VALIDATE DATES SUBMITTED AND
     C**              CONVERT TO A NUMBER OF DAYS.
     C**
     C**   THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
     C**
     C           ZDATE1    BEGSR                           *** ZDATE1 ***
     C**
     C**   CLEAR DAY NUMBER. RESET ERROR INDICATOR
     C                     Z-ADD0         ZDAYNO  50
     C                     SETOF                     99
     C**
     C**   CALCULATION TO DEFINE INPUT DATE FIELD.
     C                     Z-ADDZDATE     ZDATE   60
     C**
     C**   GET INDIVIDUAL DAY, MONTH AND YEAR FIELDS.
     C                     MOVE ZDATE     ZYEAR   20
     C  N98                MOVELZDATE     ZDAY    20
     C   98                MOVELZDATE     ZMTH    20
     C                     MOVE ZDATE     ZWRK4   40
     C  N98                MOVELZWRK4     ZMTH
     C   98                MOVELZWRK4     ZDAY
     C**
     C**   ENSURE MONTH IS VALID. BYPASS IF ERROR
     C           ZMTH      IFLE 0
     C           ZMTH      ORGT 12
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C**   ENSURE DAY IS VALID. BYPASS IF ERROR
     C           ZDAY      IFLE 0
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C**   CHECK FOR 30 DAY MONTHS. BYPASS IF ERROR
     C           ZMTH      IFEQ 4
     C           ZMTH      OREQ 6
     C           ZMTH      OREQ 9
     C           ZMTH      OREQ 11
     C**
     C           ZDAY      IFGT 30
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C                     ELSE
     C**
     C**   CHECK FOR 31 DAY MONTHS (ALL OTHERS BUT FEB). BYPASS IF ERROR
     C           ZDAY      IFGT 31
     C           ZMTH      ANDNE2
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C                     END
     C**
     C**   CHECK FOR LEAP YEAR: REMAINDER WILL BE ZERO IF IT IS A LEAP YR
     C           ZYEAR     ADD  28        ZYEAR
     C           ZYEAR     DIV  4         ZLYR    20
     C                     MVR            ZLY     10
     C**
     C**   IF FEBUARY FURTHER VALIDATE DAY.
     C           ZMTH      IFEQ 2
     C**
     C**   INVALID IF GREATER THAN 28 AND NOT A LEAP YEAR
     C           ZLY       IFGT 0
     C           ZDAY      ANDGT28
     C                     SETON                     99
     C                     GOTO ZDEND1                     BYPASS IF ERROR
     C                     END
     C**
     C**   INVALID IF GREATER THAN 29 AND A LEAP YEAR - BYPASS IF ERROR
     C           ZLY       IFEQ 0
     C           ZDAY      ANDGT29
     C                     SETON                     99
     C                     GOTO ZDEND1
     C                     END
     C**
     C                     END
     C**
     C**   DETERMINE NUMBER OF DAYS FROM 31/12/1971.
     C**
     C**   MULTIPLY NO. OF FOUR-YEAR SPANS, BY NO. OF DAYS IN SPAN
     C           ZLYR      MULT 1461      ZDAYNO
     C**
     C**   IF NOT A LEAP YEAR, ADD APPROPRIATE NO. OF DAYS FOR EXTRA
     C**   YEARS USING REMAINDER FIELD (IE. 1, 2 OR 3 X 356)
     C           ZLY       IFGT 0
     C           ZDAYNO    ADD  ZYDY,ZLY  ZDAYNO
     C                     END
     C**
     C**   IF IT IS A LEAP YEAR, AND LATER THAN FEBRUARY, ADD ONE FOR
     C**   29TH OF FEB
     C           ZLY       IFEQ 0
     C           ZMTH      ANDGT2
     C           ZDAYNO    ADD  1         ZDAYNO
     C                     END
     C**
     C**   ADD APPROPRIATE NUMBER OF DAYS FOR THE MONTH NUMBER
     C           ZDAYNO    ADD  ZMDY,ZMTH ZDAYNO
     C**
     C**   ADD NUMBER OF DAYS IN MONTH
     C           ZDAYNO    ADD  ZDAY      ZDAYNO
     C**
     C           ZDEND1    ENDSR                           * ZDEND1 ENDSR*
     C**
     C**
     C********************************************************************
      /EJECT                                                              ER_R10
     C/COPY ZSRSRC,ZACCH                                                  ER_R10
      /EJECT                                                              ER_R10
     C/COPY ZSRSRC,ZFWDT                                                  ER_R10
      /EJECT                                                              CEU007
     C/COPY ZSRSRC,ZCHKH                                                  CEU007
      /EJECT                                                              CEU007
     C/COPY ZSRSRC,ZDATE2Z2                                               CEU007
** @YD  USED BY SR. ZA0680 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZA0680 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES                                            CEU007
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
