      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU EMU BCA conversion prompt screen')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  DSPF/EU0010DF - Midas EMU BCA Conversion Prompt Screen       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU001 *CREATE     DATE 08Sep97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU001 - EMU Base Currency For Accounting Conversion         *
      *                                                               *
      *****************************************************************
      *--------------------------*
      ** DISPLAY FILE - EU0010DF *
      *--------------------------*
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      PRINT(MPRINT)
      *--------------------------------*
      ** Field  level entry for Header *
      *--------------------------------*
     A          R EU0010F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            SUSER     R           1  2TEXT('User')
     A                                      REFFLD(AUP1)
     A                                  1 13'EU0010'
     A            SMRDT     R        O  1 72
     A                                      REFFLD(RELD)
     A            SJOB      R        O  2  2
     A                                      REFFLD(AUP1)
     A                                  2 18'BASE CURRENCY FOR +
     A                                      ACCOUNTING CONVERSION +
     A                                      PROMPT'
     A                                      DSPATR(HI)
     A            STIME     R     Y  O  2 71REFFLD(RTIM)
     A                                      EDTWRD('  :  :  ')
      *--------------------------------*
      ** Field  level entry for Footer *
      *--------------------------------*
     A          R EU0010F1                  OVERLAY
     A                                 23  2'F3=Main Menu'
      *--------------------------------*
      ** Field  level entry for Screen *
      *--------------------------------*
     A          R EU0010F2                  OVERLAY
     A                                  5  2'Run Conversion Y/N+
     A                                       .............:'
     A            SRUNC     R        B  5 35TEXT('Run Conversion')
     A                                      REFFLD(ACCL)
     A  01                                  DSPATR(RI PC)
     A                                  6  2'Old Base Currency+
     A                                       ..............:'
     A            SOBCY     R        B  6 35TEXT('Old Base Currency')
     A                                      REFFLD(CCYD)
     A  02                                  DSPATR(RI PC)
     A                                  7  2'New Base Currency+
     A                                       ..............:'
     A            SNBCY     R        B  7 35TEXT('New Base Currency')
     A                                      REFFLD(CCYD)
     A  03                                  DSPATR(RI PC)
     A                                  9  2'Batch Currency +
     A                                      Equivalents Transfer +
     A                                      Batch Details'
     A                                      DSPATR(HI)
     A                                 11  2'Batch Number +
     A                                       ..................:'
     A            SBTNB     R        B 11 35TEXT('Batch Number')
     A                                      REFFLD(CCYD)
     A  04                                  DSPATR(RI PC)
     A                                 12  2'Branch+
     A                                       ......................+
     A                                      ...:'
     A            SBRCA     R        B 12 35TEXT('Branch code')
     A                                      REFFLD(CCYD)
     A  05                                  DSPATR(RI PC)
     A                                 13  2'Department+
     A                                       .....................:'
     A            SDEPT     R        B 13 35TEXT('Department Code')
     A                                      REFFLD(CCYD)
     A  06                                  DSPATR(RI PC)
      *----------*
      ** Subfile *
      *----------*
     A          R EU0010S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *------------------*
      ** Subfile control *
      *------------------*
     A          R EU0010C0                  TEXT('Program messages')
     A                                      SFLCTL(EU0010S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  40
     AON40                                  SFLEND
     A            SPGM                      SFLPGMQ
