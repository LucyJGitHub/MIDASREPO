     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU 'In' Currency to Euro Mapping Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - EMU Retail Utilities                                 *
     A*                                                               *
     A*  DSPF/EU0120DF - 'IN' CURRENCY TO EURO MAPPING MAINTENANCE    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CRE008             Date 19Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CEU022  *CREATE    Date 24Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CRE008 - Cash Management                                     *
     A*  CEU022 - EMU Retail Utilities                                *
     A*                                                               *
     A*****************************************************************
     A*
     A**Start of change CGL029
     A**********                            DSPSIZ(24 80 *DS3)                                CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A*=========================================================================
     A*
     A* Header format
     A*
     A          R EU0120F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A            S#USER    R        O  1  2REFFLD(USER)
     A                                  1 14'EU0120'
      *                                                           /*CRE008*/
     A**********************************1 26'''IN'' CURRENCY TO EURO RETAIL ACC-
     A**********************************    OUNT'
     A**********************************    DSPATR(UL)
     A**********************************    DSPATR(HI)
     A  78                              1 26'''IN'' CURRENCY TO EURO RETAIL ACC-
     A                                      OUNT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N78                              1 26'RETAIL ACCOUNT MAPPING MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *                                                           /*CRE008*/
     A            S#RUNA    R        O  1 73REFFLD(RUNA)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
      *                                                           /*CRE008*/
     A**********************************2 33'MAPPING MAINTENANCE'
     A**********************************    DSPATR(UL)
     A**********************************    DSPATR(HI)
     A  78                              2 33'MAPPING MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *                                                           /*CRE008*/
     A                                  2 72TIME
     A*
     A*=========================================================================
     A*
     A          R EU0120F1
     A                                      OVERLAY
     A                                  4  2' '
     A                                 23  2'F3=Exit'
     A                                 21  2'Map Number:'
     A            S1MAPN         6   B 21 14
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A N70                                  DSPATR(HI)
     A                                 21 22'Action Code:'
     A            S1ACTN         1   B 21 35
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A N71                                  DSPATR(HI)
     A                                 21 38'(A=Amend, D=Delete, E=Enquire)'
     A*
     A*=========================================================================
     A*
     A          R EU0120F2
     A                                      OVERLAY
     A                                      CA12(12 'PREVIOUS')
      *                                                           /*CRE008*/
     A**********************************5  1'Existing ''In'' Currency Accounts-
     A**********************************     to be Mapped'
     A**********************************    DSPATR(UL)
     A**********************************    DSPATR(HI)
     A  78                              5  1'Existing ''In'' Currency Accounts-
     A                                       to be Mapped'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N78                              5  1'Existing Accounts to be Mapped'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *                                                           /*CRE008*/
     A                                  7  2'Branch'
     A            S2BRCA         3   O  7 19DSPATR(HI)
     A                                  7 29'Account Code'
     A**Start of change CGL029
     A**********  S2ACOD         4   O  7 44DSPATR(HI)                                        CGL029
     A            S2ACOD        10   O  7 44DSPATR(HI)
     A**End of change CGL029
      *                                                           /*CRE008*/
     A**********************************8  2'''In'' Currencies'
     A  78                              8  2'''In'' Currencies'
     A N78                              8  2'Currency'
      *                                                           /*CRE008*/
     A                                  9  2'Customer'
     A            S2CCY1         3   O  8 19DSPATR(HI)
     A            S2CCY2         3   O  8 23DSPATR(HI)
     A            S2CCY3         3   O  8 27DSPATR(HI)
     A            S2CCY4         3   O  8 31DSPATR(HI)
     A            S2CCY5         3   O  8 35DSPATR(HI)
     A            S2CCY6         3   O  8 39DSPATR(HI)
     A            S2CCY7         3   O  8 43DSPATR(HI)
     A            S2CCY8         3   O  8 47DSPATR(HI)
     A            S2CCY9         3   O  8 51DSPATR(HI)
     A            S2CY10         3   O  8 55DSPATR(HI)
     A            S2CNUM        10   O  9 19DSPATR(HI)
     A                                  9 34'(Blank = ALL)'
     A                                 11  1'Mapping Rules'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *                                                           /*CRE008*/
     A*********************************13  2'Create one Euro account for each c-
     A*********************************     ustomer,'
     A*********************************14  2'account code and account sequence'
     A************S2MULT*********1***O*14 39DSPATR(HI)
     A  78                             13  2'Create one Euro account for each c-
     A                                      ustomer,'
     A  78                             14  2'account code and account sequence'
     A  78        S2MULT         1   O 14 39DSPATR(HI)
      *                                                           /*CRE008*/
     A                                 16  2'Retail account number preserved'
     A N81                                  DSPATR(ND)
     A                                 17  2'OR'
     A N81                                  DSPATR(ND)
     A                                 18  2'Construct new retail account numbe-
     A                                      r'
     A N81                                  DSPATR(ND)
     A                                 19  2'at position'
     A N81                                  DSPATR(ND)
     A            S2RNPO         2   O 19 14DSPATR(HI)
     A N81                                  DSPATR(ND)
     A                                 19 17','
     A N81                                  DSPATR(ND)
     A                                 19 19'number of digits'
     A N81                                  DSPATR(ND)
     A            S2RNPR         1   O 16 39DSPATR(HI)
     A N81                                  DSPATR(ND)
     A            S2RNND         1   O 19 39DSPATR(HI)
     A N81                                  DSPATR(ND)
     A                                 19 41','
     A N81                                  DSPATR(ND)
     A                                 19 44'digits'
     A N81                                  DSPATR(ND)
     A            S2RNCD         5   O 19 54DSPATR(HI)
     A N81                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Previous'
     A*
     A*=========================================================================
     A*
     A          R EU0120F3
     A                                      OVERLAY
     A                                      CA12(12 'PREVIOUS')
      *                                                           /*CRE008*/
     A**********************************5  1'New Euro Accounts - Basic Account -
     A**********************************    Details'
     A**********************************    DSPATR(UL)
     A**********************************    DSPATR(HI)
     A  78                              5  1'New Euro Accounts - Basic Account -
     A                                      Details'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N78                              5  1'New Accounts - Basic Account -
     A                                      Details'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *                                                           /*CRE008*/
     A                                  7  2'Date Account Opened'
     A            S3DACO         6   B  7 24
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(HI)
     A                                  8  2'Next Statement Date'
     A            S3NSTD         6   B  8 24
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(HI)
     A                                  8 34'Statement Frequency'
     A            S3STFQ         1   B  8 55
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A                                  8 59'Statement Day'
     A            S3STDY         2   B  8 73
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
      *                                                           /*CRE008*/
     A*********************************11  1'New Euro Accounts - Interest Detai-
     A*********************************     ls'
     A*********************************     DSPATR(UL)
     A*********************************     DSPATR(HI)
     A  78                             11  1'New Euro Accounts - Interest Detai-
     A                                      ls'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A N78                             11  1'New Accounts - Interest Details'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *                                                           /*CRE008*/
     A                                 12 44'DEBIT Interest'
     A                                 12 64'CREDIT Interest'
     A                                 14  2'Base Rate Type'
     A            S3DBRT         2   B 14 44
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(HI)
     A            S3CBRT         2   B 14 64
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(HI)
     A                                 15  2'Rate/Spread'
     A            S3DRTS        12   B 15 44
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(HI)
     A            S3DRSS         1   B 15 57
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(HI)
     A            S3CRTS        12   B 15 64
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(HI)
     A            S3CRSS         1   B 15 77
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N39                                  DSPATR(HI)
     A                                 16  2'Interest Type/Sub Type'
     A            S3DINT         2   B 16 44
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(HI)
     A            S3DIST         5   B 16 47
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(HI)
     A            S3CINT         2   B 16 64
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(HI)
     A            S3CIST         5   B 16 67
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43                                  DSPATR(HI)
     A                                 17  2'Calculation Basis'
     A            S3DCLB         1   B 17 44
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A N44                                  DSPATR(HI)
     A            S3CCLB         1   B 17 64
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N45                                  DSPATR(HI)
     A                                 18  2'Next Capitalization Date/Frequency-
     A                                      /Day'
     A            S3DNIC         6   B 18 44
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N46                                  DSPATR(HI)
     A            S3DCFQ         1   B 18 51
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A N47                                  DSPATR(HI)
     A            S3DCDY         2   B 18 53
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A N48                                  DSPATR(HI)
     A            S3CNIC         6   B 18 64
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A N49                                  DSPATR(HI)
     A            S3CCFQ         1   B 18 71
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A N50                                  DSPATR(HI)
     A            S3CCDY         2   B 18 73
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N51                                  DSPATR(HI)
     A                                 19  2'Credit Charges Calc Type/Sub Type'
     A            S3CCCT         2   B 19 64
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A N52                                  DSPATR(HI)
     A            S3CCST         5   B 19 67
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 69                                  DSPATR(PR)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A N53                                  DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Previous'
     A*
     A*=========================================================================
     A*
     A          R EU0120F4
     A                                      OVERLAY
     A                                      CA12(12 'PREVIOUS')
     A                                  5  1'Sweep Details'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  73                                  DSPATR(ND)
     A                                  7  2'Profit Centre'
     A  72
     AO 73                                  DSPATR(ND)
     A            S4PRFC         4   B  7 24
     A  80
     AAN82                                  DSPATR(UL)
     A N80
     AO 73
     AO 72
     AO 82                                  DSPATR(PR)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A N54                                  DSPATR(HI)
     A  72
     AO 73                                  DSPATR(ND)
     A                                  8  2'Transaction Type'
     A  73                                  DSPATR(ND)
     A            S4TRAT         5   B  8 24
     A  80
     AAN82                                  DSPATR(UL)
     A N80
     AO 73
     AO 82                                  DSPATR(PR)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A N55                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A                                  9  2'Narrative'
     A  73                                  DSPATR(ND)
     A            S4PNAR        30   B  9 24
     A  80
     AAN82                                  DSPATR(UL)
     A N80
     AO 73
     AO 82                                  DSPATR(PR)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A N56                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A                                 10  2'Sweep Date'
     A  73                                  DSPATR(ND)
     A            S4NXTP         6   B 10 24
     A  80
     AAN82                                  DSPATR(UL)
     A N80
     AO 73
     AO 82                                  DSPATR(PR)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A N57                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A                                 11  2'Sweep Frequency'
     A  73                                  DSPATR(ND)
     A            S4PAYF         1   B 11 24
     A  80
     AAN82                                  DSPATR(UL)
     A N80
     AO 73
     AO 82                                  DSPATR(PR)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A N58                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A                                 12  2'Sweep Day Number'
     A  73                                  DSPATR(ND)
     A            S4DYTP         2   B 12 24
     A  80
     AAN82                                  DSPATR(UL)
     A N80
     AO 73
     AO 82                                  DSPATR(PR)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A N59                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A                                 13  2'Final Date'
     A  73                                  DSPATR(ND)
     A            S4DLPY         6   B 13 24
     A  80
     AAN82                                  DSPATR(UL)
     A N80
     AO 73
     AO 82                                  DSPATR(PR)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
     A                                 16  1'Scheduled Action Dates'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *                                                           /*CRE008*/
     A*********************************18  2'Date to create Euro accounts'
     A  78                             18  2'Date to create Euro accounts'
     A N78                             18  2'Date to create accounts'
      *                                                           /*CRE008*/
     A            S4CRAC         6   B 18 39
     A  80
     AAN69                                  DSPATR(UL)
     A  69
     AON80                                  DSPATR(PR)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                 19  2'Date to create sweeps'
     A  73                                  DSPATR(ND)
     A            S4CRSW         6   B 19 39
     A  80
     AAN82                                  DSPATR(UL)
     A  82
     AO 73
     AON80                                  DSPATR(PR)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A  73                                  DSPATR(ND)
      *                                                           /*CRE008*/
     A*********************************20  2'Date to close ''in'' currency acco-
     A*********************************     unts'
     A  78                             20  2'Date to close ''in'' currency acco-
     A                                      unts'
     A N78                             20  2'Date to close existing accounts'
      *                                                           /*CRE008*/
     A            S4CLAC         6   B 20 39
     A  80                                  DSPATR(UL)
     A N80                                  DSPATR(PR)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 14'F12=Previous'
     A*
     A*=========================================================================
     A          R EU0120F5
     A                                      OVERLAY
     A                                 14 33'No records found'
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A*
     A*=========================================================================
     A* Dummy format to clear subfile screen
     A          R EU0120F6
     A*
     A*=========================================================================
     A*
     A          R EU0120S1                  SFL
     A  29                                  SFLNXTCHG
     A            S5SEL          1A  B 11  3
     A  80                                  DSPATR(UL)
     A N80
     AO 24
     AO 69                                  DSPATR(PR)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A  69
     AON80
     AO 24                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  S5INAC        22A  O 11  6                                                  CGL029
     A            S5INAC        28A  O 11  6
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  S5IACN        10A  O 11 29                                                  CGL029
     A            S5IACN        10A  O 11 35
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  S5EUAC        15A  O 11 45                                                  CGL029
     A            S5EUAC        15A  O 11 51
     A**End of change CGL029
     A                                      DSPATR(HI)
     A  24                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  S5EACD         4A  B 11 61                                                  CGL029
     A            S5EACD        10A  B 11 67
     A**End of change CGL029
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 24
     AO 69                                  DSPATR(PR)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A  24                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  S5ESQN         2A  B 11 66                                                  CGL029
     A            S5ESQN         2A  B 11 78
     A**End of change CGL029
     A  80
     AAN69                                  DSPATR(UL)
     A N80
     AO 24
     AO 69                                  DSPATR(PR)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A  24                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  S5EACN        10A  B 11 69                                                  CGL029
     A            S5EACN        10A  B 11 81
     A**End of change CGL029
     A  81
     AAN69 80N83                            DSPATR(UL)
     A  81
     A N80
     AO 24
     AO 69
     AO 83                                  DSPATR(PR)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A  24                                  DSPATR(ND)
     A*
     A** Hidden fields used for subfile update
     A**Start of change CGL029
     A**********  H#EACD         4S 0H                                                        CGL029
     A            H#EACD        10S 0H
     A**End of change CGL029
     A            H#ESQN         2S 0H
     A            H#EACN        10S 0H
     A*
     A*=========================================================================
     A*
     A          R EU0120S2                  SFLCTL(EU0120S1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      CA12(12 'PREVIOUS')
     A N26                                  ROLLUP(28)
     A                                      OVERLAY
     A  25                                  SFLDSP
     A                                      SFLDSPCTL
     A  27                                  SFLCLR
     A  26N27                               SFLEND(*MORE)
     A            ##SFRC         4S 0H      SFLRCDNBR
     A                                  4  3'Map Number'
     A            S5MAPN         6A  O  4 15
     A                                      DSPATR(HI)
     A                                  4 60'Review from'
     A  23                                  DSPATR(ND)
     A            S5REVF         6A  B  4 73
     A                                      DSPATR(UL)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A  23                                  DSPATR(ND)
     A  23                                  DSPATR(PR)
     A                                  6  3'Actions,'
     A  69
     AON80                                  DSPATR(ND)
     A                                  7  4'D=Delete, Amend account details'
     A  69
     AON80                                  DSPATR(ND)
     A                                 10  2'Sel'
     A  69
     AON80                                  DSPATR(ND)
      *                                                           /*CRE008*/
     A**********************************9 45'<------------- EURO ------------>'
     A**Start of change CGL029
     A**78******                        9 45'<------------- EURO ------------>'               CGL029
     A*N78******                        9 45'<-------------- NEW ------------>'               CGL029
     A  78                              9 51'<------------- EURO ------------>'
     A N78                              9 51'<-------------- NEW ------------>'
     A**End of change CGL029
      *                                                           /*CRE008*/
     A**Start of change CGL029
     A**********                       10 45'GL-Account'--- EURO ------------>'               CGL029
     A**********                       10 69'RetailoNo.'                                      CGL029
     A                                 10 51'GL Account'
     A                                 10 81'Retail No.'
     A**End of change CGL029
      *                                                           /*CRE008*/
     A**********************************9  6'<-------- ''IN'' CURRENCY ---------
     A**********************************    >'
     A**Start of change CGL029
     A**78******                        9  6'<-------- ''IN'' CURRENCY ---------
     A**********                            >'
     A*N78******                        9  6'<------------ EXISTING ------------
     A**********                            >'
     A  78                              9  6'<-------- ''IN'' CURRENCY ---------+
     A                                      ------>'
     A N78                              9  6'<------------ EXISTING ------------
     A                                      ------>'
     A**End of change CGL029
      *                                                           /*CRE008*/
     A                                 10  6'GL Account'
     A**Start of change CGL029
     A**********                       10 29'Retail No.'                                      CGL029
     A                                 10 35'Retail No.'
     A**End of change CGL029
     A*
     A*=========================================================================
     A*
     A* Subfile footer record format
     A*
     A          R EU0120S3
     A                                      OVERLAY
     A                                 23  3'F3=Exit'
     A                                 23 14'F12=Previous'
     A*
     A*=========================================================================
     A*
     A* Error message subfile
      *
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A                                      TEXT('Message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A  75                                  SFLDSP
     A  75                                  SFLDSPCTL
     A  75                                  SFLINZ
     A  75                                  SFLCLR
     A  75                                  SFLEND
     A            ##PGM                     SFLPGMQ
