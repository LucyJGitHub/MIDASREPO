      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU EMU Mapping Detail List Report')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Retail Utilities                                 *
      *                                                               *
      *  DSPF/EU0170DF - Mapping Detail List                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU022  *CREATE    Date 27Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU022 - EMU retail utilities                                *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CA12(12 'Previous')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A*=========================================================================
     A*
     A*Header Format
     A*
     A          R EU0170F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'EU0170F1'
     A                                  1 31'MAPPING DETAIL LIST'
     A                                      DSPATR(UL HI)
     A            RUNA      R        O  1 73REFFLD(RUNA)
      *
     A            ##JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 72TIME
      *
     A*
     A*=========================================================================
     A*
     A          R EU0170F1
     A                                      OVERLAY
     A                                  7  6'Branch Code'
     A            S1BRCA         3A  B  7 38DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30N37                               DSPATR(UL)
     A  37                                  DSPATR(PR)
     A                                  9  6'Account Code'
     A**Start of change CGL029
     A**********  S1ACOD         4A  B  9 38DSPATR(HI)                                        CGL029
     A            S1ACOD        10A  B  9 38DSPATR(HI)
     A**End of change CGL029
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A                                 11  6'Customer'
     A            S1CNUM        10A  B 11 38DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A                                 13  6'Map Number'
     A            S1MAPN         6A  B 13 38DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A                                 15  6'Date to create Euro accounts'
     A            S1CRAC         6A  B 15 38DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A                                 17  6'Date to close ''In'' Ccy accounts'
     A            S1CLAC         6A  B 17 38DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A  71                             19  6'Date to create sweeps'
     A            S1CRSW         6A  B 19 38DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N71                                  DSPATR(ND)
     A N71                                  DSPATR(PR)
     A                                 23  3'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F12=Previous'
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      SFLMSGRCD(24)
     A                                      TEXT('Message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A  89                                  SFLDSP
     A  89                                  SFLDSPCTL
     A  89                                  SFLINZ
     A  89                                  SFLCLR
     A  89                                  SFLEND
     A            ##PGM                     SFLPGMQ
