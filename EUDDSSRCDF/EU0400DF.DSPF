      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU EMU Limit Currency Conversion')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  DSPF/EU0400DF - Limit Currency/Amount Conversion             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 138219             Date 03Jul98               *
      *                 CEU010  *CREATE    Date 23Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  138219 - Help tags point to wrong part of manual             *
      *  CEU010 - Limit Currency Conversion for EMU                   *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A*                                   Start of fix     /*138219*/
     A*                                   End of fix       /*138219*/
      *
     A          R EU0400F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY PUTOVR
      *
      *** Screen Heading
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'EU0400'
     A                                  1 25'Limit Currency/Amount Conversion'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      *** Screen Details
     A                                  8 12'New Limit Currency. . . . . . :'
     A            SLCCY          3A  B  8 44DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10 12'Module to Convert (EM/DL/AL). :'
     A            SMODI          2A  B 10 44DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 12 12'Rounding Factor . . . . . . . :'
     A            SRFAC          1   B 12 44DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
     A                                      OVRDTA
      *
      *** Screen Footer
     A                                 21  3'EM - Exposure Management, -
     A                                       DL - Dealing, Lending,-
                                             Securities, -
     A                                       AL - All'
     A                                 23  3'F3=End   F5=Refresh Screen'
      *
      *** Message Subfile
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *** Message Subfile Control
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
