     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU Statement balance in EUROs update')
     A*****************************************************************
     A*                                                               *
     A*  Midas - EMU Utilities                                        *
     A*                                                               *
     A*  DSPF/EU0420DF - EMU STATEMENT BALANCE IN EUROS UPDATE        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU013  *CREATE    Date 10Jul98               *
     A*                                                               *
     A*---------------------------------------------------------------*
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    CEU013 - EMU A/C Posting Narrative Enhancements            *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03(03 'Exit.')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(24)
     A*=========================================================================
     A*
     A* Header format
     A*
     A          R EU0420F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CA12
     A            S#USER    R        O  1  2REFFLD(USER)
     A                                  1 14'EU0420'
     A                                  1 27'STATEMENT BALANCES IN EUROS'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            S#RUNA    R        O  1 73REFFLD(RUNA)
     A            S#WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 27'ACCOUNTS MASTER FILE UPDATE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A*
     A                                  4  2' '
     A                                 07 17'ACCOUNT CODE:'
     A**Start of change CGL029
     A**********  S1ACOD         4   B 07 35                                                  CGL029
     A            S1ACOD        10   B 07 35
     A**End of change CGL029
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A                                 08 21'CURRENCY:'
     A            S1CCY          3   B 08 35
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A                                 09 21'CUSTOMER:'
     A            S1CNUM         6   B 09 35
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A*
     A                                 23  3'F3=Exit'
     A                                 23 14'F12=Previous'
     A*=========================================================================
     A*
     A* Error message subfile
      *
     A*=========================================================================
     A          R EU0420S1                  SFL
     A                                      SFLMSGRCD(24)
     A                                      TEXT('Message subfile')
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*=========================================================================
     A          R EU0420C1                  SFLCTL(EU0420S1)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  27                                  SFLCLR
     A N27                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
