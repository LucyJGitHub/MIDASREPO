      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU EMU DL Base Ccy Conv. Details Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - E.M.U. Module                                        *
      *                                                               *
      *  DSPF/EU0500DF - Dealing Base Ccy Conversion Details          *
      *                  Maintenance                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU009  *CREATE    Date 11Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU009 - EMU Base Currency for Dealing Conversion            *
      *                                                               *
      *****************************************************************
      *  USE OF INDICATORS                                            *
      *                                                               *
      *  01 - Error in Run Conversion field                           *
      *  02 - Error in Old Dealing Base Currency field                *
      *  03 - Error in New Dealing Base Currency field                *
      *  04 - Protect screen fields                                   *
      *  25 - SFLEND conditioner                                      *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R EU0500X0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'EU0500'
     A                                  1 22'EMU Dealing Base Currency Conv+
                                            ersion Details'
     A                                      DSPATR(UL HI)
     A                                  1 73TIME
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A            SRUNA     R        O  2 73REFFLD(RUNA)
      *
     A                                  8 20'Run Conversion Y/N..........:'
     A            SRCON          1   B  8 50DSPATR(HI)
     A  01                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A N01N04                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10 20'Old Base Ccy for Dealing....:'
     A            SODBC     R        B 10 50REFFLD(CCY)
     A                                      DSPATR(HI)
     A  02                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A N02N04                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 12 20'New Base Ccy for Dealing....:'
     A            SNDBC     R        B 12 50REFFLD(CCY)
     A                                      DSPATR(HI)
     A  03                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A N03N04                               DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 23  3'F3=Exit'
     A*=========================================================================
     A          R EU0500S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R EU0500C0                  TEXT('Program messages')
     A                                      SFLCTL(EU0500S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
