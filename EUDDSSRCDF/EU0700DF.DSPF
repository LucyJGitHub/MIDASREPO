      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU In ccy to Euro GL account mapping input')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  DSPF/EU0700DF - 'In' Currency to Euro GL Account Mapping     *
      *                  Input - Display File                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU028  *CREATE    Date 24Aug98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU028 - EMU GL Utilities                                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA06
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R EU0700F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'EU0700'
     A                                  1 25'''IN'' CURRENCY TO EURO +
     A                                      GL ACCOUNT'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 38'MAPPING'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
     A          R EU0700F1                  OVERLAY
     A                                  5  2'Existing ''In'' Currency +
     A                                      GL Accounts to be Mapped'
     A                                  7  5'Branch'
     A            SBRCA     R        B  7 15TEXT('Branch Code')
     A                                      REFFLD(BRCA)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A                                  9  5'Customer'
     A            SCNUM     R        B  9 15TEXT('Customer')
     A                                      REFFLD(CSNM)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A                                 11  5'''In'' Currencies'
     A            SCCY1     R        B 11 22TEXT('Currency')
     A                                      REFFLD(CCY)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A            SCCY2     R        B 11 26TEXT('Currency')
     A                                      REFFLD(CCY)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI PC)
     A            SCCY3     R        B 11 30TEXT('Currency')
     A                                      REFFLD(CCY)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A            SCCY4     R        B 11 34TEXT('Currency')
     A                                      REFFLD(CCY)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI PC)
     A            SCCY5     R        B 11 38TEXT('Currency')
     A                                      REFFLD(CCY)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI PC)
     A            SCCY6     R        B 11 42TEXT('Currency')
     A                                      REFFLD(CCY)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A            SCCY7     R        B 11 46TEXT('Currency')
     A                                      REFFLD(CCY)
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI PC)
     A            SCCY8     R        B 11 50TEXT('Currency')
     A                                      REFFLD(CCY)
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A            SCCY9     R        B 11 54TEXT('Currency')
     A                                      REFFLD(CCY)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A            SCCY10    R        B 11 58TEXT('Currency')
     A                                      REFFLD(CCY)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A            SCCY11    R        B 11 62TEXT('Currency')
     A                                      REFFLD(CCY)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A                                 13  5'Account Code'
     A                                 13 22'section'
     A            SACSC     R        B 13 31TEXT('Account Section')
     A                                      REFFLD(ACSC)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A                                 14 22'From'
     A**Start of change CGL029
     A**********  SFRAC     R        B 14 31TEXT('From Account')                              CGL029
     A            SFRAC         10   B 14 31TEXT('From Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********                       14 37'to'                                              CGL029
     A**********  STOAC     R        B 14 41TEXT('To Account')                                CGL029
     A                                 14 43'to'
     A            STOAC         10   B 14 47TEXT('To Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A                                 17  2'Mapping Rules'
     A                                 19  5'Create one Euro account +
     A                                      for each customer'
     A                                 20  5'account code and account +
     A                                      sequence'
     A            S1TO1     R        B 20 40TEXT('1-to-1 Mapping')
     A                                      REFFLD(INDI)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A                                 23  2'F3=Exit     F6=Create +
     A                                      GL EURO Account'
      *
     A          R EU0700S0                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
     A          R EU0700C0                  TEXT('Program messages')
     A                                      SFLCTL(EU0700S0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
