      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas EU In ccy to Euro GL a/c mapping maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  DSPF/EU0740DF - 'In' Currency to Euro GL Account Mapping     *
      *                  Maintenance - Display File                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU028  *CREATE    Date 24Aug98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU028 - EMU GL Utilities                                    *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R EU0740F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'EU0740'
     A                                  1 25'''IN'' CURRENCY TO EURO +
     A                                      GL ACCOUNT'
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 35'MAPPING MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
     A          R EU0740F1                  OVERLAY
     A                                  5  2'Display existing mapping +
     A                                      for:'
     A                                  7  5'Branch'
     A            SBRCH     R        B  7 15TEXT('Branch Code')
     A                                      REFFLD(BRCA)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A                                  9  5'Customer'
     A            SCUST     R        B  9 15TEXT('Customer')
     A                                      REFFLD(CSNM)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                 11  5'Account Code from'
     A**Start of change CGL029
     A**********  SFMAC     R        B 11 24TEXT('From Account')                              CGL029
     A            SFMAC         10   B 11 24TEXT('From Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A**Start of change CGL029
     A**********                       11 29'to'                                              CGL029
     A**********  STOAC     R        B 11 32TEXT('To Account')                                CGL029
     A                                 11 35'to'
     A            STOAC         10   B 11 38TEXT('To Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                 23  2'F3=Exit'
      *
     A          R EU0740S0                  SFL
     A  75                                  SFLNXTCHG
     A            SACT      R        B 12  2TEXT('Action')
     A                                      REFFLD(OPTION)
     A N73N74                               DSPATR(HI)
     A  73                                  DSPATR(ND)
     A N73 74                               DSPATR(RI PC)
     A            SIAC      R        O 12  4TEXT('''IN'' Account')
     A                                      REFFLD(ZZ019)
     A            SEAC      R        O 12 24TEXT('Euro Accnt')
     A                                      REFFLD(ZZ019)
     A            SIAB      R        O 12 44TEXT('''IN'' Account +
     A                                      Balance')
     A                                      REFFLD(BAMT)
     A            SEAB      R        O 12 63TEXT('Euro Account +
     A                                      Balance')
     A                                      REFFLD(BAMT)
      *
     A          R EU0740C0                  OVERLAY
     A                                      CA08
     A                                      SFLCTL(EU0740S0)
     A                                      SFLPAG(0010)
     A                                      SFLSIZ(0011)
     A  70                                  SFLDSP
     A  71                                  SFLDSPCTL
     A N70N71                               SFLCLR
     A  72                                  SFLEND
     A            SRRN           4  0H      SFLRCDNBR(CURSOR)
      *
     A                                  5  5'Branch'
     A            SBRCH     R        B  5 15TEXT('Branch Code')
     A                                      REFFLD(BRCA)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A                                  7  5'Customer'
     A            SCUST     R        B  7 15TEXT('Customer')
     A                                      REFFLD(CSNM)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                  7 34'Account Code from'
     A**Start of change CGL029
     A**********  SFMAC     R        B  7 55TEXT('From Account')                              CGL029
     A            SFMAC         10   B  7 55TEXT('From Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A**Start of change CGL029
     A**********                        7 60'to'                                              CGL029
     A**********  STOAC     R        B  7 63TEXT('To Account')                                CGL029
     A                                  7 66'to'
     A            STOAC         10   B  7 69TEXT('To Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                 10  7'''IN'' ACCOUNT'
     A                                      DSPATR(HI)
     A                                 10 27'EURO ACCOUNT'
     A                                      DSPATR(HI)
     A                                 10 54'BALANCE MAPPED'
     A                                      DSPATR(HI)
     A                                 11 48'IN ACCOUNT'
     A                                      DSPATR(HI)
     A                                 11 67'EURO ACCOUNT'
     A                                      DSPATR(HI)
      *
     A          R EU0740F2                  OVERLAY
     A                                      CA08
     A                                  5  5'Branch'
     A            SBRCH     R        B  5 15TEXT('Branch Code')
     A                                      REFFLD(BRCA)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A                                  7  5'Customer'
     A            SCUST     R        B  7 15TEXT('Customer')
     A                                      REFFLD(CSNM)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                  7 34'Account Code from'
     A**Start of change CGL029
     A**********  SFMAC     R        B  7 55TEXT('From Account')                              CGL029
     A            SFMAC         10   B  7 55TEXT('From Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A**Start of change CGL029
     A**********                        7 60'to'                                              CGL029
     A**********  STOAC     R        B  7 63TEXT('To Account')                                CGL029
     A                                  7 66'to'
     A            STOAC         10   B  7 69TEXT('To Account')
     A**********                            REFFLD(ACODA)                                     CGL029
     A**End of change CGL029
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                 10  7'''IN'' ACCOUNT'
     A                                      DSPATR(HI)
     A                                 10 27'EURO ACCOUNT'
     A                                      DSPATR(HI)
     A                                 10 54'BALANCE MAPPED'
     A                                      DSPATR(HI)
     A                                 11 48'IN ACCOUNT'
     A                                      DSPATR(HI)
     A                                 11 67'EURO ACCOUNT'
     A                                      DSPATR(HI)
     A                                 15 25'No Records to Display'
     A                                      DSPATR(RI)
      *
     A          R EU0740F3                  OVERLAY
     A                                 23  2'F3=Exit    F8=Print   +
     A                                      D=Delete'
      *
     A          R EU0740S1
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
     A          R EU0740C1
     A                                      SFLCTL(EU0740S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
