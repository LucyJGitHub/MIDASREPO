      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas EU EMU BCAE conversion detail report')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  PRTF/EU0030P1 - Midas EMU BCAE Conversion Detail Report      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU001 *CREATE     Date 08Sep97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CEU001 - EMU Base Currency for Accounting Conversion         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------------------*
      *** Report Header Record Format *
      *-------------------------------*
      *
     A          R EU0030D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  1  6'REFERENCE EU0030'
     A            BJURPT    R           1 40REFFLD(URPT)
     A                                  1101'DATE'
     A            BJMRDT    R           1106REFFLD(MRDT)
     A                                  1119'PAGE'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                  2101'TIME'
     A                                  2106TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 37'BASE CURRENCY FOR +
     A                                      ACCOUNTING EQUIVALENTS +
     A                                      CONVERSION REPORT'
     A                                  4 37'------------------+
     A                                      -----------------------+
     A                                      -----------------'
     A                                  6  1'Branch'
     A            RBRCA     R           6  8REFFLD(BBRC)
     A            RBRNM     R           6 12REFFLD(DBRNM)
     A                                  7  1'Module'
     A            RMDLE     R           7  8REFFLD(CTYP1)
     A            RMODL     R           7 12REFFLD(ANAM)
     A                                  8 80'Oth'
     A                                  8 85'Value'
     A                                  8 93'Maturity'
     A                                  8103'Old Bccy Equiv/Rate/+
     A                                      Currency'
     A                                  9  4'Transaction ID'
     A                                  9 22'Customer'
     A                                  9 32'Transaction Amount'
     A                                  9 53'Ccy'
     A                                  9 62'Other Amount'
     A                                  9 80'Ccy'
     A                                  9 86'Date'
     A                                  9 95'Date'
     A                                  9103'New Bccy Equiv/Rate/+
     A                                      Currency'
     A                                 10  1'--------------------'
     A                                 10 22'--------'
     A                                 10 31'--------------------'
     A                                 10 53'---'
     A                                 10 58'--------------------'
     A                                 10 80'---'
     A                                 10 85'-------'
     A                                 10 93'--------'
     A                                 10103'--------------------+
     A                                      --------'
      *
      *-----------------------*
      *** Detail Record Format *
      *------------------------*
      *
     A          R EU0030D1
     A  01                                  SPACEB(1)
     A                                      SPACEA(1)
     A            RTRID     R              1REFFLD(ANAM)
     A            RCUST     R             23REFFLD(RINO)
     A            RTAMT     R             31REFFLD(AMNT0)
     A            RCCY1     R             53REFFLD(ACCY)
     A            ROAMT     R             58REFFLD(AMNT0)
     A            RCCY2     R             80REFFLD(ACCY)
     A            RVLDT     R             85REFFLD(VLUE)
     A            RMTDT     R             94REFFLD(MTRY)
     A            ROBCE     R            103REFFLD(AMNT0)
     A            ROBCY     R            126REFFLD(ACCY)
     A                                      SPACEA(1)
     A            RNBCE     R            103REFFLD(AMNT0)
     A            RNBCY     R            126REFFLD(ACCY)
      *
      *----------------------------*
      *** Database Error Reference *
      *----------------------------*
      *
     A          R EU0030D2
     A                                      SPACEB(3)
     A                                    32'*** REFERENCE EU0030'
     A                                    53'DATABASE ERROR ***'
      *
      *------------------*
      *** Report Trailer *
      *------------------*
      *
     A          R EU0030D3
     A                                      SPACEB(2)
     A N02                                49'*** END OF REPORT FOR +
     A                                      BRANCH'
     A N02        RBRCA     R             78REFFLD(BBRC)
     A N02                                82'***'
     A  02                                55'*** END OF REPORT ***'
