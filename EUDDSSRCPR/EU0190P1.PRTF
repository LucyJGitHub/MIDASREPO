      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas EU EMU retail sett. a/c exception report')       *
      *****************************************************************
      *                                                               *
      *  Midas - EMU Retail Utilities                                 *
      *                                                               *
      *  PRTF/EU0190P1 - Retail Settlement A/C Exception Report       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CLE042             Date 06Sep05               *
      *                 CDL038             Date 10May05               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 239481             Date 18May06               *
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU022  *CREATE    Date 17Jul98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  239481 - Show type of account to make report clearer.        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CEU022 - EMU Retail Utilities                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE EU0190P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A            REPNAM        25A        6
     A                                    46'RETAIL SETTLEMENT ACCOUNT EXCEPTIO-
     A                                      N REPORT'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      ------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R CUSESUB
     A                                     6'INPUT'
     A                                    16'VALUE'
     A                                    25'DEBIT'
     A                                    32'DEBIT RETAIL'
     A                                    46'A/C'
     A                                    74'CREDIT'
     A                                    82'CREDIT RETAIL'
     A                                   121'A/C'
     A                                      SPACEA(1)
     A                                     6'DOC. NO.'
     A                                    16'DATE'
     A                                    25'CCY'
     A                                    32'ACCOUNT NO'
     A                                    46'CCY'
     A                                    51'DEBIT AMOUNT'
     A                                    74'CCY'
     A                                    82'ACCOUNT NO.'
     A                                    98'CREDIT AMOUNT'
     A                                   121'CCY'
     A                                      SPACEA(1)
     A                                     6'--------'
     A                                    16'------'
     A                                    25'----'
     A                                    32'-----------'
     A                                    46'---'
     A                                    51'--------------------'
     A                                    74'------'
     A                                    82'--------------'
     A                                    98'--------------------'
     A                                   121'---'
     A                                      SPACEA(2)
      *
      *-----------------------------------------------*
      ***  Detail Record Format for Customer Exchange *
      *-----------------------------------------------*
      *
     A          R CUSEDTL
     A            RIPDN     R              6REFFLD(IPDN)
     A            RVALD     R             16REFFLD(MRDT)
     A            RDRCY     R             26REFFLD(DRCY)
     A            RDRAN     R             33REFFLD(DRAN)
     A            RDACCY    R             46REFFLD(CCY)
     A            RDRAM     R             51REFFLD(AMNT0)
     A            RCRCY     R             75REFFLD(CRCY)
     A            RCRAN     R             83REFFLD(DRAN)
     A            RCRAM     R             98REFFLD(AMNT0)
     A            RCACCY    R            121REFFLD(CCY)
      *
     A                                      SPACEA(1)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R DEALSUB
     A                                    14'DEAL'
     A                                    25'DEAL TYPE/'
     A                                    37'EVENT'
     A                                    46'TRANS'
     A                                    54'RETAIL NO.'
     A                                    70'A/C'
     A**Start of change 239481
     A                                    76'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'NUMBER'
     A                                    25'SUBTYPE'
     A                                    37'DATE'
     A                                    46'CCY'
     A                                    54'SETTLEMENT A/C'
     A                                    70'CCY'
     A**Start of change 239481
     A                                    76'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'-----'
     A                                    25'----------'
     A                                    37'-------'
     A                                    46'-----'
     A                                    54'--------------'
     A                                    70'---'
     A**Start of change 239481
     A                                    76'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *-----------------------------------*
      ***  Detail Record Format for Deals *
      *-----------------------------------*
      *
     A          R DEALDTL
     A            RDLNO     R             14REFFLD(DLNO)
     A            RDTYP     R             27REFFLD(DTYP)
     A                                    29'/'
     A            RDLST     R             30REFFLD(DLST)
     A            RDDAT     R             37REFFLD(MRDT)
     A            RTRCCY    R             46REFFLD(CCY)
     A            RRSAN     R             56REFFLD(DRAN)
     A            RACCY     R             70REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             76REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R DEAMSUB
     A                                    14'DEAL'
     A                                    23'AMENDMENT'
     A                                    34'DEAL TYPE/'
     A                                    47'VALUE'
     A                                    56'TRANS'
     A                                    63'RETAIL NO.'
     A                                    79'A/C'
     A                                      SPACEA(1)
     A                                    14'NUMBER'
     A                                    23'TYPE'
     A                                    34'SUBTYPE'
     A                                    47'DATE'
     A                                    56'CCY'
     A                                    63'SETTLEMENT A/C'
     A                                    79'CCY'
     A                                      SPACEA(1)
     A                                    14'-------'
     A                                    23'---------'
     A                                    34'----------'
     A                                    47'----'
     A                                    56'----'
     A                                    63'--------------'
     A                                    79'---'
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Detail Record Format for Deal Amendments *
      *---------------------------------------------*
      *
     A          R DEAMDTL
     A            RDLNO     R             14REFFLD(DLNO)
     A            RAMTP     R             26REFFLD(AMTP)
     A            RDTYP     R             36REFFLD(DTYP)
     A                                    38'/'
     A            RDLST     R             39REFFLD(DLST)
     A            RVDAT     R             46REFFLD(MRDT)
     A            RTRCCY    R             56REFFLD(CCY)
     A            RRSAN     R             65REFFLD(DRAN)
     A            RACCY     R             79REFFLD(CCY)
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R INPYSUB
     A                                    35'PAYMENT'
     A                                    49'TRANS'
     A                                    56'RETAIL NO.'
     A                                    73'A/C'
     A**Start of change 239481
     A                                    79'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'PAYMENT REF.'
     A                                    35'TYPE/SUBTYPE'
     A                                    49'CCY'
     A                                    56'SETTLEMENT A/C'
     A                                    73'CCY'
     A**Start of change 239481
     A                                    79'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'---------------'
     A                                    35'------------'
     A                                    49'-----'
     A                                    56'--------------'
     A                                    73'---'
     A**Start of change 239481
     A                                    79'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *-----------------------------------------------*
      ***  Detail Record Format for Incoming Payments *
      *-----------------------------------------------*
      *
     A          R INPYDTL
     A            RPREF     R             14REFFLD(PREF)
     A            RPYTP     R             38REFFLD(PYTP)
     A                                    40'/'
     A            RPYST     R             41REFFLD(PYST)
     A            RTRCCY    R             50REFFLD(CCY)
     A            RRSAN     R             57REFFLD(DRAN)
     A            RACCY     R             73REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             79REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R OTPYSUB
     A                                    35'PAYMENT'
     A                                    49'TRANS'
     A                                    56'RETAIL NO.'
     A                                    73'A/C'
     A**Start of change 239481
     A                                    79'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'PAYMENT REF.'
     A                                    35'TYPE/SUBTYPE'
     A                                    49'CCY'
     A                                    56'SETTLEMENT A/C'
     A                                    73'CCY'
     A**Start of change 239481
     A                                    79'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'---------------'
     A                                    35'------------'
     A                                    49'-----'
     A                                    56'--------------'
     A                                    73'---'
     A**Start of change 239481
     A                                    79'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *-----------------------------------------------*
      ***  Detail Record Format for Outgoing Payments *
      *-----------------------------------------------*
      *
     A          R OTPYDTL
     A            RPREF     R             14REFFLD(PREF)
     A            RPYTP     R             38REFFLD(PYTP)
     A                                    40'/'
     A            RPYST     R             41REFFLD(PYST)
     A            RTRCCY    R             50REFFLD(CCY)
     A            RRSAN     R             57REFFLD(DRAN)
     A            RACCY     R             73REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             79REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R CQCDSUB
     A                                    35'CHEQUE'
     A                                    45'PAYMENT'
     A                                    59'TRANS'
     A                                    66'RETAIL NO.'
     A                                    83'A/C'
     A**Start of change 239481
     A                                    89'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'PAYMENT REF.'
     A                                    35'SEQUENCE'
     A                                    45'TYPE/SUBTYPE'
     A                                    59'CCY'
     A                                    66'SETTLEMENT A/C'
     A                                    83'CCY'
     A**Start of change 239481
     A                                    89'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'---------------'
     A                                    35'--------'
     A                                    45'------------'
     A                                    59'-----'
     A                                    66'--------------'
     A                                    83'---'
     A**Start of change 239481
     A                                    89'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *----------------------------------------------------*
      ***  Detail Record Format for Cheques for Collection *
      *----------------------------------------------------*
      *
     A          R CQCDDTL
     A            RPREF     R             14REFFLD(PREF)
     A            RCQSQ     R             38REFFLD(CQSQ)
     A            RPYTP     R             48REFFLD(PYTP)
     A                                    50'/'
     A            RPYST     R             51REFFLD(PYST)
     A            RTRCCY    R             60REFFLD(CCY)
     A            RRSAN     R             67REFFLD(DRAN)
     A            RACCY     R             83REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             89REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R CQPCSUB
     A                                    35'CHEQUE'
     A                                    45'PAYMENT'
     A                                    59'TRANS'
     A                                    66'RETAIL NO.'
     A                                    83'A/C'
     A**Start of change 239481
     A                                    89'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'PAYMENT REF.'
     A                                    35'SEQUENCE'
     A                                    45'TYPE/SUBTYPE'
     A                                    59'CCY'
     A                                    66'SETTLEMENT A/C'
     A                                    83'CCY'
     A**Start of change 239481
     A                                    89'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    14'---------------'
     A                                    35'--------'
     A                                    45'------------'
     A                                    59'-----'
     A                                    66'--------------'
     A                                    83'---'
     A**Start of change 239481
     A                                    89'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *------------------------------------------------*
      ***  Detail Record Format for Cheques to be paid *
      *------------------------------------------------*
      *
     A          R CQPCDTL
     A            RPREF     R             14REFFLD(PREF)
     A            RCQSQ     R             38REFFLD(CQSQ)
     A            RPYTP     R             48REFFLD(PYTP)
     A                                    50'/'
     A            RPYST     R             51REFFLD(PYST)
     A            RTRCCY    R             60REFFLD(CCY)
     A            RRSAN     R             67REFFLD(DRAN)
     A            RACCY     R             83REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             89REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R LOANSUB
     A                                    10'LOAN'
     A                                    19'LOAN TYPE/'
     A                                    31'FACILITY'
     A                                    53'TRANS'
     A                                    60'RETAIL NO.'
     A                                    76'A/C'
     A**Start of change 239481
     A                                    82'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    10'NUMBER'
     A                                    19'SUBTYPE'
     A                                    31'NUMBER'
     A                                    41'EVENT DATE'
     A                                    53'CCY'
     A                                    60'SETTLEMENT A/C'
     A                                    76'CCY'
     A**Start of change 239481
     A                                    82'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    10'-------'
     A                                    19'----------'
     A                                    31'--------'
     A                                    41'----------'
     A                                    53'-----'
     A                                    60'--------------'
     A                                    76'---'
     A**Start of change 239481
     A                                    82'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *-----------------------------------*
      ***  Detail Record Format for Loans *
      *-----------------------------------*
      *
     A          R LOANDTL
      **Start of change CLE148
     A**********  RLNRF     R             10REFFLD(LNRF)                                      CLE148
     A            RLNRF          6A       10
      **End of change CLE148
     A            RLTYP     R             21REFFLD(LTYP)
     A                                    23'/'
     A            RSUTP     R             24REFFLD(SUTP)
     A            RFTYP     R             32REFFLD(FTYP)
     A                                    35'/'
     A            RFSEQ     R             36REFFLD(FSEQ)
     A            RDDAT     R             43REFFLD(MRDT)
     A            RTRCCY    R             54REFFLD(CCY)
     A            RRSAN     R             62REFFLD(DRAN)
     A            RACCY     R             76REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             82REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R LOAMSUB
     A                                    10'LOAN'
     A                                    19'LOAN TYPE/'
     A                                    31'AMENDMENT'
     A                                    42'VALUE'
     A                                    51'AMEND'
     A                                    58'TRANS'
     A                                    65'RETAIL NO.'
     A                                    81'A/C'
     A**Start of change 239481
     A                                    87'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    10'NUMBER'
     A                                    19'SUBTYPE'
     A                                    31'TYPE'
     A                                    42'DATE'
     A                                    51'SEQ.'
     A                                    58'CCY'
     A                                    65'SETTLEMENT A/C'
     A                                    81'CCY'
     A**Start of change 239481
     A                                    87'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    10'-------'
     A                                    19'----------'
     A                                    31'---------'
     A                                    42'-------'
     A                                    51'-----'
     A                                    58'-----'
     A                                    65'--------------'
     A                                    81'---'
     A**Start of change 239481
     A                                    87'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Detail Record Format for Loan Amendments *
      *---------------------------------------------*
      *
     A          R LOAMDTL
      **Start of change CLE148
     A**********  RLNRF     R             10REFFLD(LNRF)                                      CLE148
     A            RLNRF          6A       10
      **End of change CLE148
     A            RLTYP     R             21REFFLD(LTYP)
     A                                    23'/'
     A            RSUTP     R             24REFFLD(SUTP)
     A            RAMTP     R             34REFFLD(AMTP)
     A            RVDAT     R             42REFFLD(MRDT)
     A            RLASN     R             52REFFLD(LASN)
     A            RTRCCY    R             59REFFLD(CCY)
     A            RRSAN     R             67REFFLD(DRAN)
     A            RACCY     R             81REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             87REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R FCLTSUB
     A                                    15'FACILITY TYPE/'
     A                                    31'CUSTOMER'
     A                                    42'TRANS'
     A                                    49'RETAIL NO.'
     A                                    64'A/C'
     A**Start of change 239481
     A                                    70'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    15'NUMBER'
     A                                    49'SETTLEMENT A/C'
     A                                    64'CCY'
     A**Start of change 239481
     A                                    70'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    15'--------------'
     A                                    31'---------'
     A                                    42'-----'
     A                                    49'--------------'
     A                                    64'---'
     A**Start of change 239481
     A                                    70'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *----------------------------------------*
      ***  Detail Record Format for Facilities *
      *----------------------------------------*
      *
     A          R FCLTDTL
     A            RFACT     R             18REFFLD(FACT)
     A                                    21'/'
     A            RFCNO     R             22REFFLD(FCNO)
      * Begin /CSD027/
     A**********  RCNUM     R             32REFFLD(CNUM)                                      CSD027
     A            RCNUM          6A       32TEXT('Customer number')
      * End /CSD027/
     A            RTRCCY    R             43REFFLD(CCY)
     A            RRSAN     R             51REFFLD(DRAN)
     A            RACCY     R             64REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             70REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R FEEDSUB
     A                                    16'CUSTOMER'
     A                                    27'FACILITY'
     A                                    37'FEE'
     A                                    43'LOAN'
     A                                    52'TRANS'
     A                                    59'RETAIL NO.'
     A                                    75'A/C'
     A**Start of change 239481
     A                                    81'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    16'NUMBER'
     A                                    27'NUMBER'
     A                                    37'SEQ'
     A                                    43'NUMBER'
     A                                    52'CCY'
     A                                    59'SETTLEMENT A/C'
     A                                    75'CCY'
     A**Start of change 239481
     A                                    81'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    16'--------'
     A                                    27'--------'
     A                                    37'---'
     A                                    43'-------'
     A                                    52'-----'
     A                                    59'--------------'
     A                                    75'---'
     A**Start of change 239481
     A                                    81'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *--------------------------------------------*
      ***  Detail Record Format for Facilities Fee *
      *--------------------------------------------*
      *
     A          R FEEDDTL
      * Begin /CSD027/
     A**********  RFCNUM    R             17REFFLD(CNUM)                                      CSD027
     A            RFCNUM         6A       17TEXT('Customer number')
      * End /CSD027/
     A            RFFACL    R             28REFFLD(FCLY)
     A            RFFSEQ    R             37REFFLD(FEFSEQ)
      **Start of change CLE148
     A**********  RFLOAN    R             43REFFLD(LOAN)                                      CLE148
     A            RFLOAN         6A       43
      **End of change CLE148
     A            RTRCCY    R             53REFFLD(CCY)
     A            RRSAN     R             61REFFLD(DRAN)
     A            RACCY     R             75REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             81REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R FEADSUB
     A                                    16'FACILITY'
     A                                    26'FEE'
     A                                    31'CUSTOMER'
     A                                    41'TRANS'
     A                                    48'RETAIL ACC'
     A                                    62'A/C'
     A**Start of change 239481
     A                                    68'ACCOUNT'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    16'NUMBER'
     A                                    26'SEQ'
     A                                    31'NUMBER'
     A                                    41'CCY'
     A                                    48'NO. AFFECTED'
     A                                    62'CCY'
     A**Start of change 239481
     A                                    68'TYPE'
     A**End of change 239481
     A                                      SPACEA(1)
     A                                    16'--------'
     A                                    26'---'
     A                                    31'--------'
     A                                    41'-----'
     A                                    48'------------'
     A                                    62'---'
     A**Start of change 239481
     A                                    68'-------'
     A**End of change 239481
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Detail Record Format for Fees Adjusments *
      *---------------------------------------------*
      *
     A          R FEADDTL
     A            RFFACL    R             18REFFLD(FCLY)
     A            RFFSEQ    R             26REFFLD(FSEQ)
      * Begin /CSD027/
     A**********  RFCNUM    R             32REFFLD(CNUM)                                      CSD027
     A            RFCNUM         6A       32TEXT('Customer number')
      * End /CSD027/
     A            RTRCCY    R             42REFFLD(CCY)
     A            RRSAN     R             49REFFLD(DRAN)
     A            RACCY     R             62REFFLD(CCY)
     A**Start of change 239481
     A            RACTP     R             68REFFLD(RPNA)
     A**End of change 239481
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R STANSUB
     A                                    15'REF'
     A                                    24'TRANS'
     A                                    31'DEBIT RETAIL'
     A                                    45'A/C'
     A                                    50'CREDIT RETAIL'
     A                                    65'A/C'
     A                                      SPACEA(1)
     A                                    15'NUMBER'
     A                                    24'CCY'
     A                                    31'ACCOUNT NO.'
     A                                    45'CCY'
     A                                    50'ACCOUNT NO.'
     A                                    65'CCY'
     A                                      SPACEA(1)
     A                                    15'-------'
     A                                    24'-----'
     A                                    31'------------'
     A                                    45'---'
     A                                    50'-------------'
     A                                    65'---'
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Detail Record Format for Standing Orders *
      *---------------------------------------------*
      *
     A          R STANDTL
     A            RSTOR     R             16REFFLD(STOR)
     A            RTRCCY    R             25REFFLD(CCY)
     A            RDRAN     R             32REFFLD(DRAN)
     A            RDACCY    R             45REFFLD(CCY)
     A            RCRAN     R             51REFFLD(DRAN)
     A            RCACCY    R             65REFFLD(CCY)
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R TTRNSUB
     A                                    15'BATCH'
     A                                    24'BATCH TRANS.'
     A                                    38'TRANS'
     A                                    45'RETAIL NO.'
     A                                    61'A/C'
     A                                      SPACEA(1)
     A                                    15'NUMBER'
     A                                    24'NUMBER'
     A                                    38'CCY'
     A                                    45'SETTLEMENT A/C'
     A                                    61'CCY'
     A                                      SPACEA(1)
     A                                    15'-------'
     A                                    24'------------'
     A                                    38'-----'
     A                                    45'--------------'
     A                                    61'---'
     A                                      SPACEA(2)
      *
      *---------------------------------------------------*
      ***  Detail Record Format for RE Teller Transaction *
      *---------------------------------------------------*
      *
     A          R TTRNDTL
     A            RTBID     R             17REFFLD(TBID)
     A            RTBTN     R             27REFFLD(TBTN)
     A            RTRCCY    R             39REFFLD(CCY)
     A            RRSAN     R             47REFFLD(DRAN)
     A            RACCY     R             61REFFLD(CCY)
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R TRADSUB
     A                                    10'TRADE'
     A                                    18'SECURITY'
     A                                    32'CLIENT'
     A                                    40'TRADE'
     A                                    47'TRANS'
     A                                    54'RETAIL NO.'
     A                                    70'A/C'
     A                                      SPACEA(1)
     A                                    10'REF.'
     A                                    18'SHORTNAME'
     A                                    32'NUMBER'
     A                                    40'TYPE'
     A                                    47'CCY'
     A                                    54'SETTLEMENT A/C'
     A                                    70'CCY'
     A                                      SPACEA(1)
     A                                    10'------'
     A                                    18'------------'
     A                                    32'------'
     A                                    40'-----'
     A                                    47'-----'
     A                                    54'--------------'
     A                                    70'---'
     A                                      SPACEA(2)
      *
      *------------------------------------*
      ***  Detail Record Format for Trades *
      *------------------------------------*
      *
     A          R TRADDTL
     A            RTDRF     R             10REFFLD(TDRF)
     A            RTDSS     R             19REFFLD(TDSS)
      * Begin /CSD027/
     A**********  RTCNR     R             32REFFLD(TCNR)                                      CSD027
     A            RTCNR          6A       32TEXT('Counterparty Customer Number +
                                                  - TR')
      * End /CSD027/
     A            RTDTP     R             42REFFLD(TDTP)
     A            RTRCCY    R             48REFFLD(CCY)
     A            RRSAN     R             56REFFLD(DRAN)
     A            RACCY     R             70REFFLD(CCY)
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R POSESUB
     A                                    10'BOOKING'
     A                                    24'SECURITY'
     A                                    53'EVENT'
     A                                    70'TRANS'
     A                                    77'RETAIL NO.'
     A                                    93'A/C'
     A                                      SPACEA(1)
     A                                    10'BRANCH CODE'
     A                                    24'SHORTNAME'
     A                                    38'COUNTERPARTY'
     A                                    53'TYPE'
     A                                    60'DUE DATE'
     A                                    70'CCY'
     A                                    77'SETTLEMENT A/C'
     A                                    93'CCY'
     A                                      SPACEA(1)
     A                                    10'-----------'
     A                                    23'------------'
     A                                    38'------------'
     A                                    53'-----'
     A                                    60'--------'
     A                                    70'-----'
     A                                    77'--------------'
     A                                    93'---'
     A                                      SPACEA(2)
      *
      *------------------------------------------------*
      ***  Detail Record Format for Postion Settlement *
      *------------------------------------------------*
      *
     A          R POSEDTL
     A            RPBCA     R             13REFFLD(PBCA)
     A            RPSSH     R             24REFFLD(PSSH)
      * Begin /CSD027/
     A**********  RPCPY     R             41REFFLD(PCPY)                                      CSD027
     A            RPCPY          6A       41TEXT('Counterparty')
      * End /CSD027/
     A            RPEVT     R             54REFFLD(PEVT)
     A            RPDUD     R             61REFFLD(MRDT)
     A            RTRCCY    R             71REFFLD(CCY)
     A            RRSAN     R             79REFFLD(DRAN)
     A            RACCY     R             93REFFLD(CCY)
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R SWEPSUB
     A                                    15'SWEEP'
     A                                    23'DEBIT'
     A                                    30'DEBIT RETAIL'
     A                                    44'A/C'
     A                                    49'CREDIT'
     A                                    57'CREDIT RETAIL'
     A                                    72'A/C'
     A                                      SPACEA(1)
     A                                    15'REF.'
     A                                    23'CCY'
     A                                    30'ACCOUNT NO.'
     A                                    44'CCY'
     A                                    49'CCY'
     A                                    57'ACCOUNT NO.'
     A                                    72'CCY'
     A                                      SPACEA(1)
     A                                    15'------'
     A                                    23'-----'
     A                                    30'------------'
     A                                    44'---'
     A                                    49'-----'
     A                                    57'-------------'
     A                                    72'---'
     A                                      SPACEA(2)
      *
      *------------------------------------*
      ***  Detail Record Format for Sweeps *
      *------------------------------------*
      *
     A          R SWEPDTL
     A            RSWEP     R             15REFFLD(ASOC)
     A            RDCCY     R             24REFFLD(CCY)
     A            RDRAN     R             31REFFLD(DRAN)
     A            RDACCY    R             44REFFLD(CCY)
     A            RCCYC     R             51REFFLD(CCYC)
     A            RCRAN     R             58REFFLD(DRAN)
     A            RACCY     R             72REFFLD(CCY)
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R FFTRSUB
     A                                    15'TRANSACTION'
     A                                    28'TRANS'
     A                                    35'BROKER'
     A                                    47'A/C'
     A                                    52'CUSTOMER'
     A                                    64'A/C'
     A                                      SPACEA(1)
     A                                    15'NUMBER'
     A                                    28'CCY'
     A                                    35'SETT ACC'
     A                                    47'CCY'
     A                                    52'SETT ACC'
     A                                    64'CCY'
     A                                      SPACEA(1)
     A                                    15'-----------'
     A                                    28'-----'
     A                                    35'----------'
     A                                    47'---'
     A                                    52'----------'
     A                                    64'---'
     A                                      SPACEA(2)
      *
      *-------------------------------------------------------*
      ***  Detail Record Format for Future and Options trades *
      *-------------------------------------------------------*
      *
     A          R FFTRDTL
     A            RTNBR     R             17REFFLD(TNBR)
     A            RISCY     R             29REFFLD(ISCY)
     A            RBSLA     R             35REFFLD(DRAN)
     A            RBCCY     R             47REFFLD(CCY)
     A            RCSLA     R             52REFFLD(DRAN)
     A            RCCCY     R             64REFFLD(CCY)
      *
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R PORTSUB
     A                                    15'CUSTOMER'
     A                                    25'PORTFOLIO'
     A                                    37'CUSTOMER'
     A                                    47'CUSTOMER'
     A                                    59'A/C'
     A                                      SPACEA(1)
     A                                    15'NUMBER'
     A                                    25'REFERENCE'
     A                                    37'SETT CCY'
     A                                    47'SETT CCY'
     A                                    59'CCY'
     A                                      SPACEA(1)
     A                                    15'--------'
     A                                    25'----------'
     A                                    37'--------'
     A                                    47'----------'
     A                                    59'---'
     A                                      SPACEA(2)
      *
      *--------------------------------------------------*
      ***  Detail Record Format for Portfolio Management *
      *--------------------------------------------------*
      *
     A          R PORTDTL
      * Begin /CSD027/
     A**********  RPCNUM    R             16REFFLD(CNUM)                                      CSD027
     A            RPCNUM         6A       16TEXT('Customer number')
      * End /CSD027/
     A            RPPTFR    R             28REFFLD(PTFR)
     A            RPCSCY    R             39REFFLD(CSCY)
     A            RPCUSA    R             47REFFLD(DRAN)
     A            RACCY     R             59REFFLD(CCY)
      *
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      ***  Example /COPY.
     A*/COPY WNCPYSRC,MMnnnnP101
      *
