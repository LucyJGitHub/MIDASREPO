      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas EU EMU invalid nostro shortname report')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  PRTF/EU0300P1 - Midas EMU Invalid Nostro Shortname Report    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU007             Date 14Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU007 - Holiday Processing for EMU                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE EU0300P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    51'EMU INVALID NOSTRO SHORTNAME'
     A                                      SPACEA(001)
     A                                    51'----------------------------'
     A                                      SPACEA(002)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                    20'Currency'
     A                                    34'Nostro Number'
     A                                    53'Nostro Shortname'
     A                                    75'Last Change'
     A                                    92'Last Change'
     A                                      SPACEA(001)
     A                                    78'Date'
     A                                    95'Type'
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEA(001)
     A            RCYCD          3A  O    22
     A            RNONB          2A  O    39
     A            RNOSN         11A  O    55
     A            RLCDT          7A  O    77
     A            RTYLC          7A  O    94
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
