      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('EU Limit Currency Conversion Report')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  PRTF/EU0410P1 - Limit Currency Conversion Report             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU010  *CREATE    Date 19Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CEU010 - Limit Currency Conversion                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE EU0410P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A  50N51N52                          46'LIMIT CURRENCY CONVERSIO+
     A                                      N - EXPOSURE MANAGEMENT'
     A  51N50N52                          46'LIMIT CURRENCY CONVERSIO+
     A                                      N - DEALING AND LENDING'
     A  52N50N51                          46'LIMIT CURRENCY CONVERSIO+
     A                                      N - SECURITIES TRADING '
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'.........................'
     A                                      SPACEA(2)
      *
      ***  -------------- *
      ***  Sub-Heading 1: *
      ***  -------------- *
      *
     A          R SUBHD1
     A  50N51N52                          10'Current Exposure Managem+
     A                                      ent Limit Ccy:'
     A  51N50
     AO 52N50                             10'Current Dealing Base Lim+
     A                                      it Ccy:'
     A  50N51N52  H1EXCY         3        49
     A  51N50
     AO 52N50     H1DLCY         3        42
     A                                   132' '
     A                                      SPACEA(1)
     A  50N51N52                          10'New Exposure Management +
     A                                      Limit Ccy    :'
     A  51N50
     AO 52N50                             10'New Dealing Base Limit C+
     A                                      cy    :'
     A  50N51N52  H1NWEX         3        49
     A  51N50
     AO 52N50     H1NWDL         3        42
     A                                    55'Exchange Rate:'
     A  50N51N52  H1EXER        13S 8     70EDTCDE(3)
     A  51N50
     AO 52N50     H1DLER        13S 8     70EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      ***  ----------------------------------- *
      ***  Dealing Client Limit Sub-Heading 2: *
      ***  ----------------------------------- *
      *
     A          R DLCLH2
     A                                    10'Client Limits - '
     A                                      SPACEA(1)
     A                                    10'-------------           '
     A                                      SPACEA(2)
      *
      ***  ------------------------------------ *
      ***  Dealing Country Limit Sub-Heading 2: *
      ***  ------------------------------------ *
      *
     A          R DLCNH2
     A                                    10'Country Limits - '
     A                                      SPACEA(1)
     A                                    10'--------------   '
     A                                      SPACEA(2)
      *
      ***  ----------------------------------- *
      ***  Dealing Region Limit Sub-Heading 2: *
      ***  ----------------------------------- *
      *
     A          R DLRGH2
     A                                    10'Region Limits - '
     A                                      SPACEA(1)
     A                                    10'-------------   '
     A                                      SPACEA(2)
      *
      ***  --------------------------------------- *
      ***  Securities Country Limit Sub-Heading 2: *
      ***  --------------------------------------- *
      *
     A          R SECNH2
     A                                    10'Country Limits - '
     A                                      SPACEA(1)
     A                                    10'--------------   '
     A                                      SPACEA(2)
      *
      ***  ---------------------------------------- *
      ***  Securities Currency Limit Sub-Heading 2: *
      ***  ---------------------------------------- *
      *
     A          R SECCH2
     A                                    10'Currency Limits - '
     A                                      SPACEA(1)
     A                                    10'---------------   '
     A                                      SPACEA(2)
      *
      ***  ---------------------------------------- *
      ***  Securities Customer Limit Sub-Heading 2: *
      ***  ---------------------------------------- *
      *
     A          R SECSH2
     A                                    10'Customer Limits - '
     A                                      SPACEA(1)
     A                                    10'---------------   '
     A                                      SPACEA(2)
      *
      ***  ---------------------------------------- *
      ***  Securities Security Limit Sub-Heading 2: *
      ***  ---------------------------------------- *
      *
     A          R SESCH2
     A                                    10'Security Limits - '
     A                                      SPACEA(1)
     A                                    10'---------------   '
     A                                      SPACEA(2)
      *
      ***  ----------------------------------------------- *
      ***  Securities Investment Type Limit Sub-Heading 2: *
      ***  ----------------------------------------------- *
      *
     A          R SEIVH2
     A                                    10'Investment Type Limits -'
     A                                      SPACEA(1)
     A                                    10'----------------------  '
     A                                      SPACEA(2)
      *
      ***  ----------------------------------- *
      ***  Dealing Client Limit Sub-Heading 3: *
      ***  ----------------------------------- *
      *
     A          R DLCLH3
     A                                    10'Customer Number:'
     A            SZCUST         6        27
     A                                    35'Level:'
     A            SZLEVL         1        42
     A                                    46'Branch/Company Code:'
     A            SZBCCD         3        67
     A                                      SPACEA(1)
     A                                    10'Customer Name  :'
     A            PCSNAM        35        27
     A                                      SPACEA(2)
      *
      ***  ------------------------------------ *
      ***  Dealing Country Limit Sub-Heading 3: *
      ***  ------------------------------------ *
      *
     A          R DLCNH3
     A                                    10'Country Code:'
     A            SXCNCD         2        24
     A                                    35'Level:'
     A            SXLEVL         1        42
     A                                    46'Branch/Company Code:'
     A            SXBCCD         3        67
     A                                      SPACEA(1)
     A                                    10'Country Name:'
     A            PCNNAM        35        24
     A                                      SPACEA(2)
      *
      ***  ----------------------------------- *
      ***  Dealing Region Limit Sub-Heading 3: *
      ***  ----------------------------------- *
      *
     A          R DLRGH3
     A                                    10'Region Code:'
     A            SYRGCD         2        23
     A                                    35'Level:'
     A            SYLEVL         1        42
     A                                    46'Branch/Company Code:'
     A            SYBCCD         3        67
     A                                      SPACEA(1)
     A                                    10'Region Name:'
     A            PRGNAM        35        23
     A                                      SPACEA(2)
      *
      ***  --------------------------------------- *
      ***  Securities Country Limit Sub-Heading 3: *
      ***  --------------------------------------- *
      *
     A          R SECNH3
     A                                    10'Country Code:'
     A            SVCTYL         2        24
     A                                    38'Book:'
     A            SVBOKE         2        44
     A                                    49'Level:'
     A            SVLVLE         1        56
     A                                    62'Branch/Company Code:'
     A            SVETYE         3        83
     A                                      SPACEA(1)
     A                                    10'Country Name:'
     A            PCNNAM        35        24
     A                                      SPACEA(2)
      *
      ***  ---------------------------------------- *
      ***  Securities Currency Limit Sub-Heading 3: *
      ***  ---------------------------------------- *
      *
     A          R SECCH3
     A                                    10'Currency Code:'
     A            SVCNCL         3        25
     A                                    38'Book:'
     A            SVBOKF         2        44
     A                                    49'Level:'
     A            SVLVLF         1        56
     A                                    62'Branch/Company Code:'
     A            SVETYF         3        83
     A                                      SPACEA(1)
     A                                    10'Currency Name:'
     A            PCCNAM        35        25
     A                                      SPACEA(2)
      *
      ***  ---------------------------------------- *
      ***  Securities Customer Limit Sub-Heading 3: *
      ***  ---------------------------------------- *
      *
     A          R SECSH3
     A                                    10'Customer Number:'
      * Begin /CSD027/
     A**********  SVCSTL         6S 0     27                                                  CSD027
     A            SVCSTL         6A       27
      * End /CSD027/
     A                                    38'Book:'
     A            SVBOKH         2        44
     A                                    49'Level:'
     A            SVLVLH         1        56
     A                                    62'Branch/Company Code:'
     A            SVETYH         3        83
     A                                      SPACEA(1)
     A                                    10'Customer Name  :'
     A            PCSNAM        35        27
     A                                      SPACEA(2)
      *
      ***  ---------------------------------------- *
      ***  Securities Security Limit Sub-Heading 3: *
      ***  ---------------------------------------- *
      *
     A          R SESCH3
     A                                    10'Security Code:'
     A            SVSCTL        10        25
     A                                    38'Book:'
     A            SVBOKI         2        44
     A                                    49'Level:'
     A            SVLVLI         1        56
     A                                    62'Branch/Company Code:'
     A            SVETYI         3        83
     A                                      SPACEA(1)
     A                                    10'Security Name:'
     A            PSCNAM        35        25
     A                                      SPACEA(2)
      *
      ***  ----------------------------------------------- *
      ***  Securities Investment Type Limit Sub-Heading 3: *
      ***  ----------------------------------------------- *
      *
     A          R SEIVH3
     A                                    10'Investment Type:'
     A            SVINTL         3        27
     A                                    38'Book:'
     A            SVBOKG         2        44
     A                                    49'Level:'
     A            SVLVLG         1        56
     A                                    62'Branch/Company Code:'
     A            SVETYG         3        83
     A                                      SPACEA(1)
     A                                    10'Investment Name:'
     A            PIVNAM        35        27
     A                                      SPACEA(2)
      *
      ***  --------------------
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A          R DETHDG
     A                                    49'CURRENT LIMIT'
     A                                    74'NEW LIMIT'
     A                                      SPACEA(1)
     A                                    49'-------------'
     A                                    74'---------'
     A                                      SPACEA(2)
      *
      *-------------------------------------------- *
      ***  Detail Exposure Management Record Format *
      *-------------------------------------------- *
      *
     A          R EXMNDT
     A                                    10'Overall Limit'
     A            CUMOAL        11S 0     50EDTCDE(3)
     A            NWMOAL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Foreign Exchange Limit +
     A                                       for EM'
     A            CUMFXL        11S 0     50EDTCDE(3)
     A            NWMFXL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Money Market Limit'
     A            CUMMML        11S 0     50EDTCDE(3)
     A            NWMMML        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Lending Limit for EM'
     A            CUMLEL        11S 0     50EDTCDE(3)
     A            NWMLEL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Retail Limit for EM'
     A            CUMREL        11S 0     50EDTCDE(3)
     A            NWMREL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Sec. Trad. Limit +
     A                                       for EM'
     A            CUMSTL        11S 0     50EDTCDE(3)
     A            NWMSTL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'SE Issuer Limit for EM'
     A            CUMSLI        11S 0     50EDTCDE(3)
     A            NWMSLI        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'SE Counterparty Limit +
     A                                       for EM'
     A            CUMSCL        11S 0     50EDTCDE(3)
     A            NWMSCL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'FRA Limit for EM'
     A            CUMFRL        11S 0     50EDTCDE(3)
     A            NWMFRL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'IRS Limit for EM'
     A            CUMIRL        11S 0     50EDTCDE(3)
     A            NWMIRL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Futures Limit for EM'
     A            CUMFUL        11S 0     50EDTCDE(3)
     A            NWMFUL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Options Limit for EM'
     A            CUMOPL        11S 0     50EDTCDE(3)
     A            NWMOPL        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'EM Daily Delivery Risk +
     A                                       Limit'
     A            CUEMDR        11S 0     50EDTCDE(3)
     A            NWEMDR        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
      *
      *--------------------------------------------- *
      ***  Detail Dealing Client Limit Record Format *
      *--------------------------------------------- *
      *
     A          R DLCLDT
     A                                    10'FX Limit Amount'
     A            CZFLAM        11S 0     50EDTCDE(3)
     A            NZFLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'FX Line Amount'
     A            CZFXLA        11S 0     50EDTCDE(3)
     A            NZFXLA        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'FX Forward Limit Amount'
     A            CZFXFA        11S 0     50EDTCDE(3)
     A            NZFXFA        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'LE Limit Amount'
     A            CZLLAM        11S 0     50EDTCDE(3)
     A            NZLLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'MDDR Amount'
     A            CZMDDR        11S 0     50EDTCDE(3)
     A            NZMDDR        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'FRA Limit Amount'
     A            CZFRLA        11S 0     50EDTCDE(3)
     A            NZFRLA        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'IRS Limit Amount'
     A            CZIRLM        11S 0     50EDTCDE(3)
     A            NZIRLM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Money Market Limit Amount'
     A            CZMMAM        11S 0     50EDTCDE(3)
     A            NZMMAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Money Market Line Amount'
     A            CZMLAM        11S 0     50EDTCDE(3)
     A            NZMLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Precious Metals Fwd. +
     A                                       Limit Amount'
     A            CZPFAM        11S 0     50EDTCDE(3)
     A            NZPFAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Precious Metals Spot +
     A                                       Limit Amount'
     A            CZPSAM        11S 0     50EDTCDE(3)
     A            NZPSAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Precious Metals Line +
     A                                       Amount'
     A            CZPLAM        11S 0     50EDTCDE(3)
     A            NZPLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *---------------------------------------------- *
      ***  Detail Dealing Country Limit Record Format *
      *---------------------------------------------- *
      *
     A          R DLCNDT
     A                                    10'FX Limit Amount'
     A            CXFLAM        11S 0     50EDTCDE(3)
     A            NXFLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'LE Limit Amount'
     A            CXLLAM        11S 0     50EDTCDE(3)
     A            NXLLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'FRA Limit Amount'
     A            CXFRLA        11S 0     50EDTCDE(3)
     A            NXFRLA        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'IRS Limit Amount'
     A            CXIRLM        11S 0     50EDTCDE(3)
     A            NXIRLM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *---------------------------------------------- *
      ***  Detail Dealing Region Limit Record Format *
      *---------------------------------------------- *
      *
     A          R DLRGDT
     A                                    10'FX Limit Amount'
     A            CYFLAM        11S 0     50EDTCDE(3)
     A            NYFLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'LE Limit Amount'
     A            CYLLAM        11S 0     50EDTCDE(3)
     A            NYLLAM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'FRA Limit Amount'
     A            CYFRLA        11S 0     50EDTCDE(3)
     A            NYFRLA        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'IRS Limit Amount'
     A            CYIRLM        11S 0     50EDTCDE(3)
     A            NYIRLM        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *----------------------------------------------- *
      ***  Detail Security Country Limit Record Format *
      *----------------------------------------------- *
      *
     A          R SECNDT
     A                                    10'Long Limit'
     A            CULLME        11S 0     50EDTCDE(3)
     A            NWLLME        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Short Limit'
     A            CUSLME        11S 0     50EDTCDE(3)
     A            NWSLME        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *------------------------------------------------ *
      ***  Detail Security Currency Limit Record Format *
      *------------------------------------------------ *
      *
     A          R SECCDT
     A                                    10'Long Limit'
     A            CULLMF        11S 0     50EDTCDE(3)
     A            NWLLMF        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Short Limit'
     A            CUSLMF        11S 0     50EDTCDE(3)
     A            NWSLMF        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *------------------------------------------------ *
      ***  Detail Security Customer Limit Record Format *
      *------------------------------------------------ *
      *
     A          R SECSDT
     A                                    10'Long Limit'
     A            CULLMH        11S 0     50EDTCDE(3)
     A            NWLLMH        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Short Limit'
     A            CUSLMH        11S 0     50EDTCDE(3)
     A            NWSLMH        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Counterparty Limit +
     A                                       Purchase'
     A            CUCLMP        11S 0     50EDTCDE(3)
     A            NWCLMP        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Counterparty Limit +
     A                                       Sale'
     A            CUCLMS        11S 0     50EDTCDE(3)
     A            NWCLMS        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *------------------------------------------------ *
      ***  Detail Security Security Limit Record Format *
      *------------------------------------------------ *
      *
     A          R SESCDT
     A                                    10'Long Limit'
     A            CULLMI        11S 0     50EDTCDE(3)
     A            NWLLMI        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Short Limit'
     A            CUSLMI        11S 0     50EDTCDE(3)
     A            NWSLMI        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *-------------------------------------------------- *
      ***  Detail Security Investment Limit Record Format *
      *-------------------------------------------------- *
      *
     A          R SEIVDT
     A                                    10'Long Limit'
     A            CULLMG        11S 0     50EDTCDE(3)
     A            NWLLMG        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    10'Short Limit'
     A            CUSLMG        11S 0     50EDTCDE(3)
     A            NWSLMG        11S 0     73EDTCDE(3)
     A                                   132' '
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      *
