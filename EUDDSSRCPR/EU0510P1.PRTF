      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas EMU Base Ccy Conversion detail report')
      *****************************************************************
      *                                                               *
      *  Midas - E.M.U. Module                                        *
      *                                                               *
      *  PRTF/EU0510P1 - EMU Base Currency for Dealing                *
      *                  Conversion - detail report                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU009  *CREATE    Date 13Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU009 - EMU Base Currency for Dealing conversion            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE EU0510P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'EMU Base Currency for De+
     A                                      aling Conversion Details'
      *
     A                                    99'Consolidated'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    40'------------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     2'Old Dealing Base Currency:'
     A            ODBC      R             29REFFLD(CCY)
     A                                    33'New Dealing Base Currency:'
     A            NDBC      R             60REFFLD(CCY)
     A                                    64'Conversion Rate:'
     A            CRATE         14A       81
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Header Record Format  - FXDEALPP details *
      *---------------------------------------------*
     A          R HFXDEL
     A                                     2'File Name: FXDEALPP'
     A                                    23'FX Deals Master'
     A                                      SPACEA(2)
     A                                     3'Deal'
     A                                     9'Buy'
     A                                    13'Sell'
     A                                    29'Dealt Base Ccy Equivalent'
     A                                    83'Dealt Spot Base Ccy Equivalent'
     A                                      SPACEA(1)
     A                                    27'Before'
     A                                    54'After'
     A                                    81'Before'
     A                                   108'After'
     A                                      SPACEA(2)
     A                                    12'Other Dealt Spot Base Ccy +
     A                                      Equivalent'
     A                                      SPACEA(1)
     A                                    12'Before'
     A                                    39'After'
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Detail Record Format  - FXDEALPP details *
      *---------------------------------------------*
      *
     A          R DFXDEL
      *
     A                                      SPACEA(2)
     A            ODLNO          6A        2
     A            BCCY      R              9REFFLD(CCY)
     A            SCCY      R             13REFFLD(CCY)
     A            BUDBCE        25A       17
     A            AUDBCE        25A       44
     A            BUDSBE        25A       71
     A            AUDSBE        25A       98
     A                                      SPACEA(2)
     A            BUODBE        25A        2
     A            AUODBE        25A       29
      *
      *---------------------------------------------*
      ***  Header Record Format  - FXEPOSPP details *
      *---------------------------------------------*
     A          R HFXEPO
     A                                     2'File Name: FXEPOSPP'
     A                                    23'FX Equivalents, Positions and +
     A                                      Profits'
     A                                      SPACEA(2)
     A                                     2'Ccy'
     A                                    17'Nostro Dealt Base Ccy Equivalent'
     A                                    74'Total Dealt Base Ccy Equivalent'
     A                                      SPACEA(1)
     A                                    18'Before'
     A                                    45'After'
     A                                    74'Before'
     A                                   101'After'
     A                                      SPACEA(2)
     A                                    12'Bank Dealt Base Ccy Equivalent'
     A                                      SPACEA(1)
     A                                    12'Before'
     A                                    39'After'
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Detail Record Format  - FXEPOSPP details *
      *---------------------------------------------*
      *
     A          R DFXEPO
      *
     A                                      SPACEA(2)
     A            OCCY      R              2REFFLD(CCY)
     A            BUNBCE        25A        8
     A            AUNBCE        25A       35
     A            BUTDBE        25A       64
     A            AUTDBE        25A       91
     A                                      SPACEA(2)
     A            BUBDBE        25A        2
     A            AUBDBE        25A       29
      *
      *------------------------------------------------------*
      ***  Header Record Format  - FXFWDHPP/FXFWDDPP details *
      *------------------------------------------------------*
     A          R HFXFWD
     A  01                                 2'File Name: FXFWDHPP'
     A  01                                23'FX Forward Book Half Monthly +
     A                                      Details'
     A                                      SPACEA(2)
     A  02                                 2'File Name: FXFWDDPP'
     A  02                                23'FX Forward Book Daily Details'
     A                                      SPACEA(2)
     A                                     2'Ccy'
     A  01                                 6'Forward Book Half Month'
     A  02                                 6'Forward Book Day'
     A  01                                33'Half Month Net Dealt Base Ccy +
     A                                      Equivalent'
     A                                      SPACEA(1)
     A  02                                33'Daily Net Dealt Base Ccy +
     A                                      Equivalent'
     A                                      SPACEA(1)
     A                                    40'Before'
     A                                    67'After'
     A                                      SPACEA(2)
      *
      *------------------------------------------------------*
      ***  Detail Record Format  - FXFWDHPP/FXFWDDPP details *
      *------------------------------------------------------*
      *
     A          R DFXFWD
      *
     A                                      SPACEA(2)
     A            OCCY      R              2REFFLD(CCY)
     A            OYEAR          4A        9
     A                                    13'-'
     A            OMNTH          2A       14
     A                                    16'-'
     A            ODAY           2A       17
     A            BUNDBE        25A       30
     A            AUNDBE        25A       57
      *
      *---------------------------------------------*
      ***  Header Record Format  - ABDEALPP details *
      *---------------------------------------------*
     A          R HABDEL
     A                                     2'File Name: ABDEALPP'
     A                                    23'Internal Contracts'
     A                                      SPACEA(2)
     A                                     2'Deal1'
     A                                     9'Ccy1'
     A                                    14'Deal2'
     A                                    20'Ccy2'
     A                                    29'Deal Near Date Dealt Base Ccy +
     A                                      Equivalent'
     A                                    83'Deal Far Date Dealt Base Ccy +
     A                                      Equivalent'
     A                                      SPACEA(1)
     A                                    34'Before'
     A                                    41'After'
     A                                    88'Before'
     A                                   115'After'
     A                                      SPACEA(2)
      *
      *---------------------------------------------*
      ***  Detail Record Format  - ABDEALPP details *
      *---------------------------------------------*
      *
     A          R DABDEL
      *
     A                                      SPACEA(2)
     A            ODLN1          6A        2
     A            OCCY1     R              9REFFLD(CCY)
     A            ODLN2          6A       13
     A            OCCY2     R             20REFFLD(CCY)
     A            BUDNDE        25A       24
     A            AUDNDE        25A       51
     A            BUDFDE        25A       78
     A            AUDFDE        25A      105
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R FOOTER
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      ***  Example /COPY.
     A*/COPY WNCPYSRC,MMnnnnP101
      *
