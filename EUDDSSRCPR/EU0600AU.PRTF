      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas EU BdC rates initialisation program audit')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Teller Support Module                         *
      *                                                               *
      *  PRTF/EU0600AU - EMU RTS BdC Rates Initialisation - Audit     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU024  *CREATE    Date 14Jul98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU024 - EMU Retail Teller Support Enhancement               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-------------------------------*
      **  Report Header Record Format *
      *-------------------------------*
      *
     A          R EU0600A1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SPACEA(3)
     A                                     7'REFERENCE EU0600AU'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                      SPACEA(1)
     A                                    47'Bureau de Change Rates'
     A                                    70'Initialisation Audit'
     A                                    91'Report'
     A                                      SPACEA(1)
     A                                    47'----------------------'
     A                                    69'---------------------'
     A                                    90'-------'
     A                                      SPACEA(3)
      *
      *------------------------------*
      ** Report Detail Record Format *
      *------------------------------*
      *
     A          R EU0600A2
     A                                    40'Number of Records'
     A                                    58'Calculated: '
     A            RRCALC    R             78REFFLD(FLSZ)
     A                                      TEXT('No. of Records +
     A                                           Calculated')
     A                                      SPACEA(1)
     A                                    40'Number of Records'
     A                                    58'Updated :'
     A            RRUPDT    R             78REFFLD(FLSZ)
     A                                      TEXT('No. of Records +
     A                                           Updated')
      *
      *---------------*
      **  Null Report *
      *---------------*
      *
     A          R EU0600A3
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
      *
      *---------------------------*
      ***  Database Error  Format *
      *---------------------------*
      *
     A          R EU0600A4                  TEXT('DATABASE ERROR MSG')
      *
     A                                      SPACEA(2)
     A                                    55'**** DATABASE ERROR ****'
     A                                      SPACEA(2)
     A                                    41'***************************+
     A                                      ***************************'
     A                                      SPACEA(1)
     A                                    41'*'
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*    FILE NAME    '''
     A            DBFILE    R             60REFFLD(DBFILE)
     A                                    70''''
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*'
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*    KEY          '''
     A            DBKEY     R             60REFFLD(DBKEY)
     A                                    89''''
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*'
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*    PROGRAM NAME '''
     A            DBPGM     R             60REFFLD(DBPGM)
     A                                    70''''
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*'
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*    AT           '''
     A            DBASE     R             60REFFLD(DBKG)
     A                                    63''''
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'*'
     A                                    94'*'
     A                                      SPACEA(1)
     A                                    41'***************************+
     A                                      ***************************'
      *
      *----------------*
      ** End of Report *
      *----------------*
     A          R EU0600A5
     A                                      SPACEB(4)
     A                                    57'** END OF REPORT **'
      *
