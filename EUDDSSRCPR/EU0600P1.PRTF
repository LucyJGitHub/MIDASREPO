      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas EU BdC rates initialisation program report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Teller Support Module                         *
      *                                                               *
      *  PRTF/EU0600P1 - EMU RTS BdC Rates Initialisation             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CEU024  *CREATE    Date 14Jul98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CEU024 - EMU Retail Teller Support Enhancement               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *---------------------------------*
      **  Report Header Record Format 1 *
      *---------------------------------*
      *
     A          R EU0600H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                  1  6'REFERENCE EU0600P1'
     A            BJURPT    R           1 40REFFLD(URPT)
     A                                  1101'DATE'
     A            BJMRDT    R           1106REFFLD(MRDT)
     A                                  1119'PAGE'
     A                                  1124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  2101'TIME'
     A                                  2106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  3 48'Bureau de Change Rates'
     A                                  3 71'Initialisation'
     A                                  4 48'----------------------'
     A                                  4 70'---------------'
      *
      *---------------------------------*
      **  Report Header Record Format 2 *
      *---------------------------------*
      *
     A          R EU0600H2
      *
     A                                      SPACEB(2)
     A                                    13'Old Buy Rate/'
     A                                    29'Old Sell Rate/'
     A                                    47'Old Ave. Rate/'
     A                                    65'Old Cash Buy Rate/'
     A                                    87'Old Cash Sell Rate/'
     A                                   109'Old Cash Ave. Rate/'
     A                                      SPACEA(1)
     A                                     1'Currency'
     A                                    13'New Buy Rate/'
     A                                    29'New Sell Rate/'
     A                                    47'New Ave. Rate/'
     A                                    65'New Cash Buy Rate/'
     A                                    87'New Cash Sell Rate/'
     A                                   109'New Cash Ave. Rate/'
     A                                      SPACEA(1)
      *
      *------------------------*
      **  Detail Record Format *
      *------------------------*
      *
     A          R EU0600D1
      *
     A                                      SPACEB(1)
     A            ROCYCD         3A  O     3
     A            RODNRT         8S 3O    13
     A                                      EDTCDE(2)
     A            RODPRT         8S 3O    30
     A                                      EDTCDE(2)
     A            RODSRT         8S 3O    48
     A                                      EDTCDE(2)
     A            ROVNRT         8S 3O    68
     A                                      EDTCDE(2)
     A            ROVPRT         8S 3O    89
     A                                      EDTCDE(2)
     A            ROVSRT         8S 3O   112
     A                                      EDTCDE(2)
     A                                      SPACEA(1)
     A            RNDNRT         8S 3O    13
     A                                      EDTCDE(2)
     A            RNDPRT         8S 3O    30
     A                                      EDTCDE(2)
     A            RNDSRT         8S 3O    48
     A                                      EDTCDE(2)
     A            RNVNRT         8S 3O    68
     A                                      EDTCDE(2)
     A            RNVPRT         8S 3O    89
     A                                      EDTCDE(2)
     A            RNVSRT         8S 3O   112
     A                                      EDTCDE(2)
     A                                      SPACEA(1)
      *
      *---------------------------*
      ** Database Error Reference *
      *---------------------------*
      *
     A          R EU0600F1
     A                                      SPACEB(3)
     A                                    47'*** REFERENCE SE7533'
     A                                    68'DATABASE ERROR ***'
      *
      *---------*
      ** Footer *
      *---------*
      *
     A          R EU0600F2
     A                                      SPACEB(4)
     A                                    57'** END OF REPORT **'
      *
