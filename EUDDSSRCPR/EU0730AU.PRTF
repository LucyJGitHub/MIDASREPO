      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas EU EMU create Euro GL accounts audit')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  EU0730AU - EMU Create Euro GL Accounts - Audit Report        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CEU028  *CREATE    Date 24Aug98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CEU028 - EMU GL Utilities                                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *--------------------------------*
      ** Report Header Record Format   *
      *--------------------------------*
     A          R EU0730A1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(1)
     A                                     6'REFERENCE EU0730AU'
     A            RPTTL     R             40REFFLD(URPT)
     A                                   101'DATE'
     A            RMRDT     R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'EMU CREATE EURO GL ACCOUN+
     A                                      TS - AUDIT'
     A                                      SPACEA(1)
     A                                    50'-------------------------+
     A                                      ----------'
     A                                      SPACEA(2)
      *-----------------*
      ** File Control   *
      *-----------------*
     A          R EU0730A2
     A                                     6'RUN CONTROLS'
     A                                      SPACEA(2)
     A                                     6'LF/EUGLMLPD'
     A                                      SPACEA(1)
     A                                     6'-----------'
     A                                      SPACEA(1)
     A                                    10'NO. OF RECORDS READ'
     A            RCTR1          5  0     64EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'PF/EUPOSTPD'
     A                                      SPACEA(1)
     A                                     6'-----------'
     A                                      SPACEA(1)
     A                                    10'NO. OF RECORDS ON FILE'
     A            RENORE         5  0     64EDTCDE(3)
     A                                      SPACEA(1)
     A                                    10'HASH TOTALS'
     A            REHRWN        15  0     51EDTCDE(3)
     A            REHRDC         3  0     66EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'LF/ACCNT'
     A                                      SPACEA(1)
     A                                     6'--------'
     A                                      SPACEA(1)
     A                                    10'NO. OF ACCOUNT INSERTED'
     A            RANINU         5  0     64EDTCDE(3)
     A                                      SPACEA(1)
     A                                    10'NO. OF ACCOUNT CLOSED'
     A            RANOCL         5  0     64EDTCDE(3)
     A                                      SPACEA(3)
      *-------------------------*
      **  No Details to Report  *
      *-------------------------*
     A          R EU0730A3
     A                                    52'*** NO DETAILS TO REPORT +
     A                                      ***'
     A                                      SPACEB(3)
      *-------------------*
      **  Database Error  *
      *-------------------*
     A          R EU0730A4
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'*************************+
     A                                      **************************+
     A                                      ***'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
