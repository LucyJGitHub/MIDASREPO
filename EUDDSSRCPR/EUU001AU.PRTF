      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas List of updated guarantees - Audit List')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Module                            *
      *                                                               *
      *  PRTF/EUU001AU - List of updated Guarantees - Audit List      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd 2005             *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      *  Prev Amend No. ULX004  *CREATE    Date 04Nov98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX004 - Midas Guarantees for EMU                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2005')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(003)
     A                                     6
     A                                      'REFERENCE EUU001AU'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    49
     A                                      'List of Updated Guarantees - Audit'
     A                                      SPACEB(001)
      *
     A                                    49
     A                                      '----------------------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                      SPACEA(002)
     A                                    63
     A                                      'CALCULATED'
     A                                    63
     A                                      '----------'
     A                                      SPACEB(001)
      *
     A                                     6
     A                                      'Guarantees and Collateral file det-
     A                                      ails  - PF/EUGUARPD'
     A                                      SPACEB(003)
      *
     A            RCOUNT         5  0     65
     A                                      EDTCDE(3)
      *
     A                                     6
     A                                      '-----------------------------------
     A                                      -------------------'
     A                                      SPACEB(001)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
