     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas EU EMU BCA conversion prompt program')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  EU0010  - Midas EMU BCA Conversion Prompt Screen Program     *
      *                                                               *
      *  Function:  This prompt program will be selected off an input *
      *  (I/C)      cycle menu option (under 'System maintenance      *
      *             menu'), when the base currency for accounting     *
      *             conversion is required to take place in the       *
      *             following COB.                                    *
      *                                                               *
      *  Called by: EUC0010 - EMU Base Currency Conversion - Part I   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141402             Date 06Oct98               *
      *                  CEU001            DATE 08Sep97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  141402- Added code to move the selected Branch code,         *
      *          Department code, Old Base Currency and               *
      *          New Base Currency into the appropriate fields.       *
      *  CEU001- EMU Base Currency For Accounting Conversion          *
      *                                                               *
      *****************************************************************
      *
      ** BCA Conversion Prompt Screen
      *
     FEU0010DFCF  E                    WORKSTN      KINFSR *PSSR
      *
      ** BCA Conversion Details
      *
     FEUBCACPDUF  E                    DISK         KINFSR *PSSR A
      ****************************************************************
      *  FUNCTION OF INDICATORS                                      *
      *                                                              *
      *  01 - reverse image indicator for SRUNC                      *
      *  02 - reverse image indicator for SOBCY                      *
      *  03 - reverse image indicator for SNBCY                      *
      *  04 - reverse image indicator for SBTNB                      *
      *  05 - reverse image indicator for SBCRA                      *
      *  06 - reverse image indicator for SDEPT                      *
      *  75 - read indicator                                         *
      *  KC - Function 3 indicator                                   *
      ****************************************************************
      /SPACE 3
      ****************************************************************
      *  S U B R O U T I N E    I N D E X                            *
      *                                                              *
      *  SRINIT - program initialization                             *
      *  SRSDET - process screen details                             *
      *  SRMSG5 - moves EMU0005 to message queue                     *
      *  SRCUR  - call access object AOCURRR0                        *
      *  SRSET0 - set indicators to 0                                *
      *  *PSSR  - handles database errors                            *
      *  SRMCLR - clears the message subfile                         *
      *  ZASNMS - handles all messages                               *
      ****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E                    ADEC        3  1
     E/COPY ZSRSRC,ZALIGNZ1
     IPSDS       SDS
     I                                     *PROGRAM SPGM
     I                                      244 253 SJOB
     I                                      254 263 SUSER
     I                                      282 2870STIME
     ILDA       E DSLDA                         256
     ISDBANK    E DSSDBANKPD
     ISDBRCH    E DSSDBRCHPD
     ISDCURR    E DSSDCURRPD
     ISDDEPT    E DSSDDEPTPD
     IDSFDY     E DSDSFDY
     IDSSDY     E DSDSSDY
      /EJECT
      *
      ** Calculation Specification
      *
      *
      ** Main process
      *
     C                     EXSR SRINIT
     C                     EXSR SRSDET
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT -  Program Initialization                             *
      *                                                               *
      *  Called by   :  Main Controlling Subroutine                   *
      *  Calls       :  none                                          *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
     C                     MOVEACPY@      CPY2@  80
     C           *NAMVAR   DEFN           LDA
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANK    PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'EU0010  'DBPGM
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     MOVEL'*FIRST  'DBKEY            * DBERR 01 *
     C                     Z-ADD1         DBASE            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Read BCA Conversion details
      *
     C                     READ EUBCACPD                 75
      *
      ** Initialize screen fields
      *
     C                     MOVE BJMRDT    SMRDT
     C                     MOVE *BLANKS   SRUNC
     C                     MOVE *BLANKS   SOBCY
     C                     MOVE *BLANKS   SNBCY
     C                     MOVE *BLANKS   SBTNB
     C                     MOVE *BLANKS   SBRCA
     C                     MOVE *BLANKS   SDEPT
      *
      ** Check if file is empty
      *
     C           *IN75     IFEQ '1'
     C                     MOVE *BLANKS   SRUNC
     C                     MOVE *BLANKS   SOBCY
     C                     MOVE *BLANKS   SNBCY
     C                     MOVE *BLANKS   SBTNB
     C                     MOVE *BLANKS   SBRCA
     C                     MOVE *BLANKS   SDEPT
     C                     ELSE
     C                     MOVE BCRUNC    SRUNC
     C                     MOVE BCOBCY    SOBCY
     C                     MOVE BCNBCY    SNBCY
     C                     MOVE BCBTNB    SBTNB
     C                     MOVE BCBRCA    SBRCA
     C                     MOVE BCDEPT    SDEPT
     C                     ENDIF
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSDET - Process screen details                              *
      *                                                               *
      *  Called by   :  Main Controlling Subroutine                   *
      *  Calls       :  SRSET0 - Set indicators to 0                  *
      *                 SRMCLR - Clears the message subfile           *
      *                 ZASNMS - Handles all messages                 *
      *                 SRCUR  - Call access object AOCURRR0          *
      *                 ZALIGN - Aligns the alphanumeric field        *
      *                 SRMSG5 - Moves EMU0005 to message queue       *
      *                 AOBRCHR0 - Access object for SDBRCHPD         *
      *                 AODEPTR0 - Access object for SDDEPTPD         *
      *                                                               *
      *****************************************************************
     C           SRSDET    BEGSR
     C                     MOVEL'EUMSGF  'ZAMSGF
     C                     EXSR SRSET0
     C           *INKC     DOWEQ'0'
     C           WVALID    ANDEQ0
     C                     Z-ADD1         WVALID  10
     C                     WRITEEU0010F0
     C                     WRITEEU0010F1
     C                     WRITEEU0010C0
     C                     EXFMTEU0010F2
     C                     EXSR SRMCLR
     C                     WRITEEU0010C0
     C                     EXSR SRSET0
     C           *INKC     IFNE '1'
      *
      ** Validate Run Conversion
      *
     C           SRUNC     IFNE 'Y'
     C           SRUNC     ANDNE'N'
     C                     MOVE '1'       *IN01
     C                     MOVEL'EMU0001' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C                     ENDIF
      *
      ** Validate Old Base Currency
      *
     C                     MOVELSOBCY     WOBCY   1        141402
     C                     MOVE SOBCY     WCCY    3
     C                     EXSR SRCUR
     C           WOBCY     IFEQ '?'                        141402
     C                     MOVE A6CYCD    SOBCY            141402
     C                     END                             141402
     C           PRTCD     IFNE *BLANKS
     C                     MOVE '1'       *IN02
     C                     MOVEL'EMU0002' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C                     ELSE
     C           SOBCY     IFNE BJCYCD
     C                     MOVE '1'       *IN02
     C                     MOVEL'EMU0003' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C                     ENDIF
     C           A6INCY    IFNE 'Y'
     C                     MOVE '1'       *IN02
     C                     MOVEL'EMU0009' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C                     ENDIF
     C                     ENDIF
      *
      ** Validate New Base Currency
      *
     C                     MOVELSNBCY     WNBCY   1        141402
     C                     MOVE SNBCY     WCCY
     C                     EXSR SRCUR
     C           WNBCY     IFEQ '?'                        141402
     C                     MOVE A6CYCD    SNBCY            141402
     C                     END                             141402
     C           PRTCD     IFNE *BLANKS
     C                     MOVE '1'       *IN03
     C                     MOVEL'EMU0008' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C                     ELSE
     C           SNBCY     IFEQ SOBCY
     C                     MOVE '1'       *IN03
     C                     MOVEL'EMU0004' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C                     ENDIF
     C                     ENDIF
      *
      ** Validate Batch Number
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE SBTNB     ZFIELD
     C                     Z-ADD0         ZADEC
     C                     Z-ADD3         ZADIG
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ '1'
     C                     EXSR SRMSG5
      *
     C                     ELSE
     C                     MOVE ZFIELD    WBTNB   3
      *
     C           WBTNB     IFEQ '000'
     C                     EXSR SRMSG5
      *
     C                     ELSE
     C                     MOVEASBTNB     ADEC
     C           '.'       LOKUPADEC                     74
      *
     C           *IN74     IFEQ '1'
     C                     EXSR SRMSG5
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Validate Branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANK    PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM SBRCA     PBRCD   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           PRTCD     IFNE *BLANKS
     C                     MOVE '1'       *IN05
     C                     MOVEL'EMU0006' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C* Process for the use of '?'                         141402
     C                     ELSE                            141402
     C                     MOVE A8BRCD    SBRCA            141402
     C                     ENDIF
      *
      ** Validate Department
      *
     C                     CALL 'AODEPTR0'
     C                     PARM *BLANK    PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM SDEPT     PDPCD   3
     C           SDDEPT    PARM SDDEPT    DSFDY
     C           PRTCD     IFNE *BLANKS
     C                     MOVE '1'       *IN06
     C                     MOVEL'EMU0007' ZAMSID
     C                     EXSR ZASNMS
     C                     Z-ADD0         WVALID
     C* Process for the use of '?'                         141402
     C                     ELSE                            141402
     C                     MOVE AEDPCD    SDEPT            141402
     C                     ENDIF
      *
      ** Check if all fields are valid
      *
     C           WVALID    IFEQ 1
     C                     MOVE SRUNC     BCRUNC
     C                     MOVE SOBCY     BCOBCY
     C                     MOVE SNBCY     BCNBCY
     C                     MOVE WBTNB     BCBTNB
     C                     MOVE SBRCA     BCBRCA
     C                     MOVE SDEPT     BCDEPT
      *
      ** Check if file EUBCACPD is empty
      *
     C           *IN75     IFEQ '1'
     C                     MOVE 'BCAC'    BCFKEY
     C                     WRITEEUBCACD0
     C                     ELSE
     C                     UPDATEUBCACD0
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRMSG5 - Subroutine to send EMU0005 to message queue         *
      *                                                               *
      *  Called by   :  SRSDET - Process screen details               *
      *  Calls       :  ZASNMS - handles all messages                 *
      *                                                               *
      *****************************************************************
     C           SRMSG5    BEGSR
     C                     MOVEL'EMU0005' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN04
     C                     Z-ADD0         WVALID
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCUR - Subroutine to call AOCURRR0                          *
      *                                                               *
      *  Called by   :  SRSDET - Process screen details               *
      *  Calls       :  AOCURRR0 - Access object for SDCURRPD         *
      *                                                               *
      *****************************************************************
     C           SRCUR     BEGSR
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANK    PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM WCCY      PCYCD   3
     C           SDCURR    PARM SDCURR    DSSDY
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSET0 - Subroutine to initialize indicators                 *
      *                                                               *
      *  Called by   :  SRSDET - Process screen details               *
      *  Calls       :  none                                          *
      *                                                               *
      *****************************************************************
     C           SRSET0    BEGSR
     C                     MOVE '0'       *IN01
     C                     MOVE '0'       *IN02
     C                     MOVE '0'       *IN03
     C                     MOVE '0'       *IN04
     C                     MOVE '0'       *IN05
     C                     MOVE '0'       *IN06
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR  **
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      *
     C                     RETRN
      *
     C           #PSSR9    ENDSR                           ** *PSSR9 **
      *
      ********************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRMCLR - Clear Message Subfile                               *
      *                                                               *
      *  Called by   :  SRSDET  - Process screen details              *
      *  Calls       :  Y2CLMSC - Clear Program Message Queue         *
      *                                                               *
      *****************************************************************
      *
     C           SRMCLR    BEGSR                           ** SRMCLR **
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM SPGM      ZAPGMQ 10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C           SRMCL9    ENDSR                           ** SRMCL9 **
      /EJECT
      *
      *****************************************************************
      *                                                               *
      *  ZASNMS - Send Message to Program Message Queue               *
      *                                                               *
      *  Called by   :  SRSDET  - Process screen details              *
      *  Calls       :  Y2SNMGC - Send a Message                      *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR                           ** ZASNMS **
      *
     C           ZAPGMQ    IFEQ *BLANKS
     C                     MOVELSPGM      ZAPGMQ
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C           ZASNM9    ENDSR                           ** ZASNM9 **
      * /COPY sub-routines
     C/COPY ZSRSRC,ZALIGNZ2
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
