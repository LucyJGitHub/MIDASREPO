     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas EU Create EMU sweep records')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Retail Utilities                                 *
      *                                                               *
      *  EU0150 - Create EMU sweep records                            *
      *                                                               *
      *  Function: Creates sweep records on RESWEPPD                  *
      *                                                               *
      *  Called By: EUC0120                                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CRE401             Date 26Nov09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CRE008             Date 19Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 136729             Date 12Jun98               *
      *                 CEU022  *CREATE    Date 19Jan98               *
      *                                                               *
      *===============================================================*
      *                                                               *
      *  CRE401 - 4-Eyes Checking Gaps - Sweeps (Recompile)           *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *  136729 - Stops incorrect sweep threshold from being written  *
      *           to RESWEPPD                                         *
      *  CEU022 - EMU Retail Utilities                                *
      *                                                               *
      *===============================================================*
      *                                                               *
      *    F U N C T I O N   O F   I N D I C A T O R S                *
      *                                                               *
      *    80&81   Used for CHAIN and READ                            *
      *    90-99   Used by standard subroutines                       *
      *                                                               *
      *****************************************************************
      *                   Index to subroutines                        *
      *                   ~~~~~~~~~~~~~~~~~~~~                        *
      *  INIT    -  Initial processing                                *
      *  END     -  Exits program                                     *
      *  WRTSWP  -  Wries a sweep record ro RESWEPL3                  *
      *  *PSSR   -  Exception error handling                          *
      *****************************************************************
      ** EMU Mapping List
     FEUACMLL0UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      ** EMU Mapping Details Header
     FEUMDHDL0UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      ** Sweep details file by Account number
     FRESWEPL0IF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
     F            RESWEPD0                          KRENAMERESWL0
      *
      ** Sweep details file by sweep number
     FRESWEPL3IF  E           K        DISK         KINFSR *PSSR
     F            RESWEPD0                          KRENAMERESWL3
      *-----------------------------------------------------------------
      *E-Specifications
      *-----------------------------------------------------------------
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
      *
      *-----------------------------------------------------------------
      *I-Specifications
      *-----------------------------------------------------------------
      *                                                                -
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
     **                                     134 141 DBFILE
     **                                     142 170 DBKEY
     **                                     171 180 DBPGM
     **                                     181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** External DS for bank details
     ISDBANK    E DSSDBANKPD
      *
      ** General Ledger Details
     ISDGELR    E DSSDGELRPD
      *
      *  First DS for access program, short Data Structure
     IDSFDY     E DSDSFDY
      *
      * Second DS for access programs, long Data Structure
     IDSSDY     E DSDSSDY
      *
     I/COPY ZSRSRC,ZTNLU1Z1
      *
      * ===============================================================
      *  C - Specifications
      * ===============================================================
     C           *ENTRY    PLIST
     C                     PARM           P#MAPN  6
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C                     MOVEACPY@      MKI@   80
      *
      * ===============================================================
      *                     DEFINITIONS
      * ===============================================================
      *                                                                -
      ** Define Key Lists
      ** ~~~~~~~~~~~~~~~~
     C           K#KY01    KLIST
     C                     KFLD           EUIBRC
     C                     KFLD           EUICNM
     C                     KFLD           EUICCY
     C                     KFLD           EUIACD
     C                     KFLD           EUISQN
      *                                                                -
      ** Define Fields
      ** ~~~~~~~~~~~~~
     C           *LIKE     DEFN SWEP      K#SWEP
     C           *LIKE     DEFN EUMAPN    W#MAPN
     C           *LIKE     DEFN BJRDNB    W#RUND
      *                                                                -
      * ===============================================================
      *                M A I N        P R O C E S S I N G
      * ===============================================================
      *
     C                     EXSR INIT
      *
     C                     MOVE P#MAPN    W#MAPN
      *
     C           W#MAPN    CHAINEUMDHDL0             80
      *
     C           W#MAPN    CHAINEUACMLL0             80
      *
      **  Main Loop
     C           *IN80     DOWEQ*OFF
      *Check if sweep record is already created
     C           K#KY01    CHAINRESWEPL3             80
      *
     C           *IN80     IFEQ *OFF
     C           LCD       IFNE BJRDNB                                    CRE008
     C                     MOVE 'Y'       EUSWFA
     C                     ENDIF                                          CRE008
     C                     UPDATEUACMLPF
      *
      **  Process sweep creation
     C                     ELSE
      *
     C                     EXSR WRTSWP
      *
     C                     ENDIF
      *
      **  Read next record to be updated
     C           W#MAPN    READEEUACMLL0                 80
      **  End of loop
     C                     ENDDO
      *
      **  Set 'In' currency sweep processing indicator to 'Y'
     C                     MOVE 'Y'       EUSWPI
      *
      **  Update Mapping Details Header file, commit changes
     C                     UPDATEUMDHDPF
      *
     C                     COMIT
      *
      **  Close down processing
     C                     EXSR END
      *
      *
      *****************************************************************
      * INIT   : Initial Processing                                   *
      *****************************************************************
     C           INIT      BEGSR
      *
      **  Access Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD01        DBASE
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST  'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Check system date format, DDMMYY(98 off) or MMDDYY(98 on)
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Move Midas Run Date into work field
     C                     MOVE BJRDNB    W#RUND
      *
      ** Access general ledger details
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD02        DBASE
     C                     MOVEL'SDGELRPD'DBFILE
     C                     MOVEL'*FIRST  'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      **
      *****************************************************************
      * WRTSWP : Processes the Sweep record                           *
      *****************************************************************
     C           WRTSWP    BEGSR
      *
      **  Select next available sweep reference
     C                     MOVE *BLANK    W#EXIT
     C                     Z-ADD1         K#SWEP
      *
     C           W#EXIT    DOWNE'Y'
      *
     C           K#SWEP    SETLLRESWEPL0                 80
      *
     C           *IN80     IFEQ *ON
     C                     ADD  1         K#SWEP
     C                     ELSE
     C                     MOVE 'Y'       W#EXIT  1
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           *IN80     IFEQ *ON
      *
     C           *LOCK     IN   LDA
     C                     Z-ADD03        DBASE
     C                     MOVEL'RESWEPL0'DBFILE
     C                     MOVEL'        'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
      *
     C                     MOVE 'D'       RECI
     C                     Z-ADDK#SWEP    SWEP
     C                     MOVE EUIBRC    DRBR
     C                     MOVE EUICNM    DCUS
     C                     MOVE EUIACD    DACD
     C                     MOVE EUISQN    DACS
     C*********************MOVE EUIACN    DRAN                            CRE008
     C                     MOVE EUEBRC    CBRC
     C                     MOVE EUECNM    CCUS
     C                     MOVE EUEACD    CSCA
     C                     MOVE EUESQN    CACD
     C*********************MOVE EUEACN    CRAN                            CRE008
     C                     MOVE EUBRCA    BRCA
     C                     MOVE 'N'       MBSI
     C                     MOVE EUICCY    CCY
     C                     MOVE EUNXTP    NXTP
     C                     MOVE EUDLPY    DLPY
     C                     MOVE EUPAYF    PAYF
     C                     MOVE EUDYTP    DYTP
     C                     MOVE EUPNCD    PNCD
     C                     MOVE EUPNAR    PNAR
     C                     MOVE EUPRFC    PRFC
     C                     MOVE EUTRAT    TRAT
     C***********          MOVE 0         STHR                            136729
     C                     Z-ADD0         STHR                            136729
     C                     MOVE '4'       SWTP
     C                     MOVE BJRDNB    LCD
     C                     MOVE 'I'       CHTP
     C*********************MOVE BKEURO    CCYC                            CRE008
     C                     MOVE EUECCY    CCYC                            CRE008
      *
      ** Prevent update by multiple users
     C                     EXSR ZTNLU1
     C                     MOVE NATN      TNLU
      *
      ** Update Sweep details file
     C                     WRITERESWL0
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      * END    : Closedown                                            *
      *****************************************************************
     C           END       BEGSR
      *
     C                     SETON                     LR
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
      *
     C                     MOVE 'Y'       @RUN    1
      *
     C                     DUMP
      *
     C                     ROLBK
      *
     C                     MOVE 'N'       EUSWPI
     C                     UPDATEUMDHDPF
     C                     COMIT
      *
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
     C/COPY ZSRSRC,ZTNLU1Z2
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
