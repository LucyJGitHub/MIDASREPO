     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas EU Account closure request')                     *
      *****************************************************************
      *                                                               *
      *  Midas - EMU Retail Utilities                                 *
      *                                                               *
      *  EU0160 - Bulk Retail Account Maintenance - Close 'In' Ccy    *
      *           Accounts - Submitted Job                            *
      *                                                               *
      *  Function: Closes 'In' currency accounts after mapping to     *
      *            Euro accounts is complete.                         *
      *                                                               *
      *  Called By: EUC0120                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CGL196             Date 07Oct20               *
      *  Prev Amend No. CSD103             Date 10Aug20               *
      *                 MD046248           Date 27Oct17               *
      *                 CRE073             Date 06Dec10               *
      *                 CRE075             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CLE025             Date 20Oct03               *
      *                 CRE008             Date 19Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 134920             Date 08May98               *
      *                 CEU022  *CREATE    Date 16Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL196 - Account Mapping Stopped Cheques Processing          *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CRE008 - Cash Management                                     *
      *  134920 - No trailer processing performed when accounts       *
      *           closed                                              *
      *  CEU022 - EMU Retail Utilities                                *
      *                                                               *
      *****************************************************************
      *                                                               *
      *    F U N C T I O N   O F   I N D I C A T O R S                *
      *                                                               *
      *    80 & 81 Used for CHAIN and READ                            *
      *    98      Date format indicator                              *
      *                                                               *
      *****************************************************************
      *                   Index to subroutines                        *
      *                   ~~~~~~~~~~~~~~~~~~~~                        *
      *  INIT    -  Initial processing                                *
      *  END     -  Exits program                                     *
      *  PRCLOS  -  Processes 'In' currency account closure           *
      *  *PSSR   -  Exception error handling                          *
      *****************************************************************
      ** EMU Mapping List
     FEUACMLL0UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      ** EMU Mapping Details Header
     FEUMDHDL0UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      ** GL Account Master Detail
     FACCBR   UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *                                                                   134920
      ** GL Account Master Trailer                                        134920
     FACCNTAC UF  E                    DISK                               134920
     F                                              KCOMIT                134920
     F                                              KINFSR *PSSR          134920
      *
     F/COPY WNCPYSRC,EUH00007
      *                                                                                       CGL196
     FSTOPC   UF  E           K        DISK         KCOMIT            UC                      CGL196
     F            STOPCSCF                          KIGNORE                                   CGL196
     F            STOPCSEF                          KIGNORE                                   CGL196
     F                                              KINFSR *PSSR                              CGL196
      ** Stopped Cheques File                                                                 CGL196
      *                                                                                       CGL196
     FSTOPCSE UF  E                    DISK         KCOMIT            UC                      CGL196
     F                                              KINFSR *PSSR                              CGL196
      ** Stopped cheques trailer file                                                         CGL196
      *                                                                                       CGL196
     FRESTPCPDO   E                    DISK         KCOMIT            UC                      CGL196
     F                                              KINFSR *PSSR                              CGL196
      ** Audit Stopped Cheques Mapped                                                         CGL196
      *-----------------------------------------------------------------
      *E-Specifications
      *-----------------------------------------------------------------
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY WNCPYSRC,EUH00018
      *                                                                                       CGL196
      ** Stopped amount conversion needs POWER Array (As found in other pgms)                 CGL196
      *                                                                                       CGL196
      /COPY ZSRSRC,ZPOWERZ1                                                                   CGL196
      *                                                                                       CGL196
      /SPACE 3
      *
      *-----------------------------------------------------------------
      *I-Specifications
      *-----------------------------------------------------------------
     E/COPY WNCPYSRC,EUH00008
      *                                                                                       CGL196
     ISTOPCSDF                                                                                CGL196
      ** Rename Stopped Cheque Fields                                                         CGL196
     I              RECI                            CQRECI                                    CGL196
     I              CNUM                            CQCNUM                                    CGL196
     I              CCY                             CQCCY                                     CGL196
     I              ACODQQ                          CQACOQ                                    CGL196
     I              ACOD                            CQACOD                                    CGL196
     I              ACSQ                            CQACSQ                                    CGL196
     I              STCR                            CQSTCR                                    CGL196
     I              ACNO                            CQACNO                                    CGL196
     I              BRCA                            CQBRCA                                    CGL196
     I              FCHQ                            CQFCHQ                                    CGL196
     I              TCHQ                            CQTCHQ                                    CGL196
     I              STPA                            CQSTPA                                    CGL196
     I              DATI                            CQDATI                                    CGL196
     I              STPN                            CQSTPN                                    CGL196
     I              EXPD                            CQEXPD                                    CGL196
     I              ZZ008                           CQZZ08                                    CGL196
     I              LCD                             CQLCD                                     CGL196
     I              CHTP                            CQCHTP                                    CGL196
     I              TNLU                            CQTNLU                                    CGL196
      *                                                                                       CGL196
     IDSSTOP    E DSSTOPCSD                                                                   CGL196
      ** Format of STOPCSD to move fields at one Time in WBEFOR or WAFTER Ds                  CGL196
     I              RECI                            CQRECI                                    CGL196
     I              CNUM                            CQCNUM                                    CGL196
     I              CCY                             CQCCY                                     CGL196
     I              ACODQQ                          CQACOQ                                    CGL196
     I              ACSQ                            CQACSQ                                    CGL196
     I              STCR                            CQSTCR                                    CGL196
     I              ACNO                            CQACNO                                    CGL196
     I              BRCA                            CQBRCA                                    CGL196
     I              FCHQ                            CQFCHQ                                    CGL196
     I              TCHQ                            CQTCHQ                                    CGL196
     I              STPA                            CQSTPA                                    CGL196
     I              DATI                            CQDATI                                    CGL196
     I              STPN                            CQSTPN                                    CGL196
     I              EXPD                            CQEXPD                                    CGL196
     I              ZZ008                           CQZZ08                                    CGL196
     I              LCD                             CQLCD                                     CGL196
     I              CHTP                            CQCHTP                                    CGL196
     I              TNLU                            CQTNLU                                    CGL196
     I              ACOD                            CQACOD                                    CGL196
      *                                                                                       CGL196
     IDSMAPS    E DSRESTPCPD                                                                  CGL196
      ** Split of RESTPCD0 Format                                                             CGL196
     I                                        1 106 WBEFOR                                    CGL196
     I                                      107 212 WAFTER                                    CGL196
      *                                                                                       CGL196
     ISDCURR    E DSSDCURRPD                                                                  CGL196
      ** Currency Details                                                                     CGL196
      *                                                                                       CGL196
     IPARM1       DS                                                                          CGL196
     I                                        1   3 CXBRCA                                    CGL196
     I                                        4   9 CXCNUM                                    CGL196
     I                                       10  12 CXCCY                                     CGL196
     I                                       13  22 CXACOD                                    CGL196
     I                                       23  24 CXACSQ                                    CGL196
     I                                       25  27 CXSTCR                                    CGL196
     I                                       28  37 CXACNO                                    CGL196
      *                                                                                       CGL196
     IPARM2       DS                                                                          CGL196
     I                                        1   3 CXCCY2                                    CGL196
      *                                                                                       CGL196
     IPARM3       DS                                                                          CGL196
     I                                        1   7 RETCD                                     CGL196
      *                                                                -
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
     **                                     134 141 DBFILE
     **                                     142 170 DBKEY
     **                                     171 180 DBPGM
     **                                     181 1830DBASE
      *
      ** Midas SD Rundate
     IRUNDAT    E DSRUNDAT
      *
      ** External DS for bank details
     ISDBANK    E DSSDBANKPD
      *
      *  First DS for access program, short Data Structure
     IDSFDY     E DSDSFDY
      *
      * Second DS for access programs, long Data Structure
     IDSSDY     E DSDSSDY
      *
      * Midas lock up/release/update next transaction number
     I/COPY ZSRSRC,ZTNLU1Z1
      *
      * ===============================================================
      *  C - Specifications
      * ===============================================================
     C           *ENTRY    PLIST
     C                     PARM           P#MAPN  6
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C                     MOVEACPY@      MKI@   80
      *
      * ===============================================================
      *                     DEFINITIONS
      * ===============================================================
      *                                                                -
      ** Define Key Lists
      ** ~~~~~~~~~~~~~~~~
     C           K#KY01    KLIST
     C                     KFLD           BRCA
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
      *                                                                -
      ** Define Fields
      ** ~~~~~~~~~~~~~
     C           *LIKE     DEFN EUMAPN    W#MAPN
     C           *LIKE     DEFN BJRDNB    W#RUND
      *                                                                -
      * ===============================================================
      *                M A I N        P R O C E S S I N G
      * ===============================================================
      *
     C                     MOVE P#MAPN    W#MAPN
      *
     C                     EXSR INIT
      *
     C           W#MAPN    CHAINEUACMLL0             81
      *
      **  Main Loop
     C           *IN81     DOWEQ*OFF
      *
      **  Process account closure
     C                     EXSR PRCLOS
      *
     C           W#MAPN    READEEUACMLL0                 81
      *
     C                     ENDDO
      *
      **  Locate Map Number to be updated on EUMDHDL0
     C           W#MAPN    CHAINEUMDHDL0             80
      *
      **  Set 'In' currency account closure indicator to 'Y'
     C                     MOVE 'Y'       EUCLSE
      *
      **  Update Mapping Details Header file, commit changes
     C                     UPDATEUMDHDPF
     C                     COMIT
      *
      **  Close down processing
     C                     EXSR END
      *
      *
      *****************************************************************
      * INIT   : Initial Processing                                   *
      *****************************************************************
     C           INIT      BEGSR
      *
      **  Access Bank Details
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD01        DBASE
     C                     MOVEL'AOBANKR0'DBFILE
     C                     MOVEL'*FIRST  'DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      **  Check system date format, DDMMYY(98 off) or MMDDYY(98 on)
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Move Midas Run Date into work field
     C                     MOVE BJRDNB    W#RUND
      *
      **
     C           W#MAPN    CHAINEUMDHDL0             80
     C/COPY WNCPYSRC,EUH00009
      *                                                                                       CGL196
      ** Check if Feature CGL196 is active (Account mapping - Process Stopped Cheque)         CGL196
      *                                                                                       CGL196
     C                     MOVE 'N'       CGL196  1                                           CGL196
     C                     CALL 'AOSARDR0'                                                    CGL196
     C                     PARM *BLANK    @RTCD   7                                           CGL196
     C                     PARM '*VERIFY' @OPTN   7                                           CGL196
     C                     PARM 'CGL196'  @SARD   6                                           CGL196
      *                                                                                       CGL196
     C           @RTCD     IFEQ *BLANKS                                                       CGL196
     C                     MOVE 'Y'       CGL196  1                                           CGL196
      *                                                                                       CGL196
      ** Stopped Cheques - Old Account N°                                                     CGL196
      *                                                                                       CGL196
     C           KSTOPC    KLIST                                                              CGL196
     C                     KFLD           EUIBRC                                              CGL196
     C                     KFLD           EUICNM                                              CGL196
     C                     KFLD           EUICCY                                              CGL196
     C                     KFLD           EUIACD                                              CGL196
     C                     KFLD           EUISQN                                              CGL196
      *                                                                                       CGL196
     C                     ENDIF                                                              CGL196
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      * PRCLOS : Processes 'In' currency account closure              *
      *****************************************************************
     C           PRCLOS    BEGSR
      *
     C                     MOVE EUIBRC    BRCA
     C                     MOVE EUICNM    CNUM
     C                     MOVE EUICCY    CCY
     C                     MOVE EUIACD    ACOD
     C                     MOVE EUISQN    ACSQ
      *
      ** Search for 'In' account on GL account master file
     C           K#KY01    CHAINACCBR                80
      *
      ** If account not found or already closed
     C           *IN80     IFEQ *ON
     C           RECI      OREQ 'C'                                       CRE008
     C***********ACST      OREQ 'C'                                       CRE008
      *
     C                     MOVE 'Y'       EUACLO
      *
      ** Update Mapping Details file
     C                     UPDATEUACMLPF
      *
     C                     ELSE
     C           ACST      IFEQ ' '                                       CRE008
     C           DACC      ANDEQ0                                         CRE008
      *                                                                   134920
      ** Save LCD and CHTP                                                134920
     C                     MOVELCHTP      PCHTP   1                       134920
     C                     Z-ADDLCD       PLCD    50                      134920
     C/COPY WNCPYSRC,EUH00010
      *                                                                                       CGL196
      ** If feature is active, Map Stopped check to new A/c                                   CGL196
      *                                                                                       CGL196
     C           CGL196    IFEQ 'Y'                                                           CGL196
      *                                                                                       CGL196
     C                     OPEN STOPC                                                         CGL196
      *                                                                                       CGL196
     C                     Z-ADD0         WKAMD   50       Nbr of amendment                   CGL196
     C                     Z-ADD0         WKSTPA 130       Total of Amendment                 CGL196
      *                                                                                       CGL196
     C                     OPEN RESTPCPD                                                      CGL196
     C           KSTOPC    CHAINSTOPCSDF             70                                       CGL196
     C           *IN70     DOWEQ*OFF                                                          CGL196
      *                                                                                       CGL196
      **  Reset Output Audit Record                                                           CGL196
      *                                                                                       CGL196
     C                     CLEARRESTPCD0                                                      CGL196
      *                                                                                       CGL196
     C           CQRECI    IFEQ 'D'                        Live                               CGL196
     C                     MOVELDSSTOP    WBEFOR                                              CGL196
      *                                                                                       CGL196
      ** use Mapped Account details                                                           CGL196
      *                                                                                       CGL196
     C                     MOVE EUEBRC    CQBRCA                                              CGL196
     C                     MOVELEUECNM    CQCNUM                                              CGL196
     C                     MOVE EUECCY    CQCCY                                               CGL196
     C                     Z-ADDEUEACD    CQACOD                                              CGL196
     C                     Z-ADDEUESQN    CQACSQ                                              CGL196
     C                     Z-ADDEUEACN    CQACNO                                              CGL196
      *                                                                                       CGL196
     C           EUECCY    IFNE EUICCY                     Not same CCy                       CGL196
      *                                                                                       CGL196
      ** Convert the amount in target currency (normally EURO kept as "unknown")              CGL196
      *                                                                                       CGL196
      ** From ccy Details                                                                     CGL196
      *                                                                                       CGL196
     C                     CALL 'AOCURRR0'                                                    CGL196
     C                     PARM '       ' @RTCD                                               CGL196
     C                     PARM '*KEY   ' @OPTN                                               CGL196
     C                     PARM           EUICCY                                              CGL196
     C                     PARM *BLANKS   SDCURR                                              CGL196
      *                                                                                       CGL196
     C           @RTCD     IFEQ *BLANKS                                                       CGL196
     C                     Z-ADDCQSTPA    ZAMTF            Stopped amount                     CGL196
     C                     MOVELEUICCY    ZCCYF            From ccy                           CGL196
     C                     Z-ADDA6SPRT    ZRATEF                Spot Rate                     CGL196
     C                     MOVE A6MDIN    ZMDINF                Mult/Div ind                  CGL196
     C                     Z-ADDA6NBDP    ZCDPF                 Dec. places                   CGL196
     C                     ELSE                                                               CGL196
     C           *LOCK     IN   LDA                                                           CGL196
     C                     Z-ADD02        DBASE                                               CGL196
     C                     MOVEL'AOCURRR0'DBFILE                                              CGL196
     C           'From : ' CAT  EUICCY    DBKEY     P                                         CGL196
     C                     OUT  LDA                                                           CGL196
     C                     EXSR *PSSR                                                         CGL196
     C                     ENDIF                                                              CGL196
      *                                                                                       CGL196
      ** To ccy Details                                                                       CGL196
      *                                                                                       CGL196
     C                     CALL 'AOCURRR0'                                                    CGL196
     C                     PARM '       ' @RTCD                                               CGL196
     C                     PARM '*KEY   ' @OPTN                                               CGL196
     C                     PARM           EUECCY                                              CGL196
     C                     PARM *BLANKS   SDCURR                                              CGL196
      *                                                                                       CGL196
     C           @RTCD     IFEQ *BLANKS                                                       CGL196
     C                     MOVELEUECCY    ZCCYT            From ccy                           CGL196
     C                     Z-ADDA6SPRT    ZRATET                Spot Rate                     CGL196
     C                     MOVE A6MDIN    ZMDINT                Mult/Div ind                  CGL196
     C                     Z-ADDA6NBDP    ZCDPT                 Dec. places                   CGL196
     C                     ELSE                                                               CGL196
     C           *LOCK     IN   LDA                                                           CGL196
     C                     Z-ADD03        DBASE                                               CGL196
     C                     MOVEL'AOCURRR0'DBFILE                                              CGL196
     C           'To '     CAT  EUECCY    DBKEY     P                                         CGL196
     C                     OUT  LDA                                                           CGL196
     C                     EXSR *PSSR                                                         CGL196
     C                     ENDIF                                                              CGL196
      *                                                                                       CGL196
     C                     EXSR ZCCYCN                     Conversion                         CGL196
      *                                                                                       CGL196
     C                     Z-ADDZAMTT     CQSTPA           New Stopped Amount                 CGL196
      *                                                                                       CGL196
      ** Save difference (no taking Dec. place into account)                                  CGL196
      *                                                                                       CGL196
     C                     SUB  ZAMTF     WKSTPA           STPA Diff for update               CGL196
     C                     ADD  ZAMTT     WKSTPA               of Trailer (PF/STOPCSE)        CGL196
      *                                                                                       CGL196
     C                     ENDIF                                                              CGL196
      *                                                                                       CGL196
     C                     MOVE 'D'       CQRECI                                              CGL196
     C                     MOVE 'A'       CQCHTP                                              CGL196
     C                     MOVE W#RUND    CQLCD            Today                              CGL196
     C                     EXSR ZTNLU1                                                        CGL196
     C                     MOVE NATN      CQTNLU                                              CGL196
      *                                                                                       CGL196
     C                     UPDATSTOPCSDF                                                      CGL196
     C                     ADD  1         WKAMD            total of amendment                 CGL196
      *                                                                                       CGL196
      * Update java extension files                                                           CGL196
      *                                                                                       CGL196
     C                     MOVE BBRCA     CXBRCA                                              CGL196
     C                     MOVE BCNUM     CXCNUM                                              CGL196
     C                     MOVE BCCY      CXCCY                                               CGL196
     C                     MOVE BACOD     CXACOD                                              CGL196
     C                     MOVE BACSQ     CXACSQ                                              CGL196
     C                     MOVE BSTCR     CXSTCR                                              CGL196
     C                     MOVE BACNO     CXACNO                                              CGL196
     C                     MOVE CQCCY     CXCCY2                                              CGL196
     C                     MOVE *BLANKS   RETCD                                               CGL196
      *                                                                                       CGL196
     C                     CALL 'EU0760'                                                      CGL196
     C                     PARM           PARM1                                               CGL196
     C                     PARM           PARM2                                               CGL196
     C                     PARM           PARM3                                               CGL196
      *                                                                                       CGL196
     C           RETCD     IFNE *BLANKS                                                       CGL196
     C                     EXSR *PSSR                                                         CGL196
     C                     ENDIF                                                              CGL196
      *                                                                                       CGL196
      ** Save image after                                                                     CGL196
      *                                                                                       CGL196
     C                     MOVELDSSTOP    WAFTER                                              CGL196
      *                                                                                       CGL196
      ** Keep Audit Record in PF/RESTPCPD                                                     CGL196
      *                                                                                       CGL196
     C                     WRITERESTPCD0                                                      CGL196
      *                                                                                       CGL196
     C                     ENDIF                           For Live stopped cheque            CGL196
      *                                                                                       CGL196
      ** Read Next                                                                            CGL196
      *                                                                                       CGL196
     C           KSTOPC    READESTOPCSDF                 70                                   CGL196
     C                     ENDDO                                                              CGL196
      *                                                                                       CGL196
     C                     CLOSERESTPCPD                                                      CGL196
     C                     CLOSESTOPC                                                         CGL196
      *                                                                                       CGL196
     C                     ENDIF                                                              CGL196
      *
     C                     MOVE 'C'       ACST
     C                     MOVE W#RUND    LCD
     C                     MOVE W#RUND    DACC
     C                     MOVE 'C'       CHTP
      *
      ** Prevent update by multiple users
     C                     EXSR ZTNLU1
     C                     MOVE NATN      TNLU
      *
      ** Update GL Account Master file
     C                     UPDATACCNTABF
      *                                                                   134920
      ** Access ACCNTAC                                                   134920
     C           1         SETLLACCNTAC                                   134920
      *                                                                   134920
     C                     READ ACCNTAC                  80               134920
      *                                                                   134920
     C           *IN80     IFEQ '0'                                       134920
     C           NOCL      ADD  1         NOCL                            134920
     C           NOAU      SUB  1         NOAU                            134920
      *                                                                   134920
     C           PLCD      IFEQ W#RUND                                    134920
     C           PCHTP     ANDEQ'I'                                       134920
     C                     ADD  1         NORE                            134920
     C                     SUB  1         NINU                            134920
     C                     END                                            134920
      *                                                                   134920
     C           PLCD      IFEQ W#RUND                                    134920
     C           PCHTP     ANDEQ'A'                                       134920
     C                     SUB  1         NOAM                            134920
     C                     END                                            134920
      *                                                                   134920
      ** Force rundate in LCD                                             134920
     C                     Z-ADDW#RUND    LCD                             134920
     C                     Z-ADDNATN      TNLU                            134920
     C                     UPDATACCNTACF                                  134920
     C                     ENDIF                                          134920
     C/COPY WNCPYSRC,EUH00011
      *                                                                                       CGL196
      ** If feature is active, Map Stopped check to new A/c - trailer                         CGL196
      *                                                                                       CGL196
     C           CGL196    IFEQ 'Y'                                                           CGL196
      *                                                                                       CGL196
      ** Update Trailer  PS/STOPCSE if at least 1 Amendment                                   CGL196
      *                                                                                       CGL196
     C           WKAMD     IFNE 0                          At least 1                         CGL196
      *                                                                                       CGL196
     C                     OPEN STOPCSE                                                       CGL196
     C           1         SETLLSTOPCSEF                                                      CGL196
      *                                                                                       CGL196
     C                     READ STOPCSEF                 70                                   CGL196
      *                                                                                       CGL196
     C           *IN70     IFEQ *ON                                                           CGL196
     C           *LOCK     IN   LDA                                                           CGL196
     C                     Z-ADD04        DBASE                                               CGL196
     C                     MOVEL'STOPCSE 'DBFILE                                              CGL196
     C           'Trailer 'CAT  'Error':1 DBKEY     P                                         CGL196
     C                     OUT  LDA                                                           CGL196
     C                     EXSR *PSSR                                                         CGL196
     C                     ENDIF                                                              CGL196
      *                                                                                       CGL196
     C                     ADD  WKAMD     NSQA              total of amendment                CGL196
      *                                                                                       CGL196
     C           WKSTPA    IFNE 0                                                             CGL196
     C           VQAW      MULT 1000      WRK180 180       Stopped chq amount                 CGL196
     C           WRK180    ADD  VQAD      WRK180                  Add Dec. Amount (3)         CGL196
     C                     ADD  WKSTPA    WRK180            + difference                      CGL196
     C                     Z-ADDWRK180    VQAD             Save Dec. amount (3)               CGL196
     C           WRK180    SUB  VQAD      WRK180            zeorise decimal (3)               CGL196
     C           WRK180    DIV  1000      VQAW             Save amount (15,0)                 CGL196
     C                     ENDIF                                                              CGL196
      *                                                                                       CGL196
     C                     UPDATSTOPCSEF                                                      CGL196
     C                     CLOSESTOPCSE                                                       CGL196
      *                                                                                       CGL196
     C                     ENDIF                           At least 1 amendment               CGL196
      *                                                                                       CGL196
     C                     ENDIF                                                              CGL196
      *
     C                     ENDIF                                          CRE008
     C                     ENDIF
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      * END    : Closedown                                            *
      *****************************************************************
     C           END       BEGSR
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
      *
     C                     MOVE 'Y'       @RUN    1
      *
     C                     DUMP
      *
     C                     ROLBK
      *
     C                     MOVE 'N'       EUCLSE
     C                     UPDATEUMDHDPF
     C                     COMIT
      *
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
     C/COPY ZSRSRC,ZTNLU1Z2
      ********************************************************************
     C/COPY WNCPYSRC,EUH00012
      *                                                                                       CGL196
      ***  Stopped amount conversion needs POWER 8.4 array used in ZCCYCN Routine             CGL196
      *                                                                                       CGL196
      /COPY ZSRSRC,ZCCYCN                                                                     CGL196
**  CPY@
(c) Finastra International Limited 2001
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION                                           CGL196
0000001                                                                                       CGL196
0000010                                                                                       CGL196
0000100                                                                                       CGL196
0001000                                                                                       CGL196
0010000                                                                                       CGL196
0100000                                                                                       CGL196
1000000                                                                                       CGL196
