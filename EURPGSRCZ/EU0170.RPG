     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas EU Mapping details report')                      *
      *****************************************************************
      *                                                               *
      *  Midas - EMU Retail Utilities                                 *
      *                                                               *
      *  EU0170 -  Bulk Maintenance for EMU  Report                   *
      *                                                               *
      *  Function:  This program is a new report to provide a full    *
      *   (IC)      list on-request of all Retail Accounts which      *
      *             have been mapped to Euro accounts. The header     *
      *             and the list will both be printed.                *
      *                                                               *
      *  Called By: FCC0201                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 217435             Date 12May03               *
      *                 CRE008             Date 19Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 133671             Date 17Apr98               *
      *                 CEU022             Date 30JAN98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  217435 - Refine mappings to specify A/c Seq No.              *
      *  CRE008 - Cash Management                                     *
      *  133671 - Blanks out retail account no. of 'in' currency acc  *
      *           when retail account nos have been preserved         *
      *  CEU022 - EMU Retail Utilities                                *
      *                                                               *
      *****************************************************************
      *
     F/COPY WNCPYSRC,EU0170F001
     FEUMDHDL6IF  E           K        DISK         KINFSR *PSSR
      *
     FEUACMLL3IF  E           K        DISK         KINFSR *PSSR
      *
     FEU0170AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FEU0170P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *   71  - Switchable feature CRE004 installed ON                *
      *   72  - Display customer details ON                           *
      *   78  - EMU Mapping                                           *   CRE008
      *   79  - A/c Sequence not specified (i.e. zero)                *   217435
      *                                                               *
      *   80  - Used for CHAIN and READE                              *
      *   89  - End of File                                           *
      *                                                               *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *  S U B R O U T I N E   I N D E X                              *
      *****************************************************************
      *                                                               *
      *  INIT    -  Initial processing                                *
      *  PROCES  -  Main Processing for program                       *
      *  REPORT  -  Process Report Format                             *
      *  WP1REC  -  Write Header Details to Report                    *
      *  WP2REC  -  Write List Details to Report                      *
      *  WP1END  -  Write Report Footer                               *
      *  AUDIT   -  Writes Audit Report and ends program              *
      *  *PSSR   -  Exception error subroutine                        *
      *  RCFP1   -  Registers P1 prInter file via RCF                 *
      *  RCFAU   -  Registers AU printer file via RCF                 *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
     E                    ZMDY   13  13  3 0
     E                    ZYDY    4   4  4 0
     E                    ZMNM   12  12  3               ZDATE2 SR. ARRAY
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ISPOOL1      DS
      ***  File Information Data Structure for EU0170P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for EU0170AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     I            DS
      ***  Data Structure for Prompt program
     I                                        1 100 PRM2
     I                                        1   3 W#BRCA
     I**********                              4   7 W#ACOD                                    CGL029
     I**********                              8  13 W#CNUM                                    CGL029
     I**********                             14  19 W#MAPN                                    CGL029
     I**********                             20  25 W#CRAC                                    CGL029
     I**********                             26  31 W#CRSW                                    CGL029
     I**********                             32  37 W#CLAC                                    CGL029
     I                                        4  13 W#ACOD                                    CGL029
     I                                       14  19 W#CNUM                                    CGL029
     I                                       20  25 W#MAPN                                    CGL029
     I                                       26  31 W#CRAC                                    CGL029
     I                                       32  37 W#CRSW                                    CGL029
     I                                       38  43 W#CLAC                                    CGL029
      *
      ***  Dummy Data Structures used by Access Programs
     IDSFDY     E DSDSFDY
      *
      ***  Dummy Data Structures used by Access Programs
     IDSSDY     E DSDSSDY
      *
      ***  External data structures for Bank Details
     ISDBANK    E DSSDBANKPD
      *
      ***  External data structures for Branch Details
     ISDBRCH    E DSSDBRCHPD
      *
      ** Customer details
     ISDCUST    E DSSDCUSTPD
     I              QQDFAC                          QQDFAX                                    CGL029
      *                                                                                       CGL029
     I/COPY WNCPYSRC,EUH00013
      * ===============================================================
      **                  D E F I N I T I O N S
      * ===============================================================
      *
      *
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Output Audit Report and End Program.
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PRM1  100
      * Move parameter containing input to EU0170p to data structure
     C                     MOVE PRM1      PRM2  100
      *
      **   Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'Y'
     C                     MOVE *ON       *IN37
     C                     ENDIF
      *
      ** Define Fields
      ** ~~~~~~~~~~~~~
     C           *LIKE     DEFN EUACOD    K#ACOD
     C           *LIKE     DEFN EUCNUM    K#CNUM
     C           *LIKE     DEFN EUMAPN    K#MAPN
      *
      ***  Initialise LDA field.
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'EU0170'  DBPGM
     C                     OUT  LDA
      *
     C/COPY WNCPYSRC,EU0170C001
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      *
      **  Access Switchable SAR details for the existence of CRE004
      **  (Authorisation)
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANK    @RTCD
     C                     PARM '*VERIFY' @OPTN
     C                     PARM 'CRE004'  @SARD   6
     C           @RTCD     IFEQ *BLANKS
     C                     MOVE *ON       *IN71
     C                     ELSE
     C                     MOVE *OFF      *IN71
     C                     ENDIF
      *
     C                     MOVELPRM1      PRM2  100
     C/COPY WNCPYSRC,EUH00014
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from File.
      *
     C           W#BRCA    IFEQ *BLANK
     C           W#BRCA    OREQ 'ALL'
     C           *LOVAL    SETLLEUMDHDL6
     C                     READ EUMDHDL6                 89
      *
     C                     ELSE
      *Select correct branch code if specified
     C           W#BRCA    SETLLEUMDHDL6
     C           W#BRCA    READEEUMDHDL6                 89
     C                     ENDIF
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  Do While not End of File.
      *
     C           *IN89     DOWEQ*OFF
      * Initialize error flag
     C                     MOVE *BLANK    W#ERR1
      *
      * Check for selected branch, account code, customer, map number,
      * selected action dates
      * Account code
     C           W#BRCA    IFNE *BLANKS
     C           W#BRCA    ANDNE'ALL'
     C           W#BRCA    IFNE EUBRCA
     C                     MOVE 'Y'       W#ERR1  1
     C                     ENDIF
     C                     ENDIF
      *
     C           W#ACOD    IFNE *BLANKS
     C                     MOVE W#ACOD    K#ACOD
     C           K#ACOD    IFNE EUACOD
     C                     MOVE 'Y'       W#ERR1  1
     C                     ENDIF
     C                     ENDIF
      * Customer number
     C           W#CNUM    IFNE *BLANKS
     C                     MOVE W#CNUM    K#CNUM
     C           K#CNUM    IFNE EUCNUM
     C                     MOVE 'Y'       W#ERR1
     C                     ENDIF
     C                     ENDIF
      * Map number
     C           W#MAPN    IFNE *BLANKS
     C                     MOVE W#MAPN    K#MAPN
     C           K#MAPN    IFNE EUMAPN
     C                     MOVE 'Y'       W#ERR1
     C                     ENDIF
     C                     ENDIF
      * Account creation date
     C           W#CRAC    IFNE *BLANKS
     C                     MOVE W#CRAC    ZDATE
     C                     EXSR ZDATE1
     C           ZDAYNO    IFNE EUCRAC
     C                     MOVE 'Y'       W#ERR1
     C                     ENDIF
     C                     ENDIF
      * Sweep creation date
     C           W#CRSW    IFNE *BLANKS
     C                     MOVE W#CRSW    ZDATE
     C                     EXSR ZDATE1
     C           ZDAYNO    IFNE EUCRSW
     C                     MOVE 'Y'       W#ERR1
     C                     ENDIF
     C                     ENDIF
      * Account closure date
     C           W#CLAC    IFNE *BLANKS
     C                     MOVE W#CLAC    ZDATE
     C                     EXSR ZDATE1
     C           ZDAYNO    IFNE EUCLAC
     C                     MOVE 'Y'       W#ERR1
     C                     ENDIF
     C                     ENDIF
      *
      * Don't display A/c Seq in header if not specified by user          217435
     C                     MOVE *OFF      *IN79                           217435
     C           EUACSQ    IFEQ *ZERO                                     217435
     C                     MOVE *ON       *IN79                           217435
     C                     ENDIF                                          217435
      *                                                                   217435
      *
      * If valid
     C           W#ERR1    IFNE 'Y'
      * Indicates records have been selected
     C                     ADD  1         RCOUNT  60
      * 1)- Write Header Details
     C                     EXSR REPORT
      *
      * 2)- Write all Euro accounts
      *
     C                     MOVE EUMAPN    K#MAPN
      *
     C           K#MAPN    CHAINEUACMLL3             80
      *
     C           *IN80     DOWEQ*OFF
      *
      *
      ***  Write out the record.
      *
     C                     EXSR WP2REC
     C/COPY WNCPYSRC,EUH00015
      *
      ** Read next record
     C           K#MAPN    READEEUACMLL3                 80
      *
     C                     ENDDO
      *
     C                     ENDIF
      ***  Read next record.
     C                     READ EUMDHDL6                 89
      *
     C                     ENDDO
      * If Map No. has been specified before Footer is written
      * change indicator to write single branch footer
     C           W#MAPN    IFNE *BLANKS
     C                     MOVE *OFF      *IN37
     C                     ENDIF
      *
     C           RCOUNT    IFNE 0
     C                     EXSR WP1END
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT -
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
      ***  If branch changes.
      *
     C           EUBRCA    IFNE RBRCA
      *
      ***  If not first record read.
      *
     C           RBRCA     IFNE *BLANKS
     C                     EXSR WP1END
     C                     CLOSEEU0170P1
      *
     C           *IN37     IFEQ *OFF
     C                     EXSR AUDIT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     OPEN EU0170P1
     C                     EXSR RCFP1
     C                     ENDIF
      *
      ***  Get branch name using Access Object AOBRCHR0.
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY    'WOPTN   7
     C                     PARM           EUBRCA
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBRCHPD'DBFILE           ***************
     C                     Z-ADD002       DBASE            * DB ERROR 02 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     MOVELEUBRCA    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ENDIF
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      ***  Write Header.
     C                     WRITEHEADP1                                 **
      *
      ***  Format Report fields.
     C********** EUCNUM    IFNE *ZEROS                                                        CSD027
     C           EUCNUM    IFNE *BLANKS                                                       CSD027
     C                     MOVE EUCNUM    W#CNAM 10
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM '       ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM W#CNAM    @CNUM  10
     C                     PARM           @CNST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDCUSTPD'DBFILE           ***************
     C                     Z-ADD003       DBASE            * DB ERROR 02 *
     C                     MOVE *BLANKS   DBKEY            ***************
     C                     MOVELW#CNUM    DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ELSE
      * Move customer name to print field
     C                     MOVE BBCRNM    RCRNM
     C                     MOVE *ON       *IN72
      *
     C                     ENDIF
      *
     C                     ELSE
      * Display '---ALL---' in customer name and conceal Customer No.
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C/COPY WNCPYSRC,EU0170C002
      * Move File fields to Printer File fields
     C                     MOVE EUMAPN    RMAPN
     C                     MOVE EUBRCA    RBRCA
     C                     MOVE EUACOD    RACOD
     C                     MOVE EUACSQ    RACSQ                           217435
     C                     MOVE EUCCY1    RCCY1
     C                     MOVE EUCCY2    RCCY2
     C                     MOVE EUCCY3    RCCY3
     C                     MOVE EUCCY4    RCCY4
     C                     MOVE EUCCY5    RCCY5
     C                     MOVE EUCCY6    RCCY6
     C                     MOVE EUCCY7    RCCY7
     C                     MOVE EUCCY8    RCCY8
     C                     MOVE EUCCY9    RCCY9
     C                     MOVE EUCY10    RCCY10
     C                     MOVE EUCNUM    RCNUM
     C                     MOVE EUMULT    RMULT
     C                     MOVE EURNPR    RRNPR
     C                     MOVE EURNPO    RRNPO
     C                     MOVE EURNND    RRNND
     C                     MOVE EURNCD    RRNCD
     C                     MOVE EUSTFQ    RSTFQ
     C                     MOVE EUSTDY    RSTDY
     C                     MOVE EUDBRT    RDBRT
     C                     MOVE EUDRTS    RDRTS
     C                     MOVE EUDRSS    RDRSS
     C                     MOVE EUDINT    RDINT
     C                     MOVE EUDIST    RDIST
     C                     MOVE EUDCLB    RDCLB
     C                     MOVE EUDCFQ    RDCFQ
     C                     MOVE EUDCDY    RDCDY
     C                     MOVE EUCBRT    RCBRT
     C                     MOVE EUCRTS    RCRTS
     C                     MOVE EUCRSS    RCRSS
     C                     MOVE EUCINT    RCINT
     C                     MOVE EUCIST    RCIST
     C                     MOVE EUCCLB    RCCLB
     C                     MOVE EUCCFQ    RCCFQ
     C                     MOVE EUCCDY    RCCDY
     C                     MOVE EUCCCT    RCCCT
     C                     MOVE EUCCST    RCCST
      *
      *Perform sweep processing
     C           *IN71     IFEQ *ON
      *
     C                     MOVE EUPAYF    RPAYF
     C                     MOVE EUDYTP    RDYTP
     C                     MOVE EUPNAR    RPNAR
     C                     MOVE EUPRFC    RPRFC
     C                     MOVE EUTRAT    RTRAT
      *
     C                     MOVE EUNXTP    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RNXTP
      *
     C                     MOVE EUDLPY    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RDLPY
      *
     C                     MOVE EUCRSW    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCRSW
      *
     C                     ENDIF
      *
     C                     MOVE EUOPIN    ROPIN
     C                     MOVE EUSWPI    RSWPI
     C                     MOVE EUCLSE    RCLSE
      *
     C                     MOVE EUNSTD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RNSTD
      *
     C                     MOVE EUDACO    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RDACO
      *
     C                     MOVE EUDNIC    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RDNIC
      *
     C                     MOVE EUCNIC    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCNIC
      *
     C                     MOVE EUCRAC    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCRAC
      *
     C                     MOVE EUCLAC    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RCLAC
      *                                                                   CRE008
      * EMU Mapping?                                                      CRE008
      *                                                                   CRE008
     C           EUEMUM    COMP 'Y'                      78*              CRE008
      * Write detail
     C                     WRITEMDHDDTL
      *
     C                     ENDSR
      *
      *
      *****************************************************************
      /TITLE SR/WP2REC
      *****************************************************************
      *                                                               *
      *  WP2REC - Subroutine to Write Mapping List Report.            *
      *                                                               *
      *****************************************************************
      *
     C           WP2REC    BEGSR                           *** WP1REC ***
      *
      ***  Format Report fields.
      *
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     WRITEEUACHDR
     C                     ENDIF
      *
     C           EUMAPN    IFNE RIMAPN
     C                     WRITEHEADP1
     C                     WRITEEUACHDR
     C                     ENDIF
      ***  Write out Detail Record.
      *
     C                     MOVE EUMAPN    RIMAPN  60
     C                     MOVE EUIBRC    RIBRC
     C                     MOVE EUICNM    RICNM
     C                     MOVE EUICCY    RICCY
     C                     MOVE EUIACD    RIACD
     C                     MOVE EUISQN    RISQN
      *                                                                   133671
      ** If retail account nos are preserved, blank out 'in' currency one 133671
     C           EUIACN    IFEQ *ZEROS                                    133671
     C                     MOVE *BLANKS   RIACN                           133671
     C                     ELSE                                           133671
     C                     MOVE EUIACN    RIACN
     C                     ENDIF                                          133671
      *                                                                   133671
     C                     MOVE EUEBRC    REBRC
     C                     MOVE EUECNM    RECNM
     C                     MOVE EUECCY    RECCY                           CRE008
     C                     MOVE EUEACD    REACD
     C                     MOVE EUESQN    RESQN
     C                     MOVE EUEACN    REACN
     C                     MOVE EUOPFA    ROPFA
     C                     MOVE EUACLO    RACLO
      *
     C           *IN71     IFEQ *ON
     C                     MOVE EUSWFA    RSWFA
     C                     ENDIF
      *
     C                     WRITEEUACDTL
      *
     C                     ENDSR
      *
     C/COPY WNCPYSRC,EUH00016
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
     C/COPY WNCPYSRC,EU0170C003
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           @RUN      IFEQ *BLANK
      *
     C                     MOVE 'Y'       @RUN    1
      *
     C                     SETON                     U7U8LR
      *
     C                     DUMP
      *
     C                     EXSR AUDIT
      *
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM EUBRCA    SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      ********************************************************************
     C/COPY ZSRSRC,ZDATE1Z2
      /EJECT
      ********************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
      /EJECT
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
** ZMDY
000031059090120151181212243273304334365
** ZYDY
0366073110961461
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
