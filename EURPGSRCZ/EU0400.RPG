     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas EU EMU limit currency conversion prompt')
      *****************************************************************
      *                                                               *
      *  Midas - EMU Module                                           *
      *                                                               *
      *  EU0400 - Limit Currency Conversion Prompt                    *
      *                                                               *
      *  Function:  This input program will allow the user to define  *
      *             the new limit currency, modules to convert and    *
      *             the rounding factor to be used during conversion. *
      *  (IC)                                                         *
      *                                                               *
      *  Called By: EUC0400 - Limit Currency Conversion Control       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 239503             Date 21Apr06               *
      *                 CSD031             Date 10Apr06               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU010  *CREATE    Date 23Jan98               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  239503 - If EM is not installed, and user enter 'AL',        *
      *           program generated DB error when accessing EM ICD.   *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU010 - Limit Currency Conversion
      *                                                               *
      *****************************************************************
      /EJECT
     FEU0400DFCF  E                    WORKSTN
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   25  - Error Msg Subfile End                                 *
      *                                                               *
      *  Error Indicators                                             *
      *                                                               *
      *   30  - Error in New Limit Currency Code                      *
      *   31  - Error in Module Id                                    *
      *   32  - Error Rounding Factor                                 *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
     I              AGMRDT                          SRUNA
      *                                                                                       239503
     ISDMMOD    E DSSDMMODPD                                                                  239503
      * Module details                                                                        239503
      *                                                                                       239503
      ** Data Area giving Installation Control Details
      *
     ISDGELR    E DSSDGELRPD
      *
      ** Data Area giving GL ICD File Details
      *
     ISDCURR    E DSSDCURRPD
      *
      ** Data Area giving Currency File Details
      *
     ISDEXPM    E DSSDEXPMPD
      *
      * @BPREER - EXPOSURE MANAGEMENT RECORD FORMAT DATA STRUCTURE
      *
     ISDDEAL    E DSSDDEALPD
      * @BNREEI - Dealing Data Record Format Data Structure
     I              QQACCD                          QQACD1                                    CGL029
      *
     IDSFDY     E DSDSFDY
      *
      ** Short Data Structure
      *
     IDSSDY     E DSDSSDY
      *
      ** Long Data Structure
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 SWSID
     I                                      254 263 SUSER
      *
      *****************************************************************
     C           *ENTRY    PLIST
     C           W@LCCY    PARM           SLCCY
     C           W@MODI    PARM           SMODI
     C           W@RFAC    PARM           SRFAC
     C           W@CONV    PARM           W@CONV
      *
     C*
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C                     MOVE *BLANKS   W@LCCY  3
     C                     MOVE *BLANKS   W@MODI  2
     C                     MOVE *BLANKS   W@RFAC  1
     C                     MOVE *BLANKS   W@CONV  1
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
     C*                                                                                       239503
     C** Access MIDAS Modules details via access program                                      239503
      *  (database error handling done in access program)                                     239503
     C                     CALL 'AOMMODR0'                                                    239503
     C                     PARM '*BLANKS' @RTCD   7                                           239503
     C                     PARM '*FIRST ' @OPTN   7                                           239503
     C           SDMMOD    PARM SDMMOD    DSFDY                                               239503
      *                                                                                       239503
     C**  Check for data base error.                                                          239503
     C           @RTCD     IFNE *BLANK                                                        239503
     C                     MOVEL'SDMMODPD'DBFILE           * DBERROR 007 *                    239503
     C                     MOVEL'*FIRST ' DBKEY                                               239503
     C                     Z-ADD007       DBASE                                               239503
     C                     OUT  LDA                                                           239503
     C                     EXSR *PSSR                                                         239503
     C                     END                                                                239503
     C*                                                                                       239503
      *
      ** Access GL ICD details via acess program
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST  'WOPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
      ** Data base error in file (PF/SDGELRPD)
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD001       DBASE            ***************
     C                     MOVEL'SDGELRPD'DBFILE           *DB ERROR 001 *
     C                     MOVEL'*FIRST  'DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      *
      ** Do Until F3 (Exit) is requested
      *
     C           *IN03     DOWEQ*OFF
     C           W@CONV    ANDNE'Y'
      *
      ** Display error message
      *
     C                     WRITE#MSGCTL
      *
      ** Write/Read screen
      *
     C                     EXFMTEU0400F0
      *
      ** Clear program error messages
      *
     C                     EXSR CLRMSG
      *
      ** Refresh (F5) is requested
      *
     C           *IN05     IFEQ *ON
     C                     MOVE *BLANKS   SLCCY
     C                     MOVE *BLANKS   SMODI
     C                     MOVE *BLANKS   SRFAC
     C                     MOVEA'000'     *IN,30
     C                     ENDIF
      *
      ** ENTER, Validated screen details
      *
     C           *IN03     IFEQ *OFF
     C           *IN05     ANDEQ*OFF
      *
      ** Reset error indicators
     C                     MOVEA'000'     *IN,30
      *
     C           SLCCY     IFEQ *BLANKS
      *
      ** New Limit Ccy Code is blank
     C                     MOVEA'1'       *IN,30
     C                     MOVEL'EMU0401 'ZAMSID
     C                     EXSR ZASNMS
      *
      ** New Limit Ccy Code is the Consolidate Euro Position Ccy
     C                     ELSE
     C           SLCCY     IFEQ BKEUCP
     C                     MOVEA'1'       *IN,30
     C                     MOVEL'EMU0402 'ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** Call access program to validated if it is a valid Ccy
     C           SLCCY     IFEQ '?'                        *IF            134204
      * Select currencies                                                 134204
     C                     CALL 'SD0020S'              90  Select Ccy     134204
     C                     PARM *BLANK    W0RTN   7                       134204
     C           SLCCY     PARM SLCCY     WQ0099  3        Currency       134204
      *                                                                   134204
     C           *IN90     IFEQ '1'                                       134204
      * Call to program ended in error                                    134204
     C                     MOVEL'Y2U0032' W0RTN                           134204
     C                     MOVEL*BLANKS   W0CLPG 10                       134204
     C                     MOVEL'SD0020S' W0CLPG                          134204
      * Send message '*Error occured on CALL...'                          134204
     C                     MOVEL'Y2U0032' ZAMSID                          134204
     C                     MOVEL'Y2USRMSG'ZAMSGF                          134204
     C                     MOVELW0CLPG    ZAMSDA           Message data   134204
     C                     EXSR ZASNMS                                    134204
     C                     END                                            134204
      *                                                                   134204
      * Error detected?                                                   134204
     C           W0RTN     IFNE *BLANK                                    134204
     C                     SETON                     30    *              134204
     C                     END                                            134204
     C                     END                             *FI            134204
      *                                                                   134204
     C           SLCCY     IFNE '?'                        *IF            134204
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY    'WOPTN
     C                     PARM           SLCCY
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ** DB Error in PF/SDCURRPD
      *
     C           WRTCD     IFNE *BLANKS
     C           WRTCD     ANDNE'*NRF   '
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD002       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           *DB ERROR 002 *
     C                     MOVELSLCCY     DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF                           *FI            134204
      *
      ** Currency Code not found
     C           WRTCD     IFNE *BLANKS
     C                     MOVEA'1'       *IN,30
     C                     MOVEL'EMU0403 'ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** If EM is not install,                                                                239503
      ** if selected Module to Convert equal to AL                                            239503
      **    then change Module to DL                                                          239503
      ** if selected Module to Convert equal to EM                                            239503
      **    issue error message EMU0411 'Module not installed'                                239503
      *                                                                                       239503
     C           BGEXMG    IFNE 'Y'                                                           239503
     C           SMODI     IFEQ 'AL'                                                          239503
     C                     MOVE 'DL'      SMODI                                               239503
     C                     ENDIF                                                              239503
     C           SMODI     IFEQ 'EM'                                                          239503
     C                     MOVEA'1'       *IN,31                                              239503
     C                     MOVEL'EMU0411 'ZAMSID                                              239503
     C                     EXSR ZASNMS                                                        239503
     C                     ENDIF                                                              239503
     C                     ENDIF                                                              239503
     C*
      *
      * New Limit Ccy is same as the current limit currency on Exposure
      *  Mangement ICD file.
     C           SMODI     IFEQ 'EM'
     C           BGEXMG    ANDEQ'Y'                                                           239503
     C           SMODI     OREQ 'AL'
      *
      ** Access Exposure Management ICD Data
      *
     C                     CALL 'AOEXPMR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST  'WOPTN   7
     C           SDEXPM    PARM SDEXPM    DSFDY
      *
      ** Data base error in file (PF/SDEXPMPD)
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD003       DBASE            ***************
     C                     MOVEL'SDEXPMPD'DBFILE           *DB ERROR 003 *
     C                     MOVEL'*FIRST  'DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           SLCCY     IFEQ BUCYCD
     C                     MOVEA'1'       *IN,30
     C                     MOVEL'EMU0407 'ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** Call access program to retrieve 'In' currency status and
      *   EMU transition start date of current Exposure Management
      *   Limit currency.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY    'WOPTN
     C                     PARM           BUCYCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ** DB Error in PF/SDCURRPD
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD004       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           *DB ERROR 004 *
     C                     MOVELBUCYCD    DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
      *
      * The processing date is prior to the currency transition start
      *  date if the New limit currency is 'EURO' and the current limit
      *  currency is an 'In' currency.
      *
     C           SLCCY     IFEQ BKEURO
     C           A6INCY    ANDEQ'Y'
     C           AGRDNB    ANDLTA6TPSD
     C                     MOVEA'1'       *IN,30
     C                     MOVEL'EMU0409 'ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      * New Limit currency is same as the current limit currency on
      *  Dealing ICD file.
     C           SMODI     IFEQ 'DL'
     C           SMODI     OREQ 'AL'
      *
      ** Access Dealing ICD Data
      *
     C**********           CALL 'AODEALR0'                                                    CGL029
     C                     CALL 'AODEALR1'                                                    CGL029
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST  'WOPTN   7
     C********** SDDEAL    PARM SDDEAL    DSFDY                                               CGL029
     C           SDDEAL    PARM SDDEAL    DSSDY                                               CGL029
      *
      ** Data base error in file (PF/SDDEALPD)
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD005       DBASE            ***************
     C                     MOVEL'SDDEALPD'DBFILE           *DB ERROR 005 *
     C                     MOVEL'*FIRST  'DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           SLCCY     IFEQ BNCYCD
     C                     MOVEA'1'       *IN,30
     C                     MOVEL'EMU0408 'ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** Call access program to retrieve 'In' currency status and
      *   EMU transition start date of current Dealing Limit currency.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY    'WOPTN
     C                     PARM           BNCYCD
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ** DB Error in PF/SDCURRPD
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     Z-ADD006       DBASE            ***************
     C                     MOVEL'SDCURRPD'DBFILE           *DB ERROR 006 *
     C                     MOVELBNCYCD    DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C*
     C                     ENDIF
      *
      * The processing date is prior to the currency transition start
      *  date if the New limit currency is 'EURO' and the current limit
      *  currency is an 'In' currency.
      *
     C           SLCCY     IFEQ BKEURO
     C           A6INCY    ANDEQ'Y'
     C           AGRDNB    ANDLTA6TPSD
     C                     MOVEA'1'       *IN,30
     C                     MOVEL'EMU0410 'ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Module Id is blank
      *
     C           SMODI     IFEQ *BLANKS
     C                     MOVEA'1'       *IN,31
     C                     MOVEL'EMU0404 'ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** Module Id must be 'DL', 'EM' or 'AL'
      *
     C           SMODI     IFNE 'DL'
     C           SMODI     ANDNE'EM'
     C           SMODI     ANDNE'AL'
     C                     MOVEA'1'       *IN,31
     C                     MOVEL'EMU0405 'ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** Rounding Factor when entered must be between 0-9 (Optional)
      *
     C           SRFAC     IFNE *BLANKS
     C           SRFAC     IFLT '0'
     C           SRFAC     ORGT '9'
     C                     MOVEA'1'       *IN,32
     C                     MOVEL'EMU0406 'ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** All entries are valid
      *
     C           *IN30     IFEQ *OFF
     C           *IN31     ANDEQ*OFF
     C           *IN32     ANDEQ*OFF
     C                     MOVE 'Y'       W@CONV
     C                     ENDIF
      *
      ** End of ENTER
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** End of program
     C                     MOVE '1'       *INLR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * ZASNMS - Send Message on subfile message queue                *
      *                                                               *
      * Called by:                                                    *
      *                                                               *
      * Calls:                                                        *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C                     MOVEL'EUMSGF'  ZAMSGF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  CLRMSG     : Clear message subfile and set off indicators    *
      *                                                               *
      *  Called by  : Main Progcessing Routine                        *
      *                                                               *
      *  Calls      : Y2CLMSC                                         *
      *                                                               *
      *****************************************************************
      *
     C           CLRMSG    BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGMQ 10
     C                     PARM           ZAPGRL  5
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      *                                                               *
      * Calls: DBERRCTL                                               *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
      *
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
