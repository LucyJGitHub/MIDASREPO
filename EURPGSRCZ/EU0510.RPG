     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas EMU Dealing Base Ccy Conversion report')         *
      *****************************************************************
      *                                                               *
      *  Midas - E.M.U. Module                                        *
      *                                                               *
      *  EU0510 - EMU Base Currency for Dealing Conversion Program    *
      *                                                               *
      *  Function:  This program is called once only during I/C       *
      *             Initiation when Base Currency for Dealing         *
      *             conversion has been requested in the previous     *
      *             Input Cycle. It performs updates for all DRS      *
      *             files which contain one or more Base Currency     *
      *             for Dealing Equivalent fields. A report is        *
      *             produced showing before/after values of all       *
      *             updated fields                                    *
      *                                                               *
      *  Called By: EUC0510 - Base Ccy for Dealing Conversion control *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL094             Date 11Jun14               *
      *  Prev Amend No. CSW212             Date 03May12               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 240994             Date 25Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 06Jul06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CAS005             Date 16Dec02               *
      *                 CDL010             Date 02Aug02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU009  *CREATE    Date 16fEB98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CSW212 - SWIFT 2012 Changes (Recompile)                      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  240994 - All figures in EU0510P1 Report are zeroes. Do not   *
      *           report records when all affected fields are zero.   *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CDL010 - Prevent last user that actioned the trade from      *
      *           authorising it.Recompile due to changes in FXDEALPP.*
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  CEU009 - EMU Base Currency for Dealing Conversion            *
      *                                                               *
      *****************************************************************
      *
      * Foreign Exchange deals - update
     FFXDEAULLUF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      * FX Equivalents, Positions and Profits - update
     FFXEPOULLUF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      * FX Forward Book Half Month Details - update
     FFXFWMULLUF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      * FX Forward Book Daily Details - update
     FFXFWDULLUF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      * Internal Contracts - update
     FABDEAULLUF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     F            ABDEALP1                          KIGNORE
      *
      * Dealing ICD - update
     FSDDEALL0UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      * EMU Base Ccy for Dealing Conversion details - update
     FEUDBCEL0UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
      *
      * EMU Base Ccy for Dealing Conversion audit report
     FEU0510AUO   E                    PRINTER      KINFSR *PSSR      UC
     F                                              KINFDS SPOOLU
      *
      * EMU Base Ccy for Dealing Conversion detail report
     FEU0510P1O   E                    PRINTER      KINFSR *PSSR
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   01  - Output header text for FXFWDHPP                       *
      *   02  - Output header text for FXFWDDPP                       *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  SR200  - Call standard conversion program EU0200             *
      *  SRFXDL - Process LF/FXDEAULL records                         *
      *  SRFXEP - Process LF/FXEPOULL records                         *
      *  SRFXFH - Process LF/FXFWMULL records                         *
      *  SRFXFD - Process LF/FXFWDULL records                         *
      *  SRABDL - Process LF/ABDEAULL records                         *
      *  UPDEAL - Update Dealing ICD for new Dealing Base Currency    *
      *  UPDBCE - Update EMU Dealing Base Currency conversion details *
      *  WP1END - Write End of Report                                 *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZSEFMT1
     E/COPY ZSRSRC,ZFRPEDZ1
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
      *
     ISPOOL1      DS
      ***  File Information Data Structure for EU0510P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for EU0510AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     I/COPY ZSRSRC,ZSEFMT2
     I/COPY ZSRSRC,ZFRPEDZ2
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
     ISDCURR    E DSSDCURRPD
      ***  External data structures for Bank Details
     IDSFDY     E DSDSFDY
      ***  Standard access object parm for rec. length < 200
     IDSSDY     E DSDSSDY
      ***  Standard access object parm for rec. length > 200
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform file updating procedures
      *
      * FXDEALPP
     C                     EXSR SRFXDL
      * FXEPOSPP
     C                     EXSR SRFXEP
      * FXFWDHPP
     C                     EXSR SRFXFH
      * FXFWDDPP
     C                     EXSR SRFXFD
      * ABDEALPP
     C                     EXSR SRABDL
      * SDDEALPD
     C                     EXSR UPDEAL
      * EUDBCEPD
     C                     EXSR UPDBCE
      *
      ***  Output report footer, commit file updates and
      ***  end program
     C                     EXSR WP1END
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SR200 - Subroutine to perform calls to EMU standard          *
      *          amount conversion program EU0200                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  EU0200 - EMU standard amount conversion program              *
      *                                                               *
      *****************************************************************
     C           SR200     BEGSR
      *
      * Field WFAMT is passed by the calling routine
     C                     CALL 'EU0200'
     C                     PARM *BLANKS   P@RTCD  7        Return Code
     C                     PARM WFAMT     P@FRAM 183       From Amt.
     C                     PARM ODBC      P@FRCY  3        From Ccy
     C                     PARM NDBC      P@TOCY  3        To Ccy
     C                     PARM           P@OUTA 150       Outright Amt
     C                     PARM           P@INTA 183       Interim Amt.
     C                     PARM           P@EXRT 159       Exchnge Rate
     C                     PARM           P@MDIN  1        Mult/Div Ind
      *
      * If error occurred in EU0200 report database error and
      * terminate this program
     C           P@RTCD    IFEQ '*ERROR'
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE            ***********
     C                     MOVEL'EU0200  'DBFILE           *DBASE 005*
     C                     MOVEL'*CALL   'DBKEY            ***********
     C                     MOVEL'EU0510'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/SRFXDL
      *****************************************************************
      *                                                               *
      *  SRFXDL - Process all records on FXDEALPP                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  ZFRPED - Format amounts for output to report                 *
      *  SR200  - Call standard conversion program EU0200             *
      *                                                               *
      *****************************************************************
      *
     C           SRFXDL    BEGSR                                       **
      *
      * Write report sub-header for FXDEALPP records
     C                     WRITEHFXDEL
      *
      * Process all records on FXDEAULL
      *
     C                     READ FXDEAULL                 89
      *
     C           *IN89     DOWNE*ON
      *
      * Convert amounts and format for output
      *
      * Dealt Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFHBCDE    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDFHBCDE    B4BCE  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUDBCE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFHBCDE    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FHBCDE
      *
      * Format after update amount for output
     C                     Z-ADDFHBCDE    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUDBCE
      *
      * Dealt Spot Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFHDSBE    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDFHDSBE    B4SBE  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUDSBE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFHDSBE    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FHDSBE
      *
      * Format after update amount for output
     C                     Z-ADDFHDSBE    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUDSBE
      *
      * Other Dealt Spot Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFHSDBE    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUODBE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFHSDBE    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FHSDBE
      *
      * Format after update amount for output
     C                     Z-ADDFHSDBE    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     Z-ADDFHSDBE    B4DBE  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUODBE
      *
      * Format remaining detail format fields
     C                     MOVE FHDN38    ODLNO
     C                     MOVELFHCCY1    BCCY
     C                     MOVELFHCCY2    SCCY
      *
      * Update file
     C           B4BCE     IFNE *ZERO                                                         240994
     C           B4SBE     ORNE *ZERO                                                         240994
     C           B4DBE     ORNE *ZERO                                                         240994
     C                     UPDATFXDEALP0
      *
      * Write a detail record to report checking first for overflow
     C                     Z-ADD3         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADER
     C                     WRITEHFXDEL
     C                     ENDIF
      *
     C                     WRITEDFXDEL
     C                     ENDIF                                                              240994
      *
      ***  Read next record.
      *
     C                     READ FXDEAULL                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRFXEP
      *****************************************************************
      *                                                               *
      *  SRFXEP - Process all records on FXEPOSPP                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  ZFRPED - Format amounts for output to report                 *
      *  SR200  - Call standard conversion program EU0200             *
      *                                                               *
      *****************************************************************
      *
     C           SRFXEP    BEGSR                                       **
      *
      * Write report main header and report sub-header for
      * FXEPOSPP records
     C                     WRITEHEADER
     C                     WRITEHFXEPO
      *
      * Process all records on FXEPOULL
      *
     C                     READ FXEPOULL                 89
      *
     C           *IN89     DOWNE*ON
      *
      * Convert amounts and format for output
      *
      * Nostro Dealt Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFONDBC    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDFONDBC    B4DBC  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUNBCE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFONDBC    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FONDBC
      *
      * Format after update amount for output
     C                     Z-ADDFONDBC    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUNBCE
      *
      * Total Dealt Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFOTDPE    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDFOTDPE    B4DPE  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUTDBE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFOTDPE    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FOTDPE
      *
      * Format after update amount for output
     C                     Z-ADDFOTDPE    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUTDBE
      *
      * Bank Dealt Spot Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFOBDSE    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDFOBDSE    B4DSE  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUBDBE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFOBDSE    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FOBDSE
      *
      * Format after update amount for output
     C                     Z-ADDFOBDSE    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUBDBE
      *
      * Format remaining detail format fields
     C                     MOVELFOCCY     OCCY
      *
      * Update file
     C           B4DBC     IFNE *ZERO                                                         240994
     C           B4DPE     ORNE *ZERO                                                         240994
     C           B4DSE     ORNE *ZERO                                                         240994
     C                     UPDATFXEPOSP0
      *
      * Write a detail record to report checking first for overflow
     C                     Z-ADD3         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADER
     C                     WRITEHFXEPO
     C                     ENDIF
      *
     C                     WRITEDFXEPO
     C                     ENDIF                                                              240994
      *
      ***  Read next record.
      *
     C                     READ FXEPOULL                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRFXFH
      *****************************************************************
      *                                                               *
      *  SRFXFH - Process all records on FXFWDHPP                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  ZFRPED - Format amounts for output to report                 *
      *  SR200  - Call standard conversion program EU0200             *
      *                                                               *
      *****************************************************************
      *
     C           SRFXFH    BEGSR                                       **
      *
      * Since the two forward book files share the same print
      * format, notify program to print text for FXFWDHPP on
      * header
     C                     MOVE *ON       *IN01
     C                     MOVE *OFF      *IN02
      *
      * Write report main header and report sub-header for
      * FXFWDHPP records
     C                     WRITEHEADER
     C                     WRITEHFXFWD
      *
      * Process all records on FXFWMULL
      *
     C                     READ FXFWMULL                 89
      *
     C           *IN89     DOWNE*ON
      *
      * Convert amounts and format for output
      *
      * Half Month Net Dealt Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFQHMDE    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDFQHMDE    B4MDE  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUNDBE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFQHMDE    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FQHMDE
      *
      * Format after update amount for output
     C                     Z-ADDFQHMDE    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUNDBE
      *
      * Format remaining detail format fields
     C                     MOVELFQCCY     OCCY
     C                     MOVE FQFWBY    OYEAR
     C                     MOVE FQFWBM    OMNTH
     C                     MOVE FQFBHM    ODAY
      *
      * Update file
     C           B4MDE     IFNE *ZERO                                                         240994
     C                     UPDATFXFWDHP0
      *
      * Write a detail record to report checking first for overflow
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADER
     C                     WRITEHFXFWD
     C                     ENDIF
      *
     C                     WRITEDFXFWD
     C                     ENDIF                                                              240994
      *
      ***  Read next record.
      *
     C                     READ FXFWMULL                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRFXFD
      *****************************************************************
      *                                                               *
      *  SRFXFD - Process all records on FXFWDDPP                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  ZFRPED - Format amounts for output to report                 *
      *  SR200  - Call standard conversion program EU0200             *
      *                                                               *
      *****************************************************************
      *
     C           SRFXFD    BEGSR                                       **
      *
      * Since the two forward book files share the same print
      * format, notify program to print text for FXFWDDPP on
      * header
     C                     MOVE *OFF      *IN01
     C                     MOVE *ON       *IN02
      *
      * Write report main header and report sub-header for
      * FXFWDDPP records
     C                     WRITEHEADER
     C                     WRITEHFXFWD
      *
      * Process all records on FXFWDULL
      *
     C                     READ FXFWDULL                 89
      *
     C           *IN89     DOWNE*ON
      *
      * Convert amounts and format for output
      *
      * Half Month Net Dealt Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDFPDYDE    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDFPDYDE    B4YDE  150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUNDBE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDFPDYDE    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    FPDYDE
      *
      * Format after update amount for output
     C                     Z-ADDFPDYDE    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUNDBE
      *
      * Format remaining detail format fields
     C                     MOVELFPCCY     OCCY
     C                     MOVE FPFWBY    OYEAR
     C                     MOVE FPFWBM    OMNTH
     C                     MOVE FPFWBD    ODAY
      *
      * Update file
     C           B4YDE     IFNE *ZERO                                                         240994
     C                     UPDATFXFWDDP0
      *
      * Write a detail record to report checking first for overflow
     C                     Z-ADD2         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADER
     C                     WRITEHFXFWD
     C                     ENDIF
      *
     C                     WRITEDFXFWD
     C                     ENDIF                                                              240994
      *
      ***  Read next record.
      *
     C                     READ FXFWDULL                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRABDL
      *****************************************************************
      *                                                               *
      *  SRABDL - Process all records on ABDEALPP                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  ZFRPED - Format amounts for output to report                 *
      *  SR200  - Call standard conversion program EU0200             *
      *                                                               *
      *****************************************************************
      *
     C           SRABDL    BEGSR                                       **
      *
      * Write report main header and report sub-header for
      * ABDEALPP records
     C                     WRITEHEADER
     C                     WRITEHABDEL
      *
      * Process all records on ABDEAULL
      *
     C                     READ ABDEAULL                 89
      *
     C           *IN89     DOWNE*ON
      *
      * Convert amounts and format for output
      *
      * Deal Near Date Dealt Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDJPNBCD    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDJPNBCD    B4NBCD 150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUDNDE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDJPNBCD    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    JPNBCD
      *
      * Format after update amount for output
     C                     Z-ADDJPNBCD    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUDNDE
      *
      * Deal Far Date Dealt Base Ccy Equivalent
      * Before update amount
     C                     Z-ADDJPFBCD    ZFLD15
     C                     Z-ADDOLDDP     ZDECS
     C                     Z-ADDJPFBCD    B4FBCD 150                                          240994
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    BUDFDE
      *
      * Convert amount to Euro Currency
     C                     Z-ADDJPFBCD    WFAMT  183
     C                     EXSR SR200
      *
      * Update file field with amount returned from EU0200
     C                     Z-ADDP@OUTA    JPFBCD
      *
      * Format after update amount for output
     C                     Z-ADDJPFBCD    ZFLD15
     C                     Z-ADDNEWDP     ZDECS
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    AUDFDE
      *
      * Format remaining detail format fields
     C                     MOVE JPFD38    ODLN1
     C                     MOVE JPSD38    ODLN2
     C                     MOVELJPDFCY    OCCY1
     C                     MOVELJPDSCY    OCCY2
      *
      * Update file
     C           B4NBCD    IFNE *ZERO                                                         240994
     C           B4FBCD    ORNE *ZERO                                                         240994
     C                     UPDATABDEALP0
      *
      * Write a detail record to report checking first for overflow
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADER
     C                     WRITEHABDEL
     C                     ENDIF
      *
     C                     WRITEDABDEL
     C                     ENDIF                                                              240994
      *
      ***  Read next record.
      *
     C                     READ ABDEAULL                 89
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  UPDEAL - Subroutine to update Dealing ICD file               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           UPDEAL    BEGSR                                       **
      *
      * Retrieve Dealing ICD record
     C                     READ SDDEALL0                 89
      *
      * Database error if not found
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE             ***********
     C                     MOVEL'SDDEALL0'DBFILE            * DBERR 06*
     C                     MOVEL'*ONLY   'DBKEY             ***********
     C                     MOVEL'EU0510'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Update value of Base Currency for Dealing
     C                     MOVELNDBC      BNCYDL
     C                     Z-ADDBJRDNB    BNLCD
     C                     MOVE 'A'       BNTYLC
      *
     C                     UPDAT@BNREEH
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  UPDBCE - Subroutine to update Base Currency for Dealing      *
      *           Conversion details file                             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           UPDBCE    BEGSR                                       **
      *
      * Base Currency for Dealing conversion details have already
      * been retrieved in SR/INIT
      *
      * Set conversion date to today's date and Run Conversion
      * flag back to 'N'
     C                     Z-ADDBJRDNB    CDAT
     C                     MOVE 'N'       RCON
      *
     C                     UPDATEUDBCED0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  RCFP1  - RCF Processing for DetailReport                     *
      *  AOBANKR0 Access Program                                      *
      *  AOCURRR0 Access Program                                      *
      *  ZSEFMT - Format 13,8 rate for output                         *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      * Define LDA
     C           *NAMVAR   DEFN           LDA
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     MOVEL'*ONLY   'DBKEY            ***************
     C                     MOVEL'EU0510'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ***  RCF Processing for Detail File
      *
     C                     EXSR RCFP1
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      * Read Base Currency for Dealing Conversion details file
     C                     READ EUDBCEL0                 89
      *
      * If record not found then database error as shouldn't have
      * got this far if there were no details
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE             ***********
     C                     MOVEL'EUDBCEL0'DBFILE            * DBERR 02*
     C                     MOVEL'*ONLY   'DBKEY             ***********
     C                     MOVEL'EU0510'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Retrieve currency details for Old Base Currency for
      * Dealing
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANK    WRTCD
     C                     PARM '*KEY    'WOPTN
     C                     PARM ODBC      PCYCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      * Database error if currency not found
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE             ***********
     C                     MOVEL'SDCURRPD'DBFILE            * DBERR 03*
     C                     MOVELODBC      DBKEY             ***********
     C                     MOVEL'EU0510'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Save currency d.p. for formatting of amounts
     C                     Z-ADDA6NBDP    OLDDP   10
      *
      * Set up Euro Exchange Rate for output to report
     C                     Z-ADDA6EUER    ZRT13
     C                     MOVE *BLANK    ZPBAS
     C                     EXSR ZSEFMT
     C                     MOVELZRT13A    CRATE
      *
      * Retrieve currency details for New Base Currency for
      * Dealing
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANK    WRTCD
     C                     PARM '*KEY    'WOPTN
     C                     PARM NDBC      PCYCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      * Database error if currency not found
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE             ***********
     C                     MOVEL'SDCURRPD'DBFILE            * DBERR 04*
     C                     MOVELNDBC      DBKEY             ***********
     C                     MOVEL'EU0510'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      * Save currency d.p. for formatting of amounts
     C                     Z-ADDA6NBDP    NEWDP   10
      *
      * Write report main header
     C                     WRITEHEADER
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADER
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITEFOOTER
      *
      * Commit file updates
     C                     COMIT
      *
      * End program
     C                     MOVE *ON       *INLR
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: *PSSR                                             *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
     C                     OPEN EU0510AU
      *
      * Report audit file to RCF
     C                     EXSR RCFAU
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ** Output the DB Error Information.
      *
     C                     WRITEDBERROR
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
      * Rollback any file changes
     C                     ROLBK
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZSEFMT
     C/COPY ZSRSRC,ZSEFMT3
      /TITLE SR/ZFRPED
     C/COPY ZSRSRC,ZFRPEDZ3
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
