/*********************************************************************/
/*XBI    OVRDBF FILE(AUSPLATR) TOFILE(QAFDPRT)                       */
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas FC Copy Spool Files per entity.')               */
/*********************************************************************/
/*                                                                   */
/*       Midas - Report Control Facility Module                      */
/*                                                                   */
/*       FCC000010 - Copy duplicate of spool file and distribute     */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CFC002  *CREATE    Date 29Apr02              */
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*********************************************************************/
             PGM        PARM(&FLNAM &JBNAM &JBUSR &JBNUM +
                          &SFNUM &XOUTQ &MFLG &NOCPY &SAVE)
 
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FLNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JBNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JBNUM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JBUSR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SFNUM) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XOUTQ) TYPE(*CHAR) LEN(30)
             DCL        VAR(&OUTQ1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COUNT) TYPE(*DEC) LEN(1) VALUE(0)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(128)
             DCL        VAR(&MFLG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&NOCPY) TYPE(*DEC) LEN(2)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OUTQX) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRTFX) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
             DCLF       FILE(AUSPLATR)
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGVAR     VAR(&OUTQ1) VALUE(%SST(&XOUTQ 1 10))
             CHGVAR     VAR(&OUTQ2) VALUE(%SST(&XOUTQ 11 10))
             CHGVAR     VAR(&OUTQ3) VALUE(%SST(&XOUTQ 21 10))
 
/** If file is an AU, ignore the report */
 
             IF         COND(%SST(&FLNAM 7 2) *EQ 'AU') THEN(GOTO +
                          CMDLBL(END))
 
/** Make sure that QTEMP is at the top of the library list */
 
             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF2110 CPC2197 CPF2104)
             ADDLIBLE   LIB(QTEMP) POSITION(*FIRST)
 
/** Check if copy spool file target database file exist */
 
             CHKOBJ     OBJ(QTEMP/SPLCOPY) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CRTPF      FILE(QTEMP/SPLCOPY) RCDLEN(133) +
                           SIZE(*NOMAX)
             ENDDO
 
/** If no OUTQS to duplicate to, go to end of program */
 
             IF         COND(&OUTQ1 = ' ' *AND &OUTQ2 = ' ' *AND +
                          &OUTQ3 = ' ') THEN(GOTO CMDLBL(END))
 
/** Repeat duplication of spool files in OUTQS */
 
             IF         COND(&OUTQ3 *NE ' ') THEN(DO)
             CHGVAR     VAR(&COUNT) VALUE(3)
             GOTO LOOP1
             ENDDO
 
             IF         COND(&OUTQ2 *NE ' ') THEN(DO)
             CHGVAR     VAR(&COUNT) VALUE(2)
             GOTO LOOP1
             ENDDO
 
             IF         COND(&OUTQ1 *NE ' ') THEN(DO)
             CHGVAR     VAR(&COUNT) VALUE(1)
             ENDDO
 
/**  Loop round to duplicate the number spool files as per */
/**  number of output queues                               */
 
LOOP1:
             IF         COND(&COUNT *LT 1) THEN(GOTO CMDLBL(END))
 
/** Copy spool file to printer file in QTEMP */
 
RETRY:
             CPYSPLF    FILE(&FLNAM) TOFILE(QTEMP/SPLCOPY) +
                          JOB(&JBNUM/&JBUSR/&JBNAM) SPLNBR(&SFNUM) +
                          CTLCHAR(*FCFC)
 
             MONMSG     MSGID(CPF2207 CPF3309 CPF3342 CPF3429 CPF3483 +
                          CPF3493 CPF9837 CPF3207 CPF3330 CPF3343 +
                          CPF3430 CPF3486 CPF3499 CPF9845 CPF3303 +
                          CPF3340 CPF3344 CPF3482 CPF3492 CPF9812 +
                          CPF9846) EXEC(DO)
                RCVMSG     MSGTYPE(*EXCP) MSG(&MSG) MSGID(&MSGID)
 
/** If spool file still open, delay and try again */
 
                IF         COND(&MSGID *EQ 'CPF3482') THEN(DO)
                   DLYJOB     DLY(60)
                   GOTO RETRY
                ENDDO
                SNDPGMMSG  MSG(&MSG)
                GOTO       CMDLBL(END)
             ENDDO
 
/** If no PRTF, then use the system print file */
 
             CHGVAR     VAR(&PRTFX) VALUE(&FLNAM)
             CHKOBJ     OBJ(&FLNAM) OBJTYPE(*FILE)
/**/
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CHGVAR     VAR(&PRTFX) VALUE('QSYSPRT   ')
             ENDDO
/**/
             DSPFD      FILE(&PRTFX) TYPE(*ATR) OUTPUT(*OUTFILE) +
                          FILEATR(*PRTF) OUTFILE(QTEMP/AUSPLATR)
/**/
             OPNDBF     FILE(AUSPLATR) OPTION(*INP)
             RCVF
             CLOF       OPNID(AUSPLATR)
/**/
  LOOP2:
             IF         COND(&COUNT *EQ 1)  THEN(DO)
                        CHGVAR     VAR(&OUTQX) VALUE(&OUTQ1)
             ENDDO
             IF         COND(&COUNT *EQ 2)  THEN(DO)
                        CHGVAR     VAR(&OUTQX) VALUE(&OUTQ2)
             ENDDO
             IF         COND(&COUNT *EQ 3)  THEN(DO)
                        CHGVAR     VAR(&OUTQX) VALUE(&OUTQ3)
             ENDDO
 
             OVRPRTF    FILE(FCSPLWRK) PAGESIZE(&PRFSZL &PRFSZW) +
                          LPI(&PRLPI) CPI(&PRCPI) OVRFLW(&PROVRF) +
                          CTLCHAR(*FCFC) OUTQ(&OUTQX) +
                          COPIES(&NOCPY) SAVE(&SAVE) SPLFNAME(&FLNAM)
 
             CPYF       FROMFILE(QTEMP/SPLCOPY) TOFILE(FCSPLWRK)
 
/** Do more duplication?  */
 
             CHGVAR     VAR(&COUNT) VALUE(&COUNT - 1)
             IF         COND(&COUNT *GT 0) THEN(GOTO CMDLBL(LOOP2))
             GOTO END
 
ABNOR:       CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
             RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGID(&MSGID) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                          MSGDTA(&MSG) TOPGMQ(*EXT) TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
             DMPCLPGM
             SNDPGMMSG  MSG('FCC000010 Spool file duplication +
                          terminated abnormally') TOPGMQ(*EXT) +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
END:         ENDPGM
