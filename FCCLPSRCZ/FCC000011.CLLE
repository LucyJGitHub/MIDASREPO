/*********************************************************************/
/*XBI    OVRDBF FILE(WRKFILE) TOFILE(QAFDPHY)                        */
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas FC Copy Spool files')                           */
/*********************************************************************/
/*                                                                   */
/*       Midas - Report Control Facility Module                      */
/*                                                                   */
/*       FCC000011 - Retrieve Spool File Attributes for Copying.     */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2002           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CFC001  *CREATE    Date 15Apr02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CFC001 - Retain to spool files (Copy to Archive)            */
/*                                                                   */
/********************************************************************/
             PGM        PARM(&FLNAM &JBNAM &JBUSR &JBNUM &SFNUM &CPI +
                          &PAGEWIDE &LPI &PAGELEN)
 
             DCL        VAR(&FLNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JBNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JBUSR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JBNUM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SFNUM) TYPE(*CHAR) LEN(4)
             DCL        VAR(&CPI) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PAGEWIDE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LPI) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PAGELEN) TYPE(*CHAR) LEN(3)
 
             DCL        VAR(&TEST) TYPE(*CHAR) LEN(20)
             DCL        VAR(&WRKFILE) TYPE(*CHAR) LEN(200)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2002')
 
             DCLF       FILE(QTEMP/WRKFILE)
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(END))
 
 /*  Reset output variables */
 
             CHGVAR     VAR(&CPI) VALUE(' ')
             CHGVAR     VAR(&PAGEWIDE) VALUE(' ')
             CHGVAR     VAR(&LPI) VALUE(' ')
             CHGVAR     VAR(&PAGELEN) VALUE(' ')
 
 /* Create workfile if not present */
 
             CHKOBJ     OBJ(QTEMP/WRKFILE) OBJTYPE(*FILE)
 
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CRTPF      FILE(QTEMP/WRKFILE) RCDLEN(200)
             ENDDO
 
             CLRPFM     FILE(QTEMP/WRKFILE)
 CONT1:
 
 /* Get the attributes of spool file */
 
             WRKSPLFA   FILE(&FLNAM) JOB(&JBNUM/&JBUSR/&JBNAM) +
                          SPLNBR(&SFNUM) OUTPUT(*PRINT)
             CPYSPLF    FILE(QPDSPSFA) TOFILE(QTEMP/WRKFILE) +
                          SPLNBR(*LAST)
 
 CONT2:
             RCVF
             MONMSG     MSGID(CPF0859 CPF0864) EXEC(GOTO CMDLBL(CONT3))
             CHGVAR     VAR(&TEST) VALUE(%SST(&WRKFILE 1 20))
 
 /* Get CPI */
 
             IF         COND(&TEST *EQ ' CHARACTERS PER INCH') THEN(DO)
             CHGVAR     VAR(&CPI) VALUE(%SST(&WRKFILE 47 4))
             ENDDO
 
/* Get page width */
 
             IF         COND(&TEST *EQ '   WIDTH  . . . . . ') THEN(DO)
             CHGVAR     VAR(&PAGEWIDE) VALUE(%SST(&WRKFILE 47 3))
             ENDDO
 
 
/* Get LPI */
 
             IF         COND(&TEST *EQ ' LINES PER INCH . . ') THEN(DO)
             CHGVAR     VAR(&LPI) VALUE(%SST(&WRKFILE 47 4))
             ENDDO
 
/* Get page length */
 
             IF         COND(&TEST *EQ '   LENGTH . . . . . ') THEN(DO)
             CHGVAR     VAR(&PAGELEN) VALUE(%SST(&WRKFILE 47 3))
             ENDDO
 
             IF         COND(&PAGEWIDE *EQ ' ' *OR &CPI *EQ ' ' *OR +
                          &LPI *EQ ' ' *OR &PAGELEN *EQ ' ') +
                          THEN(GOTO CMDLBL(CONT2))
CONT3:
             DLTSPLF    FILE(QPDSPSFA) SPLNBR(*LAST)
 
END:
             ENDPGM
