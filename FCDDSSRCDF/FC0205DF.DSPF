      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FC Request a Single Rpt,Output Instructions')
     A**************************************************************
     A*                                                            *
     A*  Midas - Report Control Facility Module
     A*                                                            *
     A*           DSPF/FC0205DF  -   REQUEST A SINGLE REPORT       *
     A*                              OUTPUT INSTRUCTIONS PROMPT    *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO.  LN1133             DATE  13FEB91       *
     A*     PREV AMEND NO.  LN0839             DATE  05OCT90       *
     A*                     LN0705             DATE  05SEP90       *
     A*                     LN0210             DATE  08/06/90      *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*     LN1133 - CORRECT HELP TAG LOCATIONS                    *
     A*     LN0839 - REMOVE ORIGINAL HEADING ON FORMAT FC0205C1    *
     A*              (SHOULD HAVE BEEN DONE AS PART OF LN0705)     *
     A*     LN0705 - STANDARDISE SCREEN APPEARANCE                 *
     A*     LN0210 - ARCHIVING OPTIONS ADDED TO PROCESSING         *
     A*                                                            *
     A****************************************************************
      *
     A                                      INDARA
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'Exit')
     A          R FC0205F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
     A*                                            /*START LN1133*/
     A*                                            /*END LN1133*/
     A            JUSR          10A  O  1  2TEXT('User ID')
     A                                      OVRDTA
     A                                  1 13'FC0205'
     A*                                                                 /LN0705/
     A************                      1 30'Request a Single Report'
     A                                  2 29'Request a Single Report'
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SRUNA          7   O  1 72
     A                                      OVRDTA
     A            SWSID         10   O  2  2
     A                                      OVRDTA
     A                                  2 71TIME
     A                                  4  2'Report:'
     A            SRNAM         10   O  4 10
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SRDSC         50   O  4 21
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A************                      7  2'Instruction Identifier.:'
     A                                  7  2'Instruction Identifier :'
     A*                                                                 /LN0705/
     A            SSOID         10A  B  7 29DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N54                               DSPATR(UL)
     A  54                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                                                 /LN0705/
     A**50N51*****                      8  2'Distribution List......:'
     A  50N51                           8  2'Distribution List  . . :'
     A*                                                                 /LN0705/
     A  50        SDST1          8A  B  8 29DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N51N54                            DSPATR(UL)
     A  54
     AO 51                                  DSPATR(PR)
     A                                      OVRDTA
     A  50N51     SDST2          8A  B  9 29DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N51N54                            DSPATR(UL)
     A  54
     AO 51                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                                                 /LN0705/
     A*N51********                     10  2'User for Output........:'
     A N51                             10  2'User for Output  . . . :'
     A*                                                                 /LN0705/
     A            SUSRO         10A  B 10 29DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N51N54                            DSPATR(UL)
     A  54
     AO 51                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                                                 /LN0705/
     A**52********                     12  2'Archive to Microfiche..:'
     A  52                             12  2'Archive to Microfiche  :'
     A*                                                                 /LN0705/
     A  52        SARMF          1A  B 12 29DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N33N54                               DSPATR(UL)
     A  54                                  DSPATR(PR)
     A                                      OVRDTA
     A  53                             13  2'Archive to Optical Disk:'
     A  53        SAROD          1A  B 13 29DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N34N54                               DSPATR(UL)
     A  54                                  DSPATR(PR)
     A                                      OVRDTA
     A N54                             23  2'F3=Main Menu'
     A*                                                                 /LN0705/
     A**54********                     23  2'F3=Main Menu, F12=Return'
     A  54                             23  2'F3=Main Menu   F12=Previous'
     A*                                                                 /LN0705/
     A          R FC0205S0                  SFL
     A  20                                  SFLNXTCHG
     A            SEXST          1   H
      *                                                              /LN0210/
     A*********** SSEL           1   B  7  3DSPATR(HI)
     A            SSEL           1   B  7  2DSPATR(HI)
      *                                                              /LN0210/
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(UL)
      *                                                              /LN0210/
     A*********** SARID         10   O  7  6
     A*********** SMFOD          7   O  7 19
     A            SRSFX         10   O  7  4
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SPFTX         50   O  7 15
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SARID         10   O  7 66
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SMFOD          2   O  7 77
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
      *                                                              /LN0210/
     A N52 53
     AO 52N53                               DSPATR(ND)
      *                                                              /LN0210/
     A*********** SADSC         50   O  7 28
     A            SADSC         50   O  8 15
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
      *                                                              /LN0210/
     A          R FC0205C0                  SFLCTL(FC0205S0)
     A                                      CA12(12 'Previous')
     A                                      OVERLAY
     A  21                                  SFLDSP
     A  21                                  SFLDSPCTL
     A  22                                  SFLDLT
     A  23                                  SFLEND
      *                                                              /LN0210/
     A************************************* SFLSIZ(0015)
     A************************************* SFLPAG(0014)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
      *                                                              /LN0210/
     A*                                             /*START LN1133*/
     A*                                             /*END LN1133*/
     A                                  3  3'Archiving for'
      *                                                              /LN0210/
     A*********** PGMSFX        10   O  3 17
     A*********** SDESC         50   O  3 28
     A            SRNAM         10   O  3 17
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SRDSC         50   O  3 28
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A********************************* 5  6'Archive Id'
     A********************************* 5 19'Medium'
     A                                  5  4'Print File'
     A                                  5 15'Description'
     A                                  5 66'Archive Id'
     A                                  5 77'Med'
      *                                                              /LN0210/
     A N52 53
     AO 52N53                               DSPATR(ND)
     A                                  5 28'Description'
      *                                                              /LN0210/
     A          R FC0205S2                  SFL
     A  20                                  SFLNXTCHG
     A            SEXSTX         1   H
     A            SSELX          1   B  7  2DSPATR(HI)
     A  35                                  DSPATR(RI)
     A N35N54                               DSPATR(UL)
     A  54                                  DSPATR(PR)
     A            SRSFX         10   O  7  4
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SPFTX         50   O  7 15
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
      *
     A          R FC0205C2                  SFLCTL(FC0205S2)
     A                                      OVERLAY
     A  21                                  SFLDSP
     A  21                                  SFLDSPCTL
     A  22                                  SFLDLT
     A  23                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA12(12 'Previous')
     A*                                             /*START LN1133*/
     A*                                             /*END LN1133*/
     A                                  3  3'Archiving for'
     A            SRNAM         10   O  3 17
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SRDSC         50   O  3 28
     A*                                                                 /LN0705/
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A                                  5  4'Print File'
     A                                  5 15'Description'
      *                                                              /LN0210/
     A          R FC0205F1
      *                                                              /LN0210/
     A            JUSR          10A  O  1  2TEXT('User ID')
     A                                  1 13'FC0205'
     A*                                                                 /LN0705/
     A*                                                                 /LN0839/
     A************                      1 30'Request a Single Report'
     A*                                                                 /LN0839/
     A                                  2 29'Request a Single Report'
     A                                      DSPATR(HI)
     A*                                                                 /LN0705/
     A            SRUNA          7   O  1 72
     A            SWSID         10   O  2  2
     A                                  2 71TIME
     A**********************************1  3'FC0205'
     A**********************************1 22'Request a Single Report'
     A************SRUNA          7A  O  1 49
     A**********************************1 57TIME
     A************SWSID         10A  O  1 67
      *                                                              /LN0210/
     A          R FC0205F2
     A                                      OVERLAY
     A*                                                                 /LN0705/
     A************                     22  2'1=Select'
     A************                     23  2'F3=Main Menu   F12=Previous'
     A                                 23  2'1=Select   F3=Main Menu   F12=Prev-
     A                                      ious'
     A*                                                                 /LN0705/
     A          R FC0205S1                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A          R FC0205C1                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(FC0205S1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  60                                  SFLEND
     A*                                                                 /LN0705/
     A************                          SFLSIZ(5)
     A                                      SFLSIZ(3)
     A*                                                                 /LN0705/
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
