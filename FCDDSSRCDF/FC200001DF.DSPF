      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FC Reports Requested Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  FC200001DF - Reports Requested Browse                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. MD036944           Date 12Feb16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BG2193  *CREATE    Date 16Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD036944 - HATS Todays reporting enquiry has some formatting *
      *             issues.                                           *
      *           - Applied for MD037174                              *
      *  BG2193 - Viewing of reports in MidasPlus web-facing          *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            DDACTN         1A  I  6  2AUTO(RA)
     A N65                                  DSPATR(HI UL)
     A  65                                  DSPATR(RI PC)
     A            D5RQSQ         5A  O  6  4DSPATR(HI)
     A            D5RNAM        10A  O  6 10DSPATR(HI)
     A            DXRDSC        50A  O  6 21DSPATR(HI)
     A            D5RQTM         4  0O  6 72EDTWRD('  :  ')
     A                                      DSPATR(HI)
     A            D5JBNM        10   H
     A            D5JUSR        10   H
     A            D5JBNB         6   H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'FC200001'
     A                                  1 32'REPORTS REQUESTED BROWSE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A                                  2 32'for user'
     A        45                            DSPATR(ND)
     A                                      OVRATR
     A            DDUSER    R        O  2 41REFFLD(USER)
     A        45                            DSPATR(ND)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3  4'Seq.'
     A                                  3 10'Report'
     A                                  3 21'Description'
     A                                  3 72'Time'
     A            DDRQSQ         5A  B  4  4DSPATR(HI UL PC)
     A                                      OVRDTA
     A                                  5  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   NO DATA                                                          *
      * -------------------------------------------------------------------*
      *
     A          R NODATA
     A                                      PUTOVR
     A                                      OVERLAY
     A                                  6  2'                                  -
     A                                                                         -
     A                                               '
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                      OVERLAY
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
      ** Start of change MD036944
     A**********                       24  2'F3=Exit F12=Abandon E=Enquire     -            MD036944
     A**********                            Rollup  Rolldown'                               MD036944
      *                                                                                     MD036944
     A                                 24  2'F3=Exit   F12=Abandon   E=Enquire -
     A                                            Rollup  Rolldown'
     A                                      OVRATR
      ** End of change MD036944
