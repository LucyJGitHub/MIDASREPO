      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas GO Spooled Output')
      *****************************************************************
      *                                                               *
      *  Midas - Global Processing Module                             *
      *                                                               *
      *  FC200003DF - Spooled Output                                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BG2193  *CREATE    Date 16Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BG2193 - Viewing of reports in MidasPlus web-facing          *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      DSPSIZ(*DS4)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
     A                                      CF16
     A                                      CF19
     A                                      CF20
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            DSPDTA       131A  O  5  2DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0017)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'FC200003'
     A                                  1 32'SPOOLED OUTPUT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3  2'Control'
     A            DDCTRL         8A  B  3 10DSPATR(HI UL PC)
     A                                      OVRDTA
     A                                  3 20'Find'
     A            DDFIND        30A  B  3 25DSPATR(HI UL PC)
     A                                      OVRDTA
     A                                  3 85'File....:'
     A            DDPFILE       10A  O  3 95
     A                                      OVRDTA
     A                                  3115'PAGE'
     A            DDPAG          5  0   3120EDTWRD('   0 ')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  3126'/'
     A            DDHPAG         5  0   3128EDTWRD('   0 ')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   NO DATA                                                          *
      * -------------------------------------------------------------------*
      *
     A          R NODATA
     A                                      PUTOVR
     A                                      OVERLAY
     A                                  4  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                 '
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                      OVERLAY
     A                                 23  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                 '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit F12=Abandon F16=Find F19=L-
     A                                      eft F20=Right    Rollup  Rolldown'
