      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas FC Join rep comp to switch feature')
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module                                *
     A*                                                               *
     A*  LF/FCRCOMJ1 - Join Report Component to Switchable feature    *
     A*                 related Report Component                      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 062860             DATE 05APR94               *
      *                 062453             DATE 28OCT93               *
     A*                 S01407             DATE 20JAN93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
     A*  062860 - Core data items cannot be made switchable           *
     A*  062453 - Recompile file as SCSARDPD moved from DM to DTA lib *
     A*  S01407 - Switchability Enhancements                          *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R FCRCOMD0                  JFILE(FCRCOMPD SCSARMPD SCSARDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(D3RNAM AERNAM)
     A                                      JFLD(D3COTT AECOTT)
     A                                      JFLD(D3CSEQ AECSEQ)
     A          J                           JOIN(2 3)
     A                                      JFLD(AESARN SARN)
     A*
     A            D3RNAM
     A            AESARN
     A            AEAVAL
     A            SARD
     A            D3COTT
     A            D3CSEQ
     A            D3MORQ
     A            D3CLPM
     A            D3PRPG
     A            D3ADTX
     A            D3BRTP
     A*
     A          K D3RNAM
     A          K D3COTT
     A          K D3CSEQ
     A*
     A          S AESARN                    COMP(EQ ' ')
     A*
     A          S AESARN                    COMP(NE ' ')
     A            SARD                      COMP(NE ' ')
     A            AEAVAL                    COMP(EQ 'Y')
     A*                                                      /*062860*/
     A          S AESARN                    COMP(NE ' ')
     A            SARD                      COMP(EQ ' ')
     A            AEAVAL                    COMP(EQ 'N')
     A*                                                      /*062860*/
