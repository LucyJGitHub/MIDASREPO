      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas FC Join rep comp,COB &switch feature')
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module                                *
     A*                                                               *
     A*  LF/FCRCOMJ2 - Join Report Component to COB Component and to  *
     A*                 Switchable feature related Report Component   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD055265 *REDUNDANTDate 10Feb20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB003             Date 02Feb97               *
      *                 S01495             DATE 01OCT94               *
     A*                 062860             DATE 05APR94               *
     A*                 062453             DATE 28OCT93               *
     A*                 S01407             DATE 20JAN93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD055265 - Deliverable Data Split for Report Control Facility*
      *  MD046248 - Finastra Rebranding                               *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
     A*  CCB003 - CoB Task Split Enhancement - 2 Fields added:        *
     A*           Recompile only                                      *
     A*  S01495 - COB Enhancements.  Recompiled due to changes to     *
     A*           PF/CBCMPNPD.                                        *
     A*  062860 - Core data items cannot be made switchable           *
     A*  062453 - Recompile file as SCSARDPD moved from DM to DTA lib *
     A*  S01407 - Switchability Enhancements                          *
     A*                                                               *
     A*****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R FCRCOMD1                  JFILE(FCRCOMPD CBCMPNPD SCSARMPD +
                                            SCSARDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(D3COTT DHCOTT)
     A                                      JFLD(D3CSEQ DHCSEQ)
     A          J                           JOIN(1 3)
     A                                      JFLD(D3RNAM AERNAM)
     A                                      JFLD(D3COTT AECOTT)
     A                                      JFLD(D3CSEQ AECSEQ)
     A          J                           JOIN(3 4)
     A                                      JFLD(AESARN SARN)
     A*
     A            D3RNAM
     A            AESARN
     A            AEAVAL
     A            SARD
     A            D3COTT
     A            D3CSEQ
     A            D3MORQ
     A            D3CLPM
     A            D3PRPG
     A            D3ADTX
     A            D3BRTP
     A            DHCOTT
     A            DHCSEQ
     A*
     A          K D3RNAM
     A          K D3COTT
     A          K D3CSEQ
     A*
     A          S DHCOTT                    COMP(NE ' ')
     A            DHCSEQ                    COMP(NE ' ')
     A            AESARN                    COMP(EQ ' ')
     A*
     A          S DHCOTT                    COMP(NE ' ')
     A            DHCSEQ                    COMP(NE ' ')
     A            AESARN                    COMP(NE ' ')
     A            SARD                      COMP(NE ' ')
     A            AEAVAL                    COMP(EQ 'Y')
     A*                                                      /*062860*/
     A          S DHCOTT                    COMP(NE ' ')
     A            DHCSEQ                    COMP(NE ' ')
     A            AESARN                    COMP(NE ' ')
     A            SARD                      COMP(EQ ' ')
     A            AEAVAL                    COMP(EQ 'N')
     A*                                                      /*062860*/
