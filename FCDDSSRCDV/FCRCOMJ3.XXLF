      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas FC Join rep comp to switch feature')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Report Control Facility
     A*                                                               *
     A*  LF/FCRCOMJ3 - Join Report Component to Switchable feature    *
     A*                related Report Component                       *
     A*                Key : COB Component COTT/CSEQ                  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD055265 *REDUNDANTDate 10Feb20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 064115             Date 18Feb94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD055265 - Deliverable Data Split for Report Control Facility*
      *  MD046248 - Finastra Rebranding                               *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
     A*  064115 - New file created for use in CB0055 and FC0030A      *
     A*           to replace usage of FCRCOML3                        *
     A*           (Similar to FCRCOMJ1 except for the key fields)     *
     A*                                                               *
     A*****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R FCRCOMD0                  JFILE(FCRCOMPD SCSARMPD SCSARDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(D3RNAM AERNAM)
     A                                      JFLD(D3COTT AECOTT)
     A                                      JFLD(D3CSEQ AECSEQ)
     A          J                           JOIN(2 3)
     A                                      JFLD(AESARN SARN)
     A*
     A            D3RNAM
     A            AESARN
     A            AEAVAL
     A            SARD
     A            D3COTT
     A            D3CSEQ
     A            D3MORQ
     A            D3CLPM
     A            D3PRPG
     A            D3ADTX
     A            D3BRTP
     A*
     A          K D3COTT
     A          K D3CSEQ
     A*
     A          S AESARN                    COMP(EQ ' ')
     A*
     A          S AESARN                    COMP(NE ' ')
     A            SARD                      COMP(NE ' ')
     A            AEAVAL                    COMP(EQ 'Y')
     A*
     A          S AESARN                    COMP(NE ' ')
     A            SARD                      COMP(EQ ' ')
     A            AEAVAL                    COMP(EQ 'N')
