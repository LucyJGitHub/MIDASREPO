      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas FC Join rep name to switch feature')
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module
     A*                                                               *
     A*  LF/FCREPTJ0 - Join Report Name to Switchable feature         *
     A*                 related Report Name                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             DATE 27MAY94               *
      *                 062860             DATE 05APR94               *
     A*                 062453             DATE 28OCT93               *
     A*                 S01407             DATE 20JAN93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
     A*  S01486 - Private Banking Upgrade to Rel 10.                  *
     A*           Add Account Officer and Department Level (RCF).     *
     A*  062860 - Core data items cannot be made switchable           *
     A*  062453 - Recompile file as SCSARDPD moved from DM to DTA lib *
     A*  S01407 - Switchability Enhancements                          *
     A*                                                               *
     A*****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R FCREPTD0                  JFILE(FCREPTPD SCSARRPD SCSARDPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(DXRNAM AHRNAM)
     A          J                           JOIN(2 3)
     A                                      JFLD(AHSARN SARN)
     A*
     A            DXRNAM
     A            AHSARN
     A            AHAVAL
     A            SARD
     A            DXLCD
     A            DXCHTP
     A            DXCMOD
     A            DXRDSC
     A            DXGLVL
     A            DXCLVL
     A            DXBLVL
     A***********                                    Begin /S01486/
     A            DXALVL
     A            DXDLVL
     A***********                                    End   /S01486/
     A            DXIPCY
     A            DXCLBP
     A            DXPDFL
     A            DXDFTO
     A            DXMIDQ
     A*
     A          K DXRNAM
     A*
     A          S AHSARN                    COMP(EQ ' ')
     A*
     A          S AHSARN                    COMP(NE ' ')
     A            SARD                      COMP(NE ' ')
     A            AHAVAL                    COMP(EQ 'Y')
     A*                                                      /*062860*/
     A          S AHSARN                    COMP(NE ' ')
     A            SARD                      COMP(EQ ' ')
     A            AHAVAL                    COMP(EQ 'N')
     A*                                                      /*062860*/
