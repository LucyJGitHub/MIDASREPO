      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas FC Report Requests By Branch (Level B)')
      *****************************************************************
      *                                                               *
      *  Midas - Report Control Facility Module                       *
      *                                                               *
      *  LF/FCRREQL6 - Midas FC Report Requests By Branch (Level B)   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A*=====================================================================
     A          R @RREQAI                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(FCRREQPD)
     A*---------------------------------------------------------------------
     A            D5RNAM                    TEXT('Report name')
     A            D5RQSQ                    TEXT('Sequence Number')
     A            D5SOID                    TEXT('Instruction Identifier')
     A            D5LVL                     TEXT('Level')
     A            D5ETTY                    TEXT('Entity')
     A            D5IPCY                    TEXT('Input Cycle')
     A            D5PDFL                    TEXT('Periodic')
     A            D5DLS1                    TEXT('Distribution List part 1')
     A            D5DLS2                    TEXT('Distribution List part 2')
     A            D5USRO                    TEXT('User ID For Output')
     A            D5ARMF                    TEXT('Archived On Microfiche')
     A            D5AROD                    TEXT('Archived On Optical Disk')
     A            D5DRQD                    TEXT('Distribution Required')
     A            D5USRR                    TEXT('User ID For Request')
     A            D5DBRN                    TEXT('User''s Branch')
     A            D5RQTM                    TEXT('Request Time')
     A            D5COBR                    TEXT('Close Of Business')
     A            D5JBNM                    TEXT('Job Name')
     A            D5JUSR                    TEXT('User Name')
     A            D5JBNB                    TEXT('Job Number')
     A            D5PSQR                    TEXT('Periodic Sequence no')
     A            D5COTT                    TEXT('Component Name')
     A            D5CSEQ                    TEXT('Component Sequence Number')
     A            D5TNLU                    TEXT('Transaction No. Last Upd')
     A*.....................................................................
     A* Key fields
     A          K D5ETTY
     A          K D5RNAM
     A          K D5RQSQ
     A*.....................................................................
     A* Select: branch level reports for an entity (not ALL)
     A*          Fld/Cond: Level                     Branch
     A          S D5LVL                     COMP(EQ 'B')
     A*          Fld/Cond: Entity                    Not All Branches
     A            D5ETTY                    COMP(NE 'ALL')
     A*=====================================================================
