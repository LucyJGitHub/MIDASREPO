      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas FC Join rep suffx to switch feature')
     A*****************************************************************
     A*                                                               *
     A*  Midas - System Control Module
     A*                                                               *
     A*  LF/FCRSFXJ0 - Join Report Suffix to Switchable feature       *
     A*                 related Report Suffix (Print files)           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGP002             Date 28Aug03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 062860             Date 05Apr94               *
      *                 062453             DATE 28OCT93               *
     A*                 S01407             DATE 20JAN93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGP002 - Global Processing.                                  *
      *           - Recompile over extended SCSARDPD.                 *
     A*  062860 - Core data items cannot be made switchable           *
     A*  062453 - Recompile file as SCSARDPD moved from DM to DTA lib *
     A*  S01407 - Switchability Enhancements                          *
     A*                                                               *
     A*****************************************************************
     A                                      DYNSLT
     A                                      JDFTVAL
     A          R FCRSFXD0                  JFILE(FCRSFXPD SCSAROPD SCSARDPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(DYRSFX AFOBNM)
     A          J                           JOIN(2 3)
     A                                      JFLD(AFSARN SARN)
     A*
     A            DYRNAM
     A            AFSARN
     A            AFAVAL
     A            AFOBAT
     A            SARD
     A            DYRSFX
     A            DYAURP
     A            DYPFTX
     A            DYCOLT
     A*
     A          K DYRNAM
     A          K DYRSFX
     A*
     A          S AFSARN                    COMP(EQ ' ')
     A*
     A          S AFSARN                    COMP(NE ' ')
     A            SARD                      COMP(NE ' ')
     A            AFAVAL                    COMP(EQ 'Y')
     A            AFOBAT                    COMP(EQ 'PRTF')
     A*                                                      /*062860*/
     A          S AFSARN                    COMP(NE ' ')
     A            SARD                      COMP(EQ ' ')
     A            AFAVAL                    COMP(EQ 'N')
     A            AFOBAT                    COMP(EQ 'PRTF')
     A*                                                      /*062860*/
