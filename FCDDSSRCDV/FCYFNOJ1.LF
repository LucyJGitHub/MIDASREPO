      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXI *  TEXT('Midas Spool File Numbers index (yesterday)')
      *****************************************************************
      *                                                               *
      *  Midas - Report Control Facility Module                       *
      *                                                               *
      *  LF/FCYFNOJ1 - Join Report Request to Spool File Numbers      *
      *                Yesterday                                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG5869 *CREATE    Date 21Mar05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG5869 - For archiving from outq, assign entity details as  *
      *            held by RCF; for archiving on request, report name *
      *            should include suffix.                             *
      *                                                               *
      *****************************************************************
     A          R FCYFNOD1                  JFILE(FCYFNOPD FCYREQPD)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A          J                           JFLD(D6RQSQ D5RQSQ)
     A            D6RSFX                    TEXT('Print Suffix')
     A                                      RENAME(ELRSFX)
     A            D6SFNO                    TEXT('Spool File Number')
     A                                      RENAME(ELSFNO)
     A            D5JBNM                    TEXT('Job Name')
     A                                      RENAME(EKJBNM)
     A            D5JUSR                    TEXT('User Name')
     A                                      RENAME(EKJUSR)
     A            D5JBNB                    TEXT('Job Number')
     A                                      RENAME(EKJBNB)
     A            D6RQSQ                    TEXT('Sequence Number')
     A                                      RENAME(ELRQSQ)
     A            D5RQSQ                    TEXT('Sequence Number')
     A                                      RENAME(EKRQSQ)
     A            D5DRQD                    TEXT('Distribution Required')
     A                                      RENAME(EKDRQD)
     A            D5LVL                     TEXT('Level')
     A                                      RENAME(EKLVL )
     A            D6XETY                    TEXT('Entity of Spool File')
     A                                      RENAME(ELXETY)
     A            D6PSTS                    TEXT('Status')
     A                                      RENAME(ELPSTS)
     A            D6AURP                    TEXT('Audit Report Flag')
     A                                      RENAME(ELAURP)
     A            D5RNAM                    TEXT('Report name')
     A                                      RENAME(EKRNAM)
     A            D5SOID                    TEXT('Instruction Identifier')
     A                                      RENAME(EKSOID)
     A            D5ETTY                    TEXT('Entity')
     A                                      RENAME(EKETTY)
     A            D5IPCY                    TEXT('Input Cycle')
     A                                      RENAME(EKIPCY)
     A            D5PDFL                    TEXT('Periodic')
     A                                      RENAME(EKPDFL)
     A            D5DLS1                    TEXT('Distribution List part 1')
     A                                      RENAME(EKDLS1)
     A            D5DLS2                    TEXT('Distribution List part 2')
     A                                      RENAME(EKDLS1)
     A            D5USRO                    TEXT('User ID For Output')
     A                                      RENAME(EKUSRO)
     A            D5ARMF                    TEXT('Archived On Microfiche')
     A                                      RENAME(EKARMF)
     A            D5AROD                    TEXT('Archived On Optical Disk')
     A                                      RENAME(EKAROD)
     A            D5USRR                    TEXT('User ID For Request')
     A                                      RENAME(EKUSRR)
     A            D5DBRN                    TEXT('User''s Branch')
     A                                      RENAME(EKDBRN)
     A            D5RQTM                    TEXT('Request Time')
     A                                      RENAME(EKRQTM)
     A            D5COBR                    TEXT('Close Of Business')
     A                                      RENAME(EKCOBR)
     A            D5PSQR                    TEXT('Periodic Sequence no')
     A                                      RENAME(EKPSQR)
      *
      ** Key fields
      *
     A          K D6RSFX
     A          K D6SFNO
