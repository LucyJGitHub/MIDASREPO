      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas FC Yesterdays rqts  By Brch/User(Lvl B)')
      *****************************************************************
      *                                                               *
      *  Midas - Report Control Facility Module                       *
      *                                                               *
      *  LF/FCYREQL7 - Midas FC Yesterdays rqts  By Brch/User(lvl B)  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. nnnnnn  *CREATE    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
     A*=====================================================================
     A          R @YREQAM                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(FCYREQPD)
     A*---------------------------------------------------------------------
     A            EKRNAM                    TEXT('Report name')
     A            EKRQSQ                    TEXT('Sequence Number')
     A            EKSOID                    TEXT('Instruction Identifier')
     A            EKLVL                     TEXT('Level')
     A            EKETTY                    TEXT('Entity')
     A            EKIPCY                    TEXT('Input Cycle')
     A            EKPDFL                    TEXT('Periodic')
     A            EKDLS1                    TEXT('Distribution List part 1')
     A            EKDLS2                    TEXT('Distribution List part 2')
     A            EKUSRO                    TEXT('User ID For Output')
     A            EKARMF                    TEXT('Archived On Microfiche')
     A            EKAROD                    TEXT('Archived On Optical Disk')
     A            EKDRQD                    TEXT('Distribution Required')
     A            EKUSRR                    TEXT('User ID For Request')
     A            EKDBRN                    TEXT('User''s Branch')
     A            EKRQTM                    TEXT('Request Time')
     A            EKCOBR                    TEXT('Close Of Business')
     A            EKJBNM                    TEXT('Job Name')
     A            EKJUSR                    TEXT('User Name')
     A            EKJBNB                    TEXT('Job Number')
     A            EKPSQR                    TEXT('Periodic Sequence no')
     A            EKCOTT                    TEXT('Component Name')
     A            EKCSEQ                    TEXT('Component Sequence Number')
     A            EKTNLU                    TEXT('Transaction No. Last Upd')
     A*.....................................................................
     A* Key fields
     A          K EKETTY
     A          K EKUSRR
     A          K EKRNAM
     A          K EKRQSQ
     A*.....................................................................
     A* Select: Branch level but not ALL Branches
     A*          Fld/Cond: Level                     Branch
     A          S EKLVL                     COMP(EQ 'B')
     A*          Fld/Cond: Entity                    Not All Branches
     A            EKETTY                    COMP(NE 'ALL')
     A*=====================================================================
