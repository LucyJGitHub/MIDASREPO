      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FC Report requests report')
      *****************************************************************
      *                                                               *
      *  Midas - Report Control Facility                              *
      *                                                               *
      *  PRTF/FC0060PM - Report Requests Report                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. NNNNNN  *CREATE    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  nnnnnn - (change description)                                *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R $PAGHDR                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'REFERENCE'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##TITL        53        47TEXT('User Report Title')
     A                                   110'DATE'
     A            ##RUNA         7       115TEXT('midas rundate')
     A                                   123'PAGE'
     A                                   128PAGNBR
     A                                      EDTCDE(3)
     A                                   110'TIME'
     A                                      SPACEB(1)
     A                                   115TIME
     A                                      EDTWRD('  :  :  ')
     A            ##E8TX        53        47TEXT('Audit/Report Title')
     A                                      SPACEB(2)
     A            ##BJNA        53        47TEXT('Report Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R $1KEYHDR                  TEXT('Report name              ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     2'Report name'
     A            $1RNAM        10        14TEXT('Report name')
     A                                    26'Report description :'
     A            $1AETX        50        47TEXT('Screen report description')
      *=========================================================================
     A          R $COLHDG                   TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2' Seq.'
     A                                     9'Component'
     A                                    20'Component'
     A                                    31'Level'
     A                                    39'Entity'
     A                                    46'Distribute'
     A                                    57'Instruction'
     A                                    69' User ID'
     A                                    80'Dist''n'
     A                                    89'Archived On'
     A                                   102'  User ID'
     A                                   114'User''s'
     A                                   121'Schedule'
     A N79                               130'Seq'
     A                                     2'Number'
     A                                      SPACEB(1)
     A                                     9'Name'
     A                                    20'Sequence'
     A                                    46'by Entity'
     A                                    57'Identifier'
     A                                    69'For Output'
     A                                    80'List'
     A                                   102'For Request'
     A                                   114'Branch'
     A                                    20' Number'
     A                                      SPACEB(1)
      *=========================================================================
     A          R $DTLRCD                   TEXT('Detail line.             ')
     A                                      SPACEB(1)
     A                                      SPACEA(1)
      *.........................................................................
     A            $RRQSQ         5         2TEXT('Sequence Number')
     A            $RCOTT        10         9TEXT('Component Name')
     A            $RCSEQ         5        21TEXT('Component Sequence Number')
     A            $RLVLN         7        31TEXT('Level Name')
     A            $RETTY         3        40TEXT('Entity')
     A            $RDRQD         1        49TEXT('Distribution Required')
     A            $RSOID        10        57TEXT('Instruction Identifier')
     A            $RUSRO        10        69TEXT('User ID For Output')
     A            $RDLS1         8        80TEXT('Distribution List part 1')
     A            $RBPTX        12        89TEXT('Archived On')
     A            $RUSRR        10       102TEXT('User ID For Request')
     A            $RDBRN         3       115TEXT('User''s Branch')
     A            $RBOTX         8       121TEXT('Schedule')
     A N79        $RPSQR         2       131TEXT('Periodic Sequence no')
     A            $RDLS2         8        80TEXT('Distribution List part 2')
     A                                      SPACEB(1)
     A                                   116'Request Time'
     A            $RRQTM         4  0    129TEXT('Request Time')
     A                                      EDTCDE(3)
      * 'Hidden' internal version of Report name
     A  99N99     $RRNAM        10         1SPACEB(1)
      * 'Hidden' internal version of Level
     A  99N99     $RLVL          1         1SPACEB(1)
      * 'Hidden' internal version of Input Cycle
     A  99N99     $RIPCY         1         1SPACEB(1)
      * 'Hidden' internal version of Periodic
     A  99N99     $RPDFL         1         1SPACEB(1)
      * 'Hidden' internal version of Archived On Microfiche
     A  99N99     $RARMF         1         1SPACEB(1)
      * 'Hidden' internal version of Archived On Optical Disk
     A  99N99     $RAROD         1         1SPACEB(1)
      * 'Hidden' internal version of Close Of Business
     A  99N99     $RCOBR         1         1SPACEB(1)
      * 'Hidden' internal version of Job Name
     A  99N99     $RJBNM        10         1SPACEB(1)
      * 'Hidden' internal version of User Name
     A  99N99     $RJUSR        10         1SPACEB(1)
      * 'Hidden' internal version of Job Number
     A  99N99     $RJBNB         6         1SPACEB(1)
      *=========================================================================
     A          R $FINTTL                   TEXT('Final Totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A N78                                58'Number Of Records'
     A N78        $TAPNB         5  0     76TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
     A N77                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(1)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     $TCON          1         1SPACEB(1)
      *=========================================================================
     A          R $ENDRPT                   TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                    59'*** END OF REPORT ***'
      *=========================================================================
