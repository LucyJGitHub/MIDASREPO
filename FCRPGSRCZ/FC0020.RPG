     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FC Update last active date')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Report Control Facility Module
     F*                                                                  *
     F*  FC0020 - UPDATE LAST ACTIVE DATE                                *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. LN0427             Date 25Jun90               *
      *                                    Date 00/00/00              *
     F********************************************************************
      *  MD046248 - Finastra Rebranding                               *
     F*      LN0427 - READ IN OF RUNDAT NEEDED
      ********************************************************************
      *  C R E A T I O N     P A R A M E T E R S
      *
      *
      ********************************************************************
      *****************************************************************
      * OPERATION
      * ~~~~~~~~~
      *
      * THIS PROGRAM UPDATES THE LAST ACTIVE DATE ON SDPREQPD TO
      * TODAYS DATE FOR THOSE PERIODIC REPORTS RUN TODAY
      *
      *      INDICATORS:
      *                    51   END OF FILE 'FCRREQPD'
      *                    41   END OF FILE 'FCPREQPD'
      *
      *****************************************************************
     FFCPREQL0UF  E           K        DISK
     FFCRREQL3IF  E           K        DISK
      *
     E                    CPY@    1   1 80
     ILDA        UDS                            256
      **
      * DATA STRUCTURE FOR DATABASE ERROR
      *
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **
      * RUNDAT TO RETRIEVE TODAYS DATE
      *
     IRUNDAT      DS                             13
     I                                    P   8  100RDAT
      **
      * DEFINE DATA STRUCTURE FOR DBERR
      *
     ICHKEDS      DS                             12
     I                                        1  10 D5RNAM
     I                                       11  12 D5PSQR
      *
     C                     MOVEACPY@      BIS@   80
      **   DEFINE LOCAL DATA AREA & DTAARA/CBSTAT
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT                                    LN0427
      *
      **   DEFINE KLISTS
      *
     C           CHKEY     KLIST
     C                     KFLD           D5RNAM
     C                     KFLD           D5PSQR
      **
      * READ FCRREQPD UNTIL END OF FILE
      *
     C                     READ FCRREQL3                 51
      *
     C           *IN51     DOWEQ'0'
     C*
     C           CHKEY     SETLLFCPREQL0
      **
      * CHAIN TO FCPREQL0 WITH KEY IN FCRREQL3 (REPORT NAME & SEQ. NO.)
      *
     C           CHKEY     CHAINFCPREQL0             41
      **
      * DBERR. IF NO REC FOR KEY FOUND IN FCPREQL0
      *
     C           *IN41     IFEQ '1'
     C                     MOVEL'FCPREQL0'DBFILE
     C                     MOVELCHKEDS    DBKEY
     C                     Z-ADD1         DBASE
     C                     MOVEL'FC0020'  DBPGM
     C                     EXSR *PSSR
     C                     SETON                         51
     C                     END
      **
      * UPDATE LAST ACTIVE DATE ON FCPREQPD IF NO DBERR
      *
     C           *IN41     IFEQ '0'
     C                     Z-ADDRDAT      D9LPDT
     C                     UPDAT@D9REJI
      **
      * READ NEXT RECORD ON FCRREQL3
      *
     C                     READ FCRREQL3                 51
     C                     END
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
      /EJECT
     C********************************************************************
     C*
     C*                  SUMMARY  OF  SUBROUTINES
     C*                 **************************
     C*
     C*     *PSSR       Database Error Handling : Terminates program
      ****************************************************************
      /EJECT
     C********************************************************************
     C**                                                                 *
     C**   *PSSR SR. -  Database Error Handling : Terminates program;    *
     C**   ~~~~~~~~~                                                     *
     C**      Called From                  Main code                     *
     C**                                                                 *
     C**
     C           *PSSR     BEGSR
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     END
     C                     ENDSR
      ****************************************************************
** CPY@
(c) Finastra International Limited 2001
