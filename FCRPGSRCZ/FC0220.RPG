     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FC Mand output instructions maint')
/*O*RF*:*OVRDBF*(QAOKDL01)*(QAOKL01A)*******************************:**                       CAAA02
     F********************************************************************
     F*                                                                  *
     F*  Midas - Report Control Facility Module
     F*                                                                  *
     F*      FC0220 - MANDATORY REPORT OUTPUT INSTRUCTIONS               *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 TDA035             Date 02Apr04               *
      *                 CAAA02             Date 01Dec03               *
      *                 CFC002             Date 29Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01407             Date 26Jan93               *
      *                 043702             Date 21Aug92               *
     F*                     S01382           DATE 19AUG92                *
     F*                     S01117           DATE 25MAR92                *
     F*                     S01269           DATE 25JUN91                *
     F*                     LN1151           DATE 23MAY91                *
     F*                     LN1186           DATE 03MAR91                *
     F*                     LN0210           DATE 18MAY90                *
     F*                                                                  *
     F********************************************************************
      *                                                                  *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  TDA035 - RTS Signon changes for MidasPlus. (Recompile)       *
      *  CAAA02 - Webfacing. Rename QAOK* references to the V5R2 names.  *
      *  CFC002 - Multiple Output Queues In Std Output Instructions   *
      *           Recompiled due to change in FCSOUTPD                *
     F*      S01407 - SWITCHABILITY ENHANCEMENTS                         *
     F*      043702 - VALIDATE DISTRIBUTION LIST                         *
     F*      S01382 - OPTICAL DISK DEVELOPMENT.                          *
     F*               - IF THE ARCHIVE ID MEDIUM IS BOTH THEN 'B'        *
     F*                 SHOULD BE OUTPUT ON THE SCREEN AS THE MEDIUM.    *
     F*      S01117 - RECOMPILE FOR FT CHANGE TO MUSERDD                 *
     F*      S01269 - RECOMPILE FOR TRADE AUTHORISATION CHANGE TO
     F*               MUSERDD
     F*      LN1151 - SEND AN ERROR MESSAGE IF USER ATTEMPTS TO          *
     F*               ASSOCIATE A REPORT WITH A USER TO WHICH HE HAS NO  *
     F*               AUTHORITY.                                         *
     F*      LN1186 - SEND NEW MESSAGE ID FROM FCUSRMSG TO FCC0240       *
     F*      LN0210 - ARCHIVING OPTIONS TO BE ADDED TO THE REPORT        *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F****************************************************************
     FFC0220DFCF  E                    WORKSTN
     F                                        RRN   KSFILE FC0220S0       LN0210
     F                                        RRN2  KSFILE FC0220S2       LN0210
     FFCSOUTL1IF  E           K        DISK
     F***********FCREPTL1IF  E           K        DISK                    S01407
     F***********FCRCOML1IF  E           K        DISK                    S01407
     FFCREPTJ0IF  E           K        DISK                               S01407
     FFCRCOMJ1IF  E           K        DISK                               S01407
     FFCRSFXL1IF  E           K        DISK                               LN0210
     FMUSER   IF  E           K        DISK
     FFCMOUTL1IF  E           K        DISK
     FFCMOUTL0UF  E           K        DISK                      A
     F                                              KCOMIT
     F                                              KINFDS INFDS
     FMAARCHL1IF  E           K        DISK                               LN0210
     FMAARCHL2IF  E           K        DISK                               LN0210
     FMAARCPL2UF  E           K        DISK                               LN0210
     F                                              KCOMIT                LN0210
     FMAARCPL1UF  E           K        DISK                               LN0210
     F                                              KCOMIT                LN0210
     FMAARCPL0O   E           K        DISK                               LN0210
     F                                              KCOMIT                LN0210
     F                                              KINFDS INFDSA         LN0210
     FSDFCTLL0UF  E           K        DISK
     F                                              KCOMIT
     F*QAOKDL01IF*E*****      K        DISK                                            043702 CAAA02
     FQAOKL01AIF  E           K        DISK                                                   CAAA02
     F            WOSFMT01                          KIGNORE               043702
     F********************************************************************
     F*             FUNCTION OF INDICATORS
     F*
     F*  03         FUNCTION KEY 3 TAKEN FROM SCREEN
     F*  10         FUNCTION KEY 10 TAKEN FROM SCREEN
     F*  12         FUNCTION KEY 12 TAKEN FROM SCREEN
     F*  20         SFLNXTCHG INDICATOR
     F*  21         SFLDSP/SFLDSPCTL INDICATOR
     F*  22         SFLDLT INDICATOR
     F*  23         SFLEND INDICATOR
     F*  30         STANDARD OUTPUT INSTRUCTIONS ERROR INDICATOR
     F*  31         DISTRIBUTION LIST ERROR INDICATOR
     F*  32         USER ID FOR OUTPUT ERROR INDICATOR
     F*  33         ARCHIVE ON MICROFICHE ERROR INDICATOR
     F*  34         ARCHIVE ON OPTICAL DISK ERROR INDICATOR
     F*  35         SUBFILE SELECT ERROR INDICATOR
     F*  36         REPORT NAME ERROR
     F*  37         ASSOCIATED USER ERROR
     F*  38         DISTRIBUTION REQUIRED ERROR
     F*  50         OFFICE PRESENT
     F*  51         PROTECT USER ID, DIST LIST, AS DISTRIBUTION IS REQUIRED
     F*  52         MICROFICHE MODULE PRESENT
     F*  53         OPTICAL DISK MODULE PRESENT
     F*  54         PROTECT ALL FIELDS FOR ENQUIRY
     F*  56         ENABLE COMMAND 10 FOR DELETE
     F*  55         CHAIN FAIL TO FCMOUT FILE FOR INSERT CHECK
     F*  57         MULTIBRANCH SYSTEM
     F*  58         'KEY' SCREEN VALID: DISPLAY DETAILS
     F*  59         MAIN SCREEN VALID: DISPLAY OUTPUT INSTRUCTIONS
     F*  60         MESSAGE SUBFILE END
     F*  65         MANDATORY REPORT
     F*  70         ERROR ON SCREEN
     F*  71         ERROR ON ARCHIVING SCREEN                             LN0210
     F*  72         NO ARCHIVE ID'S SELECTED                              LN0210
     F*  75         ARCHIVE UPDATES PERFORMED                             LN0210
     F*  79         REPORT DESCRIPTIONS CHAIN FAIL
     F*  80         STANDARD OUTPUT INSTRUCTIONS NOT FOUND
     F*  84         CHAIN FAIL INDICATOR FOR MUSER
     F*  85         RECORDS FOR CURRENT SUFFIX EXIST                      LN0210
     F*  86         CHAIN FAIL TO FCMOUT ON AMEND
     F*
     F********************************************************************
     E                    CPY@    1   1 80
     E******************* SFX         8  2                                LN0210
     E******************* XNM        10  1                                LN0210
     IWOSFMT02                                                            043702
     I              WOS2LDEN                        S2LDEN                043702
     I              WOS2LDGN                        S2LDGN                043702
     I              WOS2DESC                        S2DESC                043702
     I              WOS2USRP                        S2USRP                043702
     I              WOS2GCID                        S2GCID                043702
     I              WOS2IND                         S2IND                 043702
     I*
     IINFDS       DS
     I*
     I** DATA STRUCTURE FOR RPG 01021 ERROR HANDLING
     I*
     I***********************************  *STATUS  STATUS                LN0210
     I                                     *STATUS  MOSTAT                LN0210
     I*                                                                   LN0210
     IINFDSA      DS                                                      LN0210
     I*                                                                   LN0210
     I** DATA STRUCTURE FOR RPG 01021 ERROR HANDLING                      LN0210
     I*                                                                   LN0210
     I                                     *STATUS  APSTAT                LN0210
     I*
     ILDA         DS                            256
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR STORE
     I*
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     IRUNDAT      DS
     I*
      ** DATA AREA OF BANK DETAILS
     I*
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I*
     ISWARN       DS
     I*
      ** DATA STRUCTURE FOR WARNING ERRORS
     I*
     I                                        1  10 SSOID
     I                                       11  18 SDST1
     I                                       19  26 SDST2
     I                                       27  36 SUSRO
     I                                       37  37 SARMF
     I                                       38  38 SAROD
     I*
     ILWARN       DS
     I*
      ** DATA STRUCTURE FOR WARNING ERRORS: LAST SCREEN OF DATA
     I*
     I                                        1  10 LSOID
     I                                       11  18 LDST1
     I                                       19  26 LDST2
     I                                       27  36 LUSRO
     I                                       37  37 LARMF
     I                                       38  38 LAROD
     ICMTTXT      DS                             25
      *
      **  COMIT TEXT
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTCDX
     IREPSEQ      DS
     I*
     I** REPORT SEQUENCE NUMBER DATA AREA
     I*
     I                                        1   50UUSEQ
      /COPY ZSRSRC,ZTNLU1Z1
     IDSSDY     E DSDSSDY
     IDSFDY     E DSDSFDY
     I*
     ID@BANK    E DSSDBANKPD
      *
      ** DATA STRUCTURE FOR BANK FILE ACCESS OBJECT
     I*
     IPSDS       SDS
     I*
     I** PROGRAM STATUS DATA STRUCTURE FOR USER ETC.
     I*
     I                                      244 253 JBNM
     I                                      254 263 JUSR
     I                                      264 269 JBNB
     IP@RCF     E DSSDRCFPD
      * LF/SDRCFL1 ACCESS PROGRAM RECORD FORMAT DATA STRUCTURE
      *
     IP@MMOD    E DSSDMMODPD
      * LF/SDMMODL1 ACCESS PROGRAM RECORD FORMAT DATA STRUCTURE
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Input Parameters:
      **          Report Name
      **          Action Code
      **          Return Code
      *
     C           *ENTRY    PLIST
     C                     PARM           RNAM
     C                     PARM           ACTN    1
     C                     PARM           RTCD    7
      *
     C                     EXSR INIT1
      *
     C                     EXSR INIT2
     C*
     C** ENQUIRE/DELETE PROCESSING
     C*
     C           ACTN      IFEQ 'D'                                       LN0210
     C********** ACTN      IFEQ 'E'                                       LN0210
     C********** ACTN      OREQ 'D'                                       LN0210
     C**********           EXSR ENQDEL                                    LN0210
     C                     EXSR DELETE                                    LN0210
     C                     ELSE
     C*
     C** WRITE KEY SCREEN IF INSERT, OTHERWISE FIRST DETAIL
     C*
     C           ACTN      IFEQ 'I'
     C                     MOVE 'Y'       KEYF    1
     C                     END                                            LN0210
      *                                                                   LN0210
     C******************** ELSE                                           LN0210
     C           ACTN      IFEQ 'A'                                       LN0210
     C                     MOVE 'Y'       MAINF   1
     C                     END
      *                                                                   LN0210
     C           ACTN      IFEQ 'E'                                       LN0210
     C                     MOVE 'Y'       OUTPF   1                       LN0210
     C                     END                                            LN0210
      *
      **          DOWHILE F3 is not taken or screen is invalid
      *
     C           KEYF      DOWEQ'Y'
     C           MAINF     OREQ 'Y'
     C           OUTPF     OREQ 'Y'
     C           RSFXF     OREQ 'Y'                                       LN0210
     C           ARCHF     OREQ 'Y'                                       LN0210
     C           UPDF      OREQ 'Y'                                       LN0210
      *
      ** VALIDATE INITIAL SCREEN
      *
     C           KEYF      IFEQ 'Y'
     C                     EXSR VALRPT
     C                     END
      *
      ** PROCESS MAIN SCREEN IF FIRST IS VALID
      *
     C           MAINF     IFEQ 'Y'
     C                     EXSR VALINT
     C                     END
      *
      ** PROCESS OUTPUT SCREEN IF FIRST IS VALID
      *
     C           OUTPF     IFEQ 'Y'
     C                     EXSR VALID
     C                     END
      *                                                                   LN0210
      ** PROCESS SUFFIXES IF ARCHIVING REQUESTED                          LN0210
      *                                                                   LN0210
     C           RSFXF     IFEQ 'Y'                                       LN0210
     C                     EXSR RSFXS                                     LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** PROCESS ARCHIVE DETAILS IF REQUESTED                             LN0210
      *                                                                   LN0210
     C           ARCHF     IFEQ 'Y'                                       LN0210
     C                     EXSR SETSUB                                    LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           UPDF      IFEQ 'Y'                                       LN0210
     C                     EXSR INSAMD                                    LN0210
     C                     MOVE 'N'       UPDF                            LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *
     C                     END
      *******                                                             LN0210
      *******Issue*Commit*****************                                LN0210
      *******                                                             LN0210
     C*******    ACTN      IFNE 'E'                                       LN0210
     C*******              TIME           MTIME                           LN0210
     C*******    CMTTXT    COMIT                                          LN0210
     C*******              END                                            LN0210
      *
     C                     SETON                     LR
      *
      /EJECT                                                              LN0210
     C********************************************************************
      *                INDEX OF SUBROUTINES
     C*  S/R INSAMD - TO PERFORM FILE UPDATES FOR INSERTION/AMENDMENT
     C*  S/R VALRPT - TO VALIDATE KEY SCREEN                              LN0210
     C*  S/R VALINT - TO VALIDATE INITIAL SCREEN                          LN0210
     C*  S/R VALID - TO VALIDATE MAIN SCREEN
     C*  S/R RSFXS - TO DISPLAY SUFFIX SELECTION SCREEN FOR ARCHIVING     LN0210
     C*  S/R VALRSX - TO VALIDATE SUFFIX SELECTION SCREEN                 LN0210
     C*  S/R SETSUB - TO LOAD SUBFILE OF ARCHIVE IDENTIFIERS              LN0210
     C*  S/R ARCHV - TO VALIDATE ARCHIVING SCREEN                         LN0210
     C*  S/R ENQDEL - TO PERFORM ENQUIRE/DELETION PROCESSING
     C*  S/R INIT2 - TO INITIALISE MAIN SCREEN
     C*  S/R INIT1 - TO INITIALISE PROGRAM VARIABLES
     C*  S/R UPDOWS - TO DISPLAY SCREEN ERROR IF UPDATE BY ANOTHER W/S
     C*  S/R DBERR - TO PROCESS DATABASE ERRORS AND TERMINATE PROGRAM
     C*  S/R END - TO TERMINATE PROGRAM
     C*  S/R *PSSR - ERROR ROUTINE
     C*  S/R ZTNLU1 - ACCESS NEXT AVAILABLE TRANSACTION NUMBER FROM MNATN
     C***S/R*ZDATE1*-*CONVERT*DATE*TO*DAYNO*******************************LN0210
     C***S/R*ZDATE2*-*CONVERT*DAYNO*TO*DATE*******************************LN0210
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R INSAMD - TO PERFORM FILE UPDATES FOR INSERTION/AMENDMENT
     C*  CALLED FROM MAIN
     C*  CALLS ZTNLU1, UPDOWS, DBERR
     C*
     C********************************************************************
      *
     C           INSAMD    BEGSR
      *
      **     Write report requests record
      **     IF parameters is not blank
      **          Write Parameters daily record
      **     ENDIF
      *
     C                     EXSR ZTNLU1
      *
     C           SRNAM     CHAINFCMOUTL0             86
      *
      **  IF TRANSACTION NUMBER HAS CHANGED, ALERT USER TO UPDATE
      *
     C           ACTN      IFEQ 'A'
     C           D2TNLU    ANDNEPTNLU
     C***********          MOVE 'RCF0060' MSGID                           LN1186
     C                     MOVE 'RCF1060' MSGID                           LN1186
     C                     EXSR UPDOWS
     C                     END
      *
      **  IF RECORD HAS BEEN INSERTED, ALERT USER TO UPDATE
      *
     C           ACTN      IFEQ 'I'
     C           *IN86     ANDEQ'0'
     C***********          MOVE 'RCF0064' MSGID                           LN1186
     C                     MOVE 'RCF1064' MSGID                           LN1186
     C                     EXSR UPDOWS
     C                     END
      *                                                                   LN0210
      **                    Delete all archiving if not required          LN0210
      *                                                                   LN0210
     C           SAROD     IFEQ 'N'                                       LN0210
     C           SARMF     ANDEQ'N'                                       LN0210
      *                                                                   LN0210
     C           ARCPKP    SETLLMAARCPL2                                  LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
      *                                                                   LN0210
     C           *IN88     DOWEQ'0'                                       LN0210
     C                     DELET@ARCPAP                                   LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    Delete Optical Disk if not required           LN0210
      *                                                                   LN0210
     C           SAROD     IFEQ 'N'                                       LN0210
     C           D2AROD    ANDEQ'Y'                                       LN0210
      *                                                                   LN0210
     C           ARCPKP    SETLLMAARCPL2                                  LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
      *                                                                   LN0210
     C           *IN88     DOWEQ'0'                                       LN0210
     C           ECARID    CHAINMAARCHL1             89                   LN0210
      *                                                                   LN0210
     C           *IN89     IFEQ '0'                                       LN0210
     C           EAMEDM    ANDEQ'O'                                       LN0210
     C                     DELET@ARCPAP                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           *IN89     IFEQ '1'                                       LN0210
     C           *LOCK     IN   LDA                                       LN0210
     C                     MOVEL'MAARCHL1'DBFILE                          LN0210
     C                     MOVELECARID    DBKEY            *************  LN0210
     C                     Z-ADD8         DBASE            * DBERR 008 *  LN0210
     C                     EXSR DBERR                      *************  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    Delete Microfiche archiving if not required   LN0210
      *                                                                   LN0210
     C           D2ARMF    IFEQ 'Y'                                       LN0210
     C           SARMF     ANDEQ'N'                                       LN0210
      *                                                                   LN0210
     C           ARCPKP    SETLLMAARCPL2                                  LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
      *                                                                   LN0210
     C           *IN88     DOWEQ'0'                                       LN0210
     C           ECARID    CHAINMAARCHL1             89                   LN0210
      *                                                                   LN0210
     C           *IN89     IFEQ '0'                                       LN0210
     C           EAMEDM    ANDEQ'M'                                       LN0210
     C                     DELET@ARCPAP                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           *IN89     IFEQ '1'                                       LN0210
     C           *LOCK     IN   LDA                                       LN0210
     C                     MOVEL'MAARCHL1'DBFILE                          LN0210
     C                     MOVELECARID    DBKEY            *************  LN0210
     C                     Z-ADD9         DBASE            * DBERR 009 *  LN0210
     C                     EXSR DBERR                      *************  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** If archiving has been requested                                  LN0210
      *                                                                   LN0210
     C           SARMF     IFEQ 'Y'                                       LN0210
     C           SAROD     OREQ 'Y'                                       LN0210
      *                                                                   LN0210
     C                     MOVE 'N'       SARMF                           LN0210
     C                     MOVE 'N'       SAROD                           LN0210
      *                                                                   LN0210
      ** Read Archive Details Subfile                                     LN0210
      *                                                                   LN0210
     C                     Z-ADD1         RRN     50                      LN0210
     C                     READCFC0220S0                 83               LN0210
      *                                                                   LN0210
      **               DOWHILE changed records                            LN0210
      *                                                                   LN0210
     C           *IN83     DOWEQ'0'                                       LN0210
      *                                                                   LN0210
      **     If no record previously existed,                             LN0210
      **                    Write Archive Contents                        LN0210
      *                                                                   LN0210
     C           SSEL      IFEQ '1'                                       LN0210
     C           SEXST     ANDEQ'N'                                       LN0210
     C                     MOVE SARID     ECARID                          LN0210
     C                     MOVE RNAM      ECRNAM                          LN0210
     C                     MOVE PSQN      ECPSQN                          LN0210
     C                     MOVE SRSFX     ECRSFX                          LN0210
     C                     WRITE@ECREJT                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **      If a record did exist, but no current request made          LN0210
      **                    Delete Archive Contents                       LN0210
      *                                                                   LN0210
     C           SSEL      IFEQ *BLANK                                    LN0210
     C           SEXST     ANDEQ'Y'                                       LN0210
     C           ARCPKY    CHAINMAARCPL1             89                   LN0210
     C  N89                DELET@ECREJU                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **    If any records selected for this suffix                       LN0210
      *                                                                   LN0210
     C           SSEL      IFEQ '1'                                       LN0210
      *                                                                   LN0210
      **     Set Archiving flags depending of archive type                LN0210
      *                                                                   LN0210
     C           SMFOD     IFEQ 'MF'                                      LN0210
     C           SMFOD     OREQ 'B '                                      S01382
     C                     MOVE 'Y'       SARMF                           LN0210
     C                     END                                            S01382
      **                                                                  S01382
     C***                  ELSE                                     LN0210S01382
      **                                                                  S01382
     C           SMFOD     IFEQ 'OD'                                      S01382
     C           SMFOD     OREQ 'B '                                      S01382
     C                     MOVE 'Y'       SAROD                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    Read changed records                          LN0210
      *                                                                   LN0210
     C                     READCFC0220S0                 83               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Read Suffix Selection Subfile                                    LN0210
      *                                                                   LN0210
     C                     Z-ADD1         RRN2                            LN0210
     C                     READCFC0220S2                 83               LN0210
      *                                                                   LN0210
      **               DOWHILE changed records                            LN0210
      *                                                                   LN0210
     C           *IN83     DOWEQ'0'                                       LN0210
      *                                                                   LN0210
      **      If a record did exist, but no current request made          LN0210
      **                    Delete Archive Contents                       LN0210
      *                                                                   LN0210
     C           SSELX     IFEQ *BLANK                                    LN0210
     C           SEXSTX    ANDEQ'Y'                                       LN0210
     C           ARCPK     SETLLMAARCPL2                                  LN0210
     C           ARCPK     READEMAARCPL2                 89               LN0210
      *                                                                   LN0210
     C           *IN89     DOWEQ'0'                                       LN0210
     C                     DELET@ARCPAP                                   LN0210
     C           ARCPK     READEMAARCPL2                 89               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    Read changed records                          LN0210
      *                                                                   LN0210
     C                     READCFC0220S2                 83               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *
     C                     MOVE SRNAM     D2RNAM
     C                     MOVE ACTN      D2CHTP
     C                     MOVE SSOID     D2DFTO
     C                     Z-ADDNATN      D2TNLU
     C                     MOVE SDST1     D2DLS1
     C                     MOVE SDST2     D2DLS2
     C                     MOVE SUSRO     D2USRO
     C                     MOVE SARMF     D2ARMF
     C                     MOVE SAROD     D2AROD
     C                     MOVE SDRQD     D2DRQD
     C                     MOVE SUSRR     D2DFTU
      *
      **  WRITE TO FILES IF INSERTION
      *
     C           ACTN      IFEQ 'I'
     C           *IN86     ANDEQ'1'
     C                     WRITE@D2REIZ                20
     C*
     C** TEST FOR UPDATE BY ANOTHER W/S, OR A TERMINAL ERROR
     C*
     C           *IN20     IFEQ '1'
     C*
     C********** STATUS    IFEQ 01021                                     LN0210
     C           MOSTAT    IFEQ 01021                                     LN0210
     C***********          MOVE 'RCF0064' MSGID                           LN1186
     C                     MOVE 'RCF1064' MSGID                           LN1186
     C                     EXSR UPDOWS
     C                     ELSE
     C                     EXSR *PSSR
     C                     END
      *
     C                     END
      *
     C                     END
      *
      **  IF ACTION CODE IS AMEND, UPDATE RECORD
      *
     C           ACTN      IFEQ 'A'
     C           *IN86     ANDEQ'0'
     C                     UPDAT@D2REIZ
     C                     END
      *
      **  Update file controls
      *
     C                     MOVEL'FCMOUTPD'AHFLNM
     C           AHFLNM    CHAINSDFCTLL0             99
      *
      **  Database error if no record found for file
      *
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'SDFCTLL0'DBFILE
     C                     MOVEL'FCMOUTPD'DBKEY            *************
     C                     Z-ADD1         DBASE            * DBERR 001 *
     C                     EXSR DBERR                      *************
     C                     END
      *
      **  Update number of amendments
      *
     C           ACTN      IFEQ 'A'
     C                     ADD  1         AHNOAM
     C                     END
      *
      **  Update number of insertions
      *
     C           ACTN      IFEQ 'I'
     C                     ADD  1         AHNOIN
     C                     ADD  1         AHNORC
     C                     END
      *
      **  Update file control record
      *
     C                     UPDAT@AHREAU
      *
      **     Issue Commit                                                 LN0210
      *                                                                   LN0210
     C                     TIME           MTIME                           LN0210
     C           CMTTXT    COMIT                                          LN0210
     C                     MOVE *BLANK    RTCD                            LN0210
     C                     SETON                     75                   LN0210
      *                                                                   LN0210
     C                     ENDSR
      *
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R VALRPT - TO VALIDATE KEY SCREEN
     C*  CALLED FROM MAIN
     C*
     C********************************************************************
      *
     C           VALRPT    BEGSR
      *
      *
     C                     CALL 'ZA0250'
     C                     SETON                     70
     C                     SETOF                     585936
     C                     WRITEFC0220C1
     C                     EXFMTFC0220F0
      *
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C           *IN70     ANDEQ'1'
      *
     C                     SETOF                     7036
     C                     CALL 'ZA0250'
      *
      ** ALLOW SELECTION
      *
     C           SRNAM     IFEQ '?'
      *
     C                     CALL 'FC0300S'
     C                     PARM *BLANK    W0RTN   7
     C           SRNAM     PARM *BLANK    WQ0006 10
     C*
     C           W0RTN     IFEQ 'PREVSCN'
     C                     SETON                     70
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0059' MSGID                           LN1186
     C                     PARM 'RCF1059' MSGID                           LN1186
     C                     END
     C*
     C           W0RTN     IFEQ 'Y2U9999'
     C                     MOVE W0RTN     RTCD
     C                     EXSR END
     C                     END
     C*
     C                     END
     C*
      ** A REPORT NAME MUST BE ENTERED
     C*
     C           SRNAM     IFEQ *BLANK
     C                     SETON                     7036
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0044' MSGID   7                       LN1186
     C                     PARM 'RCF1044' MSGID   7                       LN1186
     C                     END
     C*
     C           SRNAM     IFNE *BLANK
     C***********SRNAM     CHAINFCREPTL1             79                   S01407
     C           SRNAM     CHAINFCREPTJ0             79                   S01407
      *
      **                         IF no record is found
      **                              Send error message
      *
     C           *IN79     IFEQ '1'
     C                     SETON                     7036
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0042' MSGID   7                       LN1186
     C                     PARM 'RCF1042' MSGID                           LN1186
     C                     ELSE
     C*
     C** CHECK REPORT NAME AGAINST ACTION CODE
     C*
     C           SRNAM     CHAINFCMOUTL1             55
     C*
     C           *IN55     IFEQ '0'
     C           ACTN      ANDEQ'I'
     C                     SETON                     7036
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0068' MSGID   7                       LN1186
     C                     PARM 'RCF1068' MSGID                           LN1186
     C                     END
     C*
     C                     MOVE DXRNAM    RNAM                            LN0210
     C                     MOVE DXRDSC    SRDSC
     C                     MOVE DXDFTO    SSOID
     C*
     C** CHECK MANDATORY VERSION AVAILABLE
     C*
     C                     SETOF                     65
     C***********SRNAM     SETLLFCRCOML1                                  S01407
     C***********SRNAM     READEFCRCOML1                 79               S01407
     C           SRNAM     SETLLFCRCOMJ1                                  S01407
     C           SRNAM     READEFCRCOMJ1                 79               S01407
     C*
     C           *IN79     DOWEQ'0'
     C           D3MORQ    COMP 'M'                      65
     C   65                SETON                     79
     C**N79******SRNAM     READEFCRCOML1                 79               S01407
     C  N79      SRNAM     READEFCRCOMJ1                 79               S01407
     C                     END
     C*
      ** IF NO RECORD TYPE 'M' FOUND, SEND ERROR MESSAGE
     C*
     C           *IN65     IFEQ '0'
     C                     SETON                     7036
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0043' MSGID   7                       LN1186
     C                     PARM 'RCF1043' MSGID                           LN1186
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C   70                WRITEFC0220C1
     C   70                EXFMTFC0220F0
      *
     C                     END
      *
      ** IF SCREEN IS VALID, SET TO NEXT DISPLAY
      *
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C                     MOVE 'Y'       MAINF
     C                     MOVE 'N'       KEYF
     C                     END
     C*
     C** IF EXIT REQUESTED, TERMINATE PROGRAM
     C*
     C           *IN03     IFEQ '1'
     C                     MOVE 'Y2U9999' RTCD
     C                     EXSR END
     C                     END
     C*
     C** IF PREVIOUS SCREEN REQUESTED, EXIT PROGRAM
     C*
     C           *IN12     IFEQ '1'
     C                     SETOF                     12
     C                     MOVE *BLANK    RTCD
     C                     EXSR END
     C                     END
      *
     C                     ENDSR
      *
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R VALINT - TO VALIDATE INITIAL SCREEN
     C*  CALLED FROM MAIN
     C*
     C********************************************************************
      *
     C           VALINT    BEGSR
      *
     C                     CALL 'ZA0250'
     C                     SETON                     7058
     C                     SETOF                     593738
     C                     WRITEFC0220C1
     C                     EXFMTFC0220F0
      *
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C           *IN70     ANDEQ'1'
      *
     C                     SETOF                     703738
     C                     CALL 'ZA0250'
      *
     C           SUSRR     CHAINMUSER                84
      *
      **                         IF no record is found
      **                              Send error message
      *
     C           *IN84     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     SETON                     7037
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0006' MSGID   7                       LN1186
     C                     PARM 'RCF1006' MSGID                           LN1186
     C                     END
      *                                                                   LN1151
      ** CHAIN TO MUSER TO PICK UP THE USERS AUTHORITY TO OUTPUT          LN1151
      ** INSTRUCTIONS.IF CHAIN FAILS PERFORM DATABASE ERROR PROCESSING.   LN1151
      *                                                                   LN1151
     C           *IN37     IFEQ '0'                                       LN1151
     C           JUSR      CHAINMUSER                84                   LN1151
     C           *IN84     IFEQ '1'                                       LN1151
     C           RECI      ORNE 'D'                                       LN1151
     C           *LOCK     IN   LDA                                       LN1151
     C                     MOVEL'MUSER   'DBFILE                          LN1151
     C                     MOVELJUSR      DBKEY            *************  LN1151
     C                     Z-ADD10        DBASE            * DBERR 010 *  LN1151
     C                     EXSR DBERR                      *************  LN1151
     C                     END                                            LN1151
      *                                                                   LN1151
      ** SEND AN ERROR MESSAGE IF THE USER ATTEMPTS TO                    LN1151
      ** ASSOCIATE A REPORT WITH A USER TO WHICH HE HAS NO                LN1151
      ** AUTHORITY.                                                       LN1151
      *                                                                   LN1151
     C           MUREP     IFNE 'Y'                                       LN1151
     C           SUSRR     ANDNE*BLANKS                                   LN1151
     C           SUSRR     ANDNEJUSR                                      LN1151
     C                     SETON                     7037                 LN1151
     C                     CALL 'FCC0240'                                 LN1151
     C                     PARM 'RCF1087' MSGID                           LN1151
     C                     END                                            LN1151
     C                     END                                            LN1151
     C*
     C** DISTRIBUTION REQUIRED MUST BE 'Y' OR 'N'
     C*
     C           SDRQD     IFNE 'Y'
     C           SDRQD     ANDNE'N'
     C           SDRQD     ANDNE*BLANK
     C                     SETON                     7038
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0031' MSGID   7                       LN1186
     C                     PARM 'RCF1031' MSGID                           LN1186
     C                     END
     C*
      **  IF DISTRIBUTION IS BLANK SET TO N.
     C*
     C           SDRQD     IFEQ *BLANKS
     C                     MOVE 'N'       SDRQD
     C                     END
      *
     C   70                WRITEFC0220C1
     C   70                EXFMTFC0220F0
      *
     C                     END
      *
      ** IF SCREEN IS VALID, SET TO NEXT DISPLAY
      *
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C                     MOVE 'Y'       OUTPF   1
     C                     MOVE 'N'       MAINF
     C                     END
     C*
     C** IF EXIT REQUESTED, TERMINATE PROGRAM
     C*
     C           *IN03     IFEQ '1'
     C                     MOVE 'Y2U9999' RTCD
     C                     EXSR END
     C                     END
     C*
     C** IF PREVIOUS SCREEN REQUESTED, DETERMINE WHETHER KEY/CALLING PGM
     C*
     C           *IN12     IFEQ '1'
     C*
     C                     SETOF                     12
     C*
     C           ACTN      IFEQ 'I'
     C                     MOVE 'Y'       KEYF
     C                     MOVE 'N'       MAINF
     C                     ELSE
     C                     MOVE *BLANK    RTCD
     C                     EXSR END
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R VALID - TO VALIDATE MAIN SCREEN
     C*  CALLED FROM MAIN
     C*
     C********************************************************************
      *
     C           VALID     BEGSR
      *                                                                   LN0210
     C           ACTN      IFEQ 'E'                                       LN0210
     C                     SETON                     54                   LN0210
     C                     END                                            LN0210
      *
      **  Reset Error display for archiving only if no archive id's exist LN0210
      *                                                                   LN0210
     C           NOARCH    IFEQ 'N'                                       LN0210
     C                     CALL 'ZA0250'
     C                     SETOF                     3334                 LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     MOVE 'N'       NOARCH                          LN0210
      *                                                                   LN0210
     C                     SETON                     5970
     C******************** SETOF                     583334               LN0210
     C                     SETOF                     58                   LN0210
     C                     SETOF                     303132
      *
      **               Do not display Distribution list fields, or
      **               Midas User field if Distribution is required
      *
     C           SDRQD     IFEQ 'Y'
     C                     SETON                     51
     C                     ELSE
     C                     SETOF                     51
     C                     END
      *
      ** IF DISTRIBUTION IS REQUIRED, ENSURE DIST LIST/USER ARE BLANK
      *
     C           SDRQD     IFEQ 'Y'
     C                     MOVE *BLANK    SDST1
     C                     MOVE *BLANK    SDST2
     C                     MOVE *BLANK    SUSRO
     C                     END
      *
      ** IF DISTRIBUTION NOT REQUIRED, DISPLAY DIST LIST/USER
      *
     C           SDRQD     IFEQ 'N'
     C           ACTN      ANDEQ'A'
     C                     MOVE D2DLS1    SDST1
     C                     MOVE D2DLS2    SDST2
     C                     MOVE D2USRO    SUSRO
     C                     END
      *
     C                     WRITEFC0220C1
     C                     EXFMTFC0220F0
      *
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C           *IN70     ANDEQ'1'
      *
     C                     CALL 'ZA0250'
      *
     C                     SETOF                     703334
     C                     SETOF                     303132
      *
      **      Validation of input fields for insertion/amendment
      *
     C           ACTN      IFEQ 'I'
     C           ACTN      OREQ 'A'
      *
      **               IF standard output instructions is non-blank
      **               AND Distribution list parts 1/2 is non-blank
      *
     C           SSOID     IFNE *BLANK
     C           SDST1     ANDNE*BLANK
     C           SSOID     ORNE *BLANK
     C           SDST2     ANDNE*BLANK
      *
      **               OR Standard output instructions is non-blank
      **               AND User Id is non-blank
      *
     C           SSOID     ORNE *BLANK
     C           SUSRO     ANDNE*BLANK
      *
      **               OR Distribution list parts 1/2 is non-blank
      **               AND User Id is non-blank
      *
     C           SUSRO     ORNE *BLANK
     C           SDST1     ANDNE*BLANK
     C           SUSRO     ORNE *BLANK
     C           SDST2     ANDNE*BLANK
      *
      **                    Send error message
      *
     C                     SETON                     70
     C                     SETON                     303132
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0001' MSGID   7                       LN1186
     C                     PARM 'RCF1001' MSGID                           LN1186
     C                     END
      *
      **               IF Standard Output Instructions field is
      **               non-blank
      *
     C           SSOID     IFNE *BLANK
     C           SWARN     ANDNELWARN
      *
      **                    Access Standard Output Instructions
      **                    file for this Id
      *
     C                     MOVELSSOID     KSOID
     C           SOIKEY    SETLLFCSOUTL1                 80
      *
      **                    IF no record is found
      **                         Send warning message to user
      *
     C           *IN80     IFEQ '0'
     C                     SETON                     7030
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0002' MSGID   7                       LN1186
     C                     PARM 'RCF1002' MSGID                           LN1186
     C                     END
      *
     C                     MOVE SSOID     LSOID
     C                     MOVE SDST1     LDST1
     C                     MOVE SDST2     LDST2
     C                     MOVE SUSRO     LUSRO
     C                     MOVE SARMF     LARMF
     C                     MOVE SAROD     LAROD
      *
     C                     END
      *
      **               IF Distribution list field 1 has been
      **               entered
      **               AND Distribution list field 2 is blank
      *
     C           SDST1     IFNE *BLANK
     C           SDST2     ANDEQ*BLANK
      *
      **               OR Distribution list 2 field has been
      **               entered
      **               AND Distribution list 1 field is blank
      *
     C           SDST2     ORNE *BLANK
     C           SDST1     ANDEQ*BLANK
      *
      **                    Send error message to user
      *
     C                     SETON                     7031
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0003' MSGID   7                       LN1186
     C                     PARM 'RCF1003' MSGID                           LN1186
     C                     END
      *                                                                   043702
      **               IF Distribution list fields 1 and 2 are not        043702
      **               blank                                              043702
      *                                                                   043702
     C           SDST1     IFNE *BLANK                                    043702
     C           SDST2     ANDNE*BLANK                                    043702
      *                                                                   043702
      **                    Access Distribution List Logical file for     043702
      **                    this distribution list                        043702
      *                                                                   043702
     C                     MOVELSDST1     KDIST1                          043702
     C                     MOVELSDST2     KDIST2                          043702
     C********** KDLIST    CHAINQAOKDL01             80                                043702 CAAA02
     C           KDLIST    CHAINQAOKL01A             80                                       CAAA02
      *                                                                   043702
      **                    IF no record is found                         043702
      **                         Send warning message to user             043702
      *                                                                   043702
     C           *IN80     IFEQ '1'                                       043702
     C                     SETON                     7031                 043702
     C                     CALL 'FCC0240'                                 043702
     C                     PARM 'RCF1017' MSGID                           043702
     C                     END                                            043702
      *                                                                   043702
     C                     END                                            043702
      *
      **               IF a user id has been entered
      *
     C           SUSRO     IFNE *BLANK
      *
      **                    IF this is the user's own Id
      **                    AND the user has no output queue
      **                         Send error message
      *
     C           SUSRO     IFEQ JUSR
     C           POUTQ     ANDEQ*BLANK
     C                     SETON                     7032
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0004' MSGID   7                       LN1186
     C                     PARM 'RCF1004' MSGID                           LN1186
     C                     END
      *
      **                    IF this is not the user's own Id
      **                    AND user is not authorised to others
      **                         Send error message
      *
     C           SUSRO     IFNE JUSR
     C           PMUREP    ANDEQ'N'
     C                     SETON                     7032
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0005' MSGID   7                       LN1186
     C                     PARM 'RCF1005' MSGID                           LN1186
     C                     END
      *
      **                    IF user is authorised to others
      **                    AND this is not the user's own Id
      *
     C           PMUREP    IFEQ 'Y'
     C           SUSRO     ANDNEJUSR
      *
      **                         Access MUSER for the user Id
      **                         entered
      *
     C           SUSRO     CHAINMUSER                84
      *
      **                         IF no record is found
      **                              Send error message
      *
     C           *IN84     IFEQ '1'
     C           RECI      ORNE 'D'
     C                     SETON                     7032
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0006' MSGID   7                       LN1186
     C                     PARM 'RCF1006' MSGID                           LN1186
     C                     ELSE
      *
      **                         Otherwise
      **                                   IF this user has no
      **                                   output queue
      **                                        Send error message
      *
     C           OUTQ      IFEQ *BLANK
     C                     SETON                     7032
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0007' MSGID   7                       LN1186
     C                     PARM 'RCF1007' MSGID                           LN1186
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     END
      *
      **               IF Archived on Fiche is not Y or N
      **                    Send error message
      *
     C           SARMF     IFNE 'Y'
     C           SARMF     ANDNE'N'
     C           SARMF     ANDNE*BLANK
     C                     SETON                     7033
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0008' MSGID   7                       LN1186
     C                     PARM 'RCF1008' MSGID                           LN1186
     C                     END
      *
      **               IF Archived on Optical is not Y or N
      **                    Send error message
      *
     C           SAROD     IFNE 'Y'
     C           SAROD     ANDNE'N'
     C           SAROD     ANDNE*BLANK
     C                     SETON                     7034
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0009' MSGID   7                       LN1186
     C                     PARM 'RCF1009' MSGID                           LN1186
     C                     END
      *
      **         Reset Defaults
      *
     C           SARMF     IFEQ *BLANK
     C                     MOVE 'N'       SARMF
     C                     END
      *
     C           SAROD     IFEQ *BLANK
     C                     MOVE 'N'       SAROD
     C                     END
      *
     C                     END
      *
      *********Validation*of*Deletion*Processing**************************LN0210
      *
     C********** ACTN      IFEQ 'D'                                       LN0210
      *
     C********** *IN10     IFEQ '0'                                       LN0210
     C******************** SETON                     70                   LN0210
     C******************** CALL 'FCC0240'                                 LN0210
     C******************** PARM 'RCF0021' MSGID   7                       LN0210
     C******************** END                                            LN0210
      *
     C******************** END                                            LN0210
      *
     C   70                WRITEFC0220C1
     C   70                EXFMTFC0220F0
      *
     C                     END
      *
      ***IF*SCREEN*IS*VALID,*UPDATE*FILES*AND*EXIT************************LN0210
      ** SCREEN VALID AND EXIT/PREVIOUS NOT REQUESTED                     LN0210
      *
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
      **********                                                          LN0210
     C********** ACTN      IFEQ 'A'                                       LN0210
     C********** ACTN      OREQ 'I'                                       LN0210
     C**********           EXSR INSAMD                                    LN0210
     C**********           MOVE 'N'       OUTPF                           LN0210
     C**********           END                                            LN0210
      **********                                                          LN0210
     C**********           MOVE *BLANK    RTCD                            LN0210
      *                                                                   LN0210
      ** SET CURRENT SCREEN OFF, CHECK ARCHIVING REQUIREMENT              LN0210
      **    IF ARCHIVING NOT PRESENT, PERFORM UPDATES                     LN0210
      **    IF ARCHIVING MAY BE REQUIRED, PROCESS ARCHIVING               LN0210
      *                                                                   LN0210
     C                     MOVE 'N'       OUTPF                           LN0210
      *                                                                   LN0210
     C           SAROD     IFEQ 'Y'                                       LN0210
     C           SARMF     OREQ 'Y'                                       LN0210
     C                     MOVE 'Y'       RSFXF   1                       LN0210
     C                     ELSE                                           LN0210
     C                     MOVE 'Y'       UPDF    1                       LN0210
     C                     END                                            LN0210
      *
     C                     END
     C*
     C** IF EXIT REQUESTED, TERMINATE PROGRAM
     C*
     C           *IN03     IFEQ '1'
     C                     MOVE 'Y2U9999' RTCD
     C                     EXSR END
     C                     END
     C*
     C** IF PREVIOUS SCREEN REQUESTED, RETURN TO MAIN DETAILS
     C*
     C           *IN12     IFEQ '1'
     C*                                                                   LN0210
     C           ACTN      IFEQ 'E'                                       LN0210
     C                     MOVE *BLANK    RTCD                            LN0210
     C                     EXSR END                                       LN0210
     C                     ELSE                                           LN0210
     C                     MOVE 'Y'       MAINF
     C                     END                                            LN0210
     C*                                                                   LN0210
     C                     MOVE 'N'       OUTPF
     C                     SETOF                     12
     C                     END
      *
     C                     ENDSR
     C********************************************************************
      /EJECT                                                              LN0210
     C********************************************************************LN0210
     C*                                                                   LN0210
     C*  S/R RSFXS - TO DISPLAY SUFFIX SELECTION SCREEN FOR ARCHIVING     LN0210
     C*  CALLED FROM MAIN                                                 LN0210
     C*  CALLS                                                            LN0210
     C*                                                                   LN0210
     C********************************************************************LN0210
      *                                                                   LN0210
     C           RSFXS     BEGSR                                          LN0210
      *                                                                   LN0210
      ** Access Report Suffixes (Spool file names) for the report         LN0210
      *                                                                   LN0210
     C           RNAM      SETLLFCRSFXL1                                  LN0210
     C           RNAM      READEFCRSFXL1                 81               LN0210
      *                                                                   LN0210
      ** Ignore Audit Report sections                                     LN0210
      *                                                                   LN0210
     C           DYAURP    IFEQ 'Y'                                       LN0210
     C           RNAM      READEFCRSFXL1                 81               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    IF no records found                           LN0210
      *                                                                   LN0210
     C           *IN81     IFEQ '1'                                       LN0210
     C           *LOCK     IN   LDA                                       LN0210
     C                     MOVEL'FCRSFXL1'DBFILE                          LN0210
     C                     MOVELRNAM      DBKEY            *************  LN0210
     C                     Z-ADD7         DBASE            * DBERR 007 *  LN0210
     C                     EXSR DBERR                      *************  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           *IN02     IFEQ '1'                                       LN0210
      *                                                                   LN0210
      ** Reset Relative Record number for subfile processing              LN0210
      *                                                                   LN0210
     C                     Z-ADD0         RRN2    50                      LN0210
     C                     SETON                     22                   LN0210
     C                     SETOF                     21                   LN0210
     C                     WRITEFC0220C2                                  LN0210
     C                     SETOF                     22                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     SETON                     02                   LN0210
      *                                                                   LN0210
      ** Process all report suffixes and load into subfile                LN0210
      *                                                                   LN0210
     C           *IN81     DOWEQ'0'                                       LN0210
      *                                                                   LN0210
      ** Set Subfile fileds                                               LN0210
      *                                                                   LN0210
     C                     MOVE DYRSFX    SRSFX                           LN0210
     C                     MOVE DYPFTX    SPFTX                           LN0210
      *                                                                   LN0210
      ** Check for existing details for the suffix being processed        LN0210
      *                                                                   LN0210
     C           ARCPK     SETLLMAARCPL2                 85               LN0210
      *                                                                   LN0210
      ** If details exist, set select field to '1' and record exists      LN0210
      ** flag to Yes. Set on SFLNXTCHG to enable READC operation later    LN0210
      *                                                                   LN0210
     C           *IN85     IFEQ '1'                                       LN0210
     C                     MOVE 'Y'       SEXSTX                          LN0210
     C                     MOVE '1'       SSELX                           LN0210
     C                     SETON                         20               LN0210
     C                     ELSE                                           LN0210
      *                                                                   LN0210
      ** If no records exist, blank subfile fields and set off SFLNXTCHG  LN0210
      *                                                                   LN0210
     C                     MOVE 'N'       SEXSTX                          LN0210
     C                     MOVE *BLANK    SSELX                           LN0210
     C                     SETOF                         20               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Write subfile record                                             LN0210
      *                                                                   LN0210
     C                     ADD  1         RRN2                            LN0210
     C                     WRITEFC0220S2                                  LN0210
      *                                                                   LN0210
      ** Access next suffix detail                                        LN0210
      *                                                                   LN0210
     C           RNAM      READEFCRSFXL1                 81               LN0210
      *                                                                   LN0210
      ** Ignore if audit report section                                   LN0210
      *                                                                   LN0210
     C           DYAURP    IFEQ 'Y'                                       LN0210
     C           RNAM      READEFCRSFXL1                 81               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Validate Suffixes Screen                                         LN0210
      *                                                                   LN0210
     C                     EXSR VALRSX                                    LN0210
      *                                                                   LN0210
     C                     ENDSR                                          LN0210
     C********************************************************************LN0210
      /EJECT                                                              LN0210
     C********************************************************************LN0210
     C*                                                                   LN0210
     C*  S/R VALRSX - TO VALIDATE SUFFIX SELECTION SCREEN                 LN0210
     C*  CALLED FROM RSFXS                                                LN0210
     C*  CALLS                                                            LN0210
     C*                                                                   LN0210
     C********************************************************************LN0210
      *                                                                   LN0210
     C           VALRSX    BEGSR                                          LN0210
      *                                                                   LN0210
      **               Set on SFLNXTCHG                                   LN0210
      *                                                                   LN0210
     C                     SETON                     202123               LN0210
     C                     SETON                     71                   LN0210
     C                     SETOF                     72                   LN0210
     C                     MOVE 'N'       SELCT                           LN0210
     C                     WRITEFC0220F1                                  LN0210
     C                     WRITEFC0220F2                                  LN0210
     C                     WRITEFC0220C1                                  LN0210
     C                     EXFMTFC0220C2                                  LN0210
      *                                                                   LN0210
      **               DOWHILE screen not valid                           LN0210
      *                                                                   LN0210
     C           *IN71     DOWEQ'1'                                       LN0210
     C           *IN03     ANDEQ'0'                                       LN0210
     C           *IN12     ANDEQ'0'                                       LN0210
      *                                                                   LN0210
     C                     SETOF                     7135                 LN0210
     C                     CALL 'ZA0250'                                  LN0210
      *                                                                   LN0210
      **               Read Changed Subfile Records                       LN0210
      *                                                                   LN0210
     C                     READCFC0220S2                 83               LN0210
      *                                                                   LN0210
      **               DOWHILE changed subfile records remain             LN0210
      *                                                                   LN0210
     C           *IN83     DOWEQ'0'                                       LN0210
      *                                                                   LN0210
      **                    IF Subfile select is not '1'/blank            LN0210
      **                         Send error message                       LN0210
      *                                                                   LN0210
     C           SSELX     IFNE '1'                                       LN0210
     C           SSELX     ANDNE*BLANK                                    LN0210
     C                     SETON                     713520               LN0210
     C                     CALL 'FCC0240'                                 LN0210
     C***********          PARM 'RCF0012' MSGID   7                LN0210 LN1186
     C                     PARM 'RCF1012' MSGID                           LN1186
     C                     UPDATFC0220S2                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **     If selection made, or if record previously existed for       LN0210
      **     archive, set on SFLNXTCHG indicator for updates              LN0210
      *                                                                   LN0210
     C           SSELX     IFEQ *BLANK                                    LN0210
     C           SEXSTX    ANDEQ'Y'                                       LN0210
     C           SSELX     OREQ '1'                                       LN0210
     C                     SETON                     20                   LN0210
     C                     SETOF                     35                   LN0210
     C                     UPDATFC0220S2                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **     If record changed to have selection blanked out, and no      LN0210
      **     archive record previously existed, set of SFLNXTCHG          LN0210
      *                                                                   LN0210
     C           SSELX     IFEQ *BLANK                                    LN0210
     C           SEXSTX    ANDNE'Y'                                       LN0210
     C                     SETOF                     3520                 LN0210
     C                     UPDATFC0220S2                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **     If an attempt has been made to select an archive, reset      LN0210
      **     warning flags                                                LN0210
      *                                                                   LN0210
     C           SSELX     IFNE *BLANK                                    LN0210
     C                     MOVE 'Y'       SELCT   1                       LN0210
     C                     SETOF                     72                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    Read changed records                          LN0210
      *                                                                   LN0210
     C                     READCFC0220S2                 83               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **               IF no selection has been made                      LN0210
      **               AND no warning is currently                        LN0210
      **               displayed                                          LN0210
      **                    Send Warning message                          LN0210
      *                                                                   LN0210
     C           SELCT     IFEQ 'N'                                       LN0210
     C           *IN72     ANDEQ'0'                                       LN0210
     C                     SETON                     7172                 LN0210
     C                     CALL 'FCC0240'                                 LN0210
     C***********          PARM 'RCF0011' MSGID   7                LN0210 LN1186
     C                     PARM 'RCF1011' MSGID                           LN1186
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Display screen if still in error: reset warning flag             LN0210
      *                                                                   LN0210
     C           *IN71     IFEQ '1'                                       LN0210
     C                     MOVE 'N'       SELCT                           LN0210
     C                     WRITEFC0220F1                                  LN0210
     C                     WRITEFC0220F2                                  LN0210
     C                     WRITEFC0220C1                                  LN0210
     C                     EXFMTFC0220C2                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **    Subfile valid, further processing required                    LN0210
      *                                                                   LN0210
     C           *IN71     IFEQ '0'                                       LN0210
     C           *IN03     ANDEQ'0'                                       LN0210
     C           *IN12     ANDEQ'0'                                       LN0210
     C           SELCT     ANDEQ'Y'                                       LN0210
     C                     MOVE 'Y'       ARCHF   1                       LN0210
     C                     MOVE 'N'       RSFXF                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **    Subfile valid, no selection made                              LN0210
      *                                                                   LN0210
     C           *IN71     IFEQ '0'                                       LN0210
     C           *IN03     ANDEQ'0'                                       LN0210
     C           *IN12     ANDEQ'0'                                       LN0210
     C           SELCT     ANDEQ'N'                                       LN0210
     C                     MOVE 'N'       SARMF                           LN0210
     C                     MOVE 'N'       SAROD                           LN0210
     C                     MOVE 'Y'       UPDF                            LN0210
     C                     MOVE 'N'       RSFXF                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Exit requested                                                   LN0210
      *                                                                   LN0210
     C           *IN03     IFEQ '1'                                       LN0210
     C                     MOVE 'Y2U9999' RTCD                            LN0210
     C                     EXSR END                                       LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Previous screen requested                                        LN0210
      *                                                                   LN0210
     C           *IN12     IFEQ '1'                                       LN0210
     C                     MOVE 'Y'       OUTPF                           LN0210
     C                     MOVE 'N'       RSFXF                           LN0210
     C                     SETOF                     12                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     ENDSR                                          LN0210
      ********************************************************************LN0210
      /EJECT                                                              LN0210
     C********************************************************************LN0210
     C*                                                                   LN0210
     C*  S/R SETSUB - TO LOAD SUBFILE OF ARCHIVE IDENTIFIERS              LN0210
     C*  CALLED FROM ARCHS                                                LN0210
     C*                                                                   LN0210
     C********************************************************************LN0210
     C*                                                                   LN0210
     C           SETSUB    BEGSR                                          LN0210
      *                                                                   LN0210
      **     Set to beginning of subfile                                  LN0210
      *                                                                   LN0210
     C                     Z-ADD1         RRN2    50                      LN0210
     C                     READCFC0220S2                 83               LN0210
      *                                                                   LN0210
      ** Read next record if not selected                                 LN0210
      *                                                                   LN0210
     C           SSELX     IFNE '1'                                       LN0210
     C                     SETON                     20                   LN0210
     C                     UPDATFC0220S2                                  LN0210
     C                     READCFC0220S2                 83               LN0210
     C                     SETOF                     20                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           *IN01     IFEQ '1'                                       LN0210
     C                     Z-ADD0         RRN                             LN0210
     C                     SETON                     22                   LN0210
     C                     SETOF                     21                   LN0210
     C                     WRITEFC0220C0                                  LN0210
     C                     SETOF                     22                   LN0210
     C                     END                                            LN0210
     C                     SETON                     01                   LN0210
     C                     MOVE 'N'       SEXST                           LN0210
     C                     SETOF                     20                   LN0210
      *                                                                   LN0210
      **               DOWHILE changed records                            LN0210
      *                                                                   LN0210
     C           *IN83     DOWEQ'0'                                       LN0210
      *                                                                   LN0210
      **                    Access Archive Id's for the required medium   LN0210
      *                                                                   LN0210
     C           *LIKE     DEFN EAMEDM    KMEDM                           LN0210
      *                                                                   LN0210
     C                     CALL 'ZA0250'                                  LN0210
      *                                                                   LN0210
     C           SARMF     IFEQ 'Y'                                       LN0210
     C           SAROD     ANDEQ'N'                                       LN0210
     C                     MOVE 'M'       KMEDM                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           SAROD     IFEQ 'Y'                                       LN0210
     C           SARMF     ANDEQ'N'                                       LN0210
     C                     MOVE 'O'       KMEDM                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           SAROD     IFEQ 'Y'                                       LN0210
     C           SARMF     ANDEQ'Y'                                       LN0210
     C                     MOVE *BLANK    KMEDM                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           KMEDM     SETLLMAARCHL2                                  LN0210
     C                     READ MAARCHL2                 82               LN0210
      *                                                                   LN0210
      **                   Send error message if no Archive Id's availableLN0210
      *                                                                   LN0210
     C           *IN82     IFEQ '1'                                       LN0210
     C           *IN82     OREQ '0'                                       LN0210
     C           KMEDM     ANDNEEAMEDM                                    LN0210
     C           KMEDM     ANDNE*BLANK                                    LN0210
     C                     CALL 'FCC0240'                                 LN0210
     C***********          PARM 'RCF0010' MSGID   7                LN0210 LN1186
     C                     PARM 'RCF1010' MSGID                           LN1186
      *                                                                   LN0210
     C           SARMF     IFEQ 'Y'                                       LN0210
     C                     SETON                     33                   LN0210
     C                     MOVE 'N'       SARMF                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           SAROD     IFEQ 'Y'                                       LN0210
     C                     MOVE 'N'       SAROD                           LN0210
     C                     SETON                     34                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **  RETURN TO OUTPUT SCREEN                                         LN0210
      *                                                                   LN0210
     C                     MOVE 'N'       ARCHF                           LN0210
     C                     MOVE 'Y'       OUTPF                           LN0210
     C                     MOVE 'Y'       NOARCH  1                       LN0210
      *                                                                   LN0210
     C                     GOTO SUBEND                                    LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    DOWHILE Archive Id's exist for the media      LN0210
      **                         Write Subfile Record                     LN0210
      **                         Read Archive Id's for the media          LN0210
      **                    ENDDO                                         LN0210
      *                                                                   LN0210
     C           *IN82     DOWEQ'0'                                       LN0210
     C           KMEDM     ANDEQ*BLANK                                    LN0210
     C           *IN82     OREQ '0'                                       LN0210
     C           KMEDM     ANDEQEAMEDM                                    LN0210
     C                     ADD  1         RRN                             LN0210
     C                     MOVE EAARID    SARID                           LN0210
      *                                                                   LN0210
     C           EAMEDM    IFEQ 'O'                                       LN0210
     C                     MOVE 'OD'      SMFOD                           LN0210
     C                     END                                            S01382
      *                                                                   S01382
     C***                  ELSE                                     LN0210S01382
      *                                                                   S01382
     C           EAMEDM    IFEQ 'M'                                       S01382
     C                     MOVE 'MF'      SMFOD                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C           EAMEDM    IFEQ 'B'                                       S01382
     C                     MOVE 'B '      SMFOD                           S01382
     C                     END                                            S01382
      *                                                                   S01382
     C                     MOVE EAADSC    SADSC                           LN0210
      *                                                                   LN0210
     C                     SETOF                     2035                 LN0210
     C                     MOVE 'N'       SEXST                           LN0210
     C                     MOVE *BLANK    SSEL                            LN0210
      *                                                                   LN0210
      ** Access corresponding archive details record                      LN0210
      *                                                                   LN0210
     C           ARCPKY    CHAINMAARCPL1             89                   LN0210
      *                                                                   LN0210
      ** If record exists, set subfile indicators: selected               LN0210
      **                                           record exists          LN0210
      **                                           SFLNXTCHG              LN0210
      *                                                                   LN0210
     C           *IN89     IFEQ '0'                                       LN0210
     C                     MOVE '1'       SSEL                            LN0210
     C                     MOVE 'Y'       SEXST                           LN0210
     C                     SETON                     20                   LN0210
     C                     EXCPTRELACP                                    LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     WRITEFC0220S0                                  LN0210
     C                     READ MAARCHL2                 82               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                    Read changed records                          LN0210
      *                                                                   LN0210
     C                     SETON                     20                   LN0210
     C                     UPDATFC0220S2                                  LN0210
     C                     SETOF                     20                   LN0210
      *                                                                   LN0210
     C                     READCFC0220S2                 83               LN0210
      *                                                                   LN0210
      ** Read next record if not selected                                 LN0210
      *                                                                   LN0210
     C           SSELX     IFNE '1'                                       LN0210
     C                     SETON                     20                   LN0210
     C                     UPDATFC0220S2                                  LN0210
     C                     READCFC0220S2                 83               LN0210
     C                     SETOF                     20                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     SETON                     2123                 LN0210
      *                                                                   LN0210
     C                     EXSR ARCHV                                     LN0210
      *                                                                   LN0210
     C           SUBEND    ENDSR                                          LN0210
     C********************************************************************LN0210
      /EJECT                                                              LN0210
     C********************************************************************LN0210
     C*                                                                   LN0210
     C*  S/R ARCHV - TO VALIDATE CHANGED SUBFILE RECORDS FOR PRINT SUFFIX LN0210
     C*  CALLED FROM ARCHS                                                LN0210
     C*                                                                   LN0210
     C********************************************************************LN0210
     C           ARCHV     BEGSR                                          LN0210
      *                                                                   LN0210
      **               Set on SFLNXTCHG                                   LN0210
      *                                                                   LN0210
     C                     SETON                     202171               LN0210
     C                     SETOF                     72                   LN0210
     C                     MOVE 'N'       SELCT                           LN0210
     C                     WRITEFC0220F1                                  LN0210
     C                     WRITEFC0220F2                                  LN0210
     C                     WRITEFC0220C1                                  LN0210
     C                     EXFMTFC0220C0                                  LN0210
      *                                                                   LN0210
      **               DOWHILE screen not valid                           LN0210
      *                                                                   LN0210
     C           *IN71     DOWEQ'1'                                       LN0210
     C           *IN03     ANDEQ'0'                                       LN0210
     C           *IN12     ANDEQ'0'                                       LN0210
      *                                                                   LN0210
     C                     SETOF                     7135                 LN0210
     C                     CALL 'ZA0250'                                  LN0210
      *                                                                   LN0210
      **               Read Changed Subfile Records                       LN0210
      *                                                                   LN0210
     C                     READCFC0220S0                 83               LN0210
      *                                                                   LN0210
      **               DOWHILE changed subfile records remain             LN0210
      *                                                                   LN0210
     C           *IN83     DOWEQ'0'                                       LN0210
      *                                                                   LN0210
      **                    IF Subfile select is not '1'/blank            LN0210
      **                         Send error message                       LN0210
      *                                                                   LN0210
     C           SSEL      IFNE '1'                                       LN0210
     C           SSEL      ANDNE*BLANK                                    LN0210
     C                     SETON                     713520               LN0210
     C                     CALL 'FCC0240'                                 LN0210
     C***********          PARM 'RCF0012' MSGID   7                LN0210 LN1186
     C                     PARM 'RCF1012' MSGID                           LN1186
     C                     UPDATFC0220S0                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **     If selection made, or if record previously existed for       LN0210
      **     archive, set on SFLNXTCHG indicator for updates              LN0210
      *                                                                   LN0210
     C           SSEL      IFEQ *BLANK                                    LN0210
     C           SEXST     ANDEQ'Y'                                       LN0210
     C           SSEL      OREQ '1'                                       LN0210
     C                     SETON                     20                   LN0210
     C                     SETOF                     35                   LN0210
     C                     UPDATFC0220S0                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **     If record changed to have selection blanked out, and no      LN0210
      **     archive record previously existed, set of SFLNXTCHG          LN0210
      *                                                                   LN0210
     C           SSEL      IFEQ *BLANK                                    LN0210
     C           SEXST     ANDNE'Y'                                       LN0210
     C                     SETOF                     3520                 LN0210
     C                     UPDATFC0220S0                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **     If an attempt has been made to select an archive, reset      LN0210
      **     warning flags                                                LN0210
      *                                                                   LN0210
     C           SSEL      IFNE *BLANK                                    LN0210
     C                     MOVE 'Y'       SELCT   1                       LN0210
     C                     SETOF                     72                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **                         Read changed subfile records             LN0210
      *                                                                   LN0210
     C                     READCFC0220S0                 83               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **               IF no selection has been made                      LN0210
      **               AND no warning is currently                        LN0210
      **               displayed                                          LN0210
      **                    Send Warning message                          LN0210
      **               Otherwise                                          LN0210
      *                                                                   LN0210
     C           SELCT     IFEQ 'N'                                       LN0210
     C           *IN72     ANDEQ'0'                                       LN0210
     C                     SETON                     7172                 LN0210
     C                     CALL 'FCC0240'                                 LN0210
     C***********          PARM 'RCF0011' MSGID   7                LN0210 LN1186
     C                     PARM 'RCF1011' MSGID                           LN1186
     C                     END                                            LN0210
      *                                                                   LN0210
      ** If screen is not valid, redisplay                                LN0210
      *                                                                   LN0210
     C           *IN71     IFEQ '1'                                       LN0210
     C                     MOVE 'N'       SELCT                           LN0210
     C                     WRITEFC0220F1                                  LN0210
     C                     WRITEFC0220F2                                  LN0210
     C                     WRITEFC0220C1                                  LN0210
     C                     EXFMTFC0220C0                                  LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **    Subfile valid, but no archive records selected                LN0210
      *                                                                   LN0210
     C           *IN71     IFEQ '0'                                       LN0210
     C           *IN03     ANDEQ'0'                                       LN0210
     C           *IN12     ANDEQ'0'                                       LN0210
     C           SELCT     ANDEQ'N'                                       LN0210
     C                     MOVE 'N'       SARMF                           LN0210
     C                     MOVE 'N'       SAROD                           LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      **    Subfile valid, proceed to updating                            LN0210
      *                                                                   LN0210
     C           *IN71     IFEQ '0'                                       LN0210
     C           *IN03     ANDEQ'0'                                       LN0210
     C           *IN12     ANDEQ'0'                                       LN0210
     C                     MOVE 'N'       ARCHF                           LN0210
     C                     MOVE 'Y'       UPDF                            LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Exit requested                                                   LN0210
      *                                                                   LN0210
     C           *IN03     IFEQ '1'                                       LN0210
     C                     MOVE 'Y2U9999' RTCD                            LN0210
     C                     EXSR END                                       LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
      ** Previous screen requested                                        LN0210
      *                                                                   LN0210
     C           *IN12     IFEQ '1'                                       LN0210
     C                     MOVE 'Y'       RSFXF                           LN0210
     C                     MOVE 'N'       ARCHF                           LN0210
     C                     SETOF                     12                   LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     ENDSR                                          LN0210
      ********************************************************************LN0210
      /EJECT
     C********************************************************************
     C*
     C***S/R*ENQDEL*-*TO*PERFORM*DELETION*PROCESSING**********************LN0210
     C*  S/R DELETE - TO PERFORM DELETION PROCESSING                      LN0210
     C*  CALLED FROM MAIN
     C*  CALLS
     C*
     C********************************************************************
      *
     C********** ENQDEL    BEGSR                                          LN0210
     C           DELETE    BEGSR                                          LN0210
     C*
     C                     SETON                     54
     C*
     C           SDRQD     IFEQ 'Y'
     C                     SETON                     51
     C                     ELSE
     C                     SETOF                     51
     C                     END
     C*
     C***********ACTN      IFEQ 'D'                                       LN0210
     C                     SETON                     56
     C******************** END                                            LN0210
     C*
     C                     CALL 'ZA0250'
     C                     WRITEFC0220C1
     C                     EXFMTFC0220F0
     C*
      ** DISPLAY SCREEN UNTIL VALID COMMAND KEY PRESSED
     C*
     C           *IN03     DOWEQ'0'
     C           *IN10     ANDEQ'0'
     C           *IN12     ANDEQ'0'
     C*
     C                     CALL 'ZA0250'
     C*
     C********** ACTN      IFEQ 'D'                                       LN0210
     C                     CALL 'FCC0240'
     C***********          PARM 'RCF0021' MSGID                           LN1186
     C                     PARM 'RCF1021' MSGID                           LN1186
     C                     WRITEFC0220C1
     C                     EXFMTFC0220F0
     C******************** ELSE                                           LN0210
     C******************** SETON                     12                   LN0210
     C******************** END                                            LN0210
     C*
     C                     END
      *
      ****Delete*Periodic*Request*Record*****************************     LN0210
      **  Delete Output Instruction Record                                LN0210
      *
     C           *IN10     IFEQ '1'
     C           SRNAM     CHAINFCMOUTL0             86
     C  N86                DELET@D2REIZ
      *                                                                   LN0210
      **  Delete Archiving Option Records if modules present              LN0210
      *                                                                   LN0210
     C           *IN52     IFEQ '1'                                       LN0210
     C           *IN53     OREQ '1'                                       LN0210
      *                                                                   LN0210
     C           ARCPKP    SETLLMAARCPL2                                  LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
      *                                                                   LN0210
     C           *IN88     DOWEQ'0'                                       LN0210
     C                     DELET@ARCPAP                                   LN0210
     C           ARCPKP    READEMAARCPL2                 88               LN0210
     C                     END                                            LN0210
      *                                                                   LN0210
     C                     END                                            LN0210
      *
      **  Update file controls
      *
     C                     MOVEL'FCMOUTPD'AHFLNM
     C           AHFLNM    CHAINSDFCTLL0             99
      *
      **  Database error if no record found for file
      *
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'SDFCTLL0'DBFILE
     C                     MOVEL'FCMOUTPD'DBKEY            *************
     C                     Z-ADD5         DBASE            * DBERR 005 *
     C                     EXSR DBERR                      *************
     C                     END
      *
      **  Update number of deletions
      *
     C                     ADD  1         AHNODL
     C                     SUB  1         AHNORC
      *
      **  Update file control record
      *
     C                     UPDAT@AHREAU
      *                                                                   LN0210
      **     Issue Commit                                                 LN0210
      *                                                                   LN0210
     C                     TIME           MTIME                           LN0210
     C           CMTTXT    COMIT                                          LN0210
      *
     C                     END
     C*
     C** IF EXIT REQUESTED, TERMINATE PROGRAM
     C*
     C           *IN03     IFEQ '1'
     C                     MOVE 'Y2U9999' RTCD
     C                     EXSR END
     C                     END
     C*
     C** IF PREVIOUS SCREEN REQUESTED, EXIT PROGRAM
     C*
     C           *IN12     IFEQ '1'
     C                     SETOF                     12
     C                     MOVE *BLANK    RTCD
     C                     EXSR END
     C                     END
      *
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R INIT2 - TO INITIALISE MAIN SCREEN
     C*  CALLED FROM MAIN
     C*  CALLS DBERR
     C*
     C********************************************************************
      *
     C           INIT2     BEGSR
      *
      **          Access Installation Control Data
      *
     C                     CALL 'AORCFR0'
     C                     PARM *BLANK    P@APRC             B:Return code
     C                     PARM '*FIRST'  P@APOP             B:Proc Optn
     C           P@RCF     PARM P@RCF     DSFDY              O:Rec format
      *
      **          IF no record is found
      **               Database error
      *
     C           P@APRC    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDRCFPD' DBFILE
     C                     MOVEL'RCF'     DBKEY            *************
     C                     Z-ADD2         DBASE            * DBERR 002 *
     C                     EXSR DBERR                      *************
     C                     END
      *
      **          Access Midas Modules Data
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANK    P@APRC  7          B:Return code
     C                     PARM '*FIRST'  P@APOP  7          I:Option
     C           P@MMOD    PARM P@MMOD    DSSDY              O:Rec format
      *
      **          IF no record is found
      **               Database error
      *
     C           P@APRC    IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDMMODPD'DBFILE
     C                     MOVEL'MMOD'    DBKEY            *************
     C                     Z-ADD3         DBASE            * DBERR 003 *
     C                     EXSR DBERR                      *************
     C                     END
      *
      **          Set Screen conditioning indicators:
      **               Do not display archiving on Microfiche if MF
      **               not present
      *
     C           BGMFIC    COMP 'Y'                      52
      *
      **               Do not display archiving on Optical Disk if OD
      **               not present
      *
     C           BGOPDK    COMP 'Y'                      53
      *
     C                     MOVE RUNA      SRUNA
     C                     MOVE JBNM      SWSID
      *
      **               Do not Display Distribution list fields if
      **               Office is not present
      *
     C           D0OFPR    COMP 'Y'                      50
      *
     C           JUSR      CHAINMUSER                84
      *
      **          IF no record is found
      **               Database error
      **          ENDIF
      *
     C           *IN84     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'MUSER'   DBFILE
     C                     MOVELJUSR      DBKEY            *************
     C                     Z-ADD6         DBASE            * DBERR 006 *
     C                     EXSR DBERR                      *************
     C                     END
      *
     C           *LIKE     DEFN MUREP     PMUREP
     C           *LIKE     DEFN OUTQ      POUTQ
      *
     C                     MOVE MUREP     PMUREP
     C                     MOVE OUTQ      POUTQ
      *
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R INIT1 - TO INITIALISE PROGRAM VARIABLES
     C*  CALLED FROM MAIN
     C*  CALLS UPDOWS, ZBKDT, PFREQB
     C*
     C********************************************************************
     C           INIT1     BEGSR
     C*
     C           *LIKE     DEFN D2RNAM    RNAM
     C           *LIKE     DEFN D2TNLU    PTNLU
     C           *LIKE     DEFN SSOID     KSOID
      *
     C           SOIKEY    KLIST
     C                     KFLD           KSOID
      *                                                                   043702
      ** DISTRIBUTION LIST FILE KEY                                       043702
      *                                                                   043702
     C           KDLIST    KLIST                                          043702
     C                     KFLD           KDIST1  8                       043702
     C                     KFLD           KDIST2  8                       043702
      *                                                                   LN0210
      ** ARCHIVING FILE: PARTIAL KEY                                      LN0210
      *                                                                   LN0210
     C           ARCPKP    KLIST                                          LN0210
     C                     KFLD           SRNAM                           LN0210
     C                     KFLD           PSQN                            LN0210
      *                                                                   LN0210
      ** ARCHIVING FILE: KEY FOR CHAINING                                 LN0210
      *                                                                   LN0210
     C           ARCPK     KLIST                                          LN0210
     C                     KFLD           SRNAM                           LN0210
     C                     KFLD           PSQN                            LN0210
     C                     KFLD           SRSFX                           LN0210
      *                                                                   LN0210
      ** ARCHIVING FILE: KEY FOR CHAINING                                 LN0210
      *                                                                   LN0210
     C           ARCPKY    KLIST                                          LN0210
     C                     KFLD           SARID                           LN0210
     C                     KFLD           SRNAM                           LN0210
     C                     KFLD           PSQN                            LN0210
     C                     KFLD           SRSFX                           LN0210
      *                                                                   LN0210
     C           *LIKE     DEFN ECPSQN    PSQN                            LN0210
      *                                                                   LN0210
      ** ARCHIVING STORE IS UNDER SEQUENCE NUMBER '00'                    LN0210
      *                                                                   LN0210
     C                     MOVE '00'      PSQN                            LN0210
      *
      **          Access Dataarea RUNDAT
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'FC0220'  DBPGM
     C                     OUT  LDA
     C                     MOVEL'FC0220'  PGMN
     C                     MOVEL'FC'      MDID
     C                     MOVELJBNM      WSIDX
     C                     MOVE ACTN      ACTCDX
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C           DATF      COMP 'M'                      98
     C           MBIN      COMP 'Y'                      57
     C           ACTN      COMP 'I'                  5858
      *
     C                     MOVEL'*'       SPGMQ
     C                     CALL 'ZA0250'
     C                     SETON                     60
     C*
     C** ACCESS REPORT DESCRIPTION: DATABASE ERROR IF NONE FOUND
     C*
     C           RNAM      IFNE *BLANK
     C***********RNAM      CHAINFCREPTL1             79                   S01407
     C           RNAM      CHAINFCREPTJ0             79                   S01407
     C*
     C           *IN79     IFEQ '1'
     C           *LOCK     IN   LDA
     C***********          MOVEL'MUSER'   DBFILE                          S01407
     C***********          MOVELJUSR      DBKEY            *************  S01407
     C                     MOVEL'FCREPTJ0'DBFILE                          S01407
     C                     MOVELRNAM      DBKEY            *************  S01407
     C                     Z-ADD4         DBASE            * DBERR 004 *
     C                     EXSR DBERR                      *************
     C                     END
     C*
     C                     END
     C*
     C                     MOVE DXRDSC    SRDSC
     C*
      ** SET ON PROTECT SCREEN INDICATOR FOR ENQUIRE/DELETE
     C*
     C           ACTN      IFEQ 'E'
     C           ACTN      OREQ 'D'
     C                     SETON                     54
     C                     END
     C*
      ** SET ON ENABLE COMMAND 10 INDICATOR FOR DELETE
     C*
     C           ACTN      IFEQ 'D'
     C                     SETON                     56
     C                     END
      *
      **     If action code is insert, set screen defaults
      *
     C           ACTN      IFEQ 'I'
     C                     MOVE 'N'       SAROD
     C                     MOVE 'N'       SARMF
     C                     ELSE
      *
      **     If action is not insert, access current details from
      **      report requests file
     C*
     C           RNAM      CHAINFCMOUTL1             86
     C*
     C           *IN86     IFEQ '1'
     C***********          MOVE 'RCF0023' MSGID                           LN1186
     C                     MOVE 'RCF1023' MSGID                           LN1186
     C                     EXSR UPDOWS
     C                     ELSE
     C*
      **     Set up screen fields from report request record
     C*
     C                     MOVE RNAM      SRNAM
     C                     MOVE D2AROD    SAROD
     C                     MOVE D2DRQD    SDRQD
     C                     MOVE D2ARMF    SARMF
     C                     MOVE D2DFTO    SSOID
     C                     MOVE D2TNLU    PTNLU
     C                     MOVE D2DLS1    SDST1
     C                     MOVE D2DLS2    SDST2
     C                     MOVE D2USRO    SUSRO
     C                     MOVE D2DFTU    SUSRR
     C                     END
     C*
     C                     END
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R UPDOWS - TO DISPLAY SCREEN WHERE UPDATE BY ANOTHER W/S
     C*  CALLED FROM MAIN PROCESSING, INSAMD, INIT1
     C*  CALLS END
     C*
     C********************************************************************
     C*
     C           UPDOWS    BEGSR
     C*
     C                     CALL 'ZA0250'
     C                     CALL 'FCC0240'
     C                     PARM           MSGID
      *
     C                     WRITEFC0220C1
     C                     SETON                     54
     C*
      **      Alert the user
      **        and allow them to exit/return
     C*
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C                     EXFMTFC0220F0
     C                     END
     C*
     C           *IN03     IFEQ '1'
     C                     MOVE 'Y2U9999' RTCD
     C                     EXSR END
     C                     END
     C*
     C           *IN12     IFEQ '1'
     C                     MOVE *BLANK    RTCD
     C                     EXSR END
     C                     END
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R DBERR - TO PROCESS DATABASE ERRORS AND TERMINATE PROGRAM
     C*  CALLED FROM
     C*
     C********************************************************************
     C*
     C           DBERR     BEGSR
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     MOVE 'Y2U9999' RTCD
     C                     EXSR *PSSR
     C                     RETRN
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R END - TO TERMINATE PROGRAM
     C*  CALLED FROM
     C*
     C********************************************************************
     C*
     C           END       BEGSR
     C                     SETON                     LR
     C                     RETRN
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  S/R *PSSR - ERROR ROUTINE
     C*  CALLED FROM DBERR
     C*
     C********************************************************************
     C*
     C           *PSSR     BEGSR
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     MOVE 'Y2U9999' RTCD
     C                     DUMP
     C                     END
     C                     ENDSR
     C********************************************************************
      /EJECT
      /COPY ZSRSRC,ZTNLU1Z2
     O@ECREJU E                RELACP                                     LN0210
** CPY@
(c) Misys International Banking Systems Ltd. 2001
