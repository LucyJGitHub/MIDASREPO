      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD Exchange Rates Maintenance')
     A*****************************************************************
     A*                                                               *
     A*                                                               *
     A*  Midas - Dealing (FX) Module
     A*                                                               *
     A*   FD0140DD - EXCHANGE RATES MAINTENANCE.                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 01Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 104633             Date 15Jul96               *
     A*                 090199             Date 12Jul95               *
     A*                 S01319             DATE 19NOV91               *
     A*       AMEND NO. E23462             DATE 28SEP90               *
     A*                 S01253             DATE 17APR90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01319  -  Changes for Project Harry - add help              *
     A*  E23462  -  Remove all references to DRS.                     *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03
     A                                      CA12
     A  63                                  CF10
     A  40N73 66                            ROLLUP(50)
     A                                      MSGLOC(22)
     A          R FD0140S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R FD0140S0                  TEXT('MAINTENANCE SCREEN')
     A                                      SFLCTL(FD0140S1)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A N45                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A N66                                  CF08
     A N66                                  CF07
     A N66                                  CF15
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      ** Start of change CAAA03
     A          R FD0140H1                  TEXT('MAIN SCREEN HEADER')
     A                                      OVERLAY
     A                                      PUTOVR
      ** End of change CAAA03
     A                                  1  2'Ref FD0140'
      *                                                   START OF CHANGE E23462
     A**********************************1 30'MIDAS DEALING ROOM SYSTEM'
     A                                  1 26'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                      DSPATR(HI)
     A            DDTIME         6  0O  1 64TEXT('TIME')
     A                                      EDTWRD('0 .  .  ')
     A                                      OVRDTA
     A                                  1 73'WID'
     A            DDWID          3   O  1 77TEXT('WORKSTATION ID')
     A                                  3  2'Action'
     A            DDACTN         1   B  3 10TEXT('ACTION CODE')
     A                                      OVRDTA
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A                                  3 12'(I,A,D,E)'
     A                                  3 36'EXCHANGE RATES'
     A                                  5  6'Currency Code'
     A                                      DSPATR(HI)
     A            DDCCY     R        B  5 20REFFLD(CCY)
     A                                      OVRDTA
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  46                                  DSPATR(PR)
     A                                  5 31'Name'
     A            DDCCNM    R           5 37REFFLD(CCNM)
     A                                      OVRDTA
     A                                  6 70'Period'
     A                                  7 31'Low'
     A                                  7 52'High'
     A                                  7 69'Shortname'
     A            DDDSC1        15      8  6TEXT('PERIOD DESCRIPTION 1')
     A            DD01BI         8   B  8 35TEXT('BID RATE 1')
     A                                      OVRDTA
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A            DD01OF         8   B  8 56TEXT('OFFER RATE 1')
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDPDX1    R        B  8 71REFFLD(PRDX)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDDSC2        15      9  6TEXT('PERIOD DESCRIPTION 2')
     A            DD02BI         8   B  9 35TEXT('BID RATE 2')
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A                                      OVRDTA
     A            DD02OF         8   B  9 56TEXT('OFFER RATE 2')
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A                                      OVRDTA
     A            DDPDX2    R        B  9 71REFFLD(PRDX)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A                                      OVRDTA
     A                                 10  6'*** SPOT ***'
     A            DDSPT1    R     Y  B 10 25REFFLD(SPOT)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A            DDSPT2    R     Y  B 10 46REFFLD(SPOT)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A*
     A          R FD0140D0                  TEXT('MAIN SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A N66                                  CF08
     A N66                                  CF07
     A N66                                  CF15
      *                          Beginning of change       /S01319/
      *                                End of change       /S01319/
      ******                                        Start  /104633/
     A******                           23  2'F10 - CONFIRM DELETE'
     A                                 23  2'F10=Delete'
      ******                                        End    /104633/
     A  63                                  DSPATR(HI)
     A N63                                  DSPATR(ND)
     A                                      OVRATR
      ******                                        Start  /104633/
     A******                           24  1'F3-End F12-Previous Screen'
     A******                              +1'F8 & F7-Next & Previous Record'
      ** Start of change CAAA03
     A**********                       24  1'F3=Exit   F7=Prev Record   '                     CAAA03
     A**********                          +1'F8=Next Record   F12=Previous'                   CAAA03
     A                                 24  1'F3=Exit   12=Previous'
     A                                    +2'F7=Prev Record'
     A  66                                  DSPATR(ND)
     A                                      OVRATR
     A                                    +2'F8=Next Record'
      ** End of change CAAA03
      ******                                        End    /104633/
     A  66                                  DSPATR(ND)
     A                                      OVRATR
      ******                                        Start  /104633/
     A******                              +1'F15-Select Screen'
     A                                    +1'F15=Select Screen'
      ******                                        End    /104633/
     A  66                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A          R FD0140S3                  TEXT('SELECT SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8 08TEXT('SELECT OPTION')
     A***                                   VALUES(' ' 'X')
     A            DDCCY     R           8 21REFFLD(CCY)
     A            DDCCNM    R           8 56REFFLD(CCNM)
     A*
     A          R FD0140S2                  TEXT('SELECT SCREEN')
     A                                      SFLCTL(FD0140S3)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A  40                                  SFLDSP
     A  41                                  SFLCLR
     A  40                                  SFLDSPCTL
     A  73                                  SFLEND
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'Ref FD0140A'
      *                                                   START OF CHANGE E23462
     A**********************************1 30'MIDAS DEALING ROOM SYSTEM'
     A                                  1 26'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 64TEXT('TIME')
     A                                      EDTWRD('0 .  .  ')
     A                                      OVRDTA
     A                                  1 73'WID'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 36'EXCHANGE RATES'
     A                                  4 39'ENQUIRY'
     A                                  6 05'Select'
     A                                  6 21'Currency Code'
     A                                  6 56'Currency Name'
     A*
     A          R FD0140D1                  TEXT('SELECT SCREEN FOOTER ')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A                                 23  2'Rollup/Rolldown for further'
     A                                    +1'Records'
      ******                                          Start  /104633/
     A******                           24  1'F3-End F12-Previous Screen'
     A                                 24  1'F3=Exit   F12=Previous'
      ******                                            End  /104633/
     A*
     A          R FD0140S5                  TEXT('SUBFILE FOR PERIODS')
     A                                      SFL
     A            DDDSC3        15     11  6TEXT('PERIOD DESCRIPTION')
     A            DD03BI         8   B 11 35TEXT('BID RATE 3')
     A  30                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A            DD03OF         8   B 11 56TEXT('OFFER RATE')
     A  31                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A            DDPDX3    R        B 11 71REFFLD(PRDX)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A*
     A          R FD0140S4                  TEXT('CONTROL FOR PERIODS')
     A                                      SFLCTL(FD0140S5)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(10)
     A N43                                  SFLDSP
     A  43                                  SFLCLR
     A  10                                  SFLEND
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
