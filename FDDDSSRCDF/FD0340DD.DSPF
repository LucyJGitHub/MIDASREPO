      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD Deals Number Maintenance Program')
     A****************************************************************
     A*                                                              *
     A*                                                              *
     A*  Midas - Standing Data Module.
     A*                                                              *
     A*   DSPF/FD0340DD - DEAL NUMBER MAINTENANCE SCREEN.            *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 104633             Date 18JUN96               *
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*                 S01319             DATE 26SEP91              *
     A*       AMEND NO. E23462             DATE 01OCT90              *
     A*                 S01253             DATE 17APR90              *
     A*                                                              *
     A*                                                              *
     A****************************************************************
     A*                                                               *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01319  -  Upgrade to R10 standards.                         *
     A*  E23462  -  Remove all references to DRS.                     *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                               *
     A*****************************************************************
     A**  INDICATOR FUNCTION SUMMARY
     A*
     A*       10       SUBFILE END CONDITIONING INDICATOR
     A*
     A*       11       SCREEN BUILD 1 - INITIAL SCREEN.
     A*       12       SCREEN BUILD 2 - ENQUIRE SCREEN.
     A*       13       SCREEN BUILD 3 - AMEND SCREEN.
     A*       14       SCREEN BUILD 4 - CONFIRM AMENDMENT SCREEN.
     A*       15       SCREEN BUILD 5 - AMEND WARNING NUMBER SCREEN.
     A*
     A*       20       INPUT ERROR ON SCREEN BUILD 1 - ACTION CODE.
     A*       21       INPUT ERROR ON SCREEN BUILD 3 - RESERVE DEAL NO.
     A*       22       INPUT ERROR ON SCREEN BUILD 5 - WARNING DEAL NO.
     A*
     A*       KA       CA03 INDICATOR
     A*       KB       CA12 INDICATOR
     A*       KN       CA14 INDICATOR
     A*
     A*       80
     A*       |        USED IN STANDARD SUBROUTINES
     A*       89
     A*
     A*          U7    SET ON WITH U8 IF A DATABASE ERROR OCCURS.
     A*          U8    FILE OUT OF BALANCE IF ON ON ITS OWN.
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*            CA03 - Exit program
     A                                      CA03
     A*            CA12 - Return to previous Screen.
     A                                      CA12
     A*            CA14 - Confirm new reserve Deal No.
     A  14                                  CA14
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
      *                          Beginning of change       /S01319/
      *                                End of change       /S01319/
     A*
     A**  MESSAGE SUBFILE FORMAT
     A*                                                             /*S01319*/
     A**********R FD0340D0                  TEXT('MESSAGE SUBFILE')
     A          R FD0340D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                                             /*S01319*/
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A*
     A**  MAIN SCREEN FORMAT - ALSO CONTROLS MESSAGE SUBFILE FORMAT
     A          R FD0340D1                  TEXT('SUBFILE CONTROL FMT')
     A                                      SFLCTL(FD0340D0)
     A                                      SFLSIZ(4)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  10                                  SFLEND
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A*
     A                                      PUTOVR
     A                                      BLINK
     A                                      OVERLAY
     A*
     A            PGMQ                      SFLPGMQ
     A*
     A                                  1  2'Ref:'
     A                                  1  7'FD0340'
      *                                                   START OF CHANGE E23462
     A**********************************1 28'MIDAS DEALING ROOM SYSTEM'   E23462
     A                                  1 26'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                      DSPATR(HI)
     A            DDTIME         6  0O  1 63EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A                                  1 72'WID:'
     A            DDWID     R     A  O  1 77REFFLD(WSID)
     A*
     A                                  3  2'Action'
     A            DDACTN         1A  B  3 10
     A                                      OVRDTA
     A  11                                  DSPATR(UL)
     A  11
     AO 14                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A N11                                  DSPATR(PR)
     A                                  3 12'( ,A, ,E)'
     A                                  3 28'DEAL NUMBER MAINTENANCE'
     A*
     A                                  5 16'CURRENT'
     A                                      OVRATR
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(ND)
     A                                  5 46'DESIRED'
     A                                      OVRATR
       N11
     AAN12                                  DSPATR(UL)
     A  11
     AO 12                                  DSPATR(ND)
     A                                  7 11'Next Deal Number'
     A                                      OVRATR
     A  11                                  DSPATR(ND)
     A            DDNDLN    R        O  7 28REFFLD(DLNO)
     A N11                                  OVRDTA
     A  11                                  OVRATR
     A  11                                  DSPATR(ND)
     A*
     A                                  9 11'Reserve Number'
     A                                      OVRATR
     A  11                                  DSPATR(ND)
     A            DDRDLN    R        O  9 28REFFLD(DLNO)
     A N11                                  OVRDTA
     A  11                                  OVRATR
     A  11                                  DSPATR(ND)
     A                                  9 41'Reserve'
     A                                      OVRATR
     A  11
     AO 12                                  DSPATR(ND)
     A            DDARDN    R     Y  B  9 51REFFLD(DLNO)
     A N11
     AAN12                                  OVRDTA
     A  11
     AO 12                                  OVRATR
     A  11
     AO 12                                  DSPATR(ND)
     A  13                                  DSPATR(UL PC)
     A N13                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A*
     A                                 11 11'Warning Number'
     A                                      OVRATR
     A  11                                  DSPATR(ND)
     A            DDWDLN    R        O 11 28REFFLD(DLNO)
     A N11                                  OVRDTA
     A  11                                  OVRATR
     A  11                                  DSPATR(ND)
     A                                 11 41'Warning'
     A                                      OVRATR
     A N15                                  DSPATR(ND)
     A            DDAWDN    R     Y  B 11 51REFFLD(DLNO)
     A  15                                  OVRDTA
     A N15                                  OVRATR
     A N15                                  DSPATR(ND PR)
     A  15                                  DSPATR(UL PC)
     A  22                                  DSPATR(RI)
     A*
     A** COMMAND KEY OUTPUT FORMAT
     A          R FD0340D2                  TEXT('FORMAT TO OUTPUT+
     A                                       CMD KEY DESCRIPTIONS')
     A                                      BLINK
     A                                      PUTOVR
     A                                      OVERLAY
      *                          Beginning of change       /S01319/
      *                                End of change       /S01319/
      *******                                         Start  /104633/
     A*******                          24  2'F3-End F12-Previous Screen'
     A*******                             +1'F14-Confirm Amendment'
     A                                 24  2'F3=Exit   F12=Previous  '
     A                                    +1'F14=Confirm Amendment'
      *******                                         End    /104633/
     A                                      OVRATR
     A N14                                  DSPATR(ND)
