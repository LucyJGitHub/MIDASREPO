      *****************************************************************
/*STD *  DSPFBASE
/*EXI *  TEXT('Midas ER Deal Type/Subtype Extension Display')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting Luxembourg                        *
      *                                                               *
      *  FD0652DF - Deal Type/Sub-Type Extension Display              *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A                                      CF12
      *
      *----------------------------------------------------------------
      *
     A          R FD0652F6
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
      *
     A            SJOB          10A  O  2  2
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                      OVRATR
     A                                  2 26'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
     A                                  4  2'Floating Rate Note Indicator......-
     A                                      ... :  '
     A                                      OVRATR
     A            #0FRNI         1A  B  4 44OVRDTA
     A                                      OVRATR
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  26N15                               DSPATR(PC)
     A                                  5  2'Mobilisable.......................-
     A                                      ... :  '
     A                                      OVRATR
     A            #0MOBC         1Y 0B  5 44OVRDTA
     A                                      OVRATR
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  23N15                               DSPATR(PC)
     A                                  6  2'Possibility of Mobilisation.......-
     A                                      ... :  '
     A                                      OVRATR
     A            #0PMOB         1Y 0B  6 44OVRDTA
     A                                      OVRATR
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  24N15                               DSPATR(PC)
     A                                  7  2'Discount type.....................-
     A                                      ... :  '
     A                                      OVRATR
     A            #0DIST         1Y 0B  7 44OVRDTA
     A                                      OVRATR
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  25N15                               DSPATR(PC)
     A                                  8  2'Depot type........................-
     A                                      ... :  '
     A                                      OVRATR
     A            #0DEPT         1Y 0B  8 44OVRDTA
     A                                      OVRATR
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PR)
     A  26N15                               DSPATR(PC)
      *
     A                                 23  2'F12=Previous'
      *
      *----------------------------------------------------------------
      *
     A          R #MSGRCD                   TEXT('Program messages')
      *
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            WWPGM                     SFLPGMQ
      *
      *----------------------------------------------------------------
      *
     A          R #MSGCTL                   TEXT('Program messages')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(50)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  69
     AON69                                  SFLEND
     A            WWPGM                     SFLPGMQ
