/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD TOF - Edit deal Types/Subtypes details')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/FD0652R# - Edit Deal Types/Subtype details              *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD037467           Date 25May16               *
      *                 AR795970           Date 05Jul11               *
      *                 AR787620           Date 10Jun11               *
      *                 CER059             Date 19Jul10               *
      *                 BUG24306           Date 11Jun09               *
      *                 BUG24223           Date 06Jun09               *
      *                 BUG24222           Date 06Jun09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG15930           Date 27Feb08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 BUG10769           Date 07Mar06               *
      *                 CER001             Date 25Apr05               *
      *                 ULX003             Date 04Sep96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD037467 - SG-Deal Type/Sub Types Enquiry - Editable field   *
      *             in enquiry screen                                 *
      *  AR795970 - Change §24c to plain 24c                          *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,FDH00013                                 *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG24306 - Enquiry of New Field should show correct          *
      *             symbol "§"                                        *
      *  BUG24223 - Enquiry Screen Not working                        *
      *  BUG24222 - Enquiry for New Field KWG not available on screen *
      *  BUG15930 - Unknown field displayed on MidasPlus              *
      *  CSD031 - Enhanced Centralised Static Data                    *
      *  BUG10769 - IAS and Global fields not available for input     *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX003 - TOF/Midas Interface                                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A**Start of change CER001
     A**********                            HELP                                              CER001
     A**********                            HLPDOC(H416@000 SDINS ABSENG/DBAR3)               CER001
     A**End of change CER001
     A          R #RCDKEY                   TEXT('(c) Finastra International +
     A                                      Limited 2005')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A                                      CF05(05 'Reset.')
     A**                                    HELP(25 'Help.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1DSTC')
     A*.........................................................................
     A**Start of change CER001
     A**********H                           HLPDOC(H416@005 SDINS ABSENG/DBAR3)               CER001
     A**********                            HLPARA(04 01 04 23)                               CER001
     A**End of change CER001
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      ** Start of change CSD031
     A**********                        2 29'Edit Deal Type/Sub-Type'
      **Start of change BUG24223
     A**56******                                                                            BUG24223
     A*ON51*****                        2 29'Edit Deal Type/Sub-Type'                       BUG24223
     A  56
     AON51                              2 28'Deal Type/Sub-Type Enquiry'
      **End of change BUG24223
      ** End of change CSD031
     A                                      DSPATR(UL HI)
      ** Start of change CSD031
     A N56 51                               DSPATR(ND)
     A                                      OVRATR
     A N56 51                           2 29'Edit Central Deal Type/Sub-Type'
     A                                      DSPATR(UL HI)
     A  56                                  DSPATR(ND)
     A                                      OVRATR
      ** End of change CSD031
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  2'Deal Type Code:'
     A            #1DTPE         2   B  4 22TEXT('Deal Type Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      **Start of change CER001
      *
     A  37                              4 42'Class:'
     A  37        #1DCLA         4   B  4 49TEXT('Class')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
      **End of change CER001
      *
     A                                  5  2'Deal Sub-type Code:'
     A            #1DSTC         2   B  5 22TEXT('Deal Sub-type Code')
     A                                      CHECK(FE)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A                                      CF05(05 'Reset.')
     A**                                    HELP(25 'Help.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1DSTN')
     A                                      SETOF(32 'Invalid: #1FUID')
     A                                      SETOF(33 'Invalid: #1FLID')
      **Start of Change BUG10769
     A                                      SETOF(38 'Invalid: #1PRDC')
     A                                      SETOF(39 'Invalid: #1INSC')
     A                                      SETOF(40 'Invalid: #1FBRT')
     A                                      SETOF(41 'Invalid: #1OURY')
     A                                      SETOF(42 'Invalid: #1THRY')
     A                                      SETOF(43 'Invalid: #1YLDM')
     A                                      SETOF(44 'Invalid: #1ACAI')
     A                                      SETOF(45 'Invalid: #1ORFY')
     A                                      SETOF(46 'Invalid: #1THFY')
     A                                      SETOF(47 'Invalid: #1FYCM')
     A                                      SETOF(48 'Invalid: #1OFCY')
     A                                      SETOF(49 'Invalid: #1TFCY')
     A                                      SETOF(50 'Invalid: #1FCCM')
      **End of Change BUG10769
      *
     A*.........................................................................
     A**Start of change CER001
     A**********H                           HLPDOC(H416@005 SDINS ABSENG/DBAR3)               CER001
     A**********                            HLPARA(05 01 05 35)                               CER001
     A**********H                           HLPDOC(H416@010 SDINS ABSENG/DBAR3)               CER001
     A**********                            HLPARA(07 01 07 63)                               CER001
     A**********H                           HLPDOC(H416@015 SDINS ABSENG/DBAR3)               CER001
     A**********                            HLPARA(08 01 08 34)                               CER001
     A**********H                           HLPDOC(H416@020 SDINS ABSENG/DBAR3)               CER001
     A**********                            HLPARA(09 01 09 34)                               CER001
     A**End of change CER001
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
      ** Start of change CSD031
     A**********                        2 32'Edit Deal Type/SubType'
      **Start of change BUG24223
     A**56******                                                                            BUG24223
     A*ON51*****                        2 32'Edit Deal Type/SubType'                        BUG24223
     A  56
     AON51                              2 31'Deal Type/Sub-Type Enquiry'
      **End of change BUG24223
      ** End of change CSD031
     A                                      DSPATR(HI)
      ** Start of change CSD031
     A N56 51                               DSPATR(ND)
     A                                      OVRATR
     A N56 51                           2 29'Edit Central Deal Type/Sub-Type'
     A                                      DSPATR(HI)
     A  56                                  DSPATR(ND)
     A                                      OVRATR
      ** End of change CSD031
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1PRCS        10   H      TEXT('Midas Export Value PRCS')
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  5  2'Deal Type Code................:'
     A            #1DTPE         2      5 34TEXT('Deal Type Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
      **Start of change CER001
      *
     A  37                              5 42'Class....................:'
     A  37        #1DCLA         4      5 69TEXT('Class')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *
      **End of change CER001
      *
      **Start of change BUG10769
     A**********                        7  2'Deal Sub-type Code............:'
     A**********  #1DSTC         2      7 34TEXT('Deal Sub-type Code')
     A                                  6  2'Deal Sub-type Code............:'
     A            #1DSTC         2A  O  6 34TEXT('Deal Sub-type Code')
      **End of change BUG10769
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      **Start of change BUG10769
     A**********                        9  2'Description...................:'
     A**********  #1NARR        35   B  9 34TEXT('Deal Sub-type name')
     A                                  8  2'Description...................:'
     A            #1NARR        35A  B  8 34TEXT('Deal Sub-type name')
      **End of change BUG10769
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
      ** Start of change CSD031
     A*N32N35***                            DSPATR(UL HI)
     A**35******                            DSPATR(PR)
     A N32N34                               DSPATR(UL HI)
     A  34                                  DSPATR(PR)
      ** End of change CSD031
     A N25                                  OVRATR
      **Start of change BUG10769
     A**********                       11  2'Instrument type...............:'
     A**********  #1INNR         3   B 11 34TEXT('Instrument type')
     A  68                              9  2'Instrument type...............:'
     A            #1INNR         3A  B  9 34TEXT('Instrument type')
      **End of change BUG10769
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33N35                               DSPATR(UL HI)
      **Start of Change BUG10769
     A**35******                            DSPATR(PR)
     A N68
     AO 35                                  DSPATR(PR)
     A N68                                  DSPATR(ND)
      **End of Change BUG10769
     A N25                                  OVRATR
      **Start of change BUG10769
     A  60                             11  2'Plato Pricing Group........:'
     A            #1PLDP         5   B 11 31TEXT('Pricing Group')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A N35                                  DSPATR(UL HI)
     A N60
     AO 35                                  DSPATR(PR)
     N N60                                  DSPATR(ND)
     A N25                                  OVRATR
     A  60                             12  2'Plato Reporting Group......:'
     A            #1PLDR         8   B 12 31TEXT('Reporting Group')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A N35                                  DSPATR(UL HI)
     A N60
     AO 35                                  DSPATR(PR)
     N N60                                  DSPATR(ND)
     A N25                                  OVRATR
     A  60                             13  2'Plato Credit Rating Code...:'
     A            #1PLCR         4   B 13 31TEXT('Credit Rating Code')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A N35                                  DSPATR(UL HI)
     A N60
     AO 35                                  DSPATR(PR)
     N N60                                  DSPATR(ND)
     A N25                                  OVRATR
     A  61                             11 42'Product Code........:'
     A            #1PRDC        10   B 11 64TEXT('Product Code')
     A N25                                  OVRDTA
      ** Start of Change CSD031
     A**38******                            DSPATR(RI PC)
     A*N38N35***                            DSPATR(UL HI)
     A*N61******
     A*O*35*****                             DSPATR(PR)
     A*N61******                            DSPATR(ND)
     A  38N51
     AO 38 51N56                            DSPATR(RI PC)
     A N38N35N51
     AON35
     AAN56 51N38                            DSPATR(UL HI)
     A N61
     AO 35
     AO 56 51                               DSPATR(PR)
     A N61N51                               DSPATR(ND)
      ** End of Change CSD031
     A N25                                  OVRATR
     A  61                             12 42'Instrument Code.....:'
     A            #1INSC        10   B 12 64TEXT('Instrument Code')
     A N25                                  OVRDTA
      ** Start of Change CSD031
     A**39*******                           DSPATR(RI PC)
     A*N39N35****                           DSPATR(UL HI)
     A*N61*******
     A*O*35******                            DSPATR(PR)
     A*N61*******                           DSPATR(ND)
     A  39N51
     AO 39 51N56                            DSPATR(RI PC)
     A N39N35N51
     AON35
     AAN56 51N39                            DSPATR(UL HI)
     A N61
     AO 35
     AO 56 51                               DSPATR(PR)
     A N61N51                               DSPATR(ND)
      ** End of Change CSD031
     A N25                                  OVRATR
     A  63                             15  2'Fix Base Rate..............:'
     A            #1FBRT         1   B 15 31TEXT('Fix Base Rate')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A  62
     AAN40N35 63                            DSPATR(UL HI)
     A N63
     AO 35                                  DSPATR(PR)
     A N63                                  DSPATR(ND)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,FDH00013
     A  64                             16  2'Buy/Our Yield Curve........:'
     A            #1OURY         5   B 16 31TEXT('Buy/Our Yield Curve')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41N35                               DSPATR(UL HI)
     A N64
     AO 35                                  DSPATR(PR)
     A N64                                  DSPATR(ND)
     A N25                                  OVRATR
     A  64                             17  2'Sell/Their Yield Curve.....:'
     A            #1THRY         5   B 17 31TEXT('Sell/Their Yield Curve')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42N35                               DSPATR(UL HI)
     A N64
     AO 35                                  DSPATR(PR)
     A N64                                  DSPATR(ND)
     A N25                                  OVRATR
     A  64                             18  2'Yield Curve Mandatory......:'
     A            #1YLDM         1   B 18 31TEXT('Yield Curve Mandatory')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43N35                               DSPATR(UL HI)
     A N64
     AO 35                                  DSPATR(PR)
     A N64                                  DSPATR(ND)
     A N25                                  OVRATR
     A  65                             15 39'Amortised Cost..............:'
     A            #1ACAI         1   B 15 69TEXT('Amortised Cost')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44N35                               DSPATR(UL HI)
     A N65
     AO 35                                  DSPATR(PR)
     A N65                                  DSPATR(ND)
     A N25                                  OVRATR
     A  66                             16 39'Buy/Our Fwd Yield Curve.....:'
     A            #1ORFY         5   B 16 69TEXT('Buy/Our Fwd Yield Curve')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45N35                               DSPATR(UL HI)
     A N66
     AO 35                                  DSPATR(PR)
     A N66                                  DSPATR(ND)
     A N25                                  OVRATR
     A  66                             17 39'Sell/Their Fwd Yield Curve..:'
     A            #1THFY         5   B 17 69TEXT('Sell/Their Fwd Yield Curve')
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI PC)
     A N46N35                               DSPATR(UL HI)
     A N66
     AO 35                                  DSPATR(PR)
     A N66                                  DSPATR(ND)
     A N25                                  OVRATR
     A  66                             18 39'Fwd Yield Curve Mandatory...:'
     A            #1FYCM         1   B 18 69TEXT('Fwd Yield Curve Mandatory')
     A N25                                  OVRDTA
     A  47                                  DSPATR(RI PC)
     A N47N35                               DSPATR(UL HI)
     A N66
     AO 35                                  DSPATR(PR)
     A N66                                  DSPATR(ND)
     A N25                                  OVRATR
     A  67                             19  2'Buy/Our Fwd Cmt Yld Curve..:'
     A            #1OFCY         5   B 19 31TEXT('Buy/Our Fwd Cmt Yld Curve')
     A N25                                  OVRDTA
     A  48                                  DSPATR(RI PC)
     A N48N35                               DSPATR(UL HI)
     A N67
     AO 35                                  DSPATR(PR)
     A N67                                  DSPATR(ND)
     A N25                                  OVRATR
      *
      ** Start of Change CSD031
      *
     A  51N57N56                       19 39'Accept to Zone..............:'
     A  51N57 56                       19 39'Central Data................:'
      **Start of change BUG24222
      **Start of change BUG24306
     A**77******                       19 39'Subject to @24C.............:'                 BUG24306
     A**********  #1K24C         1   B 19 69TEXT('Subject to @24C Flag')                    BUG24306
      **Start of change AR795970
     A**77******                       19 39'Subject to §24c.............:'                 AR795970
     A**********  #1K24C         1   B 19 69TEXT('Subject to §24c Flag')                    AR795970
     A  77                             19 39'Subject to 24c..............:'
     A            #1K24C         1   B 19 69TEXT('Subject to 24c Flag')
      ** Start of Change MD037467
     A N77                                  DSPATR(ND)
     AO 35                                  DSPATR(PR)
     A N25                                  OVRATR
      **End of change MD037467
      **End of change AR795970
      **End of change BUG24306
      *
      ** Start of Change BUG15930
      *
     A**********  #1ACZN         1   B 19 69TEXT('Accept to Zone')                          BUG15930
     A  51N57N56  #1ACZN         1   B 19 69TEXT('Accept to Zone')
      *
      ** End of Change BUG15930
      *
     A N25                                  OVRDTA
     A  52                                  DSPATR(RI PC)
     A N52N35N54                            DSPATR(UL HI)
     A  57
     A  54
     AO 35
     AO 56                                  DSPATR(PR)
     A N51
     AO 58                                  DSPATR(ND)
     A N25                                  OVRATR
      *
      ** End of Change CSD031
      *
      **End of change BUG24222
     A  67                             20  2'Sell/Thr Fwd Cmt Yld Curve.:'
     A            #1TFCY         5   B 20 31TEXT('Sell/Thr Fwd Cmt Yld Curve')
     A N25                                  OVRDTA
     A  49                                  DSPATR(RI PC)
     A N49N35                               DSPATR(UL HI)
     A N67
     AO 35                                  DSPATR(PR)
     A N67                                  DSPATR(ND)
     A N25                                  OVRATR
      *
      **Start of Change CSD031
      *
     A  51                             20 39'Effective Date..............:'
     A  51        #1EFDT         6M  B 20 69TEXT('Effective Date')
     A N25                                  OVRDTA
     A  53                                  DSPATR(RI PC)
     A N53N35N55                            DSPATR(UL HI)
     A  35
     AO 55                                  DSPATR(PR)
     A N51                                  DSPATR(ND)
     A N25                                  OVRATR
      *
      **End of Change CSD031
      *
     A  67                             21  2'Fwd Cmt Yld Curve Mandatory:'
     A            #1FCCM         1   B 21 31TEXT('Fwd Cmt Yld Curve Mandatory')
     A N25                                  OVRDTA
     A  50                                  DSPATR(RI PC)
     A N50N35                               DSPATR(UL HI)
     A N67
     AO 35                                  DSPATR(PR)
     A N67                                  DSPATR(ND)
     A N25                                  OVRATR
      **End of Change BUG10769
      *
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1 - ACT')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
