     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD Forward rates details browser screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing ILE Module                                   *
     A*                                                               *
     A*  FDFWRTBDF - Forward Rates Details - Browse                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CAP059  *CREATE    Date 12Oct00               *
     A*  Prev Amend No. XXXXXX             Date DDMMMYY               *
     A*                                                               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP059 - Conversion of Forward Rates into Modular APIs       *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      MSGLOC(22)
     A                                      INDARA
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R FDFWRTM1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FDFWRTM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(FDFWRTM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A* -------------------------------------------------------------------*
     A* TRANSACTION NUMBER SUBFILE
     A* -------------------------------------------------------------------*
     A          R FDFWRTS1                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  6  2
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDCCY          3A  O  6 10
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDYS1         5A  O  6 20
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDRTE1        14A  O  6 27
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDYS2         5A  O  6 43
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDRTE2        14A  O  6 49
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSFW2         1A  O  6 65
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * TRANSACTION NUMBER SUBFILE CONTROL                               *
     A* -------------------------------------------------------------------*
     A*
     A          R FDFWRTS0                  SFLCTL(FDFWRTS1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FDFWRTBDF'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 30'Forward Rates Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Select'
     A                                  5 10'Currency'
     A                                  5 20'Days'
     A                                  5 27'Rate'
     A                                  5 43'Days'
     A                                  5 49'Rate'
     A                                  5 65'Sign'
     A*           SBTRNN         6   B  6 13OVRDTA
     A*                                     DSPATR(UL HI)
     A*           SBTREF        10   B  6 22OVRDTA
     A*                                     DSPATR(UL HI)
     A*
     A* -------------------------------------------------------------------*
     A* FRONT OFFICE ID SUBFILE
     A* -------------------------------------------------------------------*
     A*
     A          R FDFWRTS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  6  2
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFRNT        20A  O  6  9
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDCCY          3A  O  6 30
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDYS1         5A  O  6 34
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDRTE1        14A  O  6 40
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDDYS2         5A  O  6 55
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDRTE2        14A  O  6 61
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSFW2         1A  O  6 76
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * FRONT OFFICE ID SUBFILE CONTROL                                  *
     A* -------------------------------------------------------------------*
     A*
     A          R FDFWRTS2                  SFLCTL(FDFWRTS3)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FDFWRTBDF'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 30'Forward Rates Enquiry'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Select'
     A                                  5  9'Front Office ID'
     A                                  5 30'Ccy'
     A                                  5 34'Days'
     A                                  5 40'Rate'
     A                                  5 55'Days'
     A                                  5 61'Rate'
     A                                  5 76'Sign'
      *
      * -------------------------------------------------------------------*
      * SELECT SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R FDFWRTF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 23  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
