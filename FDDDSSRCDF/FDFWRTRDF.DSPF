     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD Forward rates repair browser screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Dealing ILE Module                                   *
     A*                                                               *
     A*  FDFWRTRDF - Invalid Forward Rates Details Browse             *
     A*              ("Repair Function")                              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CAP059  *CREATE    Date 17Oct00               *
     A*  Prev Amend No.                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP059 - Conversion of Forward Rates into Modular APIs       *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R FDFWRTS1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDOPT          1A  B  6  2TEXT('SELECT OPTION')
     A                                      DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(UL)
     A            DDACTN         1   O  6  6DSPATR(HI)
     A            DDFOTRANID    20   O  6 10DSPATR(HI)
     A            DDCCY          3A  O  6 31DSPATR(HI)
     A            DDDYS1         5A  O  6 35DSPATR(HI)
     A            DDRTE1        14A  O  6 41DSPATR(HI)
     A            DDDYS2         5A  O  6 56DSPATR(HI)
     A            DDRTE2        14A  O  6 62DSPATR(HI)
     A            DDSFW2         1A  O  6 77DSPATR(HI)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDBRSN    R        H      REFFLD(BRSN)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FDFWRTS0                  SFLCTL(FDFWRTS1)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FDFWRTRPB'
     A                                  1 30'Invalid Forward Rates Enquiry'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  2'Sel'
     A                                  5  6'Act'
     A                                  5 10'Front Office Id.'
     A                                  5 31'Ccy'
     A                                  5 35'Days'
     A                                  5 41'Rate'
     A                                  5 56'Days'
     A                                  5 62'Rate'
     A                                  5 77'Sign'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R FDFWRTF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A*
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R FDFWRTEND                 TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FDFWRTRPB'
     A                                  1 24'Invalid Forward +
     A                                      Rates Enquiry'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
