      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD MM interest rates details browser screen')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  FDINTRBDF - Interest Rates Details Browser Screen            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CAP058  *CREATE    Date 13Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP058 - Money Market Interest Rates API                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      MSGLOC(22)
     A                                      CF03(03 'Exit.')
     A                                      CF12(12 'Cancel')
     A                                      CF11(11 'Toggle Front Ofc Id')
      *
      ** Header Format
      *
     A
     A          R FDINTRH0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A                                  1 13'FDINTRB'
     A                                  2 25'Money Market Interest +
     A                                      Rates Enquiry'
     A                                      DSPATR(HI)
     A                                  1 66'DATE'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWSID        10A  O  2  2
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
      ** Subfile Format (Normal Screen - Non-Front Office Id)
      *
     A          R FDINTRS0                  SFL
     A  95                                  SFLNXTCHG
      *
     A            DDSELT         1A  B  6  2TEXT('Selection Field')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DDCCY          3A  O  6 10TEXT('Currency')
     A                                      DSPATR(HI)
     A            DDOBRW        12A  O  6 17TEXT('Overnight Borrow')
     A                                      DSPATR(HI)
     A            DDOLND        12A  O  6 30TEXT('Overnight Lend')
     A                                      DSPATR(HI)
     A            DDNBRW        12A  O  6 43TEXT('Tommrw/Next Borrow')
     A                                      DSPATR(HI)
     A            DDNLND        12A  O  6 56TEXT('Tommrw/Next Lend')
     A                                      DSPATR(HI)
      *
      ** Subfile Control (Normal Screen - Non-Front Office Id)
      *
     A          R FDINTRC0                  TEXT('Subfile Control')
     A                                      SFLCTL(FDINTRS0)
     A                                      SFLPAG(16) SFLSIZ(17)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  97                                  SFLCLR
     A  96                                  SFLEND(*MORE)
     A N96                                  ROLLUP(98 'Next page.')
     A                                      OVERLAY
     A                                      BLINK
      *
      ** Setoff error indicators for subfile
      *
     A                                      SETOF(20 'Invalid: SSELCT')
      *
     A            DDRLRN         4  0H      SFLRCDNBR
      *
     A                                  5  2'Select'
     A                                  5  9'Currency'
     A                                  4 25'Overnight'
     A                                  5 20'Borrow'
     A                                  5 34'Lend'
     A                                  4 49'Tommorow/Next'
     A                                  5 46'Borrow'
     A                                  5 60'Lend'
      *
      ** Subfile Format (Front Office Id Screen - Borrow)
      *
     A          R FDINTRS1                  SFL
     A  95                                  SFLNXTCHG
      *
     A            DDSELT         1A  B  6  2TEXT('Selection Field')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DDBOFR        20A  O  6  9TEXT('Front Office Id')
     A                                      DSPATR(HI)
     A            DDCCY          3A  O  6 34TEXT('Currency')
     A                                      DSPATR(HI)
     A            DDOBRW        12A  O  6 40TEXT('Overnight Borrow')
     A                                      DSPATR(HI)
     A            DDNBRW        12A  O  6 56TEXT('Tommrw/Next Borrow')
     A                                      DSPATR(HI)
      *
      ** Subfile Control (Front Office Id Screen - Borrow)
      *
     A          R FDINTRC1                  TEXT('Subfile Control')
     A                                      SFLCTL(FDINTRS1)
     A                                      SFLPAG(16) SFLSIZ(17)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  96                                  SFLEND(*MORE)
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98 'Next page.')
     A                                      OVERLAY
     A                                      BLINK
      *
      ** Setoff error indicators for subfile
      *
     A                                      SETOF(20 'Invalid: SSELCT')
      *
     A            DDRLRN         4  0H      SFLRCDNBR
      *
     A                                  5  2'Select'
     A                                  5  9'Borrow Front Office Id'
     A                                  5 32'Currency'
     A                                  4 41'Overnight'
     A                                  5 43'Borrow'
     A                                  4 56'Tommorow/Next'
     A                                  5 59'Borrow'
      *
      ** Subfile Format (Front Office Id Screen - Lend)
      *
     A          R FDINTRS2                  SFL
     A  95                                  SFLNXTCHG
      *
     A            DDSELT         1A  B  6  2TEXT('Selection Field')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DDLEFR        20A  O  6  9TEXT('Front Office Id')
     A                                      DSPATR(HI)
     A            DDCCY          3A  O  6 34TEXT('Currency')
     A                                      DSPATR(HI)
     A            DDOLND        12A  O  6 40TEXT('Overnight Lend')
     A                                      DSPATR(HI)
     A            DDNLND        12A  O  6 56TEXT('Tommrw/Next Lend')
     A                                      DSPATR(HI)
      *
      ** Subfile Control (Front Office Id Screen - Lend)
      *
     A          R FDINTRC2                  TEXT('Subfile Control')
     A                                      SFLCTL(FDINTRS2)
     A                                      SFLPAG(16) SFLSIZ(17)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  97                                  SFLCLR
     A  96                                  SFLEND(*MORE)
     A N96                                  ROLLUP(98 'Next page.')
     A                                      OVERLAY
     A                                      BLINK
      *
      ** Setoff error indicators for subfile
      *
     A                                      SETOF(20 'Invalid: SSELCT')
      *
     A            DDRLRN         4  0H      SFLRCDNBR
      *
     A                                  5  2'Select'
     A                                  5  9'Borrow Front Office Id'
     A                                  5 32'Currency'
     A                                  4 41'Overnight'
     A                                  5 44'Lend'
     A                                  4 56'Tommorow/Next'
     A                                  5 60'Lend'
      *
      ** Format trailer
      *
     A          R FDINTRF0                  TEXT('Screen Footer')
     A                                      PUTOVR
     A                                      OVERLAY
     A                                 24  2'F3=Exit'
     A                                 24 11'F12=Previous'
     A                                 24 25'F11=Toggle Front Office Id'
