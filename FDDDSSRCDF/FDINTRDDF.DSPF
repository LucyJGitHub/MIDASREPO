      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD MM interest rates maintenance screen')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  FDINTRDDF - Interest Rates Maintenance Screen                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 213558             Date 09Jun03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006             Date 11May01               *
      *                 CAP058  *CREATE    Date 13Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  213558 - Change data feed req id and timestamp to output only*
      *  CSD006 - Market Data Feeds                                   *
      *  CAP058 - Money Market Interest Rates API                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      MSGLOC(22)
     A                                      CF03(03 'Exit.')
     A  72                                  CF07(07 'Prev.')
     A  72                                  CF08(08 'Next')
     A                                      CF12(12 'Cancel')
     A  74                                  CF15(15 'Select')
      *
      ** Header Format
      *
     A
     A          R FDINTRH0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A                                  1 13'FDINTRD'
     A                                  2 23'Money Market Interest +
     A                                      Rates Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'DATE'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWSID        10A  O  2  2
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
      ** Subfile Format
      *
     A          R FDINTRS0                  SFL
      *
     A            DDPDSC        15A  O 10  6TEXT('Period Description')
     A                                      DSPATR(HI)
     A            DDBRWR        12A  B 10 28TEXT('Borrow Rates')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A  25                                  DSPATR(RI PC)
     A N25N70                               DSPATR(UL)
     A            DDLNDR        12A  B 10 49TEXT('Lend Rates')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A  26                                  DSPATR(RI PC)
     A N26N70                               DSPATR(UL)
     A            DDEXTD         4A  B 10 71TEXT('Extra Data')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
      *
      ** Subfile Control
      *
     A          R FDINTRC0                  TEXT('Subfile Control')
     A  71                                  CF10(10 'Delete')
     A                                      SFLCTL(FDINTRS0)
     A                                      SFLPAG(11) SFLSIZ(12)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  97                                  SFLCLR
     A  96                                  SFLEND(*MORE)
     A N96                                  ROLLUP(98 'Next page.')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
      ** Setoff error indicators for subfile
      *
     A                                      SETOF(25 'Invalid: DDBRWR')
     A                                      SETOF(26 'Invalid: DDLNDR')
      *
      ** Action code
      *
      *
      ** Currency
      *
      *
      ** Borrow Rate
      *
      *
      ** Lend Rate
      *
      *
      ** Extra Data
      *
      *                                                         /Start CSD006 /
      *
      ** Data Request Identifier
      *
      *
      ** TimeStamp
      *
      *                                                         /End   CSD006 /
      *
     A            DDRLRN         4  0H      SFLRCDNBR
     A                                  4  4'Action'
     A            DDACTN         1A  B  4 11
     A                                      DSPATR(HI)
     A N69                                  DSPATR(PR)
     A  20                                  DSPATR(RI)
     A  20 73                               DSPATR(PC)
     A N20 69                               DSPATR(UL)
     A                                      OVRDTA
     A                                  4 14'Currency'
     A            DDCCY          3A  B  4 23
     A                                      DSPATR(HI)
     A N69                                  DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A N21 69                               DSPATR(UL)
     A                                      OVRDTA
     A            DDCYNM        32A  O  4 28
     A                                      OVRDTA
      *                                                         /Start CSD006 /
     A                                  6  4'Data Request Identifier'
     A  90                                  DSPATR(ND)                                        CSD006
      * Start of change 212558
     A************DDDTFR        32A  B  6 31                                                  213558
     A            DDDTFR        32A  O  6 31
      * End of change 212558
     A  90                                  DSPATR(ND)                                        CSD006
     A                                      DSPATR(HI)
      * Start of change 212558
     A**70********                          DSPATR(PR)                                        213558
      * End of change 212558
     A  22                                  DSPATR(RI PC)
     A*N22N70*****                          DSPATR(UL)                                        213558
     A                                      OVRDTA
     A                                  7  4'Interest Rate Timestamp'
     A  90                                  DSPATR(ND)                                        CSD006
      * Start of change 212558
     A************DDDFT1         6A  B  7 31                                                  213558
     A            DDDFT1         6A  O  7 31
      * End of change 212558
     A  90                                  DSPATR(ND)                                        CSD006
     A                                      DSPATR(HI)
      * Start of change 212558
     A**70********                          DSPATR(PR)                                        213558
      * End of change 212558
     A  23                                  DSPATR(RI PC)
     A*N23N70*****                          DSPATR(UL)                                        213558
     A                                      OVRDTA
      * Start of change 212558
     A************DDDFT2         6A  B  7 38                                                  213558
     A            DDDFT2         6A  O  7 38
      * End of change 212558
     A  90                                  DSPATR(ND)                                        CSD006
     A                                      DSPATR(HI)
      * Start of change 212558
     A**70********                          DSPATR(PR)                                        213558
      * End of change 212558
     A  23                                  DSPATR(RI PC)
      * Start of change 212558
     A*N23N70*****                          DSPATR(UL)                                        213558
      * End of change 212558
     A                                      OVRDTA
     A                                  9 31'Borrow'
     A                                  9 52'Lend'
     A                                  8 71'Extra'
     A                                  9 71'Data'
      *                                                         /End   CSD006 /
      *
      ** Format trailer
      *
     A          R FDINTRF0                  TEXT('Screen Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 24  2'F3=Exit'
     A                                 24 11'F12=Previous'
     A                                 24 25'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 34'F7=Prev'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 43'F10=Delete'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 55'F15=Select'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
      *
      ** Error message subfile record format
      *
     A          R FDINTRS1                  SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Subfile Control for error messages
      *
     A          R FDINTRC1                  SFLCTL(FDINTRS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
