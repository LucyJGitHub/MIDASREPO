      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FD MM interest rates repair browser screen')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  FDINTRRDF - Interest Rates Repair Browser Screen             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CAP058  *CREATE    Date 13Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP058 - Money Market Interest Rates API                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      MSGLOC(22)
     A                                      CF03(03 'Exit.')
     A                                      CF12(12 'Cancel')
      *
      ** Header Format
      *
     A
     A          R FDINTRH0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2
     A                                  1 13'FDINTRR'
     A                                  2 21'Invalid Money Market Interest +
     A                                      Rates Enquiry'
     A                                      DSPATR(HI)
     A                                  1 66'DATE'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWSID        10A  O  2  2
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
      ** Subfile Format
      *
     A          R FDINTRS0                  SFL
     A  95                                  SFLNXTCHG
      *
     A            DDSELT         1A  B  6  2TEXT('Selection Field')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DDACTN         1A  O  6  6TEXT('Action')
     A                                      DSPATR(HI)
     A            DDFOTRANIDR        O  6 10REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDCCY          3A  O  6 35TEXT('Currency')
     A                                      DSPATR(HI)
     A            DDOBRW        12A  O  6 41TEXT('Overnight Borrow')
     A                                      DSPATR(HI)
     A            DDOLND        12A  O  6 56TEXT('Overnight Lend')
     A                                      DSPATR(HI)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            DDTMESTMP R        H      REFFLD(TIMESTMP)
      *
      ** Subfile Control (Front Office Id Screen - Borrow)
      *
     A          R FDINTRC0                  TEXT('Subfile Control')
     A                                      SFLCTL(FDINTRS0)
     A                                      SFLPAG(16) SFLSIZ(17)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  97                                  SFLCLR
     A  96                                  SFLEND(*MORE)
     A N96                                  ROLLUP(98 'Next page.')
     A                                      OVERLAY
      *
     A            DDRLRN         4  0H      SFLRCDNBR
      *
     A                                  5  2'Sel'
     A                                  5  6'Act'
     A                                  5 10'Front Office Id'
     A                                  5 32'Currency'
     A                                  4 50'Overnight'
     A                                  5 44'Borrow'
     A                                  5 60'Lend'
      *
      ** Format trailer
      *
     A          R FDINTRF0                  TEXT('Screen Footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** End Message
      *
     A          R FDINTREND                 TEXT('Screen Footer')
     A                                      BLINK
     A                                      OVERLAY
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      *
      ** Error message subfile record format
      *
     A          R FDINTRS1                  SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Subfile Control for error messages
      *
     A          R FDINTRC1                  SFLCTL(FDINTRS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
