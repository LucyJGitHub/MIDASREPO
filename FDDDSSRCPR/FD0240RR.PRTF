      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FD Location Currency Report')
     A****************************************************************
     A*                                                              *
     A*                                                              *
     A*  Midas - Standing Data
     A*                                                              *
     A*   PRTF/FD0240RR - LOCATION DETAIL REPORT.                    *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. E23462     DATE  01OCT90                    *
     A*                                                              *
     A*--------------------------------------------------------------*
     A*   E23462 - REREMOVE ALL REFERENCES TO DRS                    *
     A*                                                              *
     A****************************************************************
     A*
     A**VERSION**
     A**LOCATION DETAIL REPORT
     A**00.00.08
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A**  FORMAT TO OUTPUT HEADINGS
     A          R FD0240R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     7'REFERENCE FD0240'
     A                                      SKIPB(2)
     A            RRTITL    R             37REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *                                                   START OF CHANGE E23462
     A************************************37'MIDAS DEALING ROOM SYSTEM'
     A                                    33'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                      SPACEB(2)
     A                                    62' - LOCATION CODES'
      *                                                   START OF CHANGE E23462
     A************************************37'-------------------------'
     A                                    33'            -----------------'
      *                                                     END OF CHANGE E23462
     A                                      SPACEB(1)
     A                                    62'-----------------'
     A                                    21'CODE'
     A                                      SKIPB(9)
     A                                    33'NAME'
     A                                    47'LOCAL CCY'
     A                                    64'CHANGE DATE'
     A                                    81'CHANGE TYPE'
     A                                    99'USER ID'
     A                                      SPACEA(2)
     A*
     A**  DETAIL OUTPUT FORMAT
     A          R FD0240R1                  TEXT('DETAIL OUTPUT FMT')
     A            RRLCOD    R             21REFFLD(LOCD)
     A            RRLCNM    R             33REFFLD(LCTM)
     A            RRLCCY    R             50REFFLD(LCCY)
     A            RRLUPD    R             66REFFLD(RUNA)
     A            RRCHTP         3        85TEXT('LAST CHANGE TYPE')
     A            RRLUID    R            100REFFLD(LUID)
     A                                      SPACEA(1)
     A*
     A**  'NO DETAILS TO REPORT' FORMAT.
     A          R FD0240R2                  SKIPB(35)
     A                                      TEXT('NO DETAILS TO REPORT+
     A                                        FMT')
     A                                    47'*** NO DETAILS TO REPORT'
     A                                    71' ***'
     A*
     A**  'END OF REPORT' FORMAT.
     A          R FD0240R3                  TEXT('END OF REPORT FMT')
     A                                    47'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A*
     A**  DATA BASE ERROR FORMAT.
     A          R FD0240R4                  TEXT('DATA BASE ERROR FORMAT')
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            RRBASE         3        48TEXT('LDA SUBFIELD:DBASE')
     A                                    52'IN PROGRAM'
     A            RRPGM         10        63TEXT('PGM IN WHICH ERROR OCCURRED')
     A                                      SPACEA(1)
     A*
     A                                    33'FILE'
     A            RRFILE         8        38TEXT('LDA SUBFIELD:DBFILE')
     A                                    46', KEY'
     A            RRKEY         35        52TEXT('LDA SUBFIELD:DBKEY')
