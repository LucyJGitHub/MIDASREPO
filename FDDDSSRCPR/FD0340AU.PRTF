      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FD Deal Number Maintenance Audit Report')
     A****************************************************************
     A*                                                              *
     A*                                                              *
     A*  Midas - Standing Data
     A*                                                              *
     A*   PRTF/FD0340AU - DEAL NUMBER MAINTENANCE AUDIT REPORT.      *
     A*                                                              *
      *  (c) Finastra International Limited 2001                      *
     A*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01319             Date 15Oct91               *
      *                 E23462             Date 01Oct90               *
     A*                                                              *
     A*--------------------------------------------------------------*
      *  MD046248 - Finastra Rebranding                               *
     A*   S01319 - Remove redundant processing
     A*             File renamed from FD0340RR
     A*   E23462 - REPLACE DRS WITH TRADER SUPPORT, (TS).            *
     A*                                                              *
     A****************************************************************
     A*
     A**  INDICATOR FUNCTION SUMMARY
     A*
     A*       15       SCREEN BUILD 5 - AMEND WARNING NUMBER SCREEN.
     A*
     A*       66       OVERFLOW LINE OF PRINTER FILE PASSED
     A*
     A*       80
     A*       |        USED IN STANDARD SUBROUTINES
     A*       89
     A*
     A*          U7    SET ON WITH U8 IF A DATABASE ERROR OCCURS.
     A*          U8    FILE OUT OF BALANCE IF ON ON ITS OWN.
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
/********SPOOL(*YES) OUTQ(*JOB) FORMTYPE(*STD) COPIES(1)            : *   S01319
     A*****************************************************************
     A*
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT TO OUTPUT HEADINGS
     A          R FD0340R0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(2)
     A                                      SPACEA(3)
     A*                                              Begin /S01319/
     A*************************************7'Reference FD0340'
     A                                     7'Reference FD0340AU'
     A*                                              End   /S01319/
     A            RRTITL    R             37REFFLD(TITL)
     A                                   100'Date'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      SPACEA(2)
     A                                      EDTCDE(Z)
     A*
     A*                                                   START OF CHANGE E23462
     A************************************37'Midas Dealing Room System'
     A                                    33'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                    62' - Deal Number'
     A                                    76' Maintenance'
     A                                      SPACEA(1)
     A*
      *                                                   START OF CHANGE E23462
     A************************************37'-------------------------'   E23462
     A                                    33'            -----------------'
      *                                                     END OF CHANGE E23462
     A                                    62'--------------'
     A                                    76'------------'
     A                                      SPACEA(1)
     A*
     A                                    53'Audit Report'
     A*
     A**  FORMAT TO OUTPUT DETAILS OF CHANGES MADE
     A          R FD0340R1                  TEXT('Detail Output Fmt')
     A                                      SPACEA(3)
     A                                    27'Original'
     A                                    82'Amended'
     A                                      SPACEA(1)
     A*
     A                                    27'--------'
     A                                    82'-------'
     A                                      SPACEA(2)
     A*
     A                                    15'Reserve Deal Number'
     A            RRRDLN    R             39REFFLD(DLNO)
     A                                    70'Reserve Deal Number'
     A            RRARDN    R             95TEXT('Amended Reserve +
     A                                      Deal Number')
     A                                      REFFLD(DLNO)
     A                                      SPACEA(2)
     A                                    15'Warning Deal Number'
     A            RRWDLN    R             39REFFLD(DLNO)
     A  15                                70'Warning Deal Number'
     A  15        RRAWDN    R             95TEXT('Amended Warning +
     A                                      Deal Number')
     A                                      REFFLD(DLNO)
     A*
     A                                    38'User Occasioning Change'
     A                                      SPACEB(3)
     A            RRUID     R             65REFFLD(LUID)
     A                                      SPACEA(2)
     A*
     A                                    38'Workstation ID'
     A            RRWID     R             65REFFLD(WSID)
     A                                      SPACEA(2)
     A*
     A                                    38'Time of Change'
     A            RRTOC          4  0     65TEXT('Time of Change+
     A                                       - HH.MM Format')
     A                                      EDTWRD('0 .  ')
     A*
     A**  'No Details to Report' Format.
     A          R FD0340R2                  SKIPB(35)
     A                                      TEXT('No Details to Report+
     A                                        Fmt')
     A                                    47'*** NO DETAILS TO REPORT'
     A                                    71' ***'
     A*                                              Begin /S01319/
     A****'End*of*Report'*Format.***********
     A**********R FD0340R3******************TEXT('End of Report Fmt')
     A************************************47'*** END OF REPORT ***'
     A*                                              End   /S01319/
     A*
     A**  Data Base Error Format.
     A          R FD0340R4                  TEXT('Data Base Error Format')
     A                                      SPACEA(3)
     A                                    41'Database Error'
     A            RRDBSE         3        56TEXT('Database Error Number')
     A                                    60'in Program'
     A            RRDPGM        10        71TEXT('Pgm in Which Error Occurred')
     A                                      SPACEA(3)
     A                                    41'File'
     A            RRDBFL         8        46TEXT('Database File Causing Error')
     A                                    56'Key'
     A            RRDKEY        29        60TEXT('Key Causing DB Error')
