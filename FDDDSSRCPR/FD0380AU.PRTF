      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FD Location currency audit report')
     A****************************************************************
     A*                                                              *
     A*  Midas - Standing Data
     A*                                                              *
     A*   PRTF/FD0380AU - EXCHANGE RATES CCY POINTS AUDIT REPORT.    *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. X00000     DATE  00XXX00                    *
     A*                                                              *
     A*--------------------------------------------------------------*
     A*                                                              *
     A*   X00000 -                                                   *
     A*                                                              *
     A*****************************************************************
     A*
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A**  FORMAT FOR HEADINGS
     A          R FDAUDIR0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(2)
     A                                     7'Reference FD0380AU'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'Date'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'Time'
     A            RRRUNT         6  0    106
     A                                      EDTWRD('  :  : 0')
     A                                      SPACEA(1)
     A*
     A                                    33'            Midas            '
     A            RRTIT2        16        63
     A            RRTIT3         4        79
     A*
     A                                    83'AUDIT REPORT'
     A                                      SPACEA(1)
     A*
     A                                    33'            -----------------'
     A                                    62'------------------------'
     A*
     A                                    86'---------'
     A                                      SPACEA(3)
     A                                    10'Number of Records before-
     A                                       update:'
     A                                      SPACEB(3)
     A            RRNREC         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Insertions:'
     A            RRNINS         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Deletions:'
     A            RRNDEL         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Amendments:'
     A            RRNAMD         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Records after -
     A                                      update:'
     A            RRNCAL         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Actual Number of Records-
     A                                       on the file:'
     A            RRNORE         9  0     50EDTCDE(4)
     A*
     A  66                                69'** Difference **'
     A  66        RRNDIF         9  0     85EDTCDE(4)
     A  85                                10'** No. of records on Mod-
     A                                      ules file:'
     A                                      SPACEB(3)
     A  85        RRNMOD         9  0     50EDTCDE(4)
     A*
     A*
     A** NO VALID DETAIL RECORDS FOUND TO PRINT. *IN62 NOT ON.
     A N62                                47'*** NO DETAILS TO'
     A N62                                  SKIPB(35)
     A N62                                65'REPORT ***'
     A*
     A**  DATABASE ERROR MESSAGE.
     A          R FDAUDIR1                  TEXT('DATABASE ERROR MSG')
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(37)
     A            RRBASE         3        48TEXT('LDA SUBFIELD:DBASE')
     A                                    52'IN PROGRAM'
     A            RRPGM         10        63TEXT('LDA SUBFIELD:DBPGM')
     A*
     A                                    33'FILE'
     A                                      SPACEB(1)
     A            RRFILE         8        38TEXT('LDA SUBFIELD:DBFILE')
     A                                    46', KEY'
     A            RRKEY         35        52TEXT('LDA SUBFIELD:DBKEY')
