      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FD Location Currency Audit Report.')
     A****************************************************************
     A*                                                              *
     A*                                                              *
     A*  Midas - Standing Data
     A*                                                              *
     A*   FD0390 - LOCATION CURRENCY AUDIT REPORT.                   *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. E23462    DATE  01OCT90                     *
     A*                                                              *
     A*--------------------------------------------------------------*
     A*   E23462 - REREMOVE ALL REFERENCES TO DRS                    *
     A****************************************************************
     A*
     A**VERSION**
     A**LOCATION CURRENCY AUDIT REPORT
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A**  FORMAT FOR HEADINGS
     A          R FD0390R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(2)
     A                                     7'Reference FD0390'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'Date'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'Page'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'Time'
     A            RRTIME         6  0    106TEXT('Run Time')
     A                                      EDTWRD('  :  : 0')
     A                                      SPACEA(2)
      *                                                   START OF CHANGE E23462
     A************************************37'MIDAS DEALING ROOM SYSTEM'   E23462
     A                                    33'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                    63'- LOCATION CODES AUDIT '
     A                                    86'REPORT'
     A                                      SPACEA(1)
     A*
      *                                                   START OF CHANGE E23462
     A************************************37'-------------------------'   E23462
     A                                    33'            -----------------'
     A                                    62'------------------------'
     A                                    86'------'
     A                                      SPACEA(3)
     A*
     A                                    30'CHANGE'
     A                                    39'CHANGE'
     A                                    71'NORMAL'
     A                                      SPACEA(1)
     A*
     A                                    02'CODE'
     A                                    08'USER ID'
     A                                    18'PROGRAM'
     A                                    31'TIME'
     A                                    40'TYPE'
     A                                    61'NAME'
     A                                    72'CCY'
     A                                      SPACEA(2)
     A*
     A** FORMAT FOR DETAIL LINES.
     A          R FD0390R1                  TEXT('DETAIL OUTPUT FMT')
     A            RRLCOD    R             02REFFLD(LOCD)
     A            RRLUID    R             10REFFLD(LUID)
     A            RRUPGM    R             18REFFLD(JOPGM)
     A            RRTLUP    R             29REFFLD(TLUP)
     A                                      EDTWRD('  :  : 0')
     A*
     A** CHANGE TYPE   'A':AMD   'I':INST 'D':DEL
     A            RRTYPE         3        40TEXT('PROG DEFINED WRKFLD')
     A*
     A**  THE FIELDS BEFORE THE CHANGE
     A            RRLCNB    R             59REFFLD(LCTM)
     A            RRLCCB    R             72REFFLD(LCCY)
     A                                    51'Before'
     A                                      SPACEA(1)
     A*
     A**  THE FIELDS AFTER THE CHANGE
     A  63                                17'**********'
     A                                    51'After '
     A            RRLCNA    R             59REFFLD(LCTM)
     A            RRLCCA    R             72REFFLD(LCCY)
     A N64                                  SPACEA(2)
     A  64                                14'***** THIS ACTION IS NOT REFLECTED'
     A  64                                  SPACEB(1)
     A  64                                +1'IN THE DATABASE DUE TO RECOVERY PR'
     A  64                                83'OCESSING PERFORMED TODAY *****'
     A  64                                  SPACEA(2)
     A*
     A** FORMAT FOR FINAL LINE.                  *IN62 ON.
     A          R FD0390R2                  TEXT('END OF DATA FMT')
     A                                    47'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A*
     A*
     A**  DATABASE ERROR MESSAGE.
     A          R FD0390R3                  TEXT('DATABASE ERROR MSG')
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            RRBASE         3        48TEXT('LDA SUBFIELD:DBASE')
     A                                    52'IN PROGRAM'
     A            RRPGM         10        63TEXT('LDA SUBFIELD:DBPGM')
     A*
     A                                    33'FILE'
     A                                      SPACEB(1)
     A            RRFILE         8        38TEXT('LDA SUBFIELD:DBFILE')
     A                                    46', KEY'
     A            RRKEY         35        52TEXT('LDA SUBFIELD:DBKEY')
     A          R FD0390R4                  TEXT('Record Count Error-
     A                                       Report')
     A                                      SPACEB(2)
     A                                    10'Number of Records before-
     A                                       update:'
     A                                      SPACEB(3)
     A            RRNREC         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Insertions:'
     A            RRNINS         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Deletions:'
     A            RRNDEL         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Amendments:'
     A            RRNAMD         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Number of Records after -
     A                                      update:'
     A            RRNCAL         9  0     50EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Actual Number of Records-
     A                                       on the file:'
     A            RRNORE         9  0     50EDTCDE(4)
     A*
     A                                    69'** Difference **'
     A            RRNDIF         9  0     85EDTCDE(4)
     A*
