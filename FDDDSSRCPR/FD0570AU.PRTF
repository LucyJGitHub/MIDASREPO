      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FD Audit Control Report')
     A****************************************************************
     A*                                                              *
     A*                                                              *
     A*  Midas - Standing Data Module.
     A*                                                              *
     A*   FD0570AU - INTEREST RATES AUDIT REPORT                     *
     A*                                                              *
     A*  (c) Finastra International Limited 2001                     *
     A*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   PREV AMEND NO. X00000     DATE  00XXX00                    *
     A*                                                              *
     A****************************************************************
     A*                                                              *
     A*  MD046248 - Finastra Rebranding                              *
     A****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A**  FORMAT FOR HEADINGS
     A          R FDAUDAR0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(2)
     A                                     7'REFERENCE FD0570AU'
     A            RRTITL        53        36
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A**
     A                                   100'TIME'
     A                                      SPACEB(1)
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A**
     A                                      SPACEA(2)
     A*
     A                                    22'            Midas            '
     A                                    52'MM INTEREST RATES AUDIT '
     A                                    76'REPORT TOTALS'
     A                                      SPACEA(1)
     A                                    22'            ------------------'
     A                                    52'------------------------'
     A                                    76'-------------'
     A                                      SPACEA(3)
     A*
     A**  DETAIL HEADER FORMAT
     A          R FDAUDAR1                  TEXT('DTL HEADER FORMAT')
     A                                      SPACEB(2)
     A                                    10'FILE NAME : '
     A            RRFILE         8        23
     A                                      SPACEA(2)
     A                                    10'NUMBER OF RECORDS BEFORE-
     A                                       UPDATE:'
     A            RRNREC         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF INSERTIONS:'
     A            RRNINS         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF DELETIONS:'
     A            RRNDEL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF AMENDMENTS:'
     A            RRNAMD         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'NUMBER OF RECORDS AFTER -
     A                                      UPDATE:'
     A            RRNCAL         9  0     51EDTCDE(3)
     A                                      SPACEA(2)
     A                                    10'ACTUAL NUMBER OF RECORDS-
     A                                       ON THE FILE:'
     A            RRNORE         9  0     51EDTCDE(3)
     A  65                                70'** DIFFERENCE **'
     A                                      SPACEA(2)
     A*
     A**  NO VALID DETAIL RECORDS FOUND TO PRINT. *IN62 NOT ON.
     A N62                                47'*** NO DETAILS TO'
     A N62                                  SPACEB(3)
     A N62                                65'REPORT ***'
     A*
     A**  DATA BASE ERROR FORMAT.
     A          R FDAUDAR2                  TEXT('DATA BASE ERROR FORMAT')
     A                                      SKIPB(20)
     A                                    44'***  PRINT TERMINATED  +
     A                                       ***'
     A                                      SPACEA(1)
     A                                    41'DATABASE ERROR'
     A            RRDBSE    R             56REFFLD(DBASE)
     A                                    63'FILE'
     A            RRDBFL    R             68REFFLD(DBFILE)
     A                                      SPACEA(1)
     A                                    41'KEY'
     A            RRDKEY    R             45REFFLD(DBKEY)
     A                                      SPACEA(3)
