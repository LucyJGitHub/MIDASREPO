      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FD MM Interest Rates Audit Report')
     A****************************************************************
     A*                                                              *
     A*  Midas - Standing Data Module.
     A*                                                              *
     A*   PRTF/FD0570P1 - INTEREST RATES AUDIT REPORT.               *
     A*                                                              *
      *  (c) Finastra International Limited 2001                      *
     A*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01328             Date 8FEb90                *
     A*                                                              *
     A****************************************************************
     A*                                                              *
      *  MD046248 - Finastra Rebranding                               *
     A*   S01328 - FRA/IRS REVALUATION - NEW FILE BASED ON FD0570RR  *
     A*                                                              *
     A****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A**  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  REPORT HEADER FORMAT
     A          R FD0570R0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE FD0570P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA    R            105REFFLD(RUNA)
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A*
     A                                   100'TIME'
     A                                      SPACEB(1)
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(2)
     A*
     A                                    47'MM INTEREST RATES AUDIT +
     A                                       REPORT'
     A                                      SPACEA(1)
     A*
     A                                    47'-------------------'
     A                                    66'-----------'
     A                                      SPACEA(2)
     A*
     A**  DETAIL HEADER FORMAT
     A          R FD0570R1                  TEXT('DTL HEADER FORMAT')
     A                                      SKIPB(9)
     A                                      SPACEA(2)
     A                                     1'USER ID/'
     A                                    10'CHANGE TYPE/'
     A                                    42'CURRENCY'
     A                                    54'PERIOD'
     A                                    68'PERIOD'
     A                                    82'BORROW'
     A                                    98'LEND'
     A                                   113'PERIOD'
     A                                      SPACEA(1)
     A*
     A                                     1'PROGRAM'
     A                                    17'TIME'
     A                                    25'WSID'
     A                                    44'CODE'
     A                                    52'DESCRIPTION'
     A                                    69'DATE'
     A                                    83'RATE'
     A                                    98'RATE'
     A                                   114'EXTRA'
     A*
     A**  FORMAT TO OUTPUT DETAIL LINES.
     A          R FD0570R2                  TEXT('DETAIL OUTPUT FMT')
     A                                      SPACEA(3)
     A*
     A**  FIRST LINE OF DETAILS
     A            RRUSER    R              1REFFLD(JOUSER)
     A            RRTYPE         3        14TEXT('CHANGE TYPE')
     A                                    34'BEFORE'
     A            RRCCY     R             45REFFLD(CCY)
     A            RRPDES         9        53TEXT('PERIOD DESCRIPTION')
     A            RRDATB         7        67TEXT('PERIOD DATE')
     A            RRBRTB        13        79TEXT('BORROW RATE')
     A            RRLRTB        13        95TEXT('LEND RATE')
     A            RRPEXB    R            114REFFLD(PRDX)
     A                                      SPACEA(1)
     A*
     A**  SECOND LINE OF DETAILS
     A            RRPGM     R              1REFFLD(JOPGM)
     A            RRTIME    R             12REFFLD(JOTIME)
     A                                      EDTWRD('  :  : 0')
     A            RRJOB     R             22REFFLD(JOJOB)
     A                                    34'AFTER '
     A            RRDATA         7        67TEXT('PERIOD DATE')
     A            RRBRTA        13        79TEXT('BORROW RATE')
     A            RRLRTA        13        95TEXT('LEND RATE')
     A            RRPEXA    R            114REFFLD(PRDX)
     A*
     A**  THIRD LINE OF DETAILS
     A  63
     AO 64                                  SPACEA(1)
     A  64                                12'*****  THIS ACTION IS NOT '
     A  64                                38'REFLECTED IN THE DATABASE DUE '
     A  64                                68'TO RECOVERY PROCESSING  *****'
     A  63                                  SPACEA(1)
     A  63                                 1'*********'
     A*
     A**  'END OF REPORT' FORMAT.
     A          R FD0570R6                  TEXT('END OF REPORT FMT')
     A                                    52'*** END OF REPORT ***'
