      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FD Fwd rates exceeding rate tolerance report')
      *****************************************************************
      *                                                               *
      *  Midas - FD Module                                            *
      *                                                               *
      *  PRTF/FD0580P1 - FD Forward Rates Exceeding Rate Tolerance    *
      *                  Report                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 214115             Date 02Jan03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006  *CREATE    Date 02Jan00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  214115 - Forward Rates Report shows no points when S01432 on *
      *  CSD006 - Market Data Feeds                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'Reference FD0580P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A N60                                35'MDF-TODAY''S FORWARD RATES EXCEEDI+
     A                                      NG RATE TOLERANCE'
     A                                      SPACEA(1)
      *
     A  60                                27'MDF-TODAY''S FORWARD RATES EXCEEDI+
     A                                      NG RATE TOLERANCE - SINCE LAST RUN'
     A                                      SPACEA(1)
      *
     A N60                                35'----------------------------------+
     A                                      -----------------'
      *
     A  60                                27'----------------------------------+
     A                                      ----------------------------------'
      *
      ** Detail Record Format
      *
     A          R HEADP2
     A                                     7'Currency'
     A                                      SPACEB(2)
     A                                    18'Forward Rate'
     A                                    41'Timestamp'
     A                                    67'Old Rate Details'
     A                                    92'New Rate Details'
     A                                      SPACEA(1)
     A                                     7'Code'
     A                                    18'Identifier'
     A***Start 214115
     A***********                         64'Days'
     A***********                         70'Rate'
     A***********                         82'Sign'
     A***********                         89'Days'
     A***********                         95'Rate'
     A***********                        107'Sign'
     A                                    66'Days'
     A                                    73'Rate'
     A                                    84'Sign'
     A                                    91'Days'
     A                                    98'Rate'
     A                                   109'Sign'
     A***End   214115
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETAIL
     A            WCCY           3         7
     A            WTREF         20        18
     A            WTMEST        19        41
     A            WBDFW1         5        64
     A            WBRFW1        14        70
     A            WBSFW1         1        85
     A            WADFW1         5        89
     A            WARFW1        14        95
     A            WASFW1         1       110
     A                                      SPACEA(1)
     A                                     7'Data Feed Request:'
     A            WDFRI         32        26
     A            WBDFW2         5        64
     A            WBRFW2        14        70
     A            WBSFW2         1        85
     A            WADFW2         5        89
     A            WARFW2        14        95
     A            WASFW2         1       110
     A                                      SPACEA(1)
      *
      ** Detail Record Format 2
      *
     A          R DETAIL2
     A            WBDFW          5        64
     A            WBRFW         14        70
     A            WBSFW          1        85
     A            WADFW          5        89
     A            WARFW         14        95
     A            WASFW          1       110
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
