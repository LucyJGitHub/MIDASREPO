      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FD Valid MDF Forward Rates received today')
      *****************************************************************
      *                                                               *
      *  Midas - FD Module                                            *
      *                                                               *
      *  PRTF/FD0582P1 - Valid MDF Transactions received today        *
      *                  Forward Rates                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 214115             Date 02Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. 196570  *CREATE    Date 12Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  214115 - Forward Rates Report shows no points when S01432 on *
      *  196570 - Patch for Market Data Feed: enhanced MDF reporting  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'Reference FD0582P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    35'MDF - Valid Forward Rates receive+
     A                                      d today'
     A                                      SPACEA(1)
      *
     A                                    35'---------------------------------+
     A                                      -------'
      ** Detail Record Format
      *
     A          R HEADP2
     A                                     7'Currency'
     A                                      SPACEB(2)
     A                                    +3'Forward Rate'
     A                                   +11'Timestamp'
     A                                   +23'Rate received from MDF'
     A                                      SPACEA(1)
     A                                     7'Code'
     A                                    +7'Identifier'
     A                                   +41'Days'
     A                                    +3'Rate'
     A                                    +9'Sign'
     A                                      SPACEA(1)
      *
      ** Detail Record Format
      *
     A          R DETAIL
     A            WCCY           3         7
     A            WTREF         20        18
     A            WTMEST        19        41
     A            WADFW1         5        65
     A            WARFW1        14        71
     A***Start 214115
     A************WASFW1         1        86
     A            WASFW1         1        90
     A***End   214115
     A                                      SPACEA(1)
     A                                     7'Data Feed Request:'
     A            WDFRI         32        26
     A            WADFW2         5        65
     A            WARFW2        14        71
     A***Start 214115
     A************WASFW2         1        86
     A            WASFW2         1        90
     A***End   214115
     A                                      SPACEA(1)
      *
      ** Detail Record Format 2
      *
     A          R DETAIL2
     A            WADFW          5        65
     A            WARFW         14        71
     A***Start 214115
     A************WASFW          1        86
     A            WASFW          1        90
     A***End   214115
     A                                      SPACEA(1)
      *
      *
      ** Report Trailer
      *
     A          R TRAILP1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
