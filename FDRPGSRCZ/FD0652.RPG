     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ER Deal Sub-Type - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - European Reporting                                   *
      *                                                               *
      *  FD0652 - Deal Type/Sub-Type Extension Maintenance            *
      *                                                               *
      *  Function:  This program maintains deal type/subtype exten    *
      *             sion details. Based from the window program       *
      *             FD0652W6.                                         *
      *                                                               *
      *  Called By: FD0652R - Edit Deal Type/Sub-Type                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *Create    Date 25Apr05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
      *   The following routines CANNOT be changed :
      *   ========================================
      *
      *   (Main processing)
      *   SRINS
      *   SRAMD
      *   SRDEL
      *   SRENQ
      *   SRSMSG
      *   SRCMSG
      *   SRRTRN
      *   SRSCRN
      *   SRREC
      *   SRUPD
      *   SRWRT
      *   SRDELR
      *   SRALOC
      *   SRCOMP
      *
      *   The following routines can be changed :
      *   =====================================
      *
      *   SRINIT for specific initialization
      *   SRDBER to handle database errors
      *   SRVAL  to initialize error indicators and control validation
      *          of more/less fields
      *   SRINZ  to setup fields with defaults
      *   SRFTOS to move additional fields to the screen fields
      *   SRSTOF to move additional fields to the file fields
      *
      *
      * - Changes to the existing code should be reduced to a minimum,
      *   using separate subroutines in order to preserve the program
      *   structure as defined in the skeleton, thus reducing
      *   maintenance efforts.
      *
      * - Moreover, no functionality should be added unless specifically
      *   required. In this case, it should be very well documented
      *   in the header box.
      *
      * - The data structure used to save the before image of the
      *   record is called SVRCD.
      *   Only the length should be changed.
      *
      * - The data structure used to access the current record
      *   via the DS name is called NWRCD.
      *   Only the file name should be changed.
      *
      * - The files must have their record formats renamed to:
      *      RTVIDX for the retrieve index
      *      UPDIDX for the update index
      *      SCREEN for the screen format
      *   Any file/screen access will be done through the renamed format
      *   so that these routines remain unchanged.
      *
      *----------------------------------------------------------------
      *
      *   Naming conventions
      *   ==================
      *
      * - Work fields used in the program should start with WW or WU
      *   i.e. WWPLEX or WUPLEX
      *
      * - Screen fields should start with #0 (for 1st screen format),
      *   i.e. #0PLEX
      *
      * - Key fields should start with K ,i.e. KDLST
      *   (Also for fields used in KLIST's)
      *
      * - Subroutines should start with SR, i.e. SRVAL for validation,
      *   SRINIT for initial routine , etc...
      *
      *----------------------------------------------------------------
      *  Use of indicators:
      *
      *  (Screen field error indicators should start with 20 in
      *   ascending order)
      *
      *  *IN15  -  On=Delete/Enquiry, Protect field.
      *            Off=Insert/Amend, Underline field.
      *  *IN60  -  Extension record not found via update index
      *  *IN61  -  Unable to allocate record via update index
      *  *IN68  -  Error occured during DBF update
      *  *IN69  -  SFLEND control
      *  *IN75  -  General error indicator. Used to condition output
      *            of error messages and redisplay screen.
      *  *IN81  -  Call to DBERRCTL ended in error
      *  *IN82  -  Plexus capital currency in error
      *  *IN89  -  Extension record not found via retrieval index
      *
      *  *INKJ  -  F10 pressed, enable delete
      *  *INKL  -  F12 pressed, exit
      *  *INLR  -  End processing
      *
      *  *INU7  -  :    Data-base
      *  *INU8  -  :  error control
      *
      *****************************************************************
     FFDDSX1L0IF  E           K        DISK
     F            FDDTSTF6                          KRENAMERTVIDX
      ** Extended Deal Sub-type  (Retrieval index Prefix VH.)
      *
     FFDDSX1PDUF  E           K        DISK         KCOMIT       A    UC
     F            FDDTSTF6                          KRENAMEUPDIDX
      ** Extended Deal Sub-type  (Update index Prefix VH.)
      *
     FFD0652DFCF  E                    WORKSTN
     F            FD0652F6                          KRENAMESCREEN
      ** Display file (Prefix #0.)
      *
      /EJECT
      *----------------------------------------------------------------
     E                    CPY@    1   1 80
      /EJECT
      *----------------------------------------------------------------
     I           SDS
      ** Get program name from PSDS
     I                                     *PROGRAM WWPGM
     I                                      244 253 WSID
     I                                      254 263 USER
      /EJECT
      *----------------------------------------------------------------
     IA@CPY       DS
      ** Copyright array
      *
     I                                        1  80 CPY@
      *
     IDLDA        DS                            256
      ** Data structure for data-base processing
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     IDATA        DS
     I                                        1   8 KKEY
     I                                        1   2 #1DTPE
     I                                        3   4 #1DSTC
     I                                        5   8 #1DCLA
      /EJECT
      *----------------------------------------------------------------
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     ISDMMOD    E DSSDMMODPD
      ** DUMMY record formats for access to midas modules details
      *
     ISDBANK    E DSSDBANKPD
      ** DUMMY record formats for access to midas bank details
      *
     INWRCD     E DSSDDSX1PD
      ** Current/previous master file fields
      *
      /EJECT
     ISVRCD       DS                              7
      ** Stored master file fields
      *
      /EJECT
      *----------------------------------------------------------------
      * Main processing                                               *
      *----------------------------------------------------------------
      *
      ** Execute initial routine
      *
     C                     EXSR SRINIT
      *
      * Execute specific routine depending on action
      *
     C           ACTION    CASEQ'I'       SRINS
     C           ACTION    CASEQ'D'       SRDEL
     C                     ENDCS
      *
     C           ACTION    CASEQ'A'       SRAMD
     C           ACTION    CASEQ'E'       SRENQ
     C                     ENDCS
      *
      ** Execute routine to setup return code and exit program
      *
     C                     EXSR SRRTRN
      *
      /EJECT
      *----------------------------------------------------------------
      * SRINS - Routine to handle 'INSERT' action                     *
      *----------------------------------------------------------------
      *
     C           SRINS     BEGSR
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record found,
      *
     C           *IN89     IFEQ '0'
      *
      ** In case of 'Insert over Deleted Record'
      ** Save before image
      *
     C                     EXSR SRSAVE
      *
     C                     ENDIF
      *
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
      ** Display and handle screen until no more errors or F12
      *
     C           *IN75     DOUEQ'0'
     C           *INKL     OREQ '1'
      *
     C                     EXSR SRSCRN
      *
      ** Bypass any further validation if previous DB error or F12
      *
     C           *IN69     IFEQ '0'
     C           *INKL     ANDEQ'0'
      *
      ** Clear messages for redisplay
      *
     C                     EXSR SRCMSG
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** No errors
      *
     C           *IN75     IFEQ '0'
      *
      ** In case of 'Insert over Deleted Record'
      *
     C           *IN89     IFEQ '0'
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, move screen values to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Update record
      *
     C                     EXSR SRUPD
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ELSE
      *
      ** Move screen fields to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Write new record
      *
     C                     EXSR SRWRT
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRAMD - Routine to handle 'AMEND' action                      *
      *----------------------------------------------------------------
      *
     C           SRAMD     BEGSR
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     MOVE '1'       *IN69
     C                     MOVEL'ER99920' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Protect fields for display
      *
     C                     MOVE '1'       *IN15
      *
      ** Display screen
      *
     C                     EXSR SRSCRN
      *
      ** Execute DB error routine
      *
     C                     MOVELWWEXTF    WWBFIL           ************
     C                     Z-ADD2         WWBASE           * DBER 002 *
     C                     MOVELKKEY      WWBKEY           ************
     C                     EXSR SRDBER
      *
     C                     ELSE
      *
      ** Record found,
      ** set file fields to screen fields and save before image
      *
     C                     EXSR SRFTOS
     C                     EXSR SRSAVE
      *
      ** Display and handle screen until record can be allocated and
      ** record on file and no errors left or F12 pressed
      *
     C           *IN61     DOUEQ'0'
     C           *IN60     ANDEQ'0'
     C           *IN75     ANDEQ'0'
     C           *INKL     OREQ '1'
      *
     C                     EXSR SRSCRN
      *
      ** Bypass any further validation if previous DB error or F12
      *
     C           *IN69     IFEQ '0'
     C           *INKL     ANDEQ'0'
      *
      ** Clear messages for redisplay
      *
     C                     EXSR SRCMSG
      ** Validate input
     C                     EXSR SRVAL
      *
      ** No errors
      *
     C           *IN75     IFEQ '0'
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, move screen values to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Update record
      *
     C                     EXSR SRUPD
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDEL - Routine to handle 'DELETE' action                     *
      *----------------------------------------------------------------
      *
     C           SRDEL     BEGSR
      *
      ** Set indicators on for 'DELETE' mode to protect fields
      *
     C                     MOVE '1'       *IN15
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     MOVE '1'       *IN69
     C                     MOVEL'ER99916' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Execute DB error routine
      *
     C                     MOVELWWEXTF    WWBFIL           ************
     C                     Z-ADD3         WWBASE           * DBER 003 *
     C                     MOVELKKEY      WWBKEY           ************
     C                     EXSR SRDBER
      *
     C                     ELSE
      *
      ** Record found,
      ** save before image
      *
     C                     EXSR SRSAVE
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ '0'
     C           *IN61     ANDEQ'0'
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      * Images match, delete record
      *
     C                     EXSR SRDELR
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRENQ - Routine to handle 'ENQUIRY' action                    *
      *----------------------------------------------------------------
      *
     C           SRENQ     BEGSR
      *
      ** Set indicators on for 'ENQUIRY' mode to protect fields
      *
     C                     MOVE '1'       *IN15
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     MOVE '1'       *IN69
     C                     MOVEL'ER99917' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Display screen
      *
     C                     EXSR SRSCRN
      *
      * Execute DB error routine
      *
     C                     MOVELWWEXTF    WWBFIL           ************
     C                     Z-ADD4         WWBASE           * DBER 004 *
     C                     MOVELKKEY      WWBKEY           ************
     C                     EXSR SRDBER
      *
     C                     ELSE
      *
      ** Record found, set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSMSG - Routine to send messages to message subfile.         *
      *----------------------------------------------------------------
      *
     C           SRSMSG    BEGSR
      *
     C           ZAMSGF    IFEQ *BLANKS
     C                     MOVEL'STUSRMSG'ZAMSGF
     C                     ENDIF
     C                     CALL 'SNDERMSG'
     C                     PARM WWPGM     ZAPGM  10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
      ** Clear all fields for default mechanism next time.
      *
     C                     MOVEL*BLANK    ZAPGRL           Rel queue
     C                     MOVEL*BLANK    ZAMSID           Message Id.
     C                     MOVEL*BLANK    ZAMSDA           Message data.
     C                     MOVEL*BLANK    ZAMSTP           Message type.
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRCMSG - Routine to clear program's message queue.            *
      *----------------------------------------------------------------
      *
     C           SRCMSG    BEGSR
      *
     C                     CALL 'CLRERMSG'
     C                     PARM WWPGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRREC - Routine to access file via retrieve index             *
      *----------------------------------------------------------------
      *
     C           SRREC     BEGSR
      *
     C           KDLST     CHAINRTVIDX               89
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRALOC - Routine to access record via update index            *
      *----------------------------------------------------------------
      *
     C           SRALOC    BEGSR
      *
     C           KDLST     CHAINUPDIDX               6061
      *
      ** If record not on file, setup message 'Record deleted'
      *
     C           *IN60     IFEQ '1'
     C                     MOVEL'ER99918' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRUPD - Routine to update file.                               *
      *----------------------------------------------------------------
      *
     C           SRUPD     BEGSR
      *
     C                     UPDATUPDIDX                 68
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * SRWRT - Routine to write a new record to the file.
      *-------------------------------------------------------------------------
      *
     C           SRWRT     BEGSR
      *
     C                     WRITEUPDIDX                 68
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDELR - Routine to delete a record                           *
      *----------------------------------------------------------------
      *
     C           SRDELR    BEGSR
      *
     C                     DELETUPDIDX                 68
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRCOMP - Routine to compare before/after image of records     *
      *----------------------------------------------------------------
      *
     C           SRCOMP    BEGSR
      *
     C           SVRCD     IFEQ NWRCD
     C                     MOVE 'Y'       WWMTCH  1
     C                     ELSE
     C                     MOVE 'N'       WWMTCH
     C                     MOVEL'ER99919' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Use SETLL to release record lock
      *
     C           KDLST     SETLLUPDIDX
      *
      ** Set condition to redisplay screen
      *
     C                     MOVE '1'       *IN60
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSAVE - Routine to save before image of record via DS        *
      *----------------------------------------------------------------
      *
     C           SRSAVE    BEGSR
      *
     C                     MOVELNWRCD     SVRCD
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRRTRN - Routine to set up return code for calling program    *
      *----------------------------------------------------------------
      *
     C           SRRTRN    BEGSR
      *
      ** DBF update error
      *
     C           *IN69     IFEQ '1'
     C                     MOVE 'Y2U0004' WWRTRN
     C                     ELSE
      *
      ** Database / Window error
      *
     C           *IN68     IFEQ '1'
     C                     MOVE 'USR0563' WWRTRN
     C                     ELSE
      *
      ** F12 pressed
      *
     C           *INKL     IFEQ '1'
     C                     MOVE 'USR0790' WWRTRN
     C                     ELSE
      *
      ** No errors
      *
     C                     MOVE *BLANKS   WWRTRN
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     CLOSEFDDSX1PD               99
      *
      ** Exit program
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRDBER - Routine to handle database errors                    *
      *----------------------------------------------------------------
      *
     C           SRDBER    BEGSR
      *
      ** Update data area LDA
      *
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     MOVEL'FD0652  'DBPGM
     C                     MOVE WWBFIL    DBFILE
     C                     MOVE WWBKEY    DBKEY
     C                     MOVE WWBASE    DBASE
     C                     OUT  DLDA
      *
      ** Set on data-base error indicators
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN69
      *
      ** Dump program
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             81
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSCRN - Routine to handle screen and validation              *
      *----------------------------------------------------------------
      *
     C           SRSCRN    BEGSR
      *
      ** Display messages
      *
     C                     WRITE#MSGCTL
      *
      ** Display main screen
      *
     C                     EXFMTSCREEN
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * The routines that can be changed start here...                *
      *----------------------------------------------------------------
      * SRVAL - Routine to control validation of each field.          *
      *----------------------------------------------------------------
      *
     C           SRVAL     BEGSR
      *
      ** Initialize error condition indicators
      *
     C                     MOVE '0'       *IN75
     C                     MOVEA'0000'    *IN,23
     C                     MOVE '0'       *IN26
      *
     C                     EXSR SRMOBC
     C                     EXSR SRPMOB
     C                     EXSR SRDIST
     C                     EXSR SRFRNI
     C                     EXSR SRDEPT
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRMOBC - Validation of MOBILISABLE                            *
      *----------------------------------------------------------------
      *
     C           SRMOBC    BEGSR
      *
     C           #0MOBC    IFLT 0
     C           #0MOBC    ORGT 2
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN23
     C                     MOVEL'ER25201' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      *----------------------------------------------------------------
      * SRPMOB - Validation of POSSIBILITY OF MOBILISATION            *
      *----------------------------------------------------------------
      *
     C           SRPMOB    BEGSR
      *
     C           #0PMOB    IFLT 0
     C           #0PMOB    ORGT 2
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN24
     C                     MOVEL'ER25202' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      *----------------------------------------------------------------
      * SRDIST - Validation of DISCOUNT TYPE                          *
      *----------------------------------------------------------------
      *
     C           SRDIST    BEGSR
      *
     C           #0DIST    IFLT 0
     C           #0DIST    ORGT 3
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN25
     C                     MOVEL'ER25203' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      *----------------------------------------------------------------
      * SRFRNI - Validation of FLOATING RATE NOTE INDICATOR           *
      *----------------------------------------------------------------
      *
     C           SRFRNI    BEGSR
      *
     C           #0FRNI    IFEQ *BLANKS
     C                     MOVE 'N'       #0FRNI
     C                     ENDIF
      *
     C           #0FRNI    IFNE 'Y'
     C           #0FRNI    ANDNE'N'
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN26
     C                     MOVEL'ER25204' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      *----------------------------------------------------------------
      * SRDEPT - VALIDATION OF DEPOT TYPE                             *
      *----------------------------------------------------------------
      *
     C           SRDEPT    BEGSR
      *
     C           #0DEPT    IFLT 0
     C           #0DEPT    ORGT 3
     C                     MOVE '1'       *IN75
     C                     MOVE '1'       *IN26
     C                     MOVEL'ER25205' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      *----------------------------------------------------------------
      * SRINZ - Routine to initialize screen fields with defaults     *
      *----------------------------------------------------------------
      *
     C           SRINZ     BEGSR
      *
     C                     Z-ADD0         #0PMOB
     C                     Z-ADD0         #0DIST
     C                     Z-ADD0         #0MOBC
     C                     MOVE *BLANKS   #0FRNI
     C                     Z-ADD0         #0DEPT
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRFTOS - Routine to move file fields to screen fields         *
      *----------------------------------------------------------------
      *
     C           SRFTOS    BEGSR
      *
     C                     Z-ADDXXMOBC    #0MOBC
     C                     Z-ADDXXPMOB    #0PMOB
     C                     Z-ADDXXDIST    #0DIST
     C                     MOVE XXFRNI    #0FRNI
     C                     MOVE XXDEPT    #0DEPT
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRSTOF - Routine to move screen fields to file fields         *
      *----------------------------------------------------------------
      *
     C           SRSTOF    BEGSR
      *
      * Move key field to file field
      *
     C                     MOVE KTYPE     XXXXXX
     C                     MOVE KSTYP     XXDSTC
     C                     MOVE KCLAS     XXCLAS
      *
      * Move data fields to file fields
      *
     C                     Z-ADD#0PMOB    XXPMOB
     C                     Z-ADD#0DIST    XXDIST
     C                     Z-ADD#0MOBC    XXMOBC
     C                     MOVE #0FRNI    XXFRNI
     C                     Z-ADD#0DEPT    XXDEPT
      *
     C                     ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SRINIT - Routine to handle initial processing                 *
      *----------------------------------------------------------------
      *
     C           SRINIT    BEGSR
      *
      ** Get parameters from calling program
      *
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C                     PARM           DATA
     C                     PARM           WWRTRN  7
      *
      ** Dummy parameters (used for non-DDS window method)
      *
     C                     PARM           DUMMY1  30
     C                     PARM           DUMMY2  30
     C                     PARM           DUMMY3  30
     C                     PARM           DUMMY4  30
     C                     PARM           DUMMY5 10
      *
      ** The following parameter is needed from PTF16 on.
      ** Every PTF level below should omit this parameter
      *
     C                     PARM           SPARE 256
      *
      ** Setup key values using transaction data passed from caller
      *
     C           KDLST     KLIST
     C                     KFLD           KTYPE   2
     C                     KFLD           KSTYP   2
     C                     KFLD           KCLAS   4
      *
     C                     MOVE #1DTPE    KTYPE
     C                     MOVE #1DSTC    KSTYP
     C                     MOVE #1DCLA    KCLAS
      *
      ** Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      ** Setup file value used in database error during access to
      ** retrieval index
      *
     C           *LIKE     DEFN DBFILE    WWEXTF
     C                     MOVEL'SDDSX1PD'WWEXTF
     C                     MOVEL'STUSRMSG'ZAMSGF
      *
      ** Initialise error indicators
      *
     C                     MOVEA'000'     *IN,23
     C                     MOVE '0'       *IN75
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           PRTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'WWBFIL
     C                     MOVEL'98'      WWBASE
     C                     MOVELPOPTN     WWBKEY
     C                     EXSR SRDBER
     C                     ENDIF
      *
     C                     MOVELWWPGM     SPGM
     C                     MOVELUSER      SUSER
     C                     MOVELWSID      SJOB
      *
      ** Call access program for midas modules details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM '*MSG    'PRTCD
     C                     PARM '*FIRST  'POPTN
     C           SDMMOD    PARM SDMMOD    DSFDY
      *
     C           PRTCD     IFNE *BLANK
     C                     MOVEL'SDMMODPD'WWBFIL
     C                     MOVEL'99'      WWBASE
     C                     MOVELPOPTN     WWBKEY
     C                     EXSR SRDBER
     C                     ENDIF
      *
     C           BGLRIN    IFNE 'Y'
     C                     MOVE *BLANKS   WWRTRN
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ELSE
     C                     OPEN FDDSX1PD
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2005
