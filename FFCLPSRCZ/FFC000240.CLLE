/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas FF Closing Trades input')                       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Futures and Options Module                          */
/*                                                                   */
/*       FFC000240 - Closing Trades Input                            */
/*                                                                   */
/*       (c) Finastra International Limited 2001                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSC023             Date 29Jan04              */
/* Midas Release 4 --------------- Base -----------------------------*/
/*                      CFF007  *CREATE    Date 13Feb01              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSC023 - MidasPlus additional packaging.  SBMJOB change.    */
/*                OUTQ must always be *JOBD.                         */
/*       CFF007 - Futures and Options Enhancement for Private        */
/*                Banking.                                           */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&RtnCde) TYPE(*CHAR) LEN(7)
             DCL        VAR(&OPN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SARD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SFMT) TYPE(*CHAR) LEN(200)
             DCL        VAR(&MARKT) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SBJOB) TYPE(*CHAR) LEN(30)
             DCL        VAR(&PACTN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PMRKT) TYPE(*CHAR) LEN(2)
             DCL        VAR(&PMKTN) TYPE(*CHAR) LEN(20)
             DCL        VAR(&PRUNA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PRUND) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&PDATF) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PMBIN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PBUSD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&PMKLC) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PMLOC) TYPE(*CHAR) LEN(3)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2001')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
 
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             CHGJOB     SWS(XXXXXX00)
 
 
/* Initial control program - select 'Action code' + 'Market centre' */
 
INIT:
 
             CALL       PGM(FF000240) PARM(&PACTN &PMRKT &PMKTN +
                          &PRUNA &PRUND &PDATF &PMBIN &PBUSD &PMKLC +
                          &PMLOC)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                ROLLBACK
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(END)
             ENDDO
 
/* F3 taken if action code is blank */
             IF         COND(&PACTN *EQ ' ') THEN(GOTO END)
 
/* Override files to market members */
             IF         COND(&PMRKT *NE '  ') THEN(DO)
                OVRDBF     FILE(TRANS) MBR(&PMRKT)
                OVRDBF     FILE(TRSET) MBR(&PMRKT)
                OVRDBF     FILE(POSTNC) MBR(&PMRKT)
                OVRDBF     FILE(POSTNK) MBR(&PMRKT)
                OVRDBF     FILE(CLOST) MBR(&PMRKT)
                OVRDBF     FILE(CLOST2) MBR(&PMRKT)
                OVRDBF     FILE(MEMOSM) MBR(&PMRKT)
                OVRDBF     FILE(PRONOM) MBR(&PMRKT)
                OVRDBF     FILE(FFACMVD) MBR(&PMRKT)
                OVRDBF     FILE(TRANSA) MBR(&PMRKT)
                OVRDBF     FILE(TRANSIN) TOFILE(TRANS) +
                             MBR(&PMRKT) SHARE(*NO)
                OVRDBF     FILE(TRSETIN) TOFILE(TRSET) +
                             MBR(&PMRKT) SHARE(*NO)
                ENDDO
             ELSE DO
/* Check if OTC enhancemets CFF003 are switched on */
                CHGVAR     VAR(&RtnCde) VALUE('       ')
                CHGVAR     VAR(&OPN) VALUE('*VERIFY')
                CHGVAR     VAR(&SARD) VALUE('CFF003')
                CALL       PGM(AOSARDR0) PARM(&RtnCde &OPN &SARD &SFMT)
                IF         COND(&RtnCde *EQ '       ') THEN(DO)
                   OVRDBF     FILE(TRANS) MBR(OTC) SHARE(*NO)
                   ENDDO
                ELSE DO
                   OVRDBF     FILE(TRANS) MBR(OTC)
                ENDDO
                OVRDBF     FILE(TRSET) MBR(OTC)
                OVRDBF     FILE(POSTNC) MBR(OTC)
                OVRDBF     FILE(POSTNK) MBR(OTC)
                OVRDBF     FILE(CLOST) MBR(OTC)
                OVRDBF     FILE(CLOST2) MBR(OTC)
                OVRDBF     FILE(MEMOSM) MBR(OTC)
                OVRDBF     FILE(PRONOM) MBR(OTC)
                OVRDBF     FILE(FFACMVD) MBR(OTC)
                OVRDBF     FILE(TRANSA) MBR(OTC)
                OVRDBF     FILE(TRANSIN) TOFILE(TRANS) +
                             MBR(OTC) SHARE(*NO)
                OVRDBF     FILE(TRSETIN) TOFILE(TRSET) +
                             MBR(OTC) SHARE(*NO)
             ENDDO
 
/* Main details program */
 
             CALL       PGM(FF000250) PARM(&PACTN &PMRKT &PMKTN +
                          &PRUNA &PRUND &PDATF &PMBIN &PBUSD &PMKLC +
                          &PMLOC)
 
/* Deallocate market data area, + Delete file overrides */
             IF         COND(&PMRKT *NE '  ') THEN(DO)
                DLCOBJ     OBJ((&PMRKT *DTAARA *SHRRD))
             ENDDO
 
             DLTOVR     FILE(*ALL)
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                ROLLBACK
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(END)
             ENDDO
 
/* F5 taken if action code is blank */
 
             IF         COND(&PACTN *EQ ' ') THEN(GOTO INIT)
 
/* Submit job to update unrealised P&L */
             IF         COND(%SWITCH(XXXXXX00)) THEN(DO)
 
/* Pass the market parameter as 'OTC' for OTC's */
                IF         COND(&PMRKT *EQ '  ') THEN(DO)
                   CHGVAR     VAR(&MARKT) VALUE('OTC')
                   ENDDO
                ELSE       CMD(DO)
                   CHGVAR     VAR(&MARKT) VALUE(&PMRKT)
                ENDDO
 
                CHGVAR     VAR(&SBJOB) VALUE('CALL FFC0221 +
                        PARM(' *CAT &MARKT *CAT ')')
                SNDPGMMSG  MSG('MIDAS') TOMSGQ(MSTATUS)
/************   SBMJOB     JOB('RVAL' *CAT &MARKT) JOBD(MBATCH) +                         /*CSC023*/
/************             JOBQ(FFJQRV) USER(*JOBD) RTGDTA(*JOBD) +                        /*CSC023*/
/************             RQSDTA(&SBJOB) INLLIBL(*JOBD) MSGQ(MOPERQ)                      /*CSC023*/
                SBMJOB     JOB('RVAL' *CAT &MARKT) JOBD(MBATCH) +
                          JOBQ(FFJQRV) USER(*JOBD) RTGDTA(*JOBD) +
                          RQSDTA(&SBJOB) INLLIBL(*JOBD) MSGQ(MOPERQ) OUTQ(*JOBD)          /*CSC023*/
/************   SBMJOB     JOB(MIDASRMV) JOBD(MBATCH) JOBQ(FFJQRV) +                      /*CSC023*/
/************             USER(*JOBD) RTGDTA(*JOBD) RQSDTA('CALL +                        /*CSC023*/
/************             MIDASRMV') INLLIBL(*JOBD) MSGQ(MOPERQ)                          /*CSC023*/
                SBMJOB     JOB(MIDASRMV) JOBD(MBATCH) JOBQ(FFJQRV) +
                          USER(*JOBD) RTGDTA(*JOBD) RQSDTA('CALL +
                          MIDASRMV') INLLIBL(*JOBD) MSGQ(MOPERQ) OUTQ(*JOBD)              /*CSC023*/
             ENDDO
 
             GOTO       CMDLBL(END)
 
 ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          FFC000240 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
 
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited')
             ENDPGM
