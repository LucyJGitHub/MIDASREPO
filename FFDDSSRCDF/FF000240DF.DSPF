      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Closing Trades Inputs screen formats')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  FF000240DF - Closing Trades Input Screen                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 28Apr04               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CFF007  *CREATE    Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA03 - Webfacing Changes.                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           INVITE is in this DSPF but is not used.             *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03
      *****************************************************************
     A          R FF000240F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      ** Start of CAAA02
     A**07**************                    INVITE
      ** End of CAAA02
     A                                      PUTOVR
      *
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'FF000240F1'
     A                                  1 30'CLOSING TRADES INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #1MRDT    R        O  1 72REFFLD(RUNA)
      *
     A            #1JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3  2'Action Code'
     A            #1ACTN         1   B  3 14DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  3 18'Market'
     A            #1MRKT         2   B  3 26DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  3 29'(Leave blank for OTC)'
      *
     A                                  4  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
     A            #1ERCD1       77   O 22  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
      *
     A                                 24  2'F3=Exit'
      *
      *****************************************************************
     A          R FF000240F2                TEXT('Trans. Clos. Prompt')
      *
      ** Start of CAAA02
     A**07**************                    INVITE
      ** End of CAAA02
     A                                      KEEP
     A                                      PUTOVR
     A                                      CA09
      *
      *
     A            #2USER    R        O  1  2REFFLD(USER)
     A                                  1 13'FF000240F2'
     A                                  1 30'CLOSING TRADES INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #2MRDT    R        O  1 72REFFLD(RUNA)
      *
     A            #2JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3  2'Action Code'
     A            #2ACTN         1   O  3 14DSPATR(HI)
      *
     A            #2MKTN        20   O  3 19DSPATR(HI)
      *
     A                                  5  2'Transaction Reference'
     A            #2TNBR         6   B  5 24DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  5 32'Closing Trade'
     A            #2CLSR         6   B  5 46DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  6  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
     A            #2ERCD1       77   O 22  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
     A            #2ERCD2       77   O 23  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
      *
     A                                 24  2'F3=Exit'
     A                                 24 12'F9=Change Action/Market'
      *
      *****************************************************************
     A          R FF000240F3                  TEXT('Trans. Num. Prompt')
      *
      ** Start of CAAA02
     A**07**************                    INVITE
      ** End of CAAA02
     A                                      KEEP
     A                                      PUTOVR
     A                                      CA09
      *
      *
     A            #3USER    R        O  1  2REFFLD(USER)
     A                                  1 13'FF000240F3'
     A                                  1 30'CLOSING TRADES INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #3MRDT    R        O  1 72REFFLD(RUNA)
      *
     A            #3JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3  2'Action Code'
     A            #3ACTN         1   O  3 14DSPATR(HI)
      *
     A            #3MKTN        20   O  3 19DSPATR(HI)
      *
     A                                  5  2'Transaction Reference'
     A            #3TNBR         6   B  5 24DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  6  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
     A            #3ERCD1       77   O 22  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
      *
     A                                 24  2'F3=Exit'
     A                                 24 12'F9=Change Action/Market'
      *
      *****************************************************************
     A          R FF000240F4                  TEXT('Insert Mkt Inst. Details')
      *
     A                                      KEEP
     A                                      PUTOVR
     A                                      CA12
     A                                      CA10
     A                                      CA09
      *
      *
     A            #4USER    R        O  1  2REFFLD(USER)
     A                                  1 13'FF000240F4'
     A                                  1 30'CLOSING TRADES INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #4MRDT    R        O  1 72REFFLD(RUNA)
      *
     A            #4JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3  2'Action Code'
     A            #4ACTN         1   O  3 14DSPATR(HI)
      *
     A            #4MKTN        20   O  3 19DSPATR(HI)
      *
     A                                  4  2'Transaction Reference'
     A            #4TNBR         6   O  4 24DSPATR(HI)
      *
     A                                  4 32'Closing Trade'
     A            #4CLSR         6   O  4 46DSPATR(HI)
     A                                      OVRDTA
     A                                  4 63'DELETED'
     A  72                                  DSPATR(HI RI)
     A N72                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                  5  2'Market'
     A            #4MRKT         2   O  5 12DSPATR(HI)
      *
     A                                  5 17'Instrument'
     A            #4ISTC         3   O  5 31DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5 43'Month'
     A            #4MTHN         3   O  5 50DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5 56'Year'
     A            #4YRNO         2   O  5 62DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6  2'Put/Call'
     A            #4PCAL         1   O  6 12DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6 17'Strike Price'
     A            #4STRP        16   O  6 31DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6 50'Portfolio Ref.'
     A N23                                  DSPATR(ND)
     A            #4PTFR         4   O  6 66DSPATR(HI)
     A N23                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  7  2'Purchase/Sale'
     A            #4TRTY         1   O  7 16DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  7 20'Number of Contracts'
     A                                  7 40': Original'
     A            #4NUCO         5   O  7 51DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  7 60'To Be Left Open'
     A            #4TBLO         1   O  7 76DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  8 20'Number of Contracts'
     A                                  8 40': Open'
     A            #4NOCO         5   O  8 51DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  9  2'Broker'
     A            #4BOCO        10   O  9 10DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  9 24'Customer'
     A            #4CUSC        10   O  9 34DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  9 48'Book'
     A            #4BOKC         2   O  9 54DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  9 60'Branch'
     A  64                                  DSPATR(ND)
     A            #4BRCA         3   O  9 68DSPATR(HI)
     A  64                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 10  2'Number of Contracts'
     A                                 10 22'to Close'
     A            #4NCTE         5   B 10 35DSPATR(HI)
     A  02N35N11                            DSPATR(UL)
     A N02                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 10 42'Closing Price'
     A            #4EXPR        16   B 10 58DSPATR(HI)
     A  02N30N11                            DSPATR(UL)
     A N02                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11  2'Transaction Date'
     A            #4TRSD         6   B 11 19DSPATR(HI)
     A  02N31N11                            DSPATR(UL)
     A N02                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11 26'Value Date'
     A            #4VALD         6   B 11 37DSPATR(HI)
     A  02N68N11                            DSPATR(UL)
     A N02                                  DSPATR(PR)
     A  68                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11 44'Narrative'
     A            #4TNAR        25   B 11 54DSPATR(HI)
     A  13N11                               DSPATR(UL)
     A N13                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                               O 12  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
     A                                 13 30'BROKER'
     A N14                                  DSPATR(ND)
     A                                      OVRATR
      **Start of change CGL029
     A**********                       13 52'CUSTOMER'                                        CGL029
     A                                 13 56'CUSTOMER'
      **End of change CGL029
     A N15                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 14  2'Commission Code: Std/Same'
      *
     A            #4BCMC         2   B 14 30DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N40
     AO 03 65
     AAN11N40                               DSPATR(UL)
     A  40                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A            #4BCMS         2   B 14 34DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N41
     AO 03 65
     AAN11N41                               DSPATR(UL)
     A  41                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CCMC         2   B 14 52DSPATR(HI)                                        CGL029
     A            #4CCMC         2   B 14 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N50
     AO 03 65
     AAN11N50                               DSPATR(UL)
     A  50                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CCMS         2   B 14 56DSPATR(HI)                                        CGL029
     A            #4CCMS         2   B 14 60DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N51
     AO 03 65
     AAN11N51                               DSPATR(UL)
     A  51                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 15  2'Commission Amount'
      *
     A            #4BCMA        14   B 15 30DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N42
     AO 03 65
     AAN11N42                               DSPATR(UL)
     A  42                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CCMA        14   B 15 52DSPATR(HI)                                        CGL029
     A            #4CCMA        14   B 15 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N52
     AO 03 65
     AAN11N52                               DSPATR(UL)
     A  52                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 16  2'Charge Code/Amount'
     A            #4BCHC         2   B 16 30DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N43
     AO 03 65
     AAN11N43                               DSPATR(UL)
     A  43                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A            #4BCHA        14   B 16 34DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N44
     AO 03 65
     AAN11N44                               DSPATR(UL)
     A  44                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CCHC         2   B 16 52DSPATR(HI)                                        CGL029
     A            #4CCHC         2   B 16 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N53
     AO 03 65
     AAN11N53                               DSPATR(UL)
     A  53                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CCHA        14   B 16 56DSPATR(HI)                                        CGL029
     A            #4CCHA        14   B 16 60DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N54
     AO 03 65
     AAN11N54                               DSPATR(UL)
     A  54                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 17  2'Settlement Type'
     A            #4BSLT         2   B 17 30DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N46
     AO 03 65
     AAN11N46                               DSPATR(UL)
     A  46                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CSLT         2   B 17 52DSPATR(HI)                                        CGL029
     A            #4CSLT         2   B 17 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N56
     AO 03 65
     AAN11N56                               DSPATR(UL)
     A  56                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 18  2'Settlement Account'
     A            #4BSBR         3   B 18 30DSPATR(HI)
     A  04
     AO 05
     AO 64
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N45
     AO 03 65
     AAN11N45                               DSPATR(UL)
     A  45N64                               DSPATR(PC RI)
     A  64
     AON14                                  DSPATR(ND)
     A                                      OVRDTA
      **Start of change CGL029
     A**********  #4BSLA        12   B 18 34DSPATR(HI)                                        CGL029
     A            #4BSLA        18   B 18 34DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N47
     AO 03 65
     AAN11N47                               DSPATR(UL)
     A  47                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      **Start of change CGL029
     A**********  #4CSBR         3   B 18 52DSPATR(HI)                                        CGL029
     A            #4CSBR         3   B 18 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AO 64
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N55
     AO 03 65
     AAN11N55                               DSPATR(UL)
     A  55N64                               DSPATR(PC RI)
     A  64
     AON15                                  DSPATR(ND)
     A                                      OVRDTA
      **Start of change CGL029
     A**********  #4CSLA        12   B 18 56DSPATR(HI)                                        CGL029
     A            #4CSLA        18   B 18 60DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N57
     AO 03 65
     AAN11N57                               DSPATR(UL)
     A  57                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 19  2'Counterparty Nostro'
     A            #4BCPN        10   B 19 30DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N49
     AO 03 65
     AAN11N49                               DSPATR(UL)
     A  49                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CCPN        10   B 19 52DSPATR(HI)                                        CGL029
     A            #4CCPN        10   B 19 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N59
     AO 03 65
     AAN11N59                               DSPATR(UL)
     A  59                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 20  2'For Account Of'
     A            #4BFAO        10   B 20 30DSPATR(HI)
     A  04
     AO 05
     AON14
     AO 03N65                               DSPATR(PR)
     A  02N11N48
     AO 03 65
     AAN11N48                               DSPATR(UL)
     A  48                                  DSPATR(PC RI)
     A N14                                  DSPATR(ND)
     A                                      OVRDTA
      *
      **Start of change CGL029
     A**********  #4CFAO        10   B 20 52DSPATR(HI)                                        CGL029
     A            #4CFAO        10   B 20 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AON15
     AO 03N65                               DSPATR(PR)
     A  02N11N58
     AO 03 65
     AAN11N58                               DSPATR(UL)
     A  58                                  DSPATR(PC RI)
     A N15                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A            #4ERCD1       77   O 22  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
      *
     A            #4ERCD2       77   O 23  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
      *
     A                                 24  2'F3=Exit'
     A N04                             24 12'F9=Change Action/Market   -
     A                                      F12=Previous'
     A  04                             24 12'F9=Change Action/Market   F10=Dele-
     A                                      te   F12=Previous'
      *
      *****************************************************************
     A          R FF000240F5                  TEXT('Insert O.T.C. Details')
      *
     A                                      KEEP
     A                                      PUTOVR
     A                                      CA12
     A                                      CA10
     A                                      CA09
      *
      *
     A            #5USER    R        O  1  2REFFLD(USER)
     A                                  1 13'FF000240F5'
     A                                  1 30'CLOSING TRADES INPUT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE'
     A            #5MRDT    R        O  1 72REFFLD(RUNA)
      *
     A            #5JOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A                                  3  2'Action Code'
     A            #5ACTN         1   O  3 14DSPATR(HI)
      *
     A            #5MKTN        20   O  3 19DSPATR(HI)
      *
     A                                  4  2'Transaction Reference'
     A            #5TNBR         6   O  4 24DSPATR(HI)
      *
     A                                  4 32'Closing Trade'
     A            #5CLSR         6   O  4 46DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  4 63'DELETED'
     A  72                                  DSPATR(HI RI)
     A N72                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                  5  2'OTC Type'
     A            #5ISTT         5   O  5 12DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6  2'Put/Call'
     A            #5PCAL         1   O  6 12DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  6 17'Strike Price'
     A            #5STRP        16   O  6 31DSPATR(HI)
     A                                      OVRDTA
     A                                  6 50'Portfolio Ref.'
     A N23                                  DSPATR(ND)
     A            #5PTFR         4   O  6 66DSPATR(HI)
     A N23                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                  7  2'Purchase/Sale'
     A            #5TRTY         1   O  7 16DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  7 20'Number of Contracts'
     A                                  7 40': Original'
     A            #5NUCO         5   O  7 51DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  7 60'To Be Left Open'
     A            #5TBLO         1   O  7 76DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  8 20'Number of Contracts'
     A                                  8 40': Open'
     A            #5NOCO         5   O  8 51DSPATR(HI)
     A                                      OVRDTA
      *
     A  17                              9  2'C''party'
     A N17                              9  2'Customer'
     A            #5CUSC        10   O  9 14DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  9 26'Book'
     A            #5BOKC         2   O  9 32DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  9 38'Branch'
     A  64                                  DSPATR(ND)
     A            #5BRCA         3   O  9 46DSPATR(HI)
     A  64                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 10  2'Number of Contracts'
     A                                 10 22'to Close'
     A            #5NCTE         5   B 10 35DSPATR(HI)
     A  02N35N11                            DSPATR(UL)
     A N02                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 10 42'Closing Price'
     A            #5EXPR        16   B 10 58DSPATR(HI)
     A  02N30N11                            DSPATR(UL)
     A N02                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11  2'Trade Date'
     A            #5TRSD         6   B 11 14DSPATR(HI)
     A  02N31N11                            DSPATR(UL)
     A N02                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11 26'Value Date'
     A N29                                  DSPATR(ND)
     A            #5VALD         6   B 11 37DSPATR(HI)
     A  02N39N11
     AAN68                                  DSPATR(UL)
     A N02
     AON29                                  DSPATR(PR)
     A  39
     AO 68                                  DSPATR(PC RI)
     A N29                                  DSPATR(ND)
     A                                      OVRDTA
      *
     A                                 11 44'Narrative'
     A            #5TNAR        25   B 11 54DSPATR(HI)
     A  13N11                               DSPATR(UL)
     A N13                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A                               O 12  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
     A  15 17                          13 52'COUNTERPARTY'
     A  15N17                          13 52'CUSTOMER'
      *
     A                                 14  2'Commission Code: Std/Same'
      *
     A  15        #5CCMC         2   B 14 52DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N50
     AO 03 65
     AAN11N50                               DSPATR(UL)
     A  50                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  15        #5CCMS         2   B 14 56DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N51
     AO 03 65
     AAN11N51                               DSPATR(UL)
     A  51                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 15  2'Commission Amount'
      *
     A  15        #5CCMA        14   B 15 52DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N52
     AO 03 65
     AAN11N52                               DSPATR(UL)
     A  52                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 16  2'Charge Code/Amount'
      *
     A  15        #5CCHC         2   B 16 52DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N53
     AO 03 65
     AAN11N53                               DSPATR(UL)
     A  53                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  15        #5CCHA        14   B 16 56DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N54
     AO 03 65
     AAN11N54                               DSPATR(UL)
     A  54                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 17  2'Settlement Type'
      *
     A  15        #5CSLT         2   B 17 52DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N56
     AO 03 65
     AAN11N56                               DSPATR(UL)
     A  56                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 18  2'Settlement Account'
     A  15        #5CSBR         3   B 18 52DSPATR(HI)
     A  04
     AO 05
     AO 64
     AO 03N65                               DSPATR(PR)
     A  02N11N55
     AO 03 65
     AAN11N55                               DSPATR(UL)
     A  55N64                               DSPATR(PC RI)
     A  64                                  DSPATR(ND)
     A                                      OVRDTA
      **Start of change CGL029
     A**15******  #5CSLA        12   B 18 56DSPATR(HI)                                        CGL029
     A  15        #5CSLA        18   B 18 56DSPATR(HI)
      **End of change CGL029
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N57
     AO 03 65
     AAN11N57                               DSPATR(UL)
     A  57                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 19  2'Counterparty Nostro'
      *
     A  15        #5CCPN        10   B 19 52DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N59
     AO 03 65
     AAN11N59                               DSPATR(UL)
     A  59                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 20  2'For Account Of'
     A  15        #5CFAO        10   B 20 52DSPATR(HI)
     A  04
     AO 05
     AO 03N65                               DSPATR(PR)
     A  02N11N58
     AO 03 65
     AAN11N58                               DSPATR(UL)
     A  58                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A            #5ERCD1       77   O 22  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
      *
     A            #5SERCD2      77   O 23  2DSPATR(HI)
     A N18                                  DSPATR(ND)
      ** Start of Change CAAA03
     A**********                            DSPATR(RI)                                        CAAA03
     A  18                                  DSPATR(RI)
      ** End of Change CAAA03
     A                                      OVRDTA
      *
     A                                 24  2'F3=Exit'
     A N04                             24 12'F9=Change Action/Market   -
     A                                      F12=Previous'
     A  04                             24 12'F9=Change Action/Market   F10=Dele-
     A                                      te   F12=Previous'
      *
      *****************************************************************
     A          R FF000240F6                TEXT('Rec Upd by another wkstn')
      *
     A                                      OVERLAY
     A                                      CLRL(03)
     A                                      PROTECT
     A                                      CA09
      *
     A            SERCD1        77   O 22  2DSPATR(HI RI)
      *
     A            SERCD2        77   O 23  2DSPATR(HI RI)
      *
     A                                 24  2'F3=Exit   F9=Change Action/Market'
