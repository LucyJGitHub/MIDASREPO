      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Manual Closure Input Display File')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0050DF - MANUAL CLOSURE INITIAL PROGRAM                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CFF004             Date 21Sep96               *
      *                 S71062             Date 08Dec95               *
     A*                 S01457             DATE 31DEC93               *
     A*                 E39162             DATE 30MAY92               *
     A*                 FO0027             DATE 24JAN91               *
     A*                 S01117             DATE 04DEC90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CFF004 - Increase in size of Price Fields.                   *
     A*  S71062 - FF/PM Interface Manual closure                      *
     A*  S01457  -  Enable Brokers to deal with OTC's.                *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  FO0027  -  DISPLAY ACTION CODE 'I' ON SCREEN - THIS IS USED  *
     A*             AS DUMMY ACTION CODE FOR SPF VALIDATION IN FF0050 *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHECK(AB)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03(01 'F:3 - to end program')
     A**********                                     Begin /S01199/
     A**********                                           /*S01199*/
     A**********                                           /*S01199*/
     A*                                              End   /S01199/
     A          R FF0050F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A***********                                    Begin /S71062/
     A***********                                    End   /S71062/
     A*               Start of change                      /*S01457*/
     A            S1CUST        10   O 16 13TEXT('Customer/Counterpty')
     A*               End of change                        /*S01457*/
     A                                  1  2'MANUAL CLOSURE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O  1 55TEXT('Run-date in DDMMMYY Format')
     A                                  1 63TIME
     A                                  1 72'WID'
     A            WSID           3A  O  1 76
     A*                                                           Begin /FO0027/
     A                                  4  2'Action'
     A                                  4 10'I'
     A                                      DSPATR(HI)
     A*                                                             End /FO0027/
     A*                                              Begin /S01117/
     A***********                       4 24'Branch -'
     A*********** SBRCD          3A  B  4 33DSPATR(HI)
     A N28                              4 24'Branch -'
     A N28        SBRCD          3A  B  4 33DSPATR(HI)
     A*                                              End   /S01117/
     A  30                                  DSPATR(RI PC)
     A  28                                  DSPATR(PR)
     A N28N30                               DSPATR(UL)
     A                                      OVRDTA
     A                                  6 17'Market Centre -'
     A            SMKT           2A  B  6 33DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A N29 28
     AO 31                                  DSPATR(PC)
     A                                      OVRDTA
     A                                  6 39'Instrument Code -'
     A            SINS           3A  B  6 57DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 25'Month -'
     A            SMN            3A  B  8 33DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 39'Year -'
     A            SYR            2A  B  8 46DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10 25'OR'
     A                                 10 28'OTC code -'
     A            SOTC           5A  B 10 39DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 22'Put/Call -'
     A            SPTCL          1A  B 12 33DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 39'Strike Price -'
     A*                                                    Begin of /*CFF004*/
     A*********** SSTKPR        12A  B 12 55DSPATR(HI)
     A            SSTKPR        16A  B 12 55DSPATR(HI)
     A*                                                    End of   /*CFF004*/
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14 17'Broker short name/code -'
     A            SBRK          10A  B 14 42DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL)
     A                                      OVRDTA
     A*               Start of change                      /*S01457*/
     A**********                       16 15'Customer short name/code -'
     A                                 16 24'short name/code -'
     A*               End of change                        /*S01457*/
     A            SCUS          10A  B 16 42DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 18 30'Book code -'
     A            SBOK           2A  B 18 42DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL)
     A                                      OVRDTA
     A***********                                    Begin /S71062/
     A                                 20 20'Portfolio reference -'
     A N45                                  DSPATR(ND)
     A            SPTFR          4A  B 20 42
     A N45                                  DSPATR(ND PR)
     A                                      DSPATR(HI)
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A                                      OVRDTA
     A***********                                    End   /S71062/
     A            SERCD         78A  O 22  2
     A  29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(ND)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'Enter details,F:3 to end or Help'
