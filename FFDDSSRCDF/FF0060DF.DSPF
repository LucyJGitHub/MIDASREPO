      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Manual Closure Main Program')                 *
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0060DF - MANUAL CLOSURE MAIN PROGRAM                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. 261141             Date 16Jul09               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 112563             Date 12Feb97               *
     A*                 CFF004             Date 23SEP96               *
     A*                 104199             DATE 10JUN96               *
     A*                 S71062             DATE 08DEC95               *
     A*                 E39162             DATE 18JUN92               *
     A*                 S01117             DATE 04DEC90               *
     A*               . S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  261141 - Increase the size of SSTPR from 12A to 16A.         *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  CFF004 - Increase in size of Price Fields                    *
     A*  104199 - Add program name to DSPF for GUI identification     *
     A*  S71062 - FF/PM Interface - Manual closure                    *
     A*  E39162  -  CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*              AND INDICATORS AS IT IS NOT SUPPORTED            *
     A*              ON THE AS400)                                    *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(10 'ANY VALID KEY')
     A                                      INDARA
     A                                      CHECK(AB)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A                                      CA12
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0060F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  2'MANUAL CLOSURE'
     A                                      DSPATR(HI UL)
     A***                                            START /*104199*/
     A                                  1 19'FF0060'
     A***                                            END   /*104199*/
     A            RUNA           7A  O  1 55TEXT('Run-date in DDMMMYY Format')
     A                                  1 64TIME
     A                                  1 73'WID'
     A            WSID           3A  O  1 77
     A*                                              Begin /S01117/
     A***********                       2  2'Branch'
     A*********** BRNM          30A  O  2 14DSPATR(HI)
     A  17                              2  2'Branch'
     A  17        BRNM          30A  O  2 14DSPATR(HI)
     A*                                              End   /S01117/
     A***********                                    Begin /S71062/
     A            SNARR3         9A  O  2 49
     A            SDETL3        20A  O  2 60DSPATR(HI)
     A***********                                    End   /S71062/
     A                                  3  2'Market'
     A            MKTN          20A  O  3 14DSPATR(HI)
     A***********                                    Begin /S71062/
     A*********** SNARR1         8A  O  3 49
     A            SNARR1         9A  O  3 49
     A***********                                    End   /S71062/
     A            SDETL1        20A  O  3 60DSPATR(HI)
     A                                  4  2'Instrument'
     A            ISTN          20A  O  4 14DSPATR(HI)
     A***********                                    Begin /S71062/
     A*********** SNARR2         8A  O  4 49
     A            SNARR2         9A  O  4 49
     A***********                                    End   /S71062/
     A            SDETL2        20A  O  4 60DSPATR(HI)
     A                                  5  2'Period'
     A            SMN            3A  O  5 14DSPATR(HI)
     A            SYR            2A  O  5 18DSPATR(HI)
     A            SPTCL          1A  O  5 21DSPATR(HI)
      ** Start of change 261141
     A**********  SSTPR         12A  O  5 23DSPATR(HI)                                        261141
     A            SSTPR         16A  O  5 23DSPATR(HI)
      ** End of change 261141
     A                                  6  2'Date'
     A*                                                      Start of /*CFF004*/
     A***********                       6  8'Reference'
     A***********                       6 19'P/S'
     A***********                       6 23'Price'
     A                                  6 10'Ref:'
     A                                  6 16'P/S'
     A                                  6 20'Price'
     A*                                                      End of   /*CFF004*/
     A                                  6 36'Narrative'
     A                                  5 60'Leave'
     A                                  6 60'Open'
     A                                  6 66'Open'
     A                                  6 73'To Close'
     A                                  5 68'Contracts'
     A                                  7  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A*
     A          R FF0060S0                  SFL
     A*
     A                                      SFLNXTCHG
     A            STRSD          7A  O  8  2DSPATR(HI)
     A*                                                      Start of /*CFF004*/
     A*********** TNBR           6S 0O  8 11DSPATR(HI)
     A*********** TRTY           1A  O  8 20DSPATR(HI)
     A*********** SCOPR         12A  O  8 23DSPATR(HI)
     A            TNBR           6S 0O  8 10DSPATR(HI)
     A            TRTY           1A  O  8 17DSPATR(HI)
     A            SCOPR         16A  O  8 19DSPATR(HI)
     A*                                                      End of   /*CFF004*/
     A            TNAR          25A  O  8 36DSPATR(HI)
     A            TBLO           1A  O  8 63DSPATR(HI)
     A            SNOCT          5Y 0O  8 66EDTCDE(Z)
     A                                      DSPATR(HI)
     A            SCLOS          5A  B  8 73DSPATR(HI)
     A                                      DSPATR(PC)
     A  20                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A*                                              Begin /S01117/
     A            SORBR          3   H      TEXT('ORGINATING BR')
     A*                                              End   /S01117/
     A          R FF0060C0                  SFLCTL(FF0060S0)
     A*
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A N40                                  SFLDSP
     A N41                                  SFLDSPCTL
     A  42                                  SFLCLR
     A N41                                  SFLEND
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A            TRRN           4  0H      SFLRCDNBR(CURSOR)
     A                                 20  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A            MESS          78A  O 21  2TEXT('MESSAGES')
     A N22                                  DSPATR(ND)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(HI)
     A                                      OVRDTA
     A            SERCD         78A  O 22  2TEXT('ERROR CODE FIELD')
     A N23                                  DSPATR(ND)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(HI)
     A                                      OVRDTA
     A**                                             Begin /112563/
     A***********                      23  2'Enter details,F:3 to end, F:12 for-
     A***********                            initial screen, Rollup, Rolldown o-
     A***********                           r Help'
     A                                 23  2'Enter details  F3=Exit  F12=Initia-
     A                                      l screen   Rollup/Rolldown  or Help'
     A**                                             End   /112563/
     A*
     A          R FF0060F1
     A*
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A                                  8  5'.'
     A                                      DSPATR(ND)
     A                                 10  2'CLOSE OUT SET READY FOR CREATION'
     A                                 11  2'TOTAL PURCHASES/SALES'
     A            NCLOS          6Y 0O 11 37EDTCDE(Z)
     A**                                             Begin /112563/
     A***********                      23  2'Press Enter to continue, F:3 to -
     A***********                           end, F:12 for initial screen'
     A                                 23  2'Enter to continue   F3=Exit  F12=-
     A                                      Initial screen'
     A**                                             End   /112563/
     A*
     A          R FF0060F2
     A*
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A                                 13  2'CLOSE OUT SET CREATED'
     A                                 14  2'CLOSE OUT NUMBER ASSIGNED'
     A            CLON           6Y 0O 14 37
     A                                 15  2'TOTAL PURCHASES/SALES'
     A            NCLOS          6Y 0O 15 37EDTCDE(Z)
     A                                 16  2'REALISED PROFIT/LOSS'
     A            SPRLS         22A  O 16 25
     A            ISCY           3A  O 16 49
     A**                                             Begin /112563/
     A***********                      23  2'F:3 to end, F:12 for initial sc-
     A***********                           reen '
     A                                 23  2'F3=Exit  F12=Initial screen'
     A**                                             End   /112563/
     A*
     A          R FF0060F3
     A*
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A                                 13  2'CLOSE OUT SET NOT CREATED'
     A**                                             Begin /112563/
     A***********                      23  2'F:3 to end, F:12 for initial sc-
     A***********                           reen '
     A                                 23  2'F3=Exit  F12=Initial screen'
     A**                                             End   /112563/
     A*
