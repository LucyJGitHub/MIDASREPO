      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Close Out Set Enq/Canc Initial Screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0070DF - CLOSE OUT SET ENQUIRY/CANCELATION INITIAL SCREEN  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHECK(AB)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03(01 'Cmd/3 - to end program')
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0070F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A*
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'CLOSE OUT SET ENQUIRY/CANCELLATION'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7A  O  1 55TEXT('Run-date in DDMMMYY Format')
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A*
     A                                 20  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 22  2'Action:'
     A            SACTN          1A  B 22 10DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 22 12'Close Out Set Number'
     A            SCLON          6A  B 22 33DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A                                      OVRDTA
     A            MSG           76   O 23  2
     A  20                                  DSPATR(RI HI)
     A                                      OVRDTA
     A                                 24  2'Enter details,F:3 to end or HELP'
