      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Close Out SEt Enq/Canc')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0080DF - CLOSE OUT SET ENQUIRY/CANCELATION                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CFF004             DATE 23SEP96               *
     A*  PREV AMEND NO. 104633             DATE 15JUL96               *
     A*                 S71062             DATE 19DEC95               *
     A*                 S01117             DATE 04DEC90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CFF004  -  Increase in size of Price Fields.                 *
     A*  104633  -  GUI Amendments to DSPF with non-standard footers  *
     A*  S71062  -  FF/PM INTERFACE - ADD PORTFOLIO NAME              *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************   S01117
     A*                                                                   S01117
     A*         INDICATOR  SUMMARY                                        S01117
     A*        ~~~~~~~~~~~~~~~~~~~~                                       S01117
     A*   53    MBIN = 'Y'                                                S01117
     A*                                                                   S01117
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHECK(AB)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A**********                                     Begin /S01199/
     A**********                                     End   /S01199/
     A                                      CA03(01 'Exit Program.')
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0080F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A  30                              1  2'CLOSE OUT SET CANCELLATION'
     A                                      DSPATR(HI UL)
     A N30                              1  2'CLOSE OUT SET ENQUIRY'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55TEXT('Run-date in DDMMMYY Format')
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A*                                              Begin /S01117/
     A***********                       2  2'Branch'
     A  53                              2  2'Branch'
     A*                                              End   /S01117/
     A                                  3  2'Market'
     A                                  4  2'Instrument'
     A                                  5  2'Period'
     A                                  6  2'CLOSE OUT NUMBER'
     A*                                              Begin /S01117/
     A*********** BRNM          30A  O  2 14DSPATR(HI)
     A  53        BRNM          30A  O  2 14DSPATR(HI)
     A*                                              End   /S01117/
     A            MKTN          20A  O  3 14DSPATR(HI)
     A            ISTN          20A  O  4 14DSPATR(HI)
     A            SMTHDS         3A  O  5 14DSPATR(HI)
     A            SYRNO          2A  O  5 18DSPATR(HI)
     A            PCAL           1A  O  5 21DSPATR(HI)
     A*                                                    Begin of /*CFF004*/
     A*********** SSTRP         12   O  5 23DSPATR(HI)
     A            SSTRP         16   O  5 23DSPATR(HI)
     A*                                                    End of   /*CFF004*/
     A            CLON           6S 0O  6 22DSPATR(HI)
     A                                  6 35'Realised Profit/Loss'
     A            SCOPL         19   O  6 57DSPATR(HI)
     A            SISCY          3   O  6 77DSPATR(HI)
     A***********                                    Begin /S71062/
     A************NAR1           8   O  3 51
     A************NAR2           8   O  4 51
     A            NAR1           8   O  3 50
     A            NAR2           8   O  4 50
     A***********                                    End   /S71062/
     A            DET1          20   O  3 60DSPATR(HI)
     A            DET2          20   O  4 60DSPATR(HI)
     A***********                                    Begin /S71062/
     A                                  5 50'Portfolio'
     A  02                                  DSPATR(ND)
     A            DDPTFN        20   O  5 60DSPATR(HI)
     A  02                                  DSPATR(ND)
     A***********                                    End   /S71062/
     A*
     A          R FF0080S0                  SFL
     A            STRDT          7   O  8  2DSPATR(HI)
     A*                                                    Begin of /*CFF004*/
     A*********** STNBR          6S 0O  8 11
     A***********                           DSPATR(HI)
     A*********** STRTY          1A  O  8 20
     A***********                           DSPATR(HI)
     A*********** SCOPR         12   O  8 23DSPATR(HI)
      *
     A            STNBR          6S 0O  8 10
     A                                      DSPATR(HI)
     A            STRTY          1A  O  8 17
     A                                      DSPATR(HI)
     A            SCOPR         16   O  8 19DSPATR(HI)
     A*                                                    End of   /*CFF004*/
     A            STNAR         25A  O  8 36
     A                                      DSPATR(HI)
     A            SFCIN          1   O  8 63DSPATR(HI)
     A            SNCCL          5  0O  8 73EDTCDE(3)
     A                                      DSPATR(HI)
     A*
     A          R FF0080C0                  SFLCTL(FF0080S0)
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  20                                  SFLEND
     A*                                                    Begin of /*CFF004*/
     A***********                       7  2' Date  Reference P/S     Price    -
     A                                  7  2' Date   Ref:  P/S  Price          -
     A*                                                    End of   /*CFF004*/
     A                                      Narrative          Final Closure  C-
     A                                      ontracts '
     A                                      DSPATR(UL)
     A*
     A          R FF0080F1
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      ******                                        Start  /104633/
     A******                           23  2'F:10 is required to cancel this C-
     A******                                lose Out Set                       -
     A******                                         '
     A                                 23  2'F10=Cancel this Close Out Set'
      ******                                          End  /104633/
     A                                      DSPATR(RI HI)
     A*
     A          R FF0080F2
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA10(04 'Cancel Close Out Set.')
     A                                      CA12(05 'Restart.')
      ******                                        Start  /104633/
     A******                           24  2'F:3 to end, F:10 to cancel clo-
     A******                                se out set, F:12 to Restart, Rol-
     A******                                l or Help'
     A                                 24  2'F3=Exit   F10=Cancel Clo-
     A                                      se out set   F12=Previous   Rol-
     A                                      l or Help'
      ******                                          End  /104633/
     A*
     A          R FF0080F3
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CA12(05 'Restart.')
      ******                                        Start  /104633/
     A******                           24  2'F:3 to end, F:12 to Restart, -
     A******                                Rollup, Rolldown or Help.'
     A                                 24  2'F3=Exit   F12=Previous   -
     A                                      Rollup/Rolldown  or  Help'
      ******                                         End  /104633/
     A*
     A          R FF0080F4
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  1' '
      ******                                        Start  /104633/
     A******                           24  2'Press Enter to continue, F:3 to-
     A******                                 end or Help'
     A                                 24  2'F3=Exit   Press Enter to continue-
     A                                         or  Help'
      ******                                          End  /104633/
     A*
     A          R FF0080F5
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A  70                             15 25'CLOSE OUT SET CANCELLED'
     A                                      OVRATR
     A N70                             12 23'CLOSE OUT SET NOT CANCELLED'
     A                                      DSPATR(BL)
     A                                      OVRATR
