      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Market Centres Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0110DF -  MARKET CENTRES MAINTENANCE                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG9769            Date 03Jan06               *
      *  Prev Amend No. BUG8619            Date 28Oct05               *
      *                 BUG8045            Date 02Aug05               *
      *                 BUG3807            Date 02Feb05               *
      *                 BUG2384            Date 06May04               *
      *                 CAAA03             Date 12Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 14Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 112563             Date 21Jan97               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01117             DATE 20FEB90               *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  BUG9769 - Remove universal F10                               *
      *  BUG8619 - Enable Function 10 (F10).                          *
      *  BUG8045 - F10 does not work properly                         *
      *  BUG3807 - Enable Function 12 (F12).                          *
      *  BUG2384- Midasplus webfacing changes. Adjusted month labels. *
      *  CAAA03 - Webfacing Changes                                   *
     A*  CPB001  -  'Private Banking' Module                          *
     A*  112563  -  Header and footer changes required for GUI        *
     A*             Multilanguage support.                            *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A***********                                    Begin /S01199/
     A***********                                    End   /S01199/
     A                                      CA03
      **Start of change BUG8619
      **Start of change BUG9769
     A*N23******                            CA10
      **End of change BUG9769
      **End of change BUG8619
      **Start of change BUG3807
     A N23                                  CA12
      **End of change BUG3807
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A** FORMAT FF0110F0 **
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0110F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      **Start of change BUG8045
     A N23 24                               CF10
      **End of change BUG8045
     A                                      RTNDTA
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                    /S01179/
     A**********                            ROLLDOWN                  /S01179/
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /CPB001/
     A*                                              End   /CPB001/
     A*                                              End   /S01199/
     A            DELT           7      5 72TEXT('RECORD DELETED')
     A                                      DSPATR(HI BL)
     A                                      OVRDTA
     A                                  7  2'Name'
     A            SMKTN         20A  B  7 18TEXT('NAME')
     A                                      DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'Country'
     A            SMKCN          2A  B  9 18TEXT('COUNTRY')
     A                                      DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                              Begin /S01117/
     A                                  9 40'Location'
     A            SMLOC          3A  B  9 50TEXT('LOCATION')
     A                                      DSPATR(HI)
     A N06N29                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  29                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                              End   /S01117/
     A                                 11  2'Local Currency'
     A            SMKLC          3A  B 11 18TEXT('LOCAL CURRENCY')
     A                                      DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 13  2'Price Reference'
     A            SMPRF         10A  B 13 18TEXT('PRICE REFERENCE')
     A                                      DSPATR(HI)
     A N06                                  DSPATR(UL)
     A  06                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15  2'Automatic EOC'
     A            SAEOC          1A  B 15 18TEXT('AUTOMATIC EOC')
     A                                      DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 17  2'Auto Prices'
     A            SAPRP          1A  B 17 18TEXT('AUTO PRICES')
     A                                      DSPATR(HI)
     A N06N35                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                              Begin /CPB001/
     A                                 17 40'Instrument'
     A  70                                  DSPATR(ND)
     A            SINNR          3A  B 17 51TEXT('Instrument Type')
     A                                      DSPATR(HI)
     A N06N38N70                            DSPATR(UL)
     A  70
     AO 06                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                              End   /CPB001/
      ** Start of change BUG2384
     A**********                       19 17'JAN FEB MAR APR MAY JUN JUL AUG SE-             BUG2384
     A**********                            P OCT NOV DEC'                                   BUG2384
     A                                 19 17'JAN'
     A                                 19 21'FEB'
     A                                 19 25'MAR'
     A                                 19 29'APR'
     A                                 19 33'MAY'
     A                                 19 37'JUN'
     A                                 19 41'JUL'
     A                                 19 45'AUG'
     A                                 19 49'SEP'
     A                                 19 53'OCT'
     A                                 19 57'NOV'
     A                                 19 61'DEC'
      ** End of change BUG2384
     A                                 20  2'Months quoted'
     A            SMQ01          1   B 20 18TEXT('JAN')
     A                                      DSPATR(HI)
     A N40N36                               DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SMQ02          1   B 20 22TEXT('FEB')
     A                                      DSPATR(HI)
     A N41N36                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ03          1   B 20 26TEXT('MAR')
     A                                      DSPATR(HI)
     A N42N36                               DSPATR(UL)
     A  42                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ04          1   B 20 30TEXT('APR')
     A                                      DSPATR(HI)
     A N43N36                               DSPATR(UL)
     A  43                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ05          1   B 20 34TEXT('MAY')
     A                                      DSPATR(HI)
     A N44N36                               DSPATR(UL)
     A  44                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ06          1   B 20 38TEXT('JUN')
     A                                      DSPATR(HI)
     A N45N36                               DSPATR(UL)
     A  45                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ07          1   B 20 42TEXT('JUL')
     A                                      DSPATR(HI)
     A N46N36                               DSPATR(UL)
     A  46                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ08          1   B 20 46TEXT('AUG')
     A                                      DSPATR(HI)
     A N47N36                               DSPATR(UL)
     A  47                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ09          1   B 20 50TEXT('SEP')
     A                                      DSPATR(HI)
     A N48N36                               DSPATR(UL)
     A  48                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ10          1   B 20 54TEXT('OCT')
     A                                      DSPATR(HI)
     A N49N36                               DSPATR(UL)
     A  49                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ11          1   B 20 58TEXT('NOV')
     A                                      DSPATR(HI)
     A N50N36                               DSPATR(UL)
     A  50                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ12          1   B 20 62TEXT('DEC')
     A                                      DSPATR(HI)
     A N51N36                               DSPATR(UL)
     A  51                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A** FORMAT FF0110F1 **
     A          R FF0110F1                  TEXT('MARKET CENTRES INPUT')
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'MARKET CENTRES INPUT'
     A                                      DSPATR(HI UL)
     A**                                             Begin /112563/
     A                                  1 36'FF0110'
     A**                                             End   /112563/
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 13'Market:'
     A            MRKT      R        B  3 22DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 58'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PMRKT          2   B  3 70TEXT('REVIEW FROM')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT FF0110S0 **
     A          R FF0110S0                  SFL
     A                                      TEXT('SUBFILE')
     A            ACTN           1A  I  5  2TEXT('ACTION')
     A                                      DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4TEXT('RECORD DELETED')
     A                                      DSPATR(BL HI)
     A            MRKT      R        O  5  6DSPATR(HI)
     A            MKTN      R        O  5 14DSPATR(HI)
     A            MKLC      R        O  5 54DSPATR(HI)
     A** FORMAT FF0110C0 **
     A          R FF0110C0                  SFLCTL(FF0110S0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(25)
     A N04                                  ROLLDOWN(26)
     A**********                            RTGAID(1)                /S01179/
     A                                      TEXT('CONTROL FILE')
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'MARKET CENTRES REVIEW'
     A                                      DSPATR(HI UL)
     A**                                             Begin /112563/
     A                                  1 36'FF0110A'
     A**                                             End   /112563/
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3 58'Review from'
     A            PMRKT          2   B  3 70TEXT('REVIEW FROM')
     A                                      DSPATR(HI UL)
     A                                  4  2'    Market  Name                  -
     A                                                     Currency            -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT FF0110F2 **
     A          R FF0110F2                  TEXT('DISPLAY MESSAGES')
      **Start of change BUG8045
     A*N23******                            CF10                                             BUG8045
      **End of change BUG8045
      **Start of change BUG3807
     A*N23******                            CA12                                             BUG3807
      **End of change BUG3807
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A** FORMAT FF0110F3 **
     A          R FF0110F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
