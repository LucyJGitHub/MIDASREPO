      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Charges/Commissions Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0130DF - CHARGES/COMMISSIONS MAINTENANCE                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2388            Date 21May04               *
      *  Prev Amend No. CAAA03             Date 15Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC003             Date 25Feb97               *
      *                 S01253             Date 17Apr90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  BUG2388 - F12 and F10 not working due to webfacing changes.  *
      *            Place F12 and F10 after F3.                        *
      *  CAAA03 - Webfacing changes                                   *
     A*  CAC003 - Profit Centre Accounting Development Phase 3.       *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(20 'ANY VALID KEY')
     A***********                                    Begin /S01199/
     A***********                                    End   /S01199/
     A                                      CA03
      ** Start of change BUG2388
     A N24                                  CF10
     A N24                                  CA12
      ** End of change BUG2388
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A** FORMAT FF0130F0 **
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0130F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      RTNDTA
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              Begin /CAC003/
     A*                                              End   /CAC003/
     A*                                              End   /S01199/
     A            DELT           7      5 72TEXT('RECORD DELETED')
     A                                      DSPATR(HI BL)
     A                                      OVRDTA
     A                                  6  3'Currency Name'
     A            CCNM          14   O  6 29TEXT('CURRENCY NAME')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7  3'Narrative'
     A            SCHNR         20   B  7 29TEXT('NARRATIVE')
     A                                      DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A*                                              Begin /CAC003/
     A  55                              7 54'Profit Centre'
     A  55        SPRFC          4A  B  7 69TEXT('Profit Centre')
     A                                      DSPATR(HI)
     A N06N56                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  56                                  DSPATR(PC RI)
     A                                      OVRDTA
     A  55                              7 74'or'
     A  55        SBOTR          1A  B  7 77TEXT('Book/Transaction Ind')
     A                                      DSPATR(HI)
     A N06N57                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  57                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                              End   /CAC003/
     A                                  8  3'Type'
     A            STTFI          1   B  8 29TEXT('TYPE')
     A                                      DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  9  3'Minimum Charge/Commission'
     A            SMINC         14   B  9 29TEXT('MIN CHG/COMM')
     A                                      DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 10  3'Maximum Charge/Commission'
     A            SMAXC         14   B 10 29TEXT('MAX CHG/COMM')
     A                                      DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 11 19'R  A  N  G  E'
     A                                 12  3'BAND'
     A                                      DSPATR(UL)
     A                                 12 13'Lower Limit'
     A                                      DSPATR(UL)
     A                                 12 28'Upper Limit'
     A                                      DSPATR(UL)
     A                                 13  4'01'
     A                                 14  4'02'
     A                                 15  4'03'
     A                                 16  4'04'
     A                                 17  4'05'
     A                                 18  4'06'
     A                                 19  4'07'
     A                                 20  4'08'
     A                                 21  4'09'
     A            SLR01          5A  B 13 16TEXT('LOWER LIMIT 01')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SLR02          5A  B 14 16TEXT('LOWER LIMIT 02')
     A                                      DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SLR03          5A  B 15 16TEXT('LOWER LIMIT 03')
     A                                      DSPATR(HI)
     A N06N38                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SLR04          5A  B 16 16TEXT('LOWER LIMIT 04')
     A                                      DSPATR(HI)
     A N06N39                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  39                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SLR05          5A  B 17 16TEXT('LOWER LIMIT 05')
     A                                      DSPATR(HI)
     A N06N40                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  40                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SLR06          5A  B 18 16TEXT('LOWER LIMIT 06')
     A                                      DSPATR(HI)
     A N06N41                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SLR07          5A  B 19 16TEXT('LOWER LIMIT 07')
     A                                      DSPATR(HI)
     A N06N42                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  42                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SLR08          5A  B 20 16TEXT('LOWER LIMIT 08')
     A                                      DSPATR(HI)
     A N06N43                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  43                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SLR09          5A  B 21 16TEXT('LOWER LIMIT 09')
     A                                      DSPATR(HI)
     A N06N44                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  44                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SUR01          5  0B 13 32TEXT('UPPER LIMIT 01')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR02          5  0B 14 32TEXT('UPPER LIMIT 02')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR03          5  0B 15 32TEXT('UPPER LIMIT 03')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR04          5  0B 16 32TEXT('UPPER LIMIT 04')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR05          5  0B 17 32TEXT('UPPER LIMIT 05')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR06          5  0B 18 32TEXT('UPPER LIMIT 06')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR07          5  0B 19 32TEXT('UPPER LIMIT 07')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR08          5  0B 20 32TEXT('UPPER LIMIT 08')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A            SUR09          5  0B 21 32TEXT('UPPER LIMIT 09')
     A                                      DSPATR(HI)
     A  12                                  DSPATR(UL)
     A N12                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 52'AMOUNT'
     A                                      DSPATR(UL)
     A            SCA01         14   B 13 48TEXT('AMOUNT 01')
     A                                      DSPATR(HI)
     A N06N36                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            SCA02         14   B 14 48TEXT('AMOUNT 02')
     A                                      DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A                                      OVRDTA
     A            SCA03         14   B 15 48TEXT('AMOUNT 03')
     A                                      DSPATR(HI)
     A N06N38                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A                                      OVRDTA
     A            SCA04         14   B 16 48TEXT('AMOUNT 04')
     A                                      DSPATR(HI)
     A N06N39                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A                                      OVRDTA
     A            SCA05         14   B 17 48TEXT('AMOUNT 05')
     A                                      DSPATR(HI)
     A N06N40                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A                                      OVRDTA
     A            SCA06         14   B 18 48TEXT('AMOUNT 06')
     A                                      DSPATR(HI)
     A N06N41                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A                                      OVRDTA
     A            SCA07         14   B 19 48TEXT('AMOUNT 07')
     A                                      DSPATR(HI)
     A N06N42                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A                                      OVRDTA
     A            SCA08         14   B 20 48TEXT('AMOUNT 08')
     A                                      DSPATR(HI)
     A N06N43                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A                                      OVRDTA
     A            SCA09         14   B 21 48TEXT('AMOUNT 09')
     A                                      DSPATR(HI)
     A N06N44                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A                                      OVRDTA
     A** FORMAT FF0130F1 **
     A          R FF0130F1                  TEXT('INPUT SCREEN')
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'CHARGES/COMMISSIONS INPUT'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 12'Currency:'
     A            CCY       R        B  3 22DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 26'Code:'
     A            SCGCM          2A  B  3 32TEXT('CODE')
     A                                      DSPATR(HI)
     A N05N23                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 58'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PCCY           3A  B  3 70TEXT('CURRENCY')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PCGCMA         2A  B  3 74TEXT('CODE')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT FF0130S0 **
     A          R FF0130S0                  SFL
     A            ACTN           1A  I  5  2TEXT('ACTION')
     A                                      DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4TEXT('STATUS')
     A                                      DSPATR(BL HI)
     A            CCY       R        O  5  6DSPATR(HI)
     A            CGCM      R        O  5 10DSPATR(HI)
     A            CHNR      R        O  5 14DSPATR(HI)
     A            TTFI      R        O  5 35DSPATR(HI)
     A** FORMAT FF0130C0 **
     A          R FF0130C0                  SFLCTL(FF0130S0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(25)
     A N04                                  ROLLDOWN(26)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'CHARGES/COMMISSIONS REVIEW'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A            CCNM      R        O  3  2DSPATR(HI)
     A                                  3 58'Review from'
     A            PCCY           3A  B  3 70TEXT('CURRENCY')
     A                                      DSPATR(HI UL)
     A            PCGCMA         2A  B  3 74TEXT('CODE')
     A                                      DSPATR(HI UL)
     A                                  4  2'   Ccy/code Narrative         Type-
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT FF0130F2 **
     A          R FF0130F2                  TEXT('MESSAGES')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
      ** Start of change BUG2388
     A*N24******                            CF10                                             BUG2388
     A*N24******                            CA12                                             BUG2388
      ** End of change BUG2388
     A                                      BLINK
     A            ERRMSG        76     23  2TEXT('ERROR MESSAGES')
     A                                      OVRDTA
     A            INFMSG        76     24  2TEXT('INFORMATION MSG')
     A                                      OVRDTA
     A** FORMAT FF0130F3 **
     A          R FF0130F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
