      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF LUX OTC Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  DSPF/FF0141DF - LUX OTCs Maintenance                         *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03(03)
     A  35                                  CA05(05)
     A  40                                  CF10(10 'Delete')
-----A*-------------------------------------------------------------------------
     A          R FF0141F
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      CF12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A            SPGM          10   O  1 13
     A            SUSER         10A  O  1  2TEXT('User Name')
     A            SJOB          10A  O  2  2TEXT('Workstation Id')
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  2 26'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
     A                                 23 40'F12=Previous'
     A                                 23  1'F3=Main Menu'
     A  35                             23 15'F5=Refresh'
     A  40                             23 27'F10=Delete'
     A                                  5  2'Option Calculation Method....:'
     A            SOPCM          1A  B  5 33TEXT('Option Calculation Method')
     A N50N20                               DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6  2'Security Shortname...........:'
     A            SSESN         10A  B  6 33TEXT('Security Shortname')
     A N51N20                               DSPATR(HI)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  7  2'Broad-based Index............:'
     A            SBBIX          1A  B  7 33TEXT('Broad-based Index')
     A N52N20                               DSPATR(HI)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
      *
------*-------------------------------------------------------------------------
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
------*-------------------------------------------------------------------------
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25                                  SFLEND
     A            ##PGM                     SFLPGMQ
