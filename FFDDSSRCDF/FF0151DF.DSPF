      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Instrument Types Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE                 *
     A*                                                               *
     A*  FF0151DF - MARKET INSTRUMENTS MAINTENANCE                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 12Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CFF006             Date 28Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFF005             Date 28Nov00               *
      *                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
      *  CFF006 - Fractional Ticks Enhancement.                       *
     A*  CFF005 - '?' processing for market instrument.               *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      CA03
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A          R FF0151F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      RTNDTA
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A N85                                  ROLLUP(40)
     A N85                                  ROLLDOWN(41)
      *
     A            DELT           7A  O  5 72TEXT('DELETION FLAG')
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  6  2'Instrument Name'
     A            SISTN         20A  B  6 19DSPATR(HI)
     A                                      TEXT('Instrument Name')
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                  6 41'Instrument Type (Port Mgmt)'
     A  87                                  DSPATR(ND)
     A            SINNR          3A  B  6 70TEXT('INSTRUMENT TYPE')
     A                                      DSPATR(HI)
     A  87                                  DSPATR(ND)
     A N06N86N87                            DSPATR(UL)
     A  06
     AO 87                                  DSPATR(PR)
     A  86N07                               DSPATR(PC)
     A  86N07                               DSPATR(RI)
     A                                      OVRDTA
      *
     A                                  7  2'Processing Type'
     A            SISPT          1A  B  7 19DSPATR(HI)
     A                                      TEXT('Processing Type')
      *
     A N06N32N26
     AAN83                                  DSPATR(UL)
     A  06
     AO 26
     AO 83                                  DSPATR(PR)
      *
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7 25'Currency'
     A            SISCY          3A  B  7 36DSPATR(HI)
     A                                      TEXT('Currency')
      *
     A N06N33N26
     AAN83                                  DSPATR(UL)
     A  06
     AO 26
     AO 83                                  DSPATR(PR)
      *
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  7 41'Contingent Amount'
     A            SCTAM         14A  B  7 60DSPATR(HI)
     A                                      TEXT('Contingent Amount')
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'Quotation Convention'
     A            SQOTC          2A  B  8 24DSPATR(HI)
     A                                      TEXT('Quotation Convention')
      *
     A N06N35N83                            DSPATR(UL)
     A  06
     AO 83                                  DSPATR(PR)
      *
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8 41'Ticks Denominator'
     A            STKDM          3A  B  8 60DSPATR(HI)
     A                                      TEXT('Ticks Denominator')
      *
     A N06N36N26
     AAN83                                  DSPATR(UL)
     A  06
     AO 26
     AO 83                                  DSPATR(PR)
      *
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9  2'Min.Price Fluctuation'
     A            SMNPF         12A  B  9 24DSPATR(HI)
     A                                      TEXT('Min.Price Fluctuation')
      *
     A N06N37N83                            DSPATR(UL)
     A  06
     AO 83                                  DSPATR(PR)
      *
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  9 41'Tick Value'
     A            STKVL         15A  B  9 60DSPATR(HI)
     A                                      TEXT('Tick Value')
      *
     A N06N38N83                            DSPATR(UL)
     A  06
     AO 83                                  DSPATR(PR)
      *
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A                                 10  2'Fractional Ticks Processed'
     A  88                                  DSPATR(ND)
     A            SFRTK          1A  B 10 30DSPATR(HI)
     A                                      TEXT('Fractional Ticks Indicator')
     A  88                                  DSPATR(ND)
     A N06N89                               DSPATR(UL)
     A  06
     AO 88                                  DSPATR(PR)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      OVRDTA
     A*****                            10  2'Default Broker'                                  CFF006
     A*****       SDBRK         10A  B 10 19DSPATR(HI)                                        CFF006
     A                                 11  2'Default Broker'
     A            SDBRK         10A  B 11 19DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Default Broker')
     A N06N39                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A**Start of change CFF006
     A*****                            10 41'Contract Type'                                   CFF006
     A*****       SCNTT          1A  B 10 60TEXT('Contract Type')                             CFF006
     A                                 11 41'Contract Type'
     A            SCNTT          1A  B 11 60TEXT('Contract Type')
     A**End of change CFF006
     A                                      DSPATR(HI)
     A N06N80                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A**Start of change CFF006
     A*****                            11 35'BROKER'                                          CFF006
     A*****                            11 53'CUSTOMER'                                        CFF006
     A*****                            12  2'Commission Pattern'                              CFF006
     A*****       SBCPT          1A  B 12 35DSPATR(HI)                                        CFF006
     A                                 12 35'BROKER'
     A                                 12 53'CUSTOMER'
     A                                 13  2'Commission Pattern'
     A            SBCPT          1A  B 13 35DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Brok Commission Pattern')
     A N06N46                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****       SCCPT          1A  B 12 53DSPATR(HI)                                        CFF006
     A            SCCPT          1A  B 13 53DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Cust Commission Pattern')
     A N06N47                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****                            13  2'Commsns:Standard/Same Day'                       CFF006
     A*****       SBCMC          2A  B 13 35DSPATR(HI)                                        CFF006
     A                                 14  2'Commsns:Standard/Same Day'
     A            SBCMC          2A  B 14 35DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Brok Commsns Standard')
     A N06N48                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****       SBCMS          2A  B 13 40DSPATR(HI)                                        CFF006
     A            SBCMS          2A  B 14 40DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Brok Commsns Same Day')
     A N06N50                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****       SCCMC          2A  B 13 53DSPATR(HI)                                        CFF006
     A            SCCMC          2A  B 14 53DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Cust Commsns Standard')
     A N06N49                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****       SCCMS          2A  B 13 58DSPATR(HI)                                        CFF006
     A            SCCMS          2A  B 14 58DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Cust Commsns Same Day')
     A N06N51                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****                            14  2'Charges:Purchases/Sales'                         CFF006
     A*****       SBCGP          2A  B 14 35DSPATR(HI)                                        CFF006
     A                                 15  2'Charges:Purchases/Sales'
     A            SBCGP          2A  B 15 35DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Brok Charg Purchase')
     A N06N52                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****       SBCGS          2A  B 14 40DSPATR(HI)                                        CFF006
     A            SBCGS          2A  B 15 40DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Brok Charg Sale')
     A N06N53                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****       SCCGP          2A  B 14 53DSPATR(HI)                                        CFF006
     A            SCCGP          2A  B 15 53DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Cust Charg Purchase')
     A N06N54                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                      OVRDTA
     A**Start of change CFF006
     A*****       SCCGS          2A  B 14 58DSPATR(HI)                                        CFF006
     A            SCCGS          2A  B 15 58DSPATR(HI)
     A**End of change CFF006
     A                                      TEXT('Cust Charg Sale')
     A N06N55                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 16  2'Commodity Ref'
     A            SCMRF         10A  B 16 19DSPATR(HI)
     A                                      TEXT('Commodity Ref')
     A N06N56                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 16 32'Pricing Ref'
     A            SPRRF         10A  B 16 44DSPATR(HI)
     A                                      TEXT('Pricing Ref')
     A N06N57                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 16 56'Statutory Returns'
     A            SSTRC          5A  B 16 74DSPATR(HI)
     A                                      TEXT('Statutory Returns')
     A N06N58                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 17  2'Other Currency'
     A            SOTHC          3A  B 17 19DSPATR(HI)
     A                                      TEXT('Other Currency')
     A N06N59N26                            DSPATR(UL)
     A  26
     AO 06                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 17 28'Future Type Ref'
     A            SFTRF          3A  B 17 44DSPATR(HI)
     A                                      TEXT('Future Type Ref')
     A N06N60N26                            DSPATR(UL)
     A  26
     AO 06                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 17 56'Premium Up Front'
     A            SPUPF          1A  B 17 74DSPATR(HI)
     A                                      TEXT('Premium Up Front')
     A N06N61                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 22 29'American/European'
     A            SAEIN          1A  B 22 47TEXT('American/European')
      *
     A                                      DSPATR(HI)
     A N06N81                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 18 18'JAN FEB MAR APR MAY JUN JUL AUG SE-
     A                                      P OCT NOV DEC'
     A                                 19  2'Months Quoted'
     A            SMQ1           1A  B 19 19DSPATR(HI)
     A                                      TEXT('Month 1 Quoted')
     A N06N62N07                            DSPATR(UL)
     A  62
     AO 06                                  DSPATR(PR)
     A  07                                  DSPATR(PC)
     A  07                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ2           1A  B 19 23DSPATR(HI)
     A                                      TEXT('Month 2 Quoted')
     A N06N63N08                            DSPATR(UL)
     A  63
     AO 06                                  DSPATR(PR)
     A  08                                  DSPATR(PC)
     A  08                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ3           1A  B 19 27DSPATR(HI)
     A                                      TEXT('Month 3 Quoted')
     A N06N64N09                            DSPATR(UL)
     A  64
     AO 06                                  DSPATR(PR)
     A  09                                  DSPATR(PC)
     A  09                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ4           1A  B 19 31DSPATR(HI)
     A                                      TEXT('Month 4 Quoted')
     A N06N65N13                            DSPATR(UL)
     A  65
     AO 06                                  DSPATR(PR)
     A  13                                  DSPATR(PC)
     A  13                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ5           1A  B 19 35DSPATR(HI)
     A                                      TEXT('Month 5 Quoted')
     A N06N66N14                            DSPATR(UL)
     A  66
     AO 06                                  DSPATR(PR)
     A  14                                  DSPATR(PC)
     A  14                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ6           1A  B 19 39DSPATR(HI)
     A                                      TEXT('Month 6 Quoted')
     A N06N67N15                            DSPATR(UL)
     A  67
     AO 06                                  DSPATR(PR)
     A  15                                  DSPATR(PC)
     A  15                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ7           1A  B 19 43DSPATR(HI)
     A                                      TEXT('Month 7 Quoted')
     A N06N68N17                            DSPATR(UL)
     A  68
     AO 06                                  DSPATR(PR)
     A  17                                  DSPATR(PC)
     A  17                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ8           1A  B 19 47DSPATR(HI)
     A                                      TEXT('Month 8 Quoted')
     A N06N69N18                            DSPATR(UL)
     A  69
     AO 06                                  DSPATR(PR)
     A  18                                  DSPATR(PC)
     A  18                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ9           1A  B 19 51DSPATR(HI)
     A                                      TEXT('Month 9 Quoted')
     A N06N70N19                            DSPATR(UL)
     A  70
     AO 06                                  DSPATR(PR)
     A  19                                  DSPATR(PC)
     A  19                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ10          1A  B 19 55DSPATR(HI)
     A                                      TEXT('Month 10 Quoted')
     A N06N71N27                            DSPATR(UL)
     A  71
     AO 06                                  DSPATR(PR)
     A  27                                  DSPATR(PC)
     A  27                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ11          1A  B 19 59DSPATR(HI)
     A                                      TEXT('Month 11 Quoted')
     A N06N72N28                            DSPATR(UL)
     A  72
     AO 06                                  DSPATR(PR)
     A  28                                  DSPATR(PC)
     A  28                                  DSPATR(RI)
     A                                      OVRDTA
     A            SMQ12          1A  B 19 63DSPATR(HI)
     A                                      TEXT('Month 12 Quoted')
     A N06N73N29                            DSPATR(UL)
     A  73
     AO 06                                  DSPATR(PR)
     A  29                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 20  2'Final Transn Date'
     A            SFTDF          7A  B 20 22DSPATR(HI)
     A                                      TEXT('Final Transn Date')
     A N06N74                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 20 31'Settlement Date'
     A            SSEDF          7A  B 20 47DSPATR(HI)
     A                                      TEXT('Settlement Date')
     A N06N75                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 20 56'Spot Month Date'
     A            SSPMF          7A  B 20 72DSPATR(HI)
     A                                      TEXT('Spot Month Date')
     A N06N76                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 21  2'Traded Fwd Periods'
     A            STFPD          2A  B 21 22DSPATR(HI)
     A                                      TEXT('Traded Fwd Periods')
     A N06N77                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 21 31'Fwd Spot Months'
     A            SFSPM          2A  B 21 47DSPATR(HI)
     A                                      TEXT('Fwd Spot Months')
     A N06N78                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 21 54'P&L Amort. Period'
     A            SULPD          3A  B 21 72DSPATR(HI)
     A                                      TEXT('P&L Amort. Period')
     A N06N79                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 22  2'Underlying Period'
     A            SALPD          5A  B 22 22DSPATR(HI)
     A                                      TEXT('Underlying Period')
     A N06N82                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A          R FF0151F1
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                  1  2'Market Instruments Enquiry'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10DSPATR(HI)
     A N05N20                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  20                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 13'Market:'
     A            MRKT      R        B  3 22DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 25'Code:'
     A            ISTC      R        B  3 31DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  3 58'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PMRKT     R        B  3 70DSPATR(HI UL)
     A                                      REFFLD(MRKT)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PISTC     R        B  3 73DSPATR(HI UL)
     A                                      REFFLD(ISTC)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      * SCREEN FF0150S0 **
     A          R FF0151S0                  SFL
     A            ACTN           1   I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1   O  5  4DSPATR(BL HI)
     A            MRKT      R        O  5  6DSPATR(HI)
     A            ISTC      R        O  5 10DSPATR(HI)
     A            ISTN      R        O  5 14DSPATR(HI)
      * SCREEN FF0150C0 **
     A          R FF0151C0                  SFLCTL(FF0151S0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLUP(40)
     A N04                                  ROLLDOWN(41)
      *
      *
     A                                  1  2'Market Instruments Review'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78
     A            MKTN      R        O  3  2DSPATR(HI)
     A                                  3 58'Review from'
     A            PMRKT     R        O  3 70DSPATR(HI)
     A                                      REFFLD(MRKT)
     A            PISTC     R        B  3 73DSPATR(HI UL)
     A                                      REFFLD(ISTC)
     A                                  4  2'1  Mkt/code Description           -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      * SCREEN FF0150F2 **
     A          R FF0151F2
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A N23                                  CF10
     A N23                                  CA12
     A                                      BLINK
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
      * CLEAR SCREEN **
     A          R FF0151F3
     A                                  1  2' '
