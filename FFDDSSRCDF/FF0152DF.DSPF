      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Market Instr. type - CAD Window')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  FF0152DF - LUX Market Instruments Maintenance 1              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *Create    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 -  LUX Upgrade to MidasPlus                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
      *
     A          R FF0152F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
     A                                      CA03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A            ##PGM         10A  H
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            SRUNDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
     A                                  1 27'European Reporting Luxembourg'
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                  4  2'Option Calculation Method....:'
     A            SOPCM          1A  B  4 33TEXT('Option Calculation Method')
     A N50N20                               DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  5  2'Security Shortname...........:'
     A            SSESN         10A  B  5 33TEXT('Security Shortname')
     A N51N20                               DSPATR(HI)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6  2'Broad-based Index............:'
     A            SBBIX          1A  B  6 33TEXT('Broad-based Index')
     A N52N20                               DSPATR(HI)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                 23  2'F3=Main Menu'
     A                                 23 40'F12=Previous'
     A  35                             23 16'F5=Refresh'
     A  40                             23 28'F10=Delete'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
