      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Defaults Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0160DF - DEFAULTS MAINTENANCE                              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
      *  Prev Amend No. TDA089             Date 26Mar04               *
      *                 CAAA03             Date 12Mar04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S04662             Date 09Jul93               *
      *                 S01253             Date 17Apr90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01117             DATE 22JAN90               *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  TDA089 - Activate F10 and F12 function keys.                 *
      *  CAAA03 - Webfacing Changes                                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S04662  -  Switchable Feature for Init Margins for Futures; *
     A*             Added two new fields to DEFLTD :                  *
     A*               DMRG - Deposit Margin Rate                      *
     A*               SMRG - Spread Margin Rate                       *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(55 'ANY VALID KEY')
     A***********                                    Begin /S01199/
     A***********                                    End   /S01199/
     A                                      CA03
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A** FORMAT FF0160F0 **
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0160F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A**Start of change TDA089
     A  26                                  CF10
     A                                      CA12
     A**End of change TDA089
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                                BEGIN     S01117
     A*                                                END       S01117
     A*                                                BEGIN     S01117
     A*                                                END       S01117
     A*/COPY WNCPYSRC,FF0160DF01
     A*/COPY WNCPYSRC,FF0160DF02
     A*                                              End   /S01199/
     A*                                              Begin /S04662/
     A*                                              End   /S04662/
     A            DELT           7A  O  5 72TEXT('RECORD DELETED')
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  6  2'Customer Name'
     A            CRNM          20A  O  6 29TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  7  2'Settlement Type'
     A            SSETP          2A  B  7 29TEXT('SETTLEMENT TYPE')
     A                                      DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'Settlement Account'
     A*                                              Begin /S01117/
     A            SSEBR          3A  B  8 29TEXT('SETTLEMENT BRANCH')
     A                                      DSPATR(HI)
     A N06N44                               DSPATR(UL)
     A N70                                  DSPATR(ND)
     A N70
     AO 06                                  DSPATR(PR)
     A  70 44                               DSPATR(PC RI)
     A                                      OVRDTA
     A*********** SSETA         12A  B  8 29TEXT('SETTLEMENT ACCOUNT')
      **Start of change CGL029
     A**********  SSETA         12A  B  8 33TEXT('SETTLEMENT ACCOUNT')                        CGL029
     A            SSETA         18A  B  8 33TEXT('SETTLEMENT ACCOUNT')
      **End of change CGL029
     A*                                              End   /S01117/
     A                                      DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  9  2'Counterparty Nostro'
     A            SCNOS         10A  B  9 29TEXT('COUNTERPARTY NOSTRO')
     A                                      DSPATR(HI)
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 10  2'For Account Of'
     A            SFACO         10A  B 10 29TEXT('FOR ACCOUNT OF')
     A                                      DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 11  2'Commission pattern'
     A*/COPY WNCPYSRC,FF0160DF03
     A            SCPAT          1A  B 11 29TEXT('COMMISSION PATTERN')
     A                                      DSPATR(HI)
     A N06N35                               DSPATR(UL)
     A*/COPY WNCPYSRC,FF0160DF04
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,FF0160DF05
     A  35                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 12  2'Commission Code:Standard'
     A*/COPY WNCPYSRC,FF0160DF06
     A            SCMCD          2A  B 12 29TEXT('COMMISSION CODE : STD')
     A                                      DSPATR(HI)
     A N06N36                               DSPATR(UL)
     A*/COPY WNCPYSRC,FF0160DF07
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,FF0160DF08
     A  36                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 13  2'Commission Code:Same Day'
     A*/COPY WNCPYSRC,FF0160DF09
     A            SCMSD          2A  B 13 29TEXT('COMMISSION CODE:SAME DAY')
     A                                      DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A*/COPY WNCPYSRC,FF0160DF10
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,FF0160DF11
     A  37                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 14  2'Charge Code:Purchases'
     A*/COPY WNCPYSRC,FF0160DF12
     A            SCGEP          2A  B 14 29TEXT('CHARGE CODE:PURCHASE')
     A                                      DSPATR(HI)
     A N06N38                               DSPATR(UL)
     A*/COPY WNCPYSRC,FF0160DF13
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,FF0160DF14
     A  38                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 15  2'Charge Code:Sales'
     A*/COPY WNCPYSRC,FF0160DF15
     A            SCGES          2A  B 15 29TEXT('CHARGE CODE:SALES')
     A                                      DSPATR(HI)
     A N06N39                               DSPATR(UL)
     A*/COPY WNCPYSRC,FF0160DF16
     A  06                                  DSPATR(PR)
     A*/COPY WNCPYSRC,FF0160DF17
     A  39                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 16  2'Review Frequency'
     A            SRVFR          1A  B 16 29TEXT('REVIEW FREQUENCY')
     A                                      DSPATR(HI)
     A N06N40                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  40                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 17  2'Review Base Day'
     A            SRVDY          2A  B 17 29TEXT('REVIEW BASE DAY')
     A                                      DSPATR(HI)
     A N06N41                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 18  2'Next Review Date'
     A            SRVDT          6A  B 18 29TEXT('NEXT REVIEW DATE')
     A                                      DSPATR(HI)
     A N06N42                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  42                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 19  2'Expiry Date'
     A            SEXPD          6A  B 19 29TEXT('EXPIRY DATE')
     A                                      DSPATR(HI)
     A N06N43                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  43                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                           /* Begin S04662 */
     A  53                             20  2'Deposit Margin Rate'
     A  53        SINMC         13A  B 20 29TEXT('DEPOSIT MARGIN RATE')
     A                                      DSPATR(HI)
     A N06N45                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  45                                  DSPATR(PC RI)
     A                                      OVRDTA
     A  53                             21  2'Spread Margin Rate'
     A  53        SSPMC         13A  B 21 29TEXT('SPREAD MARGIN RATE')
     A                                      DSPATR(HI)
     A N06N46                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  46                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                           /* End   S04662 */
     A** FORMAT FF0160F1 **
     A          R FF0160F1                  TEXT('INPUT SCREEN')
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'DEFAULTS INPUT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  2  2'Action:'
     A            ACTN           1A  B  2 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  2 12'Customer:'
     A            CSNM          10A  B  2 22TEXT('CUSTOMER')
     A                                      DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3  2'Market/Instrument:'
     A            MRKT      R        B  3 22DSPATR(HI)
     A N05N23                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            ISTC      R        B  3 25DSPATR(HI)
     A N05N24                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  24                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 54'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PCBCDA         6A  B  3 66TEXT('BROKER/CUSTOMER CODE')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PMRKT          2A  B  3 73TEXT('MARKET CENTER')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A            PISTC          3A  B  3 76TEXT('INSTRUMENT CODE')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT FF0160S0 **
     A          R FF0160S0                  SFL
     A            ACTN           1A  I  5  2TEXT('ACTION')
     A                                      DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4TEXT('STATUS')
     A                                      DSPATR(BL HI)
      * Begin /CSD027/
     A**********  CBCD      R        O  5  6DSPATR(HI)                                        CSD027
     A            CBCD           6A  O  5  6DSPATR(HI)
     A                                      TEXT('Customer Broker Code')
      * End /CSD027/
     A            MRKT      R        O  5 14DSPATR(HI)
     A            ISTC      R        O  5 18DSPATR(HI)
     A            CRNM          20A  O  5 22TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
     A            CSNM          10A  O  5 44TEXT('CUSTOMER')
     A                                      DSPATR(HI)
     A** FORMAT FF0160C0 **
     A          R FF0160C0                  SFLCTL(FF0160S0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A**Start of change TDA089
     A                                      CA12
     A**End of change TDA089
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(50)
     A N04                                  ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'DEFAULTS REVIEW'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3 54'Review from'
     A            PCBCDA         6A  B  3 66TEXT('CUSTOMER/BROKER CODE')
     A                                      DSPATR(HI UL)
     A            PMRKT          2A  B  3 73TEXT('MARKET CENTER')
     A                                      DSPATR(HI UL)
     A            PISTC          3A  B  3 76TEXT('INSTRUMENT CODE')
     A                                      DSPATR(HI UL)
     A                                  4  2'  Customer Mkt Inst Customer Name -
     A                                              Shortname                  -
     A                                               '
     A                                      DSPATR(UL)
     A** FORMAT FF0160F2 **
     A          R FF0160F2                  TEXT('ERROR/INFO MESSAGES')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A N25                                  CF10
     A N25                                  CA12
     A                                      BLINK
     A            ERRMSG        76     23  2
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A** FORMAT FF0160F3 **
     A          R FF0160F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
