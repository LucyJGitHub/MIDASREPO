      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Trade Number Ranges Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0170DF - TRADE NUMBER RANGES MAINTENANCE                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG12220           Date 18Oct06               *
      *  Prev Amend No. CAAA03             Date 11Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01253             Date 17Apr90               *
      *                 S01199             Date 27Feb90               *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  BUG12220 - Function Keys not functioning properly            *
      *  CAAA03 - Webfacing Changes                                   *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(50 'ANY VALID KEY')
     A**********                                     Begin /S01199/
     A**********                                     End   /S01199/
     A                                      CA03
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A** FORMAT FF0170F0 **
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0170F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      RTNDTA
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
      **Start of change BUG12220
     A  23                                  CF10
     A                                      CF12
      **End of change BUG12220
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A            DELT           7      5 72TEXT('RECORD DELETED?')
     A                                      DSPATR(HI BL)
     A                                      OVRDTA
     A                                  6  2'First Number of Range'
     A            SFNOR          6A  B  6 29TEXT('1ST NO OR RANGE')
     A                                      DSPATR(HI)
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  8  2'Last Number of Range'
     A            SLNOR          6A  B  8 29TEXT('LAST NO OF RANGE')
     A                                      DSPATR(HI)
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 10  2'Description of Range'
     A            STRDS         35A  B 10 29TEXT('DESCRIPTION OF RANGE')
     A                                      DSPATR(HI)
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
     A                                      OVRDTA
     A** FORMAT FF0170F1 **
     A          R FF0170F1                  TEXT('TRADE NO RANGES INPUT')
     A                                      BLINK
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'TRADE NUMBER RANGES INPUT'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 12'Trade number range'
     A            STRNR          2A  B  3 32TEXT('TRADE NO RANGE')
     A                                      DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 58'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PTRNRA         2A  B  3 70TEXT('TRADE NO RANGE')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT FF0170S0 **
     A          R FF0170S0                  SFL
     A            ACTN           1A  I  5  2TEXT('ACTION')
     A                                      DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4TEXT('STATUS')
     A                                      DSPATR(BL HI)
     A            TRNR      R        O  5  6DSPATR(HI)
     A            TRDS      R        O  5 14DSPATR(HI)
     A            FNOR      R        O  5 50DSPATR(HI)
     A            LNOR      R        O  5 57DSPATR(HI)
     A** FORMAT FF0170C0 **
     A          R FF0170C0                  SFLCTL(FF0170S0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
      **Start of change BUG12220
     A                                      CF12
      **End of change BUG12220
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(25)
     A N04                                  ROLLDOWN(26)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'TRADE NUMBER RANGES REVIEW'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3 58'Review from'
     A            PTRNRA         2A  B  3 70TEXT('TRADE NO RANGE')
     A                                      DSPATR(HI UL)
     A                                  4  2'    Range   Description           -
     A                                                    1st No.Last No.      -
     A                                              '
     A                                      DSPATR(UL)
     A** FORMAT FF0170F2 **
     A          R FF0170F2                  TEXT('MESSAGES')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
      **Start of change BUG12220
     A*N23******                            CF10                                            BUG12220
     A*N23******                            CA12                                            BUG12220
      **End of change BUG12220
     A                                      BLINK
     A            ERRMSG        76     23  2TEXT('ERROR MESSAGES')
     A                                      OVRDTA
     A            INFMSG        76     24  2TEXT('INFORMATION MESSAGES')
     A                                      OVRDTA
     A** FORMAT FF0170F3 **
     A          R FF0170F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
