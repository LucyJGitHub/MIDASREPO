      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF F O Account Key Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0180DF - FO ACCOUNT KEYS                                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. BUG11812           Date 04Aug06               *
     A*  Prev Amend No. CSD031             Date 10Apr06               *
      *                 TDA094             Date 01Apr04               *
      *                 CAAA03             Date 12Mar04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 104199             DATE 10JUN96               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  BUG11812 - Action Code & Account Key are not displayed in    *
     A*             the input screen                                 *
     A*  CSD031 - Enhanced Centralised Static Data                    *
      *  TDA094 - Enabled F10 and F12 which became 'not allowed'      *
      *           after change by CAAA03 in write sequence of         *
      *           display file's record formats                       *
      *  CAAA03 - Webfacing Changes                                   *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  104199  -  Add program name to DSPF for GUI identification   *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(55 'ANY VALID KEY')
     A**********                                     Begin /S01199/
     A**********                                     End   /S01199/
     A                                      CA03
     A**Start of change TDA094
     A N23                                  CF10
     A N23                                  CA12
     A**End of change TDA094
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0180F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      RTNDTA
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*/COPY WNCPYSRC,FF0180DF01
     A            DELT           7A  O  5 72DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                      TEXT('Deletion flag')
     A                                  6 12'Account code'
     A                                  6 32'Narr. code'
     A                                  6 50'Posting ref.'
     A*/COPY WNCPYSRC,FF0180DF02
     A                                  8 12'Dr'
      **Start of change CGL029
     A**********  SACD1          4A  B  8 17DSPATR(HI)                                        CGL029
     A            SACD1         10A  B  8 17DSPATR(HI)
      **End of change CGL029
     A N06N31                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A                                      TEXT('Account code 1')
     A            SNRC1          2A  B  8 36DSPATR(HI)
     A N06N37                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Narrative code 1')
     A            SPRF1          1A  B  8 55DSPATR(HI)
     A N06N43                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Posting reference 1')
      **Start of change CGL029
     A**********  SACD2          4A  B  9 17DSPATR(HI)                                        CGL029
     A            SACD2         10A  B  9 17DSPATR(HI)
      **End of change CGL029
     A N06N32                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Account code 2')
     A            SNRC2          2A  B  9 36DSPATR(HI)
     A N06N38                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Narrative code 2')
     A            SPRF2          1A  B  9 55DSPATR(HI)
     A N06N44                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Posting reference 2')
      **Start of change CGL029
     A**********  SACD3          4A  B 10 17DSPATR(HI)                                        CGL029
     A            SACD3         10A  B 10 17DSPATR(HI)
      **End of change CGL029
     A N06N33                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Account code 3')
     A            SNRC3          2A  B 10 36DSPATR(HI)
     A N06N39                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Narrative code 3')
     A            SPRF3          1A  B 10 55DSPATR(HI)
     A N06N45                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Posting reference 3')
     A                                 12 12'Cr'
      **Start of change CGL029
     A**********  SACD4          4A  B 12 17DSPATR(HI)                                        CGL029
     A            SACD4         10A  B 12 17DSPATR(HI)
      **End of change CGL029
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Account code 4')
     A            SNRC4          2A  B 12 36DSPATR(HI)
     A N06N40                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Narrative code 4')
     A            SPRF4          1A  B 12 55DSPATR(HI)
     A N06N46                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Posting reference 4')
      **Start of change CGL029
     A**********  SACD5          4A  B 13 17DSPATR(HI)                                        CGL029
     A            SACD5         10A  B 13 17DSPATR(HI)
      **End of change CGL029
     A N06N35                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Account code 5')
     A            SNRC5          2A  B 13 36DSPATR(HI)
     A N06N41                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Narrative code 5')
     A            SPRF5          1A  B 13 55DSPATR(HI)
     A N06N47                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Posting reference 5')
      **Start of change CGL029
     A**********  SACD6          4A  B 14 17DSPATR(HI)                                        CGL029
     A            SACD6         10A  B 14 17DSPATR(HI)
      **End of change CGL029
     A N06N36                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Account code 6')
     A            SNRC6          2A  B 14 36DSPATR(HI)
     A N06N42                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Narrative code 6')
     A            SPRF6          1A  B 14 55DSPATR(HI)
     A N06N48                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A                                      OVRDTA
     A                                      TEXT('Posting reference 6')
     A**Start of change CSD031
     A            #TITL         14A  O 17 12
     A  28                                  DSPATR(ND)
     A            #1GPDT         1A  B 17 28COLOR(WHT)
     A  28                                  DSPATR(ND)
     A  29                                  DSPATR(PR)
     A  59                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A N29N59                               DSPATR(UL)
     A                                 19 12'Effective Date:'
     A  82                                  DSPATR(ND)
     A            #EACC          6Y 0B 19 28DSPATR(HI)
     A  82                                  DSPATR(ND)
     A  29                                  DSPATR(PR)
     A  58                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A N29N58                               DSPATR(UL)
     A                                      EDTCDE(4)
      ** Start of change BUG11812
     A**********  RUNA      R        O  1 55                                                BUG11812
     A**********  #1HDR         33A  O  1  2COLOR(WHT)                                      BUG11812
     A**********                        1 37'FF0180'                                        BUG11812
     A**********                        1 64TIME                                            BUG11812
     A**********                        1 74'WID'                                           BUG11812
     A**********  WSID           3A  O  1 78TEXT('Workstation ID')                          BUG11812
      ** End of change BUG11812
     A**End of change CSD031
     A*/COPY WNCPYSRC,FF0180DF03
     A          R FF0180F1                  TEXT('Record format of +
     A                                      input key')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A**Start of change CSD031
     A**********                        1  2'FO ACCOUNT KEYS INPUT'                           CSD031
     A            #1HDR         33A  O  1  2COLOR(WHT)
     A**End of change CSD031
     A                                      DSPATR(HI)
     A**Start of change CSD031
     A**********                            DSPATR(UL)                                        CSD031
     A**End of change CSD031
     A***                                            Start /*104199*/
     A**Start of change CSD031
     A**********                        1 26'FF0180'                                          CSD031
     A                                  1 36'FF0180'
     A**End of change CSD031
     A***                                            End   /*104199*/
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('Workstation ID')
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10DSPATR(HI)
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      OVRDTA
     a                                      TEXT('Action code')
     A                                  3 13'Account Key'
     A            FFKY      R        B  3 25DSPATR(HI)
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A**Start of change CSD031
     A                                      OVRATR
     A**End of change CSD031
     A                                  3 48'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            PFFKY         14A  B  3 62DSPATR(HI)
     A                                      DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A                                      TEXT('Review From Key')
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A          R FF0180S0                  SFL
     A                                      TEXT('Subfile record form +
     A                                      at')
     A            ACTN           1A  I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A            STAT           1A  O  5  4DSPATR(BL HI)
     A            FFKY      R        O  5  6DSPATR(HI)
     A**Start of change CSD031
     A            #1AACC         1   O  5 28COLOR(WHT)
     A  27                                  DSPATR(ND)
     A            #1EACC         7A  O  5 43COLOR(WHT)
     A            #1ACTN         6   O  5 57COLOR(WHT)
     A  20                                  DSPATR(ND)
     A**End  of change CSD031
     A* SCREEN SUBFLCTL **
     A          R FF0180C0                  SFLCTL(FF0180S0)
     A                                      TEXT('Subfile control +
     A                                      record format')
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(50)
     A N04                                  ROLLDOWN(51)
     A**Start of change CSD031
     A                                      CF11(11)
     A**End of change CSD031
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A***                                            Start /*104199*/
     A**Start of change CSD031
     A**********                        1 26'FF0180'                                          CSD031
     A                                  1 36'FF0180'
     A**End of change CSD031
     A***                                            End   /*104199*/
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('Workstation ID')
     A                                  3 45'Review from'
     A            PFFKY         14A  B  3 57DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      TEXT('Review From Key')
     A**Start of change CSD031
     A**********                        4  2'X                                 -              CSD031
     A**********                                                               -              CSD031
     A**********                                     '                                        CSD031
     A**********                            DSPATR(UL)                                        CSD031
     A**********                        1  2'FO ACCOUNT KEYS REVIEW'                          CSD031
     A**********                            DSPATR(HI)                                        CSD031
     A**********                            DSPATR(UL)                                        CSD031
     A                                  4  2'Act'
     A                                  4  6'Account Keys'
     A            #1TITL        14   O  4 22
     A  27                                  DSPATR(ND)
     A                                  4 40'Effective Date'
     A  83                                  DSPATR(ND)
     A                                  4 57'Action'
     A  20                                  DSPATR(ND)
     A            #1HDR         33A  O  1  2COLOR(WHT)
     A                                      DSPATR(HI)
     A**End of change CSD031
     A          R FF0180F2                  TEXT('Record format to +
     A                                      display messages')
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A**Start of change TDA094
     A*N23******                            CF10                                              TDA094
     A*N23******                            CA12                                              TDA094
     A**End of change TDA094
     A                                      BLINK
     A            ERRMSG        76     23  2TEXT('Error codes')
     A                                      OVRDTA
     A            INFMSG        76     24  2TEXT('Informational Msgs.')
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R FF0180F3                  TEXT('Record format to +
     A                                      clear screen')
     A                                  1  2' '
     A**Start of change CSD031
     A          R FF0180F4
     A                                  1 36'FF0180'
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('Workstation ID')
     A                                  3 45'Review from'
     A            PFFKY         14A  B  3 57DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      TEXT('Review From Key')
     A                                  4  2'Act'
     A                                  4  6'Account Keys'
     A            #1TITL        14   O  4 22
     A  27                                  DSPATR(ND)
     A                                  4 40'Effective Date'
     A                                  4 57'Action'
     A  20                                  DSPATR(ND)
     A            #1HDR         33A  O  1  2COLOR(WHT)
     A                                      DSPATR(HI)
     A**End of change CSD031
