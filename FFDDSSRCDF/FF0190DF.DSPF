      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Position Profit Centre Display')
     A*****************************************************************
     A*                                                               *
     A*  Midas FUTURES AND OPTIONS MODULE                         *
     A*                                                               *
     A*  FF0190DF - POSITION PROFIT CENTRE MAINTENANCE                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 15Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFF004             Date 03Sep96               *
     A*                 104199             Date 10Jun96               *
     A*                     E27208             DATE 15AUG91           *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
     A*      CFF004  - Increase in size of Price Fields               *
     A*      104199  - Add program name to DSPF for GUI               *
     A*                identification                                 *
     A*      E27208  - NEW DSPF FOR PROFIT CENTRE MAINTAINANCE        *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*  CREATION REQUIREMENTS                                        *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A*
     A** FORMAT FF0190F1 **
     A          R FF0190F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CA12
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      BLINK
     A                                      PUTOVR
     A                                  1  2'POSITION PROFIT CENTRE INPUT'
     A                                      DSPATR(HI UL)
     A***                                          Start   /*104199*/
     A                                  1 37'FF0190'
     A***                                          End     /*104199*/
     A            RUNA           7   O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A                                  3  2'Branch:'
     A N88                                  DSPATR(ND)
     A            SBRCA          3   B  3 11DSPATR(HI)
     A N21 88                               DSPATR(UL)
     A N88                                  DSPATR(PR ND)
     A  21 88                               DSPATR(PC RI)
     A                                      OVRDTA
     A                                  3 21'Customer/Broker Code:'
     A            SCBCD         10   B  3 44DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  5 21'Book Code:'
     A            SBOKC          2   B  5 33DSPATR(HI)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  5 37'Instrument Type:'
     A            SISTT          5   B  5 55DSPATR(HI)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A** FORMAT FF0190S0 **
     A          R FF0190S0                  SFL
     A                                      TEXT('REVIEW SUBFILE')
     A  58                                  SFLNXTCHG
     A            ACTN           1   B  7  2TEXT('ACTION')
     A                                      DSPATR(HI UL)
     A  30                                  DSPATR(PC)
     A            XBRCA          3   O  7 10DSPATR(HI)
     A N88                                  DSPATR(ND)
     A            XCBCD          6   O  7 19DSPATR(HI)
     A            XBOKC          2   O  7 33DSPATR(HI)
     A            XISTT          5   O  7 42DSPATR(HI)
     A            XPCTR          4   O  7 55DSPATR(HI)
     A            XTNLU          5  0H
     A*
     A** FORMAT FF0190C0 **
     A          R FF0190C0                  SFLCTL(FF0190S0)
     A                                      SFLSIZ(0045)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVERLAY
     A                                      TEXT('CONTROL FILE REVIEW SCREEN')
     A                                      CA12
     A            DSPREC         4  0H      SFLRCDNBR
     A                                  1  2'POSITION PROFIT CENTRE REVIEW'
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A  88                              3  9'Branch'
     A                                  3 19'Customer/'
     A                                  3 32'Book'
     A                                  3 40'Instrument'
     A                                  3 54'Profit'
     A                                  4 19'Broker'
     A                                  4 40'Type'
     A                                  4 54'Centre'
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A** FORMAT FF0190S1 **
     A          R FF0190S1                  SFL
     A                                      TEXT('POSITION ENQUIRY SUBFILE')
     A            YYRNO          2   O  9  2DSPATR(HI)
     A            YMTH           3   O  9  5DSPATR(HI)
     A            YPCAL          1   O  9 10DSPATR(HI)
     A*                                                    Begin of /*CFF004*/
     A*********** YSTRP         12   O  9 14DSPATR(HI)
     A*********** YNETP          7  0O  9 27EDTCDE(L)
     A            YSTRP         16   O  9 14DSPATR(HI)
     A            YNETP          7  0O  9 31EDTCDE(L)
     A*                                                    End of   /*CFF004*/
     A                                      DSPATR(HI)
     A*
     A** FORMAT FF0190C1 **
     A          R FF0190C1                  SFLCTL(FF0190S1)
     A                                      SFLSIZ(0048)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  06                                  SFLDSP
     A  07                                  SFLDSPCTL
     A N06N07                               SFLCLR
     A N06N07                               SFLDLT
     A  08                                  SFLEND
     A                                      OVERLAY
     A                                      TEXT('CONTROL FILE ENQUIRY SCREEN')
     A                                      CA12
     A            DSPREK         4  0H      SFLRCDNBR
     A                                  1  2'RELATED POSITIONS ENQUIRY SCREEN'
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A  88                              3  2'Branch:'
     A            YBRCA          3   O  3 10DSPATR(HI)
     A N88                                  DSPATR(ND)
     A                                  3 15'Cust/Brok :'
     A            YCBCD          6   O  3 27DSPATR(HI)
     A                                  3 39'Book:'
     A            YBOKC          2   O  3 45DSPATR(HI)
     A                                  3 49'Instr:'
     A            YISTT          5   O  3 56DSPATR(HI)
     A                                  3 63'Profit ctr:'
     A            YPCTR          4   O  3 75DSPATR(HI)
     A*
     A                                  5  2'Yr'
     A                                  5  5'Mth'
     A                                  5  9'P/C'
     A                                  5 14'Strike'
     A                                  6 14'Price'
     A*                                                    Begin of /*CFF004*/
     A***********                       5 27'Net Current'
     A***********                       6 27'Position'
     A                                  5 31'Net Current'
     A                                  6 31'Position'
     A*                                                    End of   /*CFF004*/
     A*
     A                                  7  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A** FORMAT FF0190S2 **
     A          R FF0190S2                  SFL
     A                                      TEXT('TRADE ENQUIRY SUBFILE')
     A            YYRNO          2   O  9  2DSPATR(HI)
     A            YMTH           3   O  9  5DSPATR(HI)
     A            YPCAL          1   O  9 10DSPATR(HI)
     A*                                                    Begin of /*CFF004*/
     A*********** YSTRP         12   O  9 14DSPATR(HI)
     A*********** YTREF          6   O  9 27DSPATR(HI)
     A*********** YPRSL          1   O  9 35DSPATR(HI)
     A            YSTRP         16   O  9 12DSPATR(HI)
     A            YTREF          6   O  9 29DSPATR(HI)
     A            YPRSL          1   O  9 37DSPATR(HI)
     A*                                                    End of   /*CFF004*/
     A            YOPEN          5  0O  9 41EDTCDE(L)
     A N60
     AON56                                  DSPATR(HI)
     A  60
     AO 56                                  DSPATR(ND)
     A            YOPENA         5   O  9 41
     A  60
     AO 56                                  DSPATR(HI)
     A N60
     AON56                                  DSPATR(ND)
     A            YTPRC          4   O  9 52DSPATR(HI)
     A            YCOMT         15   O  9 61DSPATR(HI)
     A*
     A** FORMAT FF0190C2 **
     A          R FF0190C2                  SFLCTL(FF0190S2)
     A                                      SFLSIZ(0048)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  06                                  SFLDSP
     A  07                                  SFLDSPCTL
     A N06N07                               SFLCLR
     A N06N07                               SFLDLT
     A  08                                  SFLEND
     A                                      OVERLAY
     A                                      TEXT('CONTROL FILE ENQUIRY SCREEN')
     A                                      CA12
     A            DSPREK         4  0H      SFLRCDNBR
     A                                  1  2'RELATED TRADES ENQUIRY SCREEN'
     A                                      DSPATR(HI UL)
     A            RUNA           7   O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A  88                              3  2'Branch:'
     A            YBRCA          3   O  3 10DSPATR(HI)
     A N88                                  DSPATR(ND)
     A                                  3 15'Cust/Brok :'
     A            YCBCD          6   O  3 27DSPATR(HI)
     A                                  3 39'Book:'
     A            YBOKC          2   O  3 45DSPATR(HI)
     A                                  3 49'Instr:'
     A            YISTT          5   O  3 56DSPATR(HI)
     A                                  3 63'Profit ctr:'
     A            YPCTR          4   O  3 75DSPATR(HI)
     A*
     A                                  5  2'Yr'
     A                                  5  5'Mth'
     A                                  5  9'P/C'
     A                                  5 14'Strike'
     A                                  6 14'Price'
     A*                                                    Begin of /*CFF004*/
     A***********                       5 27'Trade'
     A***********                       5 34'Trade'
     A***********                       6 34'Type'
     A***********                       5 40'No.Open'
     A***********                       6 40'Contracts'
     A***********                       5 50'Prof.Ctr'
     A                                  5 29'Trade'
     A                                  5 35'Trade'
     A                                  6 35'Type'
     A                                  5 41'No.Open'
     A                                  6 41'Contracts'
     A                                  5 51'Profit'
     A                                  6 51'Centre'
     A*                                                    End of   /*CFF004*/
     A                                  5 61'Narrative'
     A                                  7  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A** FORMAT FF0190AM **
     A          R FF0190AM                  TEXT('AMEND PROFIT CENTRE')
     A                                      CA12
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                  1  2'POSITION PROFIT CENTRE AMENDMENT'
     A                                      DSPATR(HI UL)
     A***                                          Start   /*104199*/
     A                                  1 37'FF0190'
     A***                                          End     /*104199*/
     A            RUNA           7   O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3   O  1 78TEXT('WORK STATION ID')
     A  88                              5  2'Branch.............:'
     A            ABRCA          3   O  5 23DSPATR(HI)
     A N88                                  DSPATR(ND)
     A                                      OVRDTA
     A  57                              7  2'Customer...........:'
     A  57        ACBCD          6   O  7 23DSPATR(HI)
     A                                      OVRDTA
     A N57                              7  2'Book...............:'
     A N57        ABOKC          2   O  7 23DSPATR(HI)
     A                                      OVRDTA
     A                                  9  2'Instrument.........:'
     A            AISTT          5   O  9 23DSPATR(HI)
     A                                      OVRDTA
     A                                 13  2'Profit Centre......:'
     A            APCTR          4   B 13 23DSPATR(HI)
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PC)
     A  35                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20  3'Update by another work station has-
     A                                       occured press ENTER to continue'
     A N35                                  DSPATR(ND)
     A  35                                  DSPATR(RI)
     A                                      OVRATR
     A                                 22  3'without your update taking place.'
     A N35                                  DSPATR(ND)
     A  35                                  DSPATR(RI)
     A                                      OVRATR
     A*
     A** FORMAT FF0190EF **
     A          R FF0190EF                  TEXT('EMPTY SUBFILE')
     A                                      CA12
     A                                      PUTOVR
     A                                 20 27'** NO DETAILS TO DISPLAY **'
     A                                      DSPATR(HI RI)
     A*
     A** FORMAT FF0190F2 **
     A          R FF0190F2                  TEXT('DISPLAY MESSAGES')
     A                                      CA12
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A            ERRMSG        76     23  2DSPATR(HI)
     A  15                                  DSPATR(RI)
     A                                      OVRDTA
     A            INFMSG        76     24  2
     A                                      OVRDTA
     A*
     A** FORMAT FF0190F3 **
     A          R FF0190F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
