      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Spot Prices Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0235DF - SPOT PRICES MAINTENANCE                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CAAA03             Date 12Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. CFF004             Date 28AUG96               *
     A*                 S01253             Date 17APR90               *
     A*                 E20995             DATE 04APR90               *
     A*                 S01199             DATE 01/08/89              *
     A*                 E17096             DATE 15/02/88              *
     A*                 S01179             DATE 21/10/88              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CAAA03 - Webfacing Changes                                   *
     A*  CFF004  -  Increase in size of Price Fields.                 *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  E20995  -  FIX DELETE OPTION TO USE F10 INSTEAD OF CMD/4     *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  E17096  -  CHANGE CMD/5 TO F12 ON DISPLAY FILE.              *
     A*  S01179  -  AS400 CONVERSION                                  *
     A*             (REPLACE RTGAID KEYWORD WITH FUNCTION KEYS        *
     A*             AND INDICATORS AS IT IS NOT SUPPORTED             *
     A*             ON THE AS400)                                     *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A********************************************************************
     A**    THE BASIS OF THIS CODING WAS                                **
     A**    GENERATED BY PROGRAM  PRGGENX          25/06/87             **
     A********************************************************************
     A                                      REF(MFRFL)
     A***********                                                     /S01179/
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A**********                                     Begin /S01199/
     A**********                                     End   /S01199/
     A                                      CA03
     A                                      CA12
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A* RECORD DETAILS SCREEN **
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0235F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *** Start of change CAAA03
     A                                      OVERLAY
      *** End of change CAAA03
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      BLINK
     A**********                            ROLLUP                     /S01179/
     A**********                            ROLLDOWN                   /S01179/
     A                                      ROLLUP(40)
     A                                      ROLLDOWN(41)
     A**********                            RTGAID(1)                /S01179/
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A            DELT           7A  O  6 72DSPATR(HI BL)
     A                                      TEXT('Flashing "DELETED"')
     A                                      OVRDTA
     A                                  8  2'Instrument Name'
     A            SISTN     R        O  8 28REFFLD(ISTN)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A N06                             10  2'Old spot price'
     A  06                             10  2'Current spot price'
     A*                                                      Begin of /*CFF004*/
     A*********** SOSPR         12A  O 10 28DSPATR(HI)
     A            SOSPR         16A  O 10 28DSPATR(HI)
     A*                                                      End of   /*CFF004*/
     A                                      TEXT('Old spot price')
     A                                      OVRDTA
     A N06                             12  2'New spot price'
     A*                                                      Begin of /*CFF004*/
     A*********** SNSPR         12A  B 12 28DSPATR(HI)
     A            SNSPR         16A  B 12 28DSPATR(HI)
     A*                                                      End of   /*CFF004*/
     A                                      TEXT('New spot price')
     A N06N34                               DSPATR(UL)
     A  06                                  DSPATR(PR)
     A N06                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                      OVRDTA
     A* KEY INPUT SCREEN **
     A          R FF0235F1
     A                                      BLINK
      *** Start of change CAAA03
     A                                      OVERLAY
      *** End of change CAAA03
     A                                      PUTOVR
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'Spot Prices Maintenance'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('Workstation Identifier')
     A            SFFTXT        34A  O  2  8TEXT('Market-name, -date')
     A                                      DSPATR(HI)
     A************                   O  2 44'Cmd/5 for new market'      /E17096/
     A                               O  2 44'F12 for new market'
     A************                                                      /E17096/
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A                                  3  2'Action:'
     A            ACTN           1A  B  3 10DSPATR(HI)
     A                                      TEXT('Action-code I/A/D/E')
     A N05N21                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  21                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  3 12'Instrument'
     A            SMRKT     R        O  3 23REFFLD(MRKT)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A            SINST          3A  B  3 26DSPATR(HI)
     A                                      TEXT('Instrument within market')
     A N05N22                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  22                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  3 62'Review from'
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            SMRKTR    R        O  3 74REFFLD(MRKT)
     A                                      TEXT('Market-code for Review')
     A                                      DSPATR(HI)
     A  05                                  DSPATR(ND)
     A                                      OVRDTA
     A            SINSTR    R        B  3 77REFFLD(SINST *SRC)
     A                                      TEXT('Instrument for Review')
     A                                      DSPATR(HI UL)
     A  05                                  DSPATR(PR ND)
     A                                      OVRDTA
     A                                  4  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A* SUBFLREC SCREEN **
     A          R FF0235S0                  SFL
     A            ACTN      R        I  5  2DSPATR(HI UL)
     A                                      AUTO(RA)
     A                                      REFFLD(ACTN *SRC)
     A  09                                  DSPATR(PC)
     A            STAT           1A  O  5  4DSPATR(BL HI)
     A                                      TEXT('Flashing "D" if deleted')
     A            SMRKT     R        O  5  6DSPATR(HI)
     A                                      REFFLD(MRKT)
     A            SINST     R        O  5  9DSPATR(HI)
     A                                      REFFLD(SINST *SRC)
     A            SISTN     R        O  5 18DSPATR(HI)
     A                                      REFFLD(SISTN *SRC)
     A            SOSPR     R        O  5 39DSPATR(HI)
     A                                      REFFLD(SOSPR *SRC)
     A* SUBFLCTL SCREEN **
     A          R FF0235C0                  SFLCTL(FF0235S0)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0018)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A**********                            ROLLUP                   /S01179/
     A*N04******                            ROLLDOWN                 /S01179/
     A                                      ROLLUP(40)
     A N04                                  ROLLDOWN(41)
     A**********                            RTGAID(1)                /S01179/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  1  2'Spot Prices Maintenance'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('Workstation Identifier')
     A            SFFTXT        34A  O  2 28TEXT('Market-name, -date')
     A                                      DSPATR(HI)
     A                                  3 62'Review from'
     A            SMRKTR    R        O  3 74REFFLD(MRKT)
     A                                      DSPATR(HI)
     A            SINSTR    R        B  3 77DSPATR(HI UL)
     A                                      REFFLD(SINSTR *SRC)
     A                                  4  2'X   Instrument  Name              -
     A                                         Spot price                      -
     A                                               '
     A                                      DSPATR(UL)
     A* SCREEN MESSAGES **
     A          R FF0235F2
      *** Start of change CAAA03
     A                                      OVERLAY
      *** End of change CAAA03
     A                                      PUTOVR
     A*N23*******                           CF04               /E20995/
     A N23                                  CF10
     A*                                                        /E20995/
     A                                      BLINK
     A            ERRMSG        76     23  2TEXT('Error codes')
     A                                      OVRDTA
     A            INFMSG        76     24  2TEXT('Information messages')
     A                                      OVRDTA
     A* CLEAR SCREEN **
     A          R FF0235F3
     A                                  1  2' '
