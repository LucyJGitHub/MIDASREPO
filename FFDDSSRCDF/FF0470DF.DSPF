      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF End of centre control')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0470DF - END OF CENTRE INITIATION AND CONTROL              *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. E21810             DATE 16JAN91               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  E21810  -  DISPLAY CORRECT FUNCTION KEYS AT FOOT OF SCREEN.  *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A                                      CA12
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0470S0                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SMRKT          2A  O  7  4DSPATR(HI)
     A                                      TEXT('Market Centre')
     A            SMNAM         20A  O  7 10DSPATR(HI)
     A                                      TEXT('Market Name')
     A            SBDAT          7A  O  7 32DSPATR(HI)
     A                                      TEXT('Business Date')
     A            SMSTA         13A  O  7 41DSPATR(HI)
     A                                      TEXT('Market Centre Status')
     A            SECST         10A  O  7 57DSPATR(HI)
     A                                      TEXT('End of Centre Status')
     A            SECFL          6A  O  7 70DSPATR(BL)
     A                                      TEXT('End of Centre Failed Status')
     A                                      DSPATR(HI)
     A          R FF0470C0                  SFLCTL(FF0470S0)
     A                                      KEEP
     A                                      SFLSIZ(0031)
     A                                      SFLPAG(0012)
     A                                      TEXT('Subfile Control Record Format-
     A                                      ')
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A  32                                  SFLCLR
     A  33                                  SFLEND
     A            DSPNUM         2S 0H      SFLRCDNBR
     A                                      TEXT('Display No.')
     A                                  2  2'END OF CENTRE INITIATION AND CONTR-
     A                                      OL'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA      R        O  2 55
     A                                  2 64TIME
     A                                  2 74'WID'
     A            WSID           3A  O  2 78TEXT('Workstation ID')
     A                                  4  9'Market'
     A                                  4 31'Business'
     A                                  4 44'Market'
     A                                  4 57'End of centre status'
     A                                  5  3'Code'
     A                                  5 17'Name'
     A                                  5 33'date'
     A                                  5 44'status'
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A          R FF0470F0                  TEXT('Prompt and Footings Record Fo-
     A                                      rmat')
     A                                      KEEP
     A                                      OVERLAY
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                 19  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 21  2'Action:'
     A            ACTN           1A  B 21 10DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      TEXT('Action code')
     A                                 21 15'Market code:'
     A            PMRKT          2A  B 21 28DSPATR(HI)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(UL)
     A  35                                  DSPATR(PC)
     A                                      TEXT('Market code')
     A  42        INFMSG        76A  O 22  2DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      TEXT('Informational Msgs.')
     A  36        ERRMSG        76A  O 23  2DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      TEXT('Error codes')
     A*                                              Begin /E21810/
     A                                 24  2'Enter:Action Code I,H,C or O and s-
     A                                      election:F/3:F/12:Help:Rollup:Ro-
     A                                      lldown'
     A**********                       24  2'Enter:Action Code I,H,C or O and s-
     A**********                            election:F/1:F/5:Help:Rollup:Ro-
     A**********                            lldown'
     A*                                              End   /E21810/
