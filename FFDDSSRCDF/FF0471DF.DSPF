      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF End of Centre monitor')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0471DF - END OF CENTRE MONITOR                             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01253             Date 17Apr90               *
     A*                 E20768             DATE 20MAR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           INVITE is in this DSPF but is not used.             *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  E20768  -  USE CMD KEY CA12 NOT CA05                         *
     A*  S01199  -  HELP SYSTEM                                   *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*      WAITRCD(30)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0471S0                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SMRKT2    R        O  6  2DSPATR(HI)
     A                                      REFFLD(MRKT)
     A            SMKTN2    R        O  6  6DSPATR(HI)
     A                                      REFFLD(MKTN)
     A            SBUSA     R        O  6 32DSPATR(HI)
     A                                      REFFLD(RUNA)
     A                                      TEXT('Market Business Date')
     A            SECAIX        10A  O  6 43DSPATR(HI)
     A                                      TEXT('End of Centre Status Text')
     A            SECFIX         6A  O  6 57DSPATR(BL)
     A                                      DSPATR(HI)
     A                                      TEXT('End of Centre Failed Text')
     A          R FF0471C0                  SFLCTL(FF0471S0)
      ** Start of CAAA02
     A******************                    INVITE
      ** End of CAAA02
     A                                      OVERLAY
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      TEXT('MARKET CONTROL-RECORD')
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A  32                                  SFLCLR
     A  33                                  SFLEND
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                  2  2'END OF CENTRE MONITOR'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA     R        O  2 55REFFLD(RUNA)
     A                                  2 64TIME
     A                                  2 74'WID'
     A            SWSID          3A  O  2 78TEXT('Workstation Identifier')
     A            SMRKT     R        B  4  2DSPATR(HI)
     A                                      REFFLD(MRKT)
     A N37                                  DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A                                  4  5'Market Centre'
     A                                  3 32'Business'
     A                                  4 34'Date'
     A                                  4 43'End of Centre Status'
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A          R FF0471S1                  SFL
     A                                      TEXT('MESSAGE SUBFILE-RECORD')
     A            SECMT     R        O  6  2DSPATR(HI)
     A                                      REFFLD(ECMT)
     A                                      EDTWRD('  :  :  ')
     A            SECMG     R        O  6 11DSPATR(HI)
     A                                      REFFLD(ECMG)
     A          R FF0471C1                  SFLCTL(FF0471S1)
      ** Start of CAAA02
     A******************                    INVITE
      ** End of CAAA02
     A                                      OVERLAY
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      TEXT('MESSAGE CONTROL-RECORD')
     A**********                            CA05                        /E20768/
     A                                      CA12
     A**********                                                        /E20768/
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A  32                                  SFLCLR
     A  33                                  SFLEND
     A            SRECNO         4S 0H      SFLRCDNBR
     A                                      TEXT('Line whose page shows 1st')
     A                                  2  2'END OF CENTRE MONITOR'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA     R        O  2 52REFFLD(RUNA)
     A                                  2 62TIME
     A                                  2 73'WID'
     A            SWSID          3A  O  2 77TEXT('Workstation Identifier')
     A                                  3  2'Market Centre'
     A            SMRKT     R        O  4  2DSPATR(HI)
     A                                      REFFLD(MRKT)
     A            SMKTN     R        O  4  6DSPATR(HI)
     A                                      REFFLD(MKTN)
     A                                  5  2'EOC Messages                      -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A          R FF0471S2                  SFL
     A                                      TEXT('LOWER SUBFILE-RECORD')
     A            SECMTM         4  0O 15  2DSPATR(HI)
     A                                      TEXT('EOC Message Time to minute')
     A                                      EDTWRD('  :  ')
     A            SMRKT2    R        O 15  8DSPATR(HI)
     A                                      REFFLD(MRKT)
     A            SECMG     R        O 15 11DSPATR(HI)
     A                                      REFFLD(ECMG)
     A          R FF0471C2                  SFLCTL(FF0471S2)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0006)
     A                                      TEXT('LOWER CONTROL-RECORD')
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A                                 14  2'Latest Important EOC Messages for -
     A                                      all markets                        -
     A                                              '
     A                                      DSPATR(UL)
     A          R FF0471F0
     A                                      TEXT('FOOTING: ERROR MESSAGES')
     A  35        SINFMG        76A  O 22  2DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      TEXT('Information Messages')
     A  36        SERRMG        76A  O 23  2DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      TEXT('Error Codes')
     A            SCKEYS        76A  O 24  2TEXT('List of Command Keys')
