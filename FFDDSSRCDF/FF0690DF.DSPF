      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Define mkts for EOC proc. in COB')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0690DF - DEFINE MARKETS THAT ARE TO HAVE EOC PROCESSING    *
     A*                INCLUDED IN COB                                *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A***********                                    Begin /S01199/
     A***********                                    End   /S01199/
     A                                      INDARA
     A* SUBFLREC SCREEN **
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF0690S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A  09                                  SFLNXTCHG
     A            SMRKT          2   O  9  6DSPATR(HI)
     A            SMKTN         20   O  9 13DSPATR(HI)
     A            SDATE          7   O  9 41DSPATR(HI)
     A            SCOBI          1   B  9 65
     A                                      DSPATR(HI)
     A N10                                  DSPATR(UL)
     A  10                                  DSPATR(RI)
     A            SAPRP          1   H
     A* SUBFLCTL SCREEN **
     A          R FF0690C0                  SFLCTL(FF0690S0)
     A  02                                  SFLDSP
     A  02                                  SFLDSPCTL
     A                                      SFLPAG(0008)
     A                                      SFLSIZ(0009)
     A  03                                  SFLEND
     A                                      OVERLAY
     A* HEADINGS
     A                                  1  4'FUTURES AND OPTIONS END OF CENTRE +
     A                                      PROCESSING '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            RUNA      R        O  1 52
     A                                  1 60TIME
     A                                  1 73'WID'
     A            WSID           3A  O  1 78TEXT('Workstation Identifier')
     A                                  3  5'The following Market Centres shoul+
     A                                      d have had End of Centre Processing'
     A                                  4  5'performed during Input Cycle for t+
     A                                      he current Market Business Date but'
     A                                  5  5'did not.'
     A                                  7  5'CODE'
     A                                  7 13'NAME'
     A                                  7 37'BUSINESS DATE'
     A                                  7 59'INCLUDE IN COB'
     A*
     A          R FF0690F0
     A                                 19  5'Enter "N" against any Market that +
     A                                      is NOT to have End of Centre'
     A                                 20  5'Processing performed as part of Cl+
     A                                      ose of Business Processing, otherw+
     A                                      ise'
     A                                 21  5'it will be included in the Close o+
     A                                      f Business Run (for today only).'
     A  17        SEMSG         76   O 23  2TEXT('ERROR CODES')
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 24 18'Rollup/Rolldown           Enter '
     A                                 24 58'Help'
