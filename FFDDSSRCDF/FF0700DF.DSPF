      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Historic Trans. Enquiry Prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0700DF - HISTORICAL TRANSACTION ENQUIRY - PROMPT           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 12Feb97               *
      *                 CFF004             Date 20Sep96               *
     A*                 S01457             DATE 29DEC93               *
     A*                 S01117             DATE 05DEC90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  CFF004 -   Increase in size of Price Fields.                 *
     A*  S01457 -   Enable Brokers to deal with OTC's.                *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHECK(AB)
     A                                      PRINT(MPRINT)
     A                                      CA03(01 '  F:3 - to end program')
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*
     A          R FF0700D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
     A*                                              Begin /S01199/
     A*                                                      Begin of /*CFF004*/
     A*                                                      End of   /*CFF004*/
     A*                                              End   /S01199/
     A**                                             Begin /112563/
     A                                  1  6'FF0700'
     A**                                             End   /112563/
     A                                  1 23'HISTORIC TRANSACTION ENQUIRY'
     A                                      DSPATR(HI UL)
     A            RUNA           7A  O  1 55
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A                                  3 11'Branch code -'
     A*                                              Begin /S01117/
     A  28                                  DSPATR(ND)
     A*                                              End   /S01117/
     A            SBCD           3A  B  3 27DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A N29N28                               DSPATR(UL)
     A  28                                  DSPATR(PR)
     A*                                              Begin /S01117/
     A  28                                  DSPATR(ND)
     A*                                              End   /S01117/
     A                                  5 11'Market code -'
     A            SMKT           2A  B  5 27DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(UL)
     A N20 28
     AO 30                                  DSPATR(PC)
     A                                  5 32'Instrument Code -'
     A            SINS           3A  B  5 52DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(UL)
     A                                  7 17'Month -'
     A            SMN            3A  B  7 27DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
     A                                  7 43'Year -'
     A            SYR            2A  B  7 52DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
     A                                  9 25'OR'
     A                                      DSPATR(UL)
     A                                  9 28'OTC code -'
     A            SOTC           5A  B  9 40DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                 11 16'Put/call -'
     A            SPCL           1A  B 11 27DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL)
     A                                 11 36'Strike Price -'
     A*                                                      Begin of /*CFF004*/
     A*********** SSTP          12A  B 11 52DSPATR(HI)
     A            SSTP          16A  B 11 52DSPATR(HI)
     A*                                                      End of   /*CFF004*/
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL)
     A*                Start of change                 /*S01457*/
     A**********                       13 12'Customer short name/code -'
     A N26                             13 12'Customer short name/code -'
     A  26                             13 12'C''party short name/code -'
     A*                End of change                   /*S01457*/
     A            SCUS          10A  B 13 40DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A                                 13 55'( * = no customer )'
     A                                 15 14'Broker short name/code -'
     A            SBRK          10A  B 15 40DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL)
     A                                 15 55'( * = no broker )'
     A                                 17 27'Book code -'
     A            SBOK           2A  B 17 40DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL)
     A                                 19 32'Date -'
     A            SDAT           6A  B 19 40DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL)
     A**                                             Begin /112563/
     A***********                      23  2'F:3 - press to end program'
     A                                 23  2'F3=Exit'
     A**                                             End   /112563/
     A  20        SERCD         78A  O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
