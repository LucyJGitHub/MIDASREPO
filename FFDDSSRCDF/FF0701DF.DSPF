      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Historic Trans. Enquiry Display')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0701DF - HISTORIC TRANSACTION ENQUIRY                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CFF007             Date 13Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01457             Date 29Dec93               *
      *                 E28963             Date 30Sep91               *
     A*                 S01332             DATE 16MAY91               *
     A*                 S01117             DATE 27APR90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 E17440             DATE 11/05/89              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
     A*  S01457 -   Enable Brokers to deal with OTC's.                *
     A*  E28963  -  Add help tags to fields introduced by S01332      *
     A*  S01332  -  EXPOSURE MANAGEMENT RELEASE 10                    *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*  E17440  -  COMMAND KEYS USED SHOULD BE F3 (TO END) AND       *
     A*             F12 (FOR INITIAL SCREEN)                          *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
      *                                              Begin /S01332/
     A  60                                  CF01
      *                                              End   /S01332/
     A*                                              Begin /E17440/
     A**********                            CA01(01 'F:1 - to end program')
     A**********                            CA05(05 'F:12 - to first program')
     A                                      CA03(01 'F:3 - to end program')
     A                                      CA12(05 'F:12 - to first program')
     A*                                              End   /E17440/
     A**********                                     Begin /S01199/
     A**********                                     End   /S01199/
     A                                      INDARA
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*
     A          R FF0701F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                  1 23'HISTORIC TRANSACTION ENQUIRY'
     A                                      DSPATR(HI UL)
     A            RUNA      R        O  1 55TEXT('Run-date in DDMMMYY Format')
     A                                  1 64TIME
     A                                  1 74'WID'
     A            WSID           3A  O  1 78TEXT('WORK STATION ID')
     A            SBRNM         30A  O  2  2DSPATR(HI)
     A*                                                        Begin /S01117/
     A N22                                  DSPATR(ND)
     A*                                                        End   /S01117/
     A*                Start of change                 /*S01457*/
     A********                          3  2' Date Reference Instrument P/C  St-
     A********                              rike  Cntrts P/S  Price   Cls Broke-
     A********                              r Cust  Bk'
     A N26                              3  2' Date Reference Instrument P/C  St-
     A                                      rike  Cntrts P/S  Price   Cls Broke-
     A                                      r Cust  Bk'
     A N26                                  DSPATR(UL)
     A  26                              3  2' Date Reference Instrument P/C  St-
     A                                      rike  Cntrts P/S  Price   Cls Broke-
     A                                      r C''pt Bk'
     A  26                                  DSPATR(UL)
     A*                End of change                   /*S01457*/
     A*
     A          R FF0701S0                  SFL
     A*
     A            HTRDT          5S 0H
     A            STRDT          7A  O  5  2TEXT('Transaction date')
     A            TNBR      R     Y  O  5 10
      **Start of change CFF007
     A**********  SKKEY         11A  O  5 17TEXT('Instrument')                                CFF007
     A**********  SPCAL          1A  O  5 29                                                  CFF007
     A**********  SSTRP         11A  O  5 31                                                  CFF007
     A**********  NUCO      R        O  5 43EDTCDE(Z)                                         CFF007
     A            SDLFG          1A  O  5 17TEXT('Delete Flag')
     A            SKKEY         11A  O  5 19TEXT('Instrument')
     A            SPCAL          1A  O  5 31
     A            SSTRP         10A  O  5 33
     A            SNCOSC         4  0O  5 44EDTCDE(Z)
      **End of change CFF007
     A            TRTY      R        O  5 49
     A            SCOPR         11A  O  5 51TEXT('Contract Price')
     A            SCLOS          1A  O  5 63TEXT('Open Contracts Ind.')
     A            SBOCO          6A  O  5 65
     A            SCUSC          6A  O  5 72
     A            BOKC      R        O  5 79
     A*
     A          R FF0701C0                  SFLCTL(FF0701S0)
     A*
     A                                      ROLLUP(45)
     A                                      ROLLDOWN(49)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A N46                                  SFLDSP
     A N47                                  SFLDSPCTL
     A  44                                  SFLDLT
     A  41                                  SFLCLR
     A  39                                  SFLEND
     A  51
     AO 52                                  SFLMSGID(CPF5203 QCPFMSG)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*                                              Begin /E28963/
     A*                                              End   /E28963/
     A                                 19  2'Review date -'
     A            SDATE          6A  B 19 17TEXT('Review date')
      *                                             Begin /S01332/
     A N61                                  DSPATR(PC)
      *                                             End   /S01332/
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *                                             Begin /S01332/
     A                                 19 26'Reference for transact-
     A                                      ion enquiry - '
     A            SREF           6A  B 19 63TEXT('Reference')
     A  61                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A*                                              Begin /E17440/
     A**********                       22  2' Press   F:1 to end   F:12 for +
     A***********                      22  2' Press   F:3 to end   F:12 for +
     A***********                                    End   /E17440/
     A***********                           initial screen  Rollup and Rolldown'
     A N60                             22  2'F3=Exit  F12=Previous Screen  +
     A                                       Rollup and Rolldown'
     A  60                             22  2'F1=Initial Enquiry  F3=Exit  F12-
     A                                      =Previous  Rollup  Rolldown'
      *                                              End   /S01332/
     A            MESS          50A  O 20  2TEXT('NO RECORDS MESSAGES')
     A N48                                  DSPATR(ND)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(HI)
     A                                      OVRDTA
     A            SERCD         78A  O 21  2TEXT('ERROR CODE FIELD')
     A N21                                  DSPATR(ND)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(HI)
     A                                      OVRDTA
