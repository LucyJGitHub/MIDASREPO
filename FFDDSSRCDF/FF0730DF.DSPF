      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Instrument Holding Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0730DF - INSTRUMENT HOLDINGS ENQUIRY                       *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 112563             DATE 24JAN97               *
     A*                 CFF004             DATE 23SEP96               *
     A*                 S01117             DATE 01MAY90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  112563  -  Header and footer changes required for GUI        *
     A*             Multilanguage support.                            *
     A*  CFF004  -  Increase in size of Price Fields.                 *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM                                   *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      CF03
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                      INDARA
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A*
     A          R FF0730F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  1  2'INSTRUMENT HOLDINGS ENQUIRY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A**                                             Begin /112563/
     A                                  1 38'FF0730'
     A**                                             End   /112563/
     A            RUNA      R           1 53TEXT('Run-date in DDMMMYY Format')
     A                                  1 61TIME
     A                                  1 71'WID'
     A            WSID           3A  O  1 75
     A*
     A*
     A          R FF0730F2
     A*
     A*
     A                                      OVERLAY
     A                                      CLRL(*NO)
     A*                                              Begin /S01117/
     A***********                       2  1'Branch'
     A*********** SBRNM         30A  O  2 12DSPATR(HI)
     A N61                              2  1'Branch'
     A N61        SBRNM         30A  O  2 12DSPATR(HI)
     A*                                              End   /S01117/
     A                                  3  1'Market'
     A            SMKTN         20A  O  3 12DSPATR(HI)
     A            SNAR1          8A  O  3 41
     A            SDET1         30A  O  3 50DSPATR(HI)
     A                                  4  1'Instrument'
     A            SISTN         20A  O  4 12DSPATR(HI)
     A            SDET2         10A  O  4 50DSPATR(HI)
     A                                  5  1'Period'
     A            SMTHN          3A  O  5 12DSPATR(HI)
     A            SYRNO          2A  O  5 16DSPATR(HI)
     A            SPCAL          1A  O  5 20DSPATR(HI)
     A*                                                    Begin of /*CFF004*/
     A*********** SSTRP         12A  O  5 22DSPATR(HI)
     A***********                       5 37'Market Price'
     A*********** SNEWP         12A  O  5 50DSPATR(HI)
     A            SSTRP         16A  O  5 22DSPATR(HI)
     A                                  5 41'Market Price'
     A            SNEWP         16A  O  5 54DSPATR(HI)
     A*                                                    End of   /*CFF004*/
     A            SNAR2          8A  O  6  7
     A                                  6 22'Overnight'
     A                                  6 32'Changes'
     A                                  6 40'Position'
     A                                  6 52'P/L This Month'
     A                                  6 67'Average Price'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A*
     A*
     A          R FF0730S0                  SFL
     A*
     A            SDET3         20A  O  8  2DSPATR(HI)
     A            SNOPE          6Y 0O  8 23EDTCDE(L)
     A                                      DSPATR(HI)
     A            SNCHT          6Y 0O  8 31EDTCDE(L)
     A                                      DSPATR(HI)
     A            SNOPC          6Y 0O  8 39EDTCDE(L)
     A                                      DSPATR(HI)
     A            SPLM          21A  O  8 47DSPATR(HI)
     A            SAVPR         12A  O  8 69DSPATR(HI)
     A*
     A*
     A          R FF0730C0                  SFLCTL(FF0730S0)
     A*
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      PUTOVR
     A N20                                  SFLDSPCTL
     A N21                                  SFLDSP
     A  23                                  SFLCLR
     A  22                                  SFLEND
     A*
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A                                 19  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A  25N27                          20 11'T O T A L :'
     A  25N27     STNOP          6Y 0O 20 23EDTCDE(L)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A  25N27     STNCT          6Y 0O 20 31EDTCDE(L)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A  25N27     STNCP          6Y 0O 20 39EDTCDE(L)
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A  25N27     STPLM         21A  O 20 47DSPATR(HI)
     A                                      OVRDTA
     A  26                             20  2'      ** NO HOLDINGS FOR THIS COMB-
     A                                      INATION **       '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A  27        SERCD         76A  O 21  2DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A**                                             Begin /112563/
     A***********                      22  2'Enter details , F:3 to end , Rol-
     A                                 22  2'Enter details , F3=Exit , Rol-
     A**                                             End   /112563/
     A                                      lup , Rolldown or Help'
     A*                                              Begin /S01117/
     A***********                      23  2'Branch'
     A*********** SBRCD          3A  B 23  9DSPATR(HI)
     A N61                             23  2'Branch'
     A N61        SBRCA          3A  B 23  9DSPATR(HI)
     A*                                              End   /S01117/
     A  50                                  DSPATR(RI PC)
     A N50N61                               DSPATR(UL)
     A  61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 23 14'Market'
     A            SMRKT          2A  B 23 22DSPATR(HI)
     A  51                                  DSPATR(RI)
     A N51                                  DSPATR(UL)
     A N27 61
     AO 51                                  DSPATR(PC)
     A                                      OVRDTA
     A                                 23 25'Instrument'
     A            SINST          3A  B 23 37DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N52                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 23 42'OR OTC'
     A            SOTCC          5A  B 23 49DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A N53                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 23 56':Month/Year'
     A            SMNTH          3A  B 23 68DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A N54                                  DSPATR(UL)
     A                                      OVRDTA
     A            SYEAR          2A  B 23 72DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A N55                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 24  2'P/C'
     A            SPUTC          1A  B 24  6DSPATR(HI)
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(UL)
     A                                      OVRDTA
     A*                                                      Begin of /*CFF004*/
     A*********** SSPCE         12A  B 24  8DSPATR(HI)
     A            SSPCE         16A  B 24  8DSPATR(HI)
     A*                                                      End of   /*CFF004*/
     A  57                                  DSPATR(RI PC)
     A N57                                  DSPATR(UL)
     A                                      OVRDTA
     A*                                                      Begin of /*CFF004*/
     A***********                      24 21'Book/ALL'
     A*********** SBOOK          3A  B 24 30DSPATR(HI)
     A                                 24 25'Book/ALL'
     A            SBOOK          3A  B 24 34DSPATR(HI)
     A*                                                      End of   /*CFF004*/
     A  58                                  DSPATR(RI PC)
     A N58                                  DSPATR(UL)
     A                                      OVRDTA
     A*                                                      Begin of /*CFF004*/
     A***********                      24 34'Customer/ALL'
     A                                 24 38'Cust/ALL'
     A*                                                      End of   /*CFF004*/
     A            SCUST         10A  B 24 47DSPATR(HI)
     A  59                                  DSPATR(RI PC)
     A N59                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 24 58'Broker/ALL'
     A            SBROK         10A  B 24 69DSPATR(HI)
     A  60                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A                                      OVRDTA
