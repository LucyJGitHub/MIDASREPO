      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Trading Statement request')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF1270DF - TRADING STATEMENT REQUEST                         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. FO0046             Date 02APR91               *
     A*                 S01117             Date 14MAY90               *
     A*                 S01253             DATE 17APR90               *
     A*                 S01199             DATE 27FEB90               *
     A*                 E19535             DATE 07SEP89               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  FO0046  -  RCF CHANGES - ADD NEW FORMAT                      *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*  S01199  -  HELP SYSTEM.                                  *
     A*  E19535  -  CORRECTS FOOTER TO READ "F3: TO EXIT"             *
     A*                                                               *
     A*****************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      DFRWRT(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
     A*                                              Begin /FO0046/
     A                                      CA12
     A*                                              End   /FO0046/
     A*                                              Begin /S01199/
     A*                                              End   /S01199/
     A          R FF1270S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A  33                                  SFLNXTCHG
     A            SCSNM     R        B  7  3DSPATR(HI)
     A                                      TEXT('Customer Shortname/Number')
     A                                      REFFLD(CSNM)
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A            SCCY      R        B  7 23DSPATR(HI)
     A                                      REFFLD(CCY)
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A            SALLCY         1   B  7 36DSPATR(HI)
     A                                      TEXT('"Y" for All Currencies')
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI PC)
      * Begin /CSD027/
     A**********  SCBCD     R        H      REFFLD(CBCD)                                      CSD027
     A            SCBCD          6A  H
      * End /CSD027/
     A                                      TEXT('Hidden Customer-Number')
     A          R FF1270C0                  SFLCTL(FF1270S0)
     A                                      OVERLAY PUTOVR
     A                                      KEEP
     A                                      TEXT('CONTROL-RECORD: BRANCH')
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0015)
     A  30                                  SFLDSP
     A  31                                  SFLDSPCTL
     A  32                                  SFLINZ
     A*                                              Begin /S01199/
     A**********                                     Begin /S01117/
     A**********                                     End   /S01117/
     A*                                              End   /S01199/
     A                                  1  3'TRADING STATEMENT REQUEST'
     A                                      DSPATR(HI UL)
     A            SRUNA     R        O  1 53REFFLD(RUNA)
     A                                  1 62TIME
     A                                  1 72'WID'
     A            SWID           3   O  1 76TEXT('Workstation Identifier')
     A*                                              Begin /S01117/
     A***********                       3  3'Branch'
     A*********** SBRCD          3A  B  3 10DSPATR(HI) OVRDTA OVRATR
     A*N65*******                           DSPATR(PR)
     A**65N37****                           DSPATR(UL)
     A**37*******                           DSPATR(RI PC)
     A*                                              End   /S01117/
     A                                  5  3'Customer'
     A                                  5 23'Currency or ALL'
     A                                  6 35'(Y)'
     A*
     A*                                              Begin /FO0046/
     A          R FF1270F1
     A                                  1  3'TRADING STATEMENT REQUEST'
     A                                      DSPATR(HI UL)
     A            SRUNA     R        O  1 53REFFLD(RUNA)
     A                                  1 62TIME
     A                                  1 72'WID'
     A            SWID           3   O  1 76TEXT('Workstation Identifier')
     A*
     A                                  9 11'This request is currently being +
     A                                      processed for another user.'
     A                                      DSPATR(HI)
     A                                 11 11'Use ''Todays Reporting'' option +
     A                                      on RCF Enquiry Menu to determine'
     A                                      DSPATR(HI)
     A                                 12 11'when previous request has +
     A                                      completed, then resubmit request.'
     A                                      DSPATR(HI)
     A                                 24  2'Press Enter or F3 to continue'
     A*                                              End   /FO0046/
     A*
     A          R FF1270F0
     A                                      OVERLAY PUTOVR
     A            SERRMG        76A  O 23  2
     A  36                                  DSPATR(HI RI)
     A N36                                  DSPATR(ND)
     A                                      TEXT('Error Codes')
     A                                      OVRDTA
     A*                                                                   E19535
     A**********                       24  2'Press Enter to continue , CK/1 -
     A*                                              Begin /FO0046/
     A                                 24  2'Press Enter to continue , F3 -
     A                                      to end , F12 for previous screen -
     A                                      , or Help'
     A**********                       24  2'Press Enter to continue , F3: -
     A**********                            to end or Help'
     A*                                              End   /FO0046/
     A*                                                                   E19535
