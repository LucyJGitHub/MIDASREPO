      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Exchange-Traded LUX Display')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  DSPF/FFEXTRLDF - FF Exchange-Traded Extended LUX Display     *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      INDARA
      *
      *****************************************************************
      *                                                               *
      *   Main Display Screen                                         *
      *                                                               *
      *****************************************************************
      *
     A          R SCREENF
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A                                      OVERLAY
     A            SUSER         10A  O  1  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A            SJOB          10A  O  2  2
     A                                  2 65'Time:'
     A                                  2 71TIME
      *
     A            ##PGM         10A  H
     A                                  2 24'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
      *
     A                                  6  2'Transaction Time (HHMMSS) ........-
     A                                      . :'
     A            #6LXTIME       6A  B  6 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N50N20                               DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  7  2'Excluded from CAB Reporting ......-
     A                                      . :'
     A            #6LXCABR       1A  B  7 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N51N20                               DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                 23  2'F3=Main Menu'
     A                                 23 17'F5=Refresh'
     A N35                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 29'F12=Previous'
      *
      *****************************************************************
      *                                                               *
      *   Message subfile format                                      *
      *                                                               *
      *****************************************************************
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *****************************************************************
      *                                                               *
      *   Message subfile control                                     *
      *                                                               *
      *****************************************************************
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
