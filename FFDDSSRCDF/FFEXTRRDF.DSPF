/*STDA*  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FF Exchange-traded repair browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Futures and Options Module                           *
     A*                                                               *
     A*  FFEXTRRDF - INVALID EXCHANGE-TRADED BROWSE                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 166907             Date 27Dec99               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP014             Date 29Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP006             Date 19Mar99               *
     A*                 CAP004  *CREATE    Date 03SEP98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  166907 - Show 'Deal No. 999999 successfully inserted'        *
     A*           message on screen.                                  *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP006 - Show error message on screen                        *
     A*  CAP004 - APIs phase 3
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R FFIEXTRS1                 SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN    R        O  8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFOTRANIDR     A  O  8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDTNBR         6A  O  8 31
     A            DDMRKT         2A  O  8 39
     A            DDISCY         3A  O  8 46
     A            DDPCAL         1A  O  8 50
     A            DDSTRP        16   O  8 52
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDISTC         3   O  8 42
      * Start CAP014
     A            DDNUCO         5   H
     A            DDTRTY         1   H
     A            DDCOPR        16   H
     A            DDTRSD         6   H
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FFIEXTRS0                 SFLCTL(FFIEXTRS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A                                  1  2'FFEXTRR'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 24'FUTURES - INVALID TRADE MAINTENANC-
     A                                      E'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 31'Trade'
     A                                  6 39'Instrument'
      * Start CAP014
     A**********************************7  1'A,D'
      * End CAP014
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R FFIEXTRF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of CAP006
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of CAP006
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R FFIEXTREND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start of 166907
     A                                      PUTOVR
      * End of 166907
     A                                  1  2'FFEXTRR'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FUTURES - INVALID TRADE MAINTENAN+
     A                                      CE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      * Start of 166907
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
