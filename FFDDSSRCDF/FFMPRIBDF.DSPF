/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Market Prices Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Financial Futures and Options module                 *
      *                                                               *
      *  FFMPRIBDF - Midas FF Market Prices Browse                    *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG10450           Date 08Feb06               *
      *                 CAP183  *CREATE    Date 05Jan06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG10450 - Nondisplay Risk Factor if OTC                     *
      *  CAP183 - Conversion of Market Prices Input into modular      *
      *           structure to use APIs.                              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      **-------------------------------------------------------------------*
      ** MESSAGE SUBFILE FORMAT                                            *
      **-------------------------------------------------------------------*
     A          R FFMPRIM1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      **-------------------------------------------------------------------*
      ** MESSAGE SUBFILE CONTROL                                           *
      **-------------------------------------------------------------------*
     A          R FFMPRIM0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(FFMPRIM1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  94                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      **-------------------------------------------------------------------*
      ** TRANSACTION NUMBER SUBFILE
      **-------------------------------------------------------------------*
     A          R FFMPRIS1                  SFL
     A
     A            DDSTRPN       15S 8H      TEXT('Strike Price in num format')
     A
     A            DDOPTN         1A  B  8  4TEXT('SELECT SCREEN SUBFILE')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL)
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A            DDRECI         1   O  8  7TEXT('Record ID')
     A N93                                  DSPATR(HI)
     A  95                                  COLOR(RED)
     A  95                                  DSPATR(BL)
     A N95                                  DSPATR(ND)
     A            DDISTT         5A  O  8  9TEXT('Instrument Type')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDMTHN         3A  O  8 16TEXT('Month Name')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDYRNO         2A  O  8 20TEXT('Year Number')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPCAL         1A  O  8 24TEXT('Put/Call Indicator')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSTRP        16A  O  8 28TEXT('Stike Price in char format')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNEWP        16A  O  8 46TEXT('New Price')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDRSKF        10A  O  8 64TEXT('Risk Factor')
     A N93                                  DSPATR(HI)
      **Start of change BUG10450
     A**93*08***                            DSPATR(ND)                                      BUG10450
     A  08                                  DSPATR(ND)
      **End of change BUG10450
      *
      **-------------------------------------------------------------------*
      ** TRANSACTION NUMBER SUBFILE CONTROL                                *
      **-------------------------------------------------------------------*
      *
     A          R FFMPRIS0                  SFLCTL(FFMPRIS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FFMPRIBRW'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 18'Market Prices Transaction Details -
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  3'Opt'
     A                                      DSPATR(UL)
     A                                  4  9'Inst.'
     A                                      DSPATR(UL)
     A                                  4 16'Mth'
     A                                      DSPATR(UL)
     A                                  4 20'Yr'
     A                                      DSPATR(UL)
     A                                  4 23'P/C'
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                                  4 28'Strike Price'
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                                  4 46'New Price'
     A                                      DSPATR(UL)
     A                                  4 64'Risk Factor'
     A                                      DSPATR(UL)
     A  08                                  DSPATR(ND)
     A                                  5  9'Type'
     A                                      DSPATR(UL)
     A            DPISTT         5   B  6  9OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPMTHN         3   B  6 16OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPYRNO         2   B  6 20OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPPCAL         1   B  6 24OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPSTRP        16   B  6 28OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
      **-------------------------------------------------------------------*
      ** FRONT OFFICE ID SUBFILE
      **-------------------------------------------------------------------*
     A          R FFMPRIS3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPTN         1A  B  8  3TEXT('Select Option')
     A  92                                  DSPATR(PC)
     A N93                                  DSPATR(UL HI)
     A  93                                  DSPATR(ND PR)
     A            DDFOID        20A  O  8  6TEXT('Front Office ID')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDISTT         5A  O  8 27TEXT('Intrument Type')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDMTHN         3A  O  8 34TEXT('Month Name')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDYRNO         2A  O  8 38TEXT('Year Number')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDPCAL         1A  O  8 42TEXT('Put/Call Indicator')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDSTRP        16A  O  8 45TEXT('Strike Price')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
     A            DDNEWP        16A  O  8 62TEXT('New Price')
     A N93                                  DSPATR(HI)
     A  93                                  DSPATR(ND)
      **-------------------------------------------------------------------*
      ** FRONT OFFICE ID SUBFILE CONTROL                                   *
      **-------------------------------------------------------------------*
     A          R FFMPRIS2                  SFLCTL(FFMPRIS3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A                                      CF11
     A N97                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  96N97                               SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FFMPRIBRW'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2OVRDTA
     A                                  2 18'Market Prices Transaction Details -
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  6'Front Office ID'
     A                                      DSPATR(UL)
     A                                  5 27'Instr.'
     A                                      DSPATR(UL)
     A                                  5 34'Mth'
     A                                      DSPATR(UL)
     A                                  5 38'Yr'
     A                                  5 41'P/C'
     A                                      DSPATR(UL)
     A                                  5  2'Opt'
     A                                      DSPATR(UL)
     A                                  6 27'Code'
     A                                      DSPATR(UL)
     A                                  5 45'Strike Price'
     A                                      DSPATR(UL)
     A                                  5 62'Current Price'
     A                                      DSPATR(UL)
     A                                  3 32'by Front Office ID'
     A                                      COLOR(BLU)
     A            DPFRNT        20A  B  7  6OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPISTT         5A  B  7 27OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPMTHN         3A  B  7 34OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPYRNO         2A  B  7 38OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPPCAL         1   B  7 42OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
     A            DPSTRP        16   B  7 45OVRDTA
     A                                      DSPATR(UL)
     A                                      OVRATR
      **-------------------------------------------------------------------*
      ** SELECT SCREEN FOOTER                                              *
      **-------------------------------------------------------------------*
     A          R FFMPRIF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF09
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'Rollup/Rolldown for further record+
     A                                      s'
     A N80                                  DSPATR(HI)
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 22  2'No Data to Display'
     A  80                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
