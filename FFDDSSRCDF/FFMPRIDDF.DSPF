/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Market Prices Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Market Price API                 *
      *                                                               *
      *  FFMPRIDF - MIDAS FF Market Prices details display            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP183  *Create    Date 05Jan06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP183 - Conversion of Market Prices Input into modular      *
      *           structure to use APIs.                              *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      **------------------------------------------------------------------*
      ** MESSAGE SUBFILE FORMAT                                           *
      **------------------------------------------------------------------*
     A          R FFMPRIS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      **-------------------------------------------------------------------*
      **  MESSAGE SUBFILE CONTROL                                          *
      **-------------------------------------------------------------------*
     A          R FFMPRIS0                  TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF10
     A                                      SFLCTL(FFMPRIS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN 1                                            *
      * -------------------------------------------------------------------*
      *
     A          R FFMPRID1
     A                                      CF03
     A                                      CF12
     A                                      KEEP
     A                                      PUTOVR
     A                                  1 23'MARKET PRICES INPUT'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 55TEXT('RUN DATE')
     A                                  1 64TIME
     A                                  1 74'WID'
     A            DDWID          3A  O  1 78TEXT('WORK STATION ID')
     A                                  7 23'Market centre code - '
     A            DDMKT          2A  B  7 45DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  9 23'(Leave blank for OTC)'
      **-------------------------------------------------------------------*
      **  MAIN DISPLAY SCREEN                                              *
      **-------------------------------------------------------------------*
     A          R FFMPRID2                  TEXT('KEY INPUT')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      KEEP
     A                                      PUTOVR
     A                                  1  2'Market Prices Maintenance'
     A                                      DSPATR(HI UL)
     A            DDTITL        34A  O  2  8DSPATR(HI)
     A  05                                  DSPATR(ND)
     A                                      OVRATR
     A            BJMRDT         7A  O  1 55TEXT('RUN DATE')
     A                                  1 64TIME
     A                                  1 74'WID'
     A            DDWID          3A  O  1 78 TEXT('WORKSTATION ID')
     A                                  3  2'Action'
     A            DDACTN         1A  B  3  9DSPATR(HI)
     A                                       TEXT('Action')
     A N12N05                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  12                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  3 11'Instrument'
     A N74        DDMRKT         2A  B  3 22DSPATR(HI)
     A N13 69                               DSPATR(UL)
     A N69                                  DSPATR(PR)
     A  13                                  DSPATR(RI PC)
     A                                      OVRDTA
     A N74        DDISTC         3A  B  3 25DSPATR(HI)
     A                                      TEXT('Instrument code')
     A N14N05                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  14                                  DSPATR(RI PC)
     A                                      OVRDTA
     A  74        DDISTT         5A  B  3 22DSPATR(HI)
     A                                      TEXT('Instrument')
     A N14N05                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  14                                  DSPATR(RI PC)
     A                                      OVRDTA
     A N74                              3 29'Month'
     A                                      OVRATR
     A N74                              3 39'Year'
     A                                      OVRATR
     A            DDMTHN         3A  B  3 35DSPATR(HI)
     A                                      TEXT('Month')
     A  74                                  DSPATR(ND)
     A N15N05                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A  74
     AO 05                                  DSPATR(PR)
     A                                      OVRDTA
     A            DDYRNO         2A  B  3 44DSPATR(HI)
     A                                      TEXT('Year')
     A  74                                  DSPATR(ND)
     A N16N05                               DSPATR(UL)
     A  74
     AO 05                                  DSPATR(PR)
     A  16                                  DSPATR(RI PC)
     A                                      OVRDTA
     A  19                              3 70'DELETED'
     A                                      DSPATR(HI RI)
     A                                  4 11'P/C'
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDPCAL         1A  B  4 15DSPATR(HI)
     A                                      TEXT('Put/Call')
     A N17N05                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  17                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  4 18'Strike Price'
     A  09                                  DSPATR(ND)
     A                                      OVRATR
     A            DDSTRP        16A  B  4 31DSPATR(HI)
     A                                      TEXT('Strike Price')
     A N18N05                               DSPATR(UL)
     A  05                                  DSPATR(PR)
     A  18                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      **-------------------------------------------------------------------*
      ** MAIN DISPLAY SCREEN 3                                            *
      **-------------------------------------------------------------------*
     A          R FFMPRID3
     A                                      OVERLAY
     A                                      KEEP
     A                                      PUTOVR
     A            DDDELT         7A  O  8 72TEXT('DELETION FLAG')
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      OVRDTA
     A                                  8  2'Instrument Name'
     A            DDISTN        20A  O  8 18
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A N70                             10  2'Old Price'
     A  70                             10  2'Current Price'
     A            DDNEWP        16A  O 10 18TEXT('OLD PRICE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A N70                             12  2'New Price  '
     A            DDNWPI        16A  B 12 18TEXT('NEW PRICE INPUT')
     A                                      DSPATR(HI)
     A N70N21                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  20N70
     AO 21N70                               DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 14  2'Risk Factor'
     A  75                                  DSPATR(ND)
     A                                      OVRATR
     A            DDRSKF        11A  B 14 18TEXT('RISK FACTOR')
     A  75                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A N70N22                               DSPATR(UL)
     A  75
     AO 70                                  DSPATR(PR)
     A  22                                  DSPATR(RI PC)
     A                                      OVRDTA
      **------------------------------------------------------------------*
      ** MAIN SCREEN FOOTER                                               *
      **------------------------------------------------------------------*
     A          R FFMPRIF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A  71                                  CF10
     A  68                                  CF07
     A  68                                  CF08
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  68N71                          23  2'F3=Exit        F7=Previo+
     A                                      us        F8=Next        +
     A                                       F12=Cancel'
     A                                      OVRATR
     A  68 71                          23  2'F3=Exit   F7=Previous   +
     A                                         F8=Next      F10=Delet+
     A                                      e    F12=Cancel'
     A                                      OVRATR
     A  71N68                          23  2'F3=Exit        F10=Delet+
     A                                      e        F12=Cancel'
     A                                      OVRATR
     A                                 23  2'F3=Exit        F12=Cance+
     A                                      l'
     A  71
     AO 68                                  DSPATR(ND)
     A                                      OVRATR
