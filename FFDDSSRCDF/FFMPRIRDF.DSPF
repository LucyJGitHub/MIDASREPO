/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Market Prices Repair Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Financial Futures and Options module                 *
      *                                                               *
      *  FFMPRIRDF - Midas FF Market Prices Repair Browse             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAP183  *Create    Date 05Jan06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP183 - Conversion of Market Prices Input into modular      *
      *           structure to use APIs.                              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R FFIMPRIS1                 SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2006')
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
     A            DDBRSN    R        H      REFFLD(BRSN)
     A            DDOPT          1A  B  7  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1A  O  7  5DSPATR(HI)
     A            DDFOTRANID    20A  O  7  8DSPATR(HI)
     A            DDMRKT         2A  O  7 29
     A            DDISTC         3A  O  7 32
     A            DDMTHN         3A  O  7 36
     A            DDYRNO         2A  O  7 40
     A            DDPCAL         1A  O  7 44
     A            DDSTRP        16A  O  7 47
     A            DDNEWP        16A  O  7 64
      **-------------------------------------------------------------------*
      ** SELECT SCREEN CONTROL                                             *
      **-------------------------------------------------------------------*
     A          R FFIMPRIS0                 SFLCTL(FFIMPRIS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FFMPRIRPB'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 24'Invalid Market Prices Maintenance'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  5  8'Front Office Id.'
     A                                      DSPATR(UL)
     A                                  5 47'Strike Price'
     A                                      DSPATR(UL)
     A                                  5 64'New Price'
     A                                      DSPATR(UL)
     A                                  5  2'O'
     A                                      DSPATR(UL)
     A                                  5  4'Act'
     A                                      DSPATR(UL)
     A                                  5 36'Mth/Yr'
     A                                      DSPATR(UL)
     A                                  5 43'P/C'
     A                                      DSPATR(UL)
     A                                  5 29'InstCd'
     A                                      DSPATR(UL)
      **-------------------------------------------------------------------*
      ** SELECT SCREEN FOOTER                                              *
      **-------------------------------------------------------------------*
     A          R FFIMPRIF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      **-------------------------------------------------------------------*
      ** END MESSAGE                                                       *
      **-------------------------------------------------------------------*
     A          R FFIMPRIEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'FFMPRIRPB'
     A                                  1 24'Invalid Market Pric+
     A                                      es Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this Function'
