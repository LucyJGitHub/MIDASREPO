/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas FF OTC with PB details display file')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  FFOTCC2DDF - OTC Input display with Private Banking Details  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CFF007  *CREATE    Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R FFOTCC2DS0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            #0SFKY                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R FFOTCC2DC0                TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(FFOTCC2DS0)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            #0PGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R FFOTCC2DF0
     A                                      CF03
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
      ** Transaction Reference
      *
      ** Purchase or Sale
      *
      ** Number of Contracts
      *
      ** OTC Type
      *
      ** Put/Call
      *
      ** Strike Price
      *
      ** Style
      *
      ** Portfolio Reference
      ** (Delivery) Amount
      *
      ** (Delivery) Currency
      *
      ** Other Amount
      *
      ** (Other) Currency
      *
      ** Contract Price
      *
      ** Premium Currency
      *
      ** Premium Up Front
      *
      ** Quotation
      *
      ** Transaction (Trade) Date
      *
      ** Value Date
      *
      ** Expiry Date
      *
      ** Expiry Time
      *
      ** Customer/Counterparty
      *
      ** Book
      *
      ** Branch
      *
      ** Originating Branch
      *
      ** Settlement Date
      *
      ** Narrative
      *
      ** To Be Left Open
      *
      ** Linked Reference
      *
      ** Spot Price
      *
      ** Covered
      *
      ** Hedged/Traded
      *
      ** Block Underlying Position
      *
      * Underlying Security
      *
      * Economic Sector
      *
      * Nature Code
      *
      * Country of Risk
      *
      * Currency of Risk
      *
      * Weighting Code
 
     A            #0USER        10A  O  1  2OVRDTA
     A                                  1 13'FFOTCC2DDF'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            #0WSID        10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 37'Midas'
     A                                      DSPATR(HI)
     A            #0TIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
 
     A                                  3  2'Action Code'
     A            #0ACTN         1A  O  3 14TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  3 21'Processing Type'
     A            #0ISPT         1A  O  3 38
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5  2'Transaction Reference'
     A            #0TNBR         6A  O  5 24
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5 33'Purchase or Sale'
     A            #0TRTY         1A  O  5 50
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5 53'Number of Contracts'
     A            #0NUCO         5A  O  5 73
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6  2'OTC Type'
     A            #0ISTT         5A  O  6 11
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 17'Put/Call'
     A            #0PCAL         1A  O  6 26
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 28'Strike Price'
     A            #0STRP        16A  O  6 41
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 58'Style'
     A            #0AEIN         1A  O  6 64
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 66'Port Ref'
     A  82                                   DSPATR(ND)
     A                                      OVRATR
     A            #0PTFR         4A  O  6 75
     A N82                                  DSPATR(HI)
     A  82                                   DSPATR(ND)
     A                                      OVRDTA
 
     A                                  7  2'Amount'
     A            #0CTAM        14A  O  7  9
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  7 24'Currency'
     A            #0DCCY         3A  O  7 33
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  7 38'Other Amount'
     A            #0CCAM        14A  O  7 51
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  7 66'Currency'
     A            #0CTCY         3A  O  7 75
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  8  2'Price'
     A            #0COPR        16A  O  8  9
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  8 27'Premium Ccy.'
     A            #0ISCY         3A  O  8 40
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  8 45'Premium Up Front'
     A            #0PUPF         1A  O  8 62
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  8 65'Quotation'
     A            #0QOTC         2A  O  8 75
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9  2'Trade Date'
     A            #0TRSD         6A  O  9 13
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9 21'Value Date'
     A            #0VALD         6A  O  9 32
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9 40'Expiry Date'
     A            #0FTDT         6A  O  9 52
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9 60'Expiry Time'
     A            #0FTTM         6A  O  9 72
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10  2'Customer'
     A            #0CUSC        10A  O 10 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 23'Book'
     A            #0BOKC         2A  O 10 28
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 31'Branch'
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A            #0BRCD         3A  O 10 38
     A  80                                  DSPATR(ND)
     A N80                                  DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 42'Orig. Brn'
     A  81                                  DSPATR(ND)
     A                                      OVRATR
     A            #0ORBR         3A  O 10 52
     A  81                                  DSPATR(ND)
     A N81                                  DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 56'Settlement Date'
     A            #0SETD         6A  O 10 72
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 11  2'Narrative'
     A            #0TNAR        25A  O 11 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 11 38'Commodity Ref'
     A            #0CMRF        10A  O 11 52
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 11 63'To Be Left Open'
     A            #0TBLO         1A  O 11 79
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 12  2'Link Ref'
     A            #0LNKR         6A  O 12 11
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 12 20'Spot Price'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
     A            #0SPTP        16A  O 12 31
     A  83                                  DSPATR(ND)
     A N83                                  DSPATR(HI)
     A                                      OVRDTA
 
     A                                 12 57'Premium'
     A            #0PREM        15A  O 12 65DSPATR(HI)
     A                                      OVRDTA
     A                               O 13  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
      ** Covered
     A                                 15  2'Covered'
     A            #0COVE         1A  B 15 10
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N79                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 15 13'Hedged/Traded'
     A            #0HETR         1A  B 15 27
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N79                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 15 30'Block Underlying Position'
     A            #0BLUP         1A  B 15 56
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N79                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 17 02'Underlying Security'
     A  84
     AO 85                                  DSPATR(ND)
     A                                      OVRATR
     A            #0UNSE        10A  B 17 22
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  84
     AO 85                                  DSPATR(ND)
     A  84
     AO 85
     AO 79                                  DSPATR(PR)
     A N79N84N85                            DSPATR(UL)
     A                                      OVRDTA
 
     A                                 19 02'Economic Sector'
     A  85                                  DSPATR(ND)
     A                                      OVRATR
     A            #0TECO         3A  B 19 22
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  85
     AO 79                                  DSPATR(PR)
     A  85                                  DSPATR(ND)
     A N79N85                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 19 30'Nature Code'
     A  85                                  DSPATR(ND)
     A                                      OVRATR
     A            #0TNAT         3A  B 19 47
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  85
     AO 79                                  DSPATR(PR)
     A  85                                  DSPATR(ND)
     A N79N85                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 20 02'Country of Risk'
     A  85                                  DSPATR(ND)
     A                                      OVRATR
     A            #0CRIK         2A  B 20 22
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  85
     AO 79                                  DSPATR(PR)
     A  85                                  DSPATR(ND)
     A N79N85                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 20 30'Currency of Risk'
     A  85                                  DSPATR(ND)
     A                                      OVRATR
     A            #0CCYR         3A  B 20 47
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  85
     AO 79                                  DSPATR(PR)
     A  85                                  DSPATR(ND)
     A N79N85                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 21 02'Weighting Code'
     A  85                                  DSPATR(ND)
     A                                      OVRATR
     A            #0TWEI         2A  B 21 22
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  85
     AO 79                                  DSPATR(PR)
     A  85                                  DSPATR(ND)
     A N79N85                               DSPATR(UL)
     A                                      OVRDTA
      * -------------------------------------------------------------------*
      *   MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R FFOTCC2DF1                TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F3=Exit   F12=Previous'
