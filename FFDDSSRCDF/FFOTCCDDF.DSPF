      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Over-the-counter main details display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Futures and Options Module                           *
     A*                                                               *
     A*  FFOTCCDDF - OTC Input display                                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CFF007             Date 13Feb01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 06Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPL002             Date 13Mar99               *
     A*                 147775             Date 16Nov98               *
     A*                 CAP004  *CREATE    Date 24Jun98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
     A*  CPB001 - 'Private Banking' Module                            *
     A*  CPL002 - Midas-Plato Interface                               *
     A*  147775 - General FF API fixes - (Pre-release unnanotated)    *
     A*  CAP004 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R FFOTCCS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            FFSFKY                    SFLMSGKEY
     A            FFPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R FFOTCCS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  75                                  CF15
     A                                      SFLCTL(FFOTCCS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            FFPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R FFOTCCD1
     A                                      FRCDTA
     A                                      KEEP
     A                                      PUTOVR
     A                                      CA09
     A*
     A* Transaction Reference
     A*
     A* Purchase or Sale
     A*
     A* Number of Contracts
     A*
     A* OTC Type
     A*
     A* Put/Call
     A*
     A* Strike Price
     A*
     A* Style
     A*
     A* Portfolio Reference
     A* (Delivery) Amount
     A*
     A* (Delivery) Currency
     A*
     A* Other Amount
     A*
     A* (Other) Currency
     A*
     A* Contract Price
     A*
     A* Premium Currency
     A*
     A* Premium Up Front
     A*
     A* Quotation
     A*
     A* Transaction (Trade) Date
     A*
     A* Value Date
      ** Start of change CFF007
     A  87
      ** End of change CFF007
     A*
     A* Expiry Date
     A*
     A* Expiry Time
     A*
     A* Customer/Counterparty
     A*
     A* Book
     A*
     A* Branch
     A*
     A* Originating Branch
     A*
     A* Settlement Date
     A*
     A* Narrative
     A*
     A* To Be Left Open
     A*
     A* Linked Reference
 
     A*                                              Begin /CPL002/
     A* Spot Price
     A*                                              End   /CPL002/
 
      ** Profit centre (PCA off)
     A*                                              Begin /CPB001/
      ** Profit centre (PCA off)
     A*                                              End   /CPB001/
 
      ** Book position profit centre (PCA on)
 
      ** Transaction position profit centre (PCA on)
 
      ** Customer charges and commissions
 
      ** Customer initial margin
 
      ** Customer settlement instructions
 
     A                                  1  2'FFOTCCDDF'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
 
     A            FFTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
 
     A                                  1 72'WID:'
     A            FFWSID         3      1 77TEXT('WORKSTATION ID')
 
     A                                  2  2'Action Code'
     A            SACTN          1A  B  2 14TEXT('ACTION CODE')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  2 21'Processing Type'
     A            SISPT          1   B  2 38
     A                                      DFTVAL('5')
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      OVRDTA
 
     A                                  4  2'Transaction Reference'
     A            STNBR          6A  B  4 24
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  4 33'Purchase or Sale'
     A            STRTY          1A  B  4 50
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  4 53'Number of Contracts'
     A            SNUCO          5A  B  4 73
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  5  2'OTC Type'
     A            SISTT          5A  B  5 11
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  5 17'Put/Call'
     A            SPCAL          1A  B  5 26
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  5 28'Strike Price'
     A            SSTRP         16A  B  5 41
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  5 58'Style'
     A            SAEIN          1A  B  5 64
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  5 66'Port Ref'
     A*                                              Begin /CPB001/
     A**82********                           DSPATR(ND)
     A  82 86                                DSPATR(ND)
     A*                                              End   /CPB001/
     A                                      OVRATR
     A            SPTFR          4A  B  5 75
     A*                                              Begin /CPB001/
     A**82********                           DSPATR(ND)
     A  82 86                                DSPATR(ND)
     A*                                              End   /CPB001/
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A*                                              Begin /CPB001/
     A**82********
     A*O*70*******                          DSPATR(PR)
     A  82 86
     AO 70                                  DSPATR(PR)
     A*                                              End   /CPB001/
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  6  2'Amount'
     A            SCTAM         14A  B  6  9
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  6 24'Currency'
     A            SDCCY          3A  B  6 33
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  6 38'Other Amount'
     A            SCCAM         14A  B  6 51
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  6 66'Currency'
     A            SCTCY          3A  B  6 75
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  7  2'Price'
     A            SCOPR         16A  B  7  9
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  7 27'Premium Ccy.'
     A            SISCY          3A  B  7 40
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  7 45'Premium Up Front'
     A            SPUPF          1A  B  7 62
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  7 65'Quotation'
     A            SQOTC          2A  B  7 75
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  8  2'Trade Date'
     A            STRSD          6A  B  8 13
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  8 21'Value Date'
      ** Start of change CFF007
     A  87 88                               DSPATR(ND)
      ** End of change CFF007
     A            SVALD          6A  B  8 32
      ** Start of change CFF007
     A  87 88                               DSPATR(ND)
      ** End of change CFF007
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
      ** Start of change CFF007
     A**70******                            DSPATR(PR)                                        CFF007
     A  87 88
     AO 70                                  DSPATR(PR)
      ** End of change CFF007
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  8 40'Expiry Date'
     A            SFTDT          6A  B  8 52
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  8 60'Expiry Time'
     A            SFTTM          6A  B  8 72
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  9  2'Customer'
     A            SCUSC         10A  B  9 12
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  9 23'Book'
     A            SBOKC          2A  B  9 28
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  9 31'Branch'
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A            SBRCD          3A  B  9 38
     A  80                                  DSPATR(ND)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  80
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  9 42'Orig. Brn'
     A  81                                  DSPATR(ND)
     A                                      OVRATR
     A            SORBR          3A  B  9 52
     A  81                                  DSPATR(ND)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  81
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                  9 56'Settlement Date'
     A            SSETD          6A  B  9 72
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 10  2'Narrative'
     A            STNAR         25A  B 10 12
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 10 38'Commodity Ref'
     A            SCMRF         10A  B 10 52
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 10 63'To Be Left Open'
     A            STBLO          1A  B 10 79
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 11  2'Link Ref'
     A            SLNKR          6A  B 11 11
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A*                                              Begin /CPL002/
     A                                 11 20'Spot Price'
     A  85                                  DSPATR(ND)
     A            SSPTP         16   B 11 31
     A  85                                  DSPATR(ND)
     A  45                                  DSPATR(PC RI)
     A N45                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A*                                              End   /CPL002/
 
     A                                 11 57'Premium'
     A            SPREM         15   O 11 65DSPATR(HI)
     A                                      OVRDTA
     A                               O 12  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
 
     A                                 13  2'Profit Centre'
     A                                      OVRATR
     A  83                                  DSPATR(ND)
     A            SPRFC          4A  B 13 16
     A  83                                  DSPATR(ND)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  83
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 13 22'PM Instrument Type'
     A            SINNR          3A  B 13 41
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 13 67'PROFIT CENTRE'
     A  83                                  DSPATR(ND)
     A                                      OVRATR
 
     A                                 14  2'Commission Code: Std/Same'
     A            SCCMC          2A  B 14 52
     A*                                              Begin /CPL002/
     A**45*****                             DSPATR(RI PC)
     A*N45*****                             DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A            SCCMS          2A  B 14 56
     A*                                              Begin /CPL002/
     A**46******                            DSPATR(RI PC)
     A*N46******                            DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 14 69'Book'
     A                                      OVRATR
     A  84                                  DSPATR(ND)
     A            SBPRC          4   B 14 76
     A  84                                  DSPATR(ND)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  84
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 15  2'Commission Amount'
     A            SCCMA         14A  B 15 52
     A*                                              Begin /CPL002/
     A**47******                            DSPATR(RI PC)
     A*N47******                            DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 15 68'Trans.'
     A                                      OVRATR
     A  84                                  DSPATR(ND)
     A            STPRC          4   B 15 76
     A  84                                  DSPATR(ND)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  84
     AO 70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 16  2'Charge Code/Amount'
     A            SCCHC          2A  B 16 52
     A*                                              Begin /CPL002/
     A**48******                            DSPATR(RI PC)
     A*N48******                            DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N51                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A            SCCHA         14A  B 16 56
     A*                                              Begin /CPL002/
     A**49******                            DSPATR(RI PC)
     A*N49******                            DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N52                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 17  2'Initial Margin'
     A            SCMAR         14A  B 17 52
     A*                                              Begin /CPL002/
     A**50******                            DSPATR(RI PC)
     A*N50******                            DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A N53                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 18  2'Settlement Type'
     A            SCSLT          2A  B 18 52
     A*                                              Begin /CPL002/
     A**51******                            DSPATR(RI PC)
     A*N51******                            DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A N54                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 19  2'Settlement Account'
     A            SBRSC          3A  B 19 52
     A*                                              Begin /CPL002/
     A**52******                            DSPATR(RI PC)
     A*N52******                            DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A N55                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A**Start of change CGL029
     A**********  SCSLA         12A  B 19 56                                                  CGL029
     A            SCSLA         18A  B 19 56
     A**End of change CGL029
     A*                                              Begin /CPL002/
     A**53******                            DSPATR(RI PC)
     A*N53******                            DSPATR(HI)
     A  56                                  DSPATR(RI PC)
     A N56                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 20  2'Counterparty Nostro'
     A            SCCPN          8A  B 20 52
     A*                                              Begin /CPL002/
     A**54******                            DSPATR(RI PC)
     A*N54******                            DSPATR(HI)
     A  57                                  DSPATR(RI PC)
     A N57                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 21  2'For Account Of'
     A            SCFAO         10A  B 21 52
     A*                                              Begin /CPL002/
     A**55******                            DSPATR(RI PC)
     A*N55******                            DSPATR(HI)
     A  58                                  DSPATR(RI PC)
     A N58                                  DSPATR(HI)
     A*                                              End   /CPL002/
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
 
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
 
     A          R FFOTCCF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  75                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A            FFFOTX        40A    23  2OVRDTA
     A                                      DSPATR(HI)
     A            FFSTS         24     23 45OVRDTA
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
