      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF OTC - Display File - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  DSPF/FFOTCCLDF - FF Currency OTC                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      * --------------------------------------------------------------*
      *
     A          R FFOTCCF0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2005')
     A                                      OVERLAY
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O  1 72
     A                                  2 65'Time:'
     A                                  2 71TIME
      * ---------------------------------------------------------------
      * ---------------------------------------------------------------
     A            ##PGM         10A  H
     A                                  2 24'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
      *
     A                                  6  2'Transaction Time (HHMMSS) ........+
     A                                      .. :'
     A            L6LXTIME       6A  B  6 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N50N20                               DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  7  2'Excluded from CAB Reporting ......+
     A                                      .. :'
     A            L6LXCABR       1A  B  7 41OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N51N20                               DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                 23  2'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 17'F5=Refresh'
     A                                      COLOR(BLU)
     A  40                             23 29'F10=Delete'
     A                                      COLOR(BLU)
     A                                 23 41'F12=Previous'
     A                                      COLOR(BLU)
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
