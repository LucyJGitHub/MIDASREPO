/*STDA*  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FF Currency OTCs repair browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Futures and Options Module                           *
     A*                                                               *
     A*  FFOTCCRDF - Invalid currency OTC options browse              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     A*  Last Amend No. 166907             Date 27Dec99               *
      * Midas DBA 3.02 -----------------------------------------------*
     A*  Prev Amend No. CAP014             Date 29Nov99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP006             Date 19Mar99               *
     A*                 147775             Date 16Nov98               *
     A*                 CAP004  *CREATE    Date 03SEP98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  166907 - Show 'Deal No. 999999 successfully inserted'        *
     A*           message on screen.                                  *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP006 - Show error message on screen                        *
     A*  147775 - General FF API fixes                                *
     A*  CAP004 - APIs phase 3
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R FFIOTCCS1                 SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
      *           Option
     A                                      DSPATR(UL)
 
     A            DDACTN    R        O  8  6REFFLD(ACTO)
      *           Action code
     A                                      DSPATR(HI)
 
     A            DDFOTRANIDR     A  O  8  9REFFLD(FOTRNID)
      *           Front-office transaction ID
     A                                      DSPATR(HI)
 
 
     A            DDTNBR         6A  O  8 31
      *           Transaction reference
 
     A            DDISTT         5A  O  8 39
      *           OTC type
 
     A            DDPCAL         1A  O  8 45
      *           Put/call indicator
 
     A            DDTRTY         1   O  8 47
      *           Purchase or sale indicator
 
     A            DDNUCO         5   O  8 49
      *           Number of contracts
 
     A            DDSTRP        16   O  8 56
      *           Strike price
 
     A            DDAEIN         1   H
      *           Style
 
     A            DDPTFR         4   H
      *           Portfolio reference
 
     A            DDCTAM        14   H
      *           Delivery amount
 
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
      *           Associated front-office ID (not used)
 
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      *           Timestamp
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
 
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R FFIOTCCS0                 SFLCTL(FFIOTCCS1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A                                  1  2'FFOTCCR'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 24'FUTURES - INVALID TRADE MAINTENANC-
     A                                      E'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 31'Trade'
     A                                  6 39'OTC type'
      * Start CAP014
     A**********************************7  1'A,D'
      * End CAP014
     A                                  6 56'Strike price'
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R FFIOTCCF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of CAP006
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of CAP006
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R FFIOTCCEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start of 166907
     A                                      PUTOVR
      * End of 166907
     A                                  1  2'FFOTCCR'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'FUTURES - INVALID TRADE MAINTENAN+
     A                                      CE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      * Start of 166907
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
