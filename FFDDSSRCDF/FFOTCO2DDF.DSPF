/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas FF Non-ccy OTC with PB details display file')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  FFOTCO2DDF - Non-currency OTC Input Display  with Private    *
      *               Banking details                                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAP166  *CREATE    Date 15Oct01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP166 - API for Non-Currency OTC                            *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
 
      * -------------------------------------------------------------------*
      **  MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
 
     A          R FFOTCO2DS1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      * -------------------------------------------------------------------*
      **  MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
 
     A          R FFOTCO2DC1                TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(FFOTCO2DS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            #1PGMQ                    SFLPGMQ
 
      * -------------------------------------------------------------------*
      **  MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
 
     A          R FFOTCO2DF1
     A                                      CF03
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
 
      ** Transaction Reference
 
      ** Purchase or Sale
 
      ** Number of Contracts
 
      ** OTC Type
 
      ** Put/Call
 
      ** Strike Price
 
      ** Style
 
      ** Portfolio Reference
 
      ** (Delivery) Amount
 
      ** (Delivery) Currency
 
      ** Contract Price
 
      ** Premium Currency
 
      ** Premium Up Front
 
      ** Transaction (Trade) Date
 
      ** Value Date
 
      ** Expiry Date
 
      ** Expiry Time
 
      ** Customer/Counterparty
 
      ** Book
 
      ** Branch
 
      ** Originating Branch
 
      ** Settlement Date
 
      ** Narrative
 
      ** To Be Left Open
 
      ** Linked Reference
 
      ** Tick Value
 
      ** Minimum Price Fluctuation
 
      ** Covered
 
      ** Hedged/Traded
 
      ** Block Underlying Position
 
      ** Underlying Security
 
      ** Economic Sector
 
      ** Nature Code
 
      ** Country of Risk
 
      ** Currency of Risk
 
      ** Weighting Code
 
     A            #1USER        10A  O  1  2OVRDTA
     A                                  1 13'FFOTCO2DDF'
     A            BJMRDT         7A  O  1 72OVRDTA
     A            #1WSID        10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 37'Midas'
     A                                      DSPATR(HI)
     A            #1TIME         6Y 0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
 
     A                                  3  2'Action Code'
     A            #1ACTN         1A  O  3 14TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  3 21'Processing Type'
     A            #1ISPT         1A  O  3 38
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5  2'Transaction Reference'
     A            #1TNBR         6A  O  5 24
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5 33'Purchase or Sale'
     A            #1TRTY         1A  O  5 50
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  5 53'Number of Contracts'
     A            #1NUCO         5A  O  5 73
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6  2'OTC Type'
     A            #1ISTT         5A  O  6 11
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 17'Put/Call'
     A            #1PCAL         1A  O  6 26
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 28'Strike Price'
     A            #1STRP        16A  O  6 41
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 58'Style'
     A            #1AEIN         1A  O  6 64
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  6 66'Port Ref'
     A  82                                   DSPATR(ND)
     A                                      OVRATR
     A            #1PTFR         4A  O  6 75
     A N82                                  DSPATR(HI)
     A  82                                   DSPATR(ND)
     A                                      OVRDTA
 
     A                                  7  2'Amount'
     A            #1CTAM        14A  O  7  9
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  7 24'Currency'
     A            #1DCCY         3A  O  7 33
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  8  2'Price'
     A            #1COPR        16A  O  8  9
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  8 27'Premium Ccy.'
     A            #1ISCY         3A  O  8 40
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  8 45'Premium Up Front'
     A            #1PUPF         1A  O  8 62
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9  2'Trade Date'
     A            #1TRSD         6A  O  9 13
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9 21'Value Date'
     A            #1VALD         6A  O  9 32
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9 40'Expiry Date'
     A            #1FTDT         6A  O  9 52
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  9 60'Expiry Time'
     A            #1FTTM         6A  O  9 72
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10  2'Customer'
     A            #1CUSC        10A  O 10 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 23'Book'
     A            #1BOKC         2A  O 10 28
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 31'Branch'
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A            #1BRCD         3A  O 10 38
     A  80                                  DSPATR(ND)
     A N80                                  DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 42'Orig. Brn'
     A  81                                  DSPATR(ND)
     A                                      OVRATR
     A            #1ORBR         3A  O 10 52
     A  81                                  DSPATR(ND)
     A N81                                  DSPATR(HI)
     A                                      OVRDTA
 
     A                                 10 56'Settlement Date'
     A            #1SETD         6A  O 10 72
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 11  2'Narrative'
     A            #1TNAR        25A  O 11 12
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 11 38'Commodity Ref'
     A            #1CMRF        10A  O 11 52
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 11 63'To Be Left Open'
     A            #1TBLO         1A  O 11 79
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 12  2'Link Ref'
     A            #1LNKR         6A  O 12 11
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 12 18'Tick'
     A            #1TKVL        11   O 12 23
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 12 35'Min Price'
     A            #1MNPF        11   O 12 45
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                 12 57'Premium'
     A            #1PREM        15A  O 12 65DSPATR(HI)
     A                                      OVRDTA
 
     A                               O 13  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
 
      ** Covered
     A                                 15  2'Covered'
     A            #1COVE         1A  B 15 10
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N79                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 15 13'Hedged/Traded'
     A            #1HETR         1A  B 15 27
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N79                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 15 30'Block Underlying Position'
     A            #1BLUP         1A  B 15 56
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  79                                  DSPATR(PR)
     A N79                                  DSPATR(UL)
     A                                      OVRDTA
 
     A                                 17 02'Underlying Security'
     A  83
     AO 84                                  DSPATR(ND)
     A                                      OVRATR
     A            #1UNSE        10A  B 17 22
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  83
     AO 84                                  DSPATR(ND)
     A  83
     AO 84
     AO 79                                  DSPATR(PR)
     A N79N83N84                            DSPATR(UL)
     A                                      OVRDTA
 
     A                                 19 02'Economic Sector'
     A  84                                  DSPATR(ND)
     A                                      OVRATR
     A            #1TECO         3A  B 19 22
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  84
     AO 79                                  DSPATR(PR)
     A  84                                  DSPATR(ND)
     A N79N84                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 19 30'Nature Code'
     A  84                                  DSPATR(ND)
     A                                      OVRATR
     A            #1TNAT         3A  B 19 47
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  84
     AO 79                                  DSPATR(PR)
     A  84                                  DSPATR(ND)
     A N79N84                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 20 02'Country of Risk'
     A  84                                  DSPATR(ND)
     A                                      OVRATR
     A            #1CRIK         2A  B 20 22
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  84
     AO 79                                  DSPATR(PR)
     A  84                                  DSPATR(ND)
     A N79N84                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 20 30'Currency of Risk'
     A  84                                  DSPATR(ND)
     A                                      OVRATR
     A            #1CCYR         3A  B 20 47
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  84
     AO 79                                  DSPATR(PR)
     A  84                                  DSPATR(ND)
     A N79N84                               DSPATR(UL)
     A                                      OVRDTA
 
     A                                 21 02'Weighting Code'
     A  84                                  DSPATR(ND)
     A                                      OVRATR
     A            #1TWEI         2A  B 21 22
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  84
     AO 79                                  DSPATR(PR)
     A  84                                  DSPATR(ND)
     A N79N84                               DSPATR(UL)
     A                                      OVRDTA
 
      * -------------------------------------------------------------------*
      **  MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
 
     A          R FFOTCO2DF2                TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
 
     A                                 23  2'F3=Exit   F12=Previous'
