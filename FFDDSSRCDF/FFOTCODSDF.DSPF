      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas FF Non-Currency OTCs main details display')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  FFOTCODSDF - FF Non-Currency OTC main details display        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA03             Date 19Mar04               *
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP166  *CREATE    Date 15Oct01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA03 - Webfacing Changes.                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP166 - API for Non-Currency OTC                            *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
 
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
 
     A          R FFOTCODSS1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            #1SFKY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
 
     A          R FFOTCODSC1                TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF15
     A                                      SFLCTL(FFOTCODSS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            #1PGMQ                    SFLPGMQ
 
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
 
     A          R FFOTCODSF1
     A                                      FRCDTA
     A                                      KEEP
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      CF03
     A                                      CF12
     A  72                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF15
 
      ** Transaction Reference
 
      ** Purchase or Sale
 
      ** Number of Contracts
 
      ** OTC Type
 
      ** Put/Call
 
      ** Strike Price
 
      ** Style
 
      ** Portfolio Reference
      ** (Delivery) Amount
 
      ** (Delivery) Currency
 
      ** Contract Price
 
      ** Premium Currency
 
      * Premium Up Front
 
      ** Transaction (Trade) Date
 
      ** Value Date
     A N87
 
      ** Expiry Date
 
      ** Expiry Time
 
      ** Customer/Counterparty
 
      ** Book
 
      ** Branch
 
      ** Originating Branch
 
      ** Settlement Date
 
      ** Narrative
 
      ** To Be Left Open
 
      ** Linked Reference
 
      ** Tick Value
 
      ** Minimum Price Fluctuation
 
      ** Profit centre (PCA off)
 
      ** Profit centre (PCA off)
 
      ** Book position profit centre (PCA on)
 
      ** Transaction position profit centre (PCA on)
 
      ** Customer charges and commissions
 
      ** Customer initial margin
 
      ** Customer settlement instructions
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'FFOTCODSF1'
     A N09                              1 30'Non-Currency OTCs Maintenance'
     A  09                              1 30'Non-Currency OTCs Enquiry    '
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WSID        10   O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            #1TIME         6  0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
 
     A                                  3  2'Action Code'
     A            #1ACTN         1A  B  3 14TEXT('ACTION CODE')
     A N10                                  DSPATR(HI)
     A N10N03                               DSPATR(UL)
     A  10                                  DSPATR(RI)
     A  02
     AO 10N03                               DSPATR(PC)
     A  03                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  3 21'Processing Type'
     A            #1ISPT         1   B  3 38
     A N11                                  DSPATR(HI)
     A N11N03                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A  03                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  4  2'Transaction Reference'
     A            #1TNBR         6A  B  4 24
     A N12                                  DSPATR(HI)
     A N12N03                               DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A  03                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  4 33'Purchase or Sale'
     A            #1TRTY         1A  B  4 50
     A N13                                  DSPATR(HI)
     A N13N04                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  4 53'Number of Contracts'
     A            #1NUCO         5A  B  4 73
     A N14                                  DSPATR(HI)
     A N14N04                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  5  2'OTC Type'
     A            #1ISTT         5A  B  5 11
     A N15                                  DSPATR(HI)
     A N15N04                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  5 17'Put/Call'
     A            #1PCAL         1A  B  5 26
     A N16                                  DSPATR(HI)
     A N16N04                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  5 28'Strike Price'
     A            #1STRP        16A  B  5 41
     A N17                                  DSPATR(HI)
     A N17N04                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  5 58'Style'
     A            #1AEIN         1A  B  5 64
     A N18                                  DSPATR(HI)
     A N18N04                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  5 66'Port Ref'
     A  82 86                                DSPATR(ND)
     A                                      OVRATR
     A            #1PTFR         4A  B  5 75
     A  82 86                                DSPATR(ND)
     A N19                                  DSPATR(HI)
     A N19N04                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A  82 86
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  6  2'Amount'
     A            #1CTAM        14A  B  6  9
     A N20                                  DSPATR(HI)
     A N20N04                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  6 24'Currency'
     A            #1DCCY         3A  B  6 33
     A N21                                  DSPATR(HI)
     A N21N04                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  7  2'Price'
     A            #1COPR        16A  B  7  9
     A N22                                  DSPATR(HI)
     A N22N04                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  7 27'Premium Ccy.'
     A            #1ISCY         3A  B  7 40
     A N23                                  DSPATR(HI)
     A N23N04                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  7 45'Premium Up Front'
     A            #1PUPF         1A  B  7 62
     A N24                                  DSPATR(HI)
     A N24N04                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  8  2'Trade Date'
     A            #1TRSD         6A  B  8 13
     A N25                                  DSPATR(HI)
     A N25N04                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  8 21'Value Date'
     A  87 88                               DSPATR(ND)
     A            #1VALD         6A  B  8 32
     A  87 88                               DSPATR(ND)
     A N26                                  DSPATR(HI)
     A N26N04                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  87 88
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  8 40'Expiry Date'
     A            #1FTDT         6A  B  8 52
     A N27                                  DSPATR(HI)
     A N27N04                               DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  8 60'Expiry Time'
     A            #1FTTM         6A  B  8 72
     A N28                                  DSPATR(HI)
     A N28N04                               DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9  2'Customer'
     A            #1CUSC        10A  B  9 12
     A N29                                  DSPATR(HI)
     A N29N04                               DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9 23'Book'
     A            #1BOKC         2A  B  9 28
     A N30                                  DSPATR(HI)
     A N30N04                               DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9 31'Branch'
     A  80                                  DSPATR(ND)
     A                                      OVRATR
     A            #1BRCD         3A  B  9 38
     A  80                                  DSPATR(ND)
     A N31                                  DSPATR(HI)
     A N31N04                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A  80
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9 42'Orig. Brn'
     A  81                                  DSPATR(ND)
     A                                      OVRATR
     A            #1ORBR         3A  B  9 52
     A  81                                  DSPATR(ND)
     A N32                                  DSPATR(HI)
     A N32N04                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A  81
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9 56'Settlement Date'
     A            #1SETD         6A  B  9 72
     A N33                                  DSPATR(HI)
     A N33N04                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 10  2'Narrative'
     A            #1TNAR        25A  B 10 12
     A N34                                  DSPATR(HI)
     A N34N04                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 10 38'Commodity Ref'
     A            #1CMRF        10A  B 10 52
     A N35                                  DSPATR(HI)
     A N35N04                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 10 63'To Be Left Open'
     A            #1TBLO         1A  B 10 79
     A N36                                  DSPATR(HI)
     A N36N04                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 11  2'Link Ref'
     A            #1LNKR         6A  B 11 11
     A N37                                  DSPATR(HI)
     A N37N04                               DSPATR(UL)
     A  37                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 11 18'Tick'
     A            #1TKVL        11   B 11 23
     A N38                                  DSPATR(HI)
     A N38N04                               DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 11 35'Min Price'
     A            #1MNPF        11   B 11 45
     A N39                                  DSPATR(HI)
     A N39N04                               DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 11 57'Premium'
     A            #1PREM        15   O 11 65DSPATR(HI)
     A                                      OVRDTA
     A                               O 12  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
 
     A                                 13  2'Profit Centre'
     A                                      OVRATR
     A  83                                  DSPATR(ND)
     A            #1PRFC         4A  B 13 16
     A  83                                  DSPATR(ND)
     A N40                                  DSPATR(HI)
     A N40N04                               DSPATR(UL)
     A  40                                  DSPATR(RI PC)
     A  83
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A N82N86                          13 22'PM Instrument Type'
     A            #1INNR         3A  B 13 41
     A N41                                  DSPATR(HI)
     A N41N04                               DSPATR(UL)
     A  41                                  DSPATR(RI PC)
     A  82
     AO 86
     AO 04                                  DSPATR(PR)
     A  82
     AO 86                                  DSPATR(ND)
     A                                      OVRDTA
 
     A                                 13 52'COUNTERPARTY'
     A                                      DSPATR(HI)
     A                                      OVRATR
 
     A                                 13 67'PROFIT CENTRE'
     A N83                                  DSPATR(ND)
     A                                      OVRATR
 
     A                                 14  2'Commission Code: Std/Same'
     A            #1CCMC         2A  B 14 52
     A N53                                  DSPATR(HI)
     A N53N04                               DSPATR(UL)
     A  53                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A            #1CCMS         2A  B 14 56
     A N54                                  DSPATR(HI)
     A N54N04                               DSPATR(UL)
     A  54                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A N84                             14 69'Book'
     A                                      OVRATR
     A            #1BPRC         4   B 14 76
     A  84                                  DSPATR(ND)
     A N42                                  DSPATR(HI)
     A N42N04                               DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A  84
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 15  2'Commission Amount'
     A            #1CCMA        14A  B 15 52
     A N55                                  DSPATR(HI)
     A N55N04                               DSPATR(UL)
     A  55                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 15 68'Trans.'
     A                                      OVRATR
     A  84                                  DSPATR(ND)
     A            #1TPRC         4   B 15 76
     A  84                                  DSPATR(ND)
     A N43                                  DSPATR(HI)
     A N43N04                               DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A  84
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 16  2'Charge Code/Amount'
     A            #1CCHC         2A  B 16 52
     A N56                                  DSPATR(HI)
     A N56N04                               DSPATR(UL)
     A  56                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A            #1CCHA        14A  B 16 56
     A N57                                  DSPATR(HI)
     A N57N04                               DSPATR(UL)
     A  57                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 17  2'Initial Margin'
     A            #1CMAR        14A  B 17 52
     A N58                                  DSPATR(HI)
     A N58N04                               DSPATR(UL)
     A  58                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 18  2'Settlement Type'
     A            #1CSLT         2A  B 18 52
     A N59                                  DSPATR(HI)
     A N59N04                               DSPATR(UL)
     A  59                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 19  2'Settlement Account'
     A            #1BRSC         3A  B 19 52
     A N60                                  DSPATR(HI)
     A N60N04                               DSPATR(UL)
     A  60                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A**Start of change CGL029
     A**********  #1CSLA        12A  B 19 56                                                  CGL029
     A            #1CSLA        18A  B 19 56
     A**End of change CGL029
     A N61                                  DSPATR(HI)
     A N61N04                               DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 20  2'Counterparty Nostro'
     A            #1CCPN         8A  B 20 52
     A N62                                  DSPATR(HI)
     A N62N04                               DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 21  2'For Account Of'
     A            #1CFAO        10A  B 21 52
     A N63                                  DSPATR(HI)
     A N63N04                               DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
      * -------------------------------------------------------------------*
      *   Display Screen for Midas Plato non-Currency OTC I/E/A/D          *
      * -------------------------------------------------------------------*
 
     A          R FFOTCODSF2
     A                                      OVERLAY
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      FRCDTA
     A                                      CF03
     A  73                                  CF10
     A                                      CF12
 
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'FFOTCODSF2'
     A                                  1 25'Market Instruments Input -
     A                                      - Plato Details'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WSID        10   O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            #1TIME         6  0O  2 72EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('Current Time')
 
     A                                  3  2'Action Code'
     A            #1ACTN         1A  O  3 14TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
 
     A                                  3 17'Transaction Reference'
     A            #1TNBR         6A  O  3 39
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                               O  4  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL HI)
 
     A                                  5  2'Underlying Instrument Details:'
     A                                      DSPATR(HI UL)
 
     A                                  7  4'Instrument Type...............:'
     A            #2MIIT         1A  B  7 36TEXT('Instrument Type')
     A  85                                  DSPATR(ND)
     A N44                                  DSPATR(HI)
     A N44N04                               DSPATR(UL)
     A  44                                  DSPATR(RI PC)
     A  85
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  8  4'Term to Maturity..............:'
     A            #2MITM         4A  B  8 36TEXT('Term to Maturity')
     A  85                                  DSPATR(ND)
     A N45                                  DSPATR(HI)
     A N45N04                               DSPATR(UL)
     A  45                                  DSPATR(RI PC)
     A  85
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  8 50'Maturity Date...:'
     A            #2MITD         6A  B  8 69TEXT('Maturity Date')
     A  85                                  DSPATR(ND)
     A N46                                  DSPATR(HI)
     A N46N04                               DSPATR(UL)
     A  46                                  DSPATR(RI PC)
     A  85
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                  9  4'Coupon and Frequency..........:'
     A            #2MICP        12A  B  9 36TEXT('Coupon Rate')
     A  85                                  DSPATR(ND)
     A N47                                  DSPATR(HI)
     A N47N04                               DSPATR(UL)
     A  47                                  DSPATR(RI PC)
     A  85
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A            #2MICF         1A  B  9 50TEXT('Coupon Frequency')
     A  85                                  DSPATR(ND)
     A N48                                  DSPATR(HI)
     A N48N04                               DSPATR(UL)
     A  48                                  DSPATR(RI PC)
     A  85
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 10  4'Pricing Convention............:'
     A            #2MIPC         1A  B 10 36TEXT('Pricing Convention')
     A  85                                  DSPATR(ND)
     A N49                                  DSPATR(HI)
     A N49N04                               DSPATR(UL)
     A  49                                  DSPATR(RI PC)
     A  85
     AO 04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 12  2'Options Instrument Details:'
     A                                      DSPATR(HI UL)
 
     A                                 14  4'Spot Price of Underlying......:'
     A            #2MSPT        16A  B 14 36TEXT('Spot Price')
     A N50                                  DSPATR(HI)
     A N50N04                               DSPATR(UL)
     A  50                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 15  4'Volatility Type...............:'
     A            #2MIVT         1A  B 15 36TEXT('Volatility Type')
     A N51                                  DSPATR(HI)
     A N51N04                               DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 15 40'Pricing Method................:'
     A            #2MIPM         1A  B 15 72TEXT('Pricing Method')
     A N52                                  DSPATR(HI)
     A N52N04                               DSPATR(UL)
     A  52                                  DSPATR(RI PC)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 17  2'Underlying Instrument Details:'
     A                                      DSPATR(HI UL)
 
     A                                 19  4'Pricing Group Code............:'
     A            #2MIPG         8A  B 19 36TEXT('Pricing Group Code')
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A N04                                  DSPATR(UL)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 20  4'Report Group Code.............:'
     A            #2MIRG         8A  B 20 36TEXT('Report Group Code')
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A N04                                  DSPATR(UL)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
     A                                 21  4'Credit Rating Code............:'
     A            #2MICR         4A  B 21 36TEXT('Credit Rating Code')
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A N04                                  DSPATR(UL)
     A  04                                  DSPATR(PR)
     A                                      OVRDTA
 
 
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
 
     A          R FFOTCODSF3                TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  72                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
 
     A            #3FOTX        40A    23  2OVRDTA
     A                                      DSPATR(HI)
     A            #3STS         24     23 45OVRDTA
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A                                      OVRATR
 
     A                                 24  2'F3=Exit'
     A                                 24 11'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 20'F7=Prev'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 29'F10=Confirm Delete'
     A                                      DSPATR(HI)
     A N73                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 49'F12=Cancel'
     A                                 24 61'F15=Select'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
