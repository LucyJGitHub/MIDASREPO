/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FF Non-Currency OTCs repair browse')
      *****************************************************************
      *                                                               *
      *  Midas - Futures and Options Module                           *
      *                                                               *
      *  FFOTCORPDF - Invalid non-currency OTC options browse         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAP166  *CREATE    Date 15Oct01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP166 - API for Non-Currency OTC                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
 
      * -------------------------------------------------------------------*
      * * Select Screen Subfile                                            *
      * -------------------------------------------------------------------*
 
     A          R FFOTCORPS1                SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
 
      ** Select Option
     A            #1OPTN         1A  B  6  2TEXT('Select Option')
     A                                      DSPATR(UL)
 
      ** Action code
     A            #1ACTN    R        O  6  6REFFLD(ACTO)
     A                                      DSPATR(HI)
 
      ** Front-office transaction ID
     A            #1FRNT    R     A  O  6  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
 
 
      ** Transaction reference
     A            #1TNBR         6A  O  6 31
 
      ** OTC type
     A            #1ISTT         5A  O  6 39
 
      ** Put/call indicator
     A            #1PCAL         1A  O  6 45
 
      ** Purchase or sale indicator
     A            #1TRTY         1   O  6 47
 
      ** Number of contracts
     A            #1NUCO         5   O  6 49
 
      ** Strike price
     A            #1STRP        16   O  6 56
 
      ** Style
     A            #1AEIN         1   H
 
      ** Portfolio reference
     A            #1PTFR         4   H
 
      ** Delivery amount
     A            #1CTAM        14   H
 
      ** Associated front-office ID (not used)
     A            #1FOASOCIDR        H      REFFLD(FOTRNID)
 
      ** Timestamp
     A            #1TMSP    R   26A  H      REFFLD(TIMESTMP)
 
     A            #1BRSN    R        H      REFFLD(BRSN)
 
 
      * -------------------------------------------------------------------*
      * * Select Screen Control                                            *
      * -------------------------------------------------------------------*
 
     A          R FFOTCORPC1                SFLCTL(FFOTCORPS1)
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY PUTOVR
     A                                      BLINK
 
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'FFOTCORPC1'
     A                                  1 30'OPTIONS - INVALID NON-CURRENCY'
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WSID        10   O  2  2TEXT('Workstation ID')
     A                                  2 30'         MAINTENANCE          '
     A                                  2 66'Time:'
     A                                  2 72TIME
 
     A                                  4  1'Sel'
     A                                  4  5'Act'
     A                                  4  9'Front Office ID'
     A                                  4 31'Trade'
     A                                  4 39'OTC Type'
     A                                  4 56'Strike price'
 
      * -------------------------------------------------------------------*
      * * Select Screen Footer                                             *
      * -------------------------------------------------------------------*
 
     A          R FFOTCORPF1                TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
 
      * -------------------------------------------------------------------*
      * * End Message                                                      *
      * -------------------------------------------------------------------*
 
     A          R FFOTCORPF2                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      PUTOVR
 
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'FFOTCORPC1'
     A                                  1 30'OPTIONS - INVALID NON-CURRENCY'
     A                                  1 66'Date:'
     A            #1DATE         7A  O  1 73TEXT('Midas Rundate')
 
     A            #1WSID        10   O  2  2TEXT('Workstation ID')
     A                                  2 30'         MAINTENANCE          '
     A                                  2 66'Time:'
     A                                  2 72TIME
 
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this +
     A                                      function'
 
     A            #1FOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
