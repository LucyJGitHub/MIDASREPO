/*STDA*  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas FF Trades browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - FF Futures and Options Module                        *
     A*                                                               *
     A*  FFTRANBDF - Futures and Opitons Trades Browse                *
     A*              (Exchange-traded and O.T.Cs)                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. 147775             Date 16Nov98               *
     A*  Prev Amend No. CAP004  *CREATE    Date 01Jul98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  147775 - General FF API fixes - (Pre-release unnanotated)    *
     A*  CAP004 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
 
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
 
     A                                      CA03
     A                                      CA11
     A                                      CA12
 
     A          R FFTRANS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A*/COPY WNCPYSRC,FFTRANBD01
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDTNBR         6A     8  6
     A                                      DSPATR(HI)
     A            DDTRTY         1A     8 14
     A            DDISTT         5A     8 17
     A            DDINSD        22A     8 23
     A            DDCPTY         6A     8 46
     A            DDBOKC         2A     8 53
     A            DDCOPR        16A     8 56
     A            DDTEXT         7A     8 73TEXT('"DELETED"')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*/COPY WNCPYSRC,FFTRANBD02
 
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
 
     A          R FFTRANS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      SFLCTL(FFTRANS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'FFTRANB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 26'FUTURES AND OPTIONS INPUT ENQUIRY'
     A*/COPY WNCPYSRC,FFTRANBD03
     A                                  5  6'Trade'
     A                                  5 13'Buy/'
     A                                  5 60'Contract'
     A                                  6  2'Sel'
     A                                  6  7'Ref.'
     A                                  6 13'Sell'
     A                                  6 18'<------Instrument-------->'
     A                                  6 45'C/party'
     A                                  6 53'Bk'
     A                                  6 61'Price'
     A*/COPY WNCPYSRC,FFTRANBD04
 
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN SUBFILE - ALTERNATIVE VIEW                         *
     A* -------------------------------------------------------------------*
 
     A          R FFTRANS3                  SFL
 
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20   O  8  6DSPATR(HI)
     A            DDTNBR         6   O  8 27DSPATR(HI)
     A            DDTRTY         1A  O  8 34
     A            DDISTT         5A     8 36
     A            DDINSD        22A     8 42
     A            DDCPTY         6A     8 65
     A            DDTEXT         7A     8 73TEXT('"DELETED"')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
 
     A          R FFTRANS2                  SFLCTL(FFTRANS3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A                                  1  2'FFTRANB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 26'FUTURES AND OPTIONS INPUT ENQUIRY'
     A                                  5  6'Front Office'
     A                                  5 27'Trade'
     A                                  5 33'Buy/'
     A                                  6  6'Identifier'
     A                                  6 27'Ref.'
     A                                  6 33'Sell'
     A                                  6 38'<-----Instrument-------->'
     A                                  6 64'C/party'
     A          R FFTRANF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
