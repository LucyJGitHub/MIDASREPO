      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FF Invalid customer settlement instructions')
      *****************************************************************
      *                                                               *
      *  Midas - Financial Futures and Options module                 *
      *                                                               *
      *  PF/FFICSETPD - Invalid customer Settlement Instructions      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CFF007             Date 06Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003  *CREATE    Date 01Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *  CAP003 - Conversion of Midas inputs to APIs.                 *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
 
     A          R FFICSETD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
 
      ** Front Office Transaction Id
     A            SCFOTRANIDR               REFFLD(FOTRNID)
 
      ** Associated Front Office Transaction Id
     A            SCASOCFOIDR               REFFLD(FOTRNID)
                                            TEXT('Associated Front +
                                                  Office Transaction Id')
                                            COLHDG('Associated' +
                                                   'Front Office' +
                                                   'Transaction Id')
 
      ** Deal repair location
     A            SCRPRLOC  R               REFFLD(RPRLOC)
 
      ** Commision code
     A**Start of change CFF007
     A**********  SBCMC     R     A         REFFLD(BCMC)
     A            SCCMC     R     A         REFFLD(BCMC)
     A**End of change CFF007
                                            TEXT('Customer commission code')
                                            COLHDG('Cust' 'Comm' 'Code')
     A**Start of change CFF007
     A**********  SBCMS     R     A         REFFLD(BCMS)
     A            SCCMS     R     A         REFFLD(BCMS)
     A**End of change CFF007
                                            TEXT('Customer commission code +
                                                  - same day')
                                            COLHDG('Cust' 'Comm' 'Same')
 
      ** Commission amount
     A            SCCMA     R   +1A         REFFLD(BCMA)
                                            TEXT('Customer commission amount')
                                            COLHDG('Customer' +
                                                   'Commission' +
                                                   'Amount')
 
      ** Charge code
     A            SCCHC     R     A         REFFLD(BCHC)
                                            TEXT('Customer charge code')
                                            COLHDG('Cst' 'Chg' 'Cde')
 
      ** Charge amount
     A            SCCHA     R   +1A         REFFLD(BCHA)
                                            TEXT('Customer charge amount')
                                            COLHDG('Customer' +
                                                   'Charge' +
                                                   'Amount')
 
      ** Initial margin
     A            SCMAR     R   +1A         REFFLD(BMAR)
                                            TEXT('Customer initial margin')
                                            COLHDG('Customer' +
                                                   'Initial' +
                                                   'Margin')
 
      ** Settlement type
     A            SCSLT     R     A          REFFLD(BSLT)
                                             TEXT('Customer settlement type')
                                             COLHDG('Cust' 'Set' 'Typ')
 
      ** Settlement branch
     A            SBRSC     R               REFFLD(BRCA)
                                            TEXT('Customer settlement branch')
                                            COLHDG('Cust' 'Sett' 'Brch')
 
      ** Settlement account
     A**Start of change CGL029
     A**********  SCSLA     R               REFFLD(BSLA)                                      CGL029
      **********                            TEXT('Customer settlement account')               CGL029
      **********                            COLHDG('Customer' +                               CGL029
      **********                                   'Settlement' +                             CGL029
      **********                                   'Account')                                 CGL029
     A            SCSLA         18A         TEXT('Customer settlement account')
     A                                      COLHDG('Customer' +
     A                                             'Settlement' +
     A                                             'Account')
     A**End of change CGL029
 
      ** Counterparty nostro
     A            SCCPN     R               REFFLD(BCPN)
                                            TEXT('Customer counterparty nostro')
                                            COLHDG('Customer' +
                                                   'Counterparty' +
                                                   'Nostro')
 
      ** For account of
     A            SCFAO     R               REFFLD(BFAO)
                                            TEXT('Customer for account of')
                                            COLHDG('Customer' 'for' +
                                                   'account of')
 
      ** Timestamp
     A            SCTMESTMP R               REFFLD(TIMESTMP)
