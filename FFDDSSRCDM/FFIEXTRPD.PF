      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FF Invalid exchange-traded F & Os')
      *****************************************************************
      *                                                               *
      *  Midas - Financial Futures and Options module                 *
      *                                                               *
      *  PF/FFIEXTRPD - Invalid exchange-Traded Futures and Options   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CFF007             Date 06Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPL002             Date 08Mar99               *
      *                 CAP004             Date 29Oct98               *
      *                 CAP003  *CREATE    Date 01Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *  CPL002 - Midas-Plato Interface                               *
      *  CAP004 - API Phase 3:                                        *
      *           - Addition of 'Book Profit Centre' field.           *
      *           - Addition of 'Trans Profit Centre' field.          *
      *  CAP003 - Conversion of Midas inputs to APIs.                 *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
 
     A          R FFIEXTRD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
 
      ** Front Office Transaction Id
     A            SEFOTRANIDR               REFFLD(FOTRNID)
 
      ** Associated Front Office Transaction Id
     A            SEASOCFOIDR               REFFLD(FOTRNID)
                                            TEXT('Associated Front +
                                                  Office Transaction Id')
                                            COLHDG('Associated' +
                                                   'Front Office' +
                                                   'Transaction Id')
 
      ** Deal repair location
     A            SERPRLOC  R               REFFLD(RPRLOC)
 
      ** Action code
     A            SACTN     R               REFFLD(ACTO)
                                            TEXT('Action code')
                                            COLHDG('Action' 'Code')
 
      ** Transaction reference number
     A            STNBR     R     A         REFFLD(TNBR)
                                            TEXT('Transaction Reference')
                                            COLHDG('Transaction' +
                                                    'Reference')
 
      ** Transaction type: purchase or sale
     A            STRTY     R               REFFLD(TRTY)
                                            TEXT('Purchase or Sale')
                                            COLHDG('Purch' 'or' 'Sale')
 
      ** Number of contracts
     A            SNUCO     R     A         REFFLD(NUCO)
                                            TEXT('Number of Contracts')
                                            COLHDG('Number' 'of' 'Contracts')
 
      ** Market
     A            SMRKT     R               REFFLD(MRKT)
                                            TEXT('Market')
                                            COLHDG('Market')
 
      ** Instrument
     A            SISTC     R               REFFLD(ISTC)
                                            TEXT('Instrument')
                                            COLHDG('Inst')
 
      ** Month
     A            SMTHN     R   +1A         REFFLD(MTHN)
                                            TEXT('Month')
                                            COLHDG('Month')
 
      ** Year
     A            SYRNO     R     A         REFFLD(YRNO)
                                            TEXT('Year')
                                            COLHDG('Year')
 
      ** Put/call indicator
     A            SPCAL     R               REFFLD(PCAL)
                                            TEXT('Put/Call indicator')
                                            COLHDG('Put/' 'Call' 'Ind')
 
      ** Strike price
     A            SSTRP     R   +1A         REFFLD(STRPNW)
                                            TEXT('Strike price')
                                            COLHDG('Strike' 'Price')
 
      ** Portfolio reference
     A            SPTFR     R               REFFLD(PTFR)
                                            TEXT('Portfolio reference')
                                            COLHDG('Portfolio' 'Reference')
 
      ** Contract price
     A            SCOPR     R   +1A         REFFLD(COPRNW)
                                            TEXT('Contract price')
                                            COLHDG('Contract' 'Price')
 
      ** Trade date
     A            STRSD     R     A         REFFLD(CHGD)
                                            TEXT('Trade date')
                                            COLHDG('Trade' 'Date')
 
      ** Broker
     A            SBOCO     R               REFFLD(CSNM)
                                            TEXT('Broker')
                                            COLHDG('Broker')
 
      ** Customer number or shortcode
     A            SCUSC     R               REFFLD(CSNM)
                                            TEXT('Customer')
 
      ** Book code
     A            SBOKC     R     A  B      REFFLD(BOKC)
                                            TEXT('Book code')
                                            COLHDG('Book' 'Code')
 
      ** Booking branch
     A            SBRCD     R               REFFLD(BRCA)
                                            TEXT('Booking branch')
                                            COLHDG('Bkg' 'Brch')
 
      ** Narrative
     A            STNAR     R               REFFLD(TNAR)
                                            TEXT('Narrative')
                                            COLHDG('Narrative')
 
      ** To be left open
     A            STBLO     R               REFFLD(TBLO)
                                            TEXT('To Be Left Open')
                                            COLHDG('To Be' 'Left' 'Open')
 
      ** Linked reference number
     A            SLNKR     R     A         REFFLD(LNKDN)
                                            TEXT('Linked Reference')
                                            COLHDG('Linked' 'Reference')
 
      ** Originating branch
     A            SORBR     R               REFFLD(BRCA)
                                            TEXT('Originating Branch')
                                            COLHDG('Orig' 'Brch')
 
      ** Profit centre
     A            SPRFC     R               REFFLD(PRFC)
                                            TEXT('Profit Centre')
                                            COLHDG('Profit' 'Centre')
      *                                     Start of change /CAP004/
      ** Book position profit centre
     A            SBPRC     R               REFFLD(PRFC)
                                            TEXT('Book Profit Centre')
                                            COLHDG('Book' 'Profit' +
                                                   'Centre')
 
      ** Transaction position profit centre
     A            STPRC     R               REFFLD(PRFC)
                                            TEXT('Trans Profit Centre')
                                            COLHDG('Trans' 'Profit' +
                                                   'Centre')
      *                                       End of change /CAP004/
 
      ** Timestamp
     A            SETMESTMP R               REFFLD(TIMESTMP)
      *                                     Start of change /CPL002/
      ** Spot Price
     A            SSPTP         16A         TEXT('Spot Price')
     A                                      COLHDG('Spot' 'Price')
      *                                       End of change /CPL002/
     A**Start of change CFF007
     A            SCOVE          1A         TEXT('Covered')
     A                                      COLHDG('Covered')
     A            SHETR          1A         TEXT('Hedged/Traded')
     A                                      COLHDG('Hedged/Traded')
     A            SBLUP          1A         TEXT('Block Underlying Pos')
     A                                      COLHDG('Block Underlying Pos')
     A            SVALD          6A         TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A**End of change CFF007
