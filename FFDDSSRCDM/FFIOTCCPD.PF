      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FF Invalid OTC currency options')
      *****************************************************************
      *                                                               *
      *  Midas - Financial Futures and Options module                 *
      *                                                               *
      *  PF/FFIOTCCPD - Invalid over-the-counter currency options     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CFF007             Date 06Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPL002             Date 08Mar99               *
      *                 CAP004             Date 21Sep98               *
      *                 CAP003  *CREATE    Date 01Jun98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *  CPL002 - Midas-Plato Interface                               *
      *  CAP004 - Conversion of Midas inputs to APIs phase 3;         *
      *           corrected format name.                              *
      *  CAP003 - Conversion of Midas inputs to APIs.                 *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
 
      *                                                    /Start of CAP004/
     A**********R FFOTCCD0
      **********
     A          R FFIOTCCD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *                                                      /End of CAP004/
 
      ** Front Office Transaction Id
     A            SOFOTRANIDR               REFFLD(FOTRNID)
 
      ** Associated Front Office Transaction Id
     A            SOASOCFOIDR               REFFLD(FOTRNID)
                                            TEXT('Associated Front +
                                                  Office Transaction Id')
                                            COLHDG('Associated' +
                                                   'Front Office' +
                                                   'Transaction Id')
 
      ** Deal repair location
     A            SORPRLOC  R               REFFLD(RPRLOC)
 
      ** Action code
     A            SACTN     R               REFFLD(ACTO)
                                            TEXT('Action code')
                                            COLHDG('Action' 'Code')
 
      ** Processing type
     A            SISPT     R     A         REFFLD(ISPT)
                                            TEXT('Instrument processing +
                                                  type')
                                            COLHDG('Inst' 'Proc' 'Type')
 
      ** Transaction reference number
     A            STNBR     R     A         REFFLD(TNBR)
                                            TEXT('Transaction Reference')
                                            COLHDG('Transaction' +
                                                   'Reference')
 
      ** Transaction type: purchase or sale
     A            STRTY     R               REFFLD(TRTY)
                                            TEXT('Purchase or Sale')
                                            COLHDG('Purch' 'or' 'Sale')
 
      ** Number of contracts
     A            SNUCO     R     A         REFFLD(NUCO)
                                            TEXT('Number of Contracts')
                                            COLHDG('Number' 'of' 'Contracts')
 
      ** OTC instrument type
     A            SISTT     R               REFFLD(ISTT)
                                            TEXT('OTC Type')
                                            COLHDG('OTC' 'Type')
 
      ** Put/call indicator
     A            SPCAL     R               REFFLD(PCAL)
                                            TEXT('Put/Call indicator')
                                            COLHDG('Put/' 'Call' 'Ind')
 
      ** Strike price
     A            SSTRP     R   +1A         REFFLD(STRPNW)
                                            TEXT('Strike price')
                                            COLHDG('Strike' 'Price')
 
      ** American/European indicator
     A            SAEIN     R               REFFLD(IND)
                                            TEXT('American/European option +
                                                  indicator')
                                            COLHDG('Amer/' 'Euro' 'Ind')
 
      ** Portfolio reference
     A            SPTFR     R               REFFLD(PTFR)
                                            TEXT('Portfolio Reference')
                                            COLHDG('Portfolio' 'Reference')
 
      ** Contingent amount
     A            SCTAM     R   +1A         REFFLD(CTAM)
                                            TEXT('Contingent amount')
                                            COLHDG('Contingent' 'Amount')
 
      ** Delivery currency
     A            SDCCY     R               REFFLD(ISCY)
                                            TEXT('Delivery currency')
                                            COLHDG('Deliv' 'Curr')
 
      ** Other amount
     A            SCCAM     R   +1A         REFFLD(AMNT)
                                            TEXT('Other amount')
                                            COLHDG('Other' 'Amount')
 
      ** Counter currency
     A            SCTCY     R               REFFLD(ISCY)
                                            TEXT('Counter currency')
                                            COLHDG('Cntr' 'Curr')
 
      ** Contract price
     A            SCOPR     R   +1A         REFFLD(COPRNW)
                                            TEXT('Contract price')
                                            COLHDG('Contract' 'Price')
 
      ** Premium currency
     A            SISCY     R     A         REFFLD(ISCY)
                                            TEXT('Premium Currency')
                                            COLHDG('Prem' 'Curr')
 
      ** Premium up-front indicator
     A            SPUPF     R               REFFLD(PUPF)
                                            TEXT('Premium Up Front')
                                            COLHDG('Prm' 'Up' 'Frt')
 
      ** Quotation convention
     A            SQOTC     R   +1A         REFFLD(QOTC)
                                            TEXT('Quotation convention')
                                            COLHDG('Quotation' 'convention')
 
      ** Trade date
     A            STRSD     R     A         REFFLD(CHGD)
                                            TEXT('Trade date')
                                            COLHDG('Trade' 'Date')
 
      ** Value date
     A            SVALD     R     A         REFFLD(CHGD)
                                            TEXT('Value date')
                                            COLHDG('Value' 'Date')
 
      ** Expiry date
     A            SFTDT     R     A         REFFLD(CHGD)
                                            TEXT('Expiry Date'     )
                                            COLHDG('Expiry' 'Date'     )
 
      ** Expiry time
     A            SFTTM     R               REFFLD(STTM)
                                            TEXT('Expiry Time')
                                            COLHDG('Expiry' 'Time')
 
      ** Customer number or shortcode
     A            SCUSC     R               REFFLD(CSNM)
                                            TEXT('Customer')
                                            COLHDG('Customer')
 
      ** Book code
     A            SBOKC     R     A  B      REFFLD(BOKC)
                                            TEXT('Book code')
                                            COLHDG('Book' 'Code')
 
      ** Booking branch
     A            SBRCD     R               REFFLD(BRCA)
                                            TEXT('Booking branch')
                                            COLHDG('Bkg' 'Brch')
 
      ** Originating branch
     A            SORBR     R               REFFLD(BRCA)
                                            TEXT('Originating Branch')
                                            COLHDG('Orig' 'Brch')
 
      ** Settlement date
     A            SSETD     R     A         REFFLD(CHGD)
                                            TEXT('Settlement Date')
                                            COLHDG('Settlement' 'Date')
 
      ** Narrative
     A            STNAR     R               REFFLD(TNAR)
                                            TEXT('Narrative')
                                            COLHDG('Narrative')
 
      ** Commodity Reference
     A            SCMRF     R               REFFLD(CMRF)
 
      ** To be left open
     A            STBLO     R               REFFLD(TBLO)
                                            TEXT('To Be Left Open')
                                            COLHDG('To Be' 'Left' 'Open')
 
      ** Linked reference number
     A            SLNKR     R     A         REFFLD(LNKDN)
                                            TEXT('Linked Reference')
                                            COLHDG('Linked' 'Reference')
 
      ** Profit centre
     A            SPRFC     R               REFFLD(PRFC)
                                            TEXT('Profit Centre')
                                            COLHDG('Profit' 'Centre')
 
      ** PM instrument type
     A            SINNR     R               REFFLD(INNR)
                                            TEXT('PM Instrument Type')
                                            COLHDG('PM' 'Inst' 'Type')
 
      *                                                    /Start of CAP004/
      ** Book position profit centre
     A            SBPRC     R               REFFLD(PRFC)
                                            TEXT('Book Profit Centre')
                                            COLHDG('Book' 'Profit' +
                                                   'Centre')
 
      ** Transaction position profit centre
     A            STPRC     R               REFFLD(PRFC)
                                            TEXT('Trans Profit Centre')
                                            COLHDG('Trans' 'Profit' +
                                                   'Centre')
      *                                                    /End of CAP004/
 
      ** Timestamp
     A            SOTMESTMP R               REFFLD(TIMESTMP)
      *                                     Start of change /CPL002/
      ** Spot Price
     A            SSPTP         16A         TEXT('Spot Price')
     A                                      COLHDG('Spot' 'Price')
      *                                       End of change /CPL002/
     A**Start of change CFF007
     A            SCOVE          1A         TEXT('Covered')
     A                                      COLHDG('Covered')
     A            SHETR          1A         TEXT('Hedged/Traded')
     A                                      COLHDG('Hedged/Traded')
     A            SBLUP          1A         TEXT('Block Underlying Pos')
     A                                      COLHDG('Block Underlying Pos')
     A            SUNSE         10A         TEXT('Underlying Security')
     A                                      COLHDG('Underlying Security')
     A            STECO          3A         TEXT('Economic Sector')
     A                                      COLHDG('Economic Sector')
     A            STNAT          3A         TEXT('Nature Code')
     A                                      COLHDG('Nature Code')
     A            SCRIK          2A         TEXT('Country of Risk')
     A                                      COLHDG('Country of Risk')
     A            SCCYR          3A         TEXT('Currency of Risk')
     A                                      COLHDG('Currency of Risk')
     A            STWEI          2A         TEXT('Weighting Code')
     A                                      COLHDG('Weighting Code')
     A**End of change CFF007
