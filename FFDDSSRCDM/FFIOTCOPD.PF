      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas FF Invalid OTC non-currency')
      *****************************************************************
      *                                                               *
      *  Midas - Financial Futures and Options module                 *
      *                                                               *
      *  PF/FFIOTCOPD - Invalid over-the-counter non-currency         *
      *                 options                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CAP166  *CREATE    Date 15Oct01               *
      *  Prev Amend No.                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP166 - API for Non-Currency OTC                            *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
 
     A          R FFIOTCOD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
 
      ** Front Office Transaction Id
     A            SOFOTRANIDR               REFFLD(FOTRNID)
 
      ** Associated Front Office Transaction Id
     A            SOASOCFOIDR               REFFLD(FOTRNID)
                                            TEXT('Associated Front +
                                                  Office Transaction Id')
                                            COLHDG('Associated' +
                                                   'Front Office' +
                                                   'Transaction Id')
 
      ** Repair location
     A            SORPRLOC  R               REFFLD(RPRLOC)
 
      ** Action code
     A            NACTN     R               REFFLD(ACTO)
                                            TEXT('Action code')
                                            COLHDG('Action' 'Code')
 
      ** Processing type
     A            NISPT     R     A         REFFLD(ISPT)
                                            TEXT('Instrument processing +
                                                  type')
                                            COLHDG('Inst' 'Proc' 'Type')
 
      ** Transaction reference number
     A            NTNBR     R     A         REFFLD(TNBR)
                                            TEXT('Transaction Reference')
                                            COLHDG('Transaction' +
                                                   'Reference')
 
      ** Transaction type: purchase or sale
     A            NTRTY     R               REFFLD(TRTY)
                                            TEXT('Purchase or Sale')
                                            COLHDG('Purch' 'or' 'Sale')
 
      ** Number of contracts
     A            NNUCO     R     A         REFFLD(NUCO)
                                            TEXT('Number of Contracts')
                                            COLHDG('Number' 'of' 'Contracts')
 
      ** OTC instrument type
     A            NISTT     R               REFFLD(ISTT)
                                            TEXT('OTC Type')
                                            COLHDG('OTC' 'Type')
 
      ** Put/call indicator
     A            NPCAL     R               REFFLD(PCAL)
                                            TEXT('Put/Call indicator')
                                            COLHDG('Put/' 'Call' 'Ind')
 
      ** Strike price
     A            NSTRP     R   +1A         REFFLD(STRPNW)
                                            TEXT('Strike price')
                                            COLHDG('Strike' 'Price')
 
      ** American/European indicator
     A            NAEIN     R               REFFLD(IND)
                                            TEXT('American/European option +
                                                  indicator')
                                            COLHDG('Amer/' 'Euro' 'Ind')
 
      ** Portfolio reference
     A            NPTFR     R               REFFLD(PTFR)
                                            TEXT('Portfolio Reference')
                                            COLHDG('Portfolio' 'Reference')
 
      ** Contingent amount
     A            NCTAM     R   +1A         REFFLD(CTAM)
                                            TEXT('Contingent amount')
                                            COLHDG('Contingent' 'Amount')
 
      ** Delivery currency
     A            NDCCY     R               REFFLD(ISCY)
                                            TEXT('Delivery currency')
                                            COLHDG('Deliv' 'Curr')
 
      ** Contract price
     A            NCOPR     R   +1A         REFFLD(COPRNW)
                                            TEXT('Contract price')
                                            COLHDG('Contract' 'Price')
 
      ** Premium currency
     A            NISCY     R     A         REFFLD(ISCY)
                                            TEXT('Premium Currency')
                                            COLHDG('Prem' 'Curr')
 
      ** Premium up-front indicator
     A            NPUPF     R               REFFLD(PUPF)
                                            TEXT('Premium Up Front')
                                            COLHDG('Prm' 'Up' 'Frt')
 
      ** Trade date
     A            NTRSD     R     A         REFFLD(CHGD)
                                            TEXT('Trade date')
                                            COLHDG('Trade' 'Date')
 
      ** Value date
     A            NVALD     R     A         REFFLD(CHGD)
                                            TEXT('Value date')
                                            COLHDG('Value' 'Date')
 
      ** Expiry date
     A            NFTDT     R     A         REFFLD(CHGD)
                                            TEXT('Expiry Date'     )
                                            COLHDG('Expiry' 'Date'     )
 
      ** Expiry time
     A            NFTTM     R               REFFLD(STTM)
                                            TEXT('Expiry Time')
                                            COLHDG('Expiry' 'Time')
 
      ** Customer number or shortcode
     A            NCUSC     R               REFFLD(CSNM)
                                            TEXT('Customer')
                                            COLHDG('Customer')
 
      ** Book code
     A            NBOKC     R     A  B      REFFLD(BOKC)
                                            TEXT('Book code')
                                            COLHDG('Book' 'Code')
 
      ** Booking branch
     A            NBRCD     R               REFFLD(BRCA)
                                            TEXT('Booking branch')
                                            COLHDG('Bkg' 'Brch')
 
      ** Originating branch
     A            NORBR     R               REFFLD(BRCA)
                                            TEXT('Originating Branch')
                                            COLHDG('Orig' 'Brch')
 
      ** Settlement date
     A            NSETD     R     A         REFFLD(CHGD)
                                            TEXT('Settlement Date')
                                            COLHDG('Settlement' 'Date')
 
      ** Narrative
     A            NTNAR     R               REFFLD(TNAR)
                                            TEXT('Narrative')
                                            COLHDG('Narrative')
 
      ** Commodity Reference
     A            NCMRF     R               REFFLD(CMRF)
 
      ** To be left open
     A            NTBLO     R               REFFLD(TBLO)
                                            TEXT('To Be Left Open')
                                            COLHDG('To Be' 'Left' 'Open')
 
      ** Linked reference number
     A            NLNKR     R     A         REFFLD(LNKDN)
                                            TEXT('Linked Reference')
                                            COLHDG('Linked' 'Reference')
 
      ** Tick
     A            NTKVL     R   -3A         REFFLD(TKVL)
                                            TEXT('Tick')
                                            COLHDG('Tick')
 
      ** Mininmum Price
     A            NMNPF     R     A         REFFLD(MNPF)
                                            TEXT('Minimum Price')
                                            COLHDG('Minimum' 'Price')
 
      ** Profit centre
     A            NPRFC     R               REFFLD(PRFC)
                                            TEXT('Profit Centre')
                                            COLHDG('Profit' 'Centre')
 
      ** PM instrument type
     A            NINNR     R               REFFLD(INNR)
                                            TEXT('PM Instrument Type')
                                            COLHDG('PM' 'Inst' 'Type')
 
      ** Book position profit centre
     A            NBPRC     R               REFFLD(PRFC)
                                            TEXT('Book Profit Centre')
                                            COLHDG('Book' 'Profit' +
                                                   'Centre')
 
      ** Transaction position profit centre
     A            NTPRC     R               REFFLD(PRFC)
                                            TEXT('Trans Profit Centre')
                                            COLHDG('Trans' 'Profit' +
                                                   'Centre')
 
      ** Instrument type
     A            NMIIT          1A         TEXT('Instrument Type')
                                            COLHDG('Instrument' 'Type')
 
      ** Term to Maturity
     A            NMITM          4A         TEXT('Term to Maturity')
                                            COLHDG('Term' 'to' +
                                                   'Maturity')
 
      ** Maturity Date
     A            NMITD          6A         TEXT('Maturity Date')
                                            COLHDG('Maturity' 'Date')
      ** Coupon Rate
     A            NMICP         12A         TEXT('Coupon Rate')
                                            COLHDG('Coupon' 'Rate')
 
      ** Coupon Frequency
     A            NMICF          1A         TEXT('Coupon Frequency')
                                            COLHDG('Coupon' 'Frequency')
 
      ** Pricing Convention
     A            NMIPC          1A         TEXT('Pricing Convention')
                                            COLHDG('Pricing' +
                                                   'Convention')
 
      ** Spot Price
     A            NMSPT         16A         TEXT('Spot Price')
                                            COLHDG('Spot' 'Price')
 
      ** Volatility Type
     A            NMIVT          1A         TEXT('Volatility Type')
                                            COLHDG('Volatility' +
                                                   'Type')
 
      ** Pricing Method
     A            NMIPM          1A         TEXT('Pricing Method')
                                            COLHDG('Pricing' 'Method')
 
      ** Pricing Group Code
     A            NMIPG          8A         TEXT('Pricing Group Code')
                                            COLHDG('Pricing' 'Group' +
                                                   'Code')
 
      ** Report Group Code
     A            NMIRG          8A         TEXT('Report Group Code')
                                            COLHDG('Report' 'Group' +
                                                   'Code')
 
      ** Credit Rating Code
     A            NMICR          4A         TEXT('Credit Rating Code')
                                            COLHDG('Credit' 'Rating' +
                                                   'Code')
 
      ** Timestamp
     A            SOTMESTMP R               REFFLD(TIMESTMP)
 
      ** Covered
     A            NCOVE          1A         TEXT('Covered')
     A                                      COLHDG('Covered')
 
      ** Hedged/Traded
     A            NHETR          1A         TEXT('Hedged/Traded')
     A                                      COLHDG('Hedged/Traded')
 
      ** Block Underlying Position
     A            NBLUP          1A         TEXT('Block Underlying Pos')
     A                                      COLHDG('Block Underlying Pos')
 
      ** Underlying Security
     A            NUNSE         10A         TEXT('Underlying Security')
     A                                      COLHDG('Underlying Security')
 
      ** Economic Sector
     A            NTECO          3A         TEXT('Economic Sector')
     A                                      COLHDG('Economic Sector')
 
      ** Nature Code
     A            NTNAT          3A         TEXT('Nature Code')
     A                                      COLHDG('Nature Code')
 
      ** Country of Risk
     A            NCRIK          2A         TEXT('Country of Risk')
     A                                      COLHDG('Country of Risk')
 
      ** Currency of Risk
     A            NCCYR          3A         TEXT('Currency of Risk')
     A                                      COLHDG('Currency of Risk')
 
      ** Weighting Code
     A            NTWEI          2A         TEXT('Weighting Code')
     A                                      COLHDG('Weighting Code')
 
