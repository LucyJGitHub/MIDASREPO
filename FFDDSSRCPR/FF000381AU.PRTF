      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FF Transaction Update Phase I - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Futures & Options Module                             *
      *                                                               *
      *  PRTF/FF000381AU - Transactions Update Phase I - Audit        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CFF007 *CREATE     Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                       REF(MFRFL)
      *
      *-------------------------------*
      **  Report Header Record Format *
      *-------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE FF000381AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124 PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
 
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
 
     A                                    35'Transactions Update Phase I'
     A                                    63'- File Controls For'
     A            RFFTXT        34A       83TEXT('MKTN as at BUSD')
     A                                      SPACEA(1)
 
     A                                    35'----------------------------'
     A                                    63'--------------------'
     A            FULINE        34A       83
     A                                      SPACEA(3)
      *
      *----------------*
      **  File Control *
      *----------------*
      *
     A          R CONTROL
     A                                    60'Calculated'
     A                                      SPACEA(2)
 
     A                                     6'File'
     A                                    53'Number'
     A                                    74'Value'
     A                                      SPACEA(2)
 
     A                                     6'Account Keys'
     A                                    27'- PF/'
     A                                    32'Fokey'
     A                                    43'Output'
 
     A            COUNT          5  0     53EDTCDE(3)
     A            ZZTOTI        15  0     61EDTCDE(Z)
     A            ZZTOTD         3  0     76
     A                                      SPACEA(1)
      *
      *------------------*
      **  Database Error *
      *------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
 
     A                                    11'********************'
     A                                      SPACEB(2)
     A                                    31'********************'
     A                                    51'**************'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*   FILE NAME'
     A                                      SPACEB(1)
     A            DBFILE         8A       30TEXT('DATABASE FILE')
     A                                    64'*'
     A                                    11'*   KEY'
     A                                      SPACEB(1)
     A            DBKEY         29A       30TEXT('DATABASE FILE KEY')
     A                                    64'*'
     A                                    11'*   PROGRAM NAME'
     A                                      SPACEB(1)
     A            DBPGM         10A       30TEXT('PROGRAM')
     A                                    64'*'
     A                                    11'*   POSITION'
     A                                      SPACEB(1)
     A            DBASE          3  0     30TEXT('POSITION')
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'********************'
     A                                      SPACEB(1)
     A                                    31'********************'
     A                                    51'**************'
     A                                      SPACEA(3)
      *
      *------------------*
      **  Report Trailer *
      *------------------*
      *
     A          R TRAILER
     A                                    56'*** End of Report ***'
     A                                      SPACEB(3)
