      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FF Transactions Update Phase II audit')
      *****************************************************************
      *                                                               *
      *  Midas - Futures & Options Module                             *
      *                                                               *
      *  PRTF/FF000391AU - Transactions Update Phase II Audit         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CFF007  *CREATE    Date 13Feb01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *                                                               *
      *****************************************************************
     A                                       REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE FF000391AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      SPACEA(1)
      *
     A                                    38'Transactions Update Phase II'
     A                                    67'- File Controls for'
     A            MNAMED        34A       87
     A                                      SPACEA(1)
      *
     A                                    38'-----------------------------'
     A                                    67'--------------------'
     A            FULINE        34A       87
     A                                      SPACEA(2)
      *
     A                                    60'Calculated'
     A                                      SPACEA(2)
      *
     A                                     6'File'
     A                                    53'Number'
     A                                    74'Value'
     A                                      SPACEA(2)
      *
     A                                     6'Account Keys'
     A                                    27'- PF/'
     A                                    32'FOKEY'
     A                                    43'Output'
     A            COUNT          5  0     53EDTCDE(3)
     A            ZZTOTI        15  0     61EDTCDE(Z)
     A            ZZTOTD         3  0     76
      *
      *------------------------*
      ** Trailer Record Format *
      *------------------------*
      *
     A          R TRAILAU
     A                                    56'*** End of Report ***'
     A                                      SPACEB(3)
      *
      *-------------------------------*
      ** Database Error Record Format *
      *-------------------------------*
      *
     A          R DBERRAU
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
      *
     A                                    11'********************'
     A                                      SPACEB(2)
     A                                    31'********************'
     A                                    51'**************'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*   FILE NAME'
     A                                      SPACEB(1)
     A            DBFILE         8A       30TEXT('DATABASE FILE')
     A                                    64'*'
     A                                    11'*   KEY'
     A                                      SPACEB(1)
     A            DBKEY         29A       30TEXT('DATABASE FILE KEY')
     A                                    64'*'
     A                                    11'*   PROGRAM NAME'
     A                                      SPACEB(1)
     A            DBPGM         10A       30TEXT('PROGRAM')
     A                                    64'*'
     A                                    11'*   POSITION'
     A                                      SPACEB(1)
     A            DBASE          3  0     30TEXT('POSITION')
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'********************'
     A                                      SPACEB(1)
     A                                    31'********************'
     A                                    51'**************'
      *
