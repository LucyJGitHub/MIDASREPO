     A*****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF FX deals generated on exercise')
      *****************************************************************
     A*                                                               *
     A*  Midas¦ABS - FINANCIAL FUTURES AND OPTIONS MODULE             *
     A*                                                               *
     A*  PRTF/FF0392P1 - FX DEALS GENERATED ON EXERCISE               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFF003             Date 03Apr97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CFF003 - ENHANCED OTC PROCESSING - PHASE II                  *
     A*           UPGRADE FROM BLI LUX LBL001                         *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A**
     A          R HEADP1                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     6'REFERENCE FF0392P1'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A            PAGE         004S 0    125EDTCDE(Z) SPACEA(2)
      *
     A                                    49'FX DEALS GENERATED '
     A                                    68'ON EXERCISE REPORT'
     A                                      SPACEA(1)
     A                                    49'-------------------'
     A                                    68'------------------'
     A                                      SPACEA(1)
     A                                    88'BRANCH - '
     A            L3MBCA         3        97
     A            A8BRNM        30       101
     A                                   132' '
     A                                      SPACEA(3)
     A**
     A**   *******************
     A**   DETAIL PART OF REPORT
     A**   *******************
     A**
     A          R DETAIL                    SPACEB(1)
     A                                     2'Deal No:'
     A            F3DN38         6S 0     11
     A                                     6'Type:'
     A                                      SPACEB(2)
     A            F3TYPE         2        12
     A                                    17'Sub Type:'
     A            F3STYP         2        27
     A                                    33'Fed Funds:'
     A            F3FEDF         1        44
     A                                    48'Deal Date:'
     A            RDDAT          7        59
     A                                    68'B. Branch:'
     A            F3MBCA         3        79
     A                                     6'Broker:'
     A                                      SPACEB(1)
     A            F3BROK         4        14
     A                                    21'Brokerage:'
     A                                    49'Swap/Option Deal No:'
     A                                     6'Customer:'
     A                                      SPACEB(1)
      * Begin /CSD027/
     A**********  F3CNUM         6S 0     16                                                  CSD027
     A            F3CNUM         6A       16
      * End /CSD027/
     A            F3DCSN        10        23
     A                                    34'Value Date:'
     A            RVDAT          7        46
     A                                    54'Option To Date:'
     A                                     6'Book Code:'
     A                                      SPACEB(1)
     A            F3BOKC         2        17
     A                                    21'Linked Ref. No:'
     A                                     6'Buy Ccy:'
     A                                      SPACEB(1)
     A            F3CCY1         3        16
     A                                    20'Amount:'
     A            RBUYA         20        28
     A                                     5'Rate:'
     A                                      SPACEB(1)
     A            RXRATE        15        13
     A                                    30'Forward Points:'
     A                                     6'Sell Ccy:'
     A                                      SPACEB(1)
     A            F3CCY2         3        16
     A                                    20'Amount:'
     A            RSELA         20        28
     A                                    49'Profit centre:'
     A            F3PRFC         4        64
     A                                     6'B/C Equivalent:'
     A                                      SPACEB(1)
     A            RBASA         20        22
     A                                    50'Orig. Branch:'
     A            F3ORBR         3        64
     A                                    15'SETTLEMENT DETAILS'
     A                                      SPACEB(6)
     A                                     5'Settlement Method: Receipts'
     A                                      SPACEB(1)
     A            RRSTM          2        35TEXT('Receipt - Settlement Type')
     A                                    62'Payments'
     A            RPSTM          2        73TEXT('Pay - Settlement Type')
     A                                     5'Our Nostro'
     A                                      SPACEB(1)
     A**Start of change CGL029
     A**********  RRONS         15        22TEXT('Receipt - Our Nostro')                      CGL029
     A**********  RPONS         15        60TEXT('Pay - Our Nostro')                          CGL029
     A            RRONS         18        19TEXT('Receipt - Our Nostro')
     A            RPONS         18        57TEXT('Pay - Our Nostro')
     A**End of change CGL029
     A                                     5'Recipient of payment instructions'
     A                                      SPACEB(1)
     A            RRVNO         10        40TEXT('Scrn rcpt of payment istruct')
     A                                    53'Cover MT202 required'
     A            F3CVMR         1        74TEXT('Cover MT202 required')
     A                                     2'50 Ordering Customer'
     A                                      SPACEB(1)
     A            RROCN         10        27TEXT('REC Ordering Cust')
     A            RPOCN         10        65TEXT('PAY Ordering Cust')
     A                                     2'52 Ordering Bank'
     A                                      SPACEB(1)
     A            RROBN         10        27TEXT('REC ordering bank')
     A            RPOBN         10        65TEXT('PAY ordering bank')
     A                                     2'54 Recipients Correspondent'
     A                                      SPACEB(1)
     A            RRCRN         10        65TEXT('receiver Corr.')
     A            F3RCRA        35        40TEXT('Receiver Corr. A/c Line')
     A                                      SPACEB(1)
     A                                     2'56 Intermediary Bank'
     A                                      SPACEB(1)
     A            RRIBN         10        27TEXT('REC Intermediary')
     A            RPIBN         10        65TEXT('PAY Intermediary')
     A            F3RIBA        35         2TEXT('Rcpt - I/m Bank A/c Line')
     A                                      SPACEB(1)
     A            F3PIBA        35        40TEXT('Pay - I/m Bank A/c Line')
     A                                     2'57 ''Account With'' Bank'
     A                                      SPACEB(1)
     A            RAWBN         10        65TEXT('A/c with')
     A            F3AWBA        35        40TEXT('A/c With Bank Line')
     A                                      SPACEB(1)
     A                                     2'58/59 Beneficiary'
     A                                      SPACEB(1)
     A            RBENN         10        65TEXT('Beneficiary')
     A            F3BENA        35        40TEXT('Beneficiary A/c Line')
     A                                      SPACEB(1)
     A                                     2'70 Details of Payment'
     A                                      SPACEB(1)
     A            F3DTP1        35         2TEXT('Details of Payment 1')
     A                                      SPACEB(1)
     A            F3DTP2        35        40TEXT('Details of Payment 2')
     A            F3DTP3        35         2TEXT('Details of Payment 3')
     A                                      SPACEB(1)
     A            F3DTP4        35        40TEXT('Details of Payment 4')
     A                                     2'71 Details of Charges'
     A                                      SPACEB(1)
     A            F3DCHG         3        72TEXT('Details of charges')
     A                                     2'72 Bank to Bank Information'
     A                                      SPACEB(1)
     A            F3BTB1        35         2TEXT('Bank to Bank Info. 1')
     A            F3BTB2        35        40TEXT('Bank to Bank Info. 2')
     A            F3BTB3        35         2TEXT('Bank to Bank Info. 3')
     A                                      SPACEB(1)
     A            F3BTB4        35        40TEXT('Bank to Bank Info. 4')
     A            F3BTB5        35         2TEXT('Bank to Bank Info. 5')
     A                                      SPACEB(1)
     A            F3BTB6        35        40TEXT('Bank to Bank Info. 6')
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILP1
     A                                      SPACEB(2)
     A                                    50'*** END OF REPORT FOR'
     A                                    72'BRANCH ***'
