      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF EOC Margin Calculation')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Futures and Options Module
     A*                                                               *
     A*  FF0445P1 - Futures Margin Calculations Report                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CFF004             DATE 02OCT96               *
     A*  PREV AMEND NO. 065953             DATE 01MAR94               *
     A*                 S04662             DATE 13JUL93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CFF004  -  Increase in size of Price Fields,(Recompiled Only)*
     A*             - INTYPD Recompiled.                              *
     A*  065953  -  SPREAD MONTHS NOT BEING COUNTED IN CALCULATION    *
     A*  S04662  -  Switchable Feature for Init Margins for Futures;  *
     A*             Report Deposit & Spread Margin Rate for Futures.  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
      *
     A                                      INDARA
      *
      *-------------------------*
      ***  Report/Branch Header *
      *-------------------------*
      *
     A          R BRCHP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     7'REFERENCE FF0445P1'
     A            BJURPT    R             37REFFLD(BJURPT SDBANKPD)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(BJMRDT SDBANKPD)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'FUTURES MARGIN CALCULATION +
     A                                      REPORT'
     A                                      SPACEA(1)
      *
     A                                    47'---------------------------+
     A                                      ------'
     A                                      SPACEA(2)
      *
     A  11                                 4'BRANCH'
     A  11        A8BRCD    R             15REFFLD(A8BRCD SDBRCHPD)
     A  11        A8BRNM    R             22REFFLD(A8BRNM SDBRCHPD)
     A  11                                  SPACEA(2)
      *
      *------------------*
      ***  Broker Header *
      *------------------*
      *
     A          R BROKP1
     A                                     4'BROKER'
     A            BBCUST    R             15REFFLD(BBCUST SDCUSTPD)
     A            BBCSSN    R             22REFFLD(BBCSSN SDCUSTPD)
     A                                      SPACEA(2)
      *
      *--------------------*
      ***  Currency Header *
      *--------------------*
      *
     A          R CURRP1
     A                                     4'CURRENCY'
     A            A6CYCD    R             15REFFLD(A6CYCD SDCURRPD)
     A            A6CYNM    R             22REFFLD(A6CYNM SDCURRPD)
     A                                      SPACEA(1)
      *
      *
      ***  Detail Sub-Headings (only printed if new page)
      ***  --------------------
      *
     A  50                                10'DELIVERY'
     A                                      SPACEB(1)
     A  50                                24'LONG'
     A  50                                38'SHORT'
     A  50                                52'UNCOVERED'
     A  50                                66'DEPOSIT'
     A  50                                84'COVERED'
     A  50                                98'SPREAD'
     A  50                               114'TOTAL'
     A                                      SPACEA(1)
      *
     A  50                                10'DATE'
     A  50                                24'POSITION'
     A  50                                38'POSITION'
     A  50                                52'POSITION'
     A  50                                66'MARGIN RATE'
     A  50                                84'POSITION'
     A  50                                98'MARGIN RATE'
     A                                      SPACEA(1)
      *
      *----------------------*
      ***  Instrument Header *
      *----------------------*
      *
     A          R INSTP1
      *
     A                                     4'INSTRUMENT'
     A                                      SPACEB(1)
     A            ISTT      R             15REFFLD(ISTT INTYPD)
     A            ISTN      R             22REFFLD(ISTN INTYPD)
     A                                      SPACEA(2)
      *
      *----------------------*
      ***  Instrument Detail *
      *----------------------*
      *
     A          R DETLP1
      *
     A            RTRSD          5A       10TEXT('Delivery Date')
     A            RLOPE          7  0     24TEXT('Long Position')
     A                                      EDTCDE(3)
     A            RSOPE          7  0     38TEXT('Short Position')
     A                                      EDTCDE(3)
     A*          Start of change                      /*065953*/
     A*********** RUNCP          7  0     52TEXT('Uncovered Position')
     A***********                           EDTCDE(3)
     A*********** RCOVD          7  0     84TEXT('Covered Position')
     A***********                           EDTCDE(3)
     A*          End of change                         /*065953*/
     A                                      SPACEA(1)
      *
      *---------------------*
      ***  Instrument Total *
      *---------------------*
      *
     A          R INTLP1
      *
     A                                    52'-------'
     A                                    84'-------'
     A                                      SPACEA(1)
     A*
     A                                    33'MARGIN CALCULATED'
     A            RACCU          7  0     52TEXT('Uncovered Position - total')
     A                                      EDTCDE(3)
     A                                    62'@'
     A            RDMRG         18        66TEXT('Deposit Margin Rate')
     A            RACCC          7  0     84TEXT('Covered Position - total')
     A                                      EDTCDE(3)
     A                                    94'@'
     A            RSMRG         18        98TEXT('Spread Margin Rate')
     A            RTOTL         18       114TEXT('Total Margin')
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Currency Total *
      *-------------------*
      *
     A          R CYTLP1
      *
     A                                   114'------------------'
     A                                      SPACEA(1)
      *
     A                                    87'TOTAL MARGIN FOR CURRENCY'
     A            RCCYM         18       114SPACEA(1)
      *
     A                                   114'=================='
     A                                      SPACEA(1)
      *
      *------------------*
      ***  End of Report *
      *------------------*
      *
     A          R EOFRP1
      *
     A  11                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A N11                                51'***  END OF REPORT  ***'
