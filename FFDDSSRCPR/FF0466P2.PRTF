      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF Dlt Cust/Brok Posn Profit Ctr')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*   FF0466P2  -  DELETED CUSTOMER/BROKER POSITIONS              *
     A*                PROFIT CENTRES AUDIT REPORT                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. E27376             Date 15Aug91               *
     A*                                                               *
     A*---------------------------------------------------------------*
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  E27376 - NEW PRTF FOR PROFIT CENTRE                          *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FMTHDR2                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'REFERENCE FF0466P2'
     A            TITL      R           3 37TEXT('REPORT TITLE FROM SDBANKPD')
     A                                  3101'DATE'
     A            RUNA           7A     3106TEXT('TODAY IN DDMMMYY FORMAT')
     A                                  3119'PAGE'
     A            PAGE           5S 0   3124 EDTCDE( 4)
     A                                  5 28'DELETED CUSTOMER/BROKER'
     A                                  5 52'POSITIONS PROFIT CENTRES'
     A                                  5 77'REPORT'
     A  35                              5 90'BRANCH '
     A  35        BRCA      R           5 97
     A  35                              5100'-'
     A  35        BRNM          30A     5101
     A                                  6 28'------------------------'
     A                                  6 52'-------------------------'
     A                                  6 77'------'
     A                                  8 20'CUSTOMER/BROKER'
     A                                  8 50'INSTRUMENT TYPE'
     A                                  8 80'PROFIT CENTRE'
     A                                  9 20'---------------'
     A                                  9 50'---------------'
     A                                  9 80'-------------'
     A          R FMTDTL2                    TEXT('RECORD DETAILS')
     A                                       SPACEB(2)
      * Begin /CSD027/
     A**********  CBCD      R             24                                                  CSD027
     A            CBCD           6A       24TEXT('Customer Broker Code')
      * End /CSD027/
     A            ISTT      R             55
     A            PCCB      R             84
     A          R FMTEND2                   TEXT('END OF REPORT FORMAT')
     A                                      SPACEB(2)
     A N35                                56'** END OR REPORT **'
     A  35                                51'** END OF REPORT FOR BRANCH **'
