      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF Customer trading statements')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF0570P1  -  CUSTOMER TRADING STATEMENTS                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD053             Date 01Jun06               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 139994             Date 15Jun01               *
      *                 CFF007             Date 13Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFF004             Date 26Sep96               *
      *                 S01117             Date 24Apr90               *
     A*                 E14172             DATE 27/01/89              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
     A*  139994 - Price field is still 12A: price is truncated.       *
     A*  CFF007 - Futures and Options Enhancement for Private Banking *
     A*   CFF004  -  Increase in size of Price Fields                 *
     A*   S01117  -  MULTIBRANCHING                                   *
     A*   E14172  -  ADD 'TRADING STATEMENT' TO HEADER                *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FMTP101                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A**Start of Change CSD053
     A**********  CNA1      R           3  2                                                  CSD053
     A            CNA1          35O     3  2
     A**End of Change CSD053
     A*                                                                 /E14172/
     A                                  3 58'TRADING STATEMENT'
     A*                                                                 /E14172/
     A***********                                    Begin /S01117/
     A*********** BRNM      R           3 91
     A  35        BRNM      R           3 91
     A***********                                    End   /S01117/
     A**Start of Change CSD053
     A**********  CNA2      R           4  2                                                  CSD053
     A            CNA2          35O     4  2
     A**End of Change CSD053
     A                                  4120'PAGE'
     A            PAGE           4  0   4126EDTCDE(Z)
     A**Start of Change CSD053
     A**********  CNA3      R           5  2                                                  CSD053
     A**********  CNA4      R           6  2                                                  CSD053
     A            CNA3          35O     5  2
     A            CNA4          35O     6  2
     A**End of Change CSD053
     A  25                              7  2'FOR ATTENTION OF'
     A  25        FTAO      R           8  2
     A                                  9 53'CURRENCY -'
     A            CCNM      R           9 64
     A                                 10 53'--------------------------'
     A*
     A                                 12  5'INSTRUMENT'
     A*/COPY WNCPYSRC,FF0570P101
      **Start of change CFF007
     A  62                             12 16'VALUE'
      **End of change CFF007
     A                                 12 24'DATE'
     A                                 12 31'REFERENCE'
     A                                 12 45'CONTRACTS'
     A                                 12 55'P/S'
     A                                 12 63'PRICE'
     A                                 12 70'CLOSE'
     A                                 12 82'COMMISSION'
     A                                 12105'CHARGES'
     A                                 12121'PROFIT/LOSS'
     A*
     A                                 13  5'----------'
     A*/COPY WNCPYSRC,FF0570P102
      **Start of change CFF007
     A  62                             13 16'-----'
      **End of change CFF007
     A                                 13 24'----'
     A                                 13 31'---------'
     A                                 13 45'---------'
     A                                 13 55'---'
     A                                 13 63'-----'
     A                                 13 70'-----'
     A                                 13 82'----------'
     A                                 13105'-------'
     A                                 13121'-----------'
     A                                 14  1' '
     A*
     A          R FMTP102                   TEXT('DETAIL LINE FOR POSTNC1')
     A                                      SPACEB( 1)
     A N60        MKTN      R              1
     A  60                                 1'OVER THE COUNTER'
     A            RBFDT          7A       22
     A                                    30'B/FWD'
     A            RLSPO     R             47EDTCDE(L)
     A                                      REFFLD(LSPO)
     A            BFNAR          3A       55
     A*                                              Begin /CFF004/
     A*********** RBFPR         12A       59
     A            RBFPR         16A       59
     A*                                              End   /CFF004/
     A*
     A            ISTN      R              1SPACEB(1)
     A                                      SPACEA(1)
     A*
     A N60        AMTH           3A        1
     A N60        YRNO      R              4EDTCDE(X)
     A            PCAL      R              7
     A            RSTRP         12A        9
     A*
     A          R FMTP103                   TEXT('TRANSACTION NUMBER TOTALS')
     A*/COPY WNCPYSRC,FF0570P103
      **Start of change CFF007
     A  62        RVDAT          7A       14
      **End of change CFF007
     A            RPOCD          7A       22
     A            RTNBR         10A       30
     A            RNAR           3A       43
     A            RTCTS          7A       48
     A            RTRTY     R             56REFFLD(TRTY)
     A*                                              Begin /139994/
     A*********** RCOPR         12A       59
     A            RCOPR         16A       59
     A*                                              End   /139994/
     A            RFCIN          1A       72
     A            RTRCMS        19A       74
     A            RTRCHS        19A       94
     A            RTRPLS        19A      114
     A*
     A            TNAR      R             23SPACEB(1)
     A            RTCTE          7A       48
     A            RTRCME        19A       74
     A            RTRCHE        19A       94
     A            RTRPLE        19A      114
     A*
     A                                      SPACEA(1)
     A*
     A          R FMTP104                   TEXT('CHANGE DATE TOTALS')
     A*/COPY WNCPYSRC,FF0570P104
      **Start of change CFF007
     A  62        RVDAT          7A       14
      **End of change CFF007
     A            RPOCD          7A       22
     A            CDNAR         16A       30
     A            RTCDR         22A      111
     A*
     A                                      SPACEA(1)
     A*
     A          R FMTP105                   TEXT('INSRUMENT TOTALS')
     A            RCFSD          7A       22
     A  30                                30'UNREALISED P/L'
     A  30        RTIULP        19A      114SPACEA(1)
     A                                    30'C/FWD'
     A            RNETP     R             47REFFLD(LSPO)
     A                                      EDTCDE(L)
     A            CFNAR          3A       55
     A*                                              Begin /CFF004/
     A*********** RCFPR         12A       59
     A            RCFPR         16A       59
     A*                                              End   /CFF004/
     A                                      SPACEA(1)
     A*
     A          R FMTP106                   TEXT('SETTLEMENT ACCOUNT TOTAL')
     A                                      SKIPB(50)
     A*                                    1'SETTLEMENT ACCOUNT NO.'
     A*           RATID         18A       24
     A*                                     SPACEA(1)
     A*                                    1'EQUITY AT'
     A*           RBFSD          7A       11
     A*           RBFACB        19A       24
     A*                                     SPACEA(1)
     A                                     1'COMMISSION'
     A            RTSCOM        19A       24
     A                                      SPACEA(1)
     A                                     1'CHARGES'
     A            RTSCHG        19A       24
     A                                      SPACEA(1)
     A                                     1'REALISED PROFIT/LOSS'
     A            RTSRPL        19A       24
     A                                      SPACEA(1)
     A                                     1'UNREALISED PROFIT/LOSS'
     A            RTSUPL        19A       24
     A                                      SPACEA(1)
     A*                                    1'CASH ADJUSTMENTS'
     A*           RCADJ         19A       24
     A*                                     SPACEA(1)
     A*                                    1'EQUITY AT'
     A*           RCFSD          7A       11
     A*           RCFACB        19A       24
     A*                                     SPACEA(1)
     A                                     1'TOTAL MARGIN'
     A            RTMAR         19A       24
     A                                      SPACEA(1)
     A                                     1'COLLATERAL'
     A            RCLCO         19A       24
     A*
     A          R FMTP107                   TEXT('DETAIL LINE FOR POSTNC1')
     A                                      SPACEB( 1)
     A N60        MKTN      R              1
     A  60                                 1'OVER THE COUNTER'
     A            RPOCD          7A       22
     A                                    30'CONTINUED'
     A*
     A            ISTN      R              1SPACEB(1)
     A                                      SPACEA(1)
     A*
     A N60        AMTH           3A        1
     A N60        YRNO      R              4EDTCDE(X)
     A            PCAL      R              7
     A            RSTRP         12A        9
     A*
     A          R FMTP108                   TEXT('DATABASE ERROR FORMAT')
     A                                      SPACEB( 2)
     A                                    45'DATABASE ERROR'
     A***********                                    Begin /S01117/
     A            DBASE          3  0     60
     A                                    64'IN PROGRAM'
     A            DBPGM         10A       75
     A                                      SPACEA( 1)
     A                                    45'FILENAME'
     A            DBFILE         8A       54
     A                                    63'KEY'
     A            DBKEY         29A       67
     A*********** DBASE          2S       60
     A***********                         63'IN PROGRAM'
     A*********** DBPGM          8A       74
     A***********
     A***********
     A***********                           SPACEA( 1)
     A***********                         45'FILE'
     A*********** DBFILE         5A       50
     A***********                         55', KEY'
     A*********** DBKEY         29A       61
     A***********                                    End   /S01117/
