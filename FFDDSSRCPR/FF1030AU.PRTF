      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas FF Outstanding Treasury Bills Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF1030AU  -  OUTSTANDING TREASURY BILLS                      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E00000             DATE  DDMMMYY              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  E00000  -                                                    *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FMTAU00                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                  3  7'REFERENCE FF1030AU'
     A*
     A            TITL      R           3 37TEXT('REPORT TITLE FROM ICD1')
     A*
     A                                  3101'DATE'
     A*
     A            RUNA      R           3106TEXT('TODAY IN DDMMMYY FORMAT')
     A*
     A                                  3119'PAGE'
     A*
     A            PAGE1          5S 0   3124 EDTCDE( 4)
     A*
     A                                  5 40'OUTSTANDING TREASURY BILLS '
     A                                  5 67'BY BROKER    RUN CONTROLS'
     A*
     A                                  6 40'---------------------------'
     A                                  6 67'---------'
     A**
     A          R FMTAU01                   TEXT('MATURED TXN. AUDIT')
     A                                  9  6'LF/DLSDD1'
     A                                 10  6'---------'
     A                                 12  9'TOTAL RECORDS ON FILE'
     A                                 12 31'- INPUT'
     A            COUNT          5  0  12 55EDTCDE(3)
     A                                 14 31'- PRINTED'
     A            OCOUNT         5  0  14 55EDTCDE(3)
     A*
     A*
     A          R FMTAU02                   TEXT('EMPTY FILE MESSAGE')
     A                                      SPACEB(2) SPACEA(3)
     A                                    49'*** NO DETAILS '
     A                                    64'TO REPORT ***'
     A*
     A*
     A          R FMTAU03                   TEXT('DATABASE ERROR FORMAT')
     A*
     A                                 16 45'DATABASE ERROR'
     A            DBASE          3  0  16 60
     A                                 16 64'IN PROGRAM'
     A            DBPGM         10A    16 75
     A*
     A                                 17 45'FILE'
     A            DBFILE         8A    17 50
     A                                 17 59'KEY'
     A            DBKEY         29A    17 63
     A*
     A*
     A*
