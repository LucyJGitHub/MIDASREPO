      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF Instrument type list')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE                 *
     A*                                                               *
     A*  FF1140P1  -  INSTRUMENT TYPE LIST                            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001             Date 25Apr05               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CFF007             Date 16Feb01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CFF006             Date 28Nov00               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPL002             Date 08Mar99               *
      *                 138028             Date 10May99               *
     A*                 CFF004             DATE 02OCT96               *
     A*                 S01408             DATE 05SEP96               *
     A*                 CPM003             DATE 01MAR96               *
     A*                 S01408             DATE 04SEP95               *
     A*                 073714             DATE 01MAR95               *
     A*                 S01393             DATE 30OCT92               *
     A*                 FF0011             DATE 10AUG92               *
     A*                 FF0010             DATE 10AUG92               *
     A*                 S01117             DATE 22JAN90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CER001 - Midas Upgrade to MidasPlus                          *
      *  CFF007 - Futures and Options Enhancement for Private Banking *
      *  CFF006 - Fractional Ticks Enhancement.                       *
     A*  CPL002  -  Midas-Plato Interface                             *
     A*  138028  -  Field MNPF not referenced. Reference to MNPRNW    *
     A*             so that it will be displayed on the report.       *
     A*  CFF004  -  Increase in size of Price Fields                  *
     A*  S01408  -  Addition of hook FF1140P117                       *
     A*          -  Addition of hook FF1140P116                       *
     A*          -  Addition of hook FF1140P115                       *
     A*          -  Addition of hook FF1140P114                       *
     A*          -  Addition of hook FF1140P113                       *
     A*          -  Addition of hook FF1140P112                       *
     A*          -  Addition of hook FF1140P111                       *
     A*          -  Addition of hook FF1140P110                       *
     A*          -  Addition of hook FF1140P109                       *
     A*          -  Addition of hook FF1140P108                       *
     A*          -  Addition of hook FF1140P107                       *
     A*          -  Addition of hook FF1140P106                       *
     A*          -  Addition of hook FF1140P105                       *
     A*          -  Addition of hook FF1140P104                       *
     A*          -  Addition of hook FF1140P103                       *
     A*          -  Addition of hook FF1140P102                       *
     A*  CPM003  -  Add PM Instrument type                            *
     A*  S01408  -  Addition of hook FF1140P101                       *
     A*  073714  -  OTC ENHANCEMENTS                                  *
     A*  S01393  -  STRATEGIC RISK MANAGEMENT                         *
     A*             A new field called Underlying Period has been     *
     A*             added to PF/INTYPD and the old field Underlying   *
     A*             Period has been renamed to P&L Amortisation       *
     A*             Period                                            *
     A*                                                               *
     A* FUJI-FF0011 - The system does not differentiate between       *
     A*               american and european options  on exercise      *
     A*               validation and OTC confirmation wording.        *
     A*                                                               *
     A* FUJI-FF0010 - Midas only allows input of FX options where the *
     A*               contra contingent amount and the premium are    *
     A*               in the same currency ie. the instrument ccy.    *
     A*               In pratice the premium may be paid in a currency*
     A*               which is not the same as the contra contingent  *
     A*               amount. NB. Contingent and contra contingent    *
     A*               amounts are the amounts exchanged on exercising *
     A*               an option.                                      *
     A*                                                               *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FMTP101                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    Begin /S01117/
     A***********                       2  7'REFERENCE FF1140'
     A                                  2  7'REFERENCE FF1140P1'
     A***********                                    End   /S01117/
     A            TITL      R           2 37
     A                                  2101'DATE'
     A            RUNA      R           2106
     A                                  2119'PAGE'
     A            PAGE           5S 0   2124 EDTCDE( 4)
     A                                  4 50'INSTRUMENT TYPE'
     A                                  5 50'---------------'
     A                                  4 65' LIST'
     A                                  5 65'-----'
     A                                  6  2'MARKET'
     A N62        MRKT      R           6  9
     A  62                              6  9'OTC'
     A N62        MKTN      R           6 14
     A  62                              6 14'OVER THE COUNTER'
     A                                  7  2'-------'
     A                                  7  9'-----'
     A                                  7 14'--------------------'
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P102
      *                                              End   /S01408/
     A                                  8 65'BROKER'
     A                                  8 77'CUSTOMER'
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P103
      *                                              End   /S01408/
     A                                  8 87'MONTHS'
     A                                  8 99'TRADED'
     A                                  8119'LAST ACTIVITY'
     A                                  9  6'INSTRUMENT'
     A                                 10  6'----------'
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P104
      *                                              End   /S01408/
     A                                  9 64'DEFAULTS'
     A                                  9 77'DEFAULTS'
     A                                 10 64'--------'
     A                                 10 77'--------'
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P105
      *                                              End   /S01408/
     A                                  9 87'QUOTED'
     A                                 10 87'------'
     A                                  9 99'DATES'
     A                                 10 99'-----'
     A                                  9118'ACTION'
     A                                 10118'------'
     A                                  9129'DATE'
     A                                 10129'----'
     A*
     A          R FMTP102                   TEXT('INST REPORT DETAILS')
     A                                     2'TYPE'
     A                                      SPACEB( 2)
     A            ISTC      R              9
     A            ISTN      R             14
     A                                    38'NAME'
     A            CRNM      R             44
     A*/COPY WNCPYSRC,FF1140P122
     A                                     2'-------'
     A                                      SPACEB( 1)
     A                                     9'-----'
     A                                    14'--------------------'
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P106
      *                                              End   /S01408/
     A                                    38'COMMISSION PATTERN'
     A            BCPT      R             68
     A            CCPT      R             80
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P107
      *                                              End   /S01408/
     A            MTH1           3A       90TEXT('QUOTED MONTH 1')
     A                                    95'FINAL TRANS'
     A            FTDF      R            110
     A            TYPE           3A      118
     A            ENTT           3A      122
     A            FDATE     R            126REFFLD(RUNA)
     A                                      TEXT('LAST CHANGE DDMMMYY')
     A*
     A                                     2'PROC TYPE'
     A                                      SPACEB( 1)
     A            ISPT      R             12
     A            ISDSC         20A       14
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P108
      *                                              End   /S01408/
     A                                    38'COMMSNS:STANDARD/SAMEDAY'
     A            RBCMC          2A       68TEXT('COMMSNS:STANDARD')
     A            RFLGB          1A       71TEXT('SPACER')
     A            RBCMS          2A       73TEXT('COMMSNS:SAMEDAY')
     A*
     A            RCCMC          2A       80TEXT('COMMSNS:STANDARD')
     A            RFLGC          1A       83TEXT('SPACER')
     A            RCCMS          2A       85TEXT('COMMSNS:SAMEDAY')
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P109
      *                                              End   /S01408/
     A            MTH2           3A       90
     A                                    95'SETTLEMENT'
     A            SEDF      R            110
     A*
     A                                     2'PRICE REF'
     A                                      SPACEB( 1)
     A            PRRF      R             14
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P110
      *                                              End   /S01408/
     A                                    38'CHARGES:PURCHASES/SALES'
     A            RBCGP          2A       68TEXT('CHARGES:PURCHASES')
     A            RFLGY          1A       71TEXT('SPACER')
     A            RBCGS          2A       73TEXT('CHARGES:SALES')
     A            RCCGP          2A       80TEXT('CHARGES:PURCHASES')
     A            RFLGX          1A       83TEXT('SPACER')
     A            RCCGS          2A       85TEXT('CHARGES:SALES')
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P111
      *                                              End   /S01408/
     A            MTH3           3A       90
     A                                    95'SPOT MONTH'
     A            SPMF      R            110
     A*
     A                                     2'CCY/OTHER'
     A                                      SPACEB( 1)
     A            ISCY      R             14
     A            OTHC      R             19
     A            MTH4           3A       90
     A                                    95'TRADED PERIODS'
     A            TFPD      R            110EDTCDE( 2)
     A*
     A                                     2'FLUCTUATION'
     A                                      SPACEB( 1)
     A*                                              Begin /CFF004/
     A*********** MNPF      R             14EDTCDE( 2)
     A*/COPY WNCPYSRC,FF1140P119
     A*                                              Begin /138028/
     A*********** MNPFNW    R             14EDTCDE( 2)
     A            MNPF      R             14REFFLD(MNPFNW)
     A                                      EDTCDE( 2)
     A*                                              End   /138028/
     A*                                              End   /CFF004/
     A                                    38'TICK VALUE'
     A            RTKVL         16A       51
     A            MTH5           3A       90
     A*                                                    /S01393/
     A************                        95'UNDERLYING PER'
     A************ULPD      R            110EDTCDE( 2)
     A                                    95'P&L AMORT PERIOD'
     A            ULPD      R            112EDTCDE( 2)
     A*                                                    /S01393/
     A*
     A                                     2'QUOT/DENOM'
     A                                      SPACEB( 1)
     A            QOTC      R             14EDTCDE( K)
     A            TKDM      R             17EDTCDE( 2)
     A**Start of change CFF006
     A  70                                38'FRACTIONAL TICKS PROCESSED'
     A  70        RFRTK          1A       66
     A**End of change CFF006
     A            MTH6           3A       90
     A                                    95'FWARD SPOT MTH'
     A            FSPM      R            110EDTCDE( 2)
     A*
     A                                     2'CONTINGENT'
     A                                      SPACEB( 1)
     A            RCTAM         23A       14
     A            MTH7           3A       90
     A                                    95'PREM UP FRONT'
     A            PUPF           1A      110
     A*
     A                                     2'COMMODITY'
     A                                      SPACEB( 1)
     A            CMRF      R             14
     A            MTH8           3A       90
     A* /* BEGIN FF0011 */
     A* START OF CHANGE                                           /*073714*/
     A************                        95'OPTION TYPE'
     A                                    95'STYLE'
     A* END   OF CHANGE                                           /*073714*/
     A            NARR           8A      110
     A* /* END   FF0011 */
     A*
     A                                     2'STAT RETURN'
     A                                      SPACEB( 1)
     A            STRC      R             14
     A***********                                    Begin /CPM003/
     A  40                                38'PM INSTRUMENT TYPE'
     A  40        INNR      R             57
     A***********                                    End   /CPM003/
     A            MTH9           3A       90
     A* START OF CHANGE                                           /*073714*/
     A**/**BEGIN*FF0010 */
     A************                        95'CONTRA CCY'
     A************CCCY           3A      110
     A**/**END***FF0010 */
     A* END   OF CHANGE                                           /*073714*/
     A*
     A          R FMTP103                   TEXT('FUTURE INSTRUMENT')
     A*
     A                                     2'FUTURE TYPE REF'
     A                                      SPACEB( 1)
     A            RFTRF          3A       18
     A            ISTN      R             22
     A            MTH10          3A       90
     A            MTH11          3A       95
     A            MTH12          3A      100
     A*
     A          R FMTP104                   TEXT('OTC REPORT DETAILS')
     A*
     A                                     2'O T C'
     A                                      SPACEB( 2)
     A            ISTT      R              8
     A            ISTN      R             14
     A*/COPY WNCPYSRC,FF1140P123
     A                                     2'------'
     A                                      SPACEB(1)
     A                                     8'------'
     A                                    14'--------------------'
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P112
      *                                              End   /S01408/
     A                                    38'COMMISSION PATTERN'
     A            CCPT      R             80
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P113
      *                                              End   /S01408/
     A* START OF CHANGE                                           /*073714*/
     A************                        95'FINAL TRANS'
     A                                    95'EXPIRY DATE'
     A* END   OF CHANGE                                           /*073714*/
     A            RFTDT          7A      110TEXT('FINAL TRANS DATE')
     A            TYPE           3A      118
     A            ENTT           3A      122
     A            FDATE     R            126REFFLD(RUNA)
     A                                      TEXT('LAST CHANGE DDMMMYY')
     A*
     A                                     2'PROC TYPE'
     A                                      SPACEB( 1)
     A            ISPT      R             12
     A            ISDSC         20A       14
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P114
      *                                              End   /S01408/
     A                                    38'COMMSNS:STANDARD/SAMEDAY'
     A            RCCMC          2A       80TEXT('COMMSNS:STANDARD')
     A            RFLGC          1A       83TEXT('SPACER')
     A            RCCMS          2A       85TEXT('COMMSNS:SAMEDAY')
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P115
      *                                              End   /S01408/
     A* START OF CHANGE                                           /*073714*/
     A************                        95'SETTLEMENT'
     A************RSETD          7A      110TEXT('SETTLEMENT DATE')
     A                                    95'EXPIRY TIME'
     A            FTTM           6A      110TEXT('FINAL TRANS TIME')
     A* END   OF CHANGE                                           /*073714*/
     A*
     A                                     2'PRICE REF'
     A                                      SPACEB( 1)
     A            PRRF      R             14
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P116
      *                                              End   /S01408/
     A                                    38'CHARGES:PURCHASES/SALES'
     A            RCCGP          2A       80TEXT('CHARGES:PURCHASES')
     A            RFLGX          1A       83TEXT('SPACER')
     A            RCCGS          2A       85TEXT('CHARGES:SALES')
      *                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P117
      *                                              End   /S01408/
     A* START OF CHANGE                                           /*073714*/
     A************                        95'SPOT MONTH'
     A************RSPDT          7A      110TEXT('SPOT MONTH DATE')
     A************
     A************                         2'CCY/OTHER'
     A************                          SPACEB( 1)
     A************ISCY      R             14
     A************OTHC      R             19
     A                                    95'SETTLEMENT'
     A            RSETD          7A      110TEXT('SETTLEMENT DATE')
     A                                     2'PREMIUM CCY'
     A                                      SPACEB( 1)
     A            ISCY      R             15
     A                                    95'SPOT MONTH'
     A            RSPDT          7A      110TEXT('SPOT MONTH DATE')
     A* END   OF CHANGE                                           /*073714*/
     A*
     A                                     2'FLUCTUATION'
     A                                      SPACEB( 1)
     A*                                              Begin /CFF004/
     A*/COPY WNCPYSRC,FF1140P120
     A*********** MNPF      R             14EDTCDE( 2)
     A*/COPY WNCPYSRC,FF1140P121
     A*                                              Begin /138028/
     A*********** MNPFNW    R             14EDTCDE( 2)
     A            MNPF      R             14REFFLD(MNPFNW)
     A                                      EDTCDE( 2)
     A*                                              End   /138028/
     A*                                              End   /CFF004/
     A                                    38'TICK VALUE'
     A            RTKVL         16A       51
     A*                                                    /S01393/
     A************                        95'UNDERLYING PER'
     A************ULPD      R            110EDTCDE( 2)
     A                                    95'P&L AMORT PERIOD'
     A            ULPD      R            112EDTCDE( 2)
     A*                                                    /S01393/
     A*
     A                                     2'QUOT/DENOM'
     A                                      SPACEB( 1)
     A            QOTC      R             14EDTCDE( K)
     A            TKDM      R             17EDTCDE( 2)
     A**Start of change CFF006
     A  70                                38'FRACTIONAL TICKS PROCESSED'
     A  70        RFRTK          1A       66
     A**End of change CFF006
     A*
     A                                     2'CONTINGENT'
     A                                      SPACEB( 1)
     A            RCTAM         23A       14
     A* START OF CHANGE                                           /*073714*/
     A            OTHC           3A       38
     A* END   OF CHANGE                                           /*073714*/
     A                                    95'PREM UP FRONT'
     A            PUPF           1A      110
     A* START OF CHANGE                                           /*073714*/
     A                                     2'COUNTER   '
     A                                      SPACEB( 1)
     A            RCCAM         23A       14
     A            CTCY           3A       38
     A                                    95'STYLE'
     A            NARR           8A      110
     A* END   OF CHANGE                                           /*073714*/
     A*
     A                                     2'COMMODITY'
     A                                      SPACEB( 1)
     A            CMRF      R             14
     A* START OF CHANGE                                           /*073714*/
     A**/**BEGIN FF0011 */
     A************                        95'OPTION TYPE'
     A************NARR           8A      110
     A**/**END***FF0011 */
     A************
     A************                         2'STAT RETURN'
     A************                          SPACEB( 1)
     A************STRC      R             14
     A**/**BEGIN*FF0010 */
     A************                        95'CONTRA CCY'
     A************CCCY           3A      110
     A**/**END***FF0010 */
     A                                    38'STAT RETURN'
     A            STRC      R             51
     A* END   OF CHANGE                                           /*073714*/
     A***********                                    Begin /CPM003/
     A  40                                95'PM INSTRUMENT TYPE'
     A  40        INNR      R            114
     A***********                                    End   /CPM003/
     A*
     A*/COPY WNCPYSRC,FF1140P118
     A          R FMTP105                   TEXT('NO INSTR. REC')
     A                                      SPACEB( 2)
     A                                    52'**NO INSTRUMENT TYPES TO REPORT**'
     A*
     A          R FMTP106                   TEXT('END OF REPORT')
     A                                      SPACEB( 2)
     A                                    56'*** END OF REPORT ***'
     A*
     A          R FMTP107                   TEXT('DATABASE ERROR FORMAT')
     A                                      SPACEB( 2)
     A                                    45'DATABASE ERROR'
     A***********                                    Begin /S01117/
     A            DBASE          3  0     60
     A                                    64'IN PROGRAM'
     A            DBPGM         10A       75
     A*********** DBASE          2A       60
     A***********                         63'IN PROGRAM'
     A*********** DBPGM          8A       74
     A***********                           SPACEA( 1)
     A***********                         45'FILE'
     A*********** DBFILE         5A       50
     A***********                         55', KEY'
     A*********** DBKEY         29A       61
     A***********                                    End   /S01117/
     A                                      SPACEA( 2)
     A                                    56'*** END OF REPORT ***'
     A*                                              Start /S01408/
     A*/COPY WNCPYSRC,FF1140P101
     A*                                              End   /S01408/
     A*
     A*                                              Begin /CPL002/
     A          R FMTP108
     A                                     2'UNDERLYING:'
     A                                      SPACEB( 1)
     A                                    14'INSTRUMENT TYPE'
     A            PLMIIT         1A       30
     A N38                                34'TERM TO MATURITY'
     A N38        PLMITM         4A       51
     A  38                                34'MATURITY DATE'
     A  38        PRMIMD         7A       48
     A                                    60'COUPON RATE'
     A            R#MICP        12A       72
     A                                    86'COUPON FREQUENCY'
     A            PLMICF         1A      104
     A                                   114'PRICE CONVENTION'
     A            PLMIPC         1A      131
     A                                     2'OPTIONS VOLATILITY TYPE'
     A                                      SPACEB( 1)
     A            PLMIVT         1A       26
     A                                    30'OPTIONS PRICING METHOD'
     A            PLMIPM         1A       53
     A                                    60'PRICING GROUP'
     A            PLMIPG         8A       74
     A                                    86'REPORTING GROUP'
     A            PLMIRG         8A      102
     A                                   114'CREDIT RATING'
     A            PLMICR         4A      130
     A*                                              End   /CPL002/
     A*/COPY WNCPYSRC,FF1140P124
     A**Start of change CFF007
     A          R FMTP109                   TEXT('PRIVATE BANKING DETAILS')
     A*
     A                                     2'UNDERLYING SECURITY'
     A                                      SPACEB( 1)
     A            RUNSE         10A       22
     A                                    38'PB ECONOMIC SECTOR'
     A            RTECO          3A       57
     A                                    95'PB NATURE CODE'
     A            RTNAT          3A      110
     A                                     2'COUNTRY OF RISK'
     A                                      SPACEB( 1)
     A            RCRIK          2A       22
     A                                    38'CURRENCY OF RISK'
     A            RCCYR          3A       57
     A                                    95'WEIGHTING CODE'
     A            RTWEI          2A      110
     A**End of change CFF007
      **Start of change CER001
     A          R FMTP110                   TEXT('Blank line')
     A                                     2' '
     A                                      SPACEB(1)
      *
     A          R FMTPIBLC                  TEXT('Extended IBLC informations')
     A            RRLINE       132         1SPACEB(1)
     A          R ZZIBLC02                  TEXT('Extended IBLC informations')
     A            RRFROM       132         1SPACEB(1)
      **End of change CER001
