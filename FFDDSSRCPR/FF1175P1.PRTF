      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF Trade Number Gaps List')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF1175P1  -  TRADE NUMBER GAPS LIST                          *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01117             DATE  09APR90              *
     A*                 E14171             DATE 25/01/89              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*  E14171  -  'INTERIM' NOW PRINTED ON INTERIM REPORT.          *
     A*                                                               *
     A*****************************************************************
     A***********                                    Begin /S01117/
     A***********                           REF(MIDASFRLIV/MFRFL)
     A                                      REF(MFRFL)
     A***********                                    End   /S01117/
     A          R FMT9003                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  7'REFERENCE'
     A***********                                    Begin /S01117/
     A***********                       3 17'FF1175'
     A                                  3 17'FF1175P1'
     A***********                                    End   /S01117/
     A            TITL      R           3 43
     A                                  3101'DATE'
     A            RUNA      R           3106
     A                                  3119'PAGE'
     A            PAGE           5S 0   3124 EDTCDE( 4)
     A                                  5 54'TRADE NUMBER GAPS REPORT'
     A                                  6 54'------------------------'
     A  50                              6 90'*INTERIM REPORT*'
     A                                  8  9'RANGE'
     A                                  9  9'-----'
     A                                  8 24'NO'
     A                                  9 24'--'
     A                                  8 40'NAME'
     A                                  9 40'----'
     A                                  8 73'B/F'
     A                                  9 73'---'
     A                                  8 84'TRADE NUMBERS'
     A                                  9 84'-------------'
     A                                  8105'COMMENT'
     A                                  9105'-------'
     A                                  8115'NEXT AVAILABLE'
     A                                  9115'--------------'
     A*
     A          R FMT9001                   TEXT('DATA BASE ERROR')
     A                                      SPACEB(2)
     A                                    45'DATABASE ERROR '
     A***********                                    Begin /S01117/
     A            DBASE          3  0     60
     A                                    64'IN PROGRAM'
     A            DBPGM         10A       75
     A*********** DBASE          2A       60
     A***********                         63'IN PROGRAM '
     A*********** DBPGM          8A       74
     A***********
     A***********
     A***********                           SPACEA(1)
     A***********                         45'FILE '
     A*********** DBFILE         5A       50
     A***********                         55', KEY '
     A*********** DBKEY         29A       61
     A***********                                    End   /S01117/
     A*
     A          R FMT4014A                  TEXT('RANGE LINE A')
     A                                      SPACEB(1)
     A            FNOR      R              5
     A            LNOR      R             12
     A            TRNR      R             24
     A            TRDS      R             32
     A            TNBF      R             72
     A*
     A          R FMT4014B                  TEXT('RANGE LINE B')
     A  62                                  SPACEB(1)
     A            RSRLL          6  0     84TEXT('SUB-RANGE LOWER LIM')
     A            RSRUL          6  0     91TEXT('SUB RANGE UPPER LIM')
     A*
     A          R FMT4014C                  TEXT('RANGE LINE C')
     A  62 60                               SPACEB(1)
     A            RCOM          12A      103TEXT('REPORT COMMENT')
     A*
     A          R FMT4015                   TEXT('SUB-RANGE LINE')
     A                                      SPACEB(1)
     A  61                                 5'****** ******'
     A            RSRLL          6  0     84TEXT('SUB-RANGE LOWER-LIMIT')
     A            RSRUL          6  0     91TEXT('SUB-RANGE UPPER LIMIT')
     A            RCOM          12A      103TEXT('COMMENT')
     A*
     A          R FMT4016                   TEXT('NEXT AVALIABLE NO')
     A                                      SPACEB(1)
     A            RTNC          10A      103TEXT('TRADE NO. COMMENT')
     A            RNAN           6A      119TEXT('NEXT AVALIABLE NO')
     A*
     A          R FMTP101                   TEXT('END OF REPORT')
     A                                      SPACEB( 2)
     A                                    56'*** END OF REPORT ***'
     A*
     A          R FMTP102                   TEXT('NO RECS IN TRNOR1')
     A                                      SPACEB(2)
     A                                    49'*** NO RANGE DETAILS '
     A                                    70'TO REPORT ***'
     A*
     A          R FMTP103                   TEXT('NO RECS IN TRANS2')
     A                                      SPACEB(2)
     A                                    49'*** NO TRANSACTIONS '
     A                                    69'TO REPORT ***'
     *
