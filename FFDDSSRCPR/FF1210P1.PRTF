      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF Outstanding Holdngs by Instrument')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF1210P1  -  OUTSTANDING HOLDINGS BY INSTRUMENT              *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     A*  Prev Amend No. 149327             DATE  06Apr00              *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CFF004             DATE  23SEP96              *
     A*                 S01456             DATE  23DEC93              *
     A*                 E44757 (FUJI-FF0022) DATE  03SEP92            *
     A*                 S01117             DATE  03APR90              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  149327  -  Add Contingent and Other Amount to report.        *
     A*  CFF004  -  Increase in size of Price Fields                  *
     A*  S01456  -  Monthly P&L Enhancements.
     A*  E44757     - The instrument holdings report should also      *
     A*               show current unrealised P&L as well as month    *
     A*               to date unrealised P&L.                         *
     A*                                                               *
     A*                                                               *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R FMTP100                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A***********                                    Begin /S01117/
     A***********                       3  7'REFERENCE FF1210'
     A                                  3  7'REFERENCE FF1210P1'
     A***********                                    End   /S01117/
     A*
     A            TITL      R           3 37TEXT('REPORT TITLE FROM ICD1')
     A*
     A                                  3101'DATE'
     A*
     A            RUNA      R           3107
     A*
     A                                  3119'PAGE'
     A*
     A            PAGE           5S 0   3124 EDTCDE( 4)
     A*
     A***********                                    Begin /S01117/
     A***********                       5 30'OUTSTANDING HOLDINGS BY INSTRUMENT'
     A***********                       6 30'----------------------------------'
     A***********
     A***********                       5 65'for'
     A***********                       6 64'-----'
     A***********
     A*********** NAMDAT        34A     5 69TEXT('MARKET NAME AND BUS. DATE')
     A*********** FULINE        34A     6 69TEXT('UNDERLINE')
     A*
     A N50                              5 30'OUTSTANDING HOLDINGS BY INSTRUMENT'
     A N50                              6 30'----------------------------------'
     A*
     A N50                              5 65'for'
     A N50                              6 64'-----'
     A*
     A N50        NAMDAT        34A     5 69TEXT('MARKET NAME AND BUS. DATE')
     A N50        FULINE        34A     6 69TEXT('UNDERLINE')
     A*
     A  50                              5 10'OUTSTANDING HOLDINGS BY INSTRUMENT'
     A  50                              6 10'----------------------------------'
     A*
     A  50                              5 45'for'
     A  50                              6 44'-----'
     A*
     A  50        NMDAT         34A     5 49TEXT('MARKET NAME AND BUS. DATE')
     A  50        FLINE         34A     6 49TEXT('UNDERLINE')
     A  50                              5 90'BRANCH'
     A  50        BR        R           5 97
     A                                      REFFLD(BRCA)
     A  50                              5100'-'
     A  50        SEBN          30A     5101
     A*
     A***********                                    End   /S01117/
     A**
     A*
     A          R FMTP101                   TEXT('INSTRUMENT DETAILS')
     A*
     A                                  7  7'INSTRUMENT - '
     A                                  8  7'----------'
     A*
     A            ITYPE          6A     7 20TEXT('INSTRUMENT TYPE')
     A*
     A  35                              7 26'-'
     A*
     A  35        MTHNAM         3A     7 27TEXT('MONTH')
     A*
     A  35                              7 30'-'
     A*
     A  35        YRNO      R           7 31
     A*
     A            ISTN      R           7 35
     A*
     A                                  7 65'MARKET PRICE - '
     A*
     A*                                              Begin /CFF004/
     A*********** MKTPRC        12A     7 80TEXT('MARKET PRICE')
     A            MKTPRC        16A     7 80TEXT('MARKET PRICE')
     A*                                              End   /CFF004/
     A*
     A***********                                    Begin /S01117/
     A***********                       7106'BRANCH - '
     A*********** BR        R           7115
     A***********                           REFFLD(BRCD)
     A***********                                    End   /S01117/
     A*
     A            PUTC      R           9 20
     A                                      REFFLD(PCAL)
     A*
     A*                                              Begin /CFF004/
     A*********** STKPRC        12A     9 22TEXT('STRIKE PRICE')
     A            STKPRC        16A     9 22TEXT('STRIKE PRICE')
     A*                                              End   /CFF004/
     A*
     A*                                              Start /149327/
     A***********                       9 65'CURRENCY'
     A***********
     A*********** ISCY      R           9 79
     A                                  9 40'CONTINGENT - '
     A            CNGA          16A     9 53
     A            OTHC      R           9 69
     A*
     A  25                              9 75'OTHER AMOUNT - '
     A  25        OTHA          16A     9 90
     A  25        CTCY      R           9106
     A                                      REFFLD(OTHC)
     A*
     A                                  9117'CURRENCY -'
     A            ISCY      R           9128
     A*                                              End   /149327/
     A*
     A                                 11  7'CUSTOMER/BOOK'
     A                                 12  7'BROKER/NAME/TOWN'
     A                                 13  7'----------------'
     A*
     A                                 12 39'POSITION L/S'
     A                                 13 39'-------- ---'
     A*
     A*  /* BEGIN E44757 */
     A                                 12 60'CURRENT UNREALISED P&L'
     A                                 13 60'------- ---------- ---'
     A*  /* END   E44757 */
     A*
     A                                 15 26'REALISED P&L'
     A                                 16 26'THIS MONTH'
     A                                 17 26'-----------'
     A*
     A*  /* BEGIN E44757 */
     A**********                       15 54'UNREALISED'
     A**********                       16 58'P&L'
     A**********                       17 54'----------'
     A*
     A                                 15 54'UNREALISED P&L'
     A                                 16 54'THIS MONTH'
     A                                 17 54'--------------'
     A*  /* END   E44757 */
     A*
     A                                 15 80'COMMISSION'
     A                                 16 80'THIS MONTH'
     A                                 17 80'-----------'
     A*
     A                                 15108'CHARGES'
     A                                 16106'THIS MONTH'
     A                                 17106'----------'
     A*
     A                                 15122'AVERAGE'
     A                                 16123'PRICE'
     A                                 17122'-------'
     A*
     A*
     A          R FMTP102                   TEXT('TOP OF PAGE SPACE')
     A                                      SPACEB(2) SPACEA(2)
     A                                     1'  '
     A*
     A*
     A*
     A          R FMTP103                   TEXT('EMPTY FILE MESSAGE')
     A                                      SPACEB(2) SPACEA(3)
     A                                    37'*** THERE ARE NO OUTSTANDING '
     A                                    66'HOLDINGS TO REPORT ***'
     A*
     A*
     A*
     A          R FMTP104                   TEXT('POSITION DETAILS')
     A                                      SPACEA(3)
     A*
     A            CUSBKN    R              7
     A                                      REFFLD(BOKD)
     A*
     A N54        POS1      R             40EDTCDE(Z)
     A                                      REFFLD(SOPE)
     A*
     A  54        POS1A          1A       46TEXT('POSITION')
     A*
     A            LGSHT1         1A       49TEXT('LONG/SHORT IDENTIFER')
     A*
     A*  /* BEGIN E44757 */
     A            RCUPL         24A       55TEXT('CURRENT UNREAL. P&L')
     A*
     A*  /* END   E44757 */
     A*
     A*
     A            TOWN      R              7REFFLD(CTWN) SPACEB(1)
     A*
     A  55        POS2      R             40REFFLD(SOPE)
     A                                      EDTCDE(Z)
     A*
     A  55        LGSHT2         1A       49TEXT('SHORT IDENTIFER')
     A*
     A*                                              Begin /CFF004/
     A*********** PRLS          24A       19TEXT('REALISED PROFIT AND LOSS')
     A***********                           SPACEB(2)
     A***********
     A*********** UPRLS         24A       45TEXT('UNREALISED PROFIT AND LOSS')
     A***********
     A*********** COMTHM        24A       71TEXT('COMMISSION THIS MONTH')
     A***********
     A*********** CHGTHM        24A       97TEXT('CHARGES THIS MONTH   ')
     A***********
     A***********                                       Start /S01456/
     A*********** AVRPRC        12A      121TEXT('AVERAGE PRICE ')
     A**30******* AVRPRC        12A      121TEXT('AVERAGE PRICE ')
     A*N30******* FFAVR         10  6    121TEXT('AVERAGE PRICE ')
     A***********                           EDTWRD('  0 .      ')
     A***********                                       End   S01456/
      *
     A            PRLS          24A       15TEXT('REALISED PROFIT AND LOSS')
     A                                      SPACEB(2)
     A*
     A            UPRLS         24A       41TEXT('UNREALISED PROFIT AND LOSS')
     A*
     A            COMTHM        24A       67TEXT('COMMISSION THIS MONTH')
     A*
     A            CHGTHM        24A       93TEXT('CHARGES THIS MONTH   ')
     A*
     A  30        AVRPRC        16A      117TEXT('AVERAGE PRICE ')
     A N30        FFAVR         14  7    117TEXT('AVERAGE PRICE ')
     A                                      EDTWRD('     0 .       ')
     A*                                              End   /CFF004/
     A*
     A*
     A          R FMTP105                   TEXT('DATABASE ERROR FORMAT')
     A*
     A                                 14 45'DATABASE ERROR'
     A***********                                    Begin /S01117/
     A            DBASE          3  0  14 60
     A                                 14 65'IN PROGRAM'
     A            DBPGM         10A    14 76
     A*********** DBASE          2A    14 60
     A***********                      14 64'IN PROGRAM'
     A*********** DBPGM          8A    14 75
     A***********                      16 45'FILE'
     A*********** DBFILE         5A    16 50
     A***********                      16 59'KEY'
     A*********** DBKEY         29A    16 63
     A***********                                    End   /S01117/
     A*
     A*
     A          R FMTP106                   TEXT('END OF REPORT MESSAGE')
     A                                      SPACEB(1)
     A***********                                    Begin /S01117/
     A***********                         54'*** END OF REPORT ***'
     A N50                                54'*** END OF REPORT ***'
     A  50                                46 '*** END OF REPORT FOR'
     A  50                                68 'BRANCH'
     A  50        BCODE     R             75
     A                                      REFFLD(BRCA)
     A  50                                79 '***'
     A***********                                    End   /S01117/
