      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF Monthly P And L Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF1250P1  -  MONTHLY PROFIT AND LOSS REPORT                  *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01456             DATE  13DEC93              *
     A*                 S01117             DATE  26APR90              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01456  -  Monthly Profit and Loss Enhancements.             *
     A*  S01117  -  MULTIBRANCHING                                    *
     A*                                                               *
     A*****************************************************************
     A                                       REF(MFRFL)
     A          R FMTP101                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A***********                                    Begin /S01117/
     A***********                       3  7'REFERENCE FF1250'
     A                                  3  7'REFERENCE FF1250P1'
     A***********                                    End   /S01117/
     A            TITL      R           3 37
     A                                  3101'DATE'
     A            RUNA      R           3106
     A                                  3119'PAGE'
     A            PAGE           5S 0   3124 EDTCDE( 4)
     A*                                              Start /S01456/
     A  21                              5  2'BUSINESS DATE: '
     A  21        BUSDA          7A     5 18
     A*                                              End   /S01456/
     A                                  5 51'MONTHLY PROFIT AND LOSS REPORT'
     A***********                                    Begin /S01117/
     A  35                              5 90'BRANCH '
     A  35        BRCA      R           5 97
     A  35                              5100'-'
     A  35        BRNM          30A     5101
     A***********                                    End   /S01117/
     A                                  6 51'------------------------------'
     A N20                              8  2'BOOK'
     A N20        BOKC      R           8  7
     A N20                              8 10'-'
     A N20        BOKD      R           8 12
     A                                  8 56'CURRENCY -'
     A            CCY       R           8 67
     A  20                              8 86'CONSOLIDATION -'
     A            PBOCC         16A     8102
     A***********                                    Begin /S01117/
     A***********                       8121'BRANCH -'
     A*********** BRCD      R           8130
     A***********                                    End   /S01117/
     A N20                              9  2'----------'
     A N20                              9 12'------------------------------'
     A                                  9 56'--------------'
     A  20                              9 86'-----------------'
     A***********                                    Begin /S01117/
     A***********                       9121'------------'
     A***********                                    End   /S01117/
     A                                 11 15'START  AND  END'
     A                                 11 36'TOTAL  TOTAL'
     A                                 11 53'COMMISSION/'
     A                                 11 74'COMMISSION/'
     A                                 11 97'REALISED'
     A*                                              Start /S01456/
     A***********                      11117'UNREALISED'
     A N21                             11117'UNREALISED'
     A  21                             11114'UNR. P/L CURRENT'
     A*                                              End   /S01456/
     A                                 12  2'INSTRUMENT'
     A                                 12 14'POSITION'
     A                                 12 24'  PRICE   '
     A                                 12 36'PURCH  SALES'
     A                                 12 53'CHARGES PAID'
     A                                 12 72'CHARGES RECEIVED'
     A                                 12 94'PROFIT OR LOSS'
     A*                                              Start /S01456/
     A***********                      12115'PROFIT OR LOSS'
     A N21                             12115'PROFIT OR LOSS'
     A  21                             12113'UNR. P/L THIS MTH.'
     A*                                              End   /S01456/
     A                                 13  2'----------'
     A                                 13 14'--------'
     A                                 13 24'----------'
     A                                 13 36'-----  -----'
     A                                 13 54'------------'
     A                                 13 72'----------------'
     A                                 13 94'--------------'
     A*                                              Start /S01456/
     A***********                      13115'--------------'
     A N21                             13115'--------------'
     A  21                             13115'-----------------'
     A*                                              End   /S01456/
     A*
     A          R FMTP102                   TEXT('DETAILS LINE 1')
     A                                      SPACEB( 2)
     A            PISTT          6A        1TEXT('INSTRUMENT')
     A            MSNME          3A        8TEXT('MONTH')
     A            YEAR           2A       11TEXT('YEAR')
     A            POSM      R             15 EDTCDE( L)
     A            PPRSM         12A       23TEXT('PRICE AT START OF THE MONTH')
     A            TOTP      R             34 EDTCDE( L)
     A            TOTS      R             41 EDTCDE( L)
     A            PCOCP         22A       48TEXT('COM/CHRG PAID')
     A            PCOCR         22A       69TEXT('COM/CHRG REVEIVED')
     A            PRPLM         22A       90TEXT('REALISED P/L')
     A*                                              Start /S01456/
     A**********  PUPLM         22A      111TEXT('UNREALISED P/L')
     A N21        PUPLM         22A      111TEXT('UNREALISED P/L')
     A  21        PCUPL         22A      111TEXT('UNREALISED P/L CURRENT')
     A*                                              End   /S01456/
     A*
     A          R FMTP103                   TEXT('DETAIL LINE 2')
     A                                      SPACEB( 1)
     A            PCAL      R              1
     A N03        RPTPR         12A        3TEXT('STRIKE PRICE')
     A            TOPE           7S 0     15TEXT('CLOSING POSITION')
     A                                       EDTCDE( L)
     A            PPLEC         12A       23TEXT('CLOSING PRICE')
     A*                                              Start /S01456/
     A  21        PRAMON        22A       90TEXT('AMORTISATION')
     A********    PRAMO         22A      111TEXT('UNREALISED P/L')
     A N21        PRAMO         22A      111TEXT('UNREALISED P/L')
     A  21        PUPLM         22A      111TEXT('UNREALISED P/L THIS MONTH')
     A*                                              End   /S01456/
     A            PATRK          1A      132TEXT('ASTERISK')
     A*
     A          R FMTP104                   TEXT('INSTRUMENT TOTAL')
     A                                    48'--------------------'
     A                                      SPACEB( 2)
     A                                    69'--------------------'
     A                                    90'--------------------'
     A                                   111'--------------------'
     A            PICOCP        22A       48TEXT('COM/CHRG PAID AT INSTR LEVEL')
     A                                      SPACEB( 1)
     A            PICOCR        22A       69TEXT('COM/CHRG RECD AT INSTR LEVEL')
     A            PIRPLM        22A       90TEXT('REALISED P/L AT INSTR LEVEL')
     A*                                              Start /S01456/
     A  21        PICUPL        22A      111TEXT('UNREAL P/L AT INSTR LEVEL')
     A  21                                  SPACEA( 1)
     A*                                              End   /S01456/
     A            PIUPLM        22A      111TEXT('UNREAL P/L AT INSTR LEVEL')
     A                                     2' '
     A                                      SPACEA( 1)
     A*
     A          R FMTP105                   TEXT('PL CCY TOTS')
     A                                    48'--------------------'
     A                                      SPACEB( 2)
     A                                    69'--------------------'
     A                                    90'--------------------'
     A                                   111'--------------------'
     A            PCCOCP        22A       48TEXT('COM/CHRG PAID AT CCY LEVEL')
     A                                      SPACEB( 1)
     A            PCCOCR        22A       69TEXT('COM/CHRG RECD AT CCY LEVEL')
     A            PCRPLM        22A       90TEXT('REALISED P/L AT CCY LEVEL')
     A*                                              Start /S01456/
     A  21        PCCUPL        22A      111TEXT('UNREALISED P/L AT CCY LEVEL')
     A  21                                  SPACEA( 1)
     A*                                              End   /S01456/
     A            PCUPLM        22A      111TEXT('UNREALISED P/L AT CCY LEVEL')
     A*
     A***********                                    Begin /S01117/
     A**********R FMTP106                   TEXT('NO DETAIL')
     A***********                      35 50'***  NO DETAILS TO REPORT
     A***********                                    End   /S01117/
     A*
     A          R FMTP107                   TEXT('DATABASE ERROR')
     A                                      SPACEB(2)
     A                                    45'DATABASE ERROR '
     A***********                                    Begin /S01117/
     A            DBASE          3  0     60
     A                                    64'IN PROGRAM'
     A            DBPGM         10A       75
     A*********** DBASE          2A       60
     A***********                         63'IN PROGRAM'
     A*********** DBPGM          8A       74
     A***********
     A***********                           SPACEA(1)
     A***********                         45'FILE '
     A*********** DBFILE         5A       50
     A***********                         55', KEY'
     A*********** DBKEY         29A       61
     A***********
     A***********                                    End   /S01117/
     A*
     A          R FMTP108                   TEXT('END OF REPORT')
     A                                      SPACEB( 3)
     A***********                                    Begin /S01117/
     A***********                         56'*** END OF REPORT ***'
     A  35                                46'***  END OF REPORT FOR BRANCH '
     A  35        BCODE          3A       76TEXT('BRANCH CODE')
     A  35                                81'***'
     A N35                                56'*** END OF REPORT ***'
     A*                                              Start /S01456/
     A  04                                40'FOR MARKET CENTRE '
     A  04                                  SPACEB( 3)
     A  04        MRKT           2A       58
     A*
     A  04                                61'BUSINESS DATE IS NOT AN EOM DATE'
     A*                                              End   /S01456/
     A***********                                    End   /S01117/
