      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas FF Initial Margin Reconciliation')
     A*****************************************************************
     A*                                                               *
     A*  Midas   FINANCIAL FUTURES AND OPTIONS MODULE
     A*                                                               *
     A*  FF1440P1  -  Initial Margin Reconciliation                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CFF004             DATE  18SEP96              *
     A*  PREV AMEND NO. FUJI-FF0021        DATE  27AUG92              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CFF004 - Increase in size of Price Fields                    *
     A*  FUJI-FF0021 - NEW INITIAL MARGIN RECONCILIATION REPORT
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HDRMAIN                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                  3  7'REFERENCE'
     A                                  3 17'FF1440P1'
     A            TITL      R           3 43
     A                                  3101'DATE'
     A            RUNA      R           3106
     A                                  3119'PAGE'
     A            PAGE           5S 0   3124 EDTCDE( 4)
     A                                  5 25'INITIAL MARGIN '
     A                                    +1'RECONCILIATION '
     A                                    +1'REPORT'
     A                                    +5'for'
     A                                  6 25'----------------'
     A                                    +0'---------------'
     A                                    +0'------'
     A  60        RBRCA     R           5 94REFFLD(BRCA)
     A  60                              5 97'-'
     A  60        BRNM      R           5 98
     A*
     A          R HDRCCY                    TEXT('Currency Header')
     A*
     A                                  7  7'Currency -'
     A            RCCY           3A       +1
     A            RCYNM         14A       +1
     A*
     A          R HDRCB                     TEXT('Customer/broker')
     A                                  9  2'CUSTOMER/BROKER'
     A                                  9 19'REFERENCE'
     A*
     A                                  9 30'TRANS'
     A                                 10 30'TYPE'
     A*
     A                                  9 37'NO. OF'
     A                                 10 37'CONTRACT'
     A*
     A                                  9 48'NO. OF OPEN'
     A                                 10 48'CONTRACT'
     A*
     A                                  9 75'MARGIN'
     A*
     A            RCRNM         20A    11  2
     A*
     A          R HDRINS                    TEXT('Instrument Hdr')
     A                                      SPACEB( 1)
     A*
     A            RINST          7A        2 TEXT('INSTRUMENT')
     A            RMNTH     R              9 REFFLD(MNTH)
     A                                       TEXT('REPORT INSTR-MONTH')
     A            RMYS           1A       12 TEXT('MONTH/YEAR SEPARTR')
     A            RYRNO          2A       13 TEXT('REPORT YEAR NUMBER')
     A                                       SPACEA( 1)
     A            RPCAL     R              2 REFFLD(PCAL)
     A*                                              Begin /CFF004/
     A*********** RSTRP         12A        4 TEXT('RP:STRIKE PRICE')
     A            RSTRP         16A        4 TEXT('RP:STRIKE PRICE')
     A*                                              End   /CFF004/
     A*
     A          R TRADDET                   TEXT('Trade details')
     A                                      SPACEA( 1)
     A*
     A            RTNBR         10A       19
     A            TRTY      R             30
     A            NUCO      R             37 EDTCDE( 3)
     A            NOPEN     R             48 REFFLD(NOBO)
     A                                       EDTCDE( 3)
     A            RTMAR         19A       63TEXT('Trade Margin')
     A*
     A          R INSTOT                    TEXT('Instrument total')
     A                                      SPACEB( 1)
     A                                     7'Instrument Total - '
     A                                    63'-------------------'
     A                                      SPACEA( 1)
     A            RIMAR         19A       63
     A*
     A          R CBRTOT                    TEXT('Broker/Customer +
     A                                            Total')
     A                                      SPACEB( 1)
     A  55                                 7'Broker Total - '
     A N55                                 7'Customer Total -'
     A                                    90'==================='
     A                                      SPACEA( 1)
     A            RCBMAR        19A       90
     A*
     A          R CCYTOT                    TEXT('Currency total')
     A                                      SPACEB( 2)
     A                                     7'Currency Total - '
     A                                    90'==================='
     A                                      SPACEA( 1)
     A            RCYMAR        19A       90
     A                                    90'==================='
     A                                      SPACEB( 1)
     A*
     A          R ENDRPT                    TEXT('END OF REPORT')
     A                                      SPACEB( 2)
     A N60                                56'*** END OF REPORT ***'
     A  60                                48 '*** END OF REPORT FOR'
     A  60                                70 'BRANCH'
     A  60        RBRCA     R             77
     A                                      REFFLD(BRCA)
     A  60                                81 '***'
     A*
