     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas FF Market Centres Maintenance')                  *
/*OVRF*: OVRDBF MARKTX MARKT                                        : *
     F********************************************************************
     F*                                                                  *
     F*  Midas  -  Futures and Options Module
     F*                                                                  *
     F*   FF0110  -  MARKET CENTRES MAINTENANCE                          *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CAP208             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 237308             Date 27Jan06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG10372           Date 05Feb06               *
      *                 BUG8045            Date 02Aug05               *
      *                 CER001             Date 25Apr05               *
      *                 BUG3644            Date 12Jul04               *
      *                 CLE025             Date 20Oct03               *
      *                 BUG2384            Date 06May04               *
      *                 CAAA03             Date 11Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 14Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 138029             Date 10May99               *
     F*                 104633             DATE 08JUL96               *
     F*                  S01456             DATE 08NOV93                 *
     F*                  S01117             DATE 15JAN90                 *
     F*                  E15718             DATE 03AUG89                 *
     F*                  S01199             DATE 01AUG89                 *
     F*                  S01179             DATE 23/09/88                *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP208 - F&O Market Instrument API (Recompile)               *
      *  CRE026 - Consumer Banking (Recompile)                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  237308 - Funtion F10 during deletion does not work.          *
      *           Recompile due to changes in FF0110DF display file.  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG10372 - F10 is not working... again.                      *
      *  BUG8045 - F10 is not working                                 *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
      *  CLE025 - Credit Lines (Recompile)                            *
      *  BUG2384 - Recompile due to change in FF0110DF.                  *
      *  CAAA03 - Webfacing Changes                                   *
     F*   CPB001  -  'Private Banking' Module                            *
     F*  138029 - Go back to detail screen when ? is input.           *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     F*   S01456  -  'FF' MONTHLY P&L ENHANCEMENTS.                      *
     F*              Recompiled due to change to PF/MKCTLD.              *
     F*   S01117  -  MULTIBRANCHING                                      *
     F*   E15718  -  CORRECT ACTN DISPLAY DURING PGUP & PGDN ON          *
     F*   S01199  -  HELP SYSTEM                                     *
     F*                 REVIEW SCREEN                                    *
     F*   S01179     ****** AS400 CONVERSION ******                      *
     F*   The keyword RTGAID is no longer supported on the AS400         *
     F*   and command keys and indicators have been used to replace      *
     F*   this in the display file. To disrupt the structure of the      *
     F*   program as little as possible a new subroutine - ASCHGS -      *
     F*   has been coded. This moves the values which would have         *
     F*   been passed to the program (via RTGAID) into the AID           *
     F*   field depending on which function key has been pressed.        *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*     CREATION REQUIREMENTS                                        *
     F*                                                                  *
     F*     IGNDECERR(*NO)                                               *
     F*     OVRDBF     FILE(MARKTX) TOFILE(MARKT)                        *
     F*                                                                  *
     F********************************************************************
     FMARKT   UF  E           K        DISK         KCOMIT       A
     F                                              KINFDS INFDS
     F                                              KINFSR INFSR
     FMARKTA  UF  E                    DISK         KCOMIT
     FMKCTL   UF  E           K        DISK         KCOMIT       A
     F                                              KINFDS INKDS
     F                                              KINFSR INFSR
     F*ABFF***IF* E           K        DISK                               S01117
     F**ICD*1**** TABTB10F                          KIGNORE               S01117
     F*********** TABTB11F                          KIGNORE               S01117
     F*********** TABTB20F                          KIGNORE               S01117
     F*********** TABTB40F                          KIGNORE               S01117
     F*********** TABTB80F                          KIGNORE               S01117
     F*********** TABTE10F                          KIGNORE               S01117
     F**CCY*1**** TABTG10F                          KIGNORE               S01117
     F**COUNTRIES TABLETHF                          KIGNORE               S01117
     F*********** TABLETLF                          KIGNORE               S01117
     F*********** TABLETRF                          KIGNORE               S01117
     F*********** TABLET2F                          KIGNORE               S01117
     FFF0110DFCF  E                    WORKSTN      KINFDS SCRDS
     F                                        RRN   KSFILE FF0110S0
     FMARKTX  IF  E           K        DISK
     F            MARKTDF                           KRENAMEMARKTDFX
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       01         S/FF0110C0  SFLDSP
     F*       02         S/FF0110C0  SFLDSPCTL
     F*       03         S/FF0110C0  SFLEND
     F*       04         S/FF0110C0  ROLLDOWN NOT ALLOWED
     F*       05         S/FF0110F1  PROTECT/NON-DISPLAY ON LOCATION
     F*       06         S/FF0110F0  PROTECT FIELDS
     F*
     F*       10         START  SCREEN
     F*       11         REVIEW SCREEN
     F*       12         UPDATE SCREEN
     F*
     F*       15         INDICATOR USED ON WRITE/UPDATE OPS TO CONDITION
     F*                                    CALL OF INFSR
     F*
     F*       20         VLDCMDKY INDICATOR FROM DISPLAY FILE             S01179
     F*                                                                   S01179
     F*       21         S/FF0110F1  ERROR-ACTN
     F*       22         S/FF0110F1  ERROR-MRKT
     F*
     F*       23         S/FF0110F2  COMMAND KEY ENABLING
     F*
     F*       25         ROLLUP                                           S01179
     F*       26         ROLLDOWN                                         S01179
     F********27*********HELP                                       S01179S01199
     F*                                                                   S01179
     F*       29         S/FF0110F0  ERROR-MLOC                           S01117
     F*       30         S/FF0110F0  POSITION CURSOR
     F*       31         S/FF0110F0  ERROR-MKTN
     F*       32         S/FF0110F0  ERROR-MKCN
     F*       33         S/FF0110F0  ERROR-MKLC
     F*       34         S/FF0110F0  ERROR-AEOC
     F*       35         S/FF0110F0  ERROR-APRP
     F*       36         S/FF0110F0  ERROR-MKMQ
     F*       38         S/FF0110F0  ERROR-INNR                           CPB001
     F*
     F*       40-51      S/FF0110F0  PROTECT MKMQ,X (SYSTEM SETUP FLAG)
     F*
     F*       60         ADD RECORD
     F*
     F*       98         DATF = MMDDYY
     F*       99         WORK
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F*       'AID' VALUES USED
     F*
     F*       01         CA3                                              S01179
     F*       04         CA10                                             S01179
     F*       05         CA12                                             S01179
     F*       RA         ENTER PRESSED
     F*       UP         ROLLUP
     F*       DN         ROLLDOWN
     F*       HP         HELP PRESSED
     E                    CPY@    1   1 80
     E*
     E                    DET       256  1
     E** ARRAY FOR COMMIT TEXT
     E*
     E                    ERR        19  4
     E** ARRAY FOR ERROR MESSAGES
     E*
     E                    INF     1  12 76
     E** ARRAY FOR INFORM MESSAGES
     E*
     E                    MQ         12  1
     E** ARRAY FOR MONTH QUOTE CONVERSATION
     E*
     E                    VLC    26  26  1
     E** ARRAY TO VALIDATE MARKET CODE
     E*
     ISCRCTL      DS
     I*
     I** KEY FIELDS (TAKEN FROM 'KEY' PORTION OF SCREEN)
     I*
     I                                        1   1 REVW
     I                                        2   2 ACTN
     I                                        3 004 INPK
     I                                        3 004 MRKT
     I                                        3   3 MRKT1
     I                                        4   4 MRKT2
     I                                      005 006 PAGK
     I                                      005 006 PMRKT
     I*
     IDETREC    E DSMARKTD
     I*
     I** DATA STRUCTURE FOR INITIALISING A RECORD
     I*
     I              MRKT                            MRKTX
     I*
     I*DETS******E DSFF0110DF                                             S01179
     IDETS        DS                                                      S01179
     I*
     I** DATA STRUCTURE FOR SCREEN DETAIL OUTPUT TO CMTTXT
     I                                        1  20 SMKTN
     I                                       21  22 SMKCN
     I                                       23  25 SMLOC                 S01117
     I                                       26  28 SMKLC                 S01117
     I                                       29  38 SMPRF                 S01117
     I                                       39  39 SAEOC                 S01117
     I                                       40  40 SAPRP                 S01117
     I                                       41  41 SMQ01                 S01117
     I                                       42  42 SMQ02                 S01117
     I                                       43  43 SMQ03                 S01117
     I                                       44  44 SMQ04                 S01117
     I                                       45  45 SMQ05                 S01117
     I                                       46  46 SMQ06                 S01117
     I                                       47  47 SMQ07                 S01117
     I                                       48  48 SMQ08                 S01117
     I                                       49  49 SMQ09                 S01117
     I                                       50  50 SMQ10                 S01117
     I                                       51  51 SMQ11                 S01117
     I                                       52  52 SMQ12                 S01117
     I***********                            23  25 SMKLC                 S01117
     I***********                            26  35 SMPRF                 S01117
     I***********                            36  36 SAEOC                 S01117
     I***********                            37  37 SAPRP                 S01117
     I***********                            38  38 SMQ01                 S01117
     I***********                            39  39 SMQ02                 S01117
     I***********                            40  40 SMQ03                 S01117
     I***********                            41  41 SMQ04                 S01117
     I***********                            42  42 SMQ05                 S01117
     I***********                            43  43 SMQ06                 S01117
     I***********                            44  44 SMQ07                 S01117
     I***********                            45  45 SMQ08                 S01117
     I***********                            46  46 SMQ09                 S01117
     I***********                            47  47 SMQ10                 S01117
     I***********                            48  48 SMQ11                 S01117
     I***********                            49  49 SMQ12                 S01117
     I*
     IMQDS        DS
     I*
     I** DATA STRUCTURE FOR MONTHS QUOTED CONVERSION
     I*
     I                                        1  12 MQ
     I                                        1   1 MQ01
     I                                        2   2 MQ02
     I                                        3   3 MQ03
     I                                        4   4 MQ04
     I                                        5   5 MQ05
     I                                        6   6 MQ06
     I                                        7   7 MQ07
     I                                        8   8 MQ08
     I                                        9   9 MQ09
     I                                       10  10 MQ10
     I                                       11  11 MQ11
     I                                       12  12 MQ12
     ISCRMSG      DS                          1 152
     I*
     I** DATA STRUCTURE FOR ERROR MESSAGES TO SCREEN
     I*
     I                                        1  76 ERR
     I                                        1  76 ERRMSG
     I                                       77 152 INFMSG
     I*
     ISCRDS       DS
     I*
     I** DISPLAY FORMAT STATUS DATA STRUCTURE
     I*
     I                                      287 288 AID
     IINFDS       DS
     I*
     I** DATA STRUCTURE FOR RPG 01021 ERROR HANDLING
     I*
     I                                     *STATUS  STATUS
     I*
     IINKDS       DS
     I*
     I** DATA STRUCTURE FOR RPG 01021 ERROR HANDLING
     I*
     I                                     *STATUS  STATK
     I*
     I*DA********UDS                            256                       S01117
     ILDA         DS                            256                       S01117
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I** *LOCK IN LDA immediately before and OUT LDA immediately          S01117
     I** after each database error handling.                              S01117
     I*
     I***********                           134 177 DBLOT                 S01117
     I***********                           134 138 DBFILE                S01117
     I***********                           139 167 DBKEY                 S01117
     I***********                           168 175 DBPGM                 S01117
     I***********                           176 1770DBASE                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I**                                                                  S01117
     IRUNDT       DS                                                      S01117
     I** DATA AREA RUNDAT                                                 S01117
     I                                        1   7 RUNA                  S01117
     I                                    P   8  100RUND                  S01117
     I                                       11  11 SSF                   S01117
     I                                       12  12 DATF                  S01117
     I                                       13  13 MBIN                  S01117
     ISDCTRY    E DSSDCTRYPD                                              S01117
     I** COUNTRY DETAILS                                                  S01117
     I              A4CNCD                          CTRY                  S01117
     ISDCURR    E DSSDCURRPD                                              S01117
     I** CURRENCY DETAILS                                                 S01117
     I              A6CYCD                          LCCY                  S01117
     ISDLOCN    E DSSDLOCNPD                                              S01117
     I** LOCATION DETAILS                                                 S01117
     I              DVLCCD                          LCCD                  S01117
     ISDPLIN    E DSSDPLINPD                                              CPB001
     I** Intrument Type Details                                           CPB001
     ISDMMOD    E DSSDMMODPD                                              CPB001
     I** Module Details                                                   CPB001
     I/COPY WNCPYSRC,FF0110I001
     I@DSFDY    E DSDSFDY                                                 S01117
     I*  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE               S01117
     I@DSSDY    E DSDSSDY                                                 S01117
     I*  SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE               S01117
      **                                                                  S01117
     IZMUSER      DS                             17                       S01117
      ** DATA AREA OF MENU USER                                           S01117
     I                                       11  13 USRBCH                S01117
     I*
     I/COPY WNCPYSRC,FF0110I002
     I/COPY ZSRSRC,ZTNLU1Z1
     I**   DATA STRUCTURE FOR ZTNLU1
     I*
     ICMTTXT      DS
     I*
     I** DATA STRUCTURE FOR SETUP OF COMMIT TEXT
     I*
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTNX
     I                                       26  35 USIDX
     I                                       51 306 DET
     I*
     IPSDS       SDS
     I*
     I** PROGRAM STATUS DATA STRUCTURE FOR WS/USER ID'S
     I*
     I                                      244 246 WSID
     I                                      254 263 USID
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *                                                                                     BUG10372
     IWDATA       DS                           1024                                         BUG10372
     I                                        1   2 #1MRKT                                  BUG10372
     I                                     10011001 #1IMDL                                  BUG10372
     C*================================================================
     C*
     C** CALCULATION SPECIFICATIONS:KEY LISTS
     C*
     C**  DEFINE KEY LIST FOR CHAINING TO MARKT FILE
     C*
     C           MARKTK    KLIST
     C                     KFLD           MRKT
     C*================================================================
      /EJECT
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C** PERFORM SETUP OF STANDARD FIELDS
     C*
     C                     EXSR INIT
     C*
     C** D O    U N T I L   C K 3   O R   E R R O R        -  S T A R T
     C*
     C           AID       DOUEQ'01'
     C           *INU7     OREQ '1'
     C*
     C           *IN10     CASEQ'1'       START
     C*
     C           *IN11     CASEQ'1'       REVIEW
     C*
     C           *IN12     CASEQ'1'       UPDATE
     C*
     C                     END
     C                     END
     C*
     C** D O    U N T I L   C K 3   O R   E R R O R        -  E N D
     C*
     C** TERMINATE PROGRAM
     C*
     C                     SETON                         LR
     C*================================================================
     C*  P R O G R A M     E N D
     C*================================================================
      /EJECT
     C*================================================================
      ** INDEX OF SUBROUTINES
      **
      ** INIT              INITIALISATION OF STANDARD FIELDS,
      **                   ACCESS OF STANDING DATA
      **
      ** START             OUTPUT AND PROCESS KEY FIELDS SCREEN
      **
      ** REVIEW            PROCESS SUBFILE SELECTION SCREEN
      **
      ** UPDATE            PROCESS DETAIL SCREEN AND UPDATE FILE
      **
      ** VALIDK            VALIDATE KEY FIELDS
      **
      ** VALIDD            VALIDATE DETAIL SCREEN
      **
      ** DEFLTS            FORMAT DEFAULT FIELD VALUES
      **
      ** INITRC            INITIALISE ALL FILE FIELDS TO ZERO/BLANK
      **
      ** TRSFS             FORMAT RECORD FIELDS FOR SCREEN
      **
      ** UPDFLS            UPDATE FILE RECORDS
      **
      ** UPDOWS            PROCESS UPDATE BY ANOTHER WORKSTATION
      **
      ** ZTNLU1            ACCESS DATAAREA FOR NEXT TRANSACTION NO.
      **
      ** INFSR             FILE ERROR HANDLING S/R
      **
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** INIT S/R - TO INITIALISE STATIC FIELDS AND ACCESS STANDING DATA
      ** CALLED ONCE AT START OF PROGRAM FROM MAIN LINE
      ** CALLS NO OTHER ROUTINES
      **
     C*================================================================
     C*
     C           INIT      BEGSR                           ** INIT BSR  **
     C*
     C** PREPARE LDA
     C*
     C           *NAMVAR   DEFN           LDA                             S01117
     C           *LOCK     IN   LDA                                       S01117
     C***********          MOVEL*BLANK    DBLOT                           S01117
     C                     MOVEL*BLANK    DBFILE                          S01117
     C                     MOVEL*BLANK    DBKEY                           S01117
     C                     MOVEL'FF0110'  DBPGM
     C                     Z-ADD*ZEROS    DBASE                           S01117
     C                     OUT  LDA                                       S01117
     C*
     C** PREPARE COMIT TEXT
     C*
     C                     MOVEL'FF'      MDID
     C                     MOVEL'FF0110'  PGMN
     C                     MOVELWSID      WSIDX
     C                     MOVELUSID      USIDX
     C*
     C** HELP PROGRAM PARAMETER
     C*
     C                     MOVEL'FF0110'  HBPGM   8
     C                     MOVEL*BLANK    WBLNK   1
     C*
     C** FOR HIGH INTENSITY SCREEN ATTRIBUTE
     C*
     C                     BITOF'01234567'HI      1
     C                     BITON'267'     HI
     C                     MOVELHI        INF,8
     C                     MOVELHI        INF,9
     C                     MOVELHI        INF,10
     C                     MOVELHI        INF,11
     C                     MOVELHI        INF,12
     C*
     C***GET*INSTALLATION*CONTROL*DATA*RECORD*1***************************S01117
     C***********                                                         S01117
     C***********          READ TABTB10F                 99               S01117
     C**N99******RECI      COMP 'D'                  9999  ON, IS DELETED S01117
     C************IN99     IFEQ '0'                                       S01117
     C***********                                                         S01117
     C***IF*FOUND*CHECK*SYSTEM*DATE*FORMAT,*DDMMYY*OR*MMDDYY.*************S01117
     C***********                                                         S01117
     C*                                                                   S01117
     C** IN THE DATAAREA RUNDAT & ZMUSER                                  S01117
     C*                                                                   S01117
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01117
     C           *NAMVAR   DEFN           ZMUSER                          S01117
     C                     IN   RUNDT                                     S01117
     C                     IN   ZMUSER                                    S01117
     C*                                                                   S01117
     C           DATF      COMP 'M'                      98MMDDYY, 98 ON
     C***********          ELSE                                           S01117
     C***********                                                         S01117
     C***OTHERWISE*DB*ERROR***********************************************S01117
     C***********                                                         S01117
     C***********          SETON                     U7U8  ***************S01117
     C***********          MOVEL'01'      DBASE            **DB ERROR 01**S01117
     C***********          MOVEL'TABFF'   DBFILE           ***************S01117
     C***********          MOVEL'01'      ZTABKY 12                       S01117
     C***********          MOVE '10'      ZTABKY                          S01117
     C***********          MOVELZTABKY    DBKEY                           S01117
     C***********          END                                            S01117
     C*                                                                   CPB001
     C***  Retrieve Module switched on                                    CPB001
     C*                                                                   CPB001
     C                     CALL 'AOMMODR0'                                CPB001
     C                     PARM '*DBERR ' @RTCD                           CPB001
     C                     PARM '*FIRST ' @OPTN                           CPB001
     C           SDMMOD    PARM SDMMOD    @DSFDY                          CPB001
     C*                                                                   CPB001
     C           BGN4ST    IFNE 'Y'                                       CPB001
     C                     MOVE *ON       *IN70                           CPB001
     C                     ENDIF                                          CPB001
     C*
     C** PROCEED TO START SCREEN UNLESS DATABASE ERROR
     C*
     C           *INU7     IFNE '1'
     C                     MOVEA'1'       *IN,10
     C                     END
     C/COPY WNCPYSRC,FF0110C001
      *                                                                                       CER001
      **  Check if switchable feature ULX008 is installed                                     CER001
      *                                                                                       CER001
     C                     CALL 'AOSARDR0'                                                    CER001
     C                     PARM *BLANKS   @RTCD   7                                           CER001
     C                     PARM '*VERIFY' @OPTN                                               CER001
     C                     PARM 'ULX008'  @KEY    6                                           CER001
      *                                                                                       CER001
     C           @RTCD     IFEQ *BLANK                                                        CER001
     C                     MOVE 'Y'       ULX008  1                                           CER001
     C                     ELSE                                                               CER001
     C                     MOVE 'N'       ULX008                                              CER001
     C                     ENDIF                                                              CER001
      *                                                                                       CER001
     C*
     C           XTINIT    ENDSR                           ** XTINIT ESR**
     C*===================================================================
      /EJECT
     C*===================================================================
      **
      ** START S/R TO OUTPUT AND PROCESS KEY FIELDS SCREEN
      ** CALLED FROM MAIN PROCESSING
      ** CALLS: VALIDK TO VALIDATE KEY FIELDS IF ENTERED
      **
     C*===================================================================
     C*
     C           START     BEGSR                           ** START BSR **
     C*
     C** INITIALISE SCREEN DATA
     C*
     C                     MOVEA'0'       *IN,10
     C                     MOVEA'001'     *IN,21
     C                     MOVEL*BLANK    SCRCTL
     C                     MOVELINF,1     INFMSG
     C*
     C** CLEAR WHOLE SCREEN
     C*
     C                     WRITEFF0110F3
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N  -  S T A R T
     C*
     C           *IN10     DOUEQ'1'
     C           *IN11     OREQ '1'
     C           *IN12     OREQ '1'
     C*
     C** S T A R T   S C R E E N     O U T P U T           -  S T A R T
     C*
     C**********           WRITEFF0110F1                                                      CAAA03
     C                     WRITEFF0110F2
     C                     WRITEFF0110F1                                                      CAAA03
     C                     READ FF0110F1                 99
     C                     MOVELERRMSG    HERCD 160
     C                     MOVELWBLNK     HERCD
     C                     MOVEL*BLANK    SCRMSG
     C*
     C** S T A R T   S C R E E N     O U T P U T           -  E N D
     C*
     C** C K 3 , H E L P , O R  E N T E R   P R E S S E D  -  S T A R T
     C*
     C** Call S/R to move values into AID                                 S01179
     C*                                                                   S01179
     C                     EXSR ASCHGS                                    S01179
     C*                                                                   S01179
     C** CK3 - TERMINATE
     C*
     C           AID       IFEQ '01'
     C                     MOVEA'1'       *IN,10
     C                     END
     C*
     C** HELP - CALL HELP PROGRAM
     C*
     C********** AID       IFEQ 'HP'                                      S01199
     C**********           MOVELHERCD     ERRMSG                          S01199
     C********** ERR,1     IFNE *BLANK                                    S01199
     C**********           MOVELHI        ERRMSG                          S01199
     C**********           END                                            S01199
     C**********           CALL 'MHELP'                                   S01199
     C**********           PARM           HBPGM                           S01199
     C**********           PARM           HERCD                           S01199
     C**********           END                                            S01199
     C*
     C** ENTER TAKEN VALIDATE KEY INPUT
     C*
     C           AID       IFEQ 'RA'
     C                     EXSR VALIDK
     C*
     C** D E T E R M I N E    S C R E E N   C H A N G E    -  S T A R T
     C*
     C** IF NO ERRORS, DETERMINE NEXT SCREEN
     C*
     C           SCRMSG    IFEQ *BLANK
     C*
     C**  - IF KEY FIELD IS BLANK, NEXT SCREEN IS REVIEW SCREEN
     C*
     C           MRKT      IFEQ *BLANK
     C                     MOVEA'1'       *IN,11
     C*
     C**  - OTHERWISE, DISPLAY REQUESTED DETAIL SCREEN AS PER KEY INPUT
     C*
     C                     ELSE
     C                     MOVEA'1'       *IN,12
     C                     END
     C*
     C                     SETOF                     23
     C                     SETOF                     24                                     BUG10372
     C*
     C                     END
     C*
     C** D E T E R M I N E    S C R E E N   C H A N G E    -  E N D
     C*
     C                     END
     C*
     C** C K 3 , H E L P , O R  E N T E R   P R E S S E D  -  E N D
     C*
     C                     END
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N  -  E N D
     C*
     C           XTSTAR    ENDSR                           ** XTSTAR ESR**
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** REVIEW S/R TO PROCESS SUBFILE SELECTION SCREEN
      ** CALLED FROM MAIN PROCESSING
      ** CALLS: VALIDK TO VALIDATE KEY FIELDS IF SUBFILE RECORD SELECTED
      **
     C*================================================================
     C*
     C           REVIEW    BEGSR                           ** REVIEW BSR**
     C*
     C                     MOVEA'0'       *IN,11
     C*
     C** SET PROCESSING FLAG TO FORCE SUBFILE REBUILD
     C*
     C                     MOVEL'RA'      AID
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N  -  S T A R T
     C*
     C           *IN10     DOUEQ'1'
     C           *IN11     OREQ '1'
     C           *IN12     OREQ '1'
     C*
     C** I F  R O L L   U P / D O W N   O R   E N T E R   P R E S S E D
     C*
     C** R E B U I L D   S U B F I L E                     -  S T A R T
     C*
     C                     MOVELINF,2     INFMSG
     C           AID       IFEQ 'DN'
     C           AID       OREQ 'UP'
     C           AID       OREQ 'RA'
     C*
     C** WRITE CONTROL RECORD
     C*
     C                     MOVEA'000'     *IN,1
     C                     Z-ADD*ZERO     RRN
     C                     WRITEFF0110C0
     C                     MOVEA'1'       *IN,4
     C*
     C** SET FILE POINTER - PAGE BACK
     C*
     C           AID       IFEQ 'DN'
     C                     MOVEL*BLANK    MRKT
     C                     MOVEL*BLANK    PPAGK 002
     C           MARKTK    SETLLMARKTX
     C*
     C** OTHERWISE        - PAGE FORWARD
     C*
     C                     ELSE
     C                     MOVELPMRKT     MRKT
     C                     MOVELPAGK      PPAGK
     C           MARKTK    SETLLMARKTX
     C                     END
     C*
     C** READ IN DETAILS-FILL UP SUBFILE
     C*
     C           RRN       DOUEQ18
     C           *IN,03    OREQ '1'
     C*
     C** READ DETAILS UNTIL SUBFILE PAGE IS FULL
     C*
     C                     READ MARKTX                   03
     C           *IN,03    IFEQ '0'
     C*
     C** IF RECORD IS DELETED, FLAG IT AS SO ON SCREEN
     C*
     C           RECI      IFEQ '*'
     C                     MOVEL'D'       STAT    1
     C                     ELSE
     C                     MOVEL*BLANK    STAT
     C                     END
     C*
     C                     ADD  1         RRN     40     04
     C*
     C                     WRITEFF0110S0
     C*
     C** RECORD NOW WRITTEN TO SUBFILE
     C*
     C                     END
     C*
     C** PAGE NOW FULL (OR EOF)
     C*
     C                     END
     C*
     C** READ ONE MORE RECORD FOR NEXT SCREEN
     C*
     C           *IN,03    IFEQ '0'
     C                     READ MARKTX                   03
     C                     END
     C*
     C** IF NOT EOF SET NEXT 'REVIEW FROM' KEY ELSE SET IT TO BLANK
     C*
     C           *IN,03    IFEQ '0'
     C                     MOVELINPK      PAGK
     C                     ELSE
     C                     MOVEL*BLANK    PAGK
     C                     END
     C*
     C** FOR SUBFILE CONTROL
     C*
     C           *IN,4     IFEQ '1'
     C                     MOVEA'010'     *IN,1
     C                     MOVELINF,3     INFMSG
     C                     ELSE
     C                     MOVEA'11'      *IN,1
     C                     END
     C                     END
     C*
     C** R E B U I L D   S U B F I L E     -     E N D
     C*
     C** R E V I E W   S C R E E N     O U T P U T         -  S T A R T
     C*
     C**********           WRITEFF0110C0                                                      CAAA03
     C                     WRITEFF0110F2
     C                     WRITEFF0110C0                                                      CAAA03
     C                     READ FF0110C0                 99
     C                     MOVELERRMSG    HERCD
     C                     MOVELWBLNK     HERCD
     C                     MOVEL*BLANK    SCRMSG
     C                     MOVEL*BLANK    REVW
     C*
     C** R E V I E W   S C R E E N     O U T P U T         -  E N D
     C*
     C** C K 3 , C K12 , H E L P , R E C O R D  S E L E C T-  S T A R T
     C*
     C** Call S/R to move values into AID                                 S01179
     C*                                                                   S01179
     C                     EXSR ASCHGS                                    S01179
     C*                                                                   S01179
     C** CK3 - TERMINATE
     C*
     C           AID       IFEQ '01'
     C                     MOVEA'1'       *IN,11
     C                     END
     C*
     C** CK12- INITIAL SCREEN
     C*
     C           AID       IFEQ '05'
     C                     MOVEA'1'       *IN,10
     C                     END
     C*
     C** HELP - CALL HELP PROGRAM
     C*
     C********** AID       IFEQ 'HP'                                      S01199
     C**********           MOVELHERCD     ERRMSG                          S01199
     C********** ERR,1     IFNE *BLANK                                    S01199
     C**********           MOVELHI        ERRMSG                          S01199
     C**********           END                                            S01199
     C**********           CALL 'MHELP'                                   S01199
     C**********           PARM           HBPGM                           S01199
     C**********           PARM           HERCD                           S01199
     C**********           END                                            S01199
     C*
     C** ENTER PRESSED, PROCESS RECORD SELECTION
     C*
     C           AID       IFEQ 'RA'
     C                     MOVEA'1'       *IN,99
     C           *IN,04    IFEQ '0'
     C                     READCFF0110S0                 99
     C                     END
     C*
     C** R E C O R D   S E L E C T   V A L I D A T I O N   -  S T A R T
     C*
     C           *IN99     IFEQ '0'
     C                     EXSR VALIDK
     C*
     C** NO ERRORS, UPDATE SCREEN CAN BE PROCESSED NEXT
     C*
     C           SCRMSG    IFEQ *BLANK
     C                     MOVEA'1'       *IN,12
     C                     MOVEL'Y'       REVW
     C                     END
     C                     END
     C*
     C** R E C O R D   S E L E C T   V A L I D A T I O N   -  E N D
     C*
     C                     END
     C*
     C** C K 3 , C K12 , H E L P , R E C O R D  S E L E C T - E N D
     C*
     C                     END
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N   - S T A R T
     C*
     C           XTREVI    ENDSR                           ** XTREVI ESR**
     C*
     C*===================================================================
      /EJECT
     C*===================================================================
      **
      ** UPDATE S/R TO PROCESS DETAIL SCREEN AND UPDATE FILE
      ** CALLED FROM MAIN PROCESSING
      ** CALLS: VALIDD, UPDFLS, INITRC, TRSFS, DEFLTS
      **
     C*===================================================================
     C*
     C           UPDATE    BEGSR                           ** UPDATE BSR**
     C*
     C** SET SCREEN INDICATORS - PROTECT KEY, SET OFF ERROR INDS,
     C** POSITION CURSOR
     C*
     C                     MOVEA'1'       *IN,5
     C                     MOVEA'0'       *IN,12
     C                     MOVEA'1000000' *IN,30
     C                     MOVEA'0'       *IN,38                          CPB001
     C                     MOVEA'00000000'*IN,40
     C                     MOVEA'0000'    *IN,48
     C                     MOVELACTN      ACTNX                           E15718
     C*
     C** ACCESS FILE RECORD FOR OLD TNLU TO CHECK AT UPDATE TIME
     C** IF NO RECORD PRESENTLY ON FILE, ZEROISE STORED TNLU
     C*
     C                     Z-ADD*ZERO     STNLU   50
     C           MARKTK    CHAINMARKTX               99
     C           *IN,99    IFEQ '0'
     C                     Z-ADDTNLU      STNLU
     C                     END
     C*
     C** IF INSERT (NEW OR OVER DELETED RECORD)
     C** INITIALIZE ALL FIELDS ON THE RECORD (IE ZEROIZE/BLANK OUT)
     C*
     C           ACTN      IFEQ 'I'
     C                     EXSR INITRC
     C                     END
     C*
     C** IF DELETED RECORD-ENQUIRY ONLY (RECI IS SET TO BLANK FOR INSERTS)
     C*
     C           RECI      IFEQ '*'
     C                     MOVEL'E'       ACTN
     C                     MOVEL'DELETED' DELT    7
     C                     ELSE
     C                     MOVEL*BLANK    DELT
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM FILE LAYOUT TO SCREEN LAYOUT
     C*
     C                     EXSR TRSFS
     C*
     C** SET UP DEFAULTS (DONE ON BLANK FIELDS)
     C*
     C                     EXSR DEFLTS
     C*
     C** IF DELETE/ENQUIRY PROTECT FIELD INPUT ON S/FF0110F0
     C*
     C           ACTN      IFEQ 'D'
     C           ACTN      OREQ 'E'
     C                     MOVEA'1'       *IN,6
     C                     MOVEA'11111111'*IN,40
     C                     MOVEA'1111'    *IN,48
     C                     END
      *                                                                                     BUG10372
      ** If DELETE, enable F10.                                                             BUG10372
      *                                                                                     BUG10372
     C           ACTN      IFEQ 'D'                                                         BUG10372
     C                     MOVEA'1'       *IN,24                                            BUG10372
     C                     ENDIF                                                            BUG10372
     C*
     C** IF SYSTEM SETUP FLAG ON PROTECT SOME FIELD INPUT ON S/FF0110F0
     C*
     C           ACTN      IFEQ 'A'
     C***********SFLG      ANDEQ1                                         S01117
     C           SSF       ANDEQ'Y'                                       S01117
     C*
     C** SET ON INDS 37-48 TO PROTECT MONTHS DEFINED AS QUOTED 'YES'
     C*
     C                     Z-ADD1         X
     C           X         DOUEQ13
     C           'Y'       LOKUPMQ,X                     99
     C           *IN,99    IFEQ '1'
     C                     ADD  39        X
     C                     MOVEA'1'       *IN,X
     C                     SUB  38        X
     C                     ELSE
     C                     Z-ADD13        X
     C                     END
     C                     END
     C*
     C                     END
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N  -  S T A R T
     C*
     C           *IN10     DOUEQ'1'
     C           *IN11     OREQ '1'
     C           *IN12     OREQ '1'
     C*
     C** L O A D    U  P    I N F O M E S S A G E S        -  S T A R T
     C*
     C           INFMSG    IFEQ *BLANK
     C           ACTN      IFEQ 'I'
     C                     MOVELINF,4     INFMSG
     C                     END
     C           ACTN      IFEQ 'A'
     C                     MOVELINF,5     INFMSG
     C                     END
     C           ACTN      IFEQ 'E'
     C                     MOVELINF,6     INFMSG
     C                     END
     C           ACTN      IFEQ 'D'
     C                     MOVELINF,7     INFMSG
     C                     END
     C                     END
     C*
     C** L O A D    U  P    I N F O M E S S A G E S        -  E N D
     C*
     C** D E T A I L   S C R E E N     O U T P U T         -  S T A R T
     C*
     C                     WRITEFF0110F1
     C**********           WRITEFF0110F0                                                      CAAA03
     C                     WRITEFF0110F2
     C                     WRITEFF0110F0                                                      CAAA03
     C                     READ FF0110F0                 99
     C                     MOVELERRMSG    HERCD
     C                     MOVELWBLNK     HERCD
     C                     MOVEL*BLANK    SCRMSG
     C**********           MOVE '0'       *IN24                                     BUG8045 BUG10372
     C/COPY WNCPYSRC,FF0110C005
     C*
     C** D E T A I L   S C R E E N     O U T P U T         -  E N D
     C*
     C** C K 3 , C K12 , H E L P , R O L L U P / D O W N   -  S T A R T
     C*
     C** Call S/R to move values into AID                                 S01179
     C*                                                                   S01179
     C                     EXSR ASCHGS                                    S01179
     C*                                                                   S01179
     C** CK3 - TERMINATE
     C*
     C           AID       IFEQ '01'
     C                     MOVEA'1'       *IN,12
     C                     END
     C*
     C** CK12- INITIAL SCREEN
     C*
     C           AID       IFEQ '05'
     C                     MOVEA'1'       *IN,10
     C                     END
     C*
     C** HELP - CALL HELP PROGRAM
     C*
     C********** AID       IFEQ 'HP'                                      S01199
     C**********           MOVELHERCD     ERRMSG                          S01199
     C********** ERR,1     IFNE *BLANK                                    S01199
     C**********           MOVELHI        ERRMSG                          S01199
     C**********           END                                            S01199
     C**********           CALL 'MHELP'                                   S01199
     C**********           PARM           HBPGM                           S01199
     C**********           PARM           HERCD                           S01199
     C**********           END                                            S01199
     C*
     C** PAGE FORWARD TAKEN, READ NEXT RECORD
     C*
     C           AID       IFEQ 'UP'
     C           ACTN      ANDNE'I'
     C                     MOVELACTNX     ACTN                            E15718
     C                     READ MARKTX                   12
     C*
     C** CHANGE MESSAGE IF END OF FILE REACHED - NO ROLL FWD
     C*
     C           *IN,12    IFEQ '1'
     C                     MOVELINF,8     INFMSG
     C                     ELSE
     C                     MOVEA'1'       *IN,12
     C                     END
     C                     END
     C*
     C** PAGE BACK TAKEN, READ PREVIOUS RECORD
     C*
     C           AID       IFEQ 'DN'
     C           ACTN      ANDNE'I'
     C                     MOVELACTNX     ACTN                            E15718
     C                     READPMARKTX                   12
     C*
     C** CHANGE MESSAGE IF BEGINNING OF FILE REACHED - NO ROLL BACK
     C*
     C           *IN,12    IFEQ '1'
     C                     MOVELINF,9     INFMSG
     C                     ELSE
     C                     MOVEA'1'       *IN,12
     C                     END
     C                     END
     C*
     C** NOT CK3,CK12,HELP,ROLLUP,ROLLDOWN
     C*
     C           AID       IFEQ 'RA'
     C           AID       OREQ '04'
     C*
     C** V A L I D A T E   D E T A I L   S C R E E N   I N P U T
     C*
     C                     EXSR VALIDD
     C*
     C** N O    D E T A I L   S C R E E N    E R R O R S   -  S T A R T
     C*
     C           SCRMSG    IFEQ *BLANK
     C           AID       ANDEQ'RA'
      *                                                                                       CER001
     C                     MOVELACTN      ACTION                                              CER001
     C                     MOVELMRKT      #1MRKT                                              CER001
     C           ULX008    IFEQ 'Y'                                                           CER001
     C                     CALL 'FF0111'                                                      CER001
     C                     PARM *BLANK    RTCODE  7                                           CER001
     C                     PARM           ACTION  1                                           CER001
     C**********           PARM           #1MRKT  2                                  CER001 BUG10372
     C                     PARM           WDATA                                             BUG10372
     C                     PARM *BLANK    W0RTN   7                                           CER001
      *                                                                                       CER001
      ** If F12 OR F3 is pressed                                                              CER001
      *                                                                                       CER001
     C           W0RTN     IFNE *BLANK                                                        CER001
     C           W0RTN     IFNE 'USR0790'                                                     CER001
     C           W0RTN     ANDNE'Y2U9999'                                                     CER001
     C                     MOVELINF,12    INFMSG                                              CER001
     C                     ENDIF                                                              CER001
     C           W0RTN     IFEQ 'Y2U9999'                                                     CER001
     C                     MOVE '01'      AID                                                 CER001
     C                     MOVEA'1'       *IN,10                                              CER001
     C                     ENDIF                                                              CER001
     C           W0RTN     IFEQ 'USR0790'                                                     CER001
     C                     MOVE '05'      AID                                                 CER001
     C                     ENDIF                                                              CER001
     C                     ENDIF                                                              CER001
      *                                                                                       CER001
     C                     ENDIF                                                              CER001
      *                                                                                       CER001
     C*
     C**FOR REC.ADVANCE DETERMINE WHAT SCREEN TO RETURN TO -  S T A R T
     C*
     C           W0RTN     IFNE 'USR0790'                                                     CER001
     C           W0RTN     ANDNE'Y2U9999'                                                     CER001
      *                                                                                       CER001
     C           REVW      IFEQ 'Y'
     C                     MOVELPPAGK     PAGK
     C                     MOVEA'1'       *IN,11
     C                     ELSE
     C                     MOVEA'1'       *IN,10
     C                     END
     C/COPY WNCPYSRC,FF0110C002
     C*
     C**FOR REC.ADVANCE DETERMINE WHAT SCREEN TO RETURN TO -  E N D
     C*
     C**     U  P  D  A  T  E      F  I  L  E  S
     C*
     C           QUES      IFEQ 0                                         138029
     C*                                                                   138029
     C           ACTN      IFEQ 'I'
     C           ACTN      OREQ 'A'
     C           ACTN      OREQ 'D'
     C                     EXSR UPDFLS
     C                     END
     C*                                                                   138029
     C                     ELSE                                           138029
     C           QUES      IFEQ 1                                         138029
     C           REVW      ANDNE'Y'                                       138029
     C                     MOVEA'0'       *IN,10                          138029
     C                     MOVE '0'       QUES                            138029
     C                     END                                            138029
     C                     END                                            138029
     C                     ENDIF                                                              CER001
     C*
     C** N O    D E T A I L   S C R E E N    E R R O R S   -  E N D
     C*
     C                     END
     C*
     C** C K 3 , C K12 , H E L P , R O L L U P / D O W N   -  E N D
     C*
     C/COPY WNCPYSRC,FF0110C003
     C                     END
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N  -  E N D
     C*
     C                     END
     C*
     C** RESET PROTECT/NON-DISPLAY INDS
     C*
     C                     MOVEA'00'      *IN,5
     C*
     C           XTUPDT    ENDSR                           ** XTUPDA ESR**
     C*================================================================
      /EJECT
     C*================================================================
      **           ****** AS400 CONVERSION ******                         S01179
      **                                                                  S01179
      ** ASCHGS S/R TO MOVE THE APPROPRIATE VALUES INTO FIELD AID,        S01179
      ** DEPENDING ON WHICH FUNCTION KEY HAS BEEN PRESSED.                S01179
      ** CALLED FROM UPDATE                                               S01179
      **                                                                  S01179
     C*================================================================   S01179
     C*                                                                   S01179
     C           ASCHGS    BEGSR                                          S01179
     C*                                                                   S01179
     C* If ENTER pressed, the VLDCMDKEY indicator will be off             S01179
     C*                                                                   S01179
     C           *IN20     IFEQ '0'                                       S01179
     C                     MOVE 'RA'      AID                             S01179
     C                     END                                            S01179
     C*                                                                   S01179
     C* If F3                                                             S01179
     C*                                                                   S01179
     C           *INKC     IFEQ '1'                                       S01179
     C                     MOVE '01'      AID                             S01179
     C                     MOVE '0'       *INKC                           S01179
     C                     END                                            S01179
     C*                                                                   S01179
     C* If F10                                                            S01179
     C*                                                                   S01179
     C           *INKJ     IFEQ '1'                                       S01179
     C                     MOVE '04'      AID                             S01179
     C                     MOVE '0'       *INKJ                           S01179
     C                     END                                            S01179
     C*                                                                   S01179
     C* If F12                                                            S01179
     C*                                                                   S01179
     C           *INKL     IFEQ '1'                                       S01179
     C                     MOVE '05'      AID                             S01179
     C                     MOVE '0'       *INKL                           S01179
     C                     END                                            S01179
     C*                                                                   S01179
     C* If ROLLUP pressed                                                 S01179
     C*                                                                   S01179
     C           *IN25     IFEQ '1'                                       S01179
     C                     MOVE 'UP'      AID                             S01179
     C                     MOVE '0'       *IN25                           S01179
     C                     END                                            S01179
     C*                                                                   S01179
     C* If ROLLDOWN pressed                                               S01179
     C*                                                                   S01179
     C           *IN26     IFEQ '1'                                       S01179
     C                     MOVE 'DN'      AID                             S01179
     C                     MOVE '0'       *IN26                           S01179
     C                     END                                            S01179
     C*                                                                   S01179
     C* If HELP key pressed                                               S01179
     C*                                                                   S01179
     C********** *IN27     IFEQ '1'                                 S01179S01199
     C**********           MOVE 'HP'      AID                       S01179S01199
     C**********           MOVE '0'       *IN27                     S01179S01199
     C**********           END                                      S01179S01199
     C**********                                                    S01179S01199
     C*                                                                   S01179
     C                     ENDSR                                          S01179
     C*================================================================
      /EJECT
     C*================================================================
      ** VALIDK S/R TO VALIDATE KEY FIELDS
      ** CALLED FROM S/R START, REVIEW
      ** CALLS NO OTHER S/R'S
      **
     C*================================================================
     C*
     C           VALIDK    BEGSR                           ** VALIDK BSR**
     C*
     C** RESET ERROR INDICATORS
     C** SET ERROR ARRAY COUNTER TO ZERO
     C*
     C                     MOVEA'00'      *IN,21
     C                     Z-ADD*ZERO     EC      40
     C*
     C** A C T I O N    C O D E    I N P U T - MUST BE BLANK,I,A,E,D
     C*
     C           ACTN      IFNE *BLANK
     C           ACTN      ANDNE'I'
     C           ACTN      ANDNE'A'
     C           ACTN      ANDNE'E'
     C           ACTN      ANDNE'D'
     C                     ADD  1         EC         21
     C                     MOVEL' 100'    ERR,EC
     C                     END
     C*                                                                   S01117
     C**   VALIDATE ACTION CODE FOR THIS USER                             S01117
     C*                                                                   S01117
     C           ACTN      IFEQ 'I'                                       S01117
     C           ACTN      OREQ 'A'                                       S01117
     C           ACTN      OREQ 'E'                                       S01117
     C           ACTN      OREQ 'D'                                       S01117
     C*                                                                   S01117
     C**   IF SINGLE BRANCH ENVIRONMENT                                   S01117
     C*                                                                   S01117
     C           MBIN      IFNE 'Y'                                       S01117
     C                     CALL 'ZVACTU'                                  S01117
     C                     PARM ACTN      @ZACTN  1                       S01117
     C                     PARM           @ERR    10                      S01117
     C**                                                                  S01117
     C           @ERR      IFNE *ZERO                                     S01117
     C                     ADD  1         EC         21                   S01117
     C                     MOVE ' 303'    ERR,EC                          S01117
     C                     END                                            S01117
     C                     ELSE                                           S01117
     C*                                                                   S01117
     C**   IF MULTIBRANCHING ENVIRONMENT                                  S01117
     C*                                                                   S01117
     C                     CALL 'ZVACTBU'                                 S01117
     C                     PARM ACTN      @ZACTN  1                       S01117
     C                     PARM USRBCH    @ZBR    3                       S01117
     C                     PARM           @ERR    10                      S01117
     C**                                                                  S01117
     C           @ERR      IFNE *ZERO                                     S01117
     C                     ADD  1         EC         21                   S01117
     C                     MOVE ' 304'    ERR,EC                          S01117
     C                     END                                            S01117
     C                     END                                            S01117
     C                     END                                            S01117
     C*
     C***A*C*T*I*O*N****C*O*D*E****I*N*P*U*T*-*CANT*BE*D*IF*SFLG*IS*ON****S01117
     C** A C T I O N    C O D E    I N P U T - CANT BE D IF SSF IS ON     S01117
     C*
     C           ACTN      IFEQ 'D'
     C***********SFLG      ANDEQ1                                         S01117
     C           SSF       ANDEQ'Y'                                       S01117
     C                     ADD  1         EC         21
     C                     MOVEL' 101'    ERR,EC
     C                     END
     C*
     C** A C T I O N    C O D E    I N P U T - CHECK KEY FIELDS
     C*
     C           *IN,21    IFNE '1'
     C*
     C           ACTN      IFNE *BLANK
     C*
     C** CHECK THAT EACH CHARACTER OF THE MARKET CODE IS
     C** VALID AS PER THE ARRAY OF VALID CHARACTERS
     C*
     C           MRKT1     LOKUPVLC                      99
     C   99      MRKT2     LOKUPVLC                      99
     C           *IN,99    IFEQ '0'
     C                     ADD  1         EC         22
     C                     MOVEL' 106'    ERR,EC
     C                     END
     C*
     C                     ELSE
     C*
     C** ACTION CODE BLANK BUT KEY INPUT
     C*
     C           MRKT      IFNE *BLANK
     C*
     C                     ADD  1         EC         21
     C                     MOVEL' 102'    ERR,EC
     C*
     C                     END
     C                     END
     C*
     C                     END
     C*
     C** N O   E R R O R  O N  K E Y  F I E L D S
     C*
     C           *IN,22    IFNE '1'
     C*
     C** A C T I O N    C O D E    I
     C*
     C           ACTN      IFEQ 'I'
     C           MARKTK    CHAINMARKTX               99
     C  N99      RECI      COMP '*'                      99
     C           *IN99     IFEQ '0'
     C           MRKT      OREQ *BLANK
     C                     ADD  1         EC         22
     C                     MOVEL' 103'    ERR,EC
     C                     END
     C                     END
     C*
     C** A C T I O N    C O D E    A , D
     C*
     C           ACTN      IFEQ 'A'
     C           ACTN      OREQ 'D'
     C           MARKTK    CHAINMARKTX               99
     C  N99      RECI      COMP '*'                      99
     C           *IN99     IFEQ '1'
     C                     ADD  1         EC         22
     C                     MOVEL' 104'    ERR,EC
     C                     END
     C                     END
     C*
     C** CHECK STATUS OF MARKET FOR DELETIONS
     C*
     C           ACTN      IFEQ 'D'
     C           *IN22     ANDEQ'0'
     C           MARKTK    CHAINMKCTL                99
     C*
     C           *IN99     IFEQ '1'
     C           RECI      ORNE 'D'
     C           *LOCK     IN   LDA                                       S01117
     C                     SETON                     U7U8LR***************
     C***********          MOVEL03        DBASE            **DB ERROR 03**S01117
     C                     Z-ADD03        DBASE            **DB ERROR 03**S01117
     C                     MOVEL'MKCTL'   DBFILE           ***************
     C                     MOVELMRKT      DBKEY
     C                     OUT  LDA                                       S01117
     C                     EXSR INFSR
     C                     RETRN                                          S01117
     C                     END
     C*
     C           MKCI      IFNE 'S'
     C                     ADD  1         EC         22
     C                     MOVEL' 104'    ERR,EC
     C                     END
     C*
     C                     END
     C*
     C** A C T I O N    C O D E    E
     C*
     C           ACTN      IFEQ 'E'
     C           MARKTK    CHAINMARKTX               99
     C           *IN99     IFEQ '1'
     C                     ADD  1         EC         22
     C                     MOVEL' 105'    ERR,EC
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** ERROR ON SCREEN SO HIGHLIGHT ERROR CODES
     C*
     C           EC        IFNE *ZERO
     C                     MOVELHI        ERRMSG
     C                     MOVEL*BLANK    AID
     C                     END
     C                     ENDSR
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** VALIDD S/R - TO VALIDATE DETAIL SCREEN
      ** CALLED FROM UPDATE S/R
      ** CALLS: DEFLTS
      **
     C*================================================================
     C*
     C           VALIDD    BEGSR                           ** VALIDD BSR**
     C*
     C** RESET SCREEN ERROR INDICATORS
     C*
     C                     MOVEA'0'       *IN,29                          S01117
     C                     MOVEA'0000000' *IN,30
     C                     MOVEA'0'       *IN,38                          CPB001
     C*
     C** SET ERROR COUNT TO ZERO
     C*
     C                     Z-ADD*ZERO     EC
     C*
      ** ENTER PRESSED
     C*
     C           AID       IFEQ 'RA'
     C*
      ** INITIALISE FIELDS FOR INSERT
     C*
     C           ACTN      IFEQ 'I'
     C                     EXSR INITRC
     C                     END
     C*
     C** ACTION = I OR A
     C*
     C           ACTN      IFEQ 'I'
     C           ACTN      OREQ 'A'
     C*
     C** DO DEFAULTS
     C*
     C                     EXSR DEFLTS
     C*
     C** VALIDATION OF FIELDS FOR INSERT/AMEND
     C*
     C** MARKET NAME
     C*
     C                     MOVELSMKTN     MKTN
     C*
     C           MKTN      IFEQ *BLANK
     C                     MOVEL'1'       *IN,30
     C                     ADD  1         EC         31
     C                     MOVEL' 200'    ERR,EC
     C                     END
     C*
     C** MARKET COUNTRY
     C*
     C                     MOVELSMKCN     MKCN
     C*
     C           MKCN      IFNE *BLANK
     C*
     C***CHECK*COUNTRY*CODE*EXISTS*ON*S/TABFF*****************************S01117
     C** CHECK COUNTRY CODE EXISTS ON SD                                  S01117
     C*
     C                     CALL 'AOCTRYR0'                                S01117
     C                     PARM *BLANKS   @RTCD   7                       S01117
     C                     PARM '*KEY   ' @OPTN   7                       S01117
     C                     PARM MKCN      @CNCD   7                       S01117
     C           SDCTRY    PARM SDCTRY    @DSFDY                          S01117
     C*                                                                   S01117
     C***********          MOVEL*BLANK    ZTABKY                          S01117
     C***********          MOVEL'50'      ZTABKY                          S01117
     C***********          MOVE MKCN      ZTABKY                          S01117
     C***********                                                         S01117
     C***********ZTABKY    CHAINTABFF                99                   S01117
     C**N99******RECI      COMP '*'                      99               S01117
     C************IN,99    IFEQ '1'                                       S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C                     ADD  1         EC         32
     C                     MOVEL' 201'    ERR,EC
     C                     ELSE                                           S01117
     C*                                                                   138029
     C           SMKCN     IFEQ '?'                                       138029
     C                     MOVE '1'       QUES    10                      138029
     C                     END                                            138029
     C*                                                                   138029
     C           MKCN      IFNE CTRY                                      S01117
     C                     MOVELCTRY      SMKCN                           S01117
     C                     MOVELCTRY      MKCN                            S01117
     C                     END                                            S01117
     C                     END
     C                     END
     C*                                                                   S01117
     C** MARKET LOCATION                                                  S01117
     C*                                                                   S01117
     C                     MOVELSMLOC     MLOC                            S01117
     C*                                                                   S01117
     C           MLOC      IFNE *BLANK                                    S01117
     C*                                                                   S01117
     C** CHECK LOCATION CODE EXISTS ON SD                                 S01117
     C*                                                                   S01117
     C                     CALL 'AOLOCNR0'                                S01117
     C                     PARM *BLANKS   @RTCD   7                       S01117
     C                     PARM '*KEY   ' @OPTN   7                       S01117
     C                     PARM MLOC      @LOCN   3                       S01117
     C           SDLOCN    PARM SDLOCN    @DSFDY                          S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C                     ADD  1         EC         29                   S01117
     C                     MOVEL' 207'    ERR,EC                          S01117
     C                     ELSE                                           S01117
     C*                                                                   138029
     C           SMLOC     IFEQ '?'                                       138029
     C                     MOVE '1'       QUES                            138029
     C                     END                                            138029
     C*                                                                   138029
     C           MLOC      IFNE LCCD                                      S01117
     C                     MOVELLCCD      SMLOC                           S01117
     C                     MOVELLCCD      MLOC                            S01117
     C                     END                                            S01117
     C                     END                                            S01117
     C                     END                                            S01117
     C*
     C***CHECK*CURRENCY*CODE*EXISTS*ON*S/TABFF****************************S01117
     C** CHECK CURRENCY CODE EXISTS ON SD                                 S01117
     C*
     C                     MOVELSMKLC     MKLC
     C*
     C                     CALL 'AOCURRR0'                                S01117
     C                     PARM *BLANKS   @RTCD   7                       S01117
     C                     PARM '*KEY   ' @OPTN   7                       S01117
     C                     PARM SMKLC     @CYCD   7                       S01117
     C           SDCURR    PARM SDCURR    @DSSDY                          S01117
     C*                                                                   S01117
     C***********          MOVEL*BLANK    ZTABKY                          S01117
     C***********          MOVEL'20'      ZTABKY                          S01117
     C***********          MOVELMKLC      CCYWK   4                       S01117
     C***********          MOVE '1'       CCYWK                           S01117
     C***********          MOVE CCYWK     ZTABKY                          S01117
     C***********                                                         S01117
     C***********ZTABKY    CHAINTABFF                99                   S01117
     C**N99******RECI      COMP '*'                      99               S01117
     C************IN,99    IFEQ '1'                                       S01117
     C           @RTCD     IFNE *BLANKS                                   S01117
     C                     ADD  1         EC         33
     C                     MOVEL' 202'    ERR,EC
     C                     ELSE                                           S01117
     C*                                                                   138029
     C           SMKLC     IFEQ '?'                                       138029
     C                     MOVE '1'       QUES                            138029
     C                     END                                            138029
     C*                                                                   138029
     C           MKLC      IFNE LCCY                                      S01117
     C                     MOVELLCCY      SMKLC                           S01117
     C                     MOVELLCCY      MKLC                            S01117
     C                     END                                            S01117
     C                     END
     C*
     C** TRANSLATE SMPRF
     C*
     C                     MOVELSMPRF     MPRF
     C*
     C** CHECK AUTOMATIC EOC IND
     C*
     C                     MOVELSAEOC     AEOC
     C*
     C           AEOC      IFNE ' '
     C           AEOC      ANDNE'A'
     C           AEOC      ANDNE'B'
     C                     ADD  1         EC         34
     C                     MOVEL' 203'    ERR,EC
     C                     END
     C*
     C** CHECK AUTOPRICES PROMPT IND
     C*
     C                     MOVELSAPRP     APRP
     C*
     C           APRP      IFNE 'Y'
     C           APRP      ANDNE'N'
     C                     ADD  1         EC         35
     C                     MOVEL' 204'    ERR,EC
     C                     END
     C*                                                                   CPB001
     C** If Private Banking Module                                        CPB001
     C** CHECK INSTUMENT TYPE                                             CPB001
     C*                                                                   CPB001
     C           BGN4ST    IFEQ 'Y'                                       CPB001
     C                     MOVELSINNR     INNR                            CPB001
     C                     MOVELSINNR     @KEY3                           CPB001
     C*                                                                   CPB001
     C                     CALL 'AOPLINR0'                                CPB001
     C                     PARM *BLANKS   @RTCD                           CPB001
     C                     PARM '*KEY   ' @OPTN                           CPB001
     C                     PARM           @KEY3   3                       CPB001
     C           SDPLIN    PARM SDPLIN    @DSFDY                          CPB001
     C*                                                                   CPB001
     C           @RTCD     IFNE *BLANKS                                   CPB001
     C                     ADD  1         EC                              CPB001
     C                     MOVEL' 208'    ERR,EC                          CPB001
     C                     MOVE *ON       *IN38                           CPB001
     C                     ELSE                                           CPB001
     C                     MOVE *OFF      *IN38                           CPB001
     C                     MOVELPDINNR    SINNR                           CPB001
     C                     ENDIF                                          CPB001
     C                     ENDIF                                          CPB001
     C*
     C** CHECK MONTHS QUOTED
     C*
     C                     MOVEADETS      DET
     C***********          MOVEADET,38    MQ                              S01117
     C                     MOVEADET,41    MQ                              S01117
     C                     MOVEAMQ        MKMQ
     C*
     C                     DO   12        X       20
     C           MQ,X      IFNE 'Y'
     C           MQ,X      ANDNE' '
     C                     ADD  1         EC         36
     C                     MOVEL' 205'    ERR,EC
     C                     END
     C  N36                END
     C*
     C** CHECK MONTHS QUOTED - AT LEAST 1 'Y' MUST BE INPUT
     C*
     C                     Z-ADD1         X
     C           'Y'       LOKUPMQ,X                     99
     C           *IN,99    IFEQ '0'
     C                     ADD  1         EC         36
     C                     MOVEL' 206'    ERR,EC
     C                     END
     C*
     C                     END
     C*
     C** IF ACTION=D WITH ENTER PRESSED THEN ERROR                         -
     C*
     C           ACTN      IFEQ 'D'
     C           ULX008    ANDEQ'N'                                                         BUG10372
     C                     MOVELINF,10    INFMSG
     C**********           MOVE '1'       *IN24                                     BUG8045 BUG10372
     C                     END
     C*
     C                     END
     C*
     C** IF COMMAND 4 TAKEN
     C*
     C                     MOVE 'N'       #1IMDL                                            BUG10372
     C           AID       IFEQ '04'
     C*
     C** COMMAND 4 TAKEN CORRECTLY WHEN ACTION IS D-FORCE 'RECORD ADVANCE'
     C*
     C           ACTN      IFEQ 'D'
     C                     MOVEL'RA'      AID
     C                     MOVE 'Y'       #1IMDL                                            BUG10372
     C*
     C** COMMAND 4 TAKEN INCORRECTLY WHEN ACTION IS NOT D-HENCE ERROR
     C*
     C                     ELSE
     C                     MOVELINF,11    INFMSG
     C                     END
     C*
     C                     END
     C*
     C** ERROR ON SCREEN SO HIGHLIGHT ERROR CODES
     C*
     C           EC        IFNE *ZERO
     C                     MOVELHI        ERRMSG
     C                     END
     C*
     C                     ENDSR
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** DEFLTS S/R TO FORMAT DEFAULT FIELD VALUES FOR INSERT SCREEN ETC
      ** CALLED FROM UPDATE; VALIDD
      ** CALLS NO OTHER S/R'S
      **
     C*================================================================
     C           DEFLTS    BEGSR                           ** DEFLTS BSR**
     C*
     C** DEFAULTS FOR RECORD
     C*
     C           SAPRP     IFEQ *BLANK
     C                     MOVEL'N'       SAPRP
     C                     END
     C                     ENDSR
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** INITRC S/R TO INITIALISE ALL FILE FIELDS TO ZERO/BLANK
      ** CALLED FROM UPDATE; UPDFLS
      ** CALLS NO OTHER S/R'S
      **
     C*================================================================
     C*
     C           INITRC    BEGSR                           ** INITRC BSR**
     C*
     C** INITIALIZE ALL DATA ON RECORD DEFINED IN DETREC (KEY EXCLUDED)
     C*
     C                     MOVEL*BLANK    DETREC
     C                     Z-ADD*ZERO     LCD
     C                     Z-ADD*ZERO     TNLU
     C*
     C** INITIALIZE ALL DATA ON MKCTL
     C*
     C                     Z-ADD*ZERO     BUSD
     C                     MOVEL' '       ECAI
     C                     MOVEL'S'       MKCI
     C                     MOVEL'N'       ICRI
     C                     MOVEL' '       ECFI
     C                     MOVEL' '       AKMI
     C                     MOVEL' '       POBI
     C                     MOVEL' '       SMRI
     C                     ENDSR
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** TRSFS S/R TO FORMAT RECORD FIELDS FOR SCREEN
      ** CALLED FROM UPDATE
      ** CALLS NO OTHER S/R'S
      **
     C*================================================================
     C*
     C           TRSFS     BEGSR                           ** TRSFS BSR **
     C*
     C** TRANSLATE RECORD DETAILS FROM FILE LAYOUT TO SCREEN LAYOUT
     C*
     C                     MOVELMKTN      SMKTN 020
     C                     MOVELMKCN      SMKCN 002
     C                     MOVELMLOC      SMLOC 003                       S01117
     C                     MOVELMKLC      SMKLC 003
     C                     MOVELMPRF      SMPRF 010
     C                     MOVELAEOC      SAEOC 001
     C                     MOVELAPRP      SAPRP 001
     C           BGN4ST    IFEQ 'Y'                                       CPB001
     C                     MOVELINNR      SINNR                           CPB001
     C                     ENDIF                                          CPB001
     C                     MOVEAMKMQ      MQ
     C                     MOVELMQ01      SMQ01
     C                     MOVELMQ02      SMQ02
     C                     MOVELMQ03      SMQ03
     C                     MOVELMQ04      SMQ04
     C                     MOVELMQ05      SMQ05
     C                     MOVELMQ06      SMQ06
     C                     MOVELMQ07      SMQ07
     C                     MOVELMQ08      SMQ08
     C                     MOVELMQ09      SMQ09
     C                     MOVELMQ10      SMQ10
     C                     MOVELMQ11      SMQ11
     C                     MOVELMQ12      SMQ12
     C                     ENDSR
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** UPDFLS S/R TO UPDATE FILE RECORDS
      ** CALLED FROM UPDATE
      ** CALLS INITRC; ZTNLU1; INFSR
      **
     C*================================================================
     C           UPDFLS    BEGSR                           ** UPDFLS BSR**
     C*
     C** U P D A T E   F I L E S
     C*
     C** INITIALIZE ALL FIELDS ON THE RECORD (IE ZEROIZE/BLANK OUT)
     C*
     C                     EXSR INITRC
     C*
     C** RETRIEVE NEXT TRANSACTION NUMBER
     C*
     C                     EXSR ZTNLU1
     C*
     C** INITIALISE ERROR BAND
     C*
     C                     MOVEL*BLANK    INFMSG
     C*
     C**               I  N  S  E  R  T  S                 -  S T A R T
     C*
     C           ACTN      IFEQ 'I'
     C           MARKTK    CHAINMARKTDF              60
     C*
     C           TNLU      IFNE STNLU
     C                     EXSR UPDOWS
     C*
     C** IF NO PROCESSING OCCURRED EXIT TO SUPPRESS AUDIT UPDATE
     C*
     C                     GOTO XTUPDF
     C*
     C                     ELSE
     C*
      ** ACCESS EQUIVALENT MARKET CONTROL RECORD IF A MARKET RECORD EXUSTS
     C*
     C           *IN60     IFEQ '0'
     C           MARKTK    CHAINMKCTL                99
     C*
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01117
     C                     SETON                     U7U8LR***************
     C***********          MOVEL04        DBASE            **DB ERROR 04**S01117
     C                     Z-ADD04        DBASE            **DB ERROR 04**S01117
     C                     MOVEL'MKCTL'   DBFILE           ***************
     C                     MOVELMRKT      DBKEY
     C                     OUT  LDA                                       S01117
     C                     EXSR INFSR
     C                     RETRN                                          S01117
     C                     END
     C*
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM SCREEN LAYOUT TO FILE LAYOUT
     C*
     C                     EXSR VALIDD
     C                     MOVEL'D'       RECI
     C                     Z-ADDRUND      LCD
     C                     MOVEL'I'       CHTP
     C                     MOVELNATN      TNLU
     C                     END
     C   60                WRITEMARKTDF                15
     C*
     C** TEST FOR UPDATE BY ANOTHER W/S, OR A TERMINAL ERROR
     C*
     C           *IN15     IFEQ '1'
     C           STATUS    ANDEQ01021
     C           MARKTK    CHAINMARKTX               15
     C           *IN15     IFEQ '0'
     C                     EXSR UPDOWS
     C                     GOTO XTUPDF
     C                     END
     C                     END
     C           *IN15     IFEQ '1'
     C                     EXSR INFSR
     C                     END
     C*
     C  N60                UPDATMARKTDF
     C*
     C** WRITE OUT TO / UPDATE MARKET CONTROL
     C*
     C   60                WRITEMKCTLDF                99
     C*
     C           *IN60     IFEQ '1'
     C           *IN99     ANDEQ'1'
     C           STATK     ANDEQ01021
     C           *LOCK     IN   LDA                                       S01117
     C                     SETON                     U7U8LR***************
     C***********          MOVEL05        DBASE            **DB ERROR 05**S01117
     C                     Z-ADD05        DBASE            **DB ERROR 05**S01117
     C                     MOVEL'MKCTL'   DBFILE           ***************
     C                     MOVELMRKT      DBKEY
     C                     OUT  LDA                                       S01117
     C                     EXSR INFSR
     C                     RETRN                                          S01117
     C                     END
     C*
     C  N60                UPDATMKCTLDF
     C*
     C                     END
     C*
     C**               I  N  S  E  R  T  S                 -  E N D
     C*
     C**               A  M  E  N  D  S                    -  S T A R T
     C*
     C           ACTN      IFEQ 'A'
     C           MARKTK    CHAINMARKTDF              60
     C           TNLU      IFNE STNLU
     C                     EXSR UPDOWS
     C*
     C** IF NO PROCESSING OCCURRED EXIT TO SUPPRESS AUDIT UPDATE
     C*
     C                     GOTO XTUPDF
     C*
     C                     ELSE
     C*
     C** TRANSLATE RECORD DETAILS FROM SCREEN LAYOUT TO FILE LAYOUT
     C*
     C                     EXSR VALIDD
     C                     Z-ADDRUND      LCD
     C                     MOVEL'A'       CHTP
     C                     MOVELNATN      TNLU
     C                     END
     C  N60                UPDATMARKTDF
     C                     END
     C*
     C**               A  M  E  N  D  S                    -  E N D
     C*
     C**               D  E  L  E  T  E  S                 -  S T A R T
     C*
     C           ACTN      IFEQ 'D'
     C           MARKTK    CHAINMARKTDF              60
     C           TNLU      IFNE STNLU
     C                     EXSR UPDOWS
     C*
     C** IF NO PROCESSING OCCURRED EXIT TO SUPPRESS AUDIT UPDATE
     C*
     C                     GOTO XTUPDF
     C*
     C                     ELSE
     C                     MOVEL'*'       RECI
     C                     Z-ADDRUND      LCD
     C                     MOVEL'D'       CHTP
     C                     MOVELNATN      TNLU
     C                     END
     C  N60                UPDATMARKTDF
     C*
     C** UPDATE MARKET CONTROL  ( FLAG AS DELETED )
     C*
     C           MARKTK    CHAINMKCTLDF              60
     C  N60                MOVEL'*'       RECI
     C  N60                UPDATMKCTLDF
     C*
     C                     END
     C*
     C**               D  E  L  E  T  E  S                 -  E N D
     C*
     C** GET AUDIT RECORD
     C*
     C           1         CHAINMARKTAF              99
     C*
     C** UPDATE AUDIT RECORD IF FOUND
     C*
     C           *IN99     IFEQ '0'
     C*
     C** FOR INSERT, UPDATE LIVE RECORDS INSERTED TODAY, AND NO. OF LIVE
     C** RECORDS
     C*
     C           ACTN      IFEQ 'I'
     C                     ADD  1         NINS
     C                     ADD  1         NORE
     C                     END
     C*
     C** FOR AMENDS, UPDATE NO. OF RECORDS AMENDED TODAY
     C*
     C           ACTN      IFEQ 'A'
     C                     ADD  1         NAMD
     C                     END
     C*
     C** FOR DELETIONS, UPDATE NO. DELETIONS TODAY, AND NO. OF LIVE RECS
     C*
     C           ACTN      IFEQ 'D'
     C                     ADD  1         NDEL
     C                     SUB  1         NORE
     C                     END
     C*
     C** FORMAT COMIT TEXT
     C*
     C                     TIME           MTIME
     C                     MOVELACTN      ACTNX
     C                     MOVEASCRCTL    DET,01
     C                     MOVEADETS      DET,05
     C*
     C** UPDATE AUDIT RECORD
     C*
     C                     UPDATMARKTAF
     C*
     C** C  O  M  I  T
     C*
     C           CMTTXT    COMIT
     C*
     C** AUDIT RECORD NOT FOUND: DATABASE ERROR
     C*
     C                     ELSE
     C*
     C           *LOCK     IN   LDA                                       S01117
     C                     SETON                     U7U8  ***************
     C***********          MOVEL'02'      DBASE            **DB ERROR 02**S01117
     C                     Z-ADD02        DBASE            **DB ERROR 02**S01117
     C                     MOVEL'MARKT'   DBFILE           ***************
     C                     MOVEL'AUDIT'   DBKEY
     C                     OUT  LDA                                       S01117
     C                     EXSR INFSR                                     S01117
     C                     END
     C*
     C           XTUPDF    ENDSR
     C*================================================================
      /EJECT
     C*================================================================
      **
      ** UPDOWS S/R TO PROCESS UPDATE BY ANOTHER WORKSTATION
      ** CALLED FROM UPDFLS
      ** CALLS NO OTHER S/R'S
      **
     C*================================================================
     C           UPDOWS    BEGSR                           ** UPDOWS BSR**
     C**
     C                     ROLBK
     C**
     C**   SET UP NEXT SCREEN TO BE DISPLAYED:
     C**   IF RECORD IS LIVE, ALLOW AMEND PROCESSING;
     C**
     C           RECI      IFEQ 'D'
     C                     MOVEL'A'       ACTN
     C                     ELSE
     C**
     C**   IF RECORD HAS BEEN DELETED, ALLOW ENQUIRY
     C**
     C                     MOVEL'E'       ACTN
     C                     END
     C**
     C**   SET SCREEN PROCESSOR TO UPDATE AND MOVE UPDATE MESSAGE TO ERROR
     C**
     C                     MOVEA'001'     *IN,10
     C                     MOVELINF,12    INFMSG
     C**
     C                     ENDSR
     C**
     C*================================================================
      /EJECT
     C** ZTNLU1 SUBROUTINE
     C/COPY ZSRSRC,ZTNLU1Z2
     C/COPY WNCPYSRC,FF0110C004
     C*
      /EJECT
     C*================================================================
     C**
     C**   FILE EXEPTION/ERROR HANDLING SUBROUTINE
     C**
     C           INFSR     BEGSR
     C**
     C** ROLL BACK CHANGES AND CANCEL PROGRAM
     C**
     C                     ROLBK
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C***********          MOVE '*CANCL'  EXTTAG  6                       S01117
     C**
     C                     ENDSR                                          S01117
     C***********          ENDSREXTTAG                                    S01117
     C*================================================================
      * /104633/ Change
      * INF,7  DELETION F3:F10 to delete this record:F12:Rollup:Rolldown:help
      * INF,10 F10 is reqiured
      * INF,11 F10 is not reqiured
**  CPY@
(c) Finastra International Limited 2001
** INF
 Enter:Action Code I,A,D,E and selection:Review from point:F3:Help
 Enter:Review from point:Action Code A,D,E:F3:F12:Rollup:Rolldown:     Help
 No Records Found: Enter Review from point; F3; F12; Help
 INSERTION Enter:F3:F12:Help
 AMENDMENT Enter:F3:F12:Rollup:Rolldown:Help
 ENQUIRY Enter:F3:F12:Rollup:Rolldown:Help
 DELETION F3:F10=Delete record:F12:Rollup:Rolldown:Help
 Rollup is not possible
 Rolldown is not possible
 F10=Confirm
 
 Record has been updated by another workstation
** VLC
ABCDEFGHIJKLMNOPQRSTUVWXYZ
